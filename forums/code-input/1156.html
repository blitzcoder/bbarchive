<!DOCTYPE html><html lang="en" ><head ><title >Mouse Include</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Handles the mouse. mouse\doubleclick, mouse\button\released..., language=bb, category=User Input'><meta name='author' content='Curtastic'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=5>User Input</a>/Mouse Include</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1156.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mouse Include by Curtastic</td><td align="right">2004 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Stores mousespeed, mousehit, mousedown, mousex<br>Tells if the user doubleclicked. Tells if a button was released this loop.<br>Each button also remembers where it was when the click started.<br><br>Just include it, and in your loop call mouseupdate()<br><br>I am using this for my RTS and my GUI.<br><br><br>Here is an example<br><pre class=code>
;Mouse Example
;doubleclick to exit
;right click and drag to make lines


Include "mouseinclude.bb"

Graphics 800,600,0,2
SetBuffer BackBuffer()

mouse\controlled=1
HidePointer

;background image
image=CreateImage(800,600)

;main loop
Repeat
	mouseupdate()
	
	;right click to make lines
	If mouse\br\released Then
		SetBuffer ImageBuffer(image)
		Line mouse\x,mouse\y,mouse\br\startx,mouse\br\starty
		SetBuffer BackBuffer()
	EndIf

	DrawBlock image,0,0
	

	If mouse\br\down Then
		Text 1,1,"right button is down"
	EndIf
	
	
	;double click test
	If mouse\bl\hitdouble Then
		End
	EndIf
	
	;right click and drag shows the line
	If mouse\br\down Then
		Line mouse\x,mouse\y,mouse\br\startx,mouse\br\starty
	EndIf


	;draw mouse cursor
	Rect mouse\x,mouse\y,5,5
	
	Flip

	
	If KeyHit(1) Then End
Forever
</pre><br><br>.<br>.<br>.<br>.<br>Here is the include file: </td></tr><tr ><td class="cell"><pre class="code">;do not use Mouse_Speed(). Use mouse\button\speed
;do not use MouseHit(). Use mouse\button\hit
;if mouse\crontrolled then
	;do not use MouseX() Or MouseY(). Use mouse\x and mouse\y
	;do not use MoveMouse(). use mouse\x=


Type Mouse
	;button left
	Field bl.mousebutton
	;button right
	Field br.mousebutton
	;button middle
	Field bm.mousebutton
	;the position of the mouse
	Field x,y
	;stores mousexspeed(),mouseyspeed()
	Field speedx,speedy,speedz
	;this tells if the mouse pointer is hidden and kept at the center and you draw your own pointer
	Field controlled
	;how many millisecs are allowed for a double click
	Field hitdoublespeed
End Type
Global mouse.mouse=New mouse

mouse\hitdoublespeed=300


mouse\bl=New mousebutton
mouse\bl\index=1
mouse\br=New mousebutton
mouse\br\index=2
mouse\bm=New mousebutton
mouse\bm\index=3


Type MouseButton
	;what number blitz uses for this button
	Field index
	
	;where the mouse was when the click started
	Field startx,starty
	;if the button whas hit this loop
	Field hit
	;tells if the button was let go this loop
	Field released
	;tells if the button is down this loop
	Field down
	;tells if the button was doubleclicked this loop
	Field hitdouble
	;when it was let go
	Field releasedtime
	;internal: makes sure no clicks were missed
	Field hitcheck
End Type










;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
Function MouseUpdate()
	Local b.mousebutton
	

	mouse\speedz=MouseZSpeed()
	mouse\speedx=MouseXSpeed()
	mouse\speedy=MouseYSpeed()
	If mouse\controlled Then
		mouse\x=mouse\x+mouse\speedx
		mouse\y=mouse\y+mouse\speedy
		MoveMouse GraphicsWidth()/2,GraphicsHeight()/2
	
		;done after movemouse to fix mouse problems on some computers
		MouseXSpeed()
		MouseYSpeed()
	
		mouse\x=keepin(mouse\x,0,GraphicsWidth()-1)
		mouse\y=keepin(mouse\y,0,GraphicsHeight()-1)

	Else
		mouse\x=MouseX()
		mouse\y=MouseY()
	EndIf


	For b=Each mousebutton
		b\released=0
		b\hitdouble=0

		b\hit=MouseHit(b\index)&gt;0
		b\down=MouseDown(b\index)

		If b\hitcheck=0 Then b\hitcheck=b\hit
		If b\hitcheck=1 And b\down=0 Then
			b\released=1
			b\hitcheck=0
		EndIf
	
		If b\hit=1 Then
			b\startx=mouse\x
			b\starty=mouse\y

			If MilliSecs()-b\releasedtime&lt;mouse\hitdoublespeed Then
				b\hitdouble=1
			EndIf
			b\releasedtime=MilliSecs()
		EndIf
	Next

End Function


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;pass Null if you want to flush all buttons
Function MouseFlush(mb.mousebutton,speeds=1)
	Local mb2.mousebutton


	For mb2=Each mousebutton
		If mb2=mb Or mb=Null Then

			mb2\hit=0
			mb2\startx=0
			mb2\starty=0
			MouseHit(mb2\index)
			mb2\hitcheck=0

		EndIf
	Next

	If speeds=1 Then
		MouseZSpeed()
		MouseXSpeed()
		MouseYSpeed()
		mouse\speedz=0
		mouse\speedx=0
		mouse\speedy=0
	EndIf
End Function



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;keeps num in bounds
;returns the same number if its already in bounds.
Function KeepIn#(num#,min#,max#)
	If num&lt;min Then 
		Return min
	ElseIf num&gt;max
		Return max
	EndIf
	Return num
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >churchaxe</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> wow, this is exactly what I needed for my GUI, cool stuff! <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
