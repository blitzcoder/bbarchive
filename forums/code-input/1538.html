<!DOCTYPE html><html lang="en" ><head ><title >CameraMouseView</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Camera controls similar to those found in a modeller., language=bb, category=User Input'><meta name='author' content='markcw'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=5>User Input</a>/CameraMouseView</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1538.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >CameraMouseView by markcw</td><td align="right">2005 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> I couldn't find any code here like this, so I thought I'd add this example. The drag code was the tricky bit, but it seems to be working nicely now.<br><br>This is the kind of camera control you get in Milkshape or UltimateUnwrap.<br><br>edit: i never liked the way this used a lot of globals, so i've updated it so it uses no globals and instead a bank to hold the "globals". i thought about types but decided this was better as it's simpler to transfer to a project this way. </td></tr><tr ><td class="cell"><pre class="code">;CameraMouseView, on 10/10/06

Graphics3D 640,480,0,2
SetBuffer BackBuffer()

camerapivot=CreatePivot() ;pivot
camera=CreateCamera(camerapivot) ;camera on pivot
camerabank=CreateBank(48) ;camera bank variables

light=CreateLight()
RotateEntity light,90,0,0

cube=CreateCube()

While Not KeyHit(1)
 UpdateWorld
 RenderWorld

 CameraMouseView(camerapivot,camera,camerabank)

 Text 0,0,"Press LShift to Zoom, LCtrl to Drag"

 Flip
Wend

Function CameraMouseView(camerapivot,camera,camerabank,speed#=0.05)
 ;camerapivot,camera,camerabank=valid handles, speed#=speed of view

 Local movex,movey,camx#,camy#,camz#,pivx#,pivz#,angle#,xaxis#,zaxis#

 ;error messages for invalid handles
 If camerapivot=0 Then RuntimeError "Pivot entity is zero!"
 If camera=0 Then RuntimeError "Camera entity is zero!"
 If camerabank=0 Then RuntimeError "Bank handle is zero!"

 ;set default values
 If speed#&lt;0.01 Then speed#=0.01 ;min speed
 If PeekFloat(camerabank,36)=0 Then PokeFloat camerabank,36,-10 ;no camz

 ;update mouse variables
 PokeInt camerabank,0,PeekInt(camerabank,8) ;lastx=msex
 PokeInt camerabank,4,PeekInt(camerabank,16) ;lasty=msey
 PokeInt camerabank,8,MouseX() ;msex
 PokeInt camerabank,16,MouseY() ;msey
 PokeInt camerabank,20,MouseX()-PeekInt(camerabank,0) ;movex=msex-lastx
 PokeInt camerabank,24,MouseY()-PeekInt(camerabank,4) ;movey=msey-lasty

 ;fill in the position/rotation variables
 movex=PeekInt(camerabank,20)
 movey=PeekInt(camerabank,24)
 camx#=PeekFloat(camerabank,28)
 camy#=PeekFloat(camerabank,32)
 camz#=PeekFloat(camerabank,36)
 pivx#=PeekFloat(camerabank,40)
 pivz#=PeekFloat(camerabank,44)

 If MouseDown(1) ;Left Mouse button
  If KeyDown(29) ;Left Ctrl key, camera drag
   angle#=camy# : xaxis#=speed# : zaxis#=speed# ;init xz axis
   If angle#&gt;90 And angle#&lt;270 Then zaxis#=-zaxis# ;-z if 90..270
   If angle#&gt;180 Then angle#=360-angle# : xaxis#=-xaxis# ;-x if 180..360
   If angle#&gt;90 Then angle#=180-angle# ;reduce y angle to 90
   angle#=angle#*0.011 ;y as fraction of 1, ie. 90-&gt;100
   ;set mouse xy movements by y fraction and add to pivot xz positions
   pivx#=pivx#-(movex*(1-angle#)*zaxis#)-(movey*angle#*xaxis#)
   pivz#=pivz#-(movex*angle#*xaxis#)+(movey*(1-angle#)*zaxis#)
  ElseIf KeyDown(42) ;Left Shift key, camera zoom
   camz#=camz#-movey*speed#  ;-msey sets camera z mt
   If camz#&gt;-3 Then camz#=-3 ;limit zoom to 3mt
  Else ;camera rotation
   camx#=camx#+(movey*10*speed#) ;mousey sets camera x dg
   If camx#&gt;90 Then camx#=90   ;limit x to -90..90
   If camx#&lt;-90 Then camx#=-90
   camy#=camy#-(movex*10*speed#) ;-mousex sets camera y dg
   If camy#&gt;359 Then camy#=0   ;limit y to 0..360
   If camy#&lt;0 Then camy#=359
  EndIf
 EndIf

 ;update position/rotation variables
 PokeFloat camerabank,28,camx#
 PokeFloat camerabank,32,camy#
 PokeFloat camerabank,36,camz#
 PokeFloat camerabank,40,pivx#
 PokeFloat camerabank,44,pivz#

 ;move pivot, rotate and zoom camera
 PositionEntity camerapivot,pivx#,0,pivz# ;drag
 RotateEntity camerapivot,camx#,camy#,0 ;rotate
 PositionEntity camera,0,0,camz# ;zoom

End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> Here is the old code entry, just as a reminder.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Rotate, Zoom and Drag Camera with Mouse, on 18/11/05, by muk

Graphics3D 640,480,16,2
SetBuffer BackBuffer()

Global camx#,camy#,camz#=-10,pivx#,pivz#,camspd#=0.02 ;camera
Global msex,msey,lastx,lasty,movex,movey ;mouse
Global pivot,camera,light ;entity

pivot=CreatePivot() ;pivot
camera=CreateCamera(pivot) ;camera with pivot parent

light=CreateLight() ;light
RotateEntity light,90,0,0

cube=CreateCube() ;cube

;Main Loop
While Not KeyDown(1)

 UpdateCamera()

RenderWorld

 Text 0,0,"Press LShift to Zoom"
 Text 0,16,"Press LCtrl to Drag"
 Text 0,48,"X dg:"+camx
 Text 0,64,"Y dg:"+camy
 Text 0,80,"Zoom:"+camz
 Text 0,96,"X mts:"+pivx
 Text 0,112,"Z mts:"+pivz

Flip
Wend
End

;Functions
Function UpdateCamera()

lastx=msex : lasty=msey ;get mouse movements
msex=MouseX() : msey=MouseY()
movex=msex-lastx : movey=msey-lasty

If MouseDown(1)
 If KeyDown(29) ;LCtrl key, camera drag
  yrot#=camy : xaxis#=camspd : zaxis#=camspd ;init xz axis
  If yrot&gt;90 And yrot&lt;270 Then zaxis=-zaxis     ;-zaxis if 90-270dg
  If yrot&gt;180 Then yrot=360-yrot : xaxis=-xaxis ;-xaxis if 180-360dg
  If yrot&gt;90 Then yrot=180-yrot ;reduce y rotation to 90dg
  yfr#=yrot*0.011 : ifr#=1-yfr  ;set y as fraction of 1 and get inverse
  mxpx#=movex*ifr*zaxis : mxpz#=movex*yfr*xaxis ;set mouse xy movements
  mypx#=movey*yfr*xaxis : mypz#=movey*ifr*zaxis ;by y fraction and axis
  pivx=pivx-mxpx-mypx : pivz=pivz-mxpz+mypz     ;add to pivot positions
 ElseIf KeyDown(42) ;LShift key, camera zoom
  camz=camz-movey*camspd  ;-mousey sets camera z position
  If camz&gt;-3 Then camz=-3 ;limit z to 3-30mt
  If camz&lt;-30 Then camz=-30
 Else ;Neither keys, camera rotation
  camx=camx+movey*10*camspd ;mousey sets camera x rotation
  If camx&gt;90 Then camx=90   ;limit x to 0-90dg
  If camx&lt;0 Then camx=0
  camy=camy-movex*10*camspd ;-mousex sets camera y rotation
  If camy&gt;359 Then camy=0   ;wrap y to 0-359dg
  If camy&lt;0 Then camy=359
 EndIf
EndIf

RotateEntity pivot,camx,camy,0   ;rotate camera
PositionEntity camera,0,0,camz   ;zoom camera
PositionEntity pivot,pivx,0,pivz ;drag camera

End Function
</textarea> <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
