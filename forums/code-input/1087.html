<!DOCTYPE html><html lang="en" ><head ><title >COM-Port</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Show how to receive and transmit any datas with the COM-Port, language=bb, category=User Input'><meta name='author' content='Vertex'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=5>User Input</a>/COM-Port</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1087.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >COM-Port by Vertex</td><td align="right">2004 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> The original code are from <a href="http://www.c-plus-plus-archiv.de/archiv/show.ca/title/Kommunikation%20%FCber%20RS232/author/Stefan%20Schneider/file/RS232.cpp/?offset=0&amp;noclass=&amp;nolist=&amp;sort=downloads&amp;dir=up&amp;type=" target="_blank">http://www.c-plus-plus-archiv.de/archiv/show.ca/title/Kommunikation%20%FCber%20RS232/author/Stefan%20Schneider/file/RS232.cpp/?offset=0&amp;noclass=&amp;nolist=&amp;sort=downloads&amp;dir=up&amp;type=</a><br><br>How to use?<br>openComport(comPort, baudRate):<br>Create a device and return the handle of it.<br>If you use a false baudrate or a false comportindex, then the function return FALSE or if the device can't open, then return the function -1. Else the function return a valid handle of the device.<br><br>closeComport(driverHandle)<br>Close the comport.<br>If the driverHandle a invalid handle, then the function return FALSE, alse the return value is nonzero.<br><br>writeComport(driverHandle, numBytes, buffer)<br>Transmit datas.<br>numByte are the number of bytes to transmit.<br>buffer is the handle of a bank, that includes the datas.<br>If the function fails, then the return value is FALSE else a nonzero value.<br><br>readComport(driverHandle, bytesRead, bufferSize, buffer, tOut)<br>Reveive datas<br>bytesRead is a bank with a size of 4 Bytes<br>bufferSize is the size of the buffer<br>buffer is a bank to save the received datas<br>tOut is the timeout in millisecs<br>If the function succeeds, then the return value is TRUE else FALSE. The number of bytes that received saved in the bytesRead bank as integervalue.<br><br>cu olli </td></tr><tr ><td class="cell"><pre class="code">Const  parityEnable        = False ; flag if parity is enabled
Const  protocol            = 0     ; stores protocol type
Const  dataBits            = 8     ; stores number if data bits
Const  parityBit           = 0     ; stores type of parity bit
Const  stopBits            = 0     ; stores number of stop bits
Global timeouts            = CreateBank(20)

Const INVALID_HANDLE_VALUE = -1

Const  GENERIC_READ        = $80000000
Const  GENERIC_WRITE       = $40000000
Const  OPEN_EXISTING       = 3

Const  CBR_110             = 110
Const  CBR_300             = 300
Const  CBR_600             = 600
Const  CBR_1200            = 1200
Const  CBR_2400            = 2400
Const  CBR_4800            = 4800
Const  CBR_9600            = 9600
Const  CBR_14400           = 14400
Const  CBR_19200           = 19200
Const  CBR_38400           = 38400
Const  CBR_56000           = 56000
Const  CBR_57600           = 57600
Const  CBR_115200          = 115200

Const  DTR_CONTROL_DISABLE = 0
Const  DTR_CONTROL_ENABLE  = 1
Const  RTS_CONTROL_DISABLE = 0
Const  RTS_CONTROL_ENABLE  = 1
Const  EV_TXEMPTY          = 4

Const  SETRTS              = 3
Const  CLRRTS              = 4

; Example ---------------------------------------------------------------------------
Print "Open COM1 with 9600 baudrate ..."
hCom = openComport(1, 9600)
If hCom &lt;&gt; 0 Then
	Print "   Succesfully"
Else
	Print "   Error"
	WaitKey : End
EndIf


; Transmit:
buffer = CreateBank(6)
PokeByte buffer, 0, Asc("H")
PokeByte buffer, 1, Asc("e")
PokeByte buffer, 2, Asc("l")
PokeByte buffer, 3, Asc("l")
PokeByte buffer, 4, Asc("o")
PokeByte buffer, 5, 0
Print "Sending "+Chr$(34)+"Hello"+Chr$(34)+" ..."
If writeComPort(hCom, 6, buffer) Then
	Print "   Succesfully"
Else
	Print "   Error"
	closeComport(hCom)
	WaitKey : End
EndIf
FreeBank buffer

; Receive
buffer = CreateBank(1024)
bytes  = CreateBank(4)
Print "Receiving datas ..."
If readComport(hCom, bytes, 1024, buffer, 10000) Then ; 10 Sekunden timeout
	Print "   Succesfully"
	Print "   Number of receiving bytes "+PeekInt(bytes, 0)
	; Datas are in the bank buffer
Else
	Print "   Error"
EndIf

; Close and end
closeComport(hCom)
WaitKey : End
; -----------------------------------------------------------------------------------

Function openComport(comport, baudRate)
   Local dcbBaudRate, driverHandle, dcb
   
   If comport &gt; 255 Or comport &lt; 0 Then
      Return False
   EndIf
   
   Select baudRate
      Case 110
         dcbBaudRate = CBR_110
      Case 300
         dcbBaudRate = CBR_300
      Case 600
         dcbBaudRate = CBR_600
      Case 1200
         dcbBaudRate = CBR_1200
      Case 2400
         dcbBaudRate = CBR_2400
      Case 4800
         dcbBaudRate = CBR_4800
      Case 9600
         dcbBaudRate = CBR_9600
      Case 14400
         dcbBaudRate = CBR_14400
      Case 19200
         dcbBaudRate = CBR_19200
      Case 38400
         dcbBaudRate = CBR_38400
      Case 56000
         dcbBaudRate = CBR_56000
      Case 57600
         dcbBaudRate = CBR_57600
      Case 115200
         dcbBaudRate = CBR_115200
      Default
         Return False
   End Select
   
   driverHandle = apiCreateFile("COM"+comport, GENERIC_READ Or GENERIC_WRITE, 0, 0, OPEN_EXISTING, 0, 0)
   If driverHandle = INVALID_HANDLE_VALUE Then
      Return INVALID_HANDLE_VALUE
   Else
      apiSetupComm(driverHandle, 1024, 1024)
      dcb = CreateBank(80)
      PokeInt dcb, 00, 80                         ; sizeof(DCB)
      PokeInt dcb, 04, dcbBaudRate                ; current baud rate
      PokeInt dcb, 08, 1                          ; binary mode, no EOF check
      If parityEnable = True Then
         PokeInt dcb, 12, 1                       ; enable parity checking
      Else
         PokeInt dcb, 12, 0                       ; disable parity checking
      EndIf
      If protocol = 0 Then
         PokeInt dcb, 24, DTR_CONTROL_ENABLE      ; DTR flow control type
      Else
         PokeInt dcb, 24, DTR_CONTROL_DISABLE     ; DTR flow control type
      EndIf
      PokeInt dcb, 28, False                      ; DSR sensitivity
      If protocol = 1 Then
         PokeInt dcb, 52, RTS_CONTROL_ENABLE      ; RTS flow control
      Else
         PokeInt dcb, 52, RTS_CONTROL_DISABLE     ; RTS flow control
      EndIf
      PokeInt  dcb, 60, 17                        ; reserved
      PokeByte dcb, 70, dataBits                  ; number of bits/byte, 4-8
      PokeByte dcb, 71, parityBit                 ; 0-4=no, odd, even, mark, space
      PokeByte dcb, 72, stopBits                  ; 0,1,2 = 1, 1.5, 2
      
      apiSetCommState(driverHandle, dcb)
      apiSetCommMask(driverHandle, EV_TXEMPTY)
   EndIf
   
   FreeBank dcb
   Return driverHandle
End Function

Function closeComport(driverHandle)
   If driverHandle = INVALID_HANDLE_VALUE Or driverHandle = 0 Then
      Return False
   Else
      Return apiCloseHandle(driverHandle)
   EndIf
End Function

Function writeComport(driverHandle, numBytes, buffer)
   Local state, temp

   If driverHandle = INVALID_HANDLE_VALUE Or driverHandle = 0 Then
      Return False
   EndIf
   
   apiEscapeCommFunction(driverHandle, SETRTS)
   
   temp = CreateBank(4)
   state = apiWriteFile(driverHandle, buffer, numBytes, temp, 0)
   
   apiEscapeCommFunction(driverHandle, CLRRTS)
   
   FreeBank temp
   Return state
End Function

Function readComport(driverHandle, bytesRead, bufferSize, buffer, tOut)
	Local comErrors, comStat
	
   If driverHandle = INVALID_HANDLE_VALUE Or driverHandle = 0 Then
      Return False
   EndIf
	
	PokeInt timeouts, 00, 0
	PokeInt timeouts, 04, 0
	PokeInt timeouts, 08, tOut
	PokeInt timeouts, 12, 0
	apiSetCommTimeouts(driverHandle, timeouts)
	
	comErrors = CreateBank(4)
	comStat   = CreateBank(10)
	apiClearCommError(driverHandle, comErrors, comStat)
	
	apiReadFile(driverHandle, buffer, bufferSize, bytesRead, 0)
	
	FreeBank comErrors
	FreeBank comStat
	Return True
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Vertex</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Sorry i have forgot the decls:<br><pre class=code>
.lib "Kernel32.dll"
apiCreateFile%(lpFileName$, dwDesiredAccess, dwShareMode, lpSecurrityAttributes, dwCreationDistribution, dwFlagsAndAttributes, hTemplateFile) : "CreateFileA"
apiSetupComm%(hFile, dwInQueue, dwOutQueue)                                                                                                   : "SetupComm"
apiGetCommState%(hFile, lpDCB*)                                                                                                               : "GetCommState"
apiSetCommState%(hFile, lpCDB*)                                                                                                               : "SetCommState"
apiSetCommMask%(hFile, dwEvtMask)                                                                                                             : "SetCommMask"
apiCloseHandle%(hObject)                                                                                                                      : "CloseHandle"
apiEscapeCommFunction%(hFile, dwFunc)                                                                                                         : "EscapeCommFunction"
apiWriteFile%(hFile, lpBuffer*, nNumberOfBytesToWrite, lpNumberOfBytesWritten*, lpOverlapped)                                                 : "WriteFile"
apiSetCommTimeouts%(hFile, lpCommTimeouts*)                                                                                                   : "SetCommTimeouts"
apiClearCommError%(hFile, lpErrors*, lpStat*)                                                                                                 : "ClearCommError"
apiReadFile%(hFile, lpBuffer, nNumberOfBytesToRead, lpNumberOfBytesRead*, lpOverlapped)                                                       : "ReadFile"
</pre><br>And the<br><pre class=code>
hCom = openComport(1, 9600)
If hCom &lt;&gt; 0 Then
   Print "   Succesfully"
Else
   Print "   Error"
   WaitKey : End
EndIf
</pre><br>must be replace to<br><pre class=code>
hCom = openComport(1, 9600)
If hCom &lt;&gt; 0 and hCOM &lt;&gt; INVALID_HANDLE_VALUE Then
   Print "   Succesfully"
Else
   Print "   Error"
   WaitKey : End
EndIf
</pre><br>cu olli <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nigel Brown</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Am having trouble reading data from read buffer get an abnormal program termination if i try and read from the buffer with "PeekByte(buffer,0)" or release the buffer with "FreeBank buffer" ? <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Vertex</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Hi!<br>Oh sorry yes,<br>apiReadFile%(hFile, lpBuffer, nNumberOfBytesToRead, lpNumberOfBytesRead*, lpOverlapped) : "ReadFile"<br>must be<br>apiReadFile%(hFile, lpBuffer*, nNumberOfBytesToRead, lpNumberOfBytesRead*, lpOverlapped) : "ReadFile"<br><br>So the function has only poke datas in the BlitzBankStructure and has overwrite the pointer to the memoryblock.<br><br>cu olli <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nigel Brown</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> none of the word control settings ie: dataBits,parityBit,stopBits seem to make any difference when you change thier values? this means it is dependent on the port being set correctly by something else! I have checked this on a logic analyzer. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Vertex</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Hmmm if you set the parityBit, then the receiverhardware can use this as a checksum. The stopBits can be 1, 1.5, 2. This stopBit will be add at the end on a transmited value. But i can't test it. I have only here a 56k Modem to test, but it doesn't reacted on simply AT-Commandos like "ATL3" or "ATDT 0123456789"(but i can see, that the modem receive datas).<br>cu olli <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nigel Brown</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> there seems to be 7 data bits sent rather than the 8 required and asked for by the setting of dataBits. After running another piece of software that sets data bits to 8 all is well.<br>This suggests to me that the dataBits setting is having no effect. I did wonder about the offset into the dcb but calculated it at the same offset as you? <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> I think you need to test the SetCommState return value to see whether you are making any difference to the port settings or not. I set the dcblength, call GetCommState then alter the relevant values and then call SetCommState, testing it's return value for success. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nigel Brown</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Ok get what youre trying to say i think. I believe the following should set then read back 8 data bits. On my machine it returns 0 data bits? Tell me I am doing something wrong please!<br><br><pre class=code>

Const INVALID_HANDLE_VALUE = -1

Const  GENERIC_READ        = $80000000
Const  GENERIC_WRITE       = $40000000
Const  OPEN_EXISTING       = 3
Const  EV_TXEMPTY          = 4

	driverHandle = apiCreateFile("COM1", GENERIC_READ Or GENERIC_WRITE, 0, 0, OPEN_EXISTING, 0, 0)

	; Create DCB
	dcb = CreateBank(80)
	PokeInt dcb, 0, 80							; sizeof(DCB)
	
	; Get the Comm state
	apiGetCommState(driverHandle, dcb )
	Print PeekByte(dcb,70)						; should return ?

	; Set dataBits
	PokeByte dcb, 70, 8							; number of bits/byte, 4-8

	; Set comm state
	apiSetCommState(driverHandle, dcb)
	apiSetCommMask(driverHandle, EV_TXEMPTY)



	; Create another DCB and read result into it		
	dcb2 = CreateBank(80)
	PokeInt dcb2, 0, 80							; sizeof(DCB)
	
	; Read result into new DCB
	apiGetCommState(driverHandle, dcb2 )
	Print PeekByte(dcb2,70)						; should return 8


	WaitKey()

</pre> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Vertex</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Yes, i get too a succeedvalue but ByteSize remains the same value. A athor method is possibly too use BuildCommDCB:<br>apiBuildCommDCB%(lpDef$, lpDCB*) : "BuildCommDCB"<br><br>here the quote of the WinAPI:<br>The BuildCommDCB function fills a specified DCB structure with values specified in a device-control string. The device-control string uses the syntax of the mode command.<br><br>BOOL BuildCommDCB(<br><br>    LPCTSTR lpDef,	// pointer to device-control string<br>    LPDCB lpDCB	// pointer to device-control block<br>   );	<br> <br><br>Parameters<br><br>lpDef<br><br>Pointer to a null-terminated string that specifies device-control information. The string must have the same form as the mode command's command-line arguments. For example, the following string specifies a baud rate of 1200, no parity, 8 data bits, and 1 stop bit: <br><br>baud=1200 parity=N data=8 stop=1  <br> <br><br>The device name is ignored if it is included in the string, but it must specify a valid device, as follows: <br><br>COM1: baud=1200 parity=N data=8 stop=1  <br><br><br>For further information on mode command syntax, refer to the end-user documentation for your operating system.<br><br>lpDCB<br><br>Pointer to a DCB structure to be filled in. <br><br> <br><br>Return Values<br><br>If the function succeeds, the return value is nonzero.<br>If the function fails, the return value is zero. To get extended error information, call GetLastError. <br><br>Remarks<br><br>The BuildCommDCB function adjusts only those members of the DCB structure that are specifically affected by the lpDef parameter, with the following exceptions: <br><br>·	If the specified baud rate is 110, the function sets the stop bits to 2 to remain compatible with the Windows NT or MS-DOS mode command. <br>·	By default, BuildCommDCB disables XON/XOFF and hardware flow control. To enable flow control, you must explicitly set the appropriate members of the DCB structure. <br><br> <br><br>The BuildCommDCB function only fills in the members of the DCB structure. To apply these settings to a serial port, use the SetCommState function. <br>There are older and newer forms of the mode command syntax. The BuildCommDCB function supports both forms. However, you cannot mix the two forms together.<br>The newer form of the mode command syntax lets you explicitly set the values of the flow control members of the DCB structure. If you use an older form of the mode syntax, the BuildCommDCB function sets the flow control members of the DCB structure, as follows:<br><br>·	For a string such as 96,n,8,1 or any other older-form mode string that doesn't end with an x or a p:<br><br>fInX, fOutX,fOutXDsrFlow,and fOutXCtsFlow are all set to FALSE<br>fDtrControl is set to DTR_CONTROL_ENABLE<br>fRtsControl is set to RTS_CONTROL_ENABLE <br><br>·	For a string such as 96,n,8,1,x or any other older-form mode string that finishes with an x: <br><br>fInX, fOutX are both set to TRUE<br>fOutXDsrFlow,fOutXCtsFlow are both set to FALSE.<br>fDtrControl is set to DTR_CONTROL_ENABLE<br>fRtsControl is set to RTS_CONTROL_ENABLE<br><br>·	For a string such as 96,n,8,1,p or any other older-form mode string that finishes with a p: <br><br>fInX, fOutX are both set to FALSE<br>fOutXDsrFlow,fOutXCtsFlow are both set to TRUE.<br>fDtrControl is set to DTR_CONTROL_HANDSHAKE<br>fRtsControl is set to RTS_CONTROL_HANDSHAKE<br><br> <br><br>See Also<br><br>DCB, SetCommState<br><br>cu olli <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nigel Brown</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> a working example please, one that tests the results. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> try this one:<br><br><pre class=code>
Const INVALID_HANDLE_VALUE = -1

Const  GENERIC_READ        = $80000000
Const  GENERIC_WRITE       = $40000000
Const  OPEN_EXISTING       = 3
Const  EV_TXEMPTY          = 4

driverHandle = apiCreateFile("COM1", GENERIC_READ Or GENERIC_WRITE, 0, 0, OPEN_EXISTING, 0, 0)

Print "handle="+driverhandle

; Create DCB

dcb = CreateBank(28)
PokeInt dcb, 0, 28 ; sizeof(DCB)
	
; Get the Comm state

apiGetCommState(driverHandle,dcb)
Print PeekByte(dcb,18)  ; print data bits

WaitKey()
</pre> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nigel Brown</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Yes that returns a numeric handle, and also the value 8 but the offset into the DCB is 18 the offset used in the previous prog for dataBits was 70, look's like thats the problem the offsets are incorrect<br><br>What is going on?<br><br>The offsets for the DCB at offset 8 are bits not DWORDS.<br><br><br>;typedef struct _DCB {<br>;    DWORD DCBlength;      /* sizeof(DCB)                     */ 		= 0    = 28<br>;    DWORD BaudRate;       /* Baudrate at which running       */ 		= 4<br>;    DWORD fBinary: 1;     /* Binary Mode (skip EOF check)    */ 		= 8<br>;    DWORD fParity: 1;     /* Enable parity checking          */ 		=<br>;    DWORD fOutxCtsFlow:1; /* CTS handshaking on output       */ 		= <br>;    DWORD fOutxDsrFlow:1; /* DSR handshaking on output       */ 		=<br>;    DWORD fDtrControl:2;  /* DTR Flow control                */ 		=<br>;    DWORD fDsrSensitivity:1; /* DSR Sensitivity              */ 		=<br>;    DWORD fTXContinueOnXoff: 1; /* Continue TX when Xoff sent */		=<br>;    DWORD fOutX: 1;       /* Enable output X-ON/X-OFF        */ 		=<br>;    DWORD fInX: 1;        /* Enable input X-ON/X-OFF         */ 		=<br>;    DWORD fErrorChar: 1;  /* Enable Err Replacement          */ 		=<br>;    DWORD fNull: 1;       /* Enable Null stripping           */ 		=<br>;    DWORD fRtsControl:2;  /* Rts Flow control                */ 		=<br>;    DWORD fAbortOnError:1; /* Abort all reads and writes on Error */	=<br>;    DWORD fDummy2:17;     /* Reserved                        */		=<br>;    WORD wReserved;       /* Not currently used              */		= 12<br>;    WORD XonLim;          /* Transmit X-ON threshold         */		= 14<br>;    WORD XoffLim;         /* Transmit X-OFF threshold        */		= 16<br>;    BYTE ByteSize;        /* Number of bits/byte, 4-8        */		= 18<br>;    BYTE Parity;          /* 0-4=None,Odd,Even,Mark,Space    */		= 19<br>;    BYTE StopBits;        /* 0,1,2 = 1, 1.5, 2               */		= 20<br>;    char XonChar;         /* Tx and Rx X-ON character        */		= 21<br>;    char XoffChar;        /* Tx and Rx X-OFF character       */		= 22<br>;    char ErrorChar;       /* Error replacement char          */		= 23<br>;    char EofChar;         /* End of Input character          */		= 24<br>;    char EvtChar;         /* Received Event character        */		= 25<br>;    WORD wReserved1;      /* Fill for now.                   */		= 26<br>;<br>;} DCB, *LPDCB; <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nigel Brown</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Think this is a valid replacement for the openComport supplied above, will fix any bugs if any :-)<br><br><pre class=code>
Function openComport(comport, baudRate)

	Local dcbBaudRate, driverHandle, dcb
   
	If comport &gt; 255 Or comport &lt; 0 Then
		Return INVALID_HANDLE_VALUE
	EndIf
   
	Select baudRate
		Case 110
			dcbBaudRate = CBR_110
		Case 300
			dcbBaudRate = CBR_300
		Case 600
			dcbBaudRate = CBR_600
		Case 1200
			dcbBaudRate = CBR_1200
		Case 2400
			dcbBaudRate = CBR_2400
		Case 4800
			dcbBaudRate = CBR_4800
		Case 9600
			dcbBaudRate = CBR_9600
		Case 14400
			dcbBaudRate = CBR_14400
		Case 19200
			dcbBaudRate = CBR_19200
		Case 38400
			dcbBaudRate = CBR_38400
		Case 56000
			dcbBaudRate = CBR_56000
		Case 57600
			dcbBaudRate = CBR_57600
		Case 115200
			dcbBaudRate = CBR_115200
		Default
			Return INVALID_HANDLE_VALUE
	End Select
	
	driverHandle = apiCreateFile("COM"+comport, GENERIC_READ Or GENERIC_WRITE, 0, 0, OPEN_EXISTING, 0, 0)
	If driverHandle = INVALID_HANDLE_VALUE Then
		Return INVALID_HANDLE_VALUE
	Else
		apiSetupComm(driverHandle, 1024, 1024)

		dcb = CreateBank(28)
		
		PokeInt dcb, 00, 28                         ; sizeof(DCB)
		PokeInt dcb, 04, dcbBaudRate                ; current baud rate

		val	= 1										; Binary Mode (skip Eof check)    
		If parityEnable	= True Then val = val Or 2	; Enable parity checking
		If protocol		= DTR_CONTROL Then val = val Or 48	; DTR Flow control on
		PokeByte dcb, 08, val
		
		val = 0
		If protocol = RTS_CONTROL Then val = val Or 48
		PokeByte dcb, 09, val

		PokeByte dcb, 18, dataBits					; number of bits/byte, 4-8
		PokeByte dcb, 19, parityBit					; 0-4=no, odd, even, mark, space
		PokeByte dcb, 20, stopBits					; 0,1,2 = 1, 1.5, 2

		apiSetCommState(driverHandle, dcb)
		apiSetCommMask(driverHandle, EV_TXEMPTY)

	EndIf
   
	FreeBank dcb
	Return driverHandle
	
End Function


</pre> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Nigel, note the :1 etc. bit field operators. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nigel Brown</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> thanks skidracer, I think that was what I said 2 posts ago?<br><br>"What is going on? The offsets for the DCB at offset 8 are bits not DWORDS."<br><br>Or were you hinting at a bug you may have found? <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MErren</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Hello Vertex,<br>can you help me <br>I know the out comand and wan't to translate the following code to BB.<br>10 BA=&amp;H2F8 : REM COM2<br>20 OUT (BA+3), 64<br>30 CLS<br>40 PRINT " SERAI812 an COM2<br>100 REM Messung an 8 Kan?len<br>110 LOCATE 10,1<br>120 FOR N= 0 TO 7<br>130 SEL = 7-(N\2) -4*(N AND 1)<br>140 GOSUB 500<br>150 PRINT N,<br>160 PRINT USING "##.###"; U;<br>170 PRINT " V"<br>180 NEXT N<br>190 FOR T= 1 TO 10000 : NEXT T<br>200 LOCATE 10,1<br>210 GOTO 120<br>500 REM ************* Ansteuerung des MAX186 ******<br>510 CMD = 128 + 16 * SEL + 8 + 4 + 2<br>520 BIT = 128<br>530 FOR B= 1 TO 8<br>540 IF (CMD AND BIT)=BIT THEN RTS = 2 ELSE RTS=0<br>550 OUT (BA+4),RTS<br>560 OUT (BA+4),RTS+1<br>570 OUT (BA+4),RTS<br>580 BIT = BIT \ 2<br>590 NEXT B<br>595 OUT (BA+4),0<br>600 FOR T= 1 TO 10 : NEXT T<br>610 BIT = 2048 : AUS = 0<br>630 FOR B= 1 TO 16<br>640 OUT (BA+4),1<br>650 OUT (BA+4),0<br>660 IF ((INP(BA+6) AND 16)=16) THEN AUS = AUS + BIT<br>670 BIT = BIT \ 2<br>680 NEXT B<br>690 U = AUS / 1000<br>700 RETURN<br><br>can you help me? <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TAS</td><td align="right"><font class=tiny>2005</font></td></tr></table></td></tr><tr ><td class="posttext"> At least on the computers I have tried SetcommState is unable to set bits to 8 when stopbits are set to 1 and returns 0 (failed).  However 8,0 seems to work in place of 8,1 for the equipment I am communicating with.<br><br>SetCommState and result analysis code below.<br><br>;kernel32.decls file <br>.lib "kernel32.dll"<br>api_CreateFile%(lpFileName$, dwDesiredAccess, dwShareMode, lpSecurrityAttributes*, dwCreationDistribution, dwFlagsAndAttributes, hTemplateFile) : "CreateFileA" <br>api_SetupComm%(hFile, dwInQueue, dwOutQueue) : "SetupComm" <br>api_GetCommState%(hFile, lpDCB*) : "GetCommState" <br>api_SetCommState%(hFile, lpCDB*) : "SetCommState" <br>api_SetCommMask%(hFile, dwEvtMask) : "SetCommMask" <br>api_CloseHandle%(hObject) : "CloseHandle" <br>api_EscapeCommFunction%(hFile, dwFunc) : "EscapeCommFunction" <br>api_WriteFile%(hFile, lpBuffer*, nNumberOfBytesToWrite, lpNumberOfBytesWritten*, lpOverlapped) : "WriteFile" <br>api_SetCommTimeouts%(hFile, lpCommTimeouts*) : "SetCommTimeouts" <br>api_ClearCommError%(hFile, lpErrors*, lpStat*) : "ClearCommError" <br>api_ReadFile%(hFile, lpBuffer*, nNumberOfBytesToRead, lpNumberOfBytesRead*, lpOverlapped) : "ReadFile"<br><br><br>;Code *********************<br><br>dcb = CreateBank(28) <br>driverHandle = api_CreateFile("COM1", $80000000 Or $40000000 , 0, dcb, 3, 0, 0) <br>If driverHandle = -1 Then Notify "CreateFile failed" :End<br><br>If Not api_SetupComm(driverHandle, 1024, 1024) Then Notify "SetupComm failed": End<br><br>PokeInt   dcb, 00, 28 	; sizeof(DCB) <br>PokeInt   dcb, 04, 9600	; baud rate <br>PokeByte  dcb, 08, 1  	; Binary Mode (skip Eof check)<br>PokeByte  dcb, 09, 0	; No flow control<br>PokeShort dcb, 14, 2048	; Xon default<br>PokeShort dcb, 16, 512	; XOff default<br>PokeByte  dcb, 18, 8 	; number of bits per byte, 4-8 <br>PokeByte  dcb, 19, 0	; Parity 0-4=no, odd, even, mark, space <br>PokeByte  dcb, 20, 0 	; StopBits 0,1,2 = 1, 1.5, 2 <br>PokeByte  dcb, 21, 17	; Xon Char default<br>PokeByte  dcb, 22, 19	; Xoff Char default<br>; 8 bits/byte and 1 stopbit fails!!<br>; 8 And 0 OK<br>; 6 And 2 OK<br>; 7 and 2 OK<br><br>r=api_SetCommState(driverHandle, dcb )<br><br>; Get the Comm state <br>For i=0 To 28-1<br>	PokeByte dcb,i,0<br>Next<br>PokeInt dcb, 0, 28 ; sizeof(DCB) <br>api_GetCommState(driverHandle,dcb) <br><br>If r=1 Then<br>	;short report<br>	mx$= "SetCommState Suceed!"+Chr$(13)<br>	mx$=mx$+ "handle = "+driverhandle +Chr$(13)<br>	mx$=mx$+ "Baudrate " 		+PeekInt(dcb,4)   +Chr$(13)<br>	mx$=mx$+ "Databits " 		+PeekByte(dcb,18) +Chr$(13)<br>	mx$=mx$+ "Parity   " 		+PeekByte(dcb,19) +Chr$(13)<br>	mx$=mx$+ "Stopbits " 		+PeekByte(dcb,20) +Chr$(13)<br>	Notify mx$	<br>Else	<br>	;detailed report<br>	Print "SetCommState failed!!"<br>	Print ""<br>	Print "DCB size "+PeekInt(dcb,0)<br>	Print "Baudrate "+PeekInt(dcb,4)<br>	Print "Bit Fields 0-7  "+Right$(Bin(PeekInt(dcb,8)),8)<br>	Print "Bit Fields 8-15 "+Right$(Bin(PeekInt(dcb,8)/256),8)<br>	Print "Res "+PeekShort(dcb,12)<br>	Print "Xon "+PeekShort(dcb,14)<br>	Print "Xoff "+PeekShort(dcb,16)<br>	Print "Bits "+PeekByte(dcb,18)<br>	Print "Parity "+PeekByte(dcb,19)<br>	Print "Stop Bit "+PeekByte(dcb,20)<br>	Print "Char "+PeekByte(dcb,21)<br>	Print "Char "+PeekByte(dcb,22)<br>	Print "Char "+PeekByte(dcb,23)<br>	Print "Char "+PeekByte(dcb,24)<br>	Print "Char "+PeekByte(dcb,25)<br>	Print "Res  "+PeekShort(dcb,26)<br>	Stop<br>EndIf<br><br>api_CloseHandle(driverHandle)	<br><br><br>;Type dcb<br>;0         DCBlength As Integer<br>;4         BaudRate As Integer<br>;8         **fBitFields As Integer**<br>;12        wReserved As short<br>;14        XonLim As short<br>;16        XoffLim As short<br>;18        ByteSize As Byte<br>;19        Parity As Byte<br>;20        StopBits As Byte<br>;21        XonChar As Byte<br>;22        XoffChar As Byte<br>;23        ErrorChar As Byte<br>;24        EofChar As Byte<br>;25        EvtChar As Byte<br>;26        wReserved1 As short 'Reserved<br>;End Type<br><br>; The fourteen actual DCB bit-sized Data fields within the four bytes of fBitFields <br>; FieldName             Bit #     Description<br>; -----------------     -----     ------------------------------<br>; fBinary                 1 1      binary mode, no Eof check<br>; fParity                 2 2      enable parity checking<br>; fOutxCtsFlow            3 4      CTS output flow control<br>; fOutxDsrFlow            4 8      DSR output flow control<br>; fDtrControl             5 16,32  DTR flow control Type (2 bits)<br>; fDsrSensitivity         7 64     DSR sensitivity<br>; fTXContinueOnXoff       8 128    XOFF continues Tx<br>; fOutX                   9 1      XON/XOFF out flow control<br>; fInX                   10 2      XON/XOFF in flow control<br>; fErrorChar             11 4      enable error replacement<br>; fNull                  12 8      enable Null stripping<br>; fRtsControl            13 16,32  RTS flow control (2 bits)<br>; fAbortOnError          15 48     abort reads/writes on error<br>; fDummy2                16 64     reserved <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Litobyte</td><td align="right"><font class=tiny>2008</font></td></tr></table></td></tr><tr ><td class="posttext"> typedef struct _DCB {<br>    DWORD DCBlength;      /* sizeof(DCB)                     */<br>    DWORD BaudRate;       /* Baudrate at which running       */<br>    DWORD fBinary: 1;     /* Binary Mode (skip EOF check)    */<br>    DWORD fParity: 1;     /* Enable parity checking          */<br>    DWORD fOutxCtsFlow:1; /* CTS handshaking on output       */<br>    DWORD fOutxDsrFlow:1; /* DSR handshaking on output       */<br>    DWORD fDtrControl:2;  /* DTR Flow control                */<br>    DWORD fDsrSensitivity:1; /* DSR Sensitivity              */<br>    DWORD fTXContinueOnXoff: 1; /* Continue TX when Xoff sent */<br>    DWORD fOutX: 1;       /* Enable output X-ON/X-OFF        */<br>    DWORD fInX: 1;        /* Enable input X-ON/X-OFF         */<br>    DWORD fErrorChar: 1;  /* Enable Err Replacement          */<br>    DWORD fNull: 1;       /* Enable Null stripping           */<br>    DWORD fRtsControl:2;  /* Rts Flow control                */<br>    DWORD fAbortOnError:1; /* Abort all reads and writes on Error */<br>    DWORD fDummy2:17;     /* Reserved                        */<br>    WORD wReserved;       /* Not currently used              */<br>    WORD XonLim;          /* Transmit X-ON threshold         */<br>    WORD XoffLim;         /* Transmit X-OFF threshold        */<br>    BYTE ByteSize;        /* Number of bits/byte, 4-8        */<br>    BYTE Parity;          /* 0-4=None,Odd,Even,Mark,Space    */<br>    BYTE StopBits;        /* 0,1,2 = 1, 1.5, 2               */<br>    char XonChar;         /* Tx and Rx X-ON character        */<br>    char XoffChar;        /* Tx and Rx X-OFF character       */<br>    char ErrorChar;       /* Error replacement char          */<br>    char EofChar;         /* End of Input character          */<br>    char EvtChar;         /* Received Event character        */<br>    WORD wReserved1;      /* Fill for now.                   */<br>} DCB, *LPDCB; <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> please see this <a href="/codearcs/codearcs.php?code=1383" target="_blank">Archive</a> for more recent code <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
