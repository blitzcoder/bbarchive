<!DOCTYPE html><html lang="en" ><head ><title >[maxgui] Custom Image Button</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Simple custom image button (with toggle/cycle and mouseover), language=bmx, category=User Input'><meta name='author' content='Beaker'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=5>User Input</a>/[maxgui] Custom Image Button</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1534.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >[maxgui] Custom Image Button by Beaker</td><td align="right">2005 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Image Button needs some images which you can get from here:<br>1<img src="http://myweb.tiscali.co.uk/thetiffanys/tmp/test1.PNG"> 2<img src="http://myweb.tiscali.co.uk/thetiffanys/tmp/test1h.PNG">3<img src="http://myweb.tiscali.co.uk/thetiffanys/tmp/test2.PNG"> 4<img src="http://myweb.tiscali.co.uk/thetiffanys/tmp/test2h.PNG"> 5<img src="http://myweb.tiscali.co.uk/thetiffanys/tmp/test3.PNG"><br><br>Demonstrates the use of hooks and gadget extending.<br><br>Thanks to Mark Tiffany. </td></tr><tr ><td class="cell"><pre class="code">' Custom Image Button 
' ===================
' A custom "gadget" that can be used like web gadgets, 
' i.e. showing an alternate image on mouseover, and
' on single click cycling through a series of options.
'
' Originally created by Beaker
' Improved upon by Mark Tiffany

Strict 
Type TApplet 

	Method OnEvent(Event:TEvent) Abstract

	Method New()
		AddHook EmitEventHook,eventhook,Self
	End Method

	Function eventhook:Object(id,data:Object,context:Object)
		Local	event:TEvent
		Local	app:TApplet
		event=TEvent(data)
		If Not event Then Return
		app=TApplet(context)
		app.OnEvent event	
		Return data
	End Function

End Type


Type TImageButton Extends TApplet
	Field index
	Field lastind
	Field depressed
	Field mouseover

	Field pix:TPixmap[]
	Field rollpix:TPixmap[]
	Field pan:TGadget
		
	Method OnEvent(Event:TEvent)
		'make sure this was intended for us!
		If event.source&lt;&gt;pan Then Return
		
		Select event.id
			Case EVENT_MOUSEDOWN
				If event.data=1
					Local i:Int=(index + 1) Mod lastind
					' set the image to the new image or rollover image
					If rollpix[i]=Null Then
						SetPanelPixmap pan,pix[i]	
					Else
						SetPanelPixmap pan,rollpix[i]									
					End If
					depressed=True					
				EndIf
		
			Case EVENT_MOUSEUP
				If event.data=1 And depressed And mouseover Then
					index = (index + 1) Mod lastind
					' set the image to the new image or rollover image
					If rollpix[index]=Null Or event.x&lt;0 Or event.x&gt;pan.width Or event.y&lt;0 Or event.y&gt;pan.height Then
						SetPanelPixmap pan,pix[index]	
					Else
						SetPanelPixmap pan,rollpix[index]									
					End If
					
					'and emit an event for someone else to catch
					Local ev:TEvent = New TEvent.Create(EVENT_GADGETACTION,Self,index,event.mods,event.x,event.y)
					ev.Emit
				EndIf
				
			Case EVENT_MOUSEENTER
				If rollpix[index] &lt;&gt; Null
					SetPanelPixmap pan,rollpix[index]
				EndIf
				mouseover=True
				
			Case EVENT_MOUSELEAVE
				SetPanelPixmap pan,pix[index]				
				mouseover=False
				
		End Select
		' Print event.tostring() ' for debugging
	End Method

	Method Create:TImageButton(images$[],rollover$[],x,y,w,h,group:TGadget,style=0)
		index = 0
		lastind = images.length
		pix = New TPixmap[lastind]
		For Local f = 0 Until lastind
			pix[f] = LoadPixmap(images[f])
			' if load fails, then give up on creating the gadget
			If pix[f]=Null Then Return Null
		Next
		pan = CreatePanel(x,y,w,h,group,style|PANEL_ACTIVE)
		SetPanelPixmap pan,pix[0]	

		rollpix = New TPixmap[lastind]
		If rollover &lt;&gt; Null
			For Local f = 0 Until rollover.length
				rollpix[f] = LoadPixmap(rollover[f])
				' if load fails, then give up on creating the gadget
				If rollpix[f]=Null Then Return Null
			Next
		EndIf
		Return Self
	End Method
	
End Type

Function CreateImageButton:TImageButton(image$[],rollover$[],x,y,w,h,group:TGadget,style=0)
	Return New TImageButton.Create(image$,rollover$,x,y,w,h,group,style)
End Function

' create test GUI
Local window:TGadget = CreateWindow("My Window",40,40,320,240)

Local lbl1:TGadget = CreateLabel("This gadget cycles on single click through 3 values.",30,10,250,15,window)
Local butt1:TImageButton = CreateImageButton(["test1.png","test2.png","test3.png"],["","",""],30,30,28,28,window,PANEL_BORDER)

Local lbl2:TGadget = CreateLabel("This gadget is highlighted on mouse over.",30,60,250,15,window)
Local butt2:TImageButton = CreateImageButton(["test1.png","test2.png"],["test1h.png","test2h.png"],30,80,26,26,window)
Local txt2:TGadget = CreateTextField(100,80,60,30,window)
SetGadgetText txt2,0

' main loop
While True
	WaitEvent() 
	Select EventID()
		Case EVENT_WINDOWCLOSE
			End
		Case EVENT_GADGETACTION
			Select EventSource()
				Case butt2 ; SetGadgetText txt2,EventData()
			End Select
	End Select
Wend

End</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
