<!DOCTYPE html><html lang="en" ><head ><title >UDP Networking Class</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Easy OO UDP Networking, language=bmx, category=Networking'><meta name='author' content='JoshK'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=4>Networking</a>/UDP Networking Class</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2325.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >UDP Networking Class by JoshK</td><td align="right">2008 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This is a base networking lib on top of which more advanced features can be added. </td></tr><tr ><td class="cell"><pre class="code">SuperStrict

Import brl.socket
Import brl.map

Type TNetNode
	
	Const LOCALIP:Int=2130706433
	
	Global map:TMap=New TMap
	
	Field socket:TSocket
	Field ip:Int
	Field port:Int
	
	Method SendNetMessage:Int(recipient:TNetNode,data:Byte Ptr=Null,size:Int=0)
		If ip&lt;&gt;LOCALIP Return False
		Return sendto_(socket._socket,data,size,0,recipient.ip,recipient.port)
	EndMethod
 	
	Method GetNetMessage:TNetMessage()
		If ip&lt;&gt;LOCALIP Return Null
		Local message:TNetMessage,size:Int,ip:Int,port:Int,buf:Byte[]
		size=socket.ReadAvail()
		If size
			message=New TNetMessage
			message.data=New Byte[size]
			recvfrom_(socket._socket,message.data,size,0,ip,port)
			message.sender=Create(ip,port)
			message.size=message.data.length
			Return message
		EndIf
	EndMethod
 	
	Method WaitNetMessage:TNetMessage(timeout:Int=0)
		Local time:Int
		Local message:TNetMessage
		time=MilliSecs()
		Repeat
			message=GetNetMessage()
			If message Return message
			If timeout
				If MilliSecs()-time&gt;timeout Return Null
			EndIf
			Delay 1
		Forever
	EndMethod
	
	Function Create:TNetNode(ip:Int,port:Int=41000)
		Local netnode:TNetNode
		netnode=Find(ip,port)
		If netnode Return netnode
		netnode=New TNetNode
		netnode.ip=ip
		netnode.port=port
		If ip=LOCALIP
			netnode.socket=CreateUDPSocket()
			If Not BindSocket(netnode.socket,port) Return Null
		EndIf
		map.insert netnode,netnode
		Return netnode
	EndFunction
	
	Function Find:TNetNode(ip:Int,port:Int=41000)
		Local netnode:TNetNode
		netnode=New TNetNode
		netnode.ip=ip
		netnode.port=port
		netnode=TNetNode(map.valueforkey(netnode))
		Return netnode
	EndFunction
	
	Method Compare:Int(o:Object)
		Local netnode:TNetNode
		netnode=TNetNode(o)
		If netnode.ip&gt;ip Return 1
		If netnode.ip&lt;ip Return -1
		If netnode.port&gt;port Return 1
		If netnode.port&lt;port Return -1
		Return 0
	EndMethod
	
EndType

Type TNetMessage
	
	Field sender:TNetNode
	Field data:Byte[]
	Field size:Int
	
	Method ToString:String()
		Return String.fromCString(data)
	EndMethod
	
EndType

Function CreateNetNode:TNetNode(ip:Int=TNetNode.LOCALIP,port:Int=41000)
	Return TNetNode.Create(ip,port)
EndFunction

Function SendNetMessage:Int(sender:TNetNode,recipient:TNetNode,data:Byte Ptr,size:Int)
	Return sender.SendNetMessage(recipient,data,size)
EndFunction

Function GetNetMessage:TNetMessage(recipient:TNetNode)
	Return recipient.GetNetMessage()
EndFunction

Function WaitNetMessage:TNetMessage(recipient:TNetNode,timeout:Int=0)
	Return recipient.WaitNetMessage(timeout)
EndFunction</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>2008</font></td></tr></table></td></tr><tr ><td class="posttext"> Here is an example of usage:<br><pre class=code>SuperStrict

Framework brl.system

Import "network.bmx"

AppTitle=""

Local client:TNetNode[2]

client[0]=CreateNetNode(HostIp("127.0.0.1"),80)
client[1]=CreateNetNode(HostIp("127.0.0.1"),81)

SendNetMessage(client[0],client[1],"Hello!".toCstring(),7)

Local message:TNetMessage=WaitNetMessage(client[1],1000)
If message Notify message.ToString()</pre> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>2008</font></td></tr></table></td></tr><tr ><td class="posttext"> Looks handy, thanks. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chroma</td><td align="right"><font class=tiny>2008</font></td></tr></table></td></tr><tr ><td class="posttext"> Very clever and streamlined.  So how many players could this handle without threading? <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>2013</font></td></tr></table></td></tr><tr ><td class="posttext"> Anyone tried using this? The example program creates both client and server, so can 'see' both TNetNodes.<br><br>However, if you put the server in one program and the client in another, each program has no knowledge of the other TNetNode information.<br><br>I've just done a little hack that manually creates a TNetNode to represent the opposite party, setting only the other party's IP and port, and seems to work, but does this seem like a sensible approach?<br><br>SERVER:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Import "udpnet.bmx"

Local server:TNetNode = CreateNetNode (HostIp ("127.0.0.1"), 80)

' Hack representing client:

Local client:TNetNode = New TNetNode
client.ip = HostIp ("127.0.0.1")
client.port = 81

If server

	Local netmsg:TNetMessage
	Local msg:String
	
	Repeat
	
		netmsg = WaitNetMessage (server, 30)
		
		If netmsg

			msg = netmsg.ToString ()
			
			Print msg
		
		EndIf
		
	Until msg = "quit"

EndIf
</textarea><br><br>CLIENT:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Import "udpnet.bmx"

Local client:TNetNode = CreateNetNode (HostIp ("127.0.0.1"), 81)

' Hack representing server:

Local server:TNetNode = New TNetNode
server.ip = HostIp ("127.0.0.1")
server.port = 80

If client

	Graphics 1024, 768
	
	Repeat
	
		If KeyDown (KEY_LEFT)
			SendNetMessage (client, server, "LEFT".ToCString (), 4)
		Else
			If KeyDown (KEY_RIGHT)
				SendNetMessage (client, server, "RIGHT".ToCString (), 5)
			EndIf
		EndIf
		
		Cls
		
		Flip
		
	Until KeyHit (KEY_ESCAPE)
	
EndIf
</textarea><br><br>I've also taken out the references to LOCALIP, which I assume were just for testing purposes on a single PC, as they'd prevent sending over a network in a real situation:<br><br>UDPNET.BMX:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

SuperStrict

Import brl.socket
Import brl.map

Type TNetNode
	
	'Const LOCALIP:Int=2130706433
	
	Global map:TMap=New TMap
	
	Field socket:TSocket
	Field ip:Int
	Field port:Int
	
	Method SendNetMessage:Int(recipient:TNetNode,data:Byte Ptr=Null,size:Int=0)
	'	If ip&lt;&gt;LOCALIP Return False
		Return sendto_(socket._socket,data,size,0,recipient.ip,recipient.port)
	EndMethod
 	
	Method GetNetMessage:TNetMessage()
	'	If ip&lt;&gt;LOCALIP Return Null
		Local message:TNetMessage,size:Int,ip:Int,port:Int,buf:Byte[]
		size=socket.ReadAvail()
		If size
			message=New TNetMessage
			message.data=New Byte[size]
			recvfrom_(socket._socket,message.data,size,0,ip,port)
			message.sender=Create(ip,port)
			message.size=message.data.length
			Return message
		EndIf
	EndMethod
 	
	Method WaitNetMessage:TNetMessage(timeout:Int=0)
		Local time:Int
		Local message:TNetMessage
		time=MilliSecs()
		Repeat
			message=GetNetMessage()
			If message Return message
			If timeout
				If MilliSecs()-time&gt;timeout Return Null
			EndIf
			Delay 1
		Forever
	EndMethod
	
	Function Create:TNetNode(ip:Int,port:Int=41000)
		Local netnode:TNetNode
		netnode=Find(ip,port)
		If netnode Return netnode
		netnode=New TNetNode
		netnode.ip=ip
		netnode.port=port
		'If ip=LOCALIP
			netnode.socket=CreateUDPSocket()
			If Not BindSocket(netnode.socket,port) Return Null
		'EndIf
		map.insert netnode,netnode
		Return netnode
	EndFunction
	
	Function Find:TNetNode(ip:Int,port:Int=41000)
		Local netnode:TNetNode
		netnode=New TNetNode
		netnode.ip=ip
		netnode.port=port
		netnode=TNetNode(map.valueforkey(netnode))
		Return netnode
	EndFunction
	
	Method Compare:Int(o:Object)
		Local netnode:TNetNode
		netnode=TNetNode(o)
		If netnode.ip&gt;ip Return 1
		If netnode.ip&lt;ip Return -1
		If netnode.port&gt;port Return 1
		If netnode.port&lt;port Return -1
		Return 0
	EndMethod
	
EndType

Type TNetMessage
	
	Field sender:TNetNode
	Field data:Byte[]
	Field size:Int
	
	Method ToString:String()
		Return String.fromCString(data)
	EndMethod
	
EndType

Function CreateNetNode:TNetNode(ip:Int,port:Int=41000)
	Return TNetNode.Create(ip,port)
EndFunction

Function SendNetMessage:Int(sender:TNetNode,recipient:TNetNode,data:Byte Ptr,size:Int)
	Return sender.SendNetMessage(recipient,data,size)
EndFunction

Function GetNetMessage:TNetMessage(recipient:TNetNode)
	Return recipient.GetNetMessage()
EndFunction

Function WaitNetMessage:TNetMessage(recipient:TNetNode,timeout:Int=0)
	Return recipient.WaitNetMessage(timeout)
EndFunction
</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>2013</font></td></tr></table></td></tr><tr ><td class="posttext"> I set this up in two separate MaxIDE instances, side-by-side, so I can see the server Output tab while 'playing' in the client (left/right keys), but I notice some garbage coming into the output now and then, eg:<br><br><pre class=code>
LEFT
LEFTT
LEFTTo
LEFTT
</pre><br><br>I know packets can be lost, etc (though presumably this would be almost non-existent on localhost), but any ideas as to where the extra characters might be coming from? I don't even send a lower-case 'o' from the client! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Henri</td><td align="right"><font class=tiny>2013</font></td></tr></table></td></tr><tr ><td class="posttext"> Hello,<br><br>size variable seems to remain constant, so if you change this method from the UDPNET.bmx:<br><pre class=code>
Type TNetMessage
	
	Field sender:TNetNode
	Field data:Byte[]
	Field size:Int
	
	Method ToString:String()
		Return String.fromCString(data)
	EndMethod
	
EndType
</pre><br>...to this:<br><pre class=code>
Type TNetMessage
	
	Field sender:TNetNode
	Field data:Byte[]
	Field size:Int
	
	Method ToString:String()
		Local s:String
		For Local b:Byte = EachIn data
			s:+ Chr(b)
		Next
		Return s
	EndMethod
	
EndType
</pre><br>...it seems to work.<br><br>-Henri <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>2013</font></td></tr></table></td></tr><tr ><td class="posttext"> Hi Henri,<br><br>Many thanks for taking a look, that does indeed seem to work -- much appreciated! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hardcoal</td><td align="right"><font class=tiny>2013</font></td></tr></table></td></tr><tr ><td class="posttext"> Josh , thanks for simplifying this <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hardcoal</td><td align="right"><font class=tiny>2013</font></td></tr></table></td></tr><tr ><td class="posttext"> since i dont know so much about networking ive started investigating josh code.<br><br>I would like to increase the commands in this example.<br>for example how do you check if a port is already occupied.<br>and how do you close a client.<br><br>Anyway ill get it one way or the other<br>its all matter of practice right. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
