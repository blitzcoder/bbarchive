<!DOCTYPE html><html lang="en" ><head ><title >BlitzMail Deluxe</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='BlitzMail with error-checking, language=bb, category=Networking'><meta name='author' content='BlitzSupport'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=4>Networking</a>/BlitzMail Deluxe</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="23.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzMail Deluxe by BlitzSupport</td><td align="right">2001 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Hacked straight outta Mark's BlitzMail, this version performs error checking with the server, and is placed into a function for ease-of-plugging-in. </td></tr><tr ><td class="cell"><pre class="code">
AppTitle "BlitzMail Deluxe"
Graphics 640, 480

; -----------------------------------------------------------------------------
; BlitzMail Deluxe -- adapted from Mark Sibly's BlitzMail code
; -----------------------------------------------------------------------------
; Sends an email message with decent error checking...
; -----------------------------------------------------------------------------
; ***** INSERT EMAIL ADDRESSES below, as specified! *****
; -----------------------------------------------------------------------------

Const DebugSMTP = 1					; Debug printout = 1
Color 0, 255, 0						; '80s hacker job...

; -----------------------------------------------------------------------------
; . User settings
; -----------------------------------------------------------------------------

	; ---------------------------------------------------------------------
	; SMTP server (CrossWind's is public, but use your own if known)
	; ---------------------------------------------------------------------
		Global mailhost$ = "smtp.crosswinds.net"

	; ---------------------------------------------------------------------
	; Email client name (this program)
	; ---------------------------------------------------------------------
		Global mailer$ = "BlitzMail Deluxe"
		
	; ---------------------------------------------------------------------
	; Sender's email address (***** INSERT YOUR ADDRESS *****)
	; ---------------------------------------------------------------------
		Global mailfrom$ = ""

	; ---------------------------------------------------------------------
	; Sender's real name
	; ---------------------------------------------------------------------
		Global mailname$ = "John Wayne"

; -----------------------------------------------------------------------------
; . Message data
; -----------------------------------------------------------------------------

	; ---------------------------------------------------------------------
	; Send message to this address (***** INSERT RECIPIENT'S ADDRESS *****)
	; ---------------------------------------------------------------------
		mailto$ = ""

	; ---------------------------------------------------------------------
	; Message to send (use | for newline)... keep it fairly short!
	; ---------------------------------------------------------------------
		message$ = "Hi there,||I think you'll agree, BlitzMail rules!||Your friend, John Wayne.|Sent via BlitzMail Deluxe"

; -----------------------------------------------------------------------------
; . Send message, show response ("BlitzMailed!" means success)
; -----------------------------------------------------------------------------

	reply$ = BlitzMail (mailto$, "BlitzMail Deluxe Test!", message$)
	Delay 1000: RuntimeError reply$
	End














; -----------------------------------------------------------------------------
; . SMTP functions
; -----------------------------------------------------------------------------



; -----------------------------------------------------------------------------
; BlitzMail...
; -----------------------------------------------------------------------------
Function BlitzMail$ (mailto$, subject$, message$)
	
	If debugSMTP Then Print "": Print "BlitzMailing...": Print ""
	
	message$ = Replace$ (message$, "|", Chr (13) + Chr (10))
	error$ = "BlitzMailed!"
		
	t = OpenTCPStream (mailhost$, 25)

	If t

		; ---------------------------------------------------------------------
		; Service available?
		; ---------------------------------------------------------------------
		response$ = ReadLine (t)
		code$ = Code (response$)
		If code$ <> "220"
			If code$ = "421"
				error$ = "Service not available"
			Else
				error$ = response$
			EndIf		
			Goto abortSMTP
		EndIf
		
		; ---------------------------------------------------------------------
		; Say "Hi"
		; ---------------------------------------------------------------------
		WriteLine t, "HELO BlitzMail Deluxe"
		response$ = ReadLine (t)
		If Code (response$) <> "250"
			error$ = response$
			Goto abortSMTP
		EndIf


		; ---------------------------------------------------------------------
		; Non-existent command -- try it for error message!
		; ---------------------------------------------------------------------
;		WriteLine t, "LALA BlitzMail Deluxe"
;		response$ = ReadLine (t)
;		If Code (response$) <> "250"
;			error$ = response$
;			Goto abortSMTP
;		EndIf


		; ---------------------------------------------------------------------
		; Tell server who's sending
		; ---------------------------------------------------------------------
		WriteLine t, "MAIL FROM: <" + mailfrom$ + ">"
		response$ = ReadLine (t)
		code$ = Code (response$)
		If code$ <> "250"
			If code$ = "501"
				error$ = "Email sender not specified (or invalid address)"
			Else
				error$ = response$
			EndIf
			Goto abortSMTP
		EndIf

		; ---------------------------------------------------------------------
		; Tell server who it's going to		
		; ---------------------------------------------------------------------
		WriteLine t, "RCPT TO: <" + mailto$ + ">"
		response$ = ReadLine (t)
		code$ = Code (response$)
		If code$ <> "250"
			If code$ = "501"
				error$ = "Email recipient not specified (or invalid address)"
			Else
				error$ = response$
			EndIf
			Goto abortSMTP
		EndIf

		; ---------------------------------------------------------------------
		; Email data
		; ---------------------------------------------------------------------
		WriteLine t, "DATA"
		response$ = ReadLine (t)
		If Code (response$) <> "354"
			error$ = response$
			Goto abortSMTP
		EndIf

		; ---------------------------------------------------------------------
		; Headers
		; ---------------------------------------------------------------------
		WriteLine t, "Date: "		+ CurrentDate$ ()
		WriteLine t, "From: "		+ mailname$ + " <" + mailfrom$ + ">"
		WriteLine t, "To: "			+ mailto$ + " <" + mailto$ + ">"
		WriteLine t, "Subject: "	+ subject$
		WriteLine t, "X-Mailer: "	+ mailer$

		; ---------------------------------------------------------------------
		; Email message
		; ---------------------------------------------------------------------
		WriteLine t, message$

		; ---------------------------------------------------------------------
		; End of message
		; ---------------------------------------------------------------------
		WriteLine t, ""
		WriteLine t, "."
		response$ = ReadLine (t)
		If Code (response$) <> "250"
			error$ = response$
		EndIf

		; ---------------------------------------------------------------------
		; Say "ciao"
		; ---------------------------------------------------------------------
		WriteLine t, "QUIT"
		response$ = ReadLine (t)
		If Code (response$) <> "221"
			error$ = response$
		EndIf

		; ---------------------------------------------------------------------
		; Return error message, if any
		; ---------------------------------------------------------------------
		.abortSMTP
		CloseTCPStream t
		If error$ = "" Then error$ = "Timeout error"
		Return error$
		
	Else

		; ---------------------------------------------------------------------
		; Oops. Forgot to go online (or server isn't there)	
		; ---------------------------------------------------------------------
		Return "Failed to connect to server at " + mailhost$
		
	EndIf
		
End Function

; -----------------------------------------------------------------------------
; Ask server for help (usually list of commands)... not much use, just example
; -----------------------------------------------------------------------------

Function GetHelp (server$)
	t = OpenTCPStream (mailhost$, 25)
	If t
		ReadLine (t) ; 220
		WriteLine t, "HELO BlitzMail Deluxe"
		ReadLine (t) ; 250
		WriteLine t, "HELP"
		response$ = ReadLine (t)
		error$ = Left (response$, 3)
		If error$ = "214"
			help$ = response$
			Repeat
				readhelp$ = ReadLine (t)
				help$ = help$ + Chr (10) + readhelp$
			Until readhelp$ = ""
			RuntimeError help$
		Else
			RuntimeError "Couldn't get help information!"
		EndIf
		CloseTCPStream (t)
	EndIf
End Function

; -----------------------------------------------------------------------------
; Return 3 digit code from server's response
; -----------------------------------------------------------------------------

Function Code$ (code$)
	If debugSMTP
		Print code$
	EndIf
	Return Left (code$, 3)
End Function
</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RifRaf</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> i get a timeout error everytime <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dan_upright</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> i get a timeout error everytime <br></div>then you've probably set it up wrong or there's a problem with your mail server - the code works fine for me <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RifRaf</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> ; -----------------------------------------------------------------------------<br>; . User settings<br>; -----------------------------------------------------------------------------<br><br>	; ---------------------------------------------------------------------<br>	; SMTP server (CrossWind's is public, but use your own if known)<br>	; ---------------------------------------------------------------------<br>		Global mailhost$ = "smtp.msnhotmail.net"<br><br>	; ---------------------------------------------------------------------<br>	; Email client name (this program)<br>	; ---------------------------------------------------------------------<br>		Global mailer$ = "BlitzMail Deluxe"<br>		<br>	; ---------------------------------------------------------------------<br>	; Sender's email address (***** INSERT YOUR ADDRESS *****)<br>	; ---------------------------------------------------------------------<br>		Global mailfrom$ = "Aieroulis5@..."<br><br>	; ---------------------------------------------------------------------<br>	; Sender's real name<br>	; ---------------------------------------------------------------------<br>		Global mailname$ = "John Wayne"<br><br>; -----------------------------------------------------------------------------<br>; . Message data<br>; -----------------------------------------------------------------------------<br><br>		mailto$ = "bigchance25@..."<br><br>		message$ = "Hi there,||I think you'll agree, BlitzMail rules!||Your friend, John Wayne.|Sent via BlitzMail Deluxe"<br><br>	reply$ = BlitzMail (mailto$, "BlitzMail Deluxe Test!", message$)<br><br>i have a hotmail accoutn, shouldnt the above work ok ? <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Some SMTP servers require you to log into your POP3 account before they let you in. No idea if that's the case here, but it's (almost) definitely not the code causing timeouts! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yan</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> I think Hotmail uses HTTP not SMTP.<br><br>[EDIT]Yup...<a href="http://www.webdav.org/" target="_blank">webDAV</a>...apparently[/EDIT]<br><br><br>YAN <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RifRaf</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> OK GUYS I SET UP A CROSSWINDS ACCOUTN, I STILL GET THE TIMEOUT ERROR.. weird huh.. Why dont you have to enter a password to send mail.. i mean all yo need to know is someones account name and you an jack it to send emails?<br><br>here is how i set it up.. when i ran it "timeout error"<br><br>	; ---------------------------------------------------------------------<br>	; SMTP server (CrossWind's is public, but use your own if known)<br>	; ---------------------------------------------------------------------<br>		Global mailhost$ = "216.18.117.43"<br>;also tried smtp.crosswinds.net   no go either that above is the straight ip for their smtp server.<br>	; ---------------------------------------------------------------------<br>	; Email client name (this program)<br>	; ---------------------------------------------------------------------<br>		Global mailer$ = "BlitzMail Deluxe"<br>		<br>	; ---------------------------------------------------------------------<br>	; Sender's email address (***** INSERT YOUR ADDRESS *****)<br>	; ---------------------------------------------------------------------<br>		Global mailfrom$ = "myaccountnamehere@..."<br><br>	; ---------------------------------------------------------------------<br>	; Sender's real name<br>	; ---------------------------------------------------------------------<br>		Global mailname$ = "Jeff Frazier" <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Regular K</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> so what is hotmails address you would use for something like this?<br><br>smtp.msnhotmail.net ? Doesnt work <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Craig Watson</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Hotmail doesn't do SMTP.<br><br>Your Internet Service Provider should provide you with an SMTP server to use. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jay Kyburz</td><td align="right"><font class=tiny>2005</font></td></tr></table></td></tr><tr ><td class="posttext"> anybody done logging into pop 3 and recieving mail as well?<br><br>dont wooryy found it here<br><a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=725" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=725</a> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >KuRiX</td><td align="right"><font class=tiny>2005</font></td></tr></table></td></tr><tr ><td class="posttext"> Hotmail requires logging in. To login you need to provide your user and password. No idea about how to send password's to an smtp server. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
