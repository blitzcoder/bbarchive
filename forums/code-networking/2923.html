<!DOCTYPE html><html lang="en" ><head ><title >Graveyard Dogs GNet module</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Module that performs like Blitz3d's GNet module, language=bmx, category=Networking'><meta name='author' content='*'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=4>Networking</a>/Graveyard Dogs GNet module</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2923.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Graveyard Dogs GNet module by *</td><td align="right">2012 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This module performs like Blitz3d's GNet code, yes I know there are loads of examples over the code archives but most seem to be lacking something or other. Here is one that performs everything in one source file.<br><br>Place it in your mods directory in gd.mod and a sub directory called gnet.mod and then build modules, then running the source code in the comments below will list the servers for you. You may want to rebuild your help docs too. </td></tr><tr ><td class="cell"><pre class="code">Rem
bbdoc:Graveyard Dogs GNet module
EndRem
Module gd.gnet
ModuleInfo "Version: 1.0"
ModuleInfo "Author: Ed Upton, Mark Sibly (Blitz basic code)"
ModuleInfo "License: Public Domain"
ModuleInfo ""
ModuleInfo "History: 1.0"
ModuleInfo "       : Initial release."

SuperStrict 

Import BRL.SocketStream
Import BRL.LinkedList
Import BRL.Retro

Rem
bbdoc: GNet server data
about:this holds the data from list server it will allow you to get information from GNet.
End Rem
Type TGNetServer
	Rem
	bbdoc:  The game's name.
	End Rem
	Field game:String
	Rem
	bbdoc:  The server's name/descriptor
	End Rem
	Field server:String
	Rem
	bbdoc:  The server's global IP address
	End Rem
	Field ip:String
End Type
Global GNetServerList:TList = CreateList()
Global GNetServer:TGNetServer = Null					'here for SuperStrict declaration
Rem
bbdoc:GDGNet system
about:Main type
EndRem
Type GDGNetType
	Field Host:String = "www.blitzbasic.com"
	Field HostAddress:Int = HostIp( Host )
	Field Port:Int = 80
	Field Get:String = "/gnet/gnet.php"
	
	Field GameName:String = ""
	Field ServerName:String = ""
	
Rem
bbdoc: AddServer( game name$, server name$ )
about:Game name is the games name ie "Pong", "Space invaders" etc.
Server name is the name of your particular server, ie "CTF game", "2v2 deathmatch".
Both should be kept as short as possible.
returns: nothing
EndRem
	Function AddServer( asgame:String, asserver:String = "" )
		Exec( "add", asgame, asserver )
	End Function
	
Rem
bbdoc: RefreshServer( game name$, server name$ )
about:This must be called to keep your server alive, after five minutes of inactivity the servers are removed.
returns: nothing
EndRem
	Function RefreshServer( rsgame:String, rsserver:String ="" )
		Exec( "ref", rsgame, rsserver )
	End Function
	
Rem
bbdoc: RemoveServer( game name$ )
about:This removes the server from GNet, this can be done when you dont want it on there any more or if your game is closing down.
returns: nothing
EndRem
	Function RemoveServer( rsgame:String )
		Exec( "rem", rsgame, "" )
	End Function

Rem
bbdoc: Gets the online server list
about:Searches for servers for the specified game, or all games if @game is omitted.
The results of the search are returned as a list of TGNetServer objects.
Use @For .. @EachIn to examine the server list.
A TList containing the TGNetServer objects.
End Rem
	Function ListServers( game:String="" )
		Local Socket:TSocket
		Local Stream:TSocketStream
		
		Socket = CreateTCPSocket()
		Stream = CreateSocketStream( Socket )
		
		If stream=Null Then End
		BindSocket( Socket, 9050 )
		
		ConnectSocket( Socket, GDGNet.HostAddress, GDGNet.Port )		
		WriteLine( Stream, "GET "+GDGNet.Get+"?opt=list HTTP/1.0" )
		WriteLine( Stream, "HOST: "+GDGNet.Host )
		WriteLine( Stream, "" )

		Local t_game:String
		Local t_server:String
		Local t_ip:String
		
		Local Ignore:Int = 0
		For Ignore =0 To 7
			t_game = ReadLine( Stream )
		Next
		
		Repeat
			t_game = ReadLine( Stream )
			If t_game[..2]&lt;&gt;"&lt;b" And t_game&lt;&gt;""
				t_server = ReadLine( Stream )
				t_ip = ReadLine( Stream )
				
				If game="" Or Format( game ) =t_game
					GNetServer:TGNetServer = New TGNetServer
					GNetServer.Game = t_game
					GNetServer.Server = t_server
					GNetServer.IP = t_ip
					ListAddLast( GNetServerList, GNetServer )
				EndIf
			EndIf
		Until Eof( Stream )

		CloseStream( Stream )
		CloseSocket( Socket )
	End Function

	Function Exec( opt:String, game:String, server:String )
		Local Socket:TSocket
		Local Stream:TSocketStream
		
		Socket = CreateTCPSocket()
		Stream = CreateSocketStream( Socket )
		
		If stream=Null Then End
		BindSocket( Socket, 9050 )
		
		ConnectSocket( Socket, GDGNet.HostAddress, GDGNet.Port )
		opt :+ "&amp;game="+Format( Game )
		If server&lt;&gt;"" opt :+ "&amp;server="+Format( server )
		
		WriteLine( Stream, "GET "+GDGNet.Get+"?opt="+opt+" HTTP/1.0" )
		WriteLine( Stream, "HOST: "+GDGNet.Host )
		WriteLine( Stream, "" )
		CloseStream( Stream )
		CloseSocket( Socket )
	End Function
		
	Function Format:String( t:String )
		t$=Replace$( t$,"&amp;","" )
		t$=Replace$( t$,"%","" )
		t$=Replace$( t$,"'","" )
		t$=Replace$( t$,Chr$(34),"" )
		t$=Replace$( t$," ","_" )
		Return t$
	End Function
End Type
Global GDGNet:GDGNetType = New GDGNetType</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >*</td><td align="right"><font class=tiny>2012</font></td></tr></table></td></tr><tr ><td class="posttext"> heres the test code<br><br><pre class=code>
'
'	GD.GNet test
'

Import gd.gnet

GDGNet.ListServers()

For GNetServer:TGNetServer = EachIn GNetServerList
	If GNetServer&lt;&gt;Null
		Print "Server:"+GNetServer.Server+"  Game:"+GNetServer.Game+"   IP:"+GNetServer.IP
	EndIf
Next
</pre> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>2014</font></td></tr></table></td></tr><tr ><td class="posttext"> Here's a full example code like the b3d gnet_v1 demo. <br><br>This module has no ping code and there is a bug in "Function ListServers" where the list just grows everytime it's called so before "Repeat" I added "ClearList( GNetServerList )".<br><pre class=code>'
'	GD.GNet test
'

'Import gd.gnet
Import "gd gnet.bmx"

Local my_game$="My_GD_Game_Name",my_server$="My_GD_Server_Name"

GDGNet.ListServers()
PrintServers()

Print "~nAdding server..."
GDGNet.AddServer( my_game$ )

GDGNet.ListServers()
PrintServers()

Input "~nHit return to refresh server..."

GDGNet.RefreshServer( my_game$,my_server$ )

GDGNet.ListServers()
PrintServers()

Input "~nHit return to remove server and exit..."

GDGNet.RemoveServer( my_game$ )

GDGNet.ListServers()
PrintServers()

Function PrintServers()
	Print ""
	For GNetServer:TGNetServer = EachIn GNetServerList
		If GNetServer&lt;&gt;Null
			Print "Game:"+GNetServer.Game+" Server:"+GNetServer.Server+" IP:"+GNetServer.IP
		EndIf
	Next
EndFunction
</pre> <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
