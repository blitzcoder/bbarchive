<!DOCTYPE html><html lang="en" ><head ><title >Improved HTTP Streams</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Support for GET, HEAD and POST methods with authentication, language=bmx, category=Networking'><meta name='author' content='Otus'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=4>Networking</a>/Improved HTTP Streams</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2395.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Improved HTTP Streams by Otus</td><td align="right">2009 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This HTTP stream type allows you to easily request web documents. The functions allow use of different request methods, GET, HEAD and POST, and also support authentication.<br><br>This is a module, and requires Brucey's <a href="http://code.google.com/p/maxmods/wiki/Base64Module" target="_blank">Base64 module</a> for authentication. See first post for a non-module version without authentication. </td></tr><tr ><td class="cell"><pre class="code">SuperStrict

Rem
bbdoc: HTTP Streams
End Rem
Module Otus.HttpStream

ModuleInfo "Version: 1.00"
ModuleInfo "Author: Jan Varho"
ModuleInfo "License: Public domain"

Import BRL.SocketStream

Import BaH.Base64

Rem
bbdoc: Wrapper stream for HTTP requests
about:
The following HTTP methods are supported: GET, HEAD and POST.

An example:
&lt;pre&gt;Local s:TStream = THttpStream.Get("www.google.com")

While Not s.Eof()
	Print s.ReadLine()
Wend&lt;/pre&gt;
End Rem
Type THttpStream Extends TStreamWrapper
	
Rem
bbdoc: The HTTP User-Agent
about:
Only sent if you provide a non-empty value.
End Rem
	Global user_agent:String = ""
	
Rem
bbdoc: HTTP headers sent by the server
about:
An example:
&lt;pre&gt;Local s:THttpStream = THttpStream.Head("www.google.com")

For h:string = EachIn s.headers
	Print h
Next&lt;/pre&gt;
End Rem
	Field headers:String[]
	
	Method _ReceiveHeaders()
		Local l:String = _stream.ReadLine()
		headers = [l]
		l = _stream.ReadLine()
		While l&lt;&gt;""
			headers = headers + [l]
			l = _stream.ReadLine()
		Wend
	End Method
	
	Method _SendHeaders()
		If user_agent _stream.WriteLine "User-Agent: "+user_agent
		If headers
			For Local h:String = EachIn headers
				_stream.WriteLine h
			Next
		End If
		_stream.WriteLine ""
	End Method
	
Rem
bbdoc: Requests an URL using GET
about:
A convieniency function that allows you to easily GET an URL.
The http-prefix is optional and you can also supply authentication details:
&lt;pre&gt;Local s:TStream = THttpStream.GetURL("http://user:password@www.example.com/file.html")&lt;/pre&gt;
End Rem
	Function GetURL:THttpStream(url:String)
		If url.StartsWith("http://") url = url[7..]
		
		Local at:Int = url.Find("@"), auth:String
		If at &lt;&gt; -1
			auth = url[..at]
			Local c:Byte Ptr = auth.ToCString()
			auth = TBase64.Encode(c, auth.length)
			MemFree c
			auth = "Authorization: Basic "+auth
			url = url[at+1..]
		End If
		
		at = url.Find("/")
		Local server:String, file:String
		If at = -1
			server = url
			file = "/"
		Else
			server = url[..at]
			file = url[at..]
		End If
		
		If auth Return Get(server, 80, file, [auth])
		
		Return Get(server, 80, file)
	End Function
	
Rem
bbdoc: HTTP GET method
about:
Requests a file on a server with GET method. You can also supply your own additional headers.
End Rem
	Function Get:THttpStream(server:String, port:Int=80, file:String="/", headers:String[]=Null)
		Local stream:TStream=TSocketStream.CreateClient( server, port )
		If Not stream Return Null
		
		stream.WriteLine "GET "+file+" HTTP/1.0"
		stream.WriteLine "Host: "+server
		
		Local h:THttpStream = New THttpStream
		h._stream = stream
		h.headers = headers
		
		h._SendHeaders
		h._ReceiveHeaders
		
		Return h
	End Function
	
Rem
bbdoc: GET with authentication
about:
Requests a file on a server with GET method.
You can supply authentication details and also additional headers.
End Rem
	Function GetAuth:THttpStream(server:String, user:String, pass:String, port:Int=80,..
			file:String="/", headers:String[]=Null)
		Local auth:String = user+":"+pass
		Local c:Byte Ptr = auth.ToCString()
		auth = TBase64.Encode(c, auth.length)
		MemFree c
		
		If headers
			headers = headers + ["Authorization: Basic "+auth]
		Else
			headers = ["Authorization: Basic "+auth]
		End If
		
		Return Get(server, port, file, headers)
	End Function
	
Rem
bbdoc: HTTP HEAD method
about:
Requests the headers for a file on a server. Use #headers to read them.
End Rem
	Function Head:THttpStream(server:String, port:Int=80, file:String="/", headers:String[]=Null)
		Local stream:TStream=TSocketStream.CreateClient( server, port )
		If Not stream Return Null
		
		stream.WriteLine "HEAD "+file+" HTTP/1.0"
		stream.WriteLine "Host: "+server
		
		Local h:THttpStream = New THttpStream
		h._stream = stream
		h.headers = headers
		
		h._SendHeaders
		h._ReceiveHeaders
		
		Return h
	End Function
	
Rem
bbdoc: HEAD with authentication
about:
Requests headers for a file on a server with HEAD method.
You can supply authentication details and also additional headers.
End Rem
	Function HeadAuth:THttpStream(server:String, user:String, pass:String, port:Int=80,..
			file:String="/", headers:String[]=Null)
		Local auth:String = user+":"+pass
		Local c:Byte Ptr = auth.ToCString()
		auth = TBase64.Encode(c, auth.length)
		MemFree c
		
		If headers
			headers = headers + ["Authorization: Basic "+auth]
		Else
			headers = ["Authorization: Basic "+auth]
		End If
		
		Return Head(server, port, file, headers)
	End Function
	
Rem
bbdoc: HTTP POST method
about:
POST sends data to the server. You must specify the length any content of data.
Example:
&lt;pre&gt;Local s:String = "userid=admin&amp;password=admin"
Local data:Byte Ptr = s.ToCString()
Local s:TStream = THttpStream.Post("www.example.com", data, s.length,..
	"application/x-www-form-urlencoded", 80, "/auth.php")&lt;/pre&gt;
End Rem
	Function Post:THttpStream(server:String, content:Byte Ptr, clength:Int, ctype:String,..
			port:Int=80, file:String="/", headers:String[]=Null)
		Local stream:TStream=TSocketStream.CreateClient( server, port )
		If Not stream Return Null
		
		stream.WriteLine "POST "+file+" HTTP/1.0"
		stream.WriteLine "Host: "+server
		stream.WriteLine "Content-Length: "+clength
		stream.WriteLine "Content-Type: "+ctype
		
		Local h:THttpStream = New THttpStream
		h._stream = stream
		h.headers = headers
		
		h._SendHeaders
		
		stream.Write content, clength
		stream.WriteLine ""
		
		h._ReceiveHeaders
		
		Return h
	End Function
	
Rem
bbdoc: POST with authentication
about:
An authenticated version of #Post.
End Rem
	Function PostAuth:THttpStream(server:String, content:Byte Ptr, clength:Int, ctype:String,..
			user:String, pass:String, port:Int=80, file:String="/", headers:String[]=Null)
		Local auth:String = user+":"+pass
		Local c:Byte Ptr = auth.ToCString()
		auth = TBase64.Encode(c, auth.length)
		MemFree c
		
		If headers
			headers = headers + ["Authorization: Basic "+auth]
		Else
			headers = ["Authorization: Basic "+auth]
		End If
		
		Return Post(server, content, clength, ctype, port, file, headers)
	End Function
	
End Type</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Otus</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> Non-module without authentication (and dependencies):<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

Import BRL.SocketStream

Rem
bbdoc: Wrapper stream for HTTP requests
about:
The following HTTP methods are supported: GET, HEAD and POST.

An example:
&lt;pre&gt;Local s:TStream = THttpStream.Get("www.google.com")

While Not s.Eof()
	Print s.ReadLine()
Wend&lt;/pre&gt;
End Rem
Type THttpStream Extends TStreamWrapper
	
Rem
bbdoc: The HTTP User-Agent
about:
Only sent if you provide a non-empty value.
End Rem
	Global user_agent:String = ""
	
Rem
bbdoc: HTTP headers sent by the server
about:
An example:
&lt;pre&gt;Local s:THttpStream = THttpStream.Head("www.google.com")

For h:string = EachIn s.headers
	Print h
Next&lt;/pre&gt;
End Rem
	Field headers:String[]
	
	Method _ReceiveHeaders()
		Local l:String = _stream.ReadLine()
		headers = [l]
		l = _stream.ReadLine()
		While l&lt;&gt;""
			headers = headers + [l]
			l = _stream.ReadLine()
		Wend
	End Method
	
	Method _SendHeaders()
		If user_agent _stream.WriteLine "User-Agent: "+user_agent
		If headers
			For Local h:String = EachIn headers
				_stream.WriteLine h
			Next
		End If
		_stream.WriteLine ""
	End Method
	
Rem
bbdoc: Requests an URL using GET
about:
A convieniency function that allows you to easily GET an URL.
The http-prefix is optional.&lt;/pre&gt;
End Rem
	Function GetURL:THttpStream(url:String)
		If url.StartsWith("http://") url = url[7..]
		
		Local at% = url.Find("/")
		Local server:String, file:String
		If at = -1
			server = url
			file = "/"
		Else
			server = url[..at]
			file = url[at..]
		End If
		
		Return Get(server, 80, file)
	End Function
	
Rem
bbdoc: HTTP GET method
about:
Requests a file on a server with GET method. You can also supply your own additional headers.
End Rem
	Function Get:THttpStream(server:String, port:Int=80, file:String="/", headers:String[]=Null)
		Local stream:TStream=TSocketStream.CreateClient( server, port )
		If Not stream Return Null
		
		stream.WriteLine "GET "+file+" HTTP/1.0"
		stream.WriteLine "Host: "+server
		
		Local h:THttpStream = New THttpStream
		h._stream = stream
		h.headers = headers
		
		h._SendHeaders
		h._ReceiveHeaders
		
		Return h
	End Function
	
Rem
bbdoc: HTTP HEAD method
about:
Requests the headers for a file on a server. Use #headers to read them.
End Rem
	Function Head:THttpStream(server:String, port:Int=80, file:String="/", headers:String[]=Null)
		Local stream:TStream=TSocketStream.CreateClient( server, port )
		If Not stream Return Null
		
		stream.WriteLine "HEAD "+file+" HTTP/1.0"
		stream.WriteLine "Host: "+server
		
		Local h:THttpStream = New THttpStream
		h._stream = stream
		h.headers = headers
		
		h._SendHeaders
		h._ReceiveHeaders
		
		Return h
	End Function
	
Rem
bbdoc: HTTP POST method
about:
POST sends data to the server. You must specify the length any content of data.
Example:
&lt;pre&gt;Local s:String = "userid=admin&amp;password=admin"
Local data:Byte Ptr = s.ToCString()
Local s:TStream = THttpStream.Post("www.example.com", data, s.length,..
	"application/x-www-form-urlencoded", 80, "/auth.php")&lt;/pre&gt;
End Rem
	Function Post:THttpStream(server:String, content:Byte Ptr, clength:Int, ctype:String,..
			port:Int=80, file:String="/", headers:String[]=Null)
		Local stream:TStream=TSocketStream.CreateClient( server, port )
		If Not stream Return Null
		
		stream.WriteLine "POST "+file+" HTTP/1.0"
		stream.WriteLine "Host: "+server
		stream.WriteLine "Content-Length: "+clength
		stream.WriteLine "Content-Type: "+ctype
		
		Local h:THttpStream = New THttpStream
		h._stream = stream
		h.headers = headers
		
		h._SendHeaders
		
		stream.Write content, clength
		stream.WriteLine ""
		
		h._ReceiveHeaders
		
		Return h
	End Function
	
End Type</textarea> <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
