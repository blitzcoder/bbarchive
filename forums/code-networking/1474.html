<!DOCTYPE html><html lang="en" ><head ><title >HTTP FileSize() and more</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Get the size of a file via HTTP without having to download it., language=bmx, category=Networking'><meta name='author' content='Yan'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=4>Networking</a>/HTTP FileSize() and more</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1474.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >HTTP FileSize() and more by Yan</td><td align="right">2005 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This was written in answer to a forum post.<br>I'm putting it here as it may be of use to others. </td></tr><tr ><td class="cell"><pre class="code">Print HTTPFileSize("http://www.google.co.uk/intl/en_uk/images/logo.gif") + " Bytes"

End


Function HTTPFileSize(url$)
	url$ = url$.Replace("http://", "")
	Local slashPos = url$.Find("/"), host$, file$
	
	If slashPos &lt;&gt; -1
		host$ = url$[..slashPos]
		file$ = url$[slashPos..]
	Else
		Return -1
	EndIf
	
	Local stream:TStream = OpenStream("tcp::" + host$)
	If Not stream Then Return -1
	
	stream.WriteLine "HEAD " + file$ + " HTTP/1.0"
	stream.WriteLine "Host: " + host$
	stream.WriteLine ""
	
	While Not Eof(stream)
		Local in$ = stream.ReadLine()

		If in$.Find("Content-Length:") &lt;&gt; -1
			stream.Close()
			Return Int(in$[in$.Find(":") + 1..].Trim())
		EndIf	
	Wend
	
	stream.Close()
	Return -1		
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yan</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> Meet more of the family...<br><br>Returns the HTTP header.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Print HTTPHeader("http://www.google.co.uk/intl/en_uk/images/logo.gif")

End


Function HTTPHeader$(url$, dropError=False)
	url$ = url$.Replace("http://", "")
	Local slashPos = url$.Find("/"), host$, file$, header$
	
	If slashPos &lt;&gt; -1
		host$ = url$[..slashPos]
		file$ = url$[slashPos..]
	Else
		Return
	EndIf
	
	Local stream:TStream = OpenStream("tcp::" + host$)
	If Not stream Then Return -1
	
	stream.WriteLine "HEAD " + file$ + " HTTP/1.1"
	stream.WriteLine "Host: " + host$
	stream.WriteLine "Connection: close"
	stream.WriteLine ""
	
	Local in$ = stream.ReadLine()	
	If dropError And (in$.Find("200") &lt; 0)
		stream.Close()
		Return
	EndIf
	
	While in$
		header$ :+ in$ + "~r~n"
		
		in$ = stream.ReadLine()
	Wend
	
	stream.Close()
	
	Return header$
End Function</textarea><br><br>Download a file with a progress bar (not really useful for anything, more of an example).<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict

Print HTTPGetFile("http://www.google.co.uk/intl/en_uk/images/logo.gif", "c:\google logo.gif")

End


Function HTTPGetFile(url$, filePath$)
	url$ = url$.Replace("http://", "")
	Local slashPos = url$.Find("/"), host$, file$
	
	If slashPos &lt;&gt; -1
		host$ = url$[..slashPos]
		file$ = url$[slashPos..]
	Else
		Return -1
	EndIf
	
	Local fileStream:TStream = OpenStream(filePath$, False, True)
	If Not(fileStream) Then Return -1
	
	Local TCPStream:TStream = OpenStream("tcp::" + host$)
	If Not(TCPStream)
		fileStream.Close()
		Return -1
	EndIf
	
	TCPStream.WriteLine "GET " + file$ + " HTTP/1.1"
	TCPStream.WriteLine "Host: " + host$
	TCPStream.WriteLine "Connection: close"
	TCPStream.WriteLine ""
	
	Local dataLength
	Local in$ = TCPStream.ReadLine()
	
	If in$.Find("200") &lt; 0
		TCPStream.Close()
		fileStream.Close()
		Return -1
	EndIf
	
	While in$
		If in$.Find("Content-Length:") &lt;&gt; -1
			dataLength = Int(in$[in$.Find(":") + 1..].Trim())
		EndIf
		
		in$ = TCPStream.ReadLine()
	Wend
	
	Local byteCount
	While Not(TCPStream.Eof())
		'You'd probaly want to download more than a single byte at a time here
		fileStream.WriteByte(TCPStream.ReadByte())
		
		'You'd normaly update a progress bar here...
		Print "Done " + ((byteCount / Float(dataLength - 1)) * 100) + "%"
		
		'add number of bytes downloaded
		byteCount :+ 1
	Wend
	
	TCPStream.Close()
	fileStream.Close()
	
	If byteCount &lt;&gt; dataLength Then Return -1
End Function
</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>2008</font></td></tr></table></td></tr><tr ><td class="posttext"> Another version with progressive download :<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Global FullName:String = "http://www.blabla.com/myvideo.mpg"

DownloadFile(FullName ,45) 

End

' ---------------------------
' Function to download a file
' ---------------------------
Function DownloadFile(Url:String , Size:Int )
	Local SourceFileSize:Int=HTTPFileSize(Url) 
	Print "File size : " + SourceFileSize
	
	If SourceFileSize&lt; Size Then
		Size = SourceFileSize
	EndIf
	
	Url=Url.Replace("http://" , "http::") 
	
	
	Local RawBank:TBank
	
	Print "Opening file..."
	Local File:TStream = ReadStream(Url)
	
	Delay 2000
	
	If File Then
		
		Print "Downloading file..."
		Local Export:TStream= WriteStream("Tempo" + "." + "mpg") 
	
		Local BytesCounter:Int = 0
		Local FilesCounter:Int = 0

		Repeat
			
			If BytesCounter+Size &gt;= SourceFileSize Then
				Size=SourceFileSize-BytesCounter
			EndIf
				
			RawBank = CreateBank(Size) 
			File.ReadBytes(RawBank.Buf(),Size)
			
			Export.WriteBytes(RawBank.buf() , BankSize(RawBank) )
						
						
			Local Percent:String = GetPercentage(0 , SourceFileSize , BytesCounter)	
			
			Print BankSize(RawBank)+" / "+Percent
			RawBank = Null
					
			
			BytesCounter=BytesCounter+Size
			FilesCounter = FilesCounter + 1
	
			If BytesCounter &gt;= SourceFileSize Then
				Exit
			EndIf
			
			PollEvent
			
		Forever
	
		CloseFile(File) 
		Return True
	
	Else
	
		Return False
		
		
	EndIf
	
End Function

Function GetPercentage:Float(Range1:Float,Range2:Float,ActualValue:Float)
	Return  (ActualValue - Range1) / ((Range2 - Range1) / 100)
End Function




Function HTTPFileSize(url$)
	url$ = url$.Replace("http://", "")
	Local slashPos = url$.Find("/"), host$, file$
	
	If slashPos &lt;&gt; -1
		host$ = url$[..slashPos]
		file$ = url$[slashPos..]
	Else
		Return -1
	EndIf
	
	Local stream:TStream = OpenStream("tcp::" + host$)
	If Not stream Then Return -1
	
	stream.WriteLine "HEAD " + file$ + " HTTP/1.0"
	stream.WriteLine "Host: " + host$
	stream.WriteLine ""
	
	While Not Eof(stream)
		Local in$ = stream.ReadLine()

		If in$.Find("Content-Length:") &lt;&gt; -1
			stream.Close()
			Return Int(in$[in$.Find(":") + 1..].Trim())
		EndIf	
	Wend
	
	stream.Close()
	Return -1		
End Function
</textarea> <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
