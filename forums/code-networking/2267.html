<!DOCTYPE html><html lang="en" ><head ><title >Modified HTTPStream</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Returns Null if the network file does not exist, language=bmx, category=Networking'><meta name='author' content='JoshK'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=4>Networking</a>/Modified HTTPStream</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2267.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Modified HTTPStream by JoshK</td><td align="right">2008 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> BRL's HTTPStream module always returns a string, even when the file is not found.  This can cause errors in loading routines.  This modification adds an optional globl variable that will attempt to detect rejected requests and return Null. </td></tr><tr ><td class="cell"><pre class="code">Strict

Module BRL.HTTPStream

ModuleInfo "Version: 1.05"
ModuleInfo "Author: Mark Sibly"
ModuleInfo "License: Blitz Shared Source Code"
ModuleInfo "Copyright: Blitz Research Ltd"
ModuleInfo "Modserver: BRL"

ModuleInfo "History: 1.04 Release"
ModuleInfo "1.03: Added optional check for existing file"
ModuleInfo "1.04: Improved checking"
ModuleInfo "1.05: Went back to my way of checking for 401 and 404 errors and added optional username/password"
ModuleInfo "1.06: Did the 200 check the right way!"

Import BRL.SocketStream

'---------------------------------------------

Import "base64.bmx"

Global HTTPStreamFactoryCheckIfExists:Int
Global HTTPStreamFactoryUsername$
Global HTTPStreamFactoryPassword$

'---------------------------------------------


Type THTTPStreamFactory Extends TStreamFactory

	Method CreateStream:TStream( url:Object,proto$,path$,readable,writeable )
		If proto="http"
		
			Local i=path.Find( "/",0 ),server$,file$
			If i&lt;&gt;-1
				server=path[..i]
				file=path[i..]
			Else
				server=path
				file="/"
			EndIf
			
			Local stream:TStream=TSocketStream.CreateClient( server,80 )
			If Not stream Return
			
			stream.WriteLine "GET "+file+" HTTP/1.0"
			stream.WriteLine "Host: "+server
			
			
			'---------------------------------------------
			
			If HTTPStreamFactoryUsername&lt;&gt;"" And HTTPStreamFactoryPassword&lt;&gt;""
				Local s$=HTTPStreamFactoryUsername+":"+HTTPStreamFactoryPassword
				Local cs:Byte Ptr
				cs=s.tocstring()
				s=TBase64.encode(cs,s.length)
				MemFree cs
				stream.WriteLine "Authorization: Basic "+s
			EndIf
			
			'---------------------------------------------
			
			
			stream.WriteLine ""
			
			
			'---------------------------------------------
			
			If HTTPStreamFactoryCheckIfExists
				If Not s.contains("200")
					stream.close()
					Return
				EndIf
			EndIf
			
			'---------------------------------------------
			
			
			While Not Eof( stream )
				If Not stream.ReadLine() Exit				
			Wend

			Return stream
		EndIf
	End Method

End Type

New THTTPStreamFactory</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>2008</font></td></tr></table></td></tr><tr ><td class="posttext"> I'm not sure this is a good way to go about it - if I'm reading it right, it will return <b>Null</b> no matter where 404 is in the document. For example, linking to this page will incorrectly return <b>Null</b> due to the presence of your string literal. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yan</td><td align="right"><font class=tiny>2008</font></td></tr></table></td></tr><tr ><td class="posttext"> It's just dumping the header. The <i>'If Not s Exit'</i> jumps out of the loop at the header's terminator, delivering the 'de-headered' stream ready for consumption.<br><br><br>You only need to check the first line of the header for '200'. That's always worked for me, at least.<br><br>Look <a href="/codearcs/codearcs.php?code=1474" target="_blank"><b><u>here</u></b></a> for examples. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>2008</font></td></tr></table></td></tr><tr ><td class="posttext"> Okay, I updated the code. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>2008</font></td></tr></table></td></tr><tr ><td class="posttext"> I don't know where you are getting this "200" from.  This is what the header looks like for me:<br><br>HTTP/1.1 404 Not Found<br>Date: Wed, 11 Jun 2008 19:14:53 GMT<br>Server: Apache<br>Connection: close<br>Content-Type: text/html; charset=iso-8859-1<br><br>&lt;!DOCTYPE HTML PUBLIC "-//IETF//DTD HTML 2.0//EN"&gt;<br>&lt;HTML&gt;&lt;HEAD&gt;<br>&lt;TITLE&gt;404 Not Found&lt;/TITLE&gt;<br>&lt;/HEAD&gt;&lt;BODY&gt;<br>&lt;H1&gt;Not Found&lt;/H1&gt;<br>The requested URL /ppop was not found on this server.&lt;P&gt;<br>&lt;P&gt;Additionally, a 404 Not Found<br>error was encountered while trying to use an ErrorDocument to handle the request.<br>&lt;HR&gt;<br>&lt;ADDRESS&gt;Apache/1.3.33 Server at leadwerks.com Port 80&lt;/ADDRESS&gt;<br>&lt;/BODY&gt;&lt;/HTML&gt; <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>2008</font></td></tr></table></td></tr><tr ><td class="posttext"> Added optional username and password.  Requires the file from bah.base64:<br><br><pre class=code>SuperStrict

Rem
bbdoc: Base64
End Rem
'Module BaH.Base64

'ModuleInfo "Version: 1.00"
'ModuleInfo "License: MIT"
'ModuleInfo "Copyright: Original - Robert Harder (http://iharder.sourceforge.net/current/java/base64/)"
'ModuleInfo "Copyright: BlitzMax port - 2008 Bruce A Henderson"

'ModuleInfo "History: 1.00 Initial Release"

Rem
bbdoc: Encode/Decode Base64 data.
about:
To Encode :
&lt;pre&gt;
SuperStrict
Import BaH.Base64

Local someData:String = "Woo! BlitzMax Modules rock!"
Local encoded:String = TBase64.Encode(someData, someData.length)
Print "Encoded : " + encoded
&lt;/pre&gt;
To Decode :
&lt;pre&gt;
SuperStrict
Import BaH.Base64

Local encodedData:String = "V29vISBCbGl0ek1heCBNb2R1bGVzIHJvY2sh"
Local data:Byte[] = TBase64.Decode(encodedData)
Local decoded:String = String.FromBytes(data, data.length)
Print "Decoded : " + decoded
&lt;/pre&gt;
End Rem
Type TBase64

	Const DONT_BREAK_LINES:Int = 8
	
	' Maximum line length (76) of Base64 output.
	Const MAX_LINE_LENGTH:Int = 76
	
	' The equals sign (=) as int.
	Const EQUALS_SIGN:Int = Asc("=")
	
	' The new line character (\n) as int.
	Const NEW_LINE:Int = Asc("~n")
	
	Const WHITE_SPACE_ENC:Int = -5 ' Indicates white space in encoding
	Const EQUALS_SIGN_ENC:Int = -1 ' Indicates equals sign in encoding
	
	Const _STANDARD_ALPHABET:String = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789+/"
	
	Global _STANDARD_DECODABET:Int[] = [-9,-9,-9,-9,-9,-9,-9,-9,-9, ..
						        -5,-5, ..
						        -9,-9, ..
						        -5, ..
						        -9,-9,-9,-9,-9,-9,-9,-9,-9,-9,-9,-9,-9, ..
						        -9,-9,-9,-9,-9, ..
						        -5, ..
						        -9,-9,-9,-9,-9,-9,-9,-9,-9,-9, ..
						        62, ..
						        -9,-9,-9, ..
						        63, ..
						        52,53,54,55,56,57,58,59,60,61, ..
						        -9,-9,-9, ..
						        -1, ..
						        -9,-9,-9, ..
						        0,1,2,3,4,5,6,7,8,9,10,11,12,13, ..
						        14,15,16,17,18,19,20,21,22,23,24,25, ..    
						        -9,-9,-9,-9,-9,-9, ..
						        26,27,28,29,30,31,32,33,34,35,36,37,38, ..
						        39,40,41,42,43,44,45,46,47,48,49,50,51, ..
						        -9,-9,-9,-9]
	
	Rem
	bbdoc: Encode byte data to a Base64 encoded String, starting at @offset of @length bytes.
	End Rem
	Function Encode:String( source:Byte Ptr, length:Int, offset:Int = 0, options:Int = 0)
    
		' Isolate options
		Local dontBreakLines:Int =  options &amp; DONT_BREAK_LINES 
		
		' Convert option To boolean in way that code likes it.
		Local breakLines:Int = True
		If dontBreakLines Then
			breakLines = False
		End If
		
		Local len43:Int = length * 4 / 3
		Local nl:Int = len43 / MAX_LINE_LENGTH
		Local pad:Int = 0
		If length Mod 3 &gt; 0 Then
			pad = 4
		End If
		
		If Not breakLines Then
			nl = 0
		End If
		
		Local outBuff:Byte[] = New Byte[ len43 + pad + nl ]     
		Local d:Int = 0
		Local e:Int = 0
		Local len2:Int = length - 2
		Local lineLength:Int = 0
		While d &lt; len2
			encode3to4( source, d + offset, 3, outBuff, e, options )
			
			lineLength :+ 4
			If  breakLines And (lineLength = MAX_LINE_LENGTH) Then
				outBuff[e+4] = NEW_LINE
				e:+1
				lineLength = 0
			End If
			d:+3
			e:+4
		Wend
		
		' pad?
		If d &lt; Length Then
			encode3to4( source, d + offset, Length - d, outBuff, e, options )
			e :+ 4
		End If
		
		Return String.FromBytes(outBuff, outBuff.length)
	End Function
	
	Rem
	bbdoc: Decode Base64 encoded String to an array of Bytes, starting at @offset.
	End Rem
	Function Decode:Byte[]( source:String, offset:Int = 0, options:Int = 0 )
		
		Local length:Int = source.length
		Local len34:Int   = Length * 3 / 4
		Local outBuff:Byte[] = New Byte[ len34 ]
		Local outBuffPosn:Int = 0
		
		Local b4:Byte[]     = New Byte[4]
		Local b4Posn:Int    = 0
		Local sbiCrop:Int   = 0
		Local sbiDecode:Int = 0
		For Local i:Int = offset Until offset + length
		
			sbiCrop = source[i] &amp; $7f
			sbiDecode = _STANDARD_DECODABET[ sbiCrop ]
			
			If sbiDecode &gt;= WHITE_SPACE_ENC Then
			
				If sbiDecode &gt;= EQUALS_SIGN_ENC Then
				
					b4[ b4Posn ] = sbiCrop
					b4Posn:+1
					If b4Posn &gt; 3 Then
				
						outBuffPosn :+ decode4to3( b4, 0, outBuff, outBuffPosn, options )
						b4Posn = 0
				
						' If that was the equals sign, break out of 'for' loop
						If sbiCrop = EQUALS_SIGN Then
							Exit
						End If
					End If
			
				End If
			
			Else
				DebugLog "Bad Base64 input character at " + i + ": " + source[i]
				Return Null
			End If
		Next
		
		Return outBuff[0..outBuffPosn]
	End Function

	Function encode3to4:Byte[](source:Byte Ptr, srcOffset:Int, numSigBytes:Int, destination:Byte[], destOffset:Int, options:Int )
    
		Local inBuff:Int
		If numSigBytes &gt; 0 Then
			inBuff = (source[ srcOffset     ] Shl 24) Shr 8
			
			If numSigBytes &gt; 1 Then
				inBuff :| (source[ srcOffset + 1 ] Shl 24) Shr 16

				If numSigBytes &gt; 2 Then
					inBuff :| (source[ srcOffset + 2 ] Shl 24) Shr 24
				End If
			End If
		End If
		
		Select numSigBytes
			Case 3
				destination[ destOffset     ] = _STANDARD_ALPHABET[ (inBuff Shr 18)       ]
				destination[ destOffset + 1 ] = _STANDARD_ALPHABET[ (inBuff Shr 12) &amp; $3f ]
				destination[ destOffset + 2 ] = _STANDARD_ALPHABET[ (inBuff Shr  6) &amp; $3f ]
				destination[ destOffset + 3 ] = _STANDARD_ALPHABET[ (inBuff       ) &amp; $3f ]
				Return destination
			
			Case 2
				destination[ destOffset     ] = _STANDARD_ALPHABET[ (inBuff Shr 18)       ]
				destination[ destOffset + 1 ] = _STANDARD_ALPHABET[ (inBuff Shr 12) &amp; $3f ]
				destination[ destOffset + 2 ] = _STANDARD_ALPHABET[ (inBuff Shr  6) &amp; $3f ]
				destination[ destOffset + 3 ] = EQUALS_SIGN
				Return destination
			
			Case 1
				destination[ destOffset     ] = _STANDARD_ALPHABET[ (inBuff Shr 18)       ]
				destination[ destOffset + 1 ] = _STANDARD_ALPHABET[ (inBuff Shr 12) &amp; $3f ]
				destination[ destOffset + 2 ] = EQUALS_SIGN
				destination[ destOffset + 3 ] = EQUALS_SIGN
				Return destination
			
			Default
				Return destination
		End Select
	End Function 
	
	Function decode4to3:Int( source:Byte[] , srcOffset:Int, destination:Byte[] , destOffset:Int, options:Int )
	
		' Example: Dk==
		If source[ srcOffset + 2] = EQUALS_SIGN Then
		
			Local outBuff:Int =   ( ( _STANDARD_DECODABET[ source[ srcOffset    ] ] &amp; $FF ) Shl 18 ) ..
				| ( ( _STANDARD_DECODABET[ source[ srcOffset + 1] ] &amp; $FF ) Shl 12 )
			
			destination[ destOffset ] = outBuff Shr 16
			Return 1
		
		' Example: DkL=
		Else If source[ srcOffset + 3 ] = EQUALS_SIGN Then
		
			Local outBuff:Int =   ( ( _STANDARD_DECODABET[ source[ srcOffset     ] ] &amp; $FF ) Shl 18 ) ..
				| ( ( _STANDARD_DECODABET[ source[ srcOffset + 1 ] ] &amp; $FF ) Shl 12 ) ..
				| ( ( _STANDARD_DECODABET[ source[ srcOffset + 2 ] ] &amp; $FF ) Shl  6 )
			
			destination[ destOffset     ] = outBuff Shr 16
			destination[ destOffset + 1 ] = outBuff Shr  8
			Return 2
			
		' Example: DkLE
		Else
		
			Local outBuff:Int =   ( ( _STANDARD_DECODABET[ source[ srcOffset     ] ] &amp; $FF ) Shl 18 ) ..
				| ( ( _STANDARD_DECODABET[ source[ srcOffset + 1 ] ] &amp; $FF ) Shl 12 ) ..
				| ( ( _STANDARD_DECODABET[ source[ srcOffset + 2 ] ] &amp; $FF ) Shl  6) ..
				| ( ( _STANDARD_DECODABET[ source[ srcOffset + 3 ] ] &amp; $FF )      )
		
			destination[ destOffset     ] =  outBuff Shr 16
			destination[ destOffset + 1 ] =  outBuff Shr  8
			destination[ destOffset + 2 ] =  outBuff
		
			Return 3
		End If
	End Function 

End Type</pre> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ShadowTurtle</td><td align="right"><font class=tiny>2008</font></td></tr></table></td></tr><tr ><td class="posttext"> see follow link and have fun: ^^<br><a href="http://www.google.com/support/webmasters/bin/answer.py?hl=en&amp;answer=40132" target="_blank">http://www.google.com/support/webmasters/bin/answer.py?hl=en&amp;answer=40132</a> <br><br>status code 200 means ...<br><br>"  200 (Successful)  	The server successfully processed the request. Generally, this means that the server provided the requested page. If you see this status for your robots.txt file, it means that Googlebot retrieved it successfully. " <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>2008</font></td></tr></table></td></tr><tr ><td class="posttext"> The header my prog returns does not contain that. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>2008</font></td></tr></table></td></tr><tr ><td class="posttext"> OHHHH, I see, 200 means it worked!  Fixed now. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
