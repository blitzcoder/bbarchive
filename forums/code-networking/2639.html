<!DOCTYPE html><html lang="en" ><head ><title >Networking Library</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='An advanced client/server system, language=bmx, category=Networking'><meta name='author' content='JoshK'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=4>Networking</a>/Networking Library</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2639.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Networking Library by JoshK</td><td align="right">2010 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This library handles all the basic functionality of a client/server game system:<br>-Client/server connections.<br>-Fast client disconnections.<br>-Chat.<br>-Name changes.<br>-IP banning.<br>-Public server list.<br>-Pinging clients, server, and unconnected servers. </td></tr><tr ><td class="cell"><pre class="code">SuperStrict

Import pub.enet

Private

Const ENET_PACKET_FLAG_UNSEQUENCED:Int=2
Const SERVERUPDATEFREQUENCY:Int=4*1000*60

Function enet_host_port:Int( peer:Byte Ptr  )
	Local ip:Int=(Int Ptr peer)[1]
	Local port:Int=(Short Ptr peer)[4]
	?LittleEndian
	ip=(ip Shr 24) | (ip Shr 8 &amp; $ff00) | (ip Shl 8 &amp; $ff0000) | (ip Shl 24)
	?
	Return port
EndFunction

Function enet_host_ip:Int( peer:Byte Ptr  )
	Local ip:Int=(Int Ptr peer)[1]
	Local port:Int=(Short Ptr peer)[4]
	?LittleEndian
	ip=(ip Shr 24) | (ip Shr 8 &amp; $ff00) | (ip Shl 8 &amp; $ff0000) | (ip Shl 24)
	?
	Return ip
EndFunction

Function enet_peer_port:Int( peer:Byte Ptr  )
	Local ip:Int=(Int Ptr peer)[3]
	Local port:Int=(Short Ptr peer)[8]
	?LittleEndian
	ip=(ip Shr 24) | (ip Shr 8 &amp; $ff00) | (ip Shl 8 &amp; $ff0000) | (ip Shl 24)
	?
	Return port
EndFunction

Function enet_peer_ip:Int( peer:Byte Ptr  )
	Local ip:Int=(Int Ptr peer)[3]
	Local port:Int=(Short Ptr peer)[8]
	?LittleEndian
	ip=(ip Shr 24) | (ip Shr 8 &amp; $ff00) | (ip Shl 8 &amp; $ff0000) | (ip Shl 24)
	?
	Return ip
EndFunction

Public

Type TNetworkNode
	
	Field port:Int
	Field ip:Int
	Field enethost:Byte Ptr
	Field enetpeer:Byte Ptr
	
	Method Delete()
		Free()
	EndMethod
	
	Method Free()
		If enethost
			enet_host_destroy(enethost)
			enethost=Null
		EndIf
	EndMethod
	
	Method Update()
		Local ip:Int,port:Int,client:TClient,ev:ENetEvent=New ENetEvent,id:Byte,packet:TPacket
		
		If Not Self.enethost RuntimeError "Can't update a remote server."
		Repeat
			If enet_host_service(Self.enethost,ev,0)
				
				Select ev.event
				
				Case ENET_EVENT_TYPE_CONNECT
					id=NETWORK_CONNECT
				
				Case ENET_EVENT_TYPE_DISCONNECT
					id=NETWORK_DISCONNECT
				
				Case ENET_EVENT_TYPE_RECEIVE
					Local size:Int=enet_packet_size(ev.packet)
					Local data:Byte[size]
					MemCopy(Varptr id,enet_packet_data(ev.packet),1)
					If size&gt;1
						packet=New TPacket
						packet._bank.resize(size-1)
						MemCopy(packet._bank.buf(),enet_packet_data(ev.packet)+1,size-1)
					EndIf
				
				Default
					Continue
					
				EndSelect
				
				EvaluateEvent(id,packet,ev.peer)
				
			Else
				Exit
			EndIf
			
			If enethost=Null Exit
			
		Forever

	EndMethod
	
	Method EvaluateEvent(id:Int,packet:TPacket,enetpeer:Byte Ptr) Abstract
	
EndType

'Public


Const NETWORK_CONNECT:Int=1
Const NETWORK_DISCONNECT:Int=2
Const NETWORK_PINGREQUEST:Int=3
Const NETWORK_PINGRESPONSE:Int=4
Const NETWORK_JOINREQUEST:Int=5
Const NETWORK_JOINRESPONSE:Int=6
Const NETWORK_CHAT:Int=7
Const NETWORK_LEAVEGAME:Int=8
Const NETWORK_CHANGENAMEREQUEST:Int=9
Const NETWORK_CHANGENAMERESPONSE:Int=10
Const NETWORK_PLAYERJOINED:Int=11
Const NETWORK_PLAYERLEFT:Int=12
Const NETWORK_LEAVEGAMEREQUEST:Int=13
Const NETWORK_LEAVEGAMERESPONSE:Int=14
Const NETWORK_PLAYERCHANGEDNAME:Int=15

Const SEND_RELIABLE:Int=ENET_PACKET_FLAG_RELIABLE
Const SEND_UNSEQUENCED:Int=ENET_PACKET_FLAG_UNSEQUENCED

Type TServer Extends TNetworkNode
	
	Field clients:TList=New TList
	Field callback(server:TServer,client:TClient,id:Int,packet:TPacket)
	Field bannedips:Int[]
	Field clientmap:TMap=New TMap
	Field url:String
	Field lastrefreshtime:Int
	
	Method Free()
		If url
			Remove()
			url=""
		EndIf
		Super.Free()
	EndMethod
	
	Function EncodeURL:String(t:String)
		Local newString:String
		Local i:Int,c:String,asciival:Int,hexstr:String,newhexstr:String
		For i = 0 To Len(t)
			c:String = t[i..i+1]
			asciival = Asc(c)
			If asciival &gt; 32 And asciival &lt; 123
				' handle replacing the special set of chars
				c = Replace(c,"'","%27")
				c = Replace(c,"%","%25")
				c = Replace(c,"&lt;","%3C")
				c = Replace(c,"&gt;","%3E")
				c = Replace(c,"\","%5C")
				c = Replace(c,"^","%5E")
				c = Replace(c,"[","%5B")
				c = Replace(c,"]","%5D")
				c = Replace(c,"+","%2B")
				c = Replace(c,"$","%24")
				c = Replace(c,",","%2C")
				c = Replace(c,"@","%40")
				c = Replace(c,":","%3A")
				c = Replace(c,";","%3B")
				c = Replace(c,"/","%2F")
				c = Replace(c,"!","%21")
				c = Replace(c,"#","%23")
				c = Replace(c,"?","%3F")
				c = Replace(c,"=","%3D")
				c = Replace(c,"&amp;","%26")
				newString:+c
			Else
				hexstr$ = Hex(asciival)
				newhexstr$ = "%" + hexstr[Len(hexstr)-2..Len(hexstr)]
				newstring:+newhexstr
			EndIf
		Next
		
		newstring = newstring[..Len(newstring)-3]
		Return newstring
	EndFunction
	
	Function Create:TServer(port:Int=0,portrange:Int=40)
		Local addr:Byte Ptr,server:TServer=New TServer

		server.ip=ENET_HOST_ANY
		If portrange&lt;=1
			addr=enet_address_create(server.ip,server.port)
			server.enethost=enet_host_create(addr,32,0,0)
			enet_address_destroy(addr)
		Else
			If port=0 port=7777
			For Local n:Int=port To port+portrange-1
				addr=enet_address_create(ENET_HOST_ANY,n)
				server.enethost=enet_host_create(addr,64,0,0)
				enet_address_destroy(addr)
				If server.enethost
					server.port=n
					Exit
				EndIf
			Next
		EndIf
		If Not server.enethost Return Null		
		Return server
	EndFunction
	
	Method Publish:Int(servername:String="",gamename:String="",url:String="http::www.leadwerks.com/gameservers/gameservers.php")
		Local stream:TStream
		gamename=EncodeURL(gamename)
		servername=EncodeURL(servername)
		Self.url=url
		stream=OpenStream(url+"?action=addserver&amp;gamename="+gamename+"&amp;servername="+port+"|"+servername)
		If Not stream Return False
		Self.url=url
		While Not stream.Eof()
			Local s:String=stream.ReadLine()
			If s.Trim()
				stream.close()
				Return False
			EndIf
		Wend
		stream.close()
		lastrefreshtime=MilliSecs()
		Return True
	EndMethod
	
	Method Remove:Int()
		Local stream:TStream
		stream=OpenStream(url+"?action=removeserver")
		If stream
			stream.close()
			Return True
		Else
			Return False
		EndIf
	EndMethod
	
	Function Query:String[](gamename:String="",url:String="")
		Local stream:TStream,s:String,sarr:String[],n:Int
		
		If url="" url="http::www.leadwerks.com/gameservers/gameservers.php"
		stream=OpenStream(url+"?action=listservers&amp;gamename="+gamename)
		If Not stream Return Null
		While Not stream.Eof()
			s=ReadLine(stream)
			If s.Trim().length&gt;0
				sarr=sarr[..sarr.length+1]
				sarr[sarr.length-1]=s.Trim()
				Local sa:String[]=sarr[sarr.length-1].split("|")
				Local name:String=sa[0]
				sarr[sarr.length-1]=HostIp(name)+"|"+sarr[sarr.length-1]
			EndIf
		Wend
		stream.close()
		Return sarr
	EndFunction
	
	Method Refresh:Int()
		Local stream:TStream
		stream=OpenStream(url+"?action=refreshserver")
		If Not stream Return False
		stream.close()
		Return True	
	EndMethod
	
	Method FindClientByName:TClient(name:String)
		Return TClient(clientmap.valueforkey(name))
	EndMethod
	
	Method FindClientByPeer:TClient(peer:Byte Ptr)
		Local client:TClient
		For client=EachIn clients
			If client.enetpeer=peer Return client
		Next
	EndMethod
	
	Method FindClient:TClient(ip:Int,port:Int)
		Local client:TClient
		For client=EachIn clients
			If client.ip=ip And client.port=port Return client
		Next
	EndMethod
	
	Method Update()
		If url
			Local time:Int=MilliSecs()
			If time-lastrefreshtime&gt;serverupdatefrequency
				lastrefreshtime=time
				Refresh()
			EndIf
		EndIf
		Super.Update()		
	EndMethod
	
	Method EvaluateEvent(id:Int,packet:TPacket,enetpeer:Byte Ptr)
		Local client:TClient=FindClient(enet_peer_ip(enetpeer),enet_peer_port(enetpeer))
		Select id
		
		Case NETWORK_PINGRESPONSE
			client.latency=MilliSecs()-packet.ReadInt()
		
		Case NETWORK_LEAVEGAMEREQUEST
			If client
				Send(client,NETWORK_LEAVEGAMERESPONSE,Null,SEND_RELIABLE)
				Local relay:TPacket=New TPacket
				relay.WriteLine(client.name)
				Broadcast(NETWORK_PLAYERLEFT,relay,SEND_RELIABLE)
				clients.remove(client)
				If clientmap.valueforkey(client.name)=client clientmap.remove(client.name)
			EndIf
			
		Case NETWORK_JOINREQUEST
			If client
				Disconnect(client,1)
				Local relay:TPacket=New TPacket
				relay.WriteLine(client.name)
				Broadcast(NETWORK_PLAYERLEFT,relay,SEND_RELIABLE)
			EndIf
			client=TClient.Find(enet_peer_ip(enetpeer),enet_peer_port(enetpeer))
			client.enetpeer=enetpeer
			
			If IPBanned(client.ip)
				Disconnect(client,0)
				Return
			Else
				client.name=packet.ReadLine()
				If Not FindClientByName(client.name)
					clientmap.insert(client.name,client)
					clients.AddLast(client)
					Local responsepacket:TPacket=New TPacket
					responsepacket.WriteByte(1)
					responsepacket.WriteByte(clients.count()-1)
					Local peer:TClient
					For peer=EachIn clients
						If peer&lt;&gt;client responsepacket.WriteLine(peer.name)
					Next
					Send(client,NETWORK_JOINRESPONSE,responsepacket,SEND_RELIABLE)
					id=NETWORK_PLAYERJOINED
					
					'Tell everyone he joined
					responsepacket=New TPacket
					responsepacket.WriteLine(client.name)
					Broadcast(NETWORK_PLAYERJOINED,responsepacket,SEND_RELIABLE)
					
				Else
					packet=New TPacket
					packet.WriteByte(0)'no, you can't joint
					packet.WriteByte(1)'reason: name already taken
					Send(client,NETWORK_JOINRESPONSE,packet,SEND_RELIABLE)
					'Disconnect(client,0)
					Return
				EndIf
			EndIf
		
		Case NETWORK_CHANGENAMEREQUEST
			Local name:String=packet.ReadLine()
			packet=New TPacket
			If client
				Local oldname:String=client.name
				If FindClientByName(name)&lt;&gt;Null And client.name&lt;&gt;name
					packet.WriteByte(0)
					send(client,NETWORK_CHANGENAMERESPONSE,packet,SEND_RELIABLE)
					Return
				Else
					packet.WriteByte(1)
					packet.WriteLine(name)
					send(client,NETWORK_CHANGENAMERESPONSE,packet,SEND_RELIABLE)
					clientmap.remove(client.name)
					client.name=name
					clientmap.insert(name,client)
					If oldname&lt;&gt;name
						packet=New TPacket
						packet.WriteLine(oldname)
						packet.WriteLine(name)
						Broadcast(NETWORK_PLAYERCHANGEDNAME,packet,SEND_RELIABLE)
					EndIf
				EndIf
			Else
				If FindClientByName(name)=Null
					packet.WriteByte(1)
					packet.WriteLine(name)
					send(client,NETWORK_CHANGENAMERESPONSE,packet,SEND_RELIABLE)
					Return
				EndIf
			EndIf
			Return
								
		Case NETWORK_CONNECT
			Return
		
		Case NETWORK_DISCONNECT
			If client
				clients.remove(client)
				If clientmap.valueforkey(client.name)=client clientmap.remove(client.name)
				If Not client.enethost client.free()
			EndIf
			
		Case NETWORK_PINGREQUEST
			If Not client
				client=New TClient
				client.enetpeer=enetpeer
			EndIf
			Send(client,NETWORK_PINGRESPONSE,packet)
			Return
		
		Case NETWORK_CHAT
			Local relay:TPacket=New TPacket
			Local count:Int=packet.ReadByte()
			relay.WriteLine(client.name)
			relay.WriteLine(packet.ReadLine())
			If count=0
				Broadcast(NETWORK_CHAT,relay,SEND_RELIABLE)
			Else
				For Local n:Int=1 To count
					client=FindClientByName(packet.ReadLine())
					If client
						Send(client,NETWORK_CHAT,relay,SEND_RELIABLE)
					EndIf
				Next
			EndIf
			Return
		
		EndSelect
		
		If callback
			If packet packet.seek(0)
			callback(Self,client,id,packet)
		EndIf
	EndMethod
	
	Method Send:Int(client:TClient,id:Int,packet:TPacket=Null,flags:Int=0,channel:Int=0)
		Local enetpacket:Byte Ptr
		Local result:Int
		
		If Not client.enetpeer RuntimeError "Can't send to local client."
		
		Local data:Byte[]
		If packet
			If packet._bank.size()=0 packet=Null
		EndIf
		If packet
			data=New Byte[packet._bank.size()+1]
			MemCopy(Varptr data[1],packet._bank.buf(),packet._bank.size())
		Else
			data=New Byte[1]
		EndIf
		data[0]=id
		enetpacket=enet_packet_create(data,data.length,flags)
		result=(enet_peer_send(client.enetpeer,channel,enetpacket)=0)		
		Return result
	EndMethod
	
	Method Broadcast:Int(id:Int,packet:TPacket=Null,flags:Int=0,channel:Int=0)
		Local result:Int=1
		For Local client:TClient=EachIn clients
			If Not Send(client,id,packet,flags,channel) result=0
		Next
		Return result
		Rem
		Local enetpacket:Byte Ptr
		Local result:Int
		
		Local data:Byte[]
		If packet
			If packet._bank.size()=0 packet=Null
		EndIf
		If packet
			data=New Byte[packet._bank.size()+1]
			MemCopy(Varptr data[1],packet._bank.buf(),packet._bank.size())
		Else
			data=New Byte[1]
		EndIf
		data[0]=id
		enetpacket=enet_packet_create(data,data.length,flags)
		enet_host_broadcast(Self.enethost,channel,enetpacket)
		EndRem
	EndMethod
	
	Method Disconnect(client:TClient,force:Int=False)
		If client.enetpeer
			If force
				enet_peer_reset(client.enetpeer)
			Else
				enet_peer_disconnect(client.enetpeer)
			EndIf
			clients.remove(client)
			If Not client.enethost
				client.link.remove()
			EndIf
			If clientmap.valueforkey(client.name)=client
				clientmap.remove(client.name)
			EndIf
			client.enetpeer=Null
		EndIf
	EndMethod
	
	Method BanIP(ip:Int)
		bannedips=bannedips[..bannedips.length+1]
		bannedips[bannedips.length-1]=ip
	EndMethod
	
	Method IPBanned:Int(ip:Int)
		For Local n:Int=0 To bannedips.length-1
			If ip=bannedips[n] Return True
		Next
		Return False
	EndMethod
	
	Method Kick(client:TClient)
		BanIP(client.ip)
		Disconnect(client)
	EndMethod
	
EndType

Type TClient Extends TNetworkNode
	
	Const maxplayers:Int=64
	
	Global list:TList=New TList
	
	Field name:String
	Field link:TLink
	Field server:TServer
	Field connected:Int
	Field joined:Int=0
	Field callback(client:TClient,id:Int,packet:TPacket)
	Field userdata:Object
	Field channels:Int=16
	Field latency:Int
	
	Method Free()
		If link
			link.remove()
			link=Null
		EndIf
		Super.Free()
	EndMethod
	
	Function Find:TClient(ip:Int,port:Int)
		Local client:TClient
		For client=EachIn list
			If client.ip=ip And client.port=port Return client
		Next
		client=New TClient
		client.ip=ip
		client.port=port
		client.link=list.addlast(client)
		Return client
	EndFunction
	
	Function Create:TClient(port:Int=0,portrange:Int=40)
		Const maxpeers:Int=32
		Local client:TClient=New TClient
		Local addr:Byte Ptr
		
		If portrange&lt;=1
			client.ip=ENET_HOST_ANY
			client.port=port
			addr=enet_address_create(client.ip,client.port)
			client.enethost=enet_host_create(addr,maxpeers,0,0)
			enet_address_destroy(addr)
			If Not client.enethost Return Null
			client.link=list.addlast(client)
		Else
			If port=0 port=7776
			For Local n:Int=port To port+portrange-1
				addr=enet_address_create(ENET_HOST_ANY,n)
				client.enethost=enet_host_create(addr,maxpeers,0,0)
				enet_address_destroy(addr)
				If client.enethost
					client.port=port
					Exit
				EndIf
			Next
		EndIf
		
		Return client
	EndFunction
	
	Method Disconnect(force:Int=False)
		Const disconnecttimeout:Int=10000
		
		If Not Self.enethost RuntimeError "Can't update a remote server."
		If server
			If force
				enet_peer_reset(server.enetpeer)
			Else
				Send(NETWORK_LEAVEGAMEREQUEST,Null,SEND_RELIABLE)
				Local ev:ENetEvent=New ENetEvent
				Local start:Int=MilliSecs()
				Repeat
					If MilliSecs()-start&gt;disconnecttimeout Exit
					If enet_host_service(Self.enethost,ev,100)
						Select ev.event
						Case ENET_EVENT_TYPE_RECEIVE
							If ev.packet
								Local id:Int,packet:TPacket
								Local size:Int=enet_packet_size(ev.packet)
								Local data:Byte[size]
								MemCopy(Varptr id,enet_packet_data(ev.packet),1)
								If size&gt;1
									packet=New TPacket
									packet._bank.resize(size-1)
									MemCopy(packet._bank.buf(),enet_packet_data(ev.packet)+1,size-1)
								EndIf
								Select id
								Case NETWORK_LEAVEGAMERESPONSE
									Exit
								EndSelect
							EndIf
						EndSelect
					Else
						Exit
					EndIf
				Forever
				enet_peer_disconnect(server.enetpeer)
			EndIf
			server=Null
		EndIf
		joined=False
	EndMethod
	
	Method SetName(name:String)
		If name.length&gt;16 name=name[..16]
		If server
			Local packet:TPacket=New TPacket
			packet.WriteLine(name)
			Send(NETWORK_CHANGENAMEREQUEST,packet,SEND_RELIABLE)
		Else
			Self.name=name
		EndIf
	EndMethod
	
	Method Connect:Int(ip:Int,port:Int)
		Local addr:Byte Ptr
				
		If ip=0 ip=2130706433
		If Not Self.enethost RuntimeError "Remote client cannot connect to server."
		If server Disconnect()		
		server=New TServer
		server.ip=ip
		server.port=port
		addr=enet_address_create(server.ip,server.port)
		server.enetpeer=enet_host_connect(enethost,addr,channels)
		enet_address_destroy(addr)
		If server.enetpeer=Null
			server=Null
			Return 0
		EndIf
		
		Rem
		Local _callback:Byte Ptr=Null
		_callback=callback		
		Local start:Int=MilliSecs()
		Repeat
			Update()
			If MilliSecs()-start&gt;10000 Or joined=1 Exit
		Forever
		callback=_callback
		EndRem
		
		Return 1
	EndMethod
	
	Method Send:Int(id:Int,packet:TPacket=Null,flags:Int=0,channel:Int=0)
		Local enetpacket:Byte Ptr
		Local result:Int
		
		If Not connected Return 0
		
		If Not server RuntimeError "Client is not connected."
		Local data:Byte[]
		If packet
			If packet._bank.size()=0 packet=Null
		EndIf
		If packet
			data=New Byte[packet._bank.size()+1]
			MemCopy(Varptr data[1],packet._bank.buf(),packet._bank.size())
		Else
			data=New Byte[1]
		EndIf
		data[0]=id
		enetpacket=enet_packet_create(data,data.length,flags)
		result=(enet_peer_send(server.enetpeer,channel,enetpacket)=0)		
		Return result
	EndMethod
	
	Method EvaluateEvent(id:Int,packet:TPacket,enetpeer:Byte Ptr)
		Select id
		Case NETWORK_CONNECT
			Self.connected=True
			packet=New TPacket
			packet.WriteLine(name)
			Send(NETWORK_JOINREQUEST,packet)			
		Case NETWORK_JOINRESPONSE
			joined=packet.ReadByte()
			If joined=0 Disconnect()
		Case NETWORK_CHANGENAMERESPONSE
			If packet.ReadByte()=1
				name=packet.ReadLine()
			EndIf
		Case NETWORK_PINGREQUEST
			Send(NETWORK_PINGRESPONSE,packet)
			Return
		Case NETWORK_PINGRESPONSE
			latency=MilliSecs()-packet.ReadInt()
			packet.WriteInt(enet_peer_ip(enetpeer))
			packet.WriteInt(enet_peer_port(enetpeer))
			If enetpeer=pingpeer
				enet_peer_disconnect(pingpeer)
				pingpeer=Null
			EndIf
		EndSelect
		If callback
			If packet packet.seek(0)
			callback(Self,id,packet)
		EndIf
	EndMethod
	
	Method Join()
		If Not joined
			Local packet:TPacket=New TPacket
			packet.WriteLine(name)
			Send(NETWORK_JOINREQUEST,packet)
		EndIf
	EndMethod
	
	Function ConvertEvent:Int(ev:EnetEvent,packet:TPacket)
		Select ev.event
		Case ENET_EVENT_TYPE_CONNECT
			Return NETWORK_CONNECT
		Case ENET_EVENT_TYPE_DISCONNECT
			Return NETWORK_DISCONNECT
		Case ENET_EVENT_TYPE_RECEIVE
			If ev.packet
				Local id:Int
				Local size:Int=enet_packet_size(ev.packet)
				Local data:Byte[size]
				MemCopy(Varptr id,enet_packet_data(ev.packet),1)
				If size&gt;1
					packet._bank.resize(size-1)
					MemCopy(packet._bank.buf(),enet_packet_data(ev.packet)+1,size-1)
				EndIf
				enet_packet_destroy(ev.packet)
				Return id
			EndIf
		EndSelect
	EndFunction
	
	Field pingpeer:Byte Ptr
	
	Method Ping:Int(ip:Int=0,port:Int=0)
		Const disconnecttimeout:Int=1000
		Local packet:TPacket=New TPacket,addr:Byte Ptr,enetpacket:Byte Ptr,ev:EnetEvent,result:Int,id:Int,start:Int
		
		If server
			If server.ip=ip And server.port=port ip=0
		EndIf
		
		If Not enethost RuntimeError("Can't ping remote client.")
		If ip
			If pingpeer
				enet_peer_disconnect(pingpeer)
				pingpeer=Null
			EndIf
			
			ev=New EnetEvent
			addr=enet_address_create(ip,port)
			pingpeer=enet_host_connect(enethost,addr,1)
			enet_address_destroy(addr)
			If pingpeer
				start=MilliSecs()
				Repeat
					If MilliSecs()-start&gt;disconnecttimeout
						enet_peer_disconnect(pingpeer)
						pingpeer=Null
						'Print "Connect timeout"
						Return 0
					EndIf
					If enet_host_service(enethost,ev,0)
						id=ConvertEvent(ev,packet)
						Select id
						Case NETWORK_CONNECT
							Exit
						EndSelect
					EndIf
				Forever
				Local data:Byte[5]
				data[0]=NETWORK_PINGREQUEST
				start=MilliSecs()
				MemCopy(Varptr data[1],Varptr start,4)
				enetpacket=enet_packet_create(data,data.length,0)
				If enet_peer_send(pingpeer,0,enetpacket)=0
					Return 1
				Else
					enet_peer_disconnect(pingpeer)
					pingpeer=Null
					'Print "can't send packet"
					Return 0
				EndIf				
			Else
				'Print "Cant connect to peer at "+ip+", "+port
				Return 0
			EndIf
		Else
			packet:TPacket=New TPacket
			packet.WriteInt(MilliSecs())
			Return Send(NETWORK_PINGREQUEST,packet)
		EndIf
	EndMethod
	
	Method Say:Int(text:String,recipients:String[]=Null)
		Local packet:TPacket=New TPacket
		If recipients
			packet.WriteByte(recipients.length)
			packet.WriteLine(text)
			For Local n:Int=0 To recipients.length-1
				packet.WriteLine(recipients[n])
			Next
		Else
			packet.WriteByte(0)
			packet.WriteLine(text)
		EndIf
		Return Send(NETWORK_CHAT,packet)		
	EndMethod
	
EndType


Type TPacket Extends TBankStream
	
	Method New()
		_bank=New TBank
	EndMethod
	
EndType


Function CreatePacket:TPacket()
	Local packet:TPacket=New TPacket
	Return packet
EndFunction</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> Here's an example of an advanced chat program using this system:<br><br>chat.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

Import "network.bmx"
Import maxgui.drivers
Import "appsettings.bmx"

Const GAMENAME:String="The_Big_Game_of_Chat"
Const PINGTIMEOUT:Int=1000

Local sendpingtime:Int

Local ip:Int=HostIp("127.0.0.1")

Local window:TGadget=CreateWindow("",0,0,640,480,,WINDOW_CENTER|WINDOW_RESIZABLE|WINDOW_TITLEBAR)

Local bs:Int=128
Local x:Int=2,y:Int=2,h:Int=18

Local panel_start:TGadget=CreatePanel(0,0,window.ClientWidth(),window.ClientHeight(),window)
Local button_JoinGame:TGadget
Local button_CreateServer:TGadget
button_JoinGame=CreateButton("Join Game",panel_start.ClientWidth()/4*1-bs/2,panel_start.ClientHeight()/2-bs/2,bs,bs,panel_start)
button_CreateServer=CreateButton("Create Server",panel_start.ClientWidth()/4*3-bs/2,panel_start.ClientHeight()/2-bs/2,bs,bs,panel_start)
SetGadgetLayout(CreateLabel("Client Name:",1,1,100,19,panel_start),1,0,1,0) ; y:+h
Local gadget_clientname:TGadget=CreateTextField(x,y,100,h,panel_start) ; y:+h
SetGadgetLayout(CreateLabel("Client Port (optional):",x,y,300,19,panel_start),1,0,1,0) ; y:+h
Local gadget_clientport:TGadget=CreateTextField(x,y,100,h,panel_start) ; y:+h
SetGadgetText(gadget_clientport,appsetting("clientport"))

SetGadgetLayout(gadget_clientname,1,0,1,0)
SeedRnd(MilliSecs())
SetGadgetText(gadget_clientname,AppSetting("clientname",""))
If GadgetText(gadget_clientname)="" ActivateGadget(gadget_clientname)
SetGadgetLayout(panel_start,1,1,1,1)
y=2

Global panel_JoinGame:TGadget=CreatePanel(0,0,window.ClientWidth(),window.ClientHeight(),window)
HideGadget(panel_JoinGame)
SetGadgetLayout(panel_JoinGame,1,1,1,1)
SetGadgetLayout(CreateLabel("Servers:",1,1,100,19,panel_joingame),1,0,1,0)
Global list_server:TGadget=CreateListBox(1,20,panel_JoinGame.ClientWidth()-2,panel_JoinGame.ClientHeight()-20-1-26,panel_JoinGame)
Local button_JoinGame_Back:TGadget=CreateButton("Back",0,panel_JoinGame.ClientHeight()-26,64,26,panel_JoinGame,BUTTON_CANCEL)
SetGadgetLayout(button_JoinGame_Back,1,0,0,1)
SetGadgetLayout(list_server,1,1,1,1)
Local button_JoinGame_Refresh:TGadget=CreateButton("Refresh",64,panel_JoinGame.ClientHeight()-26,64,26,panel_JoinGame)
SetGadgetLayout(button_JoinGame_Refresh,1,0,0,1)
Local button_JoinGame_ok:TGadget=CreateButton("Join",64*2,panel_JoinGame.ClientHeight()-26,64,26,panel_JoinGame,BUTTON_OK)
SetGadgetLayout(button_JoinGame_ok,1,0,0,1)

Global panel_CreateServer:TGadget=CreatePanel(0,0,window.ClientWidth(),window.ClientHeight(),window)
HideGadget(panel_CreateServer)
SetGadgetLayout(panel_CreateServer,1,1,1,1)
SetGadgetLayout(CreateLabel("Server Port (optional):",x,y,300,h,panel_CreateServer),1,0,1,0) ; y:+h
Local gadget_serverport:TGadget=CreateTextField(x,y,100,h,panel_CreateServer) ; y:+h
SetGadgetLayout(gadget_serverport,1,0,1,0)
SetGadgetText(gadget_serverport,appsetting("serverport"))
SeedRnd(MilliSecs()+42)
SetGadgetLayout(CreateLabel("Server Name:",x,y,160,h,panel_CreateServer),1,0,1,0) ; y:+h
Local gadget_servername:TGadget=CreateTextField(x,y,100,h,panel_CreateServer) ; y:+h
SetGadgetText(gadget_servername,AppSetting("servername",""))
SetGadgetLayout(gadget_servername,1,0,1,0)
Local gadget_createserver_ok:TGadget=CreateButton("Create",x,y,100,h,panel_CreateServer) ; y:+h
SetGadgetLayout(gadget_createserver_ok,1,0,1,0)
Local button_createserver_Back:TGadget=CreateButton("Back",0,panel_CreateServer.ClientHeight()-26,64,26,panel_CreateServer,BUTTON_CANCEL)
SetGadgetLayout(button_createserver_Back,1,0,0,1)

Local panel:TGadget

Global panel_Chat:TGadget=CreatePanel(0,0,window.ClientWidth(),window.ClientHeight(),window)
HideGadget(panel_Chat)
SetGadgetLayout(panel_Chat,1,1,1,1)
SetGadgetLayout(CreateLabel("Players:",window.ClientWidth()-200+1,1,100,19,panel_Chat),0,1,1,0)
Global gadget_chat_playerlist:TGadget=CreateListBox(panel_Chat.ClientWidth()-200,20,200,panel_Chat.ClientHeight()-20-22-20,panel_Chat)
SetGadgetLayout(gadget_chat_playerlist,0,1,1,1)
Global gadget_chat_log:TGadget=CreateTextArea(1,1,panel_Chat.ClientWidth()-200-2,panel_Chat.ClientHeight()-22-2,panel_Chat,TEXTAREA_READONLY)
SetGadgetLayout gadget_chat_log,1,1,1,1

panel=CreatePanel(64+1,panel_Chat.ClientHeight()-22+1,panel_Chat.ClientWidth()-200-64,22-2,panel_Chat)
SetGadgetLayout panel,1,1,0,1

Local gadget_chat_input:TGadget=CreateTextField(0,0,panel.ClientWidth()-22,22,panel)
SetGadgetLayout gadget_chat_input,1,1,0,1
Local gadget_chat_ok:TGadget=CreateButton("OK",panel.ClientWidth()-22,0,22,22,panel,BUTTON_OK)
SetGadgetLayout gadget_chat_ok,0,1,0,1

Local button_chat_Back:TGadget=CreateButton("Back",0,panel_Chat.ClientHeight()-26,64,26,panel_Chat,BUTTON_CANCEL)
SetGadgetLayout(button_chat_Back,1,0,0,1)
panel=CreatePanel(panel_Chat.ClientWidth()-200,panel_Chat.ClientHeight()-22-20,200,22+20,panel_Chat)
SetGadgetLayout panel,0,1,0,1
Local gadget_changename:TGadget=CreateTextField(0,panel.ClientHeight()-22,200-22,22,panel)
SetGadgetLayout gadget_changename,1,1,0,1
CreateLabel("Change name to:",0,0,200,20,panel)
Local gadget_changename_ok:TGadget=CreateButton("OK",panel.ClientWidth()-22,panel.ClientHeight()-22,22,22,panel,BUTTON_OK)
SetGadgetLayout gadget_changename_ok,0,1,0,1

Type TGame
	Field ip:Int
	Field port:Int
	Field name:String
	Field latency:Int	
	Field index:Int
	Field pinging:Int
	
	Method Update()
		ModifyGadgetItem(list_server,index,"IP="+ip+" | Port="+port+" | Name="+name+" | Ping="+latency)
	EndMethod
	
EndType

Global game:TGame[]
Global currentpinggame:Int

Global server:TServer
Global client:TClient

Repeat
	
	If server server.Update()
	If client client.Update()
	
	'Update pings for list of servers
	If client
		If Not GadgetHidden(panel_JoinGame)
			If game.length
				If game[currentpinggame].pinging
					If MilliSecs()-sendpingtime&gt;PINGTIMEOUT
						game[currentpinggame].pinging=False
						game[currentpinggame].latency=-1
						game[currentpinggame].Update()
						currentpinggame:+1
						Print "ping timeout"
						If currentpinggame&gt;game.length-1 currentpinggame=0
					EndIf
				EndIf
				If game[currentpinggame].latency=0
					If Not game[currentpinggame].pinging			
						If client.ping(game[currentpinggame].ip,game[currentpinggame].port)
							game[currentpinggame].pinging=True
							sendpingtime=MilliSecs()
						Else
							game[currentpinggame].latency=-1
							game[currentpinggame].Update()
						EndIf
					EndIf
				Else
					currentpinggame:+1
					If currentpinggame&gt;game.length-1 currentpinggame=0
				EndIf
			EndIf
		EndIf
	EndIf
		
	Delay 1
	
	While PeekEvent()
		Select WaitEvent()
		
		Case EVENT_WINDOWCLOSE
			If client
				client.Disconnect()
				client.Free()
			EndIf
			If server server.Free()
			setappsetting("clientname",GadgetText(gadget_clientname))
			setappsetting("servername",GadgetText(gadget_servername))
			setappsetting("clientport",GadgetText(gadget_clientport))
			setappsetting("serverport",GadgetText(gadget_serverport))
			SaveSettings()
			End
		
		Case EVENT_GADGETACTION
			
			Select CurrentEvent.source
			
			Case button_JoinGame_ok
				EmitEvent CreateEvent(EVENT_GADGETACTION,list_server,SelectedGadgetItem(list_server))
			
			Case button_JoinGame_Refresh
				EmitEvent CreateEvent(EVENT_GADGETACTION,button_JoinGame)
			
			Case gadget_changename_ok
				Local name:String=GadgetText(gadget_changename).Trim()
				If name&lt;&gt;""
					client.SetName(name)
					SetGadgetText(gadget_changename,name)
				Else
					Notify "Please enter a name"
				EndIf
				
			Case list_server
				Local serverip:Int,serverport:Int
				serverip=game[CurrentEvent.data].ip
				serverport=game[CurrentEvent.data].port
				client=TClient.Create(Int(GadgetText(gadget_clientport)))
				If client
					client.callback=ClientCallback
					client.SetName(GadgetText(gadget_clientname))
					If client.Connect(serverip,serverport)
						SetGadgetText(gadget_chat_log,"")
						ShowGadget(panel_Chat)
						HideGadget(panel_joingame)
						ActivateGadget(gadget_chat_input)
					Else
						Notify "Failed to connect to server.",1
						client.Free()
						client=Null
					EndIf
				Else
					Notify "Failed to create client.",1
				EndIf
				
			Case button_chat_back
				If Confirm("Do you really want to leave the server?")
					client.disconnect()
					client.Free()
					If server
						server.Free()
						ShowGadget(panel_createserver)
					Else
						ShowGadget(panel_joingame)
						EmitEvent CreateEvent(EVENT_GADGETACTION,button_JoinGame)
					EndIf
					HideGadget(paneL_chat)
					client=Null
					server=Null
				EndIf
				
			Case gadget_chat_ok
				client.say(GadgetText(gadget_chat_input))
				SetGadgetText(gadget_chat_input,"")
				ActivateGadget(gadget_chat_input)
				
			Case gadget_createserver_ok
				SetGadgetText(gadget_chat_log,"")
				server=TServer.Create(Int(GadgetText(gadget_serverport)))
				If server
					If server.Publish(GadgetText(gadget_servername),GAMENAME)
						client=TClient.Create(Int(GadgetText(gadget_clientport)))
						If client
							client.callback=clientcallback
							client.SetName(GadgetText(gadget_clientname))
							client.callback=ClientCallback
							If client.Connect(ip,server.port)
								ShowGadget(panel_Chat)
								HideGadget(panel_createserver)
								ActivateGadget(gadget_chat_input)
							Else
								Notify "Failed to connect to server.",1
								server.Free()
								client.Free()
								client=Null
								server=Null
							EndIf
						Else
							Notify "Failed to create client.",1
							server.Free()
							server=Null
							client=Null
						EndIf
					Else
						Notify "Failed to publish server.",1
						server.Free()
						server=Null
						client=Null
					EndIf
				Else
					Notify "Failed to create server.",1
					server=Null
					client=Null
				EndIf
				
			Case button_JoinGame
				ClearGadgetItems(list_server)
				ShowGadget panel_JoinGame
				HideGadget panel_start
				Local sarr:String[]=TServer.Query(GAMENAME)
				If Not client client=TClient.Create(Int(GadgetText(gadget_clientport)))
				If client
					client.callback=clientcallback
					game=New TGame[sarr.length]
					currentpinggame=0
					For Local n:Int=0 To sarr.length-1
						Local serverinfo:String[]=sarr[n].split("|")
						game[n]=New TGame
						game[n].index=n
						game[n].ip=Int(serverinfo[0])
						game[n].port=Int(serverinfo[2])
						game[n].name=serverinfo[3]
						AddGadgetItem(list_server,sarr[n])
						game[n].Update()
					Next
				Else
					Notify "Failed to create client.",1
				EndIf
				If CountGadgetItems(list_server) SelectGadgetItem(list_server,0)
				ActivateGadget(list_server)
			
			Case button_createserver
				ShowGadget(panel_CreateServer)
				HideGadget(panel_start)
				If GadgetText(gadget_servername)="" ActivateGadget(gadget_servername)
			
			Case button_JoinGame_Back
				ShowGadget panel_start	
				HideGadget panel_JoinGame				
				If client
					client.free()
					client=Null
				EndIf
			
			Case button_createserver_Back
				ShowGadget panel_start	
				HideGadget panel_createserver
			
			EndSelect
		
		EndSelect
	Wend
	
Forever


Function ServerCallBack(server:TServer,client:TClient,id:Int,packet:TPacket)
	Select id
	Case NETWORK_PLAYERJOINED
		Print "New player "+packet.ReadLine()+" joined."
	Case NETWORK_DISCONNECT
		If client
			Print client.name+" disconnected."
		Else
			Print "Unknown disconnection"
			Print server.clients.count()+" players"
		EndIf
	Case NETWORK_CONNECT
		Notify "Connection attempted"
		Print "New client connected."
	EndSelect
EndFunction


Function ClientCallBack(client:TClient,id:Int,packet:TPacket)
	Select id
	Case NETWORK_PLAYERLEFT
		Local name:String=packet.ReadLine()
		AddTextAreaText(gadget_chat_log,name+" has left the game~n")
		For Local n:Int=0 To CountGadgetItems(gadget_chat_playerlist)-1
			If GadgetItemText(gadget_chat_playerlist,n)=name
				RemoveGadgetItem(gadget_chat_playerlist,n)
				Exit
			EndIf
		Next
	Case NETWORK_PLAYERJOINED
		Local name:String=packet.ReadLine()
		AddTextAreaText(gadget_chat_log,name+" has joined the game~n")
		For Local n:Int=0 To CountGadgetItems(gadget_chat_playerlist)-1
			If GadgetItemText(gadget_chat_playerlist,n)=name
				Return
			EndIf
		Next
		AddGadgetItem(gadget_chat_playerlist,name)
	Case NETWORK_CHANGENAMERESPONSE
		If packet.ReadByte()=1
			'AddTextAreaText(gadget_chat_log,"Changed name To "+packet.ReadLine()+"~n")
		Else
			AddTextAreaText(gadget_chat_log,"Name change rejected.~n")
		EndIf
	Case NETWORK_JOINRESPONSE
		If packet.ReadByte()=1
			AddTextAreaText(gadget_chat_log,"Joined game~n")
			Local count:Int=packet.ReadByte()
			'Print count+" players"
			
			ClearGadgetItems(gadget_chat_playerlist)
			
			For Local n:Int=1 To count
				AddGadgetItem(gadget_chat_playerlist,packet.ReadLine())
			Next
		Else
			Select packet.ReadByte()
			Case 1
				Notify("Rejected from game (Name already in use)",1)
			Default
				Notify("Rejected from game (Unknown reason)",1)
			EndSelect
			client.disconnect()
			client.Free()
			If server
				server.Free()
				ShowGadget(panel_createserver)
			Else
				ShowGadget(panel_joingame)
			EndIf
			HideGadget(panel_chat)
			.client=Null
			server=Null
		EndIf
	Case NETWORK_PLAYERCHANGEDNAME
		Local name:String[2]
		name[0]=packet.ReadLine()
		name[1]=packet.ReadLine()
		AddTextAreaText(gadget_chat_log,name[0]+" changed name to "+name[1]+"~n")
		For Local n:Int=0 To CountGadgetItems(gadget_chat_playerlist)-1
			If GadgetItemText(gadget_chat_playerlist,n)=name[0]
				ModifyGadgetItem(gadget_chat_playerlist,n,name[1])
				Exit
			EndIf
		Next
	Case NETWORK_DISCONNECT
		AddTextAreaText(gadget_chat_log,"Disconnected from server~n")
	Case NETWORK_CONNECT
		AddTextAreaText(gadget_chat_log,"Connected to server~n")
	Case NETWORK_CHAT
		Local name:String=packet.ReadLine()
		Local text:String=packet.ReadLine()
		Local l:Int=GadgetText(gadget_chat_log).length
		AddTextAreaText(gadget_chat_log,name+": "+text+"~n")
		FormatTextAreaText(gadget_chat_log,0,128,0,0,l)
	Case NETWORK_PINGRESPONSE
		Local time:Int=packet.ReadInt()
		Local ip:Int=packet.ReadInt()
		Local port:Int=packet.ReadInt()
		If game.length
			If game[currentpinggame].ip=ip And game[currentpinggame].port=port
				game[currentpinggame].latency=(MilliSecs()-time)
				game[currentpinggame].pinging=0
				game[currentpinggame].Update()
				'ModifyGadgetItem(list_server,currentpinggame,GadgetItemText(list_server,currentpinggame)+"|Ping="+game[currentpinggame].latency)
				currentpinggame:+1
				If currentpinggame&gt;game.length-1 currentpinggame=0
			EndIf	
		EndIf
	EndSelect
EndFunction</textarea><br>appsettings.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Import brl.map
Import brl.retro

Private

Function ParseAppArgs()
	Local key$,value$,n
	For n=1 To AppArgs.length-1
		indicator$=Left(AppArgs[n],1)
		key$=Lower(Right(AppArgs[n],AppArgs[n].length-1))
		Select indicator$
			Case "+"
				n:+1
				If n&gt;=AppArgs.length Exit
				value$=AppArgs[n]
			Case "-"			
				value="1"
			Default
				Continue
		EndSelect
		CommandLineSettings.insert key,value
	Next
EndFunction

Public

Global AppSettings:TMap=New TMap
Global CommandLineSettings:TMap=New TMap

'OnEnd SaveSettings

LoadSettings()
ParseAppArgs()

Function GetApp$(key$,defaultvalue$="")
	Return AppSetting(key,defaultvalue)
EndFunction

Rem
bbdoc:
EndRem
Function AppSetting$(key$,defaultvalue$="")
	If key="" Return
	key=Lower(key)
	value$=String(CommandLineSettings.valueforkey(key$))
	If value="" value$=String(AppSettings.valueforkey(key$))
	If value=""
		Return defaultvalue
	Else
		Return value
	EndIf
EndFunction

Rem
bbdoc:
EndRem
Function SetAppSetting(key$,value$)
	key=Lower(key)
	If value="" value=Null
	AppSettings.insert key,value
EndFunction

Function SetApp(key$,value$)
	key=Lower(key)
	If value="" value=Null
	AppSettings.insert key,value
EndFunction

Rem
bbdoc:
EndRem
Function LoadSettings()
	Local stream:TStream
	Local s$,key$,value$
	Local keypair$[]
	Local path$
	path=StripExt(AppFile)
	If Right(path,6).ToLower()=".debug" path=Left(path,path.length-6)
	path:+".cfg"
	stream=ReadFile(path)
	If Not stream Return
	While Not stream.Eof()
		s=stream.ReadLine()
		keypair=s.split("=")
		key=Lower(Trim(keypair[0]))
		value=Replace(Trim(keypair[1]),"~q","")
		If keypair.length=2 AppSettings.insert key,value
	Wend
	stream.close()
EndFunction

Rem
bbdoc:
EndRem
Function SaveSettings()
	Local stream:TStream
	Local key$
	Local path$
	path=StripExt(AppFile)+".cfg"
	If Right(path,6).ToLower()=".debug" path=Left(path,path.length-6)
	stream=WriteFile(path)
	If Not stream Return
	For key=EachIn AppSettings.keys()
		stream.WriteLine key+"=~q"+String(AppSettings.valueforkey(key))+"~q"
	Next
	stream.close()
EndFunction</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Murilo</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> Very nice! Thanks for sharing. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Galaxy613</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> In Create:TServer() you need to assign the server.port when portrange &lt;= 1.<br><br>Basically...<br><br><pre class=code>		If portrange&lt;=1
			server.port = port
			addr=enet_address_create(server.ip,server.port)</pre><br><br>instead of<br><br><br><pre class=code>		If portrange&lt;=1
			addr=enet_address_create(server.ip,server.port) ' server.port starts out as 0...</pre><br><br>or else server.port will equal 0 unless you set server.port directly before hand. Kinda undoes having a port argument being passed.<br><br>Also kids, remember to do Server.Update or Client.Update were needed, or else NOTHING will happen. &gt;.&gt;<br><br>It's a shame you decided to use MaxGUI for the example, it's so cluttered with GUI crap it's hard to see the actual networking side of things.<br><br>*Edit*<br><br>and shouldn't in TClient.EvaluateEvent() this:<br><pre class=code>		Case NETWORK_CONNECT
			Self.connected=True
			packet=New TPacket
			packet.WriteLine(name)
			Send(NETWORK_JOINREQUEST,packet)	</pre><br>be this?<br><pre class=code>		Case NETWORK_CONNECT
			Self.connected=True
			Self.Join()</pre> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drackbolt</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> When I test this code as-is (Bmax 1.41), I get an Array OOB error at this line on the client side:<br>game[n].port=Int(serverinfo[2])<br><pre class=code>
				If Not client client=TClient.Create(Int(GadgetText(gadget_clientport)))
				If client
					client.callback=clientcallback
					game=New TGame[sarr.length]
					currentpinggame=0
					DebugLog sarr.length-1
					For Local n:Int=0 To sarr.length-1
						Local serverinfo:String[]=sarr[n].split("|")
						game[n]=New TGame
						game[n].index=n
						game[n].ip=Int(serverinfo[0])
						game[n].port=Int(serverinfo[2])
						game[n].name=serverinfo[3]
						AddGadgetItem(list_server,sarr[n])
						game[n].Update()
					Next
				Else
					Notify "Failed to create client.",1
				EndIf

</pre><br>Remming it out just makes the client give a dummy list of bad servers.  I only took a cursory glance at the source but  didn't see anything superficially wrong to me. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >warwulf</td><td align="right"><font class=tiny>2011</font></td></tr></table></td></tr><tr ><td class="posttext"> i cant start a server or connect to anything <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
