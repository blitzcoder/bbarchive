<!DOCTYPE html><html lang="en" ><head ><title >[maxgui] Max Browser</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='A simple web browser using htmlview, language=bmx, category=Networking'><meta name='author' content='Beaker'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=4>Networking</a>/[maxgui] Max Browser</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1533.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >[maxgui] Max Browser by Beaker</td><td align="right">2005 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This web browser code was originally written in B+ and was converted to Bmax to see how quickly it could be done.  I was impressed with how easy it was.  I think overall it works slightly better than the B+ version.<br><br>I haven't tested it on Mac/Linux, and would appreciate feedback from anyone who can test it on these platforms.  I'm particularly interested to know how a links menu works, and if you ever get problems with other windows opening. </td></tr><tr ><td class="cell"><pre class="code">'MAX BROWSE by Beaker 2005

SuperStrict 

Global window:TGadget
Global html:TGadget


'SETUP WINDOW
window=CreateWindow( "Max Browse",0,0,800,600)
SetMinWindowSize window,200,0


'SETUP MENUS
Local filemenu:Tgadget = CreateMenu ("  &amp;File  ",0,WindowMenu(window))
	Local openmenuitem:Tgadget = CreateMenu ("&amp;Open...",1,filemenu)

CreateMenu ("",999,filemenu)
	Local exitmenuitem:Tgadget = CreateMenu ("E&amp;xit",9,filemenu)

Local bookmenu:Tgadget = CreateMenu ("  &amp;Bookmarks  ",0,WindowMenu(window))
	Local addbookmenuitem:Tgadget = CreateMenu ("&amp;Add bookmark",21,bookmenu)
	Local organisebookmenuitem:Tgadget = CreateMenu ("&amp;Organise bookmarks",22,bookmenu)
	DisableMenu organisebookmenuitem
	Local defaultmenuitem:Tgadget = CreateMenu ("Set as &amp;default homepage",23,bookmenu)
	CreateMenu ("",999,bookmenu)

	
Local bookname$, bookURL$, bookfile:TStream
If FileType("bookmark.txt") &lt;&gt; 1	'Then create a bookmark file with Default bookmarks
	bookfile = WriteFile("bookmark.txt")
	RestoreData bookdata
	ReadData bookname$
	ReadData bookURL$
	While bookname$ &lt;&gt; "END"
		WriteLine bookfile,bookname$
		WriteLine bookfile,bookURL$
		ReadData bookname$
		ReadData bookURL
	Wend
	CloseFile bookfile
EndIf

'read the bookmarks into the bookmark menu
Global bookf:Int=500
bookfile = ReadFile("bookmark.txt")
While Not Eof(bookfile)
	bookname$ = ReadLine(bookfile)
	bookURL$ = ReadLine(bookfile)
	If bookURL&lt;&gt;""
		CreateMenu (bookname,bookf,bookmenu)
		bookf = bookf +1
	EndIf
Wend
CloseFile bookfile
CreateMenu ("",999,bookmenu)

Local helpmenu:Tgadget = CreateMenu("  &amp;Help  ",0,WindowMenu(window))
Local aboutmenu:Tgadget = CreateMenu("&amp;About",1000,helpmenu)

UpdateWindowMenu window


If FileType("default.txt") &lt;&gt; 1	'Then create the default.txt file with the Default homepage
	Local deffile:TStream = WriteFile("default.txt")
		WriteLine deffile,"http://www.blitzbasic.com"
	CloseFile deffile
EndIf

Local deffile:TStream = ReadFile("default.txt")
	Global defURL$ = ReadLine (deffile)
CloseFile deffile




'SETUP BUTTONS
Local panel:Tgadget = CreatePanel (0,0,800,40,window,0)
	SetGadgetLayout panel,1,0,1,0
	Local backbutt:TGadget = CreateButton ("Back",5,5,60,30,panel)
	Local forebutt:TGadget = CreateButton ("Forward",70,5,60,30,panel)
	Local refreshbutt:TGadget = CreateButton ("Refresh",135,5,60,30,panel)
	Local homebutt:TGadget = CreateButton ("Home",200,5,60,30,panel)
	Global URLfield:TGadget = CreateTextField (265,10,350,20,panel)
	Local URLgo:TGadget = CreateButton ("GO!",620,10,60,20,panel,BUTTON_OK)

	

'SETUP HTML VIEW
html:Tgadget=CreateHTMLView( 0,45,ClientWidth(window),ClientHeight(window)-50,window, HTMLVIEW_NONAVIGATE )
SetGadgetLayout html,1,1,1,1


Global current$
GoURL(defURL)

Local localURL$,found:Int,url$

'MAIN LOOP
While WaitEvent()
	Print EventData()
	Select EventID()
		Case EVENT_GADGETDONE	'page loaded
			If EventSource() = html
				SetGadgetText URLfield,HtmlViewCurrentURL(html)			
			EndIf
		Case EVENT_MENUACTION	'MENU EVENTS
			Select EventData()
				Case 1	'Open Local File
					DebugLog "OPEN LOCAL"
					localURL$ = RequestFile("Open local file","Web files - *.htm,html,jpg,gif,png:htm,html,jpg,gif,png;All files:*")
					If FileType (localURL) = 1
						GoURL(localURL)
					EndIf
				Case 9	'Close program
					End
				Case 21	'Add bookmark
					bookfile = OpenFile("bookmark.txt")
					found = False
					While Not Eof(bookfile)
						bookname = ReadLine(bookfile)
						bookURL = ReadLine(bookfile)
						If bookURL = current
							Notify "URL already in bookmarks"
							found = True
							Exit
						EndIf
					Wend
					If found = False
						WriteLine bookfile,current
						WriteLine bookfile,current
						CreateMenu (current,bookf,bookmenu)
						bookf = bookf +1
						UpdateWindowMenu window
					EndIf
					CloseFile bookfile
				Case 23	'Set as Default homepage
					deffile = WriteFile("default.txt")
						WriteLine deffile,current$
					CloseFile deffile
				Case 1000	'About
					Notify "Max Browse by Beaker 2005"+Chr(13)+"(bookmarks are in the bookmark.txt file)"
			End Select
			If EventData() &gt;= 500	'Jump To a specific bookmark
				Local f:Int = 0
				bookfile = ReadFile("bookmark.txt")
				While Not Eof(bookfile)
					bookname$ = ReadLine (bookfile)
					bookURL = ReadLine (bookfile)
					If f = EventData()-500 Then
						GoURL(bookURL)
						Exit
					EndIf
					f = f +1
				Wend
			EndIf	
		Case EVENT_GADGETACTION	'BUTTON EVENTS
			Select EventSource()
				Case html	' Catch clicking on URL links
					goURL String(EventExtra())
				Case backbutt
						HtmlViewBack html
				Case forebutt
						HtmlViewForward html
				Case refreshbutt
						GoURL(HtmlViewCurrentURL(html))
				Case homebutt
					deffile = ReadFile("default.txt")
						GoURL(ReadLine (deffile))
					CloseFile deffile
				Case URLgo
					GoURL(TextFieldText(URLfield))					
			End Select
					
		Case EVENT_WINDOWCLOSE	'WINDOW CLOSED EVENT
			Select EventSource()
				Case window
					End
			End Select
				
	End Select
	

Wend
End



Function GoURL(URL$)
	current = URL$
	SetGadgetText URLfield,current
	DebugLog current
	HtmlViewGo html,current
	SetGadgetText window,"Max Browse - "+current
End Function




#bookdata
DefData "Blitz Basic"
	DefData "http://www.blitzbasic.com"
DefData "gile[s] lightmapper"
	DefData "http://www.frecle.net/giles/"
DefData "GUIde GUI editor"
	DefData "http://members.home.nl/wdw/guide/"
DefData "Game Making Tools forum"
	DefData "http://playerfactory.proboards25.com"
DefData "END"
	DefData "END"</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >bruZard</td><td align="right"><font class=tiny>2005</font></td></tr></table></td></tr><tr ><td class="posttext"> First: Excuse my fu**n english ;)<br><br>Why i can't hit R<br>return and the given site is loading? Add a Event-Keydown and this Code is usefull ;) ;) <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Beaker</td><td align="right"><font class=tiny>2005</font></td></tr></table></td></tr><tr ><td class="posttext"> Ooh! that did work at one point, Mark must've changed some internal stuff since I wrote it.  I will look into it and amend. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Beaker</td><td align="right"><font class=tiny>2005</font></td></tr></table></td></tr><tr ><td class="posttext"> Updated. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >rdodson41</td><td align="right"><font class=tiny>2005</font></td></tr></table></td></tr><tr ><td class="posttext"> Im on the mac here, I like it works well, though are bookmarks not working? Anyway looks great! By the way I am posting this using Max Browse! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Beaker</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> Updated to incorporate SuperStrict and work with BMax 1.22.<br><br>Sushimasta - yes, something isn't working with the user created bookmarks. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Banshee</td><td align="right"><font class=tiny>2007</font></td></tr></table></td></tr><tr ><td class="posttext"> This is a sweet little program, but there's one limit and i've not managed to find a way around it.  It cannot cope with opening a link in a new window.<br><br>&lt;a href="example.php" target="_blank"&gt; <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>2007</font></td></tr></table></td></tr><tr ><td class="posttext"> Banshee, that limitation may not be going away any time soon. <br><br>To work around, try catching the clicked link event, hopefully you can detect the nature of the link and invoke the OpenURL command instead to launch the a new browser app instead of redirecting the page with a HTMLViewGo command. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Banshee</td><td align="right"><font class=tiny>2007</font></td></tr></table></td></tr><tr ><td class="posttext"> I tried but for some reason I couldn't find a way of making it detect the event.  I'm still not very good with BlitzMax though tbh, so i'm probably missing something. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>2007</font></td></tr></table></td></tr><tr ><td class="posttext"> You need to use the HTMLVIEW_NONAVIGATE style to intercept user behavior. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Banshee</td><td align="right"><font class=tiny>2007</font></td></tr></table></td></tr><tr ><td class="posttext"> I did that but when a user clicks a link that would normally open a new window then no event data is generated, as far as I can see.  Actually HTMLVIEW_NONAVIGATE is in the original code snippet above. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
