<!DOCTYPE html><html lang="en" ><head ><title >Using Membase (or Memcached) from Blitz+ (or Blitz3D)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Membase and Memcached interface, language=bb, category=Networking'><meta name='author' content='Mahan'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=4>Networking</a>/Using Membase (or Memcached) from Blitz+ (or Blitz3D)</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2866.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Using Membase (or Memcached) from Blitz+ (or Blitz3D) by Mahan</td><td align="right">2011 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Memcached (http://memcached.org/): Free &amp; open source, high-performance, distributed memory object caching system.<br><br>Membase (http://www.couchbase.org/membase). Similar to Memcached but also provides persistence to disk. Using the "Moxi"-proxy (also on the membase page) the interface is identical to Memcached for the clients.<br><br>Using this interface together with Membase + Moxi (the setup I tested) allows for real time operations (like in a game) from BlitzPlus (Blitz+) and B3D.<br><br>Note that the game Farmville (by Zynga) uses Membase as it's primary storage. </td></tr><tr ><td class="cell"><pre class="code">;---------------------------------------------------------------------
; Basic MemBase/MemCached API for Blitz3D / BlitzPlus
;
; Mattias Hansson (MaHan) - 2011
; 

Type TMembaseConnection
	Field ipaddr$
	Field port%
	Field tcpStream%
	Field anticipatedRecvBytes% ; 0 = we are not waiting for data
	Field tempBank% 			; used when receiving strings
	Field inRequest% 			; Indicates if this connection is currently receiving data from a request
End Type


Function paramNoFromString$(s$, paramNo%, separator$=" ")
	Local currOffset% = 0
	Local paramCount% = 1
	
	Repeat
		Local newOffset = Instr(s, separator, currOffset + 1)
		If newOffset = 0 Then newOffset = Len(s) + 1
		If paramNo = paramCount
			Return Mid(s, currOffset+1, (newOffset - currOffset) -1)
		EndIf
		currOffset = newOffset
		paramCount = paramCount+1
	Until currOffset &gt;= Len(s)
	Return ""
End Function


Function MB_Create.TMembaseConnection(ip$, port% = 11211)
	Local mb.TMembaseConnection = New TMembaseConnection
	mb\ipaddr = ip
	mb\port = port
	mb\tcpStream = OpenTCPStream(mb\ipaddr, mb\port)
	Return mb
End Function

Function MB_Free(mb.TMembaseConnection)
	If mb\tempBank Then FreeBank(mb\tempBank)
	If mb\tcpStream Then CloseTCPStream(mb\tcpStream)
	Delete mb
End Function

Function MB_Set(mb.TMembaseConnection, key$, bankId%)
	MB_internal_clearBuffer(mb)
	WriteLine(mb\tcpStream, "set " + key + " 0 0 " + BankSize(bankId))
	WriteBytes(bankId, mb\tcpStream, 0, BankSize(bankId)-1)
	WriteLine(mb\tcpStream, "")
End Function

Function MB_SetStr(mb.TMembaseConnection, key$, value$)
	MB_internal_clearBuffer(mb)
	WriteLine(mb\tcpStream, "set " + key + " 0 0 " + Len(value))
	WriteLine(mb\tcpStream, value)
End Function

; Keys might be a single key or several separated with whitespace
Function MB_Request(mb.TMembaseConnection, keys$)
	MB_internal_clearBuffer(mb)
	WriteLine(mb\tcpStream, "get " + keys)
	mb\inRequest = True
End Function	

Function MB_Delete(mb.TMembaseConnection, key$)
	MB_internal_clearBuffer(mb)
	WriteLine(mb\tcpStream, "delete " + key)
End Function	

Function MB_Avail(mb.TMembaseConnection)
	Local result = False
	If Not mb\anticipatedRecvBytes Then
		If Not Eof(mb\tcpStream)
			Local answer$=ReadLine(mb\tcpStream)
			
			Select paramNoFromString(answer, 1)
				Case "VALUE"
					mb\anticipatedRecvBytes = Int(paramNoFromString(answer, 4))
					;Print "Set anticipated bytes: " + mb\anticipatedRecvBytes
				Case "END"
					;Print "Ended request"
					mb\inRequest = False
			End Select
			
		EndIf
	EndIf
	;Print "ReadAvail() now: " + ReadAvail(mb\tcpStream)
	Return (mb\anticipatedRecvBytes &gt; 0) And (ReadAvail(mb\tcpStream) &gt;= mb\anticipatedRecvBytes + 2) ; 2 for CRLF
End Function

Function MB_Get(mb.TMembaseConnection, bankId%)
	If mb\anticipatedRecvBytes = 0 Then RuntimeError "MB_Get() - not waiting for answer. Try MB_Requst first?"
	If mb\anticipatedRecvBytes &gt; BankSize(bankId) Then RuntimeError "MB_Get() - trying to stuff to much into passed bank."
	If Not MB_Avail(mb) Then RuntimeError "MB_Get() - Requested value for key is not ready. Try waiting for positive MB_Avail() first?"
	ReadBytes(bankId, mb\tcpStream, 0, mb\anticipatedRecvBytes)
	; remove ending CRLF
	ReadByte(mb\tcpStream)
	ReadByte(mb\tcpStream)
	mb\anticipatedRecvBytes = 0
End Function

Function MB_GetStr$(mb.TMembaseConnection)
	If mb\anticipatedRecvBytes = 0 Then RuntimeError "MB_GetStr$() - not waiting for answer. Try MB_Requst first?"
	If Not mb\tempBank Then mb\tempBank = CreateBank(mb\anticipatedRecvBytes)
	If BankSize(mb\tempBank) &lt; mb\anticipatedRecvBytes Then ResizeBank(mb\tempBank, mb\anticipatedRecvBytes)
	
	Local tempRecvBytes% = mb\anticipatedRecvBytes
	MB_Get(mb, mb\tempBank)
	
	Local i
	Local s$
	For i = 0 To tempRecvBytes-1
		s = s + Chr(PeekByte(mb\tempBank, i))
	Next
	Return s
End Function

Function MB_internal_clearBuffer(mb.TMembaseConnection)
	While ReadAvail(mb\tcpStream)
		ReadLine(mb\tcpStream)
	Wend
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mahan</td><td align="right"><font class=tiny>2011</font></td></tr></table></td></tr><tr ><td class="posttext"> Test program:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Include "membase_intf.bb"


;----------------------------------------------------------
;Reference - Test workbench

;Local memBase%=OpenTCPStream("192.168.0.108", 11211)
;
;WriteLine(memBase, "get test2")
;Delay(50)
;
;While ReadAvail(memBase) &gt; 0
;	Print ReadLine(memBase)
;Wend
;
;
;CloseTCPStream(memBase)
;
;WaitKey

;----------------------------------------------------------


Const TEST_MEMBASE_KEY_PREFIX$ = "TEST_MEMBASE_KEY_PREFIX_"
Const KEYS_TO_WRITE = 1000
Const MEM_SERVER$ = "192.168.0.108"


Function test_ReadValues1(mb.TMembaseConnection)
	Local i
	
	; Read 1000 keys from Membase
	For i = 0 To KEYS_TO_WRITE-1
		Print "Reading " + i
		
		MB_Request(mb, TEST_MEMBASE_KEY_PREFIX + i)
		
		While Not MB_Avail(mb)
			Delay(1)
		Wend
		
		Local get$ = MB_GetStr(mb)
		
		If Not get = Str(i) Then Print "Error got '" + get + "' instead of " + Str(i)
	Next
	
End Function


Function test_ReadValues2(mb.TMembaseConnection)
	Local i
	
	Local keys$
	; Read 1000 keys from Membase
	For i = 0 To KEYS_TO_WRITE-1
		keys = keys + " " + TEST_MEMBASE_KEY_PREFIX + i
	Next
	
	;Print "Reading " + keys
	;WaitKey
	MB_Request(mb, keys)
	
	i = 0
	While mb\inRequest
		If MB_Avail(mb)
			Local get$ = MB_GetStr(mb)
			;Print "Got : " + get
			If Not get = Str(i) Then Print "Error got '" + get + "' instead of " + Str(i)
			i = i + 1
		EndIf
	Wend
	If Not i = KEYS_TO_WRITE Then Print "Error: didn't read all the requested keys. (Note For usage with MemCached this might be ok)"
End Function


Function main()
	Local timer% = MilliSecs()
	
	Print "-----------------------------------------"
	Print "This program will attempt to Write,"
	Print "Read And Delete " + KEYS_TO_WRITE + " key/values"
	Print "against a MemBase Or a MemCached server."
	Print "-----------------------------------------"
	Print ""
	Print "Connecting"
	;WaitKey	
	
	Local mb.TMembaseConnection = MB_Create(MEM_SERVER)
	
	Print "Writing"
	;WaitKey
	
	Local i
	
	; Write 1000 values to MemBase
	For i = 0 To KEYS_TO_WRITE-1
		;Print "writing " + i		
		MB_SetStr(mb, TEST_MEMBASE_KEY_PREFIX + i, Str(i))
	Next
	
	Print "Reading"
	;WaitKey	
	;test_ReadValues1(mb)
	test_ReadValues2(mb)
	
	
	Print "Deleting"
	
	For i = 0 To KEYS_TO_WRITE-1
		MB_Delete(mb, TEST_MEMBASE_KEY_PREFIX + i)
	Next
	
	MB_Free(mb)
	
	Print "Disconnected and Done - sweet."
	Print "All done in: " + Str(MilliSecs()-timer) + "ms." 
	
	WaitKey
End Function

main()

</textarea><br><br>You might want to edit the server constant to match your Membase server.<br><br>Const MEM_SERVER$ = "192.168.0.108"<br><br><br>Note: I've tested this code against Membase + Moxi running in Linux on another machine. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
