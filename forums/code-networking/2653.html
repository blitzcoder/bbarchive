<!DOCTYPE html><html lang="en" ><head ><title >Server and client example</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Illustrates communication between server and multiple clients, language=bb, category=Networking'><meta name='author' content='PowerPC603'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=4>Networking</a>/Server and client example</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2653.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Server and client example by PowerPC603</td><td align="right">2010 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This code is a simple example which demonstrates how to setup a server application and the clients.<br>The server program only needs to run once, you can use multiple clients, which all connect to the server application and communicate with it.<br><br>The server checks for new connections all the time and creates a new player-instance for every new connection.<br>It accepts data from each player, prints it onscreen and sends a response to the player who sent the message.<br><br>The client(s) open a TCP-stream to the server, generate a random playername and sends that to the server as well.<br>Then every second, the client sends a random number as a string to the server and prints the sent number onscreen.<br>The clients also checks for any replies by the server and prints those onscreen as well.<br><br>There is also a built-in check to see if a client disconnects from the server (by closing the client-window).<br>The server will see this and delete the player-instance.<br>The client also checks if the server is still connected and will close when the server is closed.<br><br>You can also add code to process the data to add more functionality, but this example only demonstrates how to setup server-client connection and the sending/receiving of messages, so beginners can use this code as a starting point.<br><br>This code lists the server and client programs, so copy them in 2 separate bb-files and compile them.<br>Run one instance of the server and multiple instances of the client to see what happens. </td></tr><tr ><td class="cell"><pre class="code">;***************************************************************************************************
; Server-code
;***************************************************************************************************

; Set application title
AppTitle "Server"
; Setup window size
Graphics 600, 200, 0, 2



; Setup the player-instance
Type TPlayer
	Field Name$ ; The name of the player
	Field stream ; The stream-handle for this player (all messages sent to / received from this player use this TCP-stream)
End Type



; Define a global variable that declares the port-number on which tcp-streams are sent and received
Global Port = 9876
; Create a TCP-server on the given port
Global Server = CreateTCPServer(Port)


; Check if the server was created successfully or not
If Server &lt;&gt; 0 Then
	; If the server was created successfully, print it on the screen
	Print "Server started successfully: " + Str$(Server)
Else
	; If the server couldn't be created, print it on the screen
	Print "Server failed to start."
	; Add 1 second delay
	Delay 1000
	; And end the program
	End
EndIf



; Main loop
While Not KeyHit(1)
	Local msg$ ; A variable used to read the data sent by a player
	Local stream = AcceptTCPStream(Server) ; Check if a new TCPstream has been found (new player requests a connection with the server)

	; If a new connection has been found
	If stream Then
		; Create a new TPlayer instance
		Player.TPlayer = New TPlayer
		; Read the name of the player from the stream
		Player\Name$ = ReadString$(stream)
		; Save the stream-handle in the TPlayer instance
		Player\stream = stream
		; Print the new playername to the screen
		Print "Found new player: " + Player\Name$
	EndIf



	; Process all players
	For Player.TPlayer = Each TPlayer
		; If data was received from this player
		If ReadAvail(Player\stream) Then
			; Read the integer value that was sent from the player's client to the server
			msg$ = ReadString$(Player\stream)

			; Print the message onscreen that was sent by the player
			Print "Message " + Chr$(34) + msg$ + Chr$(34) + " sent by " + Player\Name$

			; Return a message to the player (includes the message that was sent to the server and the playername)
			WriteString Player\stream, "Message " + Chr$(34) + msg$ + Chr$(34) + " received, " + Player\Name$
		EndIf

		; Check if the client disconnected
		Select Eof(Player\stream)
			Case 1 ; Player disconnected nicely
				; Print a message in the server-window to indicate which player disconnected
				Print "Player " + Player\Name$ + " closed"
				; Delete the TPlayer instance (the server would otherwise continue to send messages to this player)
				Delete Player
			Case -1 ; Connection lost
				; Print a message in the server-window to indicate which player disconnected
				Print "Player " + Player\Name$ + " aborted unexpectedly"
				; Delete the TPlayer instance (the server would otherwise continue to send messages to this player)
				Delete Player
		End Select
	Next

	; Wait 2ms (don't let the server-program use up all processor-power)
	Delay 2
Wend

; End the server
End





;***************************************************************************************************
; Client-code
;***************************************************************************************************

; Setup window size
Graphics 600, 200, 0, 2



; Declare port and IP-adress of the server
Global Port = 9876
Global IP$ = "127.0.0.1"
; Setup a global variable that holds the TCP-stream handle (used to talk to the server)
Global stream

; A global timer to time some events (used to send a message every second)
Global Timer = MilliSecs()

; Seed the random number generator
SeedRnd MilliSecs()

; Create an array with some playernames
Dim ANames$(10)
ANames$(1) = "Suzy"
ANames$(2) = "Tom"
ANames$(3) = "Adrian"
ANames$(4) = "Melissa"
ANames$(5) = "Ronaldo"
ANames$(6) = "Richard"
ANames$(7) = "Christine"
ANames$(8) = "Jackie"
ANames$(9) = "Arnold"
ANames$(10) = "Kevin"



; Try to connect to server
While stream = 0
	; Wait 1 second
	Delay 1000

	; Use a counter to count the number of tries
	Counter = Counter + 1

	; Try to open a TCP-stream to the server on the given port and IP-address
	stream = OpenTCPStream(IP$, Port)
	; Let the user know that the client is trying to connect to the server
	Print "Connecting to server..."
	Print "on IP$: " + IP$ + ", using port " + Str$(Port)

	; If the counter reached 5, then the client tried 5 times to connect to the server, but wasn't able to connect -&gt; generate an error
	If Counter = 5 Then RuntimeError "Cannot find server on IP: " + IP$
Wend

; Connection established
Print ""
Print "Successfully connected to server..."
Print ""



; Use a random playername for this client
Global Name$ = ANames$(Rand(1, 10))
; Send the playername to the server
WriteString stream, Name$
; Set application title
AppTitle "Client for: " + Name$



; Main loop
While Not KeyHit(1)
	Local msg$ ; Variable used to read messages sent by the server

	; Send a random number (as a string) to the server every second
	If MilliSecs() &gt; (Timer + 1000) Then
		Timer = Timer + 1000

		; Generate a random number for sending to the server and convert it to a string
		msg$ = Str$(Rand(0, 1000))
		; Print the number to the screen for debugging
		Print msg$
		; Send the random number as a string to the server
		WriteString stream, msg$
	EndIf

	; Check if the server has sent a response (or some other message)
	If ReadAvail(stream) Then
		; Read the server's message into a variable
		msg$ = ReadString$(stream)
		; Print the server's response to the screen
		Print msg$
	EndIf

	; Check if the connection hasn't been lost
	Select Eof(stream)
		Case 1 ; Server has been closed
			; Print it to the screen
			Print "Server has closed"
			; Wait 1 second
			Delay 1000
			; End the client program
			End
		Case -1 ; Connection lost
			; Print it to the screen
			Print "Server has aborted unexpectedly"
			; Wait 1 second
			Delay 1000
			; End the client program
			End
	End Select

	; Wait 10ms
	Delay 10
Wend

; End the client program
End</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
