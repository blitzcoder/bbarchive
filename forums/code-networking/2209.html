<!DOCTYPE html><html lang="en" ><head ><title >BlitzPing/1.0</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Uses the pingback protocol to ping all links on a web page., language=bmx, category=Networking'><meta name='author' content='Otus'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=4>Networking</a>/BlitzPing/1.0</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2209.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzPing/1.0 by Otus</td><td align="right">2008 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Finds all external links on a web page and tries to ping those using the pingback protocol <a href="http://www.hixie.ch/specs/pingback/pingback-1.0" target="_blank">http://www.hixie.ch/specs/pingback/pingback-1.0</a><br><br>You don't need a fancy blogging platform to engage in conversation - you can ping even links on a static HTML website. Also recommended to make up for Blogger's poor trackback support.<br><br>Note: Does *not* currently check for robots exclusion protocols. Probably should. </td></tr><tr ><td class="cell"><pre class="code">'BlitzPing/1.0

'GETs a document, and pings the external links found.
'Uses the pingback protocol http://www.hixie.ch/specs/pingback/pingback-1.0

SuperStrict

Framework BRL.Blitz
Import BRL.Socketstream
Import BRL.StandardIO

Type TPingSource
	Field URI:String
	Field host:String
	Field doc:String
	Field data:String
	
	Const agent:String = "BlitzPing/1.0"
	
	Function Open:TPingSource(URI:String)
	'Opens a socket to the URI specified, and reads the content to data.
	'Closes the socket and returns the created object.
		Local s:TPingSource = New TPingSource
		s.URI=URI
		s.host = URI[URI.Find("//")+2..URI.Find("/",7)]
		s.doc = URI[URI.Find("/",7)+1..]
		Local sock:TSocket = CreateTCPSocket()
		If Not ConnectSocket(sock,HostIp(s.host),80) Return Null
		Local stream:TStream = CreateSocketStream(sock)
		WriteLine(stream,..
			"GET /"+s.doc+" HTTP/1.0~n"+..
			"Host: "+s.host+"~n"+..
			"User-Agent: "+agent+"~n~n")
		While Not Eof(stream)
			s.data:+ReadLine(stream)+"~n"
		Wend
		CloseStream(stream)
		If sock CloseSocket(sock)
		Return s
	End Function
	
	Method Process:Int()
	'Reads the data. Creates a ping target from any anchors it finds.
	'Pings valid targets, and returns the number of successful pings.
		Local pos:Int = 0, tag:String, target:String, t:TPingTarget, pings:Int
		While data.Find("&lt;a ",pos)&gt;-1
			pos = data.Find("&lt;a ",pos)+1
			tag = data[pos..data.Find("&gt;",pos)]
			If tag.Find("href=")&gt;-1
				target = tag[tag.Find("href=")+6..]
				If target.Find(" ")&gt;-1
					target = target[..target.Find(" ")-1]
				Else
					target = target[..target.length-1]
				End If
				Print target
				t = TPingTarget.Open(target,host)
				If t 
					'Print "Found: "+t.URI
					If t.Process()
						'Print "Pinging: "+t.server
						pings:+t.Ping(URI)
					End If
				Else
					'Print "Skipping: "+pos+"/"+data.length
				End If
			End If
		Wend
		Return pings
	End Method
End Type

Type TPingTarget
	Field URI:String
	Field host:String
	Field doc:String
	Field data:String
	Field server:String
	
	Const agent:String = "BlitzPing/1.0"
	
	Function Open:TPingTarget(URI:String,host:String)
	'Opens a socket to the URI specified, and reads the content to data.
	'Closes the socket and returns the created object.
		Local t:TPingTarget = New TPingTarget
		t.URI=URI
		t.host = URI[URI.Find("//")+2..URI.Find("/",7)]
		If t.host=host Return Null	'Don't ping the same host
		t.doc = URI[URI.Find("/",7)+1..]
		Local sock:TSocket = CreateTCPSocket()
		If Not ConnectSocket(sock,HostIp(t.host),80) Return Null
		Local stream:TStream = CreateSocketStream(sock)
		WriteLine(stream,..
			"GET /"+t.doc+" HTTP/1.0~n"+..
			"Host: "+t.host+"~n"+..
			"User-Agent: "+agent+"~n~n")
		While Not Eof(stream)
			t.data:+ReadLine(stream)+"~n"
		Wend
		CloseStream(stream)
		If sock CloseSocket(sock)
		Return t
	End Function
	
	Method Process:Int()
	'Finds the ping server from data, and finds out host and doc. 
	'Returns True is ping server found, False if not.
		Local pos:Int = data.Find("X-Pingback:")
		If pos=-1
			'No pingback HTTP header - search for head link
			pos=0
			While data.ToLower().Find("&lt;link",pos) &gt; -1
				pos=data.ToLower().Find("&lt;link",pos)
				Local area:String = data[pos..data.Find("&gt;",pos)]
				If area.Find("pingback")&gt;-1
					server = area[area.Find("href=")+6..area.Find(" ",area.Find("href="))-1]
				End If
			Wend
		Else
			server = data[pos+11..data.Find("~n",pos)].Trim()
		End If
		If Not server Return False	'Server not found - not pingable
		host = server[server.Find("//")+2..server.Find("/",7)]
		doc = server[server.Find("/",7)+1..]
		Return True
	End Method
	
	Method Ping:Int(source:String)
	'Sends an XML/RPC ping to the ping server with source as source and URI as target.
	'Returns True on a successful ping, False on a fail.
		Local sock:TSocket = CreateTCPSocket()
		If Not ConnectSocket(sock,HostIp(host),80) Return Null
		Local stream:TStream = CreateSocketStream(sock)
		If Not stream Return Null
		Local request:String
		request = ..
			"&lt;?xml version=~q1.0~q?&gt;~n"+..
			"&lt;methodCall&gt;~n"+..
			"&lt;methodName&gt;pingback.ping&lt;/methodName&gt;~n"+..
			"&lt;params&gt;~n"+..
			"&lt;param&gt;&lt;value&gt;&lt;string&gt;"+source+"&lt;/string&gt;&lt;/value&gt;&lt;/param&gt;~n"+..
			"&lt;param&gt;&lt;value&gt;&lt;string&gt;"+URI+"&lt;/string&gt;&lt;/value&gt;&lt;/param&gt;~n"+..
			"&lt;/params&gt;~n"+..
			"&lt;/methodCall&gt;"
		request = "POST /"+doc+" HTTP/1.0~n"+..
			"Host: "+host+"~n"+..
			"User-Agent: "+agent+"~n"+..
			"Content-Type: text/xml~n"+..
			"Content-Length: "+request.length+"~n~n"+..
			request
		'Print "Sending request..."
		'Print request
		WriteLine(stream, request)
		'Print "Receiving response..."
		Rem
		While Not Eof(stream)
			Print ReadLine(stream)
		Wend
		End Rem
		Return True
	End Method
End Type


Local source:String = Input("URL for ping source:")
'Open the ping source
Local s:TPingSource = TPingSource.Open(source)
If s
	Print "Open Successful"
	'Process the source and ping the links
	Print s.Process()+" links pinged"
Else
	Print "Open failed!"
End If</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
