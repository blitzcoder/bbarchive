<!DOCTYPE html><html lang="en" ><head ><title >GNetServerList</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Use the GNet server listing service with BlitzMax, language=bmx, category=Networking'><meta name='author' content='Oddball'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=4>Networking</a>/GNetServerList</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2818.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >GNetServerList by Oddball</td><td align="right">2011 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> I know there are a few of these already, but I wasn't really happy with the way they worked, so I wrote my own.<br><br>Obviously it's based on Mark's original code.<br><br>For more information and to get the php script see &gt;<a href="/toolbox/toolbox.php?tool=61" target="_blank">here</a>&lt; </td></tr><tr ><td class="cell"><pre class="code">'GNet Server Listing Module
SuperStrict

Rem
bbdoc: Networking/GNetServerList
End Rem
Module ODD.GNetServerList

ModuleInfo "Version: 1.0"
ModuleInfo "Author: David Williamson, Mark Sibly"
ModuleInfo "License: Public Domain"

ModuleInfo "History: 1.0"
ModuleInfo "       : Initial release."

Import BRL.SocketStream
Import BRL.LinkedList

Rem
bbdoc: GNet server data
End Rem
Type TGNetServer
	Rem
	bbdoc:  The game's name.
	End Rem
	Field game:String
	Rem
	bbdoc:  The server's name/descriptor
	End Rem
	Field server:String
	Rem
	bbdoc:  The server's global IP address
	End Rem
	Field ip:String
End Type

Rem
bbdoc: Initializes GNetServerList
about: If GNetServerList is not initialized then the online server list at http://www.blitzbasic.com/gnet/gnet.php is used.
End Rem
Function InitGNetServerList( host:String, get:String, port:Int=80 )
	_host=host
	_get=get
	_port=port
End Function

Rem
bbdoc: Pings the online server list
returns: The global IP of the computer
End Rem
Function GNetPing:String()
	Local stream:TSocketStream=Open("ping")
	If Not stream Then Return False
	
	Local ip:String=ReadLine(stream)
	
	CloseStream stream
	Return ip
End Function

Rem
bbdoc: Adds a server to the online list
returns: True if successful
about: @Game is a string describing your game; eg. "Pong", "CallOfDuty", etc.

@Server is an optional string which can be used to describe the server; eg. "UK_Server", "3v3Deathmatch", etc.

@Game and @server should be kept short, and use only alphanumeric characters.
End Rem
Function AddGNetServer:Int( game:String, server:String="" )
	Return Exec("add",game,server)
End Function

Rem
bbdoc: Refreshes the server
returns: True if successful
about: Servers are automatically removed after 5 minutes of inactivity.
Use @RefreshGNetServer to keep your server 'alive'.
If @server is defined then the lsiting is updated with the new server name.
End Rem
Function RefreshGNetServer:Int( game:String, server:String="" )
	Return Exec("ref",game,server)
End Function

Rem
bbdoc: Removes the server from the online list
returns: True if successful
about: You should always remove servers at game exit, or before starting a new server session.
End Rem
Function RemoveGNetServer:Int( game:String )
	Return Exec("rem",game,"")
End Function

Rem
bbdoc: Gets the online server list
returns: A TList containing the TGNetServer objects
about: Searches for servers for the specified game, or all games if @game is omitted.

The results of the search are returned as a list of TGNetServer objects.

Use @For .. @EachIn to examine the server list.
End Rem
Function ListGNetServers:TList( game:String="" )
	Local list:TList=New TList
	
	Local stream:TSocketStream=Open("list")
	If Not stream Then Return Null
	
	Local t_game:String
	Local t_server:String
	Local t_ip:String
	
	Repeat
		t_game=ReadLine(stream)
		If t_game[..2]&lt;&gt;"&lt;b" And t_game&lt;&gt;""
			t_server=ReadLine(stream)
			t_ip=ReadLine(stream)
			
			If game="" Or Esc(game)=t_game
				Local p:TGNetServer=New TGNetServer
				
				p.game=t_game
				p.server=t_server
				p.ip=t_ip
				
				list.AddFirst p
			EndIf
		EndIf
	Until Eof(stream)
	
	CloseStream stream
	Return list
End Function

Private

Global _host:String="www.blitzbasic.com"
Global _get:String="/gnet/gnet.php"
Global _port:Int=80

Function Esc:String( t:String )
	t=t.Replace("&amp;","")
	t=t.Replace("%","")
	t=t.Replace("'","")
	t=t.Replace(Chr(34),"")
	t=t.Replace(" ","_")
	Return t
End Function

Function Open:TSocketStream( opt:String )
	Local socket:TSocket=CreateTCPSocket()
	ConnectSocket socket,HostIp(_host),_port
	Local stream:TSocketStream=CreateSocketStream(socket,True)
	
	WriteLine stream,"GET "+_get+"?opt="+opt+" HTTP/1.0"
	WriteLine stream,"HOST: "+_host
	WriteLine stream,""
	FlushStream stream
	
	While ReadLine(stream)&lt;&gt;""
	Wend
	
	Return stream
End Function

Function Exec:Int( opt:String, game:String, server:String )
	opt=opt+"&amp;game="+Esc(game)
	
	If server&lt;&gt;"" Then opt=opt+"&amp;server="+Esc(server)
	
	Local stream:TSocketStream=Open(opt)
	If Not stream Then Return False
	
	Local ok:Int=False
	If ReadLine(stream)="OK" Then ok=True
	
	CloseStream stream
	Return ok
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>2014</font></td></tr></table></td></tr><tr ><td class="posttext"> Here is some test code which is based on the b3d gnet_v1 demo. This code has a working ping. It is similar to boomboommax's "gnet conversion" code but is non-oop/procedural.<br><pre class=code>
'	GNetServerList test

SuperStrict

'Import ODD.GNetServerList
Import "GNetServerList.bmx"

Local game$="MyGameName",server$="MyServerName"

Local p$=GNetPing()
Print "~nPing:"+p

Local list:TList=ListGNetServers()
PrintServers( list )

Print "~nAdding server..."
AddGNetServer( game$ )

list=ListGNetServers()
PrintServers( list )

Input "~nHit return to refresh server..."

RefreshGNetServer( game$,server$ )

list=ListGNetServers( game$ ) 'list just my game
PrintServers( list )

Input "~nHit return to remove server and exit..."

RemoveGNetServer( game$ )

list=ListGNetServers()
PrintServers( list )

Function PrintServers( list:TList )
	Print ""
	For Local item:TGNetServer=EachIn list
		Print "Game:"+item.Game+" Server:"+item.Server+" IP:"+item.IP
	Next
EndFunction
</pre> <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
