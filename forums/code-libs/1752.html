<!DOCTYPE html><html lang="en" ><head ><title >Capture Screen functions</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Uses User32.dll and Gdi32.dll decls, language=bb, category=User Libs'><meta name='author' content='markcw'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=14>User Libs</a>/Capture Screen functions</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1752.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Capture Screen functions by markcw</td><td align="right">2006 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> I was looking at how to use the clipboard in blitz3d (using the win32 api) and found some PB code to capture the screen and send it to the clipboard.<br><br>edit: found some more code on the PB forum that extended this function, it used GetSystemMetrics (for the desktop size) which was better than GetDeviceCaps.<br><br>You need to have a file named "User32.decls" in your "userlibs" folder with these declared.<br><pre class=code>
.lib "User32.dll"

; Window Functions
Api_GetClientRect%(hWnd,lpRect*):"GetClientRect"
Api_GetDesktopWindow%():"GetDesktopWindow"
Api_GetForegroundWindow%():"GetForegroundWindow"
Api_GetWindowRect%(hWnd,lpRect*):"GetWindowRect"
; Clipboard Functions
Api_CloseClipboard%():"CloseClipboard"
Api_EmptyClipboard%():"EmptyClipboard"
Api_GetClipboardData%(uFormat):"GetClipboardData"
Api_IsClipboardFormatAvailable%(format):"IsClipboardFormatAvailable"
Api_OpenClipboard%(hwnd):"OpenClipboard"
Api_SetClipboardData%(uFormat,hData):"SetClipboardData"
; Coordinate Space and Transformation Functions
Api_ClientToScreen%(hWnd,lpPoint*):"ClientToScreen"
; Device Context Functions
Api_GetDC%(hWnd):"GetDC"
Api_ReleaseDC%(hwnd,hdc):"ReleaseDC"
; Accessibility Functions
Api_GetSystemMetrics%(nIndex):"GetSystemMetrics"
</pre><br>And a file named "Gdi32.decls" in the "userlibs" folder with these declared.<br><pre class=code>
.lib "Gdi32.dll"

; Bitmap Functions
Api_BitBlt%(hdcDest,nXDest,nYDest,nWidth,nHeight,hdcSrc,nXSrc,nYSrc,dwRop):"BitBlt"
Api_CreateCompatibleBitmap%(hdc,nWidth,nHeight):"CreateCompatibleBitmap"
Api_GetPixel%(hdc,nXPos,nYPos):"GetPixel"
Api_SetPixel%(hdc,X,Y,crColor):"SetPixel"
; Device Context Functions
Api_CreateCompatibleDC%(hdc):"CreateCompatibleDC"
Api_CreateDC%(lpszDriver$,lpszDevice$,lpszOutput$,lpInitData*):"CreateDCA"
Api_DeleteDC%(hdc):"DeleteDC"
Api_DeleteObject%(hObject):"DeleteObject"
Api_GetDeviceCaps%(hdc,nIndex):"GetDeviceCaps"
Api_GetObject%(hgdiobj,cbBuffer,lpvObject*):"GetObjectA"
Api_SelectObject%(hdc,hgdiobj):"SelectObject"
</pre><br>Or you can use the <a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=1179" target="_blank">user32.dll decls</a> and <a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=1181" target="_blank">gdi32.dll decls</a>.<br><br>The CaptureScreenArea function. </td></tr><tr ><td class="cell"><pre class="code">;Capture Screen Area function, by markcw on 14 Jul 06

Graphics3D 320,240,0,2
SetBuffer BackBuffer()

initime=MilliSecs()

While Not KeyHit(1)

 If done=0 And MilliSecs()&gt;initime+50 ;wait for text info
  done=1 : CaptureScreenArea(0,0,320,240) ;x,y,width,height
 EndIf

 Cls
 Text 0,0,"OK, paste the current clipboard data"
 Text 0,12,"to whatever program you use to see"
 Text 0,24,"the results of the screen capture."
 Text 0,36,"done="+done+" initime="+initime

Flip
Wend

Function CaptureScreenArea(sx,sy,swidth,sheight)
 ;Capture a given screen area to the clipboard
 ;From PureBasic CodeArchiv source, by wayne1 on 30/1/2002

 Local hdcSrc,hdcDst,hbmp,hold

 hdcSrc=Api_GetDC(0) ;hwnd
 hdcDst=Api_CreateCompatibleDC(hdcSrc) ;hdc
 hbmp=Api_CreateCompatibleBitmap(hdcSrc,swidth,sheight) ;hdc,width,height
 hold=Api_SelectObject(hdcDst,hbmp) ;hdc,hobject[bitmap]
 Api_BitBlt(hdcDst,0,0,swidth,sheight,hdcSrc,sx,sy,$00CC0020) ;SrcCopy
 Api_SelectObject(hdcDst,hold) ;restore old to dc
 If Api_OpenClipboard(0) ;hwnd
  Api_EmptyClipboard() ;free last data
  Api_SetClipboardData(2,hbmp) ;CF_Bitmap,hdata[bitmap]
  Api_CloseClipboard()
 EndIf
 Api_ReleaseDC(0,hdcSrc) ;hwnd,hdc
 Api_DeleteDC(hdcDst) ;hdc

End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> A CaptureDesktop function, which uses the CaptureScreenArea function.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Capture Desktop function, by markcw on 16 Jul 06

Graphics3D 320,240,0,2
SetBuffer BackBuffer()

initime=MilliSecs()

While Not KeyHit(1)

 If done=0 And MilliSecs()&gt;initime+50 ;wait for text info
  done=1 : CaptureDesktop()
 EndIf

 Cls
 Text 0,0,"OK, paste the current clipboard data"
 Text 0,12,"to whatever program you use to see"
 Text 0,24,"the results of the screen capture."
 Text 0,36,"done="+done+" initime="+initime

Flip
Wend

Function CaptureDesktop()
 ;Capture the entire desktop area to the clipboard
 ;From PureBasic Forum source, by Droopy on 18/10/2005

 Local dwidth,dheight

 dwidth=Api_GetSystemMetrics(0) ;SM_CXScreen
 dheight=Api_GetSystemMetrics(1) ;SM_CYScreen
 CaptureScreenArea(0,0,dwidth,dheight) ;x,y,width,height

End Function

Function CaptureScreenArea(sx,sy,swidth,sheight)
 ;Capture a given screen area to the clipboard
 ;From PureBasic CodeArchiv source, by wayne1 on 30/1/2002

 Local hdcSrc,hdcDst,hbmp,hold

 hdcSrc=Api_GetDC(0) ;hwnd
 hdcDst=Api_CreateCompatibleDC(hdcSrc) ;hdc
 hbmp=Api_CreateCompatibleBitmap(hdcSrc,swidth,sheight) ;hdc,width,height
 hold=Api_SelectObject(hdcDst,hbmp) ;hdc,hobject[bitmap]
 Api_BitBlt(hdcDst,0,0,swidth,sheight,hdcSrc,sx,sy,$00CC0020) ;SrcCopy
 Api_SelectObject(hdcDst,hold) ;restore old to dc
 If Api_OpenClipboard(0) ;hwnd
  Api_EmptyClipboard() ;free last data
  Api_SetClipboardData(2,hbmp) ;CF_Bitmap,hdata[bitmap]
  Api_CloseClipboard()
 EndIf
 Api_ReleaseDC(0,hdcSrc) ;hwnd,hdc
 Api_DeleteDC(hdcDst) ;hdc

End Function
</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> A CaptureWindow function, which also uses the CaptureScreenArea function.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Capture Window function, by markcw on 15 Jul 06

Graphics3D 320,240,0,2
SetBuffer BackBuffer()

initime=MilliSecs()

While Not KeyHit(1)

 If done=0 And MilliSecs()&gt;initime+50 ;wait for text info
  done=1 : CaptureWindow()
 EndIf

 Cls
 Text 0,0,"OK, paste the current clipboard data"
 Text 0,12,"to whatever program you use to see"
 Text 0,24,"the results of the screen capture."
 Text 0,36,"done="+done+" initime="+initime

Flip
Wend

Function CaptureWindow()
 ;Capture the entire window area to the clipboard
 ;From PureBasic Forum source, by Droopy on 18/10/2005

 Local hwnd,prect,rx,ry,rwidth,rheight

 hwnd=Api_GetForegroundWindow()
 If hwnd ;window valid
  prect=CreateBank(16) ;bank structure
  Api_GetWindowRect(hwnd,prect) ;hwnd,rect[left,top,right,bottom]
  rx=PeekInt(prect,0) ;rect left
  ry=PeekInt(prect,4) ;rect top
  rwidth=PeekInt(prect,8)-PeekInt(prect,0) ;rect right-left
  rheight=PeekInt(prect,12)-PeekInt(prect,4) ;rect bottom-top
  CaptureScreenArea(rx,ry,rwidth,rheight) ;x,y,width,height
  FreeBank prect ;free from memory
 EndIf

End Function

Function CaptureScreenArea(sx,sy,swidth,sheight)
 ;Capture a given screen area to the clipboard
 ;From PureBasic CodeArchiv source, by wayne1 on 30/1/2002

 Local hdcSrc,hdcDst,hbmp,hold

 hdcSrc=Api_GetDC(0) ;hwnd
 hdcDst=Api_CreateCompatibleDC(hdcSrc) ;hdc
 hbmp=Api_CreateCompatibleBitmap(hdcSrc,swidth,sheight) ;hdc,width,height
 hold=Api_SelectObject(hdcDst,hbmp) ;hdc,hobject[bitmap]
 Api_BitBlt(hdcDst,0,0,swidth,sheight,hdcSrc,sx,sy,$00CC0020) ;SrcCopy
 Api_SelectObject(hdcDst,hold) ;restore old to dc
 If Api_OpenClipboard(0) ;hwnd
  Api_EmptyClipboard() ;free last data
  Api_SetClipboardData(2,hbmp) ;CF_Bitmap,hdata[bitmap]
  Api_CloseClipboard()
 EndIf
 Api_ReleaseDC(0,hdcSrc) ;hwnd,hdc
 Api_DeleteDC(hdcDst) ;hdc

End Function
</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> A CaptureClient function, again using the CaptureScreenArea function.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Capture Client function, by markcw on 16 Jul 06

Graphics3D 320,240,0,2
SetBuffer BackBuffer()

initime=MilliSecs()

While Not KeyHit(1)

 If done=0 And MilliSecs()&gt;initime+50 ;wait for text info
  done=1 : CaptureClient()
 EndIf

 Cls
 Text 0,0,"OK, paste the current clipboard data"
 Text 0,12,"to whatever program you use to see"
 Text 0,24,"the results of the screen capture."
 Text 0,36,"done="+done+" initime="+initime

Flip
Wend

Function CaptureClient()
 ;Capture the entire client area to the clipboard
 ;From PureBasic CodeArchiv source, by Edwin Knoppert on 24/11/2003

 Local hwnd,prect,rx,ry,rwidth,rheight

 hwnd=Api_GetForegroundWindow()
 If hwnd ;window valid
  prect=CreateBank(16) ;bank structure
  Api_GetClientRect(hwnd,prect) ;hwnd,rect[left,top,right,bottom]
  Api_ClientToScreen(hwnd,prect) ;hwnd,point[x,y]
  rx=PeekInt(prect,0) ;point x
  ry=PeekInt(prect,4) ;point y
  rwidth=PeekInt(prect,8) ;rect right
  rheight=PeekInt(prect,12) ;rect bottom
  CaptureScreenArea(rx,ry,rwidth,rheight) ;x,y,width,height
  FreeBank prect ;free from memory
 EndIf

End Function

Function CaptureScreenArea(sx,sy,swidth,sheight)
 ;Capture a given screen area to the clipboard
 ;From PureBasic CodeArchiv source, by wayne1 on 30/1/2002

 Local hdcSrc,hdcDst,hbmp,hold

 hdcSrc=Api_GetDC(0) ;hwnd
 hdcDst=Api_CreateCompatibleDC(hdcSrc) ;hdc
 hbmp=Api_CreateCompatibleBitmap(hdcSrc,swidth,sheight) ;hdc,width,height
 hold=Api_SelectObject(hdcDst,hbmp) ;hdc,hobject[bitmap]
 Api_BitBlt(hdcDst,0,0,swidth,sheight,hdcSrc,sx,sy,$00CC0020) ;SrcCopy
 Api_SelectObject(hdcDst,hold) ;restore old to dc
 If Api_OpenClipboard(0) ;hwnd
  Api_EmptyClipboard() ;free last data
  Api_SetClipboardData(2,hbmp) ;CF_Bitmap,hdata[bitmap]
  Api_CloseClipboard()
 EndIf
 Api_ReleaseDC(0,hdcSrc) ;hwnd,hdc
 Api_DeleteDC(hdcDst) ;hdc

End Function
</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> A CaptureClientArea function, like the CaptureClient function except lets you capture a given area, ie. an image.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Capture Client Area function, by markcw on 16 Jul 06

Graphics3D 320,240,0,2
SetBuffer BackBuffer()

image=CreateImage(128,128) ;new image
For y=0 To ImageHeight(image)-1
 For x=0 To ImageWidth(image)-1
  rgba=y+(y*256)+(x*256^2) ;gradient color
  WritePixel x,y,rgba,ImageBuffer(image)
 Next
Next

initime=MilliSecs()

While Not KeyHit(1)

 If done=0 And MilliSecs()&gt;initime+50 ;wait for text info
  done=1 : CaptureClientArea(64,64,ImageWidth(image),ImageHeight(image))
 EndIf

 Cls
 DrawImage image,64,64 ;draw the image

 Text 0,0,"OK, paste the current clipboard data"
 Text 0,12,"to whatever program you use to see"
 Text 0,24,"the results of the screen capture."
 Text 0,36,"done="+done+" initime="+initime

Flip
Wend

Function CaptureClientArea(cx,cy,cwidth,cheight)
 ;Capture a given client area to the clipboard
 ;From PureBasic CodeArchiv source, by Edwin Knoppert on 24/11/2003

 Local hwnd,ppoint,px,py

 hwnd=Api_GetForegroundWindow()
 If hwnd ;window valid
  ppoint=CreateBank(8) ;bank structure
  Api_ClientToScreen(hwnd,ppoint) ;hwnd,point[x,y]
  px=PeekInt(ppoint,0) ;point x
  py=PeekInt(ppoint,4) ;point y
  CaptureScreenArea(px+cx,py+cy,cwidth,cheight) ;x,y,width,height
  FreeBank ppoint ;free from memory
 EndIf

End Function

Function CaptureScreenArea(sx,sy,swidth,sheight)
 ;Capture a given screen area to the clipboard
 ;From PureBasic CodeArchiv source, by wayne1 on 30/1/2002

 Local hdcSrc,hdcDst,hbmp,hold

 hdcSrc=Api_GetDC(0) ;hwnd
 hdcDst=Api_CreateCompatibleDC(hdcSrc) ;hdc
 hbmp=Api_CreateCompatibleBitmap(hdcSrc,swidth,sheight) ;hdc,width,height
 hold=Api_SelectObject(hdcDst,hbmp) ;hdc,hobject[bitmap]
 Api_BitBlt(hdcDst,0,0,swidth,sheight,hdcSrc,sx,sy,$00CC0020) ;SrcCopy
 Api_SelectObject(hdcDst,hold) ;restore old to dc
 If Api_OpenClipboard(0) ;hwnd
  Api_EmptyClipboard() ;free last data
  Api_SetClipboardData(2,hbmp) ;CF_Bitmap,hdata[bitmap]
  Api_CloseClipboard()
 EndIf
 Api_ReleaseDC(0,hdcSrc) ;hwnd,hdc
 Api_DeleteDC(hdcDst) ;hdc

End Function
</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Wayne</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> Right on Muk, good stuff ! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Wayne. I am going to see if i can copy a FreeImage bitmap directly now. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> just updated these examples to use the "Api_" prefix so there are no functions of the same name and so you can tell an api function straight away.<br><br>Also, i changed to using Api_GetDC instead of Api_CreateDC because it does the same thing and is simpler to use. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Wayne</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> I would of thought create would be functionaly differet from get. hmm <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> well it's instead of creating a dc you just get an existing one, when you call getdc(0) it gets the display/desktop window, both can be used afaik. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> ok, i updated this code again. <br><br>it doesn't really make much difference, but apparently you're supposed to put/reselect a dc's original dummy object back before deleting the dc.<br><br>also, there was no need to delete the bitmap object as the clipboard uses it, i thought it made a copy but it just becomes the new owner. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >schilcote</td><td align="right"><font class=tiny>2008</font></td></tr></table></td></tr><tr ><td class="posttext"> Umm... CaptureDesktop doesn't work for me. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>2008</font></td></tr></table></td></tr><tr ><td class="posttext"> It might be because the CaptureWindow/Client/ClientArea functions use Api_GetForegroundWindow to get the window handle which is not as reliable as using Api_FindWindow as there may be another foreground/active window. In order to use Api_FindWindow you need the define the Blitz app class (different in B+) and title, like so. Edit: SystemProperty("AppHWND") does the same thing as FindWindow.<br><pre class=code>.lib "User32.dll"
Api_FindWindow%(classname$, windowname$):"FindWindowA"</pre><pre class=code>; Const class$="GX_WIN32_CLASS" ;BlitzPlus 1.11
; Const class$="BLITZMAX_WINDOW_CLASS" ;BlitzPlus 1.34
Const class$="Blitz Runtime Class" ;Blitz3D, app class
Const title$ = "My Blitz Window" ;app title

AppTitle title$
hwnd = Api_FindWindow(class$, title$)
</pre><br>As for CaptureDesktop not working I can only assume that Api_GetDC(0) isn't working for some reason. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
