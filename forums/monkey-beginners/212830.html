<!DOCTYPE html><html lang="en" ><head ><title >I can't seem to flip images with DrawImageRect</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >I can't seem to flip images with DrawImageRect</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=528" >Monkey Beginners</a>/<a href="#bottom" >I can't seem to flip images with DrawImageRect</a><br><br>
<a name="2126292"></a>

<a name="2126293"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ponpoko</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I can't seem to flip images with DrawImageRect (or any other draw command for that matter). If I put a negative scale factor the image just doesn't draw. I really don't want to double my number of images in order to manually draw the mirrored image if I can just flip them. I'm not sure what I am doing wrong?<br><br>This works fine:<br><br>DrawImageRect(test_image,0,0,1,1,24,120,0,4,4)<br><br>My image is drawn scaled by 4 in both x and y<br><br>This doesn't:<br><br>DrawImageRect(test_image,0,0,1,1,24,120,0,-4,4)<br><br>My expectation would be that the image would be scaled by 4 in both x and y and that x would be flipped left to right. BTW the 4x scale makes no difference. If I scale by 1 I still have the same issue.<br><br>Any ideas? <br><br></td></tr></table><br>
<a name="2126298"></a>

<a name="2126299"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dawlane</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> You will need to do a little bit of matrix manipulation to do a mirror.<br>It would go some thing like this:<br>PushMatrix<br>Translate x,y ' position the drawing point<br>Scale x,y ' set scaling here<br>Translate -x,-y ' reset the drawing point<br>DrawImageRect(image,x,y,w,h)<br>PopMatrix<br><br>See the documentation on PushMatrix, Translate, Scale, PopMatrix.<br>Do a search for these of the forum. I'm sure some has some example code around. <br><br></td></tr></table><br>
<a name="2126401"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ponpoko</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks dawlane... I have been playing with this but I can't seem to make even the simplest texture mirror left to right. Could you possibly write up a short working example? I wanted to set this up as a function call or something from my main rendering loop so I could just sub in a MirrorRect (or whatever I call it) command when I need to. I am just too new to Monkey to understand the matrix stuff and there weren't any examples I could find. <br><br></td></tr></table><br>
<a name="2126408"></a>

<a name="2126409"></a>

<a name="2126410"></a>

<a name="2126411"></a>

<a name="2126412"></a>

<a name="2126414"></a>

<a name="2126415"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Xaron</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Actually it works for me, using negative scale, always did it that way:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict
Import mojo

Function Main:Int()
	New Game()
	Return 0
End

Class Game Extends App
  Field _img:Image

  Method OnCreate:Int()	
    SetUpdateRate(60)
    _img = LoadImage( "button.png" )
    Return 0
  End Method
  
  Method OnRender:Int()
    Cls()
    DrawImageRect( _img, 100, 100, 0, 0, 40, 40, 0, 1, 1 )
    DrawImageRect( _img, 100, 140, 0, 0, 40, 40, 0, -1, 1 )
    Return 0
  End Method
End Class</textarea><br><br>Edit: I see where your problem is: When you draw at 0,0 your image is drawn to the left starting at 0,0 using negative scale in x direction, which means it's simply off screen then. <br><br></td></tr></table><br>
<a name="2126428"></a>

<a name="2126429"></a>

<a name="2126430"></a>

<a name="2126434"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dawlane</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> OK finally got to have some time to answer this in a much better manner. As Xaron says, that version of DrawImageRect will work. But an image is drawn from a pivot point aka a handle. You can set this to the middle of an image as part of the parameters to load an image (see the bit about flags in the documentation for load image). The default is to set the handle to what would be the top left corner of an image, so when an image is drawn with negative scaling, it draws the mirror to the left or top of this handle. So to keep the image in a desired position; this handle has to be move to what would be the opposite side of the image e.g. it's width and height.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' Enforce strict mode for syntax and data type checking to stop MonkeyX from trying to make a best guess at what
' we want to do with number types. This means that we have to tell monkey exactly what we want to do
' with them when we create a variable and what type of data has to be returned from a function
Strict

Import mojo

' Extend the Application class data to include the mojo framework control class data
Class CGame Extends App
	Field blob:CBlob
	Method OnCreate:Int()
		SetUpdateRate(60)
		Self.blob = CBlob.CreateBlob( 0, 0, LoadImage("jetman.png") )
		Return 0
	End Method
	
	Method OnUpdate:Int()
		If KeyDown(KEY_DOWN)
			Self.blob.SetFlip()
			Self.blob.Y += 4
		Endif
		If KeyDown(KEY_UP)
			Self.blob.ClearFlip()
			Self.blob.Y -= 4
		Endif
		If KeyDown(KEY_LEFT)
			Self.blob.ClearMirror()
			Self.blob.X -= 4
		Endif
		If KeyDown(KEY_RIGHT)
			Self.blob.SetMirror()
			Self.blob.X += 4	
		Endif
		If KeyHit(KEY_1) Then Self.blob.UseMidHandle()
		If KeyHit(KEY_2) Then Self.blob.UseMidHandle()
		If KeyHit(KEY_3) Then Self.blob.Show()
		If KeyDown(KEY_4) Then Self.blob.SetRotation(Self.blob.rot+1)
		If KeyHit(KEY_Z) And Self.blob.sX &gt; 0.0 Then Self.blob.sX -= 0.1
		If KeyHit(KEY_X) Then Self.blob.sX += 0.1
		If KeyHit(KEY_Q) And Self.blob.sY &gt; 0.0 Then Self.blob.sY -= 0.1
		If KeyHit(KEY_A) Then Self.blob.sY += 0.1
		Return 0
	End Method
		
	Method OnRender:Int()
		Cls (0,0,0)
		Self.blob.Draw()
		DrawText("Press 1 to use the centre of an image as a pivot point", 0, 400)
		DrawText("Press 2 to not use the centre of an image as a pivot point", 0, 410)
		DrawText("Press 3 to show or hide the image", 0, 420)
		DrawText("Press 4 to rotate the image right", 0, 430)
		DrawText("Press Z or X the alter X Sclaing", 0, 440)
		DrawText("Press Q or A the alter Y Sclaing", 0, 450)
		DrawText("Use the ARROW keys to move the image", 0, 460)
		SetColor(255,0,0)
		DrawRect( Self.blob.X + Self.blob.hX, Self.blob.Y + Self.blob.hY, 8, 8 )
		Return 0
	End Method
End Class

' To better handle any images that need special treament, we should encapsulate such things in a class.
' This could be modified to form part of a sprite engine. Basically you write an internal method
' that modifies the image in a special way, and if its meant to be in real time; you call in in the
' update method on the main mojo class
Class CBlob

	' We will use a binary flag system to decide if an image needs to be changed
	' Do a google search for binary numbers and computing
	Const SHOW:Int = 1
	Const MIRROR:Int = 2
	Const FLIP:Int = 4
	Const MIDHANDLE:Int = 8
	
	Field img:Image
	Field X:Float	' Screen horizontal position
	Field Y:Float	' Screen vertical position
	Field sX:Float	' Scale on X
	Field sY:Float	' Scale on Y
	Field hX:Float	' Drawing point of image on horizontal plane
	Field hY:Float	' Drawing point of image on vertical plane
	Field rot:Float	' Rotation of image
	Field flag:Int	' Control flag
	
	' Create a New Blob object in memory with some defaut values if no parameter are passed
	Method New( x:Float = 0, y:Float = 0, pic:Image = Null, flags:Int = (SHOW) )
		Self.X = x
		Self.Y = y
		Self.img = pic
		Self.sX = 1.0
		Self.sY = 1.0
		Self.hX = pic.HandleX()
		Self.hY = pic.HandleY()
		Self.rot = 0
		Self.flag = flags
	End Method
	
	' Set the bit to enable horizontal mirroring
	' |= means that we are going to do a binary OR directly to the flag variable
	' to set the bit that controls the code to mirror/flip what we want to draw	
	Method SetMirror:Void()
		Self.flag |= MIRROR
	End Method
	
	' Set the bit to enable vertical mirroring
	' |= means that we are going to do a binary OR directly to the flag variable
	' to set the bit that controls the code to mirror/flip what we want to draw
	Method SetFlip:Void()
		Self.flag |= FLIP
	End Method
	
	' Clear the bit that enables horizontal mirroring
	' Now the &amp;= means that are going to do a binary AND directly to the flag variable
	' to clear the bit that controls the code to mirror/flip what we draw
	' A quick over view of how AND works
	' If bit A AND bit B are both on, then bit it stays on. If eithe bit A or bit B are off; then
	' the bit is turned off.
	' So as not to mess up any of the other bits in the flag variable, we invert the constant variable
	' that represents a bit in the flag with the Exclusive OR (~). Basically turn that bit off and the others on  
	Method ClearMirror:Void()
		Self.flag &amp;= ~MIRROR 
	End Method
	
	' Clear the bit that enables vertical mirroring
	' Now the &amp;= means that are going to do a binary AND directly to the flag variable
	' to clear the bit that controls the code to mirror/flip what we draw
	' A quick over view of how AND works
	' If bit A AND bit B are both on, then bit it stays on. If eithe bit A or bit B are off; then
	' the bit is turned off.
	' So as not to mess up any of the other bits in the flag variable, we invert the constant variable
	' that represents a bit in the flag with the Exclusive OR (~). Basically turn that bit off and the others on  
	Method ClearFlip:Void()
		Self.flag &amp;= ~FLIP 
	End Method
	
	' Set the Show unshow bit
	' ~= mean that we are using the Exclusive OR to invert the bit that controls whether or not we see the image	
	Method Show:Void()
		Self.flag ~= SHOW
	End Method
	
	' Use the binary AND operator(&amp;) to filter out or (trap) the bit that only controls the vertical flip
	' It will return the number use for FLIP or a zero.
	Method FlipState:Int()
		Return (Self.flag &amp; FLIP)
	End Method
	
	' Use the binary AND operator(&amp;) to filter out or (trap) the bit that only controls the vertical flip
	' It will return the number use for MIRROR or a zero 
	Method MirrorState:Int()
		Return (Self.flag &amp; MIRROR)
	End Method
	
	' Set the Use/Unuse mid-handle drawing point unshow bit
	' ~= mean that we are using the Exclusive OR to invert the bit that controls whether or not the drawing pivot
	' is in the middle of an image
	Method UseMidHandle:Void()
		Self.flag ~= MIDHANDLE
	End Method
	
	' Set the image rotation. Note that it can get a bit weird with mirroring and fliping enabled
	Method SetRotation:Void(r:Float)
		Self.rot = r
		If Self.rot &gt; 360 Then Self.rot = 0
	End Method
	
	' Draw the image
	Method Draw:Void()
		' The mirroring code
		' First check to set if the mid-handle flag was set.
		If (Self.flag &amp; MIDHANDLE)
			' Set the drawing pivot to the middle of the image
			Self.img.SetHandle( (Self.img.Width()/2), (Self.img.Height()/2) )
			
			' Check to see if the Miror flag was set. Note that the flag variable and the bit that represents
			' the mirror are enclosed in brackets. We have to make sure that these are worked out first before the NOT
			' or thing will not work!
			If Not (Self.flag &amp; MIRROR)
				' If it wasn't then make sure that the scale is positive
				Self.sX = Abs( Self.sX )
			Else
				' If it was then turn the scale into a negative number
				If Self.sX &gt; 0 Then Self.sX = -Self.sX
			Endif
			
			' Check to see if the Flip flag was set. Note that the flag variable and the bit that represents
			' the flip are enclosed in brackets. We have to make sure that these are worked out first before the NOT
			' operatoror thing will not work!
			If Not (Self.flag &amp; FLIP)
				' If it wasn't then make sure that the scale is positive
				Self.sY = Abs( Self.sY )
			Else
				' If it was then turn the scale into a negative number
				If Self.sY &gt; 0 Then Self.sY = -Self.sY
			Endif
		Else
		
			' If the mid-handle bit was not set; then we do a similar thing as if the mid-handle was set, but
			' we have to specifically set the drawing pivot or the image would bounce around every time we
			' mirrored it. Note that if the mid handle bit was initially set; then folowing code preserves
			' that last vertical/horizontal handle
			If Not (Self.flag &amp; MIRROR)
				' Set the handle to anchor the image on the left side
				Self.img.SetHandle( 0, Self.img.HandleY() )
				Self.sX = Abs( Self.sX )
			Else
				' Now the the image is mirrored, so the handle need to adjusted to the images actuall width
				Self.img.SetHandle( Self.img.Width(), Self.img.HandleY() )
				If Self.sX &gt; 0 Then Self.sX = -Self.sX
			Endif
			
			' Check to see if the Flip flag was set. Note that the flag variable and the bit that represents
			' the flip are enclosed in brackets. We have to make sure that these are worked out first before the NOT
			' operator or thing will not work!
			If Not (Self.flag &amp; FLIP)
				' Set the handle to anchor the image on the top
				Self.img.SetHandle( Self.img.HandleX(), 0 )
				Self.sY = Abs( Self.sY )
			Else
				' Now the the image is flipped, so the handle needs to adjusted to the images actuall height
				Self.img.SetHandle( Self.img.HandleX(), Self.img.Height() )
				If Self.sY &gt; 0 Then Self.sY = -Self.sY
			Endif
		Endif
		' If the Show flag is set then draw the image
		If Self.flag &amp; SHOW Then DrawImageRect( Self.img, Self.X, Self.Y, 0, 0, Self.img.Width(), Self.img.Height(), Self.rot, Self.sX, Self.sY )
	End Method
	
	' We should call this function to create and return something that we can actually control	
	Function CreateBlob:CBlob( x:Float = 0, y:Float = 0, pic:Image = Null )
		Return New CBlob( x, y, pic )
	End Function
End Class

Function Main:Int()
	New CGame()
	Return 0
End Function</textarea> <br><br></td></tr></table><br>
<a name="2126433"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ponpoko</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks guys,<br><br>These were great tutorials and I got everything working in my game. I also learned about a few other things like Strict etc. Monkey has quite a few differences from Blitz (I mostly used Blitz +). Thanks for all your help! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
