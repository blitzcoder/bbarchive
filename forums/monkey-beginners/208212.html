<!DOCTYPE html><html lang="en" ><head ><title >Displaying a calendar?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Displaying a calendar?</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=528" >Monkey Beginners</a>/<a href="#bottom" >Displaying a calendar?</a><br><br>
<a name="2082602"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >malick</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Does anyone know of any modules or sample code that would help with displaying a calendar (preferably platform independent)?  I'd like to be able to display a calendar showing a month, similar to what you see when you look at the calendar on your smartphone, Google calendar, etc.  I've tried searching the Forums but didn't find much of anything.  Thanks! <br><br></td></tr></table><br>
<a name="2082603"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ziggy</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think you'll have to do it yourself. <br><br></td></tr></table><br>
<a name="2082604"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Goodlookinguy</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've written code that can give you the information you'll need to be able to write a calendar, but I haven't made a graphical interface for it.<br><br>I think it has some dependencies on other bits of my code, so I could attempt to rip it out if you really need it - take a look: <a href="https://bitbucket.org/Goodlookinguy/xaddon/src/8040b2c/mojoplus/datetimefuncs.monkey" target="_blank">https://bitbucket.org/Goodlookinguy/xaddon/src/8040b2c/mojoplus/datetimefuncs.monkey</a> <br><br></td></tr></table><br>
<a name="2082606"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >malick</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Goodlookinguy - That's fantastic!  I'm going to try messing with it tonight.  That looks like it will have me well on my way. :) <br><br></td></tr></table><br>
<a name="2082757"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Goodlookinguy</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here, I ripped the code out. It didn't have that many dependencies on my other stuff.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Private
Import mojo
Public

Function Main:Int()
	New DateTimeExample()
End

Class DateTimeExample Extends App
	Method OnCreate:Int()
		SetUpdateRate(30)
	End
	
	Method OnRender:Int()
		Cls()
		'DrawText(FormatDate("l jS, Y @ g:i:sA"), 100, 100)
		
		Local today:Int = Day()
		Local endOfMonth:Int = DaysInMonth(Year(), Month())
		Local startDay:Int = DayOfWeek(Year(), Month(), 1)
		
		Local boxWidth:Int = 640.0 / 7.0
		Local date:Int[] = GetDate()
		date[2] = 1
		Local x, y
		For Local i:Int = 0 Until endOfMonth
			Local j = i + startDay
			x = j Mod 7
			y = j / 7
			
			SetColor(Rnd(100, 150), Rnd(150, 200), Rnd(200, 255))
			DrawRect(x * boxWidth, y * 32, boxWidth, 32)
			SetColor(255, 255, 255)
			If (i + 1) = today
				DrawRect(x * boxWidth, y * 32, boxWidth, 32)
			End
			DrawText(FormatDate("D jS", date), x * boxWidth, y * 32)
			date[2] = date[2] + 1
		End
	End
End

Function PadLeft:String( str:String, padding:String, length:Int )
	If str.Length &gt;= length Then Return str
	Local strsb:StringStack = New StringStack()
	Local totalLen := str.Length
	While totalLen &lt; length
		strsb.Push(padding)
		totalLen += padding.Length
	End
	strsb.Push(str)
	Local strp := strsb.Join()
	strsb.Clear()
	strsb = Null
	Return strp[totalLen - length..]
End

Function Now:Int()
	Return Millisecs()
End

Function MillisecsToDate:Int[]( millisecs:Int = Now() )
	Local date:Int[7]
	
	date[2] = Days(millisecs)
	date[3] = Hours(millisecs)
	date[4] = Minutes(millisecs)
	date[5] = Seconds(millisecs)
	date[6] = millisecs Mod 1000
	
	Return date
End

Function UnixTimeToDate:Int[]( unixTimestamp:Int )
	' this may not work as an unsigned integer is needed
	' year in seconds = 31536000
	' month in seconds = 2628000
	' day in seconds = 86400
	' hours in seconds = 3600
	' minute in seconds = 60
	Local years = unixTimestamp / 31536000
	unixTimestamp -= years * 31536000
	Local months = unixTimestamp / 2628000
	unixTimestamp -= months * 2628000
	Local days = unixTimestamp / 86400
	unixTimestamp -= days * 86400
	Local hours = unixTimestamp / 3600
	unixTimestamp -= hours * 3600
	Local minutes = unixTimestamp / 60
	unixTimestamp -= minutes * 60
	Return [1970 + years, months + 1, days + 1, hours, minutes, unixTimestamp, 0]
End


'Summary: Returns the seconds that have passed from given milliseconds
Function Seconds:Int( millisecs:Int = Now() )
	Return Int(millisecs / 1000.0)
End

'Summary: Returns the minutes that have passed from given milliseconds
Function Minutes:Int( millisecs:Int = Now() )
	Return Int((millisecs / 1000.0) / 60.0)
End

'Summary: Returns the hours that have passed from given milliseconds
Function Hours:Int( millisecs:Int = Now() )
	Return Int(((millisecs / 1000.0) / 60.0) / 60.0)
End

'Summary: Returns the days that have passed from given milliseconds
Function Days:Int( millisecs:Int = Now() )
	Return Int((((millisecs / 1000.0) / 60.0) / 60.0) / 24.0)
End


#Rem
	Summary: Returns a string with the time formatted (See more for details)
	\ = Escape next character
	H = Hours
	I = Minutes without leading zeros
	M = Minutes with leading zeros
	S = Seconds without leading zeros
	s = Seconds with leading zeros
	m = Milliseconds
	
	This function is string manipulation heavy, be wary of overuse
#End
Function FormatTime:String( format:String, millisecs:Int = Now() )
	Local sb:StringStack = New StringStack()
	Local char:Int
	
	For Local index:Int = 0 Until format.Length
		char = format[index]
		Select (char)
			Case 92  ' \
				index += 1
				sb.Push format[index..index+1]
			Case 72  ' H
				sb.Push Hours(millisecs)
			Case 73  ' I
				sb.Push Minutes(millisecs) Mod 60
			Case 77  ' M
				Local min:Int = Minutes(millisecs) Mod 60
				If min &lt; 10 sb.Push("0" + min) Else sb.Push(min)
			Case 83  ' S
				sb.Push Seconds(millisecs) Mod 60
			Case 115 ' s
				Local sec:Int = Seconds(millisecs) Mod 60
				If ( sec &lt; 10 ) sb.Push("0" + sec) Else sb.Push(sec)
			Case 109 ' m
				Local mil:Int = millisecs Mod 1000
				If mil &lt; 10 sb.Push("00" + mil) ElseIf mil &lt; 100 sb.Push("0" + mil) Else sb.Push(mil)
			Default
				sb.Push(format[index..index+1])
		End
	Next
	
	Return sb.Join()
End

Const TimeFormat_PlayTime:String = "H:M:s"


#Rem
	Summary: Returns a string with the date formatted (See more for details)
	The formatting characters used are mostly based on the php function date.
	
	\ = Escape next character
	
	Y = Full Year (e.g. 2012, 2013)
	y = Two Digit Year (e.g. 97, 99, 02, 11, 13)
	L = 1 if Leap Year and 0 if not a leap year
	
	F = Full month name (e.g. August)
	M = Abbriviated month name (e.g. Aug)
	m = Numeric month value with leading zeros (e.g. 01, 09, 12)
	n = Numeric month value without leading zeros (e.g. 1, 9, 12)
	t = Number of days in month (e.g. 30)
	
	l = Full day of the week name (e.g. Sunday)
	D = Abbriviated day of the week name (e.g. Sun)
	d = Day of month with leading zeros (e.g. 01, 09, 19, 27)
	j = Day of month without leading zeros (e.g. 1, 9, 19, 27)
	N = Numeric day of the week starting from 1
	w = Numeric day of the week starting from 0
	S = English ordinal suffix for the day of the month (e.g. st, rd, nd, th)
	z = Day of the year (1-365/6)
	
	a = Lowercase Ante Meridiem and Post Meridiem (e.g. am, pm)
	A = Uppercase Ante Meridiem and Post Meridiem (e.g. AM, PM)
	g = 12-hour format of an hour without leading zeros (e.g. 1, 12)
	G = 24-hour format of an hour without leading zeros (e.g. 0, 21)
	h = 12-hour format of an hour with leading zeros (e.g. 01, 12)
	H = 24-hour format of an hour with leading zeros (e.g. 00, 23)
	i = Minutes with leading zeros (e.g. 00, 59)
	s = Seconds with leading zeros (e.g. 00, 59)
	
	This function is string manipulation heavy, be wary of overuse
#End
Function FormatDate:String( format:String, date:Int[] = GetDate() )
	Local dayOfWeek := DayOfWeek(date)
	Local sb:StringStack = New StringStack()
	Local char:Int
	
	For Local index:Int = 0 Until format.Length
		char = format[index]
		Select (char)
			Case 92  ' \
				index += 1
				sb.Push format[index..index+1]
			Case "Y"[0] sb.Push String(YearFromDate(date))
			Case "y"[0] sb.Push String(YearFromDate(date) Mod 100)
			Case "L"[0] sb.Push String(Int(LeapYear(YearFromDate(date))))
				
			Case "F"[0] sb.Push MonthName(MonthFromDate(date))
			Case "M"[0] sb.Push MonthAbbrName(MonthFromDate(date))
			Case "m"[0] sb.Push PadLeft(String(MonthFromDate(date)), "0", 2)
			Case "n"[0] sb.Push String(MonthFromDate(date))
			Case "t"[0] sb.Push String(DaysInMonth(date))
			
			Case "l"[0] sb.Push DayName(dayOfWeek)
			Case "D"[0] sb.Push DayAbbrName(dayOfWeek)
			Case "d"[0] sb.Push PadLeft(String(DayFromDate(date)), "0", 2)
			Case "j"[0] sb.Push String(DayFromDate(date))
			Case "N"[0] sb.Push String(dayOfWeek + 1)
			Case "w"[0] sb.Push String(dayOfWeek)
			Case "S"[0] sb.Push DayOfWeekSuffix(DayFromDate(date))
			Case "z"[0] sb.Push String(DayOfYear(date))
			
			Case "a"[0] sb.Push Meridiem(TimeOfDay(HourFromDate(date))).ToLower()
			Case "A"[0] sb.Push Meridiem(TimeOfDay(HourFromDate(date)))
			Case "g"[0]
				Local hour:Int = HourFromDate(date) Mod 12
				If hour = 0 Then hour = 12
				sb.Push String(hour)
			Case "G"[0] sb.Push String(HourFromDate(date))
			Case "h"[0]
				Local hour:Int = HourFromDate(date) Mod 12
				If hour = 0 Then hour = 12
				sb.Push PadLeft(String(hour), "0", 2)
			Case "H"[0] sb.Push PadLeft(String(HourFromDate(date)), "0", 2)
			Case "i"[0] sb.Push PadLeft(String(MinuteFromDate(date)), "0", 2)
			Case "s"[0] sb.Push PadLeft(String(SecondFromDate(date)), "0", 2)
			Default sb.Push format[index..index+1]
		End
	Next
	
	Return sb.Join()
End

'Summary: Returns the full month name
Function MonthName:String()
	Return MonthName(Month())
End

'Summary: Returns the full month name
Function MonthName:String( month:Int )
	Select month
		Case 1 Return "January"
		Case 2 Return "February"
		Case 3 Return "March"
		Case 4 Return "April"
		Case 5 Return "May"
		Case 6 Return "June"
		Case 7 Return "July"
		Case 8 Return "August"
		Case 9 Return "September"
		Case 10 Return "October"
		Case 11 Return "November"
		Case 12 Return "December"
	End
End

'Summary: Returns the abbriviated month name
Function MonthAbbrName:String()
	Return MonthAbbrName(Month())
End

'Summary: Returns the abbriviated month name
Function MonthAbbrName:String( month:Int )
	Select month
		Case 1 Return "Jan"
		Case 2 Return "Feb"
		Case 3 Return "Mar"
		Case 4 Return "Apr"
		Case 5 Return "May"
		Case 6 Return "Jun"
		Case 7 Return "Jul"
		Case 8 Return "Aug"
		Case 9 Return "Sep"
		Case 10 Return "Oct"
		Case 11 Return "Nov"
		Case 12 Return "Dec"
	End
End

'Summary: Returns the full day of the week name
Function DayName:String( dayOfWeek:Int )
	Select dayOfWeek
		Case 0 Return "Sunday"
		Case 1 Return "Monday"
		Case 2 Return "Tuesday"
		Case 3 Return "Wednesday"
		Case 4 Return "Thursday"
		Case 5 Return "Friday"
		Case 6 Return "Saturday"
	End
End

'Summary: Returns the abbriviated day of the week name
Function DayAbbrName:String( dayOfWeek:Int )
	Select dayOfWeek
		Case 0 Return "Sun"
		Case 1 Return "Mon"
		Case 2 Return "Tue"
		Case 3 Return "Wed"
		Case 4 Return "Thu"
		Case 5 Return "Fri"
		Case 6 Return "Sat"
	End
End

Function DayOfWeekSuffix:String( day:Int )
	If day &gt;= 11 And day &lt;= 13 Return "th"
	
	day = day Mod 10
	Select day
		Case 1 Return "st"
		Case 2 Return "nd"
		Case 3 Return "rd"
	End
	Return "th"
End

Function DaysInMonth:Int( date:Int[] )
	Return DaysInMonth(YearFromDate(date), MonthFromDate(date))
End

'Summary: Returns the number of days in a month
Function DaysInMonth:Int( year:Int, month:Int )
	Select month
		Case 1, 3, 5, 7, 8, 10, 12
			Return 31
		Case 2
			If LeapYear(year) Then Return 29
			Return 28
		Case 4, 6, 9, 11
			Return 30
	End
End

'Summary: Returns whether or not this year is a leap year
Function LeapYear:Bool()
	Return LeapYear(Year())
End

'Summary: Returns whether or not the year is a leap year
Function LeapYear:Bool( year:Int )
	Return (year &amp; 3) = 0 And (((year Mod 25) &lt;&gt; 0) Or ((year &amp; 15) = 0))
End

'Summary: Returns meridiem of the time of day
Function Meridiem:String( timeOfDay:Bool )
	If timeOfDay Return "PM"
	Return "AM"
End

'Summary: Returns false for AM and true for PM based on the current hour
Function TimeOfDay:Bool()
	Return Hour() &gt;= 12
End

'Summary: Returns false for AM and true for PM based on a given hour
Function TimeOfDay:Bool( hour:Int )
	Return hour &gt;= 12
End

'Summary: Returns the year from a date
Function YearFromDate:Int( date:Int[] )
	Return date[0]
End

'Summary: Returns the month from a date
Function MonthFromDate:Int( date:Int[] )
	Return date[1]
End

'Summary: Returns the day from a date
Function DayFromDate:Int( date:Int[] )
	Return date[2]
End

'Summary: Returns the hour from a date
Function HourFromDate:Int( date:Int[] )
	Return date[3]
End

'Summary: Returns the minute from a date
Function MinuteFromDate:Int( date:Int[] )
	Return date[4]
End

'Summary: Returns the second from a date
Function SecondFromDate:Int( date:Int[] )
	Return date[5]
End

'Summary: Returns the millisecond from a date
Function MillisecondFromDate:Int( date:Int[] )
	Return date[6]
End

'Summary: Returns the day of the year
Function DayOfYear:Int()
	Return DayOfYear(Year(), Month(), Day())
End

'Summary: Returns the day of the year
Function DayOfYear:Int( date:Int[] )
	Return DayOfYear(YearFromDate(date), MonthFromDate(date), DayFromDate(date))
End

'Summary: Returns the day of the year
Function DayOfYear:Int( year:Int, month:Int, day:Int )
	Local days:Int = __MonthsInDays[month - 1] + day
	If LeapYear(year) And (month &gt; 2 Or (month = 2 And day = 29)) Then Return days + 1
	Return days
End

'Summary: Returns the day of the week
Function DayOfWeek:Int()
	Return DayOfWeek(Year(), Month(), Day())
End

'Summary: Returns the day of the week
Function DayOfWeek:Int( date:Int[] )
	Return DayOfWeek(YearFromDate(date), MonthFromDate(date), DayFromDate(date))
End

'Summary: Returns the day of the week
Function DayOfWeek:Int( year:Int, month:Int, day:Int )
	Return daysFromDate(year, month, day) Mod 7
End

'Summary: Returns the date year
Function Year:Int()
	Return GetDate()[0]
End

'Summary: Returns the month of the year
Function Month:Int()
	Return GetDate()[1]
End

'Summary: Returns the day of the month
Function Day:Int()
	Return GetDate()[2]
End

'Summary: Returns the hour of the day
Function Hour:Int()
	Return GetDate()[3]
End

'Summary: Returns the minutes of the hour
Function Minute:Int()
	Return GetDate()[4]
End

'Summary: Returns the seconds of the minute
Function Second:Int()
	Return GetDate()[5]
End

'Summary: Returns the milliseconds of the second
Function Millisecond:Int()
	Return GetDate()[6]
End

Private
'Summary: Returns days since sometime. Useful only internally. I did not create the calculation.
Function daysFromDate:Int( year:Int, month:Int, day:Int )
	'http://java.dzone.com/articles/algorithm-week-how-determine
	Local febFix
	If month &lt;= 2 Then febFix = 1
	
	Return  5 +
			(year - 1700) * 365 +
			(year - 1700 - febFix) / 4 -
			(year - 1700 - febFix) / 100 +
			(year - 1600 - febFix) / 400 +
			__MonthsInDays[month - 1] +
			(day - 1)
End

Global __MonthsInDays:Int[] = [  0, 31, 59, 90, ' Jan-Apr (+0, +31, +28, +31)
							120, 151, 181, 212, ' May-Aug (+30, +31, +30, +31)
							243, 273, 304, 334 ] ' Sep-Dec (+31, +30, +31, +30)
Global __DaysInMonths:Int[] = [ 31, 28, 31, 30, 31, 30, 31, 31, 30, 31, 30, 31 ]</textarea> <br><br></td></tr></table><br>
<a name="2082647"></a>

<a name="2082618"></a>

<a name="2082616"></a>

<a name="2082615"></a>

<a name="2082612"></a>

<a name="2082611"></a>

<a name="2082610"></a>

<a name="2082608"></a>

<a name="2082609"></a>

<a name="2082617"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Goodlookinguy</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I just changed the above code box and added in a real calender example (+1 minor bug fix). It can be done fairly easily with my code. <br><br></td></tr></table><br>
<a name="2082613"></a>

<a name="2082614"></a>

<a name="2082623"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >malick</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> That's perfect.  Thank you so much!  Would you have any objection to me posting any tweaks I make back here as a module or something? <br><br></td></tr></table><br>
<a name="2082630"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Goodlookinguy</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Do as you like. The code in my framework/library/whatever is under the <a href="http://www.wtfpl.net/about/" target="_blank">WTFPL</a>.<br><br><b>Edit:</b> I should mention that there are probably bugs in the DateTime code. So if you run across any that I haven't reported myself or fixed please report it to my <a href="https://bitbucket.org/Goodlookinguy/xaddon/issues?status=new&amp;status=open" target="_blank">repository bug tracker</a>. <br><br></td></tr></table><br>
<a name="2082627"></a>

<a name="2082648"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Goodlookinguy</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> I just fixed the DayOfYear function in case you were using it.<br><br>There were a couple of kinks/bugs in the system that I believe have all been resolved now. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
