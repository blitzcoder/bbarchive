<!DOCTYPE html><html lang="en" ><head ><title >Random Objects falling from the Sky</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Random Objects falling from the Sky</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=528" >Monkey Beginners</a>/<a href="#bottom" >Random Objects falling from the Sky</a><br><br>
<a name="2121019"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >En929</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I would like to know what could I add to my code to get random rectangles to keep falling from the sky to where even I wouldn't know which rectangle would fall out of the sky next. That is, I'm trying to create the effect of the game Asteroids or Tetris. Thus, what do I add to my code below to do that. Thanks!!<br><br><pre class=code>

Strict

Import mojo

Function Main:Int()
	New MyApp()
	Return True
End

Class MyApp Extends App

	Field RectX: Int = 300
	Field RectY: Int = -400
	Field RectW: Int = 100
	Field RectH: Int = 100

	Field RectX2: Int = 300
	Field RectY2: Int = -70
	Field RectW2: Int = 50
	Field RectH2: Int = 50
	
	
	Field RectX3: Int = 300
	Field RectY3: Int = -30
	Field RectW3: Int = 20
	Field RectH3: Int = 20

	Field RectX4: Int = 300
	Field RectY4: Int = -200
	Field RectW4: Int = 110
	Field RectH4: Int = 110

	Field RectX5: Int = 300
	Field RectY5: Int = -100
	Field RectW5: Int = 5
	Field RectH5: Int = 5



	Method OnCreate:Int()

		SetUpdateRate(60)
		Return True
	End
	
	Method OnUpdate:Int()
	
	
		'I want the Asteroids to keep falling out of the sky (like with the first "Rect" I made below, but
		'I want random ones to fall to where even I wouldn't know WHICH block will fall next like in the game
		'Asteroids or Tetris	

		RectY +=1
	

							
		Return True
	End
	
	Method OnRender:Int()
		Cls

'These are the different types of blocks that I wanted to keep falling out of the sky.

		'Blue
		SetColor (0,38,255)		
			DrawRect(RectX, RectY, RectW, RectH)
		'Red
		SetColor (255,0,0)
			DrawRect(RectX2, RectY2, RectW2, RectH2)
		'Green	
		SetColor (76,255,0)
			DrawRect(RectX3, RectY3, RectW3, RectH3)
		'Purple
		SetColor (178,0,255)
			DrawRect(RectX4, RectY4, RectW4, RectH4)
		'Orange
		SetColor (255,106,0)
			DrawRect(RectX5, RectY5, RectW5, RectH5)


		Return True
	End
End

</pre> <br><br></td></tr></table><br>
<a name="2121017"></a>

<a name="2121016"></a>

<a name="2121015"></a>

<a name="2121014"></a>

<a name="2121021"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gerry Quinn</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Local choice:Int = Int( Rnd( 5.0 ) )<br>' Will give a random value from 0..4 that you can use to pick your rectangle.<br><br>It shouldn't be possible to get 5, but if you are paranoid about that, use Int( Rnd( 4.99999 ) ) to be absolutely certain.<br><br>You'll find if simpler if you use an array of rectangle objects.  Lots of individually named fields makes for long-winded code.  But you can also use plain arrays.  I haven't tested the following, but it ought to work:<br><br><pre class=code>
	
	' Two arrays of arrays
	Field rects:[][] = [
			[ 300, -400, 100, 100 ],
			' And four more
		]
		
	Field colours[][] = [
			[ 0, 38, 255 ],
			' And four more
		]
		
	Field rectChoice:Int						' Which rectangle
	Field currentRect:Int = New Int[ 4 ]		' Working copy of it, so I can change y without affecting original
	Field readyForNewRect:Bool = True			' Ready for a new rect?
	
	Method OnUpdate:Int()
		If readyForNewRect			
			rectChoice = Int( Rnd( 5.0 ) )
			For Local i:Int = 0 Until 4
				currentRect[ i ] = rects[ rectChoice ][ i ]
			Next
			readyForNewRect = False
		Else
			currentRect[ 1 ] += 1
			If currentRect[ 1 ] &gt; 1000
				readyForNewRect = True
			End
		End
	End
	
	Method OnRender:Int()
		SetColor( colours[ rectChoice][ 0 ], colours[ rectChoice][ 1 ], colours[ rectChoice][ 2 ] )
		DrawRect( currentRect[ 0 ], currentRect[ 1 ], currentRect[ 2 ], currentRect[ 3 ] )
	End
</pre><br><br>Hope that gives you some ideas <br><br></td></tr></table><br>
<a name="2121020"></a>

<a name="2121040"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >En929</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Gerry, I have one thing. I incorporated the code you wrote above, but with the array line as an error in an and I don't know how to fix it. I wrote a note inside the code below to note where the error message has been mentioned:<br><br><br><pre class=code>

Strict

Import mojo

Function Main:Int()
	New MyApp()
	Return True
End

Class MyApp Extends App

	' Two arrays of arrays

	Field rects: Int [][] = [

	'I added in the 4 arrays (I know they are all in the same coordinates now, I'm just writing this for testing purposes)

			[ 300, -400, 100, 100 ],
			[ 300, -400, 100, 100 ],
			[ 300, -400, 100, 100 ],
			[ 300, -400, 100, 100 ],
			[ 300, -400, 100, 100 ],
		
	]		' And four more
		
	Field colours: Int [][] = [
			[ 0, 38, 255 ],
			[ 0, 38, 255 ],
			[ 0, 38, 255 ],
			[ 0, 38, 255 ],
			[ 0, 38, 255 ],
			' And four more
		]
		
	Field rectChoice:Int						' Which rectangle
	Field currentRect:Int = New Int[ 4 ]		' Working copy of it, so I can change y without affecting original
	Field readyForNewRect:Bool = True			' Ready for a new rect?
	
	Method OnUpdate:Int()
		If readyForNewRect			
			rectChoice = Int( Rnd( 5.0 ) )
			For Local i:Int = 0 Until 4
				currentRect[ i ] = rects[ rectChoice ][ i ]
			Next
			readyForNewRect = False
		Else
			currentRect[ 1 ] += 1
			If currentRect[ 1 ] &gt; 1000
				readyForNewRect = True
			End
		End
	End
	
	Method OnRender:Int()
		SetColor( colours[ rectChoice][ 0 ], colours[ rectChoice][ 1 ], colours[ rectChoice][ 2 ] )
		DrawRect( currentRect[ 0 ], currentRect[ 1 ], currentRect[ 2 ], currentRect[ 3 ] )
	End






</pre> <br><br></td></tr></table><br>
<a name="2121042"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >En929</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Gerry, I have one thing. I incorporated the code you wrote above, but with the array line as an error in an and I don't know how to fix it. I wrote a note inside the code below to note where the error message has been mentioned:<br><br><br><pre class=code>

Strict

Import mojo

Function Main:Int()
	New MyApp()
	Return True
End

Class MyApp Extends App

	       ' Two arrays of arrays

	Field rects: Int [][] = [

	        'I added in the 4 arrays (I know they are all in the same coordinates now, I'm just writing this for testing purposes)

			[ 300, -400, 100, 100 ],
			[ 300, -400, 100, 100 ],
			[ 300, -400, 100, 100 ],
			[ 300, -400, 100, 100 ],
			[ 300, -400, 100, 100 ],
		
'----------------Here is where I got the error message. I don't know how to fix it. Thanks!------------------------------------


	]		


		
	Field colours: Int [][] = [
			[ 0, 38, 255 ],
			[ 0, 38, 255 ],
			[ 0, 38, 255 ],
			[ 0, 38, 255 ],
			[ 0, 38, 255 ],

		
		]
		
	Field rectChoice:Int						' Which rectangle
	Field currentRect:Int = New Int[ 4 ]		' Working copy of it, so I can change y without affecting original
	Field readyForNewRect:Bool = True			' Ready for a new rect?
	
	Method OnUpdate:Int()
		If readyForNewRect			
			rectChoice = Int( Rnd( 5.0 ) )
			For Local i:Int = 0 Until 4
				currentRect[ i ] = rects[ rectChoice ][ i ]
			Next
			readyForNewRect = False
		Else
			currentRect[ 1 ] += 1
			If currentRect[ 1 ] &gt; 1000
				readyForNewRect = True
			End
		End
	End
	
	Method OnRender:Int()
		SetColor( colours[ rectChoice][ 0 ], colours[ rectChoice][ 1 ], colours[ rectChoice][ 2 ] )
		DrawRect( currentRect[ 0 ], currentRect[ 1 ], currentRect[ 2 ], currentRect[ 3 ] )
	End






</pre> <br><br></td></tr></table><br>
<a name="2121041"></a>

<a name="2121043"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Phil7</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Maybe the comma at the end of the last Rect ist to much<br><pre class=code> 	Field rects: Int [][] = [

		'I added in the 4 arrays (I know they are all in the same coordinates now, I'm just writing this for testing purposes)

	[ 300, -400, 100, 100 ],
		[ 300, -400, 100, 100 ],
		[ 300, -400, 100, 100 ],
		[ 300, -400, 100, 100 ],
		[300, -400, 100, 100]]
		
		'----------------Here is where I got the error message. I don't know how to fix it. Thanks!------------------------------------

</pre><br><br>and Field currentRect:Int has to be :Int[]<br>and end at the end ;-) is missing. (end of class MyApp) <br><br></td></tr></table><br>
<a name="2121044"></a>

<a name="2121045"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> here is an alternative if you are interested:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Import mojo

Function Main:Int()
	New MyApp()
	Return True
End

Class Rect
	Field x:Float
	Field y:Float
	Field w:Float
	Field h:Float
	Field color:int
	
	Const BLUE:Int = 1
	Const RED:Int = 2
	Const GREEN:Int = 3
	Const PURPLE:Int = 4
	Const ORANGE:Int = 5
	
	Method New(x:Float,y:Float,w:Float,h:Float,color:Int)
		Self.x = x
		Self.y = y
		Self.w = w
		Self.h = h
		Self.color = color
	End Method
	
	Method Render:Void()
		
		Select color
			Case BLUE
				SetColor (0,38,255)		
			Case RED
				SetColor (255,0,0)
			Case GREEN
				SetColor (76,255,0)
			Case PURPLE
				SetColor (178,0,255)
			Case ORANGE
				SetColor (255,106,0)
		End Select 	
		DrawRect(x, y, w, h)
	End Method
	
End Class

Class MyApp Extends App

	Field time:Int
	Field delay:Int
	
	Field rectList:List&lt;Rect&gt;

	Method OnCreate:Int()
		rectList = New List&lt;Rect&gt; 'create list storege for rectangle
		time = Millisecs() 'actual time in millisecs
		delay = 500 'millisec
		SetUpdateRate(60) '
		Return True
	End
	
	Method OnUpdate:Int()
	
		If time+delay &lt; Millisecs() ' if past time 
			Local hw:Int = Rnd(2,10)*10 ' width and hegith of rect
			Local rect:Rect = New Rect(Rnd(1,12)*60,0,hw,hw,Rnd(5.9)) 'create a rectangle
			rectList.AddLast(rect) ' add the rect to the list
			time = Millisecs() ' start time
		Endif
		
		For Local rect:Rect = Eachin rectList 'go through the ist of rect that are in the list
			rect.y += 1 	' add to y so it move down
			If rect.y &gt; 480 rectList.Remove(rect) ' remove rect from list if exitted the bottom of screen
		Next	' next rectangle in the list
									
		Return True
	End
	
	Method OnRender:Int()
		Cls
		For Local rect:Rect = Eachin rectList 'get rect in the list
			rect.Render() 'render rectangle
		Next 'get next rectangle in the list
		Return True
	End
End
</textarea> <br><br></td></tr></table><br>
<a name="2121054"></a>

<a name="2121055"></a>

<a name="2121056"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >En929</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks a lot Gerry and Jesse. I got both code examples to work.  That's what I wanted. Thanks again!! <br><br></td></tr></table><br>
<a name="2121060"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gerry Quinn</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Great, hope they are of some help.  As you get experienced you'll probably move towards using a Rect class like Jesse, or a class specifically for your object that could contain the colours as well), but I sense you are just getting started with the basics.  You won't really see the benefit of objects until you are writing programs of a certain size. <br><br></td></tr></table><br>
<a name="2122886"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >consty</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here are some corrections<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Import mojo

Function Main:Int()
	New MyApp()
	Return 0
End

Class Rect
	Field x:Float
	Field y:Float
	Field w:Float
	Field h:Float
	Field color:Int[]
	
	' Good idea to encapsulate the random creation into a concrete method.
	Method SetRandomPosSize:Void()
		' Create random size
		Local size:Float = Rnd(2, 10) * 10
		' Create random position
		' Random x position, take width dimension into consideration
		Local px:Float = Rnd(0, DeviceWidth() - size)
		Local py:Float = 0 - size
		
		' Update fields
		x = px
		y = py
		w = size
		h = size
	End
	
	' This can be set once and stored into a color array
	Method SetRandomColor:Void()
		color = [255, 255, 255] ' a good practice is to initialize the array always.

		' Rnd returns a float so it needs cast back to integer
		' Ceil is needed to ensure that the float will rounded towards the higher number.
		' The rnd range needs to be 0..5 because the ceil will discard anything below 0.9
		Local id:Int = Int(Ceil(Rnd(0, 5)))
		'Print(id)
		
		Select id 
			Case 1
				color = [0,38,255]
			Case 2
				color = [255,0,0]
			Case 3
				color = [76,255,0]
			Case 4
				color = [178,0,255]
			Case 5
				color = [255,106,0]
		End
	End
	
	Method Render:Void()
		SetColor(color[0], color[1], color[2])
		DrawRect(x, y, w, h)
	End
	
End Class

Class RectManager
	Field rectList:List&lt;Rect&gt; = New List&lt;Rect&gt;
	Field time:Int
	Field delay:Int = 500
	
	Method CreateNewRect:Void()
		Local r:Rect = New Rect
		r.SetRandomPosSize()
		r.SetRandomColor()
		rectList.AddLast(r)
	End
	
	Method StartTimer:Void()
		time = Millisecs()
	End
	
	Method UpdateTimer:Void()
		If time + delay &lt; Millisecs()
			time = Millisecs()
			CreateNewRect()
		End		
	End
	
	Method UpdateRects:Void()
		For Local r:Rect = Eachin rectList
			r.y += 1
			If r.y &gt; DeviceHeight() Then rectList.Remove(r)
		Next
	End
	
	Method RenderRects:Void()
		For Local r:Rect = Eachin rectList
			r.Render()
		Next
	End
End

Class MyApp Extends App
	Field rmgr:RectManager = New RectManager
	
	Method OnCreate:Int()
		rmgr.StartTimer()
		
		' These methods should return 0, see documentation.
		Return 0
	End
	
	Method OnUpdate:Int()
		rmgr.UpdateTimer()
		rmgr.UpdateRects()
					
		Return 0
	End
	
	Method OnRender:Int()
		Cls
		rmgr.RenderRects()
		Return 0
	End
End
</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
