<!DOCTYPE html><html lang="en" ><head ><title >File I/O - very simple</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >File I/O - very simple</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=528" >Monkey Beginners</a>/<a href="#bottom" >File I/O - very simple</a><br><br>
<a name="2124442"></a>

<a name="2124440"></a>

<a name="2124443"></a>

<a name="2124454"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >zxretrosoft</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello friends!<br><br>I need to make a simple code on the I/O file (eg. TXT). <br><br>I have it like this:<br><br><pre class=code>
Import mojo
Import brl
Import filesystem
Import fileio

Global file$
Global data:Int,data2$


Class MyApp Extends App

	Field data:DataBuffer

	Method OnCreate()
	
		file="monkey://data/text.txt"
	
		data=DataBuffer.Load(file)
		data2=LoadString(file)

		SetUpdateRate 60
	End
	
	Method OnUpdate()
	End
	
	Method OnRender()
		Cls

		DrawText data2,20,40
		DrawText "data.Length="+data.Length,0,12
		
	End
End

'main
Function Main()
	New MyApp
End
 </pre><br><br>But unfortunately, the whole file uploads. I need to be able to first work with the first line, then the second line, and so on.<br><br>Do you have any advice please?<br><br>Thank you in advance!<br><br>P. S. Monkey-X, target PC or HTML5. <br><br></td></tr></table><br>
<a name="2124446"></a>

<a name="2124452"></a>

<a name="2124451"></a>

<a name="2124450"></a>

<a name="2124449"></a>

<a name="2124448"></a>

<a name="2124447"></a>

<a name="2124460"></a>

<a name="2124453"></a>

<a name="2124484"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dawlane</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> If you are going to use data buffers you will have to walk the data to look for the new line marker (CR/LF, LF or CR depending on the operating system the file was saved).<br>To do it with strings, you need to split the string into an array with the new line marker (~n in MonkeyX).<br>So make data2$ in to an empty array with data2$[]<br>Then use data2=LoadString(file).Split("~n")<br>You can access any line with data2[n].<br><br>Note: When you post code you should use the code and /code or codebox and /codebox tags encased in square brackets e.g [/code] as opening and closing. You should also indent you code to make it more readable like so<br><pre class=code>Import mojo
Import brl
Import filesystem
Import fileio

Global file$
Global data:Int,data2$

Class MyApp Extends App

	Field data:DataBuffer

	Method OnCreate()

		file="monkey://data/text.txt"

		data=DataBuffer.Load(file)
		data2=LoadString(file)

		SetUpdateRate 60
	End

	Method OnUpdate()
	End

	Method OnRender()
		Cls

		DrawText data2,20,40
		DrawText "data.Length="+data.Length,0,12

	End
End

'main
Function Main()
	New MyApp
End
</pre> <br><br></td></tr></table><br>
<a name="2124455"></a>

<a name="2124456"></a>

<a name="2124457"></a>

<a name="2124458"></a>

<a name="2124459"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >zxretrosoft</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Wow, super, thank you very much! ;)<br>It works perfectly! Thanks!<br><br>The final code:<br><br><pre class=code>
Import mojo
Import brl
Import filesystem

Global file$
Global data:Int,data2$[]

Class MyApp Extends App

	Field data:DataBuffer

	Method OnCreate()

		file="monkey://data/text.txt"

		data=DataBuffer.Load(file)
		data2=LoadString(file).Split("~n")

		SetUpdateRate 60
	End

	Method OnUpdate()
	End

	Method OnRender()
		Cls

		DrawText data2[0],20,40
		DrawText data2[1],20,60
		DrawText data2[2],20,80
		
		DrawText "data.Length="+data.Length,0,12

	End
End

'main
Function Main()
	New MyApp
End
 </pre> <br><br></td></tr></table><br>
<a name="2124474"></a>

<a name="2124475"></a>

<a name="2124473"></a>

<a name="2124476"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Steve Ancell</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi zxretrosoft, I made this rather handy class a few years back, you're welcome to use this. ;-)<br><br>Scroll down further on this comment for of the ways it can be used.<br><br><br><pre class=code>
Class C_FileLoader
	Field fileData:String[]
	Field currentLine:Int
	
	
	Method New(fileName:String)
		fileData = LoadString(fileName).Split("~n")
		currentLine = 0
		
	End
	
	
	Method ReadLine:String()
		Local currentData:String
		
		
		if Eof() = False
			currentData = Self.fileData[Self.currentLine]
			currentLine = (currentLine + 1)
			
		Else
			currentData = "Data Exhausted!"
			
		EndIf
		
		Return currentData
		
	End
	
	
	Method Eof:Bool()
		if Self.currentLine &gt; (fileData.Length() - 1)
			Return True
			
		Else
			Return False
			
		EndIf
		
	End
	
	
	Method Purge:Void()
		Local counter:Int
		
		
		For counter = 0 to (fileData.Length() - 1)
			fileData[counter] = ""
			
		Next
		
		fileData =[]
		
	End
	
End

</pre><br><br><br>And heres the way to use it in its simplest form, although you would most probably use it in other ways.<br><br><pre class=code>
Local stream:C_FileLoader = New C_FileLoader("LevelData/testfile.dat")


While not stream.Eof()
	Print(stream.ReadLine())
	
Wend

stream.Purge()

</pre> <br><br></td></tr></table><br>
<a name="2124477"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >zxretrosoft</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello Steve,<br>thank you very much! Unfortunately, I do not know how to use this class in my program? Can you please be more vivid? :-)  Best whole code. Or use it in my code?<br><br>P. S. I'am sorry, I bought Monkey-X Studio this week, and I'm acquainted with it. <br><br></td></tr></table><br>
<a name="2124478"></a>

<a name="2124483"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dawlane</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> For simple testing with the C++-Tool target (no graphics). You can do it like so:<br><pre class=code>Class C_FileLoader
	Field fileData:String[]
	Field currentLine:Int
	
	
	Method New(fileName:String)
		fileData = LoadString(fileName).Split("~n")
		currentLine = 0
		
	End
	
	
	Method ReadLine:String()
		Local currentData:String
		
		
		if Eof() = False
			currentData = Self.fileData[Self.currentLine]
			currentLine = (currentLine + 1)
			
		Else
			currentData = "Data Exhausted!"
			
		EndIf
		
		Return currentData
		
	End
	
	
	Method Eof:Bool()
		if Self.currentLine &gt; (fileData.Length() - 1)
			Return True
			
		Else
			Return False
			
		EndIf
		
	End
	
	
	Method Purge:Void()
		Local counter:Int
		
		
		For counter = 0 to (fileData.Length() - 1)
			fileData[counter] = ""
			
		Next
		
		fileData =[]
		
	End
	
End

Function Main()
	Local stream:C_FileLoader = New C_FileLoader("LevelData/testfile.dat")

	While not stream.Eof()
		Print(stream.ReadLine())
	Wend
	stream.Purge()
End Function
</pre><br>To include this within your own code you would put the class into a separate file and use Import to include the class, then create a data object either as a local/global variable, or as a field member of one of your own classes, or extend the class to make a new one. <br><br></td></tr></table><br>
<a name="2124479"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Steve Ancell</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for what you did there dawlane. I was assuming that zxretrosoft was already sort of comfortable with Monkey-X, hence why I didn't include the Function Main bit. ;-) <br><br></td></tr></table><br>
<a name="2124480"></a>

<a name="2124481"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Steve Ancell</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's a little somthing to get you going, it will output some strings and floats to the browser's console when compiled to HTML5. Graphics cannot render outside the OnRender loop, hence the text output; but you're probably clever enough to make further sense on how to do this once you get the jist of it. ;-)<br><br>1) Create a folder, name it "Project" or somthing similar.<br><br>2) Create a file named "LoadParse.monkey", create a floder named "LoadParse.data", and then create a subfolder inside that LoadParse.data and name it AppData. You can name the LoadParse bit any way you like as long as the monkey and data prefixes are present. Next open up LoadParse.Monkey, copy and paste the following code into the editor and then save it.<br><br><pre class=code>
Strict
Import mojo




Global loader:C_FileLoader




Function Main:Int()
	Local app:C_App = New C_App()
	
	Return 0
	
End




Class C_App Extends App
	Method OnCreate:Int()
		F_Load("AppData/data01.dat")
		
		Return 0
		
	End
	
	
	Method OnUpdate:Int()
		
		Return 0
		
	End
	
	
	Method OnRender:Int()
		Cls
		
		Return 0
		
	End
	
End




Function F_Load:Void(fileName:String)
	Local lineData:String
	
	
	If loader
		loader.Purge()
		loader = Null
		
	EndIf
	
	loader = New C_FileLoader(fileName)
	
	While not loader.Eof()
		lineData = loader.ReadLine()
		
		F_ParseUserData(lineData)
		
	Wend
	
	loader.Purge()
	
End




Function F_ParseUserData:Void(cmdLine:String)
	Local tokenList:String[]
	Local command:String
	
	
	tokenList = F_TokeniseCommandLine(cmdLine)
	command = tokenList[0]
	
	Select command
		Case "Rect"
			F_Rect(tokenList)
			
		Case "Ellipse"
			F_Ellipse(tokenList)
			
		Case "Circle"
			F_Circle(tokenList)
			
		Default
			'Do nothing!
			
	End
	
End




Function F_Rect:Void(tokenList:String[])
	Local x:Float, y:Float
	Local width:Float, height:Float
	
	
	x = Float(tokenList[1])
	y = Float(tokenList[2])
	width = Float(tokenList[3])
	height = Float(tokenList[4])
	
	Print("Item: " + tokenList[0])
	Print("Coordinates: " + x + ", " + y)
	Print("Area: " + width + " x " + height)
	Print("")
	
End




Function F_Ellipse:Void(tokenList:String[])
	Local x:Float, y:Float
	Local width:Float, height:Float
	
	
	x = Float(tokenList[1])
	y = Float(tokenList[2])
	width = Float(tokenList[3])
	height = Float(tokenList[4])
	
	Print("Item: " + tokenList[0])
	Print("Coordinates: " + x + ", " + y)
	Print("Area: " + width + " x " + height)
	Print("")
	
End




Function F_Circle:Void(tokenList:String[])
	Local x:Float, y:Float
	Local radius:Float
	
	x = Float(tokenList[1])
	y = Float(tokenList[2])
	radius = Float(tokenList[3])
	
	Print("Item: " + tokenList[0])
	Print("Coordinates: " + x + ", " + y)
	Print("Radius: " + radius)
	Print("")
	
End




Function F_TokeniseCommandLine:String[] (cmdLine:String)
	'Take a string, split it up, put each part in an array and then return it.
	
	Local outputList:String[]
	Local commandName:String
	Local valueList:String[]
	Local index:Int
	
	
	commandName = cmdLine[0..(cmdLine.Find(" "))].Trim()
	valueList = cmdLine[(cmdLine.Find(" ") + 1)..].Trim().Split(", ")
	outputList = outputList.Resize(valueList.Length() + 1)
	outputList[0] = commandName
	
	For index = 0 To(valueList.Length() -1)
		outputList[index + 1] = valueList[index]
		
	Next
	
	Return outputList
	
End




Class C_FileLoader
	Field fileData:String[]
	Field currentLine:Int
	
	
	Method New(fileName:String)
		fileData = LoadString(fileName).Split("~n")
		currentLine = 0
		
	End
	
	
	Method ReadLine:String()
		Local currentData:String
		
		
		if Eof() = False
			currentData = Self.fileData[Self.currentLine]
			currentLine = (currentLine + 1)
			
		Else
			currentData = "Data Exhausted!"
			
		EndIf
		
		Return currentData
		
	End
	
	
	Method Eof:Bool()
		if Self.currentLine &gt; (fileData.Length() - 1)
			Return True
			
		Else
			Return False
			
		EndIf
		
	End
	
	
	Method Purge:Void()
		Local counter:Int
		
		
		For counter = 0 to (fileData.Length() - 1)
			fileData[counter] = ""
			
		Next
		
		fileData =[]
		
	End
	
End

</pre><br><br>3 Open up notepad or notepad++, copy and paste the following text into the editor. Next click File-&gt;Save As, then name it "data01.dat", select "All Files" from the Save as type dropdown menu and then save it to LoadParse.data/AppData.<br><br><pre class=code>
Rect 50, 50, 100, 75
Ellipse 200, 200, 75, 100
Circle 300, 300, 50

</pre><br><br>Finally compile as HTML5 and then run it in the browser. Note that output will be outputted as text in this example. <br><br></td></tr></table><br>
<a name="2124482"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Steve Ancell</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm aware that my above explanation is a bit long winded, I'm better at doing than explaining. I hope it helps though. <br><br></td></tr></table><br>
<a name="2124485"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dawlane</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> As I think that you may ask about this at some point. Here's a small example of string slicing and some of the String Class functions<br><pre class=code>Function Main()
	Local myVar:String="abcdefgh"
	Local myArray:Int[]=[65,98,67,100,69,102,71,104] ' AbCdEfGh
	Local myToArray:Int[]="MONKEY-X".ToChars() ' Convert a string to a number array 
	Print "myVar is 'abcdefgh'"
	Print "myArray is [65,98,67,100,69,102,71,104] ' AbCdEfGh"
	Print "myToArray is MONKEY-X"
	Print "String are zero index arrays [0=a,1=b,etc]"
	Print "Print the fourth letter ('d') using String.FromChar(myVar[3]) this would be equivalent to Chr$(numeric value) -&gt; " + String.FromChar(myVar[3])
	Print "Print the ascii character code for the fourth letter ('d') with myVar[3] this would be equivalent the Asc(character_at_variable_index) -&gt; " + myVar[3]
	Print String.FromChar(34)+"A"+String.FromChar(34)+ "[0] would be the same as doing Asc(" + String.FromChar(34) + "A" + String.FromChar(34) + ") -&gt; " + "A"[0] 
	Print "Print the letters 'abcde' using what would be the equivalent of Left(myVar, length) with myVar[..5] -&gt; " + myVar[..5]
	Print "Print the middle letters 'def' using what would be the equivalent of Mid(myVar, start, length) with myVar[3..((myVar.Length+1) - 3)] -&gt; " + myVar[3..((myVar.Length+1) - 3)]	
	Print "Print the last letters 'fgh' using what would be the equivalent of Right(myVar, length from right) with myVar[((myVar.Length) - 3)..] -&gt; " + myVar[((myVar.Length) - 3)..]
	Print "Print the letters 'defgh'. Some languages would use Mid(myVar,start) with myVar[3..] -&gt; " + myVar[3..]
	Print "Convert an array of integer character codes to a string with String.FromChars(myArray) -&gt; " + String.FromChars(myArray)
	Print "'MONKEY-X' that was converted to an array"
	For Local i:Int = 0 To myToArray.Length - 1
		Print "Index = " + i + ": code: " + myToArray[i] + " -&gt; " + String.FromChar(myToArray[i])
	Next	
End Function</pre> <br><br></td></tr></table><br>
<a name="2124488"></a>

<a name="2124489"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >zxretrosoft</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you so much guys!<br><br>It works. But I can not do my specific file.<br>In CSV file I have this structure:<br><br>2016-09-23 09:10:45.177815 +02:00;00283771;A343;CR8100<br>2016-09-26 22:33:55.992692 +02:00;00007064;A418;CR7030<br>2016-09-19 08:23:14.491140 +02:00;00238821;A343;CR8100<br>2016-09-28 01:47:27.143027 +02:00;72080043;A385;CR323<br><br>etc.<br><br>And now, I need to count how many times, for example, A343 (next A418, next A343...) occurs in the entire group (like COUNTIF() function in Excel).<br><br>I modified the code. It reads semicolons. But I can still get out of it what I need :/<br>Rect;50;50;100;75<br>Ellipse;200;200;75;100<br>Circle;300;300;50<br><br><pre class=code>
Strict
Import mojo

Global loader:C_FileLoader


Function Main:Int()
	Local app:C_App = New C_App()
	
	Return 0	
End



Class C_App Extends App
	Method OnCreate:Int()
		F_Load("AppData/data01.dat")
		
		Return 0
		
	End
	
	
	Method OnUpdate:Int()
		
		Return 0
		
	End
	
	
	Method OnRender:Int()
		Cls
		
		Return 0
		
	End
	
End


Function F_Load:Void(fileName:String)
	Local lineData:String
	
	
	If loader
		loader.Purge()
		loader = Null
		
	EndIf
	
	loader = New C_FileLoader(fileName)
	
	While not loader.Eof()
		lineData = loader.ReadLine()
		
		F_ParseUserData(lineData)
		
	Wend
	
	loader.Purge()
	
End


Function F_ParseUserData:Void(cmdLine:String)
	Local tokenList:String[]
	Local command:String
	
	
	tokenList = F_TokeniseCommandLine(cmdLine)
	command = tokenList[0]
	
	Select command
		Case "Rect"
			F_Rect(tokenList)
			
		Case "Ellipse"
			F_Ellipse(tokenList)
			
		Case "Circle"
			F_Circle(tokenList)
			
		Default
			'Do nothing!
			
	End
	
End


Function F_Rect:Void(tokenList:String[])
	Local x:Float, y:Float
	Local width:Float, height:Float
	
	
	x = Float(tokenList[1])
	y = Float(tokenList[2])
	width = Float(tokenList[3])
	height = Float(tokenList[4])
	
	Print("Item: " + tokenList[0])
	Print("Coordinates: " + x + ", " + y)
	Print("Area: " + width + " x " + height)
	Print("")
	
End




Function F_Ellipse:Void(tokenList:String[])
	Local x:Float, y:Float
	Local width:Float, height:Float
	
	
	x = Float(tokenList[1])
	y = Float(tokenList[2])
	width = Float(tokenList[3])
	height = Float(tokenList[4])
	
	Print("Item: " + tokenList[0])
	Print("Coordinates: " + x + ", " + y)
	Print("Area: " + width + " x " + height)
	Print("")
	
End



Function F_Circle:Void(tokenList:String[])
	Local x:Float, y:Float
	Local radius:Float
	
	x = Float(tokenList[1])
	y = Float(tokenList[2])
	radius = Float(tokenList[3])
	
	Print("Item: " + tokenList[0])
	Print("Coordinates: " + x + ", " + y)
	Print("Radius: " + radius)
	Print("")
	
End


Function F_TokeniseCommandLine:String[] (cmdLine:String)
	'Take a string, split it up, put each part in an array and then return it.
	
	Local outputList:String[]
	Local commandName:String
	Local valueList:String[]
	Local index:Int
	
	
	commandName = cmdLine[0 .. (cmdLine.Find(";"))].Trim()
	valueList = cmdLine[ (cmdLine.Find(";") + 1) ..].Trim().Split(";")
	outputList = outputList.Resize(valueList.Length() + 1)
	outputList[0] = commandName
	
	For index = 0 To(valueList.Length() -1)
		outputList[index + 1] = valueList[index]
		
	Next
	
	Return outputList
	
End



Class C_FileLoader
	Field fileData:String[]
	Field currentLine:Int
	
	
	Method New(fileName:String)
		fileData = LoadString(fileName).Split("~n")
		currentLine = 0
		
	End
	
	
	Method ReadLine:String()
		Local currentData:String
		
		
		if Eof() = False
			currentData = Self.fileData[Self.currentLine]
			currentLine = (currentLine + 1)
			
		Else
			currentData = "Data Exhausted!"
			
		EndIf
		
		Return currentData
		
	End
	
	
	Method Eof:Bool()
		if Self.currentLine &gt; (fileData.Length() - 1)
			Return True
			
		Else
			Return False
			
		EndIf
		
	End
	
	
	Method Purge:Void()
		Local counter:Int
		
		
		For counter = 0 to (fileData.Length() - 1)
			fileData[counter] = ""
			
		Next
		
		fileData =[]
		
	End
	
End
</pre> <br><br></td></tr></table><br>
<a name="2124500"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >zxretrosoft</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> OK, I have almost everything. Thank you all!<br><br>And now, the last thing. When I run this code in GLFW (or HTML5), command Print(a[343]) works OK. But I need this data written to disk. And that's the latest issue :(<br>Thank you in advance! ;)<br><br><pre class=code>
Import brl.filestream

	Method Eof:Bool()
		If Self.currentLine &gt; (fileData.Length() -1)
						
			#rem
			Local file:= FileStream.Open("data/output.txt", "u")
			
			For Local i:Int = 342 To 343
				file.WriteString(i) '+ i + ";" + a[i])
			Next i
			#end
				
			Print(a[343])
			
			Return True

		Else
		
			Return False
	
		EndIf
		
	End Method
</pre> <br><br></td></tr></table><br>
<a name="2124499"></a>

<a name="2124501"></a>

<a name="2124502"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Steve Ancell</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> You can write to disk in Windows, Linux and Mac as far as I know, I assume you can save on the web but I've never tried to. As far as I know the only way to save any data on Android and iOS is with SaveState, this does not let you see and actual file however. I'll have a pokesie around when I get back home a bit later. ;-) <br><br></td></tr></table><br>
<a name="2124503"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >zxretrosoft</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> OK, Steve, Thank you! <br><br></td></tr></table><br>
<a name="2124505"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Shinkiro1</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> I didn't read the whole thread ...<br><br>Writing files in HTML5 alone won't work, therefore I use NWJS which gives you a window with a webview in it.<br>Then I use this js function: <br><br><pre class=code>
var native = new Object();

native.WriteFile = function(filePath, content) {
	var fs = require('fs');
	if (!fs) return;
	fs.writeFileSync(filePath, content, "utf-8");
}
</pre><br><br>PS: There is a thread around here how to setup NWJS with monkey. <br><br></td></tr></table><br>
<a name="2124506"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Steve Ancell</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> @zxretrosoft:<br><br>I found something that may be useful to you. Open the folder where you installed Monkey, then open bananas/mak/filetest/filetest.monkey.<br>This seems to work OK for Desktop_game_(Glfw3), the result is saved in the "internal" folder within the buld folder. <br><br></td></tr></table><br>
<a name="2124507"></a>

<a name="2124509"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Steve Ancell</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've done a couple of modifications to my C_FileLoader class, if you dont specify a filename when calling C_FileLoader.New() then it can be used for saving data in a desktop application. First you store data, line by line by calling WriteLine("stuff"), and then call the Save("fileName") method, once done you then call the Purge() method. I forgot to mention in my previous comments, you should make the C_FileLoader object Null when finished.<br><br>P.S: Also note the line fStream.Save("internal/testfile.dat"). The data can only be saved to the "internal" folder, this folder is automatically created when the project is built. If you try to save to a different folder then the program will end prematurely without saving anything, I would assume that a sub folder could probably be put inside the "internal" folder though.<br><br><pre class=code>
Strict
Import mojo
Import brl.filestream




Global loader:C_FileLoader




Function Main:Int()
	Local app:C_App = New C_App()
	
	Return 0
	
End




Class C_App Extends App
	Method OnCreate:Int()
		Local fStream:C_FileLoader = New C_FileLoader()
		
		
		fStream.WriteLine("Big")
		fStream.WriteLine("Round")
		fStream.WriteLine("Shiny")
		fStream.WriteLine("Bulbz")
		
		fStream.Save("internal/testfile.dat")
		fStream.Purge()
		
		fStream = Null
		
		Return 0
		
	End
	
	
	Method OnUpdate:Int()
		
		Return 0
		
	End
	
	
	Method OnRender:Int()
		Cls
		
		Return 0
		
	End
	
End




Class C_FileLoader
	Field fileData:String[]
	Field currentLine:Int
	
	
	Method New(fileName:String = "")
		If fileName &lt;&gt; ""
			fileData = LoadString(fileName).Split("~n")
			currentLine = 0
			
		EndIf
		
	End
	
	
	Method ReadLine:String()
		Local currentData:String
		
		
		if Eof() = False
			currentData = Self.fileData[Self.currentLine]
			currentLine = (currentLine + 1)
			
		Else
			currentData = "Data Exhausted!"
			
		EndIf
		
		Return currentData
		
	End
	
	
	Method WriteLine:Void(data:String)
		Local index:Int
		
		
		index = Self.fileData.Length()
		Self.fileData = Self.fileData.Resize(index + 1)
		Self.fileData[index] = data
		
	End
	
	
	Method Save:Void(fileName:String)
		Local file:Stream
		Local data:String
		
		
		file = FileStream.Open(fileName, "w")
		
		For data = EachIn Self.fileData
			file.WriteString(data + "~n")
			
		Next
		
	End
	
	
	Method Eof:Bool()
		if Self.currentLine &gt; (fileData.Length() - 1)
			Return True
			
		Else
			Return False
			
		EndIf
		
	End
	
	
	Method Purge:Void()
		Local counter:Int
		
		
		For counter = 0 to (fileData.Length() - 1)
			fileData[counter] = ""
			
		Next
		
		fileData =[]
		
	End
	
End

</pre> <br><br></td></tr></table><br>
<a name="2124508"></a>

<a name="2124512"></a>

<a name="2124514"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >zxretrosoft</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you friends! Thank you so much Steve!<br>I'll try it all.<br><br>I thought of another idea. Would not it be easier to do this using the framework Playniax? The code seems to me easier?<br><br><pre class=code>
Strict

Import playniax.ignitionx.framework.storage

Function Main:Int()
	New MyApp
	Return 0
End

Class MyApp Extends App

	Method OnCreate:Int()

		iSaveState("This is a test")

'		iEraseData()												
	
		iStorage.WriteString("MyGame/Player/Name", "Jason")
		iStorage.WriteInt("MyGame/Player/Lives", 5)
		iStorage.WriteFloat("MyGame/Cash", 100.5)

		iStorage.Save

		Print "Player: " + iStorage.ReadString("MyGame/Player/Name")
		Print "Lives: " + iStorage.ReadInt("MyGame/Player/Lives")
		Print "Money In the bank: $" + iStorage.ReadFloat("MyGame/Cash")

		Print iLoadState()

		Print ""
		Print "RAW data:"

		iStorage.Show													

		Return 0

	End

End

</pre> <br><br></td></tr></table><br>
<a name="2124513"></a>

<a name="2124517"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Steve Ancell</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> If the Playniax framework works for you then it's all good. ;-) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
