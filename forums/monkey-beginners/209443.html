<!DOCTYPE html><html lang="en" ><head ><title >Platform Game Example</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Platform Game Example</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=528" >Monkey Beginners</a>/<a href="#bottom" >Platform Game Example</a><br><br>
<a name="2098092"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >En929</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I was wondering how do some of you handle platforms when you write platform games like Mario. For example, when Mario stands on top of a block vs.when he is under the same  block and can can't go through it, etc. How do you write that? I just need to see an example for one platform and one character.<br><br>Thanks <br><br></td></tr></table><br>
<a name="2098095"></a>

<a name="2098096"></a>

<a name="2098097"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pakz</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I make platformer game examples for my blog. I have a number of platformer examples on my blog. Below is one example of a map with platforms where a player can bump into and be ontop of platforms. The code can be copied and pasted into monkey and will work without extra media needed.<br><br><a href="http://monkeygameprogramming.blogspot.nl/2015/01/monkey-x-platformer-movementplayermap.html" target="_blank">http://monkeygameprogramming.blogspot.nl/2015/01/monkey-x-platformer-movementplayermap.html</a><br><br>Browse my blog for more examples.<br><br>I was planning on making a example with rotating platforms where the player can jump on/off.<br><br>edit :<br><br>I modified the map array to only have one platform. code below. If you have questions about the code then ask in this thread. Or if you need just the example of the player moving/jumping and one tile with bumping on standing on then let me know.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import mojo

Const playerwidth:Int=32
Const playerheight:Int=32
Global playerx:Float = 640/2-16
Global playery:Float = 480/2
Global pincy:Float
Global playerjump:Bool=False
Const tilewidth = 32
Const tileheight = 32
Const mapwidth:Int=20
Const mapheight:Int=10
Global map:Int[][] = [      [1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1],
                            [1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1],
                            [1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1],
                            [1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1],
                            [1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1],
                            [1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1],
                            [1,0,0,0,0,0,1,0,0,0,0,0,0,0,0,0,0,0,0,1],
                            [1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1],
                            [1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1],
                            [1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1] ]
Class MyGame Extends App
    Method OnCreate()
        SetUpdateRate(60)
    End
    Method OnUpdate()
        ' Player left and right movement
        playermovement()        
        ' player gravity
        playergravity()
    End
    Method OnRender()
        Cls(0,0,0)
        SetColor(255,255,255)
        ' draw the map
        For Local y:Int = 0 Until mapheight
        For Local x:Int = 0 Until mapwidth
            If map[y][x] = 1 Then DrawRect(x*tilewidth,y*tileheight,tilewidth,tileheight)
        End
        End
        DrawText "Platformer Example",10,10
        DrawText "Use cursor left/right and space bar",160,10
        ' draw the player
        SetColor 255,255,0        
        DrawOval playerx,playery,playerwidth,playerheight
    End
End

Function playermovement:Void()
       If KeyDown(KEY_RIGHT)
           For Local i=0 Until 2
            If playertc(1,0) = False
                   playerx+=1
            End If
           Next
       End If
       If KeyDown(KEY_LEFT)
           For Local i=0 Until 2    
               If playertc(-1,0) = False
                   playerx-=1
               End If
           Next
       End If
End Function

Function playergravity:Void()
        ' If the player is on the ground and the space bar is pressed
        If playerjump = False And playertc(0,1) = False
            playerjump = True
            pincy = 0
        End If
        If playerjump = False And KeyDown(KEY_SPACE) = True
            pincy = -4
            playerjump = True
        End
        'If the player is in the jump
        If playerjump = True
            pincy += 0.1
            'if the player is going up
            If pincy &lt;=0
                For Local i:Int = 0 Until Abs(pincy)                
                    playery -= 1
                    If playertc(0,-1) = True
                        pincy = 0
                        Exit
                    End If
                End
            End
            ' if the player if going down
            If pincy &gt; 0
                For Local i:Int = 0 Until pincy
                    playery += 1
                    'if the player touches the ground
                    If playertc(0,1) = True
                        playerjump = False                        
                        Exit
                    End
                End
            End
        End
End Function

Function playertc:Bool(x1:Int=0,y1:Int=0)
    Local cx = (playerx + x1) / tilewidth
    Local cy = (playery + y1) / tileheight
    For Local y2=cy-1 Until cy+2
    For Local x2=cx-1 Until cx+2
        If x2&gt;=0 And x2&lt;mapwidth And y2&gt;=0 And y2&lt;mapheight
            If map[y2][x2] &gt; 0
                If rectsoverlap(playerx+x1,playery+y1,playerwidth,playerheight,x2*tilewidth,y2*tileheight,tilewidth,tileheight) = True
                    Return True
                End If
            End If
        End If
    Next
    Next
    Return False
End Function


Function rectsoverlap:Bool(x1:Int, y1:Int, w1:Int, h1:Int, x2:Int, y2:Int, w2:Int, h2:Int)
    If x1 &gt; (x2 + w2) Or (x1 + w1) &lt; x2 Then Return False
    If y1 &gt; (y2 + h2) Or (y1 + h1) &lt; y2 Then Return False
    Return True
End

Function Main()
    New MyGame()
End
</textarea><br><br>the map array hold the platforms. the way it is coded it treats every 1 value as a platform. It uses the rectsoverlap function to see if the coordinates of the player is inside a platform and then things are handled.<br><br>Things like player movement and jumping is difficult for beginners to. I have a example of only that on my blog. You should start with collision and player movement and jumping. Then tilemaps for that is what games like mario uses. You check every one pixel movement of the player with the nearest platforms on the map. If he is jumping and is inside a tile with the next movement then he changes to falling down(bump)<br>when he is falling down and is inside a platform in the next step of movement then the jump is stopped and he does not fall down anymore. <br><br></td></tr></table><br>
<a name="2098201"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >En929</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Pakz, that was a perfect example and was exactly what I needed. Thanks! <br><br></td></tr></table><br>
<a name="2098229"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's another example too:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict
Import mojo

Global myGame:MyGame

Function Main:Int()
	myGame = New MyGame()
	Return True
End

Class MyGame Extends App
	Field level:Level
	Field player:Player
	
	Method OnCreate:Int()
		level = New Level()
		player = New Player(100, 100, 32, 32)
		player.speedX = 3
		player.speedY = 7
    	
		SetUpdateRate(60)
		Return True
	End
    
	Method OnUpdate:Int()
		player.Update()
		Return True
	End
    
	Method OnRender:Int()
		level.Draw()
		player.Draw()
		Return True
    End
End

Class Level
	Const TILE_WIDTH:Int = 32
	Const TILE_HEIGHT:Int = 32
	Const MAP_WIDTH:Int = 20
	Const MAP_HEIGHT:Int = 15
	Const GRAVITY:Float = 0.2
	Field map:Int[][] = [[1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1],
						[1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1],
						[1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1],
						[1,0,0,0,0,0,0,0,0,0,1,0,0,0,0,0,0,0,0,1],
						[1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1],
						[1,0,0,0,0,0,0,0,0,0,0,0,0,0,1,0,0,0,0,1],
						[1,0,1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1],
						[1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1],
						[1,0,0,0,0,0,0,0,0,1,0,0,0,0,0,0,0,0,0,1],
						[1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1],
						[1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1],
						[1,0,0,0,0,0,1,0,0,0,0,0,0,1,0,0,0,1,0,1],
						[1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1],
						[1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1],
						[1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1] ]
                            
	Method Draw:Void()
		Cls(0, 0, 0)
		SetColor(0, 200, 0)
        For Local y:Int = 0 Until MAP_HEIGHT
	        For Local x:Int = 0 Until MAP_WIDTH
    	        If map[y][x] = 1 Then DrawRect(x * TILE_WIDTH, y * TILE_HEIGHT, TILE_WIDTH, TILE_HEIGHT)
	        End
        End
		SetColor(255, 255, 255)
	End
	
	Method CheckCollision:Int(startX:Float, startY:Float, endX:Float, endY:Float)
		' use Bresenham Line
		Local x1:Int = startX
		Local y1:Int = startY
		Local x2:Int = endX
		Local y2:Int = endY

		Local deltax:Int = Abs(x2 - x1)
		Local deltay:Int = Abs(y2 - y1)
		
		Local numpixels:Int, d:Int, dinc1:Int, dinc2:Int, xinc1:Int, xinc2:Int, yinc1:Int, yinc2:Int, x:Int, y:Int, i:Int
		Local xx:Int, cx:Int
		Local yy:Int, cy:Int
		Local tile:Int
		
		If deltax &gt;= deltay 
			numpixels = deltax + 1
			d = (2 * deltay) - deltax
			dinc1 = deltay Shl 1
			dinc2 = (deltay - deltax) Shl 1
			xinc1 = 1
			xinc2 = 1
			yinc1 = 0
			yinc2 = 1
		Else 
			numpixels = deltay + 1
			d = (2 * deltax) - deltay
			dinc1 = deltax Shl 1
			dinc2 = (deltax - deltay) Shl 1
			xinc1 = 0
			xinc2 = 1
			yinc1 = 1
			yinc2 = 1
		End
		
		If x1 &gt; x2
			xinc1 = -xinc1
			xinc2 = -xinc2
		End
		
		If y1 &gt; y2 
			yinc1 = -yinc1
			yinc2 = -yinc2
		End
		
		x = x1
		y = y1

		For i = 1 To numpixels
			xx = Floor(x / TILE_WIDTH)
			yy = Floor(y / TILE_HEIGHT)
			tile = map[yy][xx]
			' exit when we have a collision
			If tile &lt;&gt; 0 Then
				cx = x
				cy = y
				Exit
			End
			
			If d &lt; 0 
				d = d + dinc1
				x = x + xinc1
				y = y + yinc1
			Else
				d = d + dinc2
				x = x + xinc2
				y = y + yinc2
			End
		
		Next
		Return tile
	End
End

Class Player Extends Sprite
	Field jumping:Int
	
	Method New(x:Float, y:Float, w:Float, h:Float)
		Self.x = x
		Self.y = y
		Self.w = w
		Self.h = h		
	End
	
	Method Update:Void()
		Controls()
	End
	
	Method Controls:Void()
		Local tempX:Float
		Local tempY:Float
		Local collisionArray:Int[3]
		Local left:Float = x - w / 2
		Local right:Float = x + w / 2
		Local midX:Float = x
		Local top:Float = y - (h / 2) + 1
		Local midY:Float = y
		Local bottom:Float = y + (h / 2) - 1
			
		If KeyDown(KEY_LEFT)
			tempX = x - speedX
			Local leftPlusMovement:Float = tempX - w / 2
			collisionArray[0] = myGame.level.CheckCollision(left, top, leftPlusMovement, top)
			collisionArray[1] = myGame.level.CheckCollision(left, midY, leftPlusMovement, midY)
			collisionArray[2] = myGame.level.CheckCollision(left, bottom, leftPlusMovement, bottom)			
			
			If collisionArray[0] = 0 And collisionArray[1] = 0 And collisionArray[2] = 0
				x = tempX
			End
		
		End
		If KeyDown(KEY_RIGHT)
			tempX = x + speedX
			Local rightPlusMovement:Float = tempX + w / 2
			collisionArray[0] = myGame.level.CheckCollision(right, top, rightPlusMovement, top)
			collisionArray[1] = myGame.level.CheckCollision(right, midY, rightPlusMovement, midY)
			collisionArray[2] = myGame.level.CheckCollision(right, bottom, rightPlusMovement, bottom)			
				
			If collisionArray[0] = 0 And collisionArray[1] = 0 And collisionArray[2] = 0
				x = tempX
			End
		End
		If KeyDown(KEY_SPACE) And Not jumping Then
			dy =- speedY
			jumping = True
		End
		If Not jumping
			collisionArray[0] = myGame.level.CheckCollision(left, bottom, left, bottom + 1)
			collisionArray[1] = myGame.level.CheckCollision(midX, bottom, midX, bottom + 1)
			collisionArray[2] = myGame.level.CheckCollision(right, bottom, right, bottom + 1)
			If collisionArray[0] = 0 And collisionArray[1] = 0 And collisionArray[2] = 0
				jumping = True
			End
		End
		If jumping
			dy += myGame.level.GRAVITY
			tempY = y + dy
			If dy &lt;&gt; 0
				If dy &gt; 0 ' falling
					Local bottomPlusMovement:Float = tempY + h / 2
					
					collisionArray[0] = myGame.level.CheckCollision(left, bottom, left, bottomPlusMovement)
					collisionArray[1] = myGame.level.CheckCollision(midX, bottom, midX, bottomPlusMovement)
					collisionArray[2] = myGame.level.CheckCollision(right, bottom, right, bottomPlusMovement)

					' did we hit the ground?
					If collisionArray[0] &lt;&gt; 0 Or collisionArray[1] &lt;&gt; 0 Or collisionArray[2] &lt;&gt; 0
						dy = 0
						jumping = False
					Else
						y = tempY
					End
				Else ' jumping
					Local topPlusMovement:Float = tempY - h / 2
					
					collisionArray[0] = myGame.level.CheckCollision(left, top, left, topPlusMovement)
					collisionArray[1] = myGame.level.CheckCollision(midX, top, midX, topPlusMovement)
					collisionArray[2] = myGame.level.CheckCollision(right, top, right, topPlusMovement)

					' did we bang our head?
					If collisionArray[0] &lt;&gt; 0 Or collisionArray[1] &lt;&gt; 0 Or collisionArray[2] &lt;&gt; 0
						dy = 0
					Else
						y = tempY
					End
				End
			End
		End
	End
	
	Method Draw:Void()
		SetColor(100, 100, 255)
		DrawRect(x - w / 2, y - h / 2, w, h)
		SetColor(255, 255, 255)
	End
End

Class Sprite
	Field x:Float, y:Float
	Field w:Float, h:Float
	Field dx:Float, dy:Float
	Field speedX:Float, speedY:Float
End
</textarea><br><br>This one uses ray casting for its collision detection, this allows you to have really fast objects moving around the map. <br><br></td></tr></table><br>
<a name="2098228"></a>

<a name="2098227"></a>

<a name="2098232"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >En929</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> therevillis, that was an excellent example. That way was helpful too. Thanks for making that. <br><br></td></tr></table><br>
<a name="2098268"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >vic</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks guys... for both examples :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
