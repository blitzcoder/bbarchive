<!DOCTYPE html><html lang="en" ><head ><title >Need Game Examples and HELP</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Need Game Examples and HELP</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=528" >Monkey Beginners</a>/<a href="#bottom" >Need Game Examples and HELP</a><br><br>
<a name="2109651"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rell</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> 1. How to put up a score and a counter system ex.score 1000 and counter go up by 1<br>2.How to make a tic-tac-toe example<br>3.How to make a health bar and a xp bar<br>4. How to make different rooms for characters ex. room 1, room2, room3, etc and title screen<br>Thanks again <br><br></td></tr></table><br>
<a name="2109653"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Duke87</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Maybe this helps you to find a starting Point.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
#rem
	This is a Plain Maniac Template.
	Just Copy this Source File and add your Game Code.
#end 

Import mojo

#rem 
	##### DESKTOP PREPROCESSOR DIRECTIVES  ####
#end 
#GLFW_USE_MINGW=True
#GLFW_WINDOW_TITLE="Forum Help"
#GLFW_WINDOW_WIDTH=1600
#GLFW_WINDOW_HEIGHT=780
#GLFW_WINDOW_SAMPLES=0
#GLFW_WINDOW_DECORATED=false  
#GLFW_WINDOW_RESIZABLE=False
#GLFW_WINDOW_FULLSCREEN=false  
#GLFW_SWAP_INTERVAL=-1

#rem 
	##### ANDROID PREPROCESSOR DIRECTIVES  ####
#end 
#ANDROID_APP_LABEL="Forum Help"
#ANDROID_APP_PACKAGE="de.monkey.forumhelp"
#ANDROID_SCREEN_ORIENTATION="portrait"					'"user", "portrait", "landscape"
#ANDROID_GAMEPAD_ENABLED=False
#ANDROID_VERSION_CODE="1"
#ANDROID_VERSION_NAME="1.0"
#ANDROID_KEY_STORE="../../release-key.keystore"
#ANDROID_KEY_ALIAS="release-key-alias"
#ANDROID_KEY_STORE_PASSWORD="password"
#ANDROID_KEY_ALIAS_PASSWORD="password"
#ANDROID_SIGN_APP=false
#ANDROID_NATIVE_GL_ENABLED=False

#OPENGL_GLES20_ENABLED=False
#OPENGL_DEPTH_BUFFER_ENABLED=false

#MOJO_AUTO_SUSPEND_ENABLED=True
#MOJO_IMAGE_FILTERING_ENABLED=false

#TEXT_FILES="*.txt|*.xFalsejson"
#IMAGE_FILES="*.png|*.jpg"
#SOUND_FILES="*.wav|*.ogg"
#MUSIC_FILES="*.wav|*.ogg"
#BINARY_FILES="*.bin|*.dat"



Function Main:Int()
	New MyApp						
	Return 0
End Function


Class MyApp Extends App
	
	Field maxValue:float = 100
	Field currValue:Float = 0
	Field lastUpdate:Int 
	
	Field startTime:Int 
	
	Method OnCreate:Int()
		SetUpdateRate 60 

		lastUpdate = Millisecs()
		startTime = Millisecs()
	End Method
	
	Method OnUpdate:Int()


		#rem
			This is kind of Counter.
			It counts every '100 ms' +1
			change the 100 to see what happens
		#end 
		If Millisecs() - lastUpdate &gt; 100
			currValue += 1
			If currValue &gt;= maxValue
				currValue = 0
			Endif 
			lastUpdate = Millisecs()
		Endif 
		
		
		If KeyHit(KEY_ENTER)
			startTime = Millisecs()
		Endif 
		
		If KeyHit(KEY_SPACE)
			currValue = 0
		Endif 
	End Method
	
	Method OnRender:Int()
		Cls 
	
		#rem
			Kind of Loading Bar, you have to set up an 'extern' counting System.
		#end 
		Drw_LoadingBar(10,10,200,20,"bla",currValue,maxValue)
		
		
		#rem
			This is a simple 'timing bar'. 
			Including own Timing System.
			it Only needs the 'starting time' as Millisecs, and the duration as millisecs.
		#end 
		Drw_TimingBar(10,50,200,20,startTime,5000)
		
		DrawText "Press Enter to reset Time, or Space to reset currValue",10,100
	End Method 

End Class 


#Rem monkeydoc
	### READY TO USE ###~n
	This Function Draws a Loading-Bar at the Position _X,_Y with the Dimensions _Witdth and _Height 
	You must call this within the OnRender() Method
	This Function calculates the percentage by its own, by giving
#End
Function Drw_LoadingBar(_X:Float,_Y:Float,_Width:Float,_Height:Float,_Caption:String,_CurrValue:Float,_MaxValue:Float)
	Local pr:Float = _CurrValue/_MaxValue

	SetColor 75,75,75
	DrawRect(_X,_Y,_Width*pr,_Height)
	
	SetColor 255,255,255
	Drw_Rect(_X,_Y,_Width,_Height,2)
	
End Function


#Rem monkeydoc
	This Functions Draws an LoadingBar @ position ix,iy with w,h in Pixels
	istartTime is the Beginning Time, length is the max Time (both in ms)
#End
Function Drw_TimingBar(ix:Int,iy:Int,w:Int,h:Int,istartTime:Int,ilengthTime:Int,iStyle:Int = 0)

	Drw_Rect(ix,iy,w,h,2)
	Local lw:Int = ( ( Millisecs()-istartTime ) * w ) / ilengthTime
	
	If w - lw &gt; 0
		DrawRect(ix,iy,w-lw,h)
	Endif 
	
	
End Function

Function Drw_Rect:Int(_X:Float,_Y:Float,_Width:Float,_Height:Float,_Thickness:Int=1)

	
	'### Drawing the Outline Rect with Thickness in Pixels for the Frame
	For Local i:Int = 0 until _Thickness
		'Obere Kante
		DrawLine (_X-i,_Y-i,_X +_Width+i,_Y-i)
		
		'Untere Kante
		DrawLine (_X-i,_Y+_Height+i,_X+_Width+i,_Y+_Height+i)
		
		'Linke Seite
		DrawLine (_X-i,_Y-i,_X-i,_Y+_Height+i)
		
		'Recht Seite
		DrawLine (_X+_Width+i,_Y-i,_X+_Width+i,_Y+_Height+i)
	Next 
	

End Function
</textarea><br><br>Bye Duke <br><br></td></tr></table><br>
<a name="2109652"></a>

<a name="2109654"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Duke87</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Give me a Second, i try to programm a Tic Tac Toe as simple as can be. I just need to extract some of my Framework Code to create a stand-alone App for you. <br><br></td></tr></table><br>
<a name="2109655"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Duke87</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> So, here's a little TicTacToe Game.<br>There is a little failure on checking for winner. Don't know why. But i have no time to check it now.<br><br>Actually i would programm the game a little bit different, but this would be more confusing. (just was hacking the code).<br><br>But it may help you answering some of your Questions :D<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
#rem
	This is a Plain Maniac Template.
	Just Copy this Source File and add your Game Code.
#end 

Import mojo

#rem 
	##### DESKTOP PREPROCESSOR DIRECTIVES  ####
#end 
#GLFW_USE_MINGW=True
#GLFW_WINDOW_TITLE="Forum Help"
#GLFW_WINDOW_WIDTH=1600
#GLFW_WINDOW_HEIGHT=780
#GLFW_WINDOW_SAMPLES=0
#GLFW_WINDOW_DECORATED=false  
#GLFW_WINDOW_RESIZABLE=False
#GLFW_WINDOW_FULLSCREEN=false  
#GLFW_SWAP_INTERVAL=-1

#rem 
	##### ANDROID PREPROCESSOR DIRECTIVES  ####
#end 
#ANDROID_APP_LABEL="Forum Help"
#ANDROID_APP_PACKAGE="de.monkey.forumhelp"
#ANDROID_SCREEN_ORIENTATION="portrait"					'"user", "portrait", "landscape"
#ANDROID_GAMEPAD_ENABLED=False
#ANDROID_VERSION_CODE="1"
#ANDROID_VERSION_NAME="1.0"
#ANDROID_KEY_STORE="../../release-key.keystore"
#ANDROID_KEY_ALIAS="release-key-alias"
#ANDROID_KEY_STORE_PASSWORD="password"
#ANDROID_KEY_ALIAS_PASSWORD="password"
#ANDROID_SIGN_APP=false
#ANDROID_NATIVE_GL_ENABLED=False

#OPENGL_GLES20_ENABLED=False
#OPENGL_DEPTH_BUFFER_ENABLED=false

#MOJO_AUTO_SUSPEND_ENABLED=True
#MOJO_IMAGE_FILTERING_ENABLED=false

#TEXT_FILES="*.txt|*.xFalsejson"
#IMAGE_FILES="*.png|*.jpg"
#SOUND_FILES="*.wav|*.ogg"
#MUSIC_FILES="*.wav|*.ogg"
#BINARY_FILES="*.bin|*.dat"



Function Main:Int()
	New MyApp						
	Return 0
End Function


Class MyApp Extends App
	
	Field plMgr:PlayerManager
	Field map:TicToeMap 
	
	Method OnCreate:Int()
		SetUpdateRate 60 
		
		map = New TicToeMap(100,100,300,300)
		plMgr = New PlayerManager()
	End Method
	
	Method OnUpdate:Int()

		plMgr.Update(map)
	End Method
	
	Method OnRender:Int()
		Cls 

		plMgr.Draw()
		map.Draw()
		
	End Method 

End Class 


#Rem monkeydoc
	### READY TO USE ###~n
	This Function Draws a Loading-Bar at the Position _X,_Y with the Dimensions _Witdth and _Height 
	You must call this within the OnRender() Method
	This Function calculates the percentage by its own, by giving
#End



Class TicToeMap
	Field arrMap:Int[][]
	Field X:Float,Y:Float,Width:Float,Height:Float 
	
	Field cellWidth:Float,cellHeight:Float 
	
	Method resetMap()
		For Local x:Int = 0 Until 3
			For Local y:Int = 0 Until 3
				arrMap[x][y] = 0
			Next
		Next 
	End Method 
	
	Method New(_X:Float,_Y:Float,_Width:Float,_Height:Float)
		arrMap = Array2D(3,3,0)
		
		X = _X
		Y = _Y
		Width =_Width
		Height = _Height
		
		cellWidth	= Width/3.0
		cellHeight 	= Height/3.0
	End Method 

	Method Draw()
		Drw_Grid(X,Y,Width,Height,3,3)
		
		
		DrawText "Cursor Pos: " + ll_GridX(cellWidth,X) + " | " + ll_GridY(cellHeight,Y),0,0
		
		For Local x:Int = 0 Until 3
			For Local y:Int = 0 Until 3
				Select arrMap[x][y]
					Case 1
						Drw_Circle(X+x*cellWidth + cellWidth/2,Y+y*cellHeight + cellHeight/2,cellWidth*0.8)
					Case 2
						Drw_Cross(X+x*cellWidth + cellWidth/2,Y+y*cellHeight + cellHeight/2,cellWidth*0.8,cellHeight*0.8)
					Default 
				End Select 
			Next 
		Next 
	End Method 
	
	Method getFieldX:Int()
		Return ll_GridX(cellWidth,X)
	End Method
	
	Method getFieldY:Int()
		Return ll_GridY(cellHeight,Y)
	End Method 
	
	Method setMapField:bool(_PosX:Int,_PosY:Int,_PlayerID:Int)
	
		If _PosX &gt;= 0 And _PosX &lt; 3 And _PosY &gt;= 0 And _PosY &lt; 3
			If arrMap[_PosX][_PosY] = 0
				arrMap[_PosX][_PosY] = _PlayerID
				Return True 
			Endif 
		End If 
	
		Return False 
	End Method 
	
	Method checkWinner:Int()
	
		If checkHorizontal() &lt;&gt; 0
			Return checkHorizontal()
		Endif 
		If checkVertical() &lt;&gt; 0
			Return checkVertical()
		Endif 
		If checkDiagonal() &lt;&gt; 0
			Return checkDiagonal()
		Endif 
		
		If checkDraw() = True 
			Return 3
		Endif 
	End Method 
	
	Private 
	Method checkHorizontal:Int()
		For Local y:Int = 0 Until 3
			
			If arrMap[0][y] = arrMap[1][y] And arrMap[1][y] = arrMap[2][y]
				Return arrMap[0][y]
			Endif 
		Next 
	End Method 
	
	Method checkVertical:Int()
		For Local x:Int = 0 Until 3
			
			If arrMap[x][0] = arrMap[x][1] And arrMap[x][1] = arrMap[x][2]
				Return arrMap[x][0]
			Endif 
		Next 
	End Method
	
	Method checkDiagonal:Int()
		
		If arrMap[0][0] = arrMap[1][1] And arrMap[1][1] = arrMap[2][2]
			Return arrMap[0][0]
		End If 
		
		If arrMap[0][2] = arrMap[1][1] And arrMap[1][1] = arrMap[2][0]
			Return arrMap[0][2]
		End If 
		
		Return 0
	End Method 
	
	Method checkDraw:bool()
		Local cnt:Int = 0
		For Local x:Int = 0 Until 3
			For Local y:Int = 0 Until 3
				If arrMap[x][y] &lt;&gt; 0
					cnt +=1
				Endif 
			Next
		Next 
		If cnt = 9
			Return True 
		Endif 
		Return False 
	End Method 

End Class 


Class PlayerManager

	Field activePlayerID:Int = 1
	Field nrPlayer:Int = 2
	
	Field waitTimer:Int = 3000
	Field lastPlayerAction:Int 
	
	Field player1Fails:Int = 0
	Field player2Fails:Int = 0
	
	Field player1Wins:Int 
	Field player2Wins:Int 
	
	Method New()
	
		lastPlayerAction = Millisecs()
		
	End Method 
	
	Method Draw()
	
		DrawText "Current PlayerID: " + activePlayerID,200,10
		If Drw_TimingBar(10,50,200,20,lastPlayerAction,waitTimer) = False  
			Select activePlayerID
				Case 1
					player1Fails += 1
				Case 2
					player2Fails += 1
			End Select 
			lastPlayerAction = Millisecs()
		Endif 
		
		DrawText "Player1",10,400
		DrawText "Wins: " +player1Wins,10,420
		DrawText "Fails: " + player1Fails , 10,440
		Drw_LoadingBar(75,440,100,15,"",player1Fails,5)
		
		DrawText "Player2" , 300,400
		DrawText "Wins: " + player2Wins, 300,420
		DrawText "Fails: " + player2Fails , 300,440
		Drw_LoadingBar(365,440,100,15,"",player2Fails,5)
	End Method 
	Method Update:Void(_map:TicToeMap)
	
		If TouchHit()
		
			Local x:Int = _map.getFieldX()
			Local y:Int = _map.getFieldY()
			If _map.setMapField(x,y,activePlayerID) = True
				nextPlayer()
				lastPlayerAction = Millisecs()
			Endif 
			
			Local winner:Int = _map.checkWinner()
			If winner &lt;&gt; 0
				Select winner
					Case 1
						player1Wins += 1
					Case 2
						player2Wins += 1
					Case 3	'Draw
						
				End Select 
				_map.resetMap()
				Print "Winner is: " + winner
			Endif 
			
		End If 
	End Method 

	Method nextPlayer()
		If activePlayerID = nrPlayer
			activePlayerID = 1
		Else
			activePlayerID +=1
		Endif 
	End Method 
End Class 


#Rem monkeydoc
	This function creates a new 2 Dimensional Int Array with i and j dimensions.
	optionaly You can add a initial Value fill. Standard fill value will then be 0.
	
	&lt;pre&gt;
	Import maniac
	
	Function Main:int()
		local array:Int[][] = Array2D(10,5)
	End Function 
	&lt;/pre&gt;
#End
Function Array2D:Int[][] (i:Int, j:Int,fill:Int = 0)
	
    Local arr:Int[][] = New Int[i][]
    For Local ind = 0 Until i
        arr[ind] = New Int[j]
    Next
    
    For Local w:Int = 0 Until i
    	For Local h:Int = 0 Until j
    		arr[w][h] = fill
    	Next
    Next 
    Return arr		
End


#Rem monkeydoc
	### READY TO USE ###~n
	This Function Draws a Grid at the Position _X,_Y with the Dimensions _Witdth and _Height and the number of cells in x and y direction
	You must call this within the OnRender() Method
#End
Function Drw_Grid:Void(_X:Float,_Y:Float,_Width:Float,_Height:Float,_CellsX:Int,_CellsY:Int,_Angle:Float = 0.0)

	'zeichnet ein Gitter an Position posx,posy (links oben), w/h breite bzw. hÃƒÂ¶he (des gesamten Grids) und zieht dabei cntx/cnty striche in der horizontalen,bzw vertikalen
	'allerdings wird hierbei das Gitter automatisch angepasst
	'### Rotate The Matrix at the MidX/MidY Position around Angle

	'1. bestimmen der breite eines KÃƒÂ¤stchens anhand der Breite und der anzahl
	Local cell_w:Float = _Width/_CellsX
	Local cell_h:Float = _Height/_CellsY

	For Local x:Int = 1 To _CellsX-1
		'Vertikale
		DrawLine(_X+x*cell_w,_Y,_X+x*cell_w,_Y+_CellsY*cell_h)	
	Next 
	
	For Local y:Int = 1 To _CellsY-1
		'Horizontale
		DrawLine(_X,_Y+y*cell_h,_X+_CellsX*cell_w,_Y+y*cell_h)
	Next 
	
End Function

Function ll_GridX:Int(width_p:float, offset:Int = 0)
	
	Return (MouseX() -offset) / (width_p)

End Function

Function ll_GridY:Int(width_p:float, offset:Int = 0)

	Return (MouseY() -offset) / (width_p)

End Function


Function Drw_Circle( posx:Float, posy:Float, _Radius:Float)
	
   	Local x:Float 
   	Local y:Float 
   	Local rx:Float = _Radius / 2
   	Local ry:Float = _Radius / 2

   	Local r:Int = Abs(rx) + Abs(ry)

   	x = 0
   	y = r
   
   	Local d:Int 
   	Local ddf_x:Int 
   	Local ddf_y:Int 
   
   	d = 1 - r
   	ddf_x = 3
	ddf_y = -2 * r + 5
   
   	While y &gt;= x And r

	    DrawPoint( posx + x * rx/r, posy + y * ry/r )
	    DrawPoint( posx + y * rx/r, posy + x * ry/r )
	    DrawPoint( posx + y * rx/r, posy - x * ry/r )
	    DrawPoint( posx + x * rx/r, posy - y * ry/r )
	    DrawPoint( posx - x * rx/r, posy - y * ry/r )
	    DrawPoint( posx - y * rx/r, posy - x * ry/r )
	    DrawPoint( posx - y * rx/r, posy + x * ry/r )
	    DrawPoint( posx - x * rx/r, posy + y * ry/r )
	       
	    If d &lt; 0
	       d= d + ddf_x
	       ddf_x= ddf_x+ 2
	       ddf_y=ddf_y+ 2
	       x=x + 1
	    Else
	       d= d+ ddf_y
	       ddf_x=ddf_x+ 2
	       ddf_y=ddf_y+ 4
	       x=x+ 1
	       y=y- 1  
	    EndIf
	Wend
End Function 


Function Drw_Cross(posx:Float,posy:Float,width:Float,height:Float)

	DrawLine ( posx - width/2,posy - height/2,posx + width/2,posy + height/2)
	DrawLine ( posx - width/2,posy + height/2,posx + width/2,posy - height/2)
	
End Function 

Function Drw_LoadingBar(_X:Float,_Y:Float,_Width:Float,_Height:Float,_Caption:String,_CurrValue:Float,_MaxValue:Float)
	Local pr:Float = _CurrValue/_MaxValue

	SetColor 75,75,75
	DrawRect(_X,_Y,_Width*pr,_Height)
	
	SetColor 255,255,255
	Drw_Rect(_X,_Y,_Width,_Height,2)
	
End Function


#Rem monkeydoc
	This Functions Draws an LoadingBar @ position ix,iy with w,h in Pixels
	istartTime is the Beginning Time, length is the max Time (both in ms)
#End
Function Drw_TimingBar:bool(ix:Int,iy:Int,w:Int,h:Int,istartTime:Int,ilengthTime:Int,iStyle:Int = 0)

	Drw_Rect(ix,iy,w,h,2)
	Local lw:Int = ( ( Millisecs()-istartTime ) * w ) / ilengthTime
	
	If w - lw &gt; 0
		DrawRect(ix,iy,w-lw,h)
		Return True 
	Endif 
	
	Return False 
End Function

Function Drw_Rect:Int(_X:Float,_Y:Float,_Width:Float,_Height:Float,_Thickness:Int=1)

	
	'### Drawing the Outline Rect with Thickness in Pixels for the Frame
	For Local i:Int = 0 until _Thickness
		'Obere Kante
		DrawLine (_X-i,_Y-i,_X +_Width+i,_Y-i)
		
		'Untere Kante
		DrawLine (_X-i,_Y+_Height+i,_X+_Width+i,_Y+_Height+i)
		
		'Linke Seite
		DrawLine (_X-i,_Y-i,_X-i,_Y+_Height+i)
		
		'Recht Seite
		DrawLine (_X+_Width+i,_Y-i,_X+_Width+i,_Y+_Height+i)
	Next 
	

End Function
</textarea> <br><br></td></tr></table><br>
<a name="2109839"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rell</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for the help <br><br></td></tr></table><br>
<a name="2109840"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rell</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Can someone help me with this can't seem to figure it out (syntax error expecting member declaration) I follow it from the learn monkey pdf what does it mean <br><br>Import mojo<br><br>Function Main()<br>	New Game<br>	End<br>	Class Game Extends App<br>			Field player:Image<br>			Field x:Float<br>			Field y:Float<br>Method OnCreate()<br>				player = LoadImage ("boing.png")<br>				SetUpdateRate 60<br>				<br>				End<br>				<br>				Method OnUpdate()<br>				<br>					If KeyDown (KEY_LEFT) Then x = x - 4<br>					If KeyDown (KEY_RIGHT) Then x = x + 4<br>					If KeyDown (KEY_UP) Then y = y - 4<br>					If KeyDown (KEY_DOWN) Then y = y + 4<br>					<br>					End<br>					<br>					Method OnRender()<br>					Cls 64, 94, 128<br>					DrawImage player, x, y<br>					<br>					End <br><br></td></tr></table><br>
<a name="2109844"></a>

<a name="2109841"></a>

<a name="2109842"></a>

<a name="2109843"></a>

<a name="2109845"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dawlane</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> You are missing End for the class declaration.<br>Instead of just using End use<br>End Method<br>End Class<br>EndIf<br>And use the keyword Strict at the beginning of the file. This means that you have to declare function/method types as well as return types.<br><pre class=code>Strict
Import mojo

Function Main:Int()
	New Game
	Return 0
End

Class Game Extends App
	Field player:Image
	Field x:Float
	Field y:Float
	Method OnCreate:Int()
		player = LoadImage ("boing.png")
		SetUpdateRate 60
		Return 0
	End Method

	Method OnUpdate:Int()
		If KeyDown (KEY_LEFT) Then x = x - 4
		If KeyDown (KEY_RIGHT) Then x = x + 4
		If KeyDown (KEY_UP) Then y = y - 4
		If KeyDown (KEY_DOWN) Then y = y + 4
		Return 0
	End Method

	Method OnRender:Int()
		Cls 64, 94, 128
		DrawImage player, x, y
		Return 0
	End Method
End Class</pre> <br><br></td></tr></table><br>
<a name="2109848"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rell</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks guy <br><br></td></tr></table><br>
<a name="2109870"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Duke87</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> by the way, if you show your code, please use [ codebox] ... [/ codebox]. makes the sources better readable :).<br><br>edit:<br>a little hint:<br><br>if you Load your images like:<br><pre class=code>
img = LoadImage ( "myimage.png",1,Image.MidHandle)
</pre><br>you set the handle to your image to the middle coordinates.<br><br>if you then use your images like:<br>DrawImage img,X,Y,_angle, scaleX,scaleY <br>you can easily rotate and scale them.<br><br>the scales i compute like the following:<br>scaleX = (sizeInPxl / img.Width() )<br><br>sizeInPxl means, you can scale them to the Pixelsize you want.<br>So you can load images with like 256x256 Pxl, but you just want have them like 128x128 Pxl scaled on Screen, write something like:<br><br><pre class=code>
DrawImage img,X,Y,0,128.0/img.Width(),128.0/img.Height()
</pre><br><br>Bye Duke <br><br></td></tr></table><br>
<a name="2109868"></a>

<a name="2109869"></a>

<a name="2109871"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Duke87</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>
Strict
Import mojo

Function Main:Int()
	New Game
	Return 0
End

Class Game Extends App
	Field player:Image
	Field x:Float
	Field y:Float
        Field angle:Float = 0
        field Pxl:float = 64
	Method OnCreate:Int()
		player = LoadImage ("boing.png",1,Image.MidHandle)
		SetUpdateRate 60
		Return 0
	End Method

	Method OnUpdate:Int()
		If KeyDown (KEY_LEFT) Then x = x - 4
		If KeyDown (KEY_RIGHT) Then x = x + 4
		If KeyDown (KEY_UP) Then y = y - 4
		If KeyDown (KEY_DOWN) Then y = y + 4
                If KeyDown (KEY_Q) then angle -= 1
                If KeyDown (KEY_E) then angle +=1
                if KeyDown (KEY_W) then Pxl +=1
                if KeyDown (KEY_S) then Pxl -=1
		Return 0
	End Method

	Method OnRender:Int()
		Cls 64, 94, 128
		DrawImage player, x, y,angle,Pxl / player.Width(),Pxl/player.Height()
		Return 0
	End Method
End Class
</pre> <br><br></td></tr></table><br>
<a name="2110167"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rell</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> what does syntax error expecting declaration means and how to solve it <br><br></td></tr></table><br>
<a name="2110207"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gerry Quinn</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> In the example above, you left out the End for your class declaration.  So when Monkey came to the bottom of your program, as far as it was concerned it was in the middle of you declaring your Game class, and you had just finished declaring the Render() method.  So it was expecting you to go on by declaring another method of the Game class.  <br><br>Of course ending the class works too, and that was what you meant to do.  But computers are not very good at understanding all possible contexts of an error message.  Generally when you get an error you should look around for something wrong nearby, but the problem is not always the most obvious thing from what the error says.<br><br>You should use tab indents to help keep your starts and ends balanced, as that's a very easy mistake to make (this applies to just about any programming language),<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Class Test

	Method AMethod:Void()
		'Do something
	End 
	
End
</textarea> <br><br></td></tr></table><br>
<a name="2110206"></a>

<a name="2110205"></a>

<a name="2110204"></a>

<a name="2110203"></a>

<a name="2110208"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rell</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> thanks alot Gerry <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
