<!DOCTYPE html><html lang="en" ><head ><title >Ini file library</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Ini file library</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=528" >Monkey Beginners</a>/<a href="#bottom" >Ini file library</a><br><br>
<a name="2107549"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >hub</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi !<br>I'm searching a way to parse an short ini file. (i only want read values)<br>So is there already a monkey code example to do this ?<br>Many Thanks. <br><br></td></tr></table><br>
<a name="2107557"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Shinkiro1</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi, I have written a module for this.<br><br><pre class=code>
'--------------------------------------------------------------------------
' File Format: Custom ini file
'
' [ Section ]
' value = property
' multilineValue = {--
' 0 1 2 3 4 5 6 7 8
' 9 10 11 12 13 14 15
' 16 17 18 19 20 21
' --}
'--------------------------------------------------------------------------

Strict
Import mojo.app

Class IniFile

	Method New(path:String)
		Local text:= LoadString(path)
		If text
			InitWithString(text, path)
		End
	End
	
	Method InitWithString:Void(iniString:String, name:String = "unnamed")
		Local lines:String[] = iniString.Split("~n")
		Local currentSection:IniSection
		Local multilineMode:Bool = False
		Local multilineData:String
		Local multilineProp:String
		sections = New StringMap&lt;IniSection&gt;
		AddSection(GLOBALS)
		currentSection = GetGlobals()
		
		For Local i:Int = 0 Until lines.Length
			Local line:String = lines[i].Trim()
			
			'comments
			If line.Length = 0 Or line.StartsWith("#")
				'Do nothing
				
			'read multiple lines
			ElseIf multilineMode
				If line = MULTILINE_END
					multilineMode = False
					SetProperty(currentSection, multilineProp, multilineData)
				Else
					multilineData += line + " "
				End
				
			'sections	
			ElseIf line.StartsWith("[")
				If line.EndsWith("]")
					Local sectionID:String = line[1..-1].Trim()
					If sections.Contains(sectionID)
						GenerateError("Section " + sectionID + " has already been declared! (on line " + (i+1) + ")", name)
					Else
						AddSection(sectionID)
						currentSection = GetSection(sectionID)
					End
				Else
					GenerateError("Sections must be closed! (on line " + (i+1) + ")", name)
				End
			
			'properties
			Else
				Local delimiterIndex:Int = line.Find(DELIMITER)
				If delimiterIndex = -1
					GenerateError("Attempting to parse line " + (i+1) + ": " + line + "~nbut could not find the delimiter " + DELIMITER, name)
				End
				Local leftSide:String = line[0..delimiterIndex].Trim()
				Local rightSide:String = line[delimiterIndex+1..].Trim()
				If rightSide = MULTILINE_START
					multilineMode = True
					multilineProp = leftSide
				Else
					SetProperty(currentSection, leftSide, rightSide)
				End
				
			End
		Next
		
		If multilineMode
			GenerateError("You forgot to close a multiline property with ~q" + MULTILINE_START + "~q", name)
		End
	End

	Method Get:String(value:String, fallback:String = "")
		Local g:= GetGlobals()
		If g
			Return g.GetString(value, fallback)
		End
		Return fallback
	End

	Method GetSection:IniSection(section:String)
		Return sections.Get(section)
	End
	
	Method GetGlobals:IniSection()
		Return GetSection(GLOBALS)
	End
	
	Method AddSection:Void(id:String)
		Local s:= New IniSection
		s.id = id
		s.data = New StringMap&lt;String&gt;
		sections.Add(id, s)
	End
	
	Method SetProperty:Void(section:IniSection, prop:String, value:String)
		If Not section Then Return
		If section.data.Contains(prop)
			section.data.Set(prop, value)
		Else
			section.data.Add(prop, value)
		End
	End
	
	Private
	Field sections:StringMap&lt;IniSection&gt; = New StringMap&lt;IniSection&gt;
	Const DELIMITER:String = "="
	Const GLOBALS:String = "_GLOBALS_"
	Const MULTILINE_START:String = "{--"
	Const MULTILINE_END:String = "--}"
	
	Method GenerateError:Void(message:String, name:String)
		Error("IniFile " + name + "~n" + message)
		sections = Null
	End
	
	'could be used for quote parsing
	Method ParseString:String(rawString:String)
		Local trimmed:String = rawString.Trim()
		Local findQuote:Int = trimmed.Find("~q")
		
		If findQuote = -1 Then Return trimmed
		
		If trimmed[0] = "~q"[0] And trimmed[trimmed.Length-1] = "~q"[0]
			Return trimmed[1..trimmed.Length-1]
		End
		
		Return ""	
	End
	
End


Class IniSection
	
	Method ID:String() Property
		Return id
	End
	
	Method GetString:String(prop:String, fallback:String = "")
		Local value:= data.Get(prop)
		If Not value
			Return fallback
		End
		Return value
	End
	
	Method GetInt:Int(prop:String, fallback:Int = 0)
		If Not data.Contains(prop)
			Return fallback
		End
		Return Int(data.Get(prop))
	End
	
	Method GetFloat:Float(prop:String, fallback:Float = 0.0)
		If Not data.Contains(prop)
			Return fallback
		End
		Return Float(data.Get(prop))
	End
	
	Method GetBool:Bool(prop:String, fallback:Bool = False)
		If Not data.Contains(prop)
			Return fallback
		End
		Local result:String = data.Get(prop)
		result = result.ToLower()
		Select result
			Case "0", "no", "false", "off"
				Return False
			Case "1", "yes", "true", "on"
				Return True
		End
		Return fallback
	End
	
	Private
	Field data:StringMap&lt;String&gt;
	Field id:String
End
</pre><br><br>Usage is like this<br><pre class=code>
Local ini:= New IniFile("path.ini")

Local globals:= ini.GetGlobals()
Print globals.GetInt("width")  'prints 100
Print globals.GetString("description") 'prints Hello World

Local colors:= ini.GetSection("ColorSection")
Print colors.GetFloat("red")  'prints 1.0
Print colors.GetFloat("green")  'prints 0.5
Print colors.GetFloat("blue")  'prints 0.2
Print colors.GetFloat("yellow", 0.8)  'prints 0.8 (fallback) because yelllow was not found
</pre><br><br>assuming an ini file like this<br><div class="quote"> <br># This is a comment, values below are in the global section<br>width = 100<br>description = Hello World<br><br>[ ColorSection ]<br>red = 1.0<br>green = 0.5<br>blue = 0.2<br> <br></div><br><br>-----<br>edit: added convinience method Get() which will use the global section automatically.<br><pre class=code>
Local ini:= New IniFile("path.ini")
Print ini.Get("width") 'prints 100
</pre> <br><br></td></tr></table><br>
<a name="2107555"></a>

<a name="2107554"></a>

<a name="2107671"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >hub</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Many thanks to share this Shinkiro1 <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
