<!DOCTYPE html><html lang="en" ><head ><title >can some one figure this out ??? or is there a bug</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >can some one figure this out ??? or is there a bug</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=528" >Monkey Beginners</a>/<a href="#bottom" >can some one figure this out ??? or is there a bug</a><br><br>
<a name="2125140"></a>

<a name="2125141"></a>

<a name="2125142"></a>

<a name="2125144"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dubbsta</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> i followed invader jims tutorial, im on video 4 in simple game at the part where he rendered the block player and the first block floor. the problem im having is the block floor is not rendering and i followed his code exact and spent hours comparing it...cant figure it out, dont want to continue till i do :/<br><br>this is main<br><pre class=code>

Import Falling


Const STATE_MENU:Int = 0
Const STATE_GAME:Int = 1
Const STATE_DEATH:Int = 2 



Class FallingGame Extends App

Field gameState:Int = STATE_MENU


Field gravity:Float = 0.2

Field Player1:Player = New Player(KEY_LEFT,KEY_RIGHT, 320, 50) 

Field Cam:Camera = New Camera()

Field mapWidth:Int
Field mapOffset:Float
  
Field blocks:List&lt;Block&gt; = New List&lt;Block&gt;()

		Method OnCreate()
		
			SetUpdateRate(60)
			
			GenerateFloor(3)
			
			mapWidth = SCREEN_WIDTH / TILE_WIDTH
			mapOffset = (SCREEN_WIDTH Mod TILE_WIDTH)/2
			
	    End 
	    
	    
	    
	    Method OnUpdate()
	   
	      Select gameState
	      
	      	Case STATE_MENU
	        	If KeyHit(KEY_ENTER)
	        	   gameState = STATE_GAME
	        	End 
	        	
	        Case STATE_GAME
	        	If KeyHit(KEY_R)
	        		Player1.Reset()
	        		Cam.Reset()
	        	End 
	        	
	            Player1.Update(gravity)	
	        	Cam.Update(1.0)	
	        
	        Case STATE_DEATH
	        
	        End 
	    End 
	    
	    
	    
	    Method OnRender()
	    
	    	Cls(0,0,0)
	    
	
	
	    	Select gameState
	    	
	    		Case STATE_MENU
	    		
	    			DrawText("Falling Game!", 320, 100, 0.5)
	     			DrawText("Press Enter to Play", 320, 400, 0.5)
	     		
	     		Case STATE_GAME
	     		
	     			PushMatrix()
	     			
	     			
	     			Translate(Cam.position.x, Cam.position.y)
	     			
	     			For Local block:Block = Eachin blocks
	     				block.Draw()
	     			End 
	     			
	     			Player1.Draw()
	     			
	     			PopMatrix()
	     			
	 			
	     		Case STATE_DEATH
	     		
	     	End 
	    
	    End 
	    
	    Method GenerateFloor(row:Int)
			For Local x:=0 Until mapWidth
			blocks.AddLast(New Block(mapOffset + TILE_WIDTH/2 + x*TILE_WIDTH, row*TILE_HEIGHT + TILE_HEIGHT/2))
			End 
		End 
		   
End 








Function Main()


 	New FallingGame()
 	
End 

</pre> <br><br><br>this is falling tab<br><br><pre class=code>
Import mojo

Const TILE_WIDTH:Int = 48
Const TILE_HEIGHT:Int = 48

Const SCREEN_WIDTH:Int = 640
Const SCREEN_HEIGHT:Int = 480



'''''''''''''''''''''''''''''''''''''
Class Vec2D
	Field x:Float
	Field y:Float
	
	
	Method New(x:Float=0, y:Float=0)
		Set(x,y)
	End 
	
	
	Method Set(x:Float, y:Float)
		Self.x= x
		Self.y= y
	End 
	
End 


Class Vec2Di
	Field x:Int 
	Field y:Int 
	
	
	Method New(x:Int =0, y:Int =0)
		Set(x,y)
	End 
	
	
	Method Set(x:Int , y:Int )
		Self.x= x
		Self.y= y
	End 
	
End 



''''''''''''''''''''''''''''''
Class Player
	
	Field originalPos:Vec2D
	Field position:Vec2D
	Field velocity:Vec2D
	
	
	Field speed:Float= 4.0
	
	Field leftkey:Int 
	Field rightkey:Int 
	
	
	Method New(leftkey:Int, rightkey:Int, x:Float, y:Float)
	
		originalPos = New Vec2D(x,y)
		position = New Vec2D(x,y)
		velocity = New Vec2D()
		
		Self.leftkey = leftkey
		Self.rightkey = rightkey
		
	
	End 
	
	
	Method Reset()
		position.Set(originalPos.x,originalPos.y) 
		velocity.Set(0,0)
	End 	
	
	
	
	Method Update(gravity:Float)
	
		velocity.x = 0 
		velocity.y += gravity
	
	
		If KeyDown(leftkey)
			velocity.x = -speed
	    End 
	    
	    If KeyDown(rightkey)
	    	velocity.x = speed 
	    End 
	    
	    position.x += velocity.x
	    position.y += velocity.y
	
	End 
	
	
	Method Draw()
		SetColor(0,255,0)
		
		DrawRect(position.x-16, position.y-16,32,32)
	End 
	
End 



''''''''''''''''''''''''
Class Camera

  Field originalPos:Vec2D
  Field position:Vec2D
  
  
  Method New(x:Float=0, y:Float=0)
  
  		position = New Vec2D(x,y)
  		originalPos = New Vec2D(x,y)
  End 
  
  
  Method Reset()
  	position.Set(originalPos.x, originalPos.y)
  	
  End 
  
  
  Method Update(fallSpeed:Float)
  
 		position.y -= fallSpeed 
 End 
  
End   


  
'''''''''''''''''''''' 
Class Block 
	Field position:Vec2D
	
	Method New(x:Float, y:Float)
	
		position = New Vec2D(x,y)
	End 
	
	
	Method Draw()
		SetColor(123,123,123)
		DrawRect(position.x-(TILE_WIDTH/2), position.y-(TILE_HEIGHT/2),TILE_WIDTH,TILE_HEIGHT)
  
  
  		SetColor(60,60,60)
  		DrawRect(position.x-((TILE_WIDTH-8)/2), position.y-((TILE_HEIGHT-8)/2),TILE_WIDTH-8,TILE_HEIGHT-8)

   
   End  
End 
  
  
'''''''''''''''''''''''' 
  
  
  
</pre> <br><br></td></tr></table><br>
<a name="2125143"></a>

<a name="2125146"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Paul - Taiphoz</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> You are calling Generate floor before you have set the floor width so those values are zero resulting in a zero loop meaning no floor. <br><br></td></tr></table><br>
<a name="2125159"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dubbsta</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> TY!!! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
