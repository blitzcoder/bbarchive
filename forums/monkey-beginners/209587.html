<!DOCTYPE html><html lang="en" ><head ><title >Wall following problems.</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Wall following problems.</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=528" >Monkey Beginners</a>/<a href="#bottom" >Wall following problems.</a><br><br>
<a name="2100392"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pakz</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I spend hours trying to get a feature from a platformer game that I saw on a stream working. It had ai that moved around platforms. I looked up how to do this wall following and did not find a lot of explanations. <br><br>One psuedo code I found was this.<br><br><pre class=code>
if nothing on the left side
  turn to the left 90 degrees
elseif nothing in fron
 move forward
else
turn to the right 90 degrees
end if
</pre><br><br>I modified the code multiple times and got it to do some kinds of movement around the map but it never really worked like it should.<br><br>I hope someone knows this wall following technique and can help me out with it.<br><br>This is the wall following code.<br><pre class=code>
	Method update()
		Local y1:Int=y/tileheight
		Local x1:Int=x/tilewidth
		' these variables are used to see if a tile is left of the ai
		' and if a tile is in front of the ai
		Local wallleft:Bool=True
		Local infront:Bool = True		
		Local angle2:Int=angle-90
		If angle2&lt;0 Then angle=270
		Local x2:Float=Cos(angle2)*tilewidth
		Local y2:Float=Sin(angle2)*tileheight
		x2/=tilewidth
		y2/=tileheight
		' see if a tile is left of the ai
		If map[y1+y2][x1+x2] = 0 Then wallleft = False
		x2=Cos(angle)*tilewidth
		y2=Sin(angle)*tileheight
		x2/=tilewidth
		y2/=tileheight
		' see if a tile is infront of te ai
		If map[y1+y2][x1+x2] = 0 Then infront = False
		' this handles the wall following.
		If wallleft = False' if nothing on the left then turn to the left
			angle-=90
		Elseif infront = False 'if nothing in front then move ahead
			x+=Cos(angle)*1
			y+=Sin(angle)*1
		Else ' if nothing is right then turn right
			angle+=90
		End If
		' keep the angle inside the 0-360 position
		If angle&lt;0 Then angle=270
		If angle&gt;360 Then angle=90
	End Method
</pre><br><br>Here is the complete code that runs.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import mojo

Const tilewidth = 32
Const tileheight = 32
Const mapwidth:Int=20
Const mapheight:Int=10
Global map:Int[][] = [      [1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1],
                            [1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1],
                            [1,0,0,0,0,0,0,0,0,0,2,0,0,0,0,0,0,0,0,1],
                            [1,0,0,0,0,0,0,0,1,1,1,1,1,0,0,0,0,0,0,1],
                            [1,0,0,0,0,0,0,0,1,0,0,0,1,0,0,0,0,0,0,1],
                            [1,0,0,0,0,0,1,1,1,0,0,0,1,1,0,0,0,0,0,1],
                            [1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1],
                            [1,1,1,1,0,0,0,0,0,0,0,0,0,0,0,0,1,1,1,1],
                            [1,1,1,1,0,0,0,0,0,0,0,0,0,0,0,0,1,1,1,1],
                            [1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1] ]

Class wallcrawler
	Field x:Float,y:Float,angle:Int
	Method New(x:Float,y:Float)
		Self.x = x
		Self.y = y
	End Method
	Method update()
		Local y1:Int=y/tileheight
		Local x1:Int=x/tilewidth
		' these variables are used to see if a tile is left of the ai
		' and if a tile is in front of the ai
		Local wallleft:Bool=True
		Local infront:Bool = True		
		Local angle2:Int=angle-90
		If angle2&lt;0 Then angle=270
		Local x2:Float=Cos(angle2)*tilewidth
		Local y2:Float=Sin(angle2)*tileheight
		x2/=tilewidth
		y2/=tileheight
		' see if a tile is left of the ai
		If map[y1+y2][x1+x2] = 0 Then wallleft = False
		x2=Cos(angle)*tilewidth
		y2=Sin(angle)*tileheight
		x2/=tilewidth
		y2/=tileheight
		' see if a tile is infront of te ai
		If map[y1+y2][x1+x2] = 0 Then infront = False
		' this handles the wall following.
		If wallleft = False' if nothing on the left then turn to the left
			angle-=90
		Elseif infront = False 'if nothing in front then move ahead
			x+=Cos(angle)*1
			y+=Sin(angle)*1
		Else ' if nothing is right then turn right
			angle+=90
		End If
		' keep the angle inside the 0-360 position
		If angle&lt;0 Then angle=270
		If angle&gt;360 Then angle=90
	End Method
	Method draw()
		SetColor 255,0,0
		DrawRect x,y,tilewidth,tileheight
	End Method
End Class

Class players
    Field x:Float = 640/2-16
    Field y:Float = 480/2
    Field pw:Int=32
    Field ph:Int=32
    Field incy:Float
    Field jump:Bool=False
    Method New()
    End Method
    Method update()
        playermovement
        playergravity
    End Method
    Method playergravity()
        ' If the player is on the ground and the space bar is pressed
        If jump = False And playertc(0,1) = False
            jump = True
            incy = 0
        End If
        If jump = False And KeyDown(KEY_SPACE) = True
            incy = -4
            jump = True
        End
        'If the player is in the jump
        If jump = True
            incy += 0.1
            'if the player is going up
            If incy &lt;=0
                For Local i:Int = 0 Until Abs(incy)                
                    y -= 1
                    If playertc(0,-1) = True
                        incy = 0
                        Exit
                    End If
                End
            End
            ' if the player if going down
            If incy &gt; 0
                For Local i:Int = 0 Until incy
                    y += 1
                    'if the player touches the ground
                    If playertc(0,1) = True
                        jump = False                        
                        Exit
                    End
                End
            End
        End
    End Method
    Method playermovement()
       If KeyDown(KEY_RIGHT)
           For Local i=0 Until 2
            If playertc(1,0) = False
                   x+=1
            End If
           Next
       End If
       If KeyDown(KEY_LEFT)
           For Local i=0 Until 2    
               If playertc(-1,0) = False
                   x-=1
               End If
           Next
       End If        
    End Method
    Method playertc:Bool(x1:Int,y1:Int)
        Local cx = (x + x1) / tilewidth
           Local cy = (y + y1) / tileheight
        For Local y2=cy-1 Until cy+2
        For Local x2=cx-1 Until cx+2
            If x2&gt;=0 And x2&lt;mapwidth And y2&gt;=0 And y2&lt;mapheight
                If map[y2][x2] &gt; 0
                    If rectsoverlap(x+x1,y+y1,pw,ph,x2*tilewidth,
                                    y2*tileheight,tilewidth,tileheight) = True
                        Return True
                    End If
                End If
            End If
        Next
        Next
        Return False
    End Method
    Method draw()
        ' draw the player
        SetColor 255,255,0        
        DrawOval x,y,pw,ph        
    End Method
End Class

Global player:List&lt;players&gt; = New List&lt;players&gt;
Global wallcrawlers:List&lt;wallcrawler&gt; = New List&lt;wallcrawler&gt;

Class MyGame Extends App
    Method OnCreate()
        SetUpdateRate(60)
        player.AddLast(New players())
        createwallcrawlers
    End
    Method OnUpdate()
        ' Player left and right movement
        For Local i:=Eachin player
            i.update
        Next
        For Local i:=Eachin wallcrawlers
        	i.update
        Next
    End
    Method OnRender()
        Cls(0,0,0)
        SetColor(255,255,255)
        ' draw the map
        For Local y:Int = 0 Until mapheight
        For Local x:Int = 0 Until mapwidth
            If map[y][x] = 1 Then DrawRect(x*tilewidth,y*tileheight,tilewidth,tileheight)
        End
        End
        DrawText "Platformer Example",10,10
        DrawText "Use cursor left/right and space bar",160,10
        For Local i:=Eachin player
            i.draw
        Next
        For Local i:=Eachin wallcrawlers
        	i.draw
        Next
    End
End

Function createwallcrawlers:Void()
	For Local y=0 Until mapheight
	For Local x=0 Until mapwidth
		If map[y][x] = 2
			map[y][x] = 0
			wallcrawlers.AddLast(New wallcrawler(x*tilewidth,y*tileheight))		
		End If
	Next
	Next
End Function

Function rectsoverlap:Bool(x1:Int, y1:Int, w1:Int, h1:Int, x2:Int, y2:Int, w2:Int, h2:Int)
    If x1 &gt;= (x2 + w2) Or (x1 + w1) &lt;= x2 Then Return False
    If y1 &gt;= (y2 + h2) Or (y1 + h1) &lt;= y2 Then Return False
    Return True
End

Function Main()
    New MyGame()
End
</textarea><br><br>Here's hoping that someone knows what is wrong  :) <br><br></td></tr></table><br>
<a name="2100399"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >muddy_shoes</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> The pseudo-code is misleading/over-simplified. If you take the case where you move forward into a space where you can turn left in a square grid environment and then turn on the spot you will always end up with a gap on your left again (the space you previously moved from). As stands you're just running those checks every frame so you'll end up with the AI making double turns or spinning on the spot. In order for the algorithm to work you need to turn left, then move into the space and then run the decision test again.<br><br>There are a number of other issues with the code and the way you're converting between pixel space and your level grid but that's the main problem with the wall-following itself. <br><br></td></tr></table><br>
<a name="2100750"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pakz</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I hoped it was a simple thing I did wrong. I wil need to try to expand the code then.<br><br>Anyways, it was not really that important for me at this time. I saw it and thought it would be nice if I could make it myself. There was not much I could find with google on the technique. I found no working code yet. One example code on a forum did not work after converting it twice.<br><br>Maybe I wil get it working in the future.<br><br>With the pixel space and the map coordinates. If you have a way that works better then I would gladly learn about that if it is not to difficult or involves vectors or so. I also tried doing this with for loops and going in to the direction one pixel at a time until I would of reached another map coordinate.<br><br>Anyways, Thanks for the help so far. <br><br></td></tr></table><br>
<a name="2113531"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pakz</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I finally figured out how to get those wall crawling baddies in the game. I went through the steps again and now finally saw how it could work. It did not take long to get working.<br><br>It goes like this.<br>A crawler has 4 possible directions it can go into. We remember the direction we are going into.(1=up-2=right-3=down-4=left)<br>going to the right means increasing the counter to 2+1 = 3(down)<br>We do this:<br>See if the right side is a free and move there;return<br>of if the forward pos is free and move there;return<br>of if the left pos is free and move there<br><br>This is the crawler class:<br><pre class=code>
Class wallcrawler
	'x and y tilepositions(cells)
	Field x:Float,y:Float
	'nx and ny is next tileposition
	Field nx:Int,ny:Int
	'offset for drawing (smooth movement)
	Field offx:Int,offy:Int
	' direction wallcrawler is goint to
	' 1 = up, 2 = right, 3 = down, 4 = left
	Field direction:Int
	Method New(x:Float,y:Float)
		Self.x = x
		Self.y = y
		Self.nx = x
		Self.ny = y
		'start going right
		Self.direction = 2
	End Method
	Method update()
		If x = nx And y = ny
			Else
			' if not on next position then slowly move there
			If x&lt;nx Then offx+=1
			If x&gt;nx Then offx-=1
			If y&lt;ny Then offy+=1
			If y&gt;ny Then offy-=1
			If offx&gt;tilewidth Then x+=1;offx=0
			If offx&lt;-tilewidth Then x-=1;offx=0
			If offy&gt;tileheight Then y+=1;offy=0
			If offy&lt;-tileheight Then y-=1;offy=0
			Return 
		End If
		' get the next directions
		Local rightd:Int=direction+1
		Local forwardd:Int=direction
		Local leftd:Int=direction-1
		' b sure to stay in legal movement
		If rightd &gt; 4 Then rightd = 1
		If leftd &lt; 1 Then leftd = 4
		' first see if we can go right
		If postaken(rightd) = True
				direction = rightd
				movepos(rightd)
				Return			
		End If
		' then see if we can go forward
		If postaken(forwardd) = True
			direction = forwardd
			movepos(forwardd)
			Return
		End If
		'then see if we can go left
		If postaken(leftd) = True
			direction = leftd
			movepos(leftd)
			Return
		End If
	End Method
	' get next cell position
	Method movepos(d:Int)
		nx = x
		ny = y
		Select d
			Case 1;ny-=1
			Case 2;nx+=1
			Case 3;ny+=1
			Case 4;nx-=1
		End Select
	End Method
	'see if the next possible position if a wall
	Method postaken(d:Int)
		Select d
			Case 1;If map[y-1][x] = 0 Then Return True
			Case 2;If map[y][x+1] = 0 Then Return True
			Case 3;If map[y+1][x] = 0 Then Return True
			Case 4;If map[y][x-1] = 0 Then Return True
		End Select
		Return False
	End Method
	Method draw()
		SetColor 255,0,0
		DrawRect x*tilewidth+offx,y*tileheight+offy,tilewidth,tileheight
	End Method
End Class

</pre><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import mojo

Const tilewidth = 32
Const tileheight = 32
Const mapwidth:Int=20
Const mapheight:Int=10
Global map:Int[][] = [      [1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1],
                            [1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1],
                            [1,0,0,0,0,0,0,0,0,0,2,0,0,0,0,0,0,0,0,1],
                            [1,0,0,0,0,0,0,0,1,1,1,1,1,0,0,0,0,0,0,1],
                            [1,0,0,0,0,0,0,0,1,0,0,0,1,0,0,0,0,0,0,1],
                            [1,0,0,0,0,0,1,1,1,0,0,0,1,1,0,0,0,0,0,1],
                            [1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1],
                            [1,1,1,1,0,0,0,0,0,0,0,0,0,0,0,0,1,1,1,1],
                            [1,1,1,1,0,0,0,0,0,0,0,0,0,0,0,0,1,1,1,1],
                            [1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1] ]

Class wallcrawler
	'x and y tilepositions(cells)
	Field x:Float,y:Float
	'nx and ny is next tileposition
	Field nx:Int,ny:Int
	'offset for drawing (smooth movement)
	Field offx:Int,offy:Int
	' direction wallcrawler is goint to
	' 1 = up, 2 = right, 3 = down, 4 = left
	Field direction:Int
	Method New(x:Float,y:Float)
		Self.x = x
		Self.y = y
		Self.nx = x
		Self.ny = y
		'start going right
		Self.direction = 2
	End Method
	Method update()
		If x = nx And y = ny
			Else
			' if not on next position then slowly move there
			If x&lt;nx Then offx+=1
			If x&gt;nx Then offx-=1
			If y&lt;ny Then offy+=1
			If y&gt;ny Then offy-=1
			If offx&gt;tilewidth Then x+=1;offx=0
			If offx&lt;-tilewidth Then x-=1;offx=0
			If offy&gt;tileheight Then y+=1;offy=0
			If offy&lt;-tileheight Then y-=1;offy=0
			Return 
		End If
		' get the next directions
		Local rightd:Int=direction+1
		Local forwardd:Int=direction
		Local leftd:Int=direction-1
		' b sure to stay in legal movement
		If rightd &gt; 4 Then rightd = 1
		If leftd &lt; 1 Then leftd = 4
		' first see if we can go right
		If postaken(rightd) = True
				direction = rightd
				movepos(rightd)
				Return			
		End If
		' then see if we can go forward
		If postaken(forwardd) = True
			direction = forwardd
			movepos(forwardd)
			Return
		End If
		'then see if we can go left
		If postaken(leftd) = True
			direction = leftd
			movepos(leftd)
			Return
		End If
	End Method
	' get next cell position
	Method movepos(d:Int)
		nx = x
		ny = y
		Select d
			Case 1;ny-=1
			Case 2;nx+=1
			Case 3;ny+=1
			Case 4;nx-=1
		End Select
	End Method
	'see if the next possible position if a wall
	Method postaken(d:Int)
		Select d
			Case 1;If map[y-1][x] = 0 Then Return True
			Case 2;If map[y][x+1] = 0 Then Return True
			Case 3;If map[y+1][x] = 0 Then Return True
			Case 4;If map[y][x-1] = 0 Then Return True
		End Select
		Return False
	End Method
	Method draw()
		SetColor 255,0,0
		DrawRect x*tilewidth+offx,y*tileheight+offy,tilewidth,tileheight
	End Method
End Class

Class players
    Field x:Float = 640/2-16
    Field y:Float = 480/2
    Field pw:Int=32
    Field ph:Int=32
    Field incy:Float
    Field jump:Bool=False
    Method New()
    End Method
    Method update()
        playermovement
        playergravity
    End Method
    Method playergravity()
        ' If the player is on the ground and the space bar is pressed
        If jump = False And playertc(0,1) = False
            jump = True
            incy = 0
        End If
        If jump = False And KeyDown(KEY_SPACE) = True
            incy = -4
            jump = True
        End
        'If the player is in the jump
        If jump = True
            incy += 0.1
            'if the player is going up
            If incy &lt;=0
                For Local i:Int = 0 Until Abs(incy)                
                    y -= 1
                    If playertc(0,-1) = True
                        incy = 0
                        Exit
                    End If
                End
            End
            ' if the player if going down
            If incy &gt; 0
                For Local i:Int = 0 Until incy
                    y += 1
                    'if the player touches the ground
                    If playertc(0,1) = True
                        jump = False                        
                        Exit
                    End
                End
            End
        End
    End Method
    Method playermovement()
       If KeyDown(KEY_RIGHT)
           For Local i=0 Until 2
            If playertc(1,0) = False
                   x+=1
            End If
           Next
       End If
       If KeyDown(KEY_LEFT)
           For Local i=0 Until 2    
               If playertc(-1,0) = False
                   x-=1
               End If
           Next
       End If        
    End Method
    Method playertc:Bool(x1:Int,y1:Int)
        Local cx = (x + x1) / tilewidth
           Local cy = (y + y1) / tileheight
        For Local y2=cy-1 Until cy+2
        For Local x2=cx-1 Until cx+2
            If x2&gt;=0 And x2&lt;mapwidth And y2&gt;=0 And y2&lt;mapheight
                If map[y2][x2] &gt; 0
                    If rectsoverlap(x+x1,y+y1,pw,ph,x2*tilewidth,
                                    y2*tileheight,tilewidth,tileheight) = True
                        Return True
                    End If
                End If
            End If
        Next
        Next
        Return False
    End Method
    Method draw()
        ' draw the player
        SetColor 255,255,0        
        DrawOval x,y,pw,ph        
    End Method
End Class

Global player:List&lt;players&gt; = New List&lt;players&gt;
Global wallcrawlers:List&lt;wallcrawler&gt; = New List&lt;wallcrawler&gt;

Class MyGame Extends App
    Method OnCreate()
        SetUpdateRate(60)
        player.AddLast(New players())
        createwallcrawlers
    End
    Method OnUpdate()
        ' Player left and right movement
        For Local i:=Eachin player
            i.update
        Next
        For Local i:=Eachin wallcrawlers
        	i.update
        Next
    End
    Method OnRender()
        Cls(0,0,0)
        SetColor(255,255,255)
        ' draw the map
        For Local y:Int = 0 Until mapheight
        For Local x:Int = 0 Until mapwidth
            If map[y][x] = 1 Then DrawRect(x*tilewidth,y*tileheight,tilewidth,tileheight)
        End
        End
        DrawText "Platformer Example with Wallcrawlers(wall following)",10,10
        DrawText "Use cursor left/right and space bar to move player",160,10
        For Local i:=Eachin player
            i.draw
        Next
        For Local i:=Eachin wallcrawlers
        	i.draw
        Next
    End
End

Function createwallcrawlers:Void()
	For Local y=0 Until mapheight
	For Local x=0 Until mapwidth
		If map[y][x] = 2
			map[y][x] = 0
			wallcrawlers.AddLast(New wallcrawler(x,y))		
		End If
	Next
	Next
End Function

Function rectsoverlap:Bool(x1:Int, y1:Int, w1:Int, h1:Int, x2:Int, y2:Int, w2:Int, h2:Int)
    If x1 &gt;= (x2 + w2) Or (x1 + w1) &lt;= x2 Then Return False
    If y1 &gt;= (y2 + h2) Or (y1 + h1) &lt;= y2 Then Return False
    Return True
End

Function Main()
    New MyGame()
End
</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
