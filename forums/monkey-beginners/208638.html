<!DOCTYPE html><html lang="en" ><head ><title >Datatype comparisons, choosing opposite condition</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Datatype comparisons, choosing opposite condition</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=528" >Monkey Beginners</a>/<a href="#bottom" >Datatype comparisons, choosing opposite condition</a><br><br>
<a name="2089500"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >consty</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>
Strict

Class Entity
End

Class Person Extends Entity
End

Class Cat Extends Entity
End

Function Main:Int()
	Local p:Person = new Person
	Local c:Cat = new Cat

	If Person(p)
		Print("The p object is an instance of the Person class")
	End

	If Entity(p) And Entity(c)
		Print("Both p and c objects are derived from the Entity base class")
	End
	
	Return 0
End
</pre><br><br>I am interested to know if it is possible to make negative type comparisons.<br><br>For example if c variable is checked against the Person class<br>Person(c)<br><br>Instead of not letting the program compile, it would be better to just take it as a false and ignore the if statement.<br><br>I don't know if this brings troubles and incompatibilities, anyway I just wanted to mention it here for the sake of it and we will see what happens. <br><br></td></tr></table><br>
<a name="2089644"></a>

<a name="2089645"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pharmhaus</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>Instead of not letting the program compile, it would be better to just take it as a false and ignore the if statement.<br> <br></div><br><br>Do you mean that you like to remove 'Person' some times?<br>You could use the preprocessor for that.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
#PERSON = false

Class Entity

End

Class Cat Extends Entity

End

Function Main:Int()
	#if PERSON
		Local p:Person = New Person
		Local c:Cat = new Cat
	
		If Person(p)
			Print("The p object is an instance of the Person class")
		End
	#else
		Local p:Entity = New Entity
		Local c:Cat = New Cat
	#endif
	
	If Entity(p) And Entity(c)
		Print("Both p and c objects are derived from the Entity base class")
	End
	
	Return 0
End
</textarea><br><br>Or something like this?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Const ENTITY:Int = 1
Const PERSON:Int = 2
Const CAT:Int = 3

Class Entity

	Method Type:Int() Property
		Return ENTITY
	End

End

Class Person Extends Entity

	Method Type:Int() Property
		Return PERSON
	End
	
End

Class Cat Extends Entity

	Method Type:Int() Property
		Return CAT
	End
End

Function Main:Int()
	Local p:Person = new Person
	Local c:Cat = new Cat

	If Entity(p).Type = PERSON
		Print("The p object is an instance of the Person class")
	End

	If Entity(p) And Entity(c)
		Print("Both p and c objects are derived from the Entity base class")
	End
	
	Return 0
End
</textarea> <br><br></td></tr></table><br>
<a name="2089676"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >consty</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Pharmhaus, thanks for responding. I liked the second example you offered, that this is also correct approach.<br><br>What I am bothered mostly is about having precise type to type comparison. I found that the code works only if the objects are derived from a base class, otherwise the comparison will never happen.<br><br>What Monkey understands is that you try to convert a person to a cat in order to compare it's datatype. Which this is impossible and hence the program fails to compile.<br><br><pre class=code>
Strict

Class Entity
End

Class Person Extends Entity
End

Class Cat Extends Entity
End

Function DoIt:Void(entity:Entity)
	If Cat(entity)
		Print("Meow!")
	End
	If Person(entity)
		Print("Hello!")
	End
End

Function Main:Int()
	Local p:Person = new Person
	Local c:Cat = new Cat
	DoIt(p)
	DoIt(c)

	' This won't compile
	'If Cat(p)
	''	Print("")
	'End

	If Cat(Entity(p))
		Print("Please replace logic processing unit")
	Else
		Print("Correct: Person object is not instance of Cat class")
	End
	Return 0
End
</pre><br><br>I don't know what is this about, as far I understand is a concept very familiar to C programmers. Personally I have used only PHP and C# so I am more convenient with reflection and dynamism. <br><br></td></tr></table><br>
<a name="2089680"></a>

<a name="2089683"></a>

<a name="2089685"></a>

<a name="2089690"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pharmhaus</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ahh, I see you try to do something similiar to duck typing.<br>sorry, that is not possible in the way you would like to do it, you would like to use polymorphism instead.<br>Anyway, I remeber that I wrote something like this some time ago, I will see if it is still around somewhere.<br><br>Using simple polymorphism:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Class Entity

	Method DoIt:Void()
		Print "Entity here."
	End
	
End

Class Person Extends Entity

	Method DoIt:Void()
		Print("Hello!")
	End
	
End

Class Cat Extends Entity

	Method DoIt:Void()
		Print("Meow!")
		Super.DoIt() ' Calling a 'hidden' method from the entity class
	End
	
End

Function Main:Int()
	Local p:Entity = New Person ' We can eliminate the need for a person variable by placing a 'Person' in an 'Entity' variable
	Local c:Entity = New Cat ' This way, a part of our program can use a Class without knowing it (see function below), all we need is a method in Entity to call

	p.DoIt() ' Person 'DoIt' now 'hides' Entity 'DoIt' (Both still exist, but Person DoIt is now called when we call DoIt on a person)
	c.DoIt() ' same goes for cats

	DoSomething(p)
	DoSomething(c)
	
	Return 0
End


Function DoSomething:Void(e:Entity)
	e.DoIt() ' Will call different DoIt depending on the real Class of 'e' this allows you to write very generic code
End
</textarea><br><br>Using reflection to get some type of ducktyping/type checking:<br>(Please note: reflection increases compilation time and is not the fastest)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

#rem
	SAVE AS ducktyping.monkey
#END

#REFLECTION_FILTER="ducktyping"  'only reflect this file or we will wait for the app forever to compile
Import reflection


Class MyClass
	Field X:int = 10
End

Class MyA Extends MyClass
	Field Y:int = 1567
End

Class MyB Extends MyClass
	Field Y:int
End

Function Main:Int()
	Local Test:MyClass = New MyA
	Local Copy:MyB = MyB(Ducktyping(Test, "MyB")) ' Converts a 'MyA' to 'MyB' without security checks so be careful
	
	If TypeComparison(Test, "MyA") Then ' Check if 'Test' is of type 'MyA' (please note: does not check if Test extends from 'MyA', so (Test,"MyClass") will return false )
		Print "Its of type MyA!"
	EndIf
	
	If TypeComparison(Copy, "MyB") Then
		Print "Its of type MyB!"
	EndIf
	
	Print Copy.X
	Print Copy.Y
	
	Return 0
End

Function Ducktyping:Object(SrcClass:Object, DestClass:String)
	Local C1:ClassInfo = GetClass(SrcClass)
	Local C2:ClassInfo = GetClass(DestClass)
	Local O:Object
	Local F:FieldInfo
	
	If C2 = Null Then
		Error "Class " + DestClass + " does not exist (Missing #REFLECTION_FILTER ?)"
	EndIf
	
	O = C2.NewInstance()
	
	For Local i:FieldInfo = EachIn C2.GetFields(True)
		F = C1.GetField(i.Name, True)
		If F Then
			i.SetValue(O, F.GetValue(SrcClass))
		EndIf
	Next
	
	Return O
End

Function TypeComparison:bool(First:Object, Second:String)
	If First = Null And Second = "" Then
		Return True
	ElseIf First = Null or Second = ""
		Return False
	EndIf
	
	Local FirstInfo:ClassInfo = GetClass(First)
	Local SecondInfo:ClassInfo = GetClass(Second)
	
	If SecondInfo = Null Then Return False
	
	If FirstInfo.Name = SecondInfo.Name Then
		Return True
	Else
		Return False
	EndIf
End
</textarea> <br><br></td></tr></table><br>
<a name="2089692"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >consty</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Wow this example took it to the next level. Monkey is indeed powerful language. Thanks for posting. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
