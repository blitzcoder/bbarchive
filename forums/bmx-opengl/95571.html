<!DOCTYPE html><html lang="en" ><head ><title >GLSL framework for BlitzMax</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >GLSL framework for BlitzMax</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=103" >OpenGL Module</a>/<a href="#bottom" >GLSL framework for BlitzMax</a><br><br>
<a name="1101595"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >taumel</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have to do some image heavy stuff and after trying it out in BlitzMax directly (single as well as threaded) i got frustrated by the speed results, so the only option was to use the GPU.<br><br>Now after i got it running i was wondering if there also exists some lightweight framework for GLSL usage in BlitzMax.<br><br>I know that there exists glsl.bmx. As the copyright is unknown, can you use this without any problems and does there exist some more information/docs about it beside of comment fields in the code? <br><br></td></tr></table><br>
<a name="1101721"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >xlsior</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> duct.mod/protog2d.mod has a bunch of shader routines for Blitzmax:<br><br><a href="https://github.com/komiga/duct-max" target="_blank">https://github.com/komiga/duct-max</a> <br><br></td></tr></table><br>
<a name="1101938"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Noobody</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's a TShader class I always use in GLSL experiments, with a small GLSL Mandelbrot example:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

Framework BRL.Basic
Import BRL.Timer
Import BRL.GLGraphics
Import Pub.OpenGL
Import Pub.Glew

Const GWIDTH:Int  = 1024
Const GHEIGHT:Int = 768

InitGL()

Local Shader:TShader = New TShader.Create(VERTEX_CODE, FRAGMENT_CODE)

Local Timer:TTimer = CreateTimer(60), Frame:Int

While Not (KeyHit(KEY_ESCAPE) Or AppTerminate())
	glClear(GL_COLOR_BUFFER_BIT)
	
	Local T:Float = Sin(Frame)
	Local RealMin:Float      = Blend(-4, -1,    T)
	Local RealMax:Float      = Blend( 4, -0,    T)
	Local ImaginaryMin:Float = Blend(-3, -1,    T)
	Local ImaginaryMax:Float = Blend( 3, -0.25, T)
	
	Shader.Enable()
	glBegin(GL_QUADS)
		glTexCoord2f(RealMin, ImaginaryMin); glVertex2f(0,      0      )
		glTexCoord2f(RealMax, ImaginaryMin); glVertex2f(GWIDTH, 0      )
		glTexCoord2f(RealMax, ImaginaryMax); glVertex2f(GWIDTH, GHEIGHT)
		glTexCoord2f(RealMin, ImaginaryMax); glVertex2f(0,      GHEIGHT)
	glEnd()
	Shader.Disable()
	
	Frame :+ 1
	
	Flip(0)
	WaitTimer(Timer)
Wend
End

Const VERTEX_CODE:String = .. 'Of course you shouldn't put these in code normally. This is just for the sake of not having to use external files for this example
	"void main( void ) {~n" + ..
		"gl_Position = gl_ModelViewProjectionMatrix*gl_Vertex;~n" + ..
		"gl_TexCoord[0]  = gl_MultiTexCoord0;~n" + ..
	"}"

Const FRAGMENT_CODE:String = ..
	"~n" + ..
	"void main(void) {~n" + ..
		"vec4 C = gl_TexCoord[0];~n" + ..
		"vec4 Z = C;~n" + ..
		"vec2 CSq;~n" + ..
		"~n" + ..
		"int Iteration = 0;~n" + ..
		"CSq.x = C.x*C.x;~n" + ..
		"CSq.y = C.y*C.y;~n" + ..
		"float OrbitTrap = 1000.0;~n" + ..
		"while(Iteration++ &lt; 32 &amp;&amp; CSq.x + CSq.y &lt; 8.0) {~n" + ..
			"Z.w = CSq.x - CSq.y + C.x;~n" + ..
			"Z.y = 2.0*Z.x*Z.y + C.y;~n" + ..
			"Z.x = Z.w;~n" + ..
			"OrbitTrap = min(OrbitTrap, min(abs(Z.x), abs(Z.y)));~n" + ..
			"CSq.x = Z.x*Z.x;~n" + ..
			"CSq.y = Z.y*Z.y;~n" + ..
		"}~n" + ..
		"~n" + ..
		"float Ratio = OrbitTrap*7.0;~n" + .. '"float(Iteration)/128.0*7.0;~n" + ..
		"float Rise = mod(Ratio, 1.0);~n" + ..
		"float Fall = 1.0 - Rise;~n" + ..
		"Ratio = floor(Ratio);~n" + ..
		"~n" + ..
		"     if(Ratio == 0.0) gl_FragColor = vec4(Rise, 0.0, 0.0, 1.0);~n" + ..
		"else if(Ratio == 1.0) gl_FragColor = vec4(1.0, Rise, 0.0, 1.0);~n" + ..
		"else if(Ratio == 2.0) gl_FragColor = vec4(Fall, 1.0, 0.0, 1.0);~n" + ..
		"else if(Ratio == 3.0) gl_FragColor = vec4(0.0, 1.0, Rise, 1.0);~n" + ..
		"else if(Ratio == 4.0) gl_FragColor = vec4(0.0, Fall, 1.0, 1.0);~n" + ..
		"else if(Ratio == 5.0) gl_FragColor = vec4(Rise, 0.0, 1.0, 1.0);~n" + ..
		"else if(Ratio == 6.0) gl_FragColor = vec4(1.0, Rise, 1.0, 1.0);~n" + ..
	"}"
	
Function Blend:Float(A:Float, B:Float, T:Float)
	Return A*(1 - T) + B*T
End Function

Function InitGL()
	GLGraphics GWIDTH, GHEIGHT
	
	glewInit()
	
	glMatrixMode(GL_PROJECTION)
	glLoadIdentity()
	glOrtho(0, GWIDTH, GHEIGHT, 0, -1, 1)
	
	glMatrixMode(GL_MODELVIEW)
	glLoadIdentity()
	
	If Not TShader.CheckCompability() Then Notify("Shader support not available", True); End
End Function

Type TShader
	Field ProgramObject:Int
	
	Method Load:TShader(VertexPath:String, FragmentPath:String)
		Local VertexCode:String, FragmentCode:String
		
		Try
			VertexCode   = LoadText(VertexPath)
			FragmentCode = LoadText(FragmentPath)
		Catch Dummy:Object
			Return Null
		EndTry
		
		Create(VertexCode, FragmentCode)
		
		Return Self
	End Method
	
	Method Create:TShader(VertexCode:String, FragmentCode:String)
		If Not ProgramObject Then ProgramObject = glCreateProgramObjectARB()
		
		Local VertexShader  :Int = glCreateShaderObjectARB(GL_VERTEX_SHADER_ARB)
		Local FragmentShader:Int = glCreateShaderObjectARB(GL_FRAGMENT_SHADER_ARB)
		
		Local ErrorMessage:String
		
		_LoadShader(VertexCode, VertexShader)
		glCompileShaderARB(VertexShader)
		
		If _CheckForErrors(VertexShader, ErrorMessage) Then
			glDeleteObjectARB(VertexShader)
			
			Throw ErrorMessage
		EndIf
		
		_LoadShader(FragmentCode, FragmentShader)
		glCompileShaderARB(FragmentShader)
		
		If _CheckForErrors(FragmentShader, ErrorMessage) Then
			glDeleteObjectARB(VertexShader)
			glDeleteObjectARB(FragmentShader)
			
			Throw ErrorMessage
		EndIf
		
		glAttachObjectARB(ProgramObject, VertexShader)
		glAttachObjectARB(ProgramObject, FragmentShader)
		
		glDeleteObjectARB(VertexShader)
		glDeleteObjectARB(FragmentShader)
		
		glLinkProgramARB(ProgramObject)
		If _CheckForErrors(ProgramObject, ErrorMessage, False) Then Throw ErrorMessage
		
		Return Self
	End Method
	
	Method Enable()
		glUseProgramObjectARB(ProgramObject)
	End Method
	
	Method Disable()
		glUseProgramObjectARB(0)
	End Method
	
	Method GetUniformLocation:Int(Name:String)
		Return glGetUniformLocationARB(ProgramObject, Name)
	End Method
	
	Method Delete()
		glDeleteObjectARB(ProgramObject)
	End Method
	
	Function _LoadShader(ShaderCode:String, ShaderObject:Int)
		Local ShaderCodeC:Byte Ptr = ShaderCode.ToCString()
		Local ShaderCodeLen:Int    = ShaderCode.Length
		
		glShaderSourceARB(ShaderObject, 1, Varptr ShaderCodeC, Varptr ShaderCodeLen)
		
		MemFree(ShaderCodeC)
	End Function
	
	Function _CheckForErrors:Int(ShaderObject:Int, ErrorString:String Var, Compiled:Int = True)
		Local Successful:Int
		
		If Compiled Then
			glGetShaderiv (ShaderObject, GL_COMPILE_STATUS, Varptr Successful)
		Else
			glGetProgramiv(ShaderObject, GL_LINK_STATUS,    Varptr Successful)
		EndIf
		
		If Not Successful Then
			Local ErrorLength:Int
			glGetObjectParameterivARB(ShaderObject, GL_OBJECT_INFO_LOG_LENGTH_ARB, Varptr ErrorLength)
			
			Local Message:Byte Ptr = MemAlloc(ErrorLength), Dummy:Int
			
			glGetInfoLogARB(ShaderObject, ErrorLength, Varptr Dummy, Message)
			
			ErrorString = String.FromCString(Message)
			MemFree(Message)
			
			Return -1
		EndIf
		
		Return 0
	End Function
	
	Function CheckCompability:Int()
		Local Extensions:String = String.FromCString(Byte Ptr glGetString(GL_EXTENSIONS))
		Local GLVersion:String  = String.FromCString(Byte Ptr glGetString(GL_VERSION))
		Local GLVersionInt:Int  = GLVersion[.. 3].Replace(".", "").ToInt()
		
		If Extensions.Find("GL_ARB_shader_objects" ) &gt;= 0 And ..
		   Extensions.Find("GL_ARB_vertex_shader"  ) &gt;= 0 And ..
		   Extensions.Find("GL_ARB_fragment_shader") &gt;= 0 Or GLVersionInt &gt;= 20 Then Return True
		
		Return False
	End Function
End Type</textarea><br><br>It supports the basic operations, such as loading from strings or files, compiler/linker error handling, compatibility checks and enabling/disabling the shader. <br><br></td></tr></table><br>
<a name="1101939"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cool, thanks for sharing. this'll come in handy. <br><br></td></tr></table><br>
<a name="1101946"></a>

<a name="1101947"></a>

<a name="1101948"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >taumel</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks @xlsior and @Noobody as well.<br><br>I also tested my Julia code i did once and it's just a shame how slow it is compared to the GPU version, same with a less intensive copper plasma, it all was about 20x faster than the CPU threaded versions.<br><br>Actually it would be interesting using the GPU shaders for visualising and calculating a 2D game. With a proper GPU this should enable a new interesting gameplay.<br><br>@Noobody<br>I just tried your example, got some freaky mandbrot set, and after some zooming it boomed my machine.<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1102036"></a>

<a name="1102037"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> It works fine for me. The coloring isn't what I typically see for mandelbrodts but kinda funky. Works pretty fast for me, mostly 60fpsish. And your framework is pretty handy for setting up the shaders.<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1102089"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Noobody</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> I just tried your example, got some freaky mandbrot set, and after some zooming it boomed my machine. <br></div><br>If you have an old graphics card, it is possible that it isn't fast enough to execute the inner loop inside the shader for real-time rendering. Depending on your drivers, this can have different outcomes - either your driver resets or the machine freezes (operating systems really don't like it if one application hogs the graphics card). I'm interested in your machine though, since even my shitty 3 year old laptop with Intel GMA is able to render it at 30-60fps :)<br><br>So basically, it's more of a problem with the shader, not the framework.<br><br><div class="quote"> The coloring isn't what I typically see for mandelbrodts but kinda funky. <br></div><br>Regular mandelbrot coloring is a bit boring, so I added in a few simple orbit traps to mix it up a little. If you're interested, I talked a bit about them in this <a href="http://noobody.org/2011/06/image-and-orbit-traps/" target="_blank">blog post</a>. The color gradients are admittedly not very pretty, it was just something I still had lying around. <br><br></td></tr></table><br>
<a name="1102126"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >taumel</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> MacBookPro 2.53GHz, 4GB RAM, GT330M, OSX 10.6.8.<br><br>I had no crashes so far with the things i did, also tesuje's  shaderexamples work fine here. The only other thing which freezes a part of my machine is returning from fullscreen to windowed mode with Unity's webplayer whilst certain new image effects are enabled. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
