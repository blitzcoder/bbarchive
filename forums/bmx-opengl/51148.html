<!DOCTYPE html><html lang="en" ><head ><title >texture trouble :/</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >texture trouble :/</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=103" >OpenGL Module</a>/<a href="#bottom" >texture trouble :/</a><br><br>
<a name="570330"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >nawi</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> My texture renders as all black(or violet with GL_NEAREST instead of GL_LINEAR modes), Im pretty certain that the U,V coordinates are right. On fullscreen it crashes with Flushmem(), without it its the same problems as in windowed mode.<br><br>Texture type:<br><pre class=code>
Type Texture
	Field GLID

	Function Create:Texture(Filename$)
		Local Txt:Texture = New Texture
		
		Local Pixmap:TPixmap = LoadPixmap(Filename$)
		If Not Pixmap Then
			Print "Could not load texture!"
			Return Null
		EndIf
		
		Txt.GLID = bglTexFromPixmap(Pixmap)
		If Not Txt.GLID Then
			Print "Could not apply texture to opengl!"
			Return Null
		EndIf
		
		
		glBindTexture GL_TEXTURE_2D,Txt.GLID
		
		glTexParameteri GL_TEXTURE_2D, GL_TEXTURE_WRAP_S, GL_REPEAT
		glTexParameteri GL_TEXTURE_2D, GL_TEXTURE_WRAP_T, GL_REPEAT		
	
		glTexParameteri GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_NEAREST
		glTexParameteri GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER, GL_NEAREST
		


		Pixmap = Null
		FlushMem
		
		Return Txt
	End Function
	
End type
</pre><br><br>Render function (parts from it)<br><pre class=code>
				'Texture
				If Ent.Txt &lt;&gt; Null Then
					glBindTexture(GL_TEXTURE_2D,Ent.Txt.GLID)
					glEnable(GL_TEXTURE_2D)
				end if

				'Tri1
				If Ent.Alpha# &lt;&gt; 1.0 Then
					glColor4f(Pol.P[0].R/255.0,Pol.P[0].G/255.0,Pol.P[0].B/255.0,Ent.Alpha#)
				Else
					glColor3f(Pol.P[0].R/255.0,Pol.P[0].G/255.0,Pol.P[0].B/255.0)
				EndIf
				glTexCoord2f(Pol.P[0].U#,Pol.P[0].V#)
				glVertex3f(Pol.P[0].X#,Pol.P[0].Y#,Pol.P[0].Z#)
</pre><br><br>This is creating 2 triangle entity with my own lib<br><pre class=code>
'Create entity
Local Cube:Entity = Entity.Create()
'Add(x,y,z,u,v)
Cube.Add(-1,1,-1,0,0)
Cube.Add(-1,-1,-1,0,1)
Cube.Add(1,-1,-1,1,1)

Cube.Add(1,-1,-1,0,0)
Cube.Add(1,1,-1,1,0)
Cube.Add(-1,1,-1,1,1)
</pre> <br><br></td></tr></table><br>
<a name="570465"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TeraBit</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I had something like this as BglTexFromPixmap will create Mipmaps by default.<br><br>Pass a false after the PixMap in the BglTexFromPixmap() call to prevent this.<br><br>Then each time you bind the texture call your:<br><br>glTexParameteri GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_NEAREST<br>glTexParameteri GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER, GL_NEAREST<br><br>I don't know if it will help. I had to play around with the settings to get my one to work. <br><br></td></tr></table><br>
<a name="570534"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >nawi</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Still doesnt work. :/ <br><br></td></tr></table><br>
<a name="570601"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >nawi</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Can somebody post a simple code to display a texture? <br><br></td></tr></table><br>
<a name="571968"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ibmurai</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is a simple example - hope it helps even though you've probably solved it by now... :P<br><pre class=code>
Strict

Incbin "ending.png"


' Variables

Global ..
	theTexture:Int

Local ..
	thePixmap:TPixmap, ..
	time:Int = 0, ..
	timeDiff:Int = 0, ..
	timeSecond:Int = 0, ..
	fps:Int = 0, shownFPS:Int = 0


' First set up openGL

bglCreateContext(800, 600, 32, 0, BGL_BACKBUFFER|BGL_DEPTHBUFFER)
glClearColor(0, 0, 0, 64)
glClearDepth(1.0)
glDepthFunc(GL_LESS)
glEnable(GL_DEPTH_TEST)
glShadeModel(GL_SMOOTH)
glEnable(GL_CULL_FACE)
glCullFace(GL_BACK)

glMatrixMode(GL_PROJECTION)
glLoadIdentity

gluPerspective(45.0, 4.0 / 3.0, 1.0, 100.0)

glTexEnvf(GL_TEXTURE_ENV, GL_TEXTURE_ENV_MODE, GL_MODULATE)
glEnable(GL_TEXTURE_2D)
glMatrixMode(GL_MODELVIEW)


' Load the texture

thePixmap = LoadPixmap("incbin::ending.png")
theTexture = bglTexFromPixmap(thePixmap)
glBindTexture(GL_TEXTURE_2D, theTexture)


' The game loop

Repeat
	timeDiff = MilliSecs() - time
	time = MilliSecs()
	timeSecond :+ timeDiff
	If timeSecond &gt; 1000.0 Then
		timeSecond :Mod 1000.0
		shownFPS = fps
		fps = 0
	EndIf
	fps :+ 1
	
	glClear(GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT)

	DrawEnding
	
	bglDrawText("FPS: " + shownFPS, 0, 0) 

	glFlush

	bglSwapBuffers
	
	FlushMem
Until KeyHit(KEY_ESCAPE)


Function DrawEnding()
	glBindTexture(GL_TEXTURE_2D, theTexture)

	glEnable(GL_TEXTURE_2D)

	glLoadIdentity
	
	glTranslatef(0.0, 0.0, -5.0)
	glScalef(4.0, 4.0, 0.0)
			
	glBegin(GL_POLYGON)
		glNormal3f(  0.0, 0.0, 1.0)
		glTexCoord3f(0.0, 1.0, 0.0) 
		glVertex3f(  -0.5, -0.5,  0.0)
		glTexCoord3f(1.0, 1.0, 0.0)
		glVertex3f(   0.5, -0.5,  0.0)
		glTexCoord3f(1.0, 0.0, 0.0)
		glVertex3f(   0.5,  0.5,  0.0)
		glTexCoord3f(0.0, 0.0, 0.0) 
		glVertex3f(  -0.5,  0.5,  0.0)
		glColor4f(1.0, 1.0, 1.0, 1.0)
	glEnd
	glDisable(GL_TEXTURE_2D)
	
	glPopMatrix
End Function
</pre> <br><br></td></tr></table><br>
<a name="572178"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Extron</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Try this nawi :<br><pre class=code>
Type Texture
	Field GLID

	Function Create:Texture(Filename$)
		Local Txt:Texture = New Texture
		
		Local Pixmap:TPixmap = LoadPixmap(Filename$)
		If Not Pixmap Then
			Print "Could not load texture!"
			Return Null
		EndIf
		
		Txt.GLID = bglTexFromPixmap(Pixmap)
		If Not Txt.GLID Then
			Print "Could not apply texture to opengl!"
			Return Null
		EndIf
		
		Txt.Init()
		
		Pixmap = Null
		FlushMem

		Return Txt
	End Function

	Method Init()
		glBindTexture GL_TEXTURE_2D,Self.GLID
		
		glTexParameteri GL_TEXTURE_2D, GL_TEXTURE_WRAP_S, GL_REPEAT
		glTexParameteri GL_TEXTURE_2D, GL_TEXTURE_WRAP_T, GL_REPEAT		
	
		glTexParameteri GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_NEAREST
		glTexParameteri GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER, GL_NEAREST
	End Method
'	
End Type
</pre> <br><br></td></tr></table><br>
<a name="572495"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >nawi</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nope, still doesnt work on my engine. Really weird.<br><br>If anyone wants to try it:<br><br>library.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'Application: opengl lib
'Author: nawi
'Description: the main library 

Global TexCount:Int = 0


'Constants
Const MDL_CUBE = 1


Extern "win32"
	Function GetActiveWindow() = "GetActiveWindow@0"
	Function SetCursorPos(x,y) = "SetCursorPos@8"
	Function GetWindowRect(hWnd,lpRect:Rect) = "GetWindowRect@8"
End Extern

Type Rect
	Field X,Y,W,H
End type

Type Entity
	Field Name$
	Field X#,Y#,Z#
	Field RotX#,RotY#,RotZ#
	
	Field Wireframe
	
	Field Alpha#
	
	Field Txt:Texture
	
	Global EntityList:Tlist
	
	Field PolygonList:TList
	Field Counter
	Field Cp#[6],Cc[6]
	
	Function Create:Entity(NewX#=0,NewY#=0,NewZ#=0)
		Local Ent:Entity = New Entity
		Ent.X# = NewX#
		Ent.Y# = NewY#
		Ent.Z# = NewZ#
		Ent.Alpha# = 1.0
		Return Ent
	End Function
	
	Method New()
		If EntityList = Null Then EntityList = CreateList()
		If PolygonList = Null Then PolygonList = CreateList()
		EntityList.AddLast Self
	End Method
	
	Method ApplyTexture(NewTxt:Texture)
		Txt = NewTxt
	End Method
	
	
	Method Add(X#,Y#,Z#,U#=0.0,V#=0.0,R=255,G=255,B=255)
		If Counter = 2 Then
			Counter = 0
			Local Poly:Polygon = New Polygon
			PolygonList.AddLast Poly
			
			Poly.P[0] = New Point
			Poly.P[0].X# = Cp#[0]; Poly.P[0].Y# = Cp#[1]; Poly.P[0].Z# = Cp#[2];
			Poly.P[0].R = Cc[0]; Poly.P[0].G = Cc[1]; Poly.P[0].B = Cc[2];
			Poly.P[0].U# = U#; Poly.P[0].V# = V#
			
			Poly.P[1] = New Point
			Poly.P[1].X# = Cp#[3]; Poly.P[1].Y# = Cp#[4]; Poly.P[1].Z# = Cp#[5];
			Poly.P[1].R = Cc[3]; Poly.P[1].G = Cc[4]; Poly.P[1].B = Cc[5];
			Poly.P[1].U# = U#; Poly.P[1].V# = V#
			
			Poly.P[2] = New Point
			Poly.P[2].X# = X#; Poly.P[2].Y# = Y#; Poly.P[2].Z# = Z#;
			Poly.P[2].R = R; Poly.P[2].G = G; Poly.P[2].B = B;
			Poly.P[2].U# = U#; Poly.P[2].V# = V#
		Else
			Cp#[Counter*3] = X#
			Cp#[Counter*3+1] = Y#
			Cp#[Counter*3+2] = Z#
			Cc[Counter*3] = R
			Cc[Counter*3+1] = G
			Cc[Counter*3+2] = B
			Counter = Counter + 1
		EndIf
		
	End Method
	
	Method Turn(AddX#=0,AddY#=0,AddZ#=0)
		RotX# = RotX# + AddX#
		RotY# = RotY# + AddY#
		RotZ# = RotZ# + AddZ#
		
		If RotX# &gt; 360 Then RotX# = 0
		If RotY# &gt; 360 Then RotY# = 0
		If RotZ# &gt; 360 Then RotZ# = 0			

		If RotX# &lt; 0 Then RotX# = 360
		If RotY# &lt; 0 Then RotY# = 360
		If RotZ# &lt; 0 Then RotZ# = 360					
	End Method
	
	Method Rotate(NewX#=0,NewY#=0,NewZ#=0)
		RotX# = NewX#
		RotY# = NewY#
		RotZ# = NewZ#
		
		If RotX# &gt; 360 Then RotX# = 0
		If RotY# &gt; 360 Then RotY# = 0
		If RotZ# &gt; 360 Then RotZ# = 0			

		If RotX# &lt; 0 Then RotX# = 360
		If RotY# &lt; 0 Then RotY# = 360
		If RotZ# &lt; 0 Then RotZ# = 360					
	End Method
	
	Method SetColor(NewR,NewG,NewB)
		For Local Pol:Polygon = EachIn PolygonList
			Pol.P[0].R = NewR
			Pol.P[0].G = NewG
			Pol.P[0].B = NewB
			Pol.P[1].R = NewR
			Pol.P[1].G = NewG
			Pol.P[1].B = NewB
			Pol.P[2].R = NewR
			Pol.P[2].G = NewG
			Pol.P[2].B = NewB			
		Next
	End Method
	
	Method AddPrimitive(ModelType)
		If ModelType = MDL_CUBE Then
			'FRONT TRI 1
			Self.Add(-1,1,1,0,1)
			Self.Add(-1,-1,1,0,0)
			Self.Add(1,1,1,1,1)
			
			'FRONT TRI2
			Self.Add(-1,-1,1,0,0)
			Self.Add(1,-1,1,1,0)
			Self.Add(1,1,1,1,1)
			
			'RIGHT TRI1
			Self.Add(1,1,1)
			Self.Add(1,-1,1)
			Self.Add(1,-1,-1)
			
			'RIGHT TRI2
			Self.Add(1,1,1)
			Self.Add(1,-1,-1)
			Self.Add(1,1,-1)
			
			'BACK TRI1
			Self.Add(1,1,-1)
			Self.Add(1,-1,-1)
			Self.Add(-1,-1,-1)
			
			'BACK TRI2
			Self.Add(1,1,-1)
			Self.Add(-1,-1,-1)
			Self.Add(-1,1,-1)
			
			'LEFT TRI1
			Self.Add(-1,1,-1)
			Self.Add(-1,-1,-1)
			Self.Add(-1,-1,1)
			
			'LEFT TRI2
			Self.Add(-1,1,-1)
			Self.Add(-1,-1,1)
			Self.Add(-1,1,1)
		
			'BOTTOM TRI1
			Self.Add(-1,-1,1)
			Self.Add(-1,-1,-1)
			Self.Add(1,-1,-1)
			
			'BOTTOM TRI2
			Self.Add(-1,-1,1)
			Self.Add(1,-1,-1)
			Self.Add(1,-1,1)
			
			'TOP TRI1
			Self.Add(1,1,-1)
			Self.Add(-1,1,-1)	
			Self.Add(-1,1,1)
					
			'TOP TRI2
			Self.Add(1,1,1)			
			Self.Add(1,1,-1)
			Self.Add(-1,1,1)
								
		EndIf
	End Method
	
	Method AddModel(Filename$) 'not done
		'.3DS
		If Lower(Right$(FileName$,4) = ".3ds") Then
			Local Chunk_ID:Short,Chunk_Length,Chunk_Quantiny,Chunk_Flags,i
			Local File:TStream = OpenFile(FileName$,1,0)
			If File Then
				While Not Eof(File)	
					Chunk_ID = ReadShort(File)
					Chunk_Length = ReadInt(File)
					
					'Print Chunk_ID
					
					Select Chunk_ID
						Case $4d4d 'Main chunk
						Print "Found main chunk!"
						
						Case $3d3d '3d editor chunk
						Print "Found 3d editor chunk!"
						
						Case $4000 'Object chunk
						Print "Found object chunk!"
						'Read model name
						Repeat
							i = ReadByte(File)
							Name$ = Name$ + Asc(i)
						Until Eof(File) Or (Asc(i) = 0) Or (i = 13)
						
						Print Name$
					
					End Select
					
					'Seek
					File.Seek(File.Pos()+Chunk_Length-6)
					
				Wend
			EndIf
		EndIF
	End Method
	
	
	Method Move(AddX#=0,AddY#=0,AddZ#=0)
		X# = X# + Cos(RotY#) * AddX#
		Y# = Y# + Sin(RotZ#) * AddX#
		Z# = Z# + Sin(RotY#) * AddX#
		
		Y# = Y# + Cos(RotX#) * -AddY#

		X# = X# + Sin(-RotY#) * AddZ#
		Y# = Y# + Sin(RotX#) * AddZ#
		Z# = Z# + Cos(-RotY#) * AddZ#
	End Method
	
End type

Type Polygon
	Field P:Point[3]
End type

Type Point
	Field X#,Y#,Z#
	Field U#,V#
	Field R,G,B
End type

Type Graphics
	Field Width,Height,Depth,Screen
	Field Window,GetRect:Rect
	
	Field FPSTimer,FPSCounter,FPSShow

	Function Start:Graphics(NewWidth,NewHeight,NewDepth=32,NewScreen=0)
		Local Gfx:Graphics = New Graphics
		Gfx.Width = NewWidth
		Gfx.Height = NewHeight
		Gfx.Depth = NewDepth
		Gfx.Screen = NewScreen
		
		Local Flags = BGL_BACKBUFFER|BGL_DEPTHBUFFER
		If NewScreen = 0 Then Flags = Flags + BGL_FULLSCREEN
		bglCreateContext(Gfx.Width,Gfx.Height,Gfx.Depth,0,Flags)
		
		glViewport(0,0,Gfx.Width,Gfx.Height)
		
		Gfx.Window = GetActiveWindow()


		glShadeModel(GL_SMOOTH)
		glHint(GL_PERSPECTIVE_CORRECTION_HINT, GL_NICEST)
		glBlendFunc(GL_SRC_ALPHA, GL_ONE_MINUS_SRC_ALPHA)
		glClearColor(0,0,0, 0.0)
		glColor3f(1.0, 1.0, 1.0)
		glClearDepth(1.0)
		glEnable(GL_CULL_FACE)
		glEnable(GL_DEPTH_TEST) 
		glDepthFunc(GL_LEQUAL) 
		glMatrixMode(GL_PROJECTION)
		glLoadIdentity()
		gluPerspective(45.0, Float(Gfx.Width)/Float(Gfx.Height), 0.1, 100.0)
		glMatrixMode(GL_MODELVIEW)
		glLoadIdentity()
		
		Gfx.GetRect = New Rect
		
		Return Gfx
	End Function
	
	Method Viewport(NewX,NewY,NewWidth,NewHeight)
		glViewport(NewX,NewY,NewWidth,NewHeight)
	End Method
	
	
	Method ClearColor(R,G,B)
		glClearColor(R/255.0,G/255.0,B/255.0, 0.0)
	End Method
	
	
	Method Render(Cam:Camera)
		glLoadIdentity()
		
		'Position camera
		glRotatef(Cam.RotX#,1.0,0,0)
		glRotatef(Cam.RotY#,0,1.0,0)
		glRotatef(Cam.RotZ#,0,0,1.0)
		glTranslatef(Cam.X#,Cam.Y#,Cam.Z#)
		glPopMatrix()
		
		'Entities
		For Local Ent:Entity = EachIn Entity.EntityList	
			For Local Pol:Polygon = EachIn Ent.PolygonList
				glPushMatrix()
				
				'Texture
				If Ent.Txt &lt;&gt; Null Then
					glEnable(GL_TEXTURE_2D)
					glBindTexture(GL_TEXTURE_2D,Ent.Txt.GLID)
				EndIf
				
				'Alpha
				If Ent.Alpha# &lt;&gt; 1.0 Then
					glEnable(GL_BLEND)
				EndIf
				
				'WireFrame
				If Ent.Wireframe then
					glPolygonMode(GL_FRONT_AND_BACK, GL_LINE)
					glDisable(GL_CULL_FACE)
				EndIf
				
				
				'Position entity
				glTranslatef(Ent.X#,Ent.Y#,Ent.Z#)
				glRotatef(Ent.RotX#,1.0,0,0)
				glRotatef(Ent.RotY#,0,1.0,0)
				glRotatef(Ent.RotZ#,0,0,1.0)

				
				
				'Draw
				glBegin(GL_TRIANGLES)
				
				'Tri1
				If Ent.Alpha# &lt;&gt; 1.0 Then
					glColor4f(Pol.P[0].R/255.0,Pol.P[0].G/255.0,Pol.P[0].B/255.0,Ent.Alpha#)
				Else
					glColor3f(Pol.P[0].R/255.0,Pol.P[0].G/255.0,Pol.P[0].B/255.0)
				EndIf
				glTexCoord2f(Pol.P[0].U#,Pol.P[0].V#)
				glVertex3f(Pol.P[0].X#,Pol.P[0].Y#,Pol.P[0].Z#)

				'Tri2
				If Ent.Alpha# &lt;&gt; 1.0 Then
					glColor4f(Pol.P[1].R/255.0,Pol.P[1].G/255.0,Pol.P[1].B/255.0,Ent.Alpha#)
				Else
					glColor3f(Pol.P[1].R/255.0,Pol.P[1].G/255.0,Pol.P[1].B/255.0)
				EndIf
				glTexCoord2f(Pol.P[1].U#,Pol.P[1].V#)
				glVertex3f(Pol.P[1].X#,Pol.P[1].Y#,Pol.P[1].Z#)				

				'Tri3
				If Ent.Alpha# &lt;&gt; 1.0 Then
					glColor4f(Pol.P[2].R/255.0,Pol.P[2].G/255.0,Pol.P[2].B/255.0,Ent.Alpha#)
				Else
					glColor3f(Pol.P[2].R/255.0,Pol.P[2].G/255.0,Pol.P[2].B/255.0)
				EndIf
				glTexCoord2f(Pol.P[2].U#,Pol.P[2].V#)
				glVertex3f(Pol.P[2].X#,Pol.P[2].Y#,Pol.P[2].Z#)	
				
				glEnd()
				
				'Alpha
				If Ent.Alpha# &lt;&gt; 1.0 Then
					glDisable(GL_BLEND)
				EndIf
				
				'Texture
				If Ent.Txt &lt;&gt; Null Then
					glDisable(GL_TEXTURE_2D)	
				EndIf
				
				'WireFrame
				If Ent.Wireframe then
					glPolygonMode(GL_FRONT, GL_FILL)
					glEnable(GL_CULL_FACE)
				EndIf		
				
				glPopMatrix()
			Next
		Next
		
		'FPS Counter
		FPSCounter = FPSCounter + 1
		If MilliSecs() &gt; FPSTimer + 999 Then
			FPSTimer = MilliSecs()
			FPSShow = FPSCounter
			FPSCounter = 0
		EndIf
	End Method
	
	Method Cls()
		glClear(GL_COLOR_BUFFER_BIT|GL_DEPTH_BUFFER_BIT)
	End Method
	
	Method Flip()
		bglSwapBuffers()
	End Method
	
	Method CenterMouse()
		GetWindowRect(Window,GetRect)
		SetCursorPos(GetRect.X-Width/2,GetRect.Y-Height/2)
	End Method
	
	Method FPS()
		Return FPSShow
	End Method
End type

Type Texture
	Field GLID
	Field Priority#

	Function Create:Texture(Filename$)
		Local Txt:Texture = New Texture
		
		Local Pixmap:TPixmap = LoadPixmap(Filename$)
		If Not Pixmap Then
			Print "Could not load texture!"
			Return Null
		EndIf

		Txt.GLID = bglTexFromPixmap(Pixmap)
		If Not Txt.GLID Then
			Print "Could not apply texture to opengl!"
			Return Null
		EndIf
		
		
		
		
		glBindTexture(GL_TEXTURE_2D, Txt.GLID)
		glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_WRAP_S, GL_REPEAT)
		glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_WRAP_T, GL_REPEAT)
		glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER, GL_NEAREST)
		glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_NEAREST)

		Pixmap = Null
				
		
		Return Txt
	End Function
	
End type


Type Camera
	Field X#,Y#,Z#
	Field RotX#,RotY#,RotZ#
	
	Function Create:Camera(NewX#=0,NewY#=0,NewZ#=0)
		Local Cam:Camera = New Camera
		Cam.X# = NewX#
		Cam.Y# = NewY#
		Cam.Z# = NewZ#
		
		Return Cam
	End Function
	
	Method Turn(AddX#=0,AddY#=0,AddZ#=0)
		RotX# = RotX# + AddX#
		RotY# = RotY# + AddY#
		RotZ# = RotZ# + AddZ#
		
		If RotX# &gt; 360 Then RotX# = 0
		If RotY# &gt; 360 Then RotY# = 0
		If RotZ# &gt; 360 Then RotZ# = 0			

		If RotX# &lt; 0 Then RotX# = 360
		If RotY# &lt; 0 Then RotY# = 360
		If RotZ# &lt; 0 Then RotZ# = 360					
	End Method
	
	Method Rotate(NewX#=0,NewY#=0,NewZ#=0)
		RotX# = NewX#
		RotY# = NewY#
		RotZ# = NewZ#
		
		If RotX# &gt; 360 Then RotX# = 0
		If RotY# &gt; 360 Then RotY# = 0
		If RotZ# &gt; 360 Then RotZ# = 0			

		If RotX# &lt; 0 Then RotX# = 360
		If RotY# &lt; 0 Then RotY# = 360
		If RotZ# &lt; 0 Then RotZ# = 360					
	End Method
	
	Method Move(AddX#=0,AddY#=0,AddZ#=0)
		X# = X# + Cos(RotY#) * AddX#
		Y# = Y# + Sin(RotZ#) * AddX#
		Z# = Z# + Sin(RotY#) * AddX#
		
		Y# = Y# + Cos(RotX#) * -AddY#

		X# = X# + Sin(-RotY#) * AddZ#
		Y# = Y# + Sin(RotX#) * AddZ#
		Z# = Z# + Cos(-RotY#) * AddZ#
	End Method
	
	
End type
</textarea><br><br>opengl.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'Application: opengl test
'Author: nawi
'Description: test the opengl lib
'
Strict


'for mouselook
Local MX,MY



Include "library.bmx"

'Initialize
Local System:Graphics = Graphics.Start(800,600,32,1)

'Create entity
Local Cube:Entity = Entity.Create()
'Cube.AddPrimitive(MDL_CUBE)
Cube.Add(-1,1,-1,0,0)
Cube.Add(-1,-1,-1,0,1)
Cube.Add(1,-1,-1,1,1)

Cube.Add(1,-1,-1,0,0)
Cube.Add(1,1,-1,1,0)
Cube.Add(-1,1,-1,1,1)

Cube.SetColor(107,230,255)

Local Cube2:Entity = Entity.Create(-4)
Cube2.AddPrimitive(MDL_CUBE)
Cube2.Alpha# = 0.5
Cube2.SetColor(255,245,68)

Local Fighter:Entity = Entity.Create(7)
'Fighter.AddModel("fighter.3ds")
Fighter.AddPrimitive(MDL_CUBE)
Fighter.Wireframe = True

'Create camera
Local Cam:Camera = Camera.Create(0,0,-10)

'Load texture
Local Txt:Texture = Texture.Create("tex.bmp")

Cube.ApplyTexture(Txt)

Repeat
	System.Cls()
	



	'Mouselook
	Cam.Turn(Float(MouseY() - MY)/10.0,Float(MouseX() - MX)/10.0,0)
	System.CenterMouse()
	MX = MouseX()
	MY = MouseY()
	
	'move camera
	If KeyDown(KEY_UP) Then Cam.Move(0,0,0.07)
	If KeyDown(KEY_DOWN) Then Cam.Move(0,0,-0.07)
	If KeyDown(KEY_LEFT) Then Cam.Move(0.07)
	If KeyDown(KEY_RIGHT) Then Cam.Move(-0.07)
	If KeyDown(KEY_Q) Then Cam.Move(0,0.07)
	If KeyDOWN(KEY_A) Then Cam.Move(0,-0.07)
	
	
	
	'rotate cube
	Cube2.Turn(0.2,0.2,0.1)
		
	System.Render(Cam)
	System.Flip()
	Print "FPS " + System.FPS() + " MEM " + MemAlloced()
	FlushMem()
Until KeyHit(KEY_ESCAPE)
</textarea> <br><br></td></tr></table><br>
<a name="572954"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Extron</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Your UV texture is broken. ;)<br>To prove this, try this :<br><pre class=code>
				glBegin(GL_TRIANGLES)
				
				'Tri1
				If Ent.Alpha# &lt;&gt; 1.0 Then
					glColor4f(Pol.P[0].R/255.0,Pol.P[0].G/255.0,Pol.P[0].B/255.0,Ent.Alpha#)
				Else
					glColor3f(Pol.P[0].R/255.0,Pol.P[0].G/255.0,Pol.P[0].B/255.0)
				EndIf
				glTexCoord2f(0.0,0.0) 'Pol.P[0].U#,Pol.P[0].V#)
				glVertex3f(Pol.P[0].X#,Pol.P[0].Y#,Pol.P[0].Z#)

				'Tri2
				If Ent.Alpha# &lt;&gt; 1.0 Then
					glColor4f(Pol.P[1].R/255.0,Pol.P[1].G/255.0,Pol.P[1].B/255.0,Ent.Alpha#)
				Else
					glColor3f(Pol.P[1].R/255.0,Pol.P[1].G/255.0,Pol.P[1].B/255.0)
				EndIf
				glTexCoord2f(1.0,0.0) 'Pol.P[1].U#,Pol.P[1].V#)
				glVertex3f(Pol.P[1].X#,Pol.P[1].Y#,Pol.P[1].Z#)				

				'Tri3
				If Ent.Alpha# &lt;&gt; 1.0 Then
					glColor4f(Pol.P[2].R/255.0,Pol.P[2].G/255.0,Pol.P[2].B/255.0,Ent.Alpha#)
				Else
					glColor3f(Pol.P[2].R/255.0,Pol.P[2].G/255.0,Pol.P[2].B/255.0)
				EndIf
				glTexCoord2f(0.0,1.0) 'Pol.P[2].U#,Pol.P[2].V#)
				glVertex3f(Pol.P[2].X#,Pol.P[2].Y#,Pol.P[2].Z#)	
				
				glEnd()
</pre> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
