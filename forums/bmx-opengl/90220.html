<!DOCTYPE html><html lang="en" ><head ><title >Speed test:  Faster Plots, Lines, and Rects</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Speed test:  Faster Plots, Lines, and Rects</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=103" >OpenGL Module</a>/<a href="#bottom" >Speed test:  Faster Plots, Lines, and Rects</a><br><br>
<a name="1025375"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >EOF</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> *EDIT: Code demo now with additional standard IO printout and Fast Rand (From Zeke)<br><br>GL coders<br><br>Can you run the code below and let me know if you see much of a speed difference between regular BlitzMax Plot, DrawLine, DrawRect vs the batch-processed versions in the demo?<br>What I mean by batch-processed is,  drawing is done within a BEGIN2D .. END2D block to cut out a little overhead<br><br>In my testing I typically see the Line routine to be somewhere like twice as fast but the Plotting and quads, although faster, aren't significantly so<br><br>BTW I don't know much about OpenGL so feel free to comment about the commands used. Especially if you know even quicker methods<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' GL 2D drawing speed test
' Plot, DrawLine, DrawRect vs Fast alternatives

SuperStrict
Framework BRL.GLMax2D
Import BRL.Random
Import BRL.StandardIO

Extern "C"
	Function FSeedRnd:Int(val:Int) = "srand"
	Function _crand:Int () = "rand"
End Extern

FSeedRnd MilliSecs()

Const width%=640 , height%=480
Const numPlots%=250000 , numLines%=150000 , numRects%=80000
' timer-related variables
Global t%,nPlot%,fPlot%,nLine%,fLine%,nRect%,fRect%

Graphics width,height,0

' batch vs standard tests
SetColor 100,50,50
SpeedTest "PLOT" , 0 ; fPlot=MilliSecs()-t
SpeedTest "PLOT" , 1 ; nPlot=MilliSecs()-t
SetColor 50,100,50
SpeedTest "LINE" , 0 ; fLine=MilliSecs()-t
SpeedTest "LINE" , 1 ; nLine=MilliSecs()-t
SetColor 50,50,100
SpeedTest "RECT" , 0 ; fRect=MilliSecs()-t
SpeedTest "RECT" , 1 ; nRect=MilliSecs()-t
Delay 500
SetColor 200,200,200

Function SpeedTest(tst$,mode%)
	Cls
	t=MilliSecs()
	Local n%
	'
	If mode=0 ' ----------- FAST  -------------
		Select tst
			Case "PLOT"
				Begin2D(GL_POINTS)
				For n=1 To numPlots
					rndc
					FastPlot Rand(width),Rand(height)
				Next
				End2D
			Case "LINE"
				Begin2D(GL_LINES)
				For n=1 To numLines
					rndc
					FastLine Rand(width),Rand(height),Rand(width),Rand(height)
				Next
				End2D
			Case "RECT"
				Begin2D(GL_QUADS)
				For n=1 To numRects
					rndc
					FastRect Rand(width),Rand(height),Rand(100),Rand(100)
				Next
				End2D
		End Select
	Else ' ----------- STANDARD -----------------
		Select tst
			Case "PLOT"
				For n=1 To numPlots
					rndc
					Plot Rand(width),Rand(height)
				Next
			Case "LINE"
				For n=1 To numLines
					rndc
					DrawLine Rand(width),Rand(height),Rand(width),Rand(height)
				Next
			Case "RECT"
				For n=1 To numRects
					rndc
					DrawRect Rand(width),Rand(height),Rand(100),Rand(100)
				Next
		End Select
	EndIf
	'
	Flip
End Function


' _________________________________________________

Cls
txt "Number of dots = "+String(numPlots)
txt "  Fast plot (m/secs)= "+String(fPlot)
txt "Normal plot (m/secs)= "+String(nPlot)
txt "---"
txt "Number of lines = "+String(numLines)
txt "  Fast line (m/secs)= "+String(fLine)
txt "Normal line (m/secs)= "+String(nLine)
txt "---"
txt "Number of rects = "+String(numRects)
txt "  Fast rect (m/secs)= "+String(fRect)
txt "Normal rect (m/secs)= "+String(nRect)
Flip
WaitKey
End

Function txt(m$)
	Global y%=5
	DrawText m,4,y ; y:+22
	Print m
End Function


' _________________________________________________

Function FastPlot(x%,y%)
	glVertex2f x,y
End Function

Function FastLine(x1%,y1%,x2%,y2%)
	glVertex2f x1,y1 ; glVertex2f x2,y2
End Function

Function FastRect(x%,y%,w%,h%)
	glVertex2f x,y ; glVertex2f x+w,y
	glVertex2f x+w,y+h ; glVertex2f x,y+h
End Function


Function Begin2D(mode%)
	glDisable GL_TEXTURE_2D ; glBegin mode
End Function

Function End2D()
	glEnd ; glEnable GL_TEXTURE_2D	
End Function

Function rndc()
	SetColor FRand(0,255),FRand(0,255),FRand(0,255)
End Function

'Faster Rand() function, range 0-32768
Function FRand:Int (s:Int,e:Int)
	Return _crand() Mod (e-s+1)+s
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1025393"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_Skully</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Number of dots = 250000<br><br>  Fast plot (m/secs)= 532<br><br>Normal plot (m/secs)= 640<br><br>---<br><br>Number of lines = 150000<br><br>  Fast line (m/secs)= 403<br><br>Normal line (m/secs)= 534<br><br>---<br><br>Number of rects = 80000<br><br>  Fast rect (m/secs)= 287<br><br>Normal rect (m/secs)= 252 <br><br></td></tr></table><br>
<a name="1025437"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> DebugLog:Number of dots = 250000<br>DebugLog:  Fast plot (m/secs)= 440<br>DebugLog:Normal plot (m/secs)= 596<br>DebugLog:---<br>DebugLog:Number of lines = 150000<br>DebugLog:  Fast line (m/secs)= 395<br>DebugLog:Normal line (m/secs)= 535<br>DebugLog:---<br>DebugLog:Number of rects = 80000<br>DebugLog:  Fast rect (m/secs)= 262<br>DebugLog:Normal rect (m/secs)= 247<br><br>(I changed the code to output on debug window, easier to copy results here)<br><br>Here rects are marginally slower with your code, dots a little bit faster and lines faster. <br><br></td></tr></table><br>
<a name="1025445"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Debug on<br>Number of dots = 250000<br>Fast plot (m/secs)= 482<br>Normal plot (m/secs)= 654<br>---<br>Number of lines = 150000<br>Fast line (m/secs)= 357<br>Normal line (m/secs)= 646<br>---<br>Number of rects = 80000<br>Fast rect (m/secs)= 386<br>Normal rect (m/secs)= 203<br><br><br>Debug OFF<br>Number of dots = 250000<br>Fast plot (m/secs)= 156<br>Normal plot (m/secs)= 268<br>---<br>Number of lines = 150000<br>Fast line (m/secs)= 155<br>Normal line (m/secs)= 293<br>---<br>Number of rects = 80000<br>Fast rect (m/secs)= 79<br>Normal rect (m/secs)= 109<br><br><br><br>Hardware specs in sig <br><br></td></tr></table><br>
<a name="1025446"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Zeke</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Blitz rand:<br>fast Plot = 164<br>normal Plot = 360<br><br>fast line = 145<br>normal line = 353<br><br>fast rect = 84<br>normal rect = 147<br><br>FastRand:<br>fast Plot = 84<br>normal Plot = 306<br><br>fast line = 69<br>normal line = 305<br><br>fast rect = 47<br>normal rect = 107<br><br>FastRand version:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' GL 2D drawing speed test
' Plot, DrawLine, DrawRect vs Fast alternatives

SuperStrict
Framework BRL.GLMax2D
Import BRL.Random

Extern "C"
	Function crndseed:Int(val:Int) = "srand"
	Function _crand:Int () = "rand"
End Extern

Function FastRand:Int (start:Int, ende:Int) 'Faster Rand() function, range 0-32768
	Return _crand() Mod (ende-start+1) + start
EndFunction

crndseed(MilliSecs())

Const width%=640 , height%=480
Const numPlots%=250000 , numLines%=150000 , numRects%=80000
' timer-related variables
Global t%,nPlot%,fPlot%,nLine%,fLine%,nRect%,fRect%

Graphics width,height,0

' batch vs standard tests
SetColor 100,50,50
SpeedTest "PLOT" , 0 ; fPlot=MilliSecs()-t
SpeedTest "PLOT" , 1 ; nPlot=MilliSecs()-t
SetColor 50,100,50
SpeedTest "LINE" , 0 ; fLine=MilliSecs()-t
SpeedTest "LINE" , 1 ; nLine=MilliSecs()-t
SetColor 50,50,100
SpeedTest "RECT" , 0 ; fRect=MilliSecs()-t
SpeedTest "RECT" , 1 ; nRect=MilliSecs()-t
Delay 500
SetColor 200,200,200

Function SpeedTest(tst$,mode%)
	Cls
	t=MilliSecs()
	Local n%
	'
	If mode=0 ' ----------- FAST  -------------
		Select tst
			Case "PLOT"
				Begin2D(GL_POINTS)
				For n=1 To numPlots
					rndc
					FastPlot FastRand(0,width),FastRand(0,height)
				Next
				End2D
			Case "LINE"
				Begin2D(GL_LINES)
				For n=1 To numLines
					rndc
					FastLine FastRand(0,width),FastRand(0,height),FastRand(0,width),FastRand(0,height)
				Next
				End2D
			Case "RECT"
				Begin2D(GL_QUADS)
				For n=1 To numRects
					rndc
					FastRect FastRand(0,width),FastRand(0,height),FastRand(0,100),FastRand(0,100)
				Next
				End2D
		End Select
	Else ' ----------- STANDARD -----------------
		Select tst
			Case "PLOT"
				For n=1 To numPlots
					rndc
					Plot FastRand(0,width),FastRand(0,height)
				Next
			Case "LINE"
				For n=1 To numLines
					rndc
					DrawLine FastRand(0,width),FastRand(0,height),FastRand(0,width),FastRand(0,height)
				Next
			Case "RECT"
				For n=1 To numRects
					rndc
					DrawRect FastRand(0,width),FastRand(0,height),FastRand(0,100),FastRand(0,100)
				Next
		End Select
	EndIf
	'
	Flip
End Function


' _________________________________________________

Cls
txt "Number of dots = "+String(numPlots)
txt "  Fast plot (m/secs)= "+String(fPlot)
txt "Normal plot (m/secs)= "+String(nPlot)
txt "---"
txt "Number of lines = "+String(numLines)
txt "  Fast line (m/secs)= "+String(fLine)
txt "Normal line (m/secs)= "+String(nLine)
txt "---"
txt "Number of rects = "+String(numRects)
txt "  Fast rect (m/secs)= "+String(fRect)
txt "Normal rect (m/secs)= "+String(nRect)
Flip
WaitKey
End

Function txt(m$)
	Global y%=5
	DrawText m$,4,y ; y:+22
End Function


' _________________________________________________

Function FastPlot(x%,y%)
	glVertex2f x,y
End Function

Function FastLine(x1%,y1%,x2%,y2%)
	glVertex2f x1,y1 ; glVertex2f x2,y2
End Function

Function FastRect(x%,y%,w%,h%)
	glVertex2f x,y ; glVertex2f x+w,y
	glVertex2f x+w,y+h ; glVertex2f x,y+h
End Function


Function Begin2D(mode%)
	glDisable GL_TEXTURE_2D ; glBegin mode
End Function

Function End2D()
	glEnd ; glEnable GL_TEXTURE_2D	
End Function

Function rndc()
	SetColor FastRand(0,255),FastRand(0,255),FastRand(0,255)
End Function</textarea> <br><br></td></tr></table><br>
<a name="1025460"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Zeke: the RAND built-in function in BlitzMax is so slow?!? 40ms? Interesting <br><br></td></tr></table><br>
<a name="1025472"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >EOF</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> (I changed the code to output on debug window, easier to copy results here)<br><br>Here rects are marginally slower with your code, dots a little bit faster and lines faster <br></div>Updated code with additional Print() output<br>Yes, I find the standard Rect sometimes reports a faster time but upon re-running the code several times the FastRect wins out (marginally)<br><br>It's interesting how the Line drawing seems to be the big winner here <br><br></td></tr></table><br>
<a name="1028889"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Noobody</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> @Zeke: the RAND built-in function in BlitzMax is so slow?!? 40ms? Interesting <br></div><br>Rand in BMax uses RndDouble(), maps the result to the interval (min, max) and casts it to Int.<br><br>This means that a call to Rand() involves a call to a normal random generator, a double division, a double multiplication and a cast from double to int. This is of course much slower than rand in C/++, which will most probably only use integers.<br><br>However, there's a problem with both functions. RndDouble() divides by 2^29 at the end (to map the integer value in range 0.0!-1.0!). This implies that the random number generated is only in range 0-2^29. If I were to call Rand( 2^31 ), only <i>a fourth</i> of the numbers in range can be actually generated. This is pretty bad.<br><br>On the other hand, the quality of rand in the cstdlib greatly depends on the implementation, but in a lot of cases the quality is poor. It will also only generate numbers in range of 0-RANGE_MAX. The value of RANGE_MAX depends on the implementation, but it is granted to be at least 32767 (and you shouldn't bet on more than that). 32767 is only 2^15, not enough for a lot of cases.<br><br>That's why I propose reimplementing BRL.Random with something faster and more exact (=covering all 32 bits). It doesn't have to be a fancy Mersenne Twister; a simple Multiply-with-carry would suffice, which is really easy to implement. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
