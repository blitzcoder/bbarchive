<!DOCTYPE html><html lang="en" ><head ><title >Simple FBO example</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Simple FBO example</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=103" >OpenGL Module</a>/<a href="#bottom" >Simple FBO example</a><br><br>
<a name="968668"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >plash</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> The code was converted from <a href="http://www.gamedev.net/reference/programming/features/fbo1/" target="_blank">this article</a> using tidbits from <a href="/posts.php?topic=85263" target="_blank">this thread</a>.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' FBO Example

SuperStrict

Framework brl.blitz
Import pub.glew
Import brl.GLGraphics
Import brl.standardio

Global fps:Int

Global fbo:Int					' Our handle to the FBO
Global depthBuffer:Int			' Our handle to the depth render buffer
Global img:Int					' Our handle to a texture

Const width:Int = 512		' The hight of the texture we'll be rendering to
Const height:Int = 512		' The width of the texture we'll be rendering to

' Used for drawing the 3D cube with our rendered texture on it
Global xrot:Float = 0			' X Rotation
Global yrot:Float = 0			' Y Rotation
Global xspeed:Float = 0.2		' X Rotation Speed
Global yspeed:Float = 0.1		' Y Rotation Speed

MainLoop()

Function MainLoop()
	
	SetGraphicsDriver GLGraphicsDriver()
	GLGraphics(800, 600, 0, 0, GRAPHICS_BACKBUFFER | GRAPHICS_DEPTHBUFFER)
	glewInit() 'must be done AFTER window creation
	Init()
	
	DebugLog("Main_begin")
	
	Local elapsed:Int
	Local frames:Int = 0
	Local frameTime:Long = 0
	
	While Not KeyDown(KEY_ESCAPE) And Not AppTerminate()
		Local time:Long = MilliSecs()
		
		Display()
		
		elapsed = MilliSecs() - time
		frames:+1
		frameTime:+elapsed
		If frameTime &gt; 1000
			fps = frames
			frames = 0
			frameTime = 0
		EndIf
		
	Wend
	
	Shutdown()
	
	End

End Function

Function Init()
	
	If GL_VERSION_1_1
		Print "GL_VERSION_1_1"
	End If
	If GL_VERSION_1_2
		Print "GL_VERSION_1_2"
	End If
	If GL_VERSION_1_3
		Print "GL_VERSION_1_3"
	End If
	If GL_VERSION_1_4
		Print "GL_VERSION_1_4"
	End If
	If GL_VERSION_1_5
		Print "GL_VERSION_1_5"
	End If
	If GL_VERSION_2_0
		Print "GL_VERSION_2_0"
	End If
	If GL_VERSION_2_1
		Print "GL_VERSION_2_1"
	End If
	If GL_VERSION_3_0
		Print "GL_VERSION_3_0"
	End If
	
	glShadeModel(GL_SMOOTH)
	glClearColor(0.0, 0.0, 0.2, 0.5)
	glClearDepth(1.0)
	glEnable(GL_DEPTH_TEST)
	glDepthFunc(GL_LEQUAL)
	glViewport(0, 0, 800, 600)
	Reshape(800, 600)
	
	DebugLog("Init")
	' Setup our FBO
	glGenFramebuffersEXT(1, Varptr(fbo))
	glBindFramebufferEXT(GL_FRAMEBUFFER_EXT, fbo)
	
	' Create the render buffer for depth	
	glGenRenderbuffersEXT(1, Varptr(depthBuffer))
	glBindRenderbufferEXT(GL_RENDERBUFFER_EXT, depthBuffer)
	glRenderbufferStorageEXT(GL_RENDERBUFFER_EXT, GL_DEPTH_COMPONENT, width, height)
	
	' Now setup a texture to render to
	glGenTextures(1, Varptr(img))
	glBindTexture(GL_TEXTURE_2D, img)
	glTexImage2D(GL_TEXTURE_2D, 0, GL_RGBA8,  width, height, 0, GL_RGBA, GL_UNSIGNED_BYTE, NULL)
	glTexParameterf(GL_TEXTURE_2D, GL_TEXTURE_WRAP_S, GL_CLAMP_TO_EDGE)
	glTexParameterf(GL_TEXTURE_2D, GL_TEXTURE_WRAP_T, GL_CLAMP_TO_EDGE)
	glTexParameterf(GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER, GL_LINEAR)
	glTexParameterf(GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_LINEAR)
	
	'The following 3 lines enable mipmap filtering and generate the mipmap data so rendering works
	'glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER, GL_LINEAR)
	'glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_LINEAR_MIPMAP_LINEAR)
	'glGenerateMipmapEXT(GL_TEXTURE_2D)

	' And attach it to the FBO so we can render to it
	glFramebufferTexture2DEXT(GL_FRAMEBUFFER_EXT, GL_COLOR_ATTACHMENT0_EXT, GL_TEXTURE_2D, img, 0)

	' Attach the depth render buffer to the FBO as it's depth attachment
	glFramebufferRenderbufferEXT(GL_FRAMEBUFFER_EXT, GL_DEPTH_ATTACHMENT_EXT, GL_RENDERBUFFER_EXT, depthBuffer)
	
	Local status:Int = glCheckFramebufferStatusEXT(GL_FRAMEBUFFER_EXT)
	If status &lt;&gt; GL_FRAMEBUFFER_COMPLETE_EXT
		DebugLog("status &lt;&gt; GL_FRAMEBUFFER_COMPLETE_EXT")
		End
	End If
	
	glBindFramebufferEXT(GL_FRAMEBUFFER_EXT, 0) 	' Unbind the FBO for now
	
	CheckError()
	
End Function

Function Display()
	' First we bind the FBO so we can render to it
	glBindFramebufferEXT(GL_FRAMEBUFFER_EXT, fbo)
	
	' Save the view port and set it to the size of the texture
	glPushAttrib(GL_VIEWPORT_BIT)
	glViewport(0, 0, width, height)
	
	' Then render as normal
	' Today's scene is a wonderful multi-coloured spinning cube )
	glClearColor(0.0, 0.0, 0.0, 0.5)
	glClear(GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT)	' Clear Screen And Depth Buffer
	glLoadIdentity()
	
	glTranslatef(0.0, 0.0, - 2.0)
	glRotatef(xrot, 1.0, 0.0, 0.0)
	glRotatef(yrot, 0.0, 1.0, 0.0)
	
	glBegin(GL_QUADS)
		' Front Face
		glColor4f(0.0, 1.0, 0.0, 1.0)
		glVertex3f(- 0.5, - 0.5, 0.5)
		glVertex3f(0.5, - 0.5, 0.5)
		glVertex3f(0.5, 0.5, 0.5)
		glVertex3f(- 0.5, 0.5, 0.5)
		' Back Face
		glColor4f(1.0, 0.0, 0.0, 1.0)
		glVertex3f(- 0.5, - 0.5, - 0.5)
		glVertex3f(- 0.5, 0.5, - 0.5)
		glVertex3f(0.5, 0.5, - 0.5)
		glVertex3f(0.5, - 0.5, - 0.5)
		' Top Face
		glColor4f(0.0, 0.0, 1.0, 1.0)
		glVertex3f(- 0.5, 0.5, - 0.5)
		glVertex3f(- 0.5, 0.5, 0.5)
		glVertex3f(0.5, 0.5, 0.5)
		glVertex3f(0.5, 0.5, - 0.5)
		' Bottom Face
		glColor4f(0.0, 1.0, 1.0, 1.0)
		glVertex3f(- 0.5, - 0.5, - 0.5)
		glVertex3f(0.5, - 0.5, - 0.5)
		glVertex3f(0.5, - 0.5, 0.5)
		glVertex3f(- 0.5, - 0.5, 0.5)
		' Right face
		glColor4f(1.0, 1.0, 0.0, 1.0)
		glVertex3f(0.5, - 0.5, - 0.5)
		glVertex3f(0.5, 0.5, - 0.5)
		glVertex3f(0.5, 0.5, 0.5)
		glVertex3f(0.5, - 0.5, 0.5)
		' Left Face
		glColor4f(1.0, 1.0, 1.0, 1.0)
		glVertex3f(- 0.5, - 0.5, - 0.5)
		glVertex3f(- 0.5, - 0.5, 0.5)
		glVertex3f(- 0.5, 0.5, 0.5)
		glVertex3f(- 0.5, 0.5, - 0.5)
	glEnd()

	' Restore old view port and set rendering back to default frame buffer
	glPopAttrib()
	glBindFramebufferEXT(GL_FRAMEBUFFER_EXT, 0)
	
	glClearColor(0.0, 0.0, 0.2, 0.5)
	glClear(GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT)	' Clear Screen And Depth Buffer
	glLoadIdentity()
	' Now bind the texture to use it
	glBindTexture(GL_TEXTURE_2D, img)
	'If you enabled the mipmap filtering on setup earlier then you'll need to uncomment the line
	'below so OpenGL can generate all the mipmap data for the new main image each frame
	'glGenerateMipmapEXT(GL_TEXTURE_2D)
	glEnable(GL_TEXTURE_2D)
	
	glTranslatef(0.0, 0.0, - 2.0)
	glRotatef(- xrot, 1.0, 0.0, 0.0)
	glRotatef(- yrot, 0.0, 1.0, 0.0)
	
	glColor4f(1.0, 1.0, 1.0, 1.0)
	
	' This time it's a textured spinning cube!
	' The texture being the scene we just rendered!
	glBegin(GL_QUADS)
		' Front Face
		glNormal3f(0.0, 0.0, 1.0)
		glTexCoord2f(0.0, 1.0) glVertex3f(- 0.5, - 0.5, 0.5)
		glTexCoord2f(1.0, 1.0) glVertex3f(0.5, - 0.5, 0.5)
		glTexCoord2f(1.0, 0.0) glVertex3f(0.5, 0.5, 0.5)
		glTexCoord2f(0.0, 0.0) glVertex3f(- 0.5, 0.5, 0.5)
		' Back Face
		glNormal3f(0.0, 0.0, - 1.0)
		glTexCoord2f(1.0, 0.0) glVertex3f(- 0.5, - 0.5, - 0.5)
		glTexCoord2f(1.0, 1.0) glVertex3f(- 0.5, 0.5, - 0.5)
		glTexCoord2f(0.0, 1.0) glVertex3f(0.5, 0.5, - 0.5)
		glTexCoord2f(0.0, 0.0) glVertex3f(0.5, - 0.5, - 0.5)
		' Top Face
		glNormal3f(0.0, 1.0, 0.0)
		glTexCoord2f(0.0, 1.0) glVertex3f(- 0.5, 0.5, - 0.5)
		glTexCoord2f(0.0, 0.0) glVertex3f(- 0.5, 0.5, 0.5)
		glTexCoord2f(1.0, 0.0) glVertex3f(0.5, 0.5, 0.5)
		glTexCoord2f(1.0, 1.0) glVertex3f(0.5, 0.5, - 0.5)
		' Bottom Face
		glNormal3f(0.0, - 1.0, 0.0)
		glTexCoord2f(1.0, 1.0) glVertex3f(- 0.5, - 0.5, - 0.5)
		glTexCoord2f(0.0, 1.0) glVertex3f(0.5, - 0.5, - 0.5)
		glTexCoord2f(0.0, 0.0) glVertex3f(0.5, - 0.5, 0.5)
		glTexCoord2f(1.0, 0.0) glVertex3f(- 0.5, - 0.5, 0.5)
		' Right face
		glNormal3f(1.0, 0.0, 0.0)
		glTexCoord2f(1.0, 0.0) glVertex3f(0.5, - 0.5, - 0.5)
		glTexCoord2f(1.0, 1.0) glVertex3f(0.5, 0.5, - 0.5)
		glTexCoord2f(0.0, 1.0) glVertex3f(0.5, 0.5, 0.5)
		glTexCoord2f(0.0, 0.0) glVertex3f(0.5, - 0.5, 0.5)
		' Left Face
		glNormal3f(- 1.0, 0.0, 0.0)
		glTexCoord2f(0.0, 0.0) glVertex3f(- 0.5, - 0.5, - 0.5)
		glTexCoord2f(1.0, 0.0) glVertex3f(- 0.5, - 0.5, 0.5)
		glTexCoord2f(1.0, 1.0) glVertex3f(- 0.5, 0.5, 0.5)
		glTexCoord2f(0.0, 1.0) glVertex3f(- 0.5, 0.5, - 0.5)
	glEnd()
	
	glDisable(GL_TEXTURE_2D)
	
	xrot:+xspeed
	yrot:+yspeed
	
	'glxSwapBuffers()
	' Swap The Buffers To Not Be Left With A Clear Screen
	
	GLDrawText("fps:" + fps, 20, 90)
	
	Flip()
	
End Function

Function Shutdown()
	
	glDeleteFramebuffersEXT(1, Varptr(fbo))
	glDeleteRenderbuffersEXT(1, Varptr(depthBuffer))
	glDeleteTextures(1, Varptr(img))
	
End Function

Function Reshape(w:Int, h:Int)
	
	glViewport(0, 0, w, h)
	glMatrixMode(GL_PROJECTION)
	glLoadIdentity()
	
	If h = 0
		gluPerspective(80, Float(w), 1.0, 5000.0)
	Else
		gluPerspective(80, Float(w) / Float(h), 1.0, 5000.0)
	End If
	
	glMatrixMode(GL_MODELVIEW)
	glLoadIdentity()
			
End Function

Function CheckError()
	Local error:Int
	
	error = glGetError()
	If error &lt;&gt; GL_NO_ERROR
		DebugLog("GLError = " + error)
	Else
		DebugLog("No error")
	End If
	
End Function</textarea><br><br>I'm planning on implementing GLSL in my modified version of GLMax2D and I decided to use an FBO as the main rendering target (so that shaders can change the whole screen if they want to). <br><br></td></tr></table><br>
<a name="969283"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cool. Haven't coded this yet myself but was planning to very soon so I'll use this as reference :D <br><br></td></tr></table><br>
<a name="969683"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Played with this just now, it's pretty cool. Thanks for sharing the code.<br><br>I get a little puzzled with the differences between render buffer and framebuffer, etc... I'll have to read more in my OpenGL books.<br><br>I presume your color buffer attachment (texture) can't be any bigger than the maximum supported texture size? like 4096x4096 for example?<br><br>I know you can attach multiple buffers at once, tho and use then in shaders.<br><br>Also, I presume you cannot Flip 1 to cause the framebuffer object to be shown at all? You have to at some point copy from the framebuffer texture to the regular backbuffer before you can see anything? ie there's no way to use the fbo to flip without copying it/drawing with it? <br><br></td></tr></table><br>
<a name="969692"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >plash</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> I presume your color buffer attachment (texture) can't be any bigger than the maximum supported texture size? like 4096x4096 for example? <br></div>Yup, same with any other texture.<br><br><div class="quote"> ie there's no way to use the fbo to flip without copying it/drawing with it?  <br></div>As far as I know, that is correct. You have to render it at some point, which is rather disappointing (simply rendering to the backbuffer is much faster than drawing a texture that is screen_width*screen_height).<br><br>I'm going to be using glDrawBuffer[s] to tell the shaders which FBO(s) are active/which to use (glDrawBuffer[s] sets the indexes for gl_FragColor in a fragment shader).<br>I hardly understand how shaders work, so the implementation might be changed when I get more into it. <br><br></td></tr></table><br>
<a name="969724"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Arowx</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Plash got an error on GL_VERSION_2_1 not found with BlitzMax version 1.33? <br><br></td></tr></table><br>
<a name="969727"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >plash</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Hi Plash got an error on GL_VERSION_2_1 not found with BlitzMax version 1.33?  <br></div>Err, forgot to mention, you need this module modification: <br><a href="http://www.blitzbasic.com/Community/posts.php?topic=85518" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=85518</a><br><br>(Don't forget to add 'Const GL_TRUE:Int = True' to 'glew.bmx' if it isn't already there - I don't think Nilium updated the package yet.) <br><br></td></tr></table><br>
<a name="969731"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Arowx</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hmm What 2000+ fps is that good, but it's only a spinnging cube?<br><br>Cheers Plash! <br><br></td></tr></table><br>
<a name="969734"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >plash</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> it's only a spinnging cube? <br></div>Does the spinning cube also have a texture of another spinning cube? If so then it is working properly.<br><br>This example renders a spinning cube to the FBO and then uses the texture for the FBO and slaps it on another spinning cube (and then finally rendering to the backbuffer - my current code uses a fullscreen FBO, essentially acts as a 'backbuffer', but stores the drawn stuff in a texture which can be used in shaders). <br><br></td></tr></table><br>
<a name="969840"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> You can use textures in shaders without fbo's, right? <br><br></td></tr></table><br>
<a name="969944"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >plash</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> You can use textures in shaders without fbo's, right?  <br></div>Yes, you can. If you mean through shader uniforms, you have to do something like this (taken from <a href="http://nehe.gamedev.net/data/articles/article.asp?article=21" target="_blank">here</a> -- scroll to the bottom):<br><pre class=code>glUseProgramObjectARB(my_program)
Local my_sampler_uniform_location:Int = glGetUniformLocationARB(my_program, "my_texture")

glActiveTexture(GL_TEXTURE0 + i)
glBindTexture(GL_TEXTURE_2D, my_texture_object)

glUniform1iARB(my_sampler_uniform_location, i)</pre><br><br>Fragment shader:<br><pre class=code>uniform sampler2D my_texture;

void main(){
<i>.....</i>
}</pre><br><br>Also, <a href="http://www.gamedev.net/reference/programming/features/fbo2/page6.asp" target="_blank">this</a> is the glDrawBuffer[s] thing I was talking about.<br>I think you need to use FBOs/multiple textures to use more than one post-process shader at a time (<a href="http://groups.google.com/group/bmx3d/browse_thread/thread/2978209a79b88759" target="_blank">but I'm still not sure on that</a>). <br><br></td></tr></table><br>
<a name="970716"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jim Teeuwen</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> if you want your FBO to render to multiple buffers simultaneously, you have to specify the attachment points and pass them to glDrawBuffers, This also goes for any tricksies you want to perform on the textures attached to all these points (shaders etc).<br><br>I wrapped your FBO code in a little TFbo helper class.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
rem
	Represents an opengl Frame Buffer object (fbo).
	This object provides an easy interface for us
	to perform render-to-texture operations.
	
	Initial FBO code by Plash.
end rem


type TFbo final
	global fboColorBuffers:int[];
	field id:int;

	method new()
		self.id = -1;

               '// initialize colorBuffer pointer array.
		if(TFbo.fboColorBuffers.Length = 0) then
			local maxBuffers:int = 0;
			glGetIntegerv(GL_MAX_COLOR_ATTACHMENTS, varptr(maxBuffers));
			local idx:int = GL_COLOR_ATTACHMENT0_EXT;
			TFbo.fboColorBuffers = new int[maxBuffers];
			for local n:int = 0 to maxBuffers - 1
				TFbo.fboColorBuffers[n] = idx + n;
			next
		end if

		glGenFramebuffersEXT(1, Varptr(self.id));
	end method
	method delete()
		self.Dispose();
	end method
	
	method Dispose()
		if(self.id &lt;&gt; -1) then
			'// detach all textures
			for local n:int = 0 to fboColorBuffers.length - 1
				glFramebufferTexture2DEXT(GL_FRAMEBUFFER_EXT, ..
					fboColorBuffers[n], GL_TEXTURE_2D, 0, 0);
			next
			
			'// delete framebuffer
			glDeleteFramebuffersEXT(1, Varptr(self.id));
			self.id = -1;
		end if
	end method
	method Bind(indices:int[] = null) '// render to multiple targets simultaneously
		if(self.id &lt;&gt; -1) then
			glBindFramebufferEXT(GL_FRAMEBUFFER_EXT, self.id);
			'// We don't have to perform the glDrawBuffers() bit if we are
			'// only dealing with GL_COLOR_ATTACHMENT0_EXT. For any other
			'// buffer or multiple buffers, glDrawBuffers() is required to
			'// tell opengl to also draw to these render targets.
			if(indices &lt;&gt; null and indices.length &gt;= 1 and indices[0] &gt; 0) then
				local buffers:int[] = new int[indices.length];
				for local n:int = 0 to indices.length - 1
					buffers[n] = fboColorBuffers[indices[n]];
				next
				glDrawBuffers(buffers.length, buffers);
			end if
		end if
	end method
	method Unbind()
		if(self.id &lt;&gt; -1) then
			glBindFramebufferEXT(GL_FRAMEBUFFER_EXT, 0);
		end if
	end method
	method Attach(index:int, texture:int)
		glFramebufferTexture2DEXT(GL_FRAMEBUFFER_EXT, ..
			fboColorBuffers[index], GL_TEXTURE_2D, texture, 0);
		
		assert glCheckFramebufferStatusEXT(GL_FRAMEBUFFER_EXT) = GL_FRAMEBUFFER_COMPLETE_EXT, ..
				"TFbo.Attach::glCheckFramebufferStatusEXT != GL_FRAMEBUFFER_COMPLETE_EXT";
	end method
	method Detach(index:int)
		glFramebufferTexture2DEXT(GL_FRAMEBUFFER_EXT, ..
			fboColorBuffers[index], GL_TEXTURE_2D, 0, 0);
	end method
end type
</textarea><br><br>It's 'Bind(indices:int[] = null)' method automatically takes care of this.<br><br>Creating an FBo is done like this:<br><br><pre class=code>
local myfbo:TFbo = new TFbo;
local texture:int = CreateSomeTexture();

'// This binds our texture to attachment point 0.
'// Use any other index to attach it elsewhere.
'// This value can range from 0 to maxBuffers.
'// maxBuffers can be polled using this:
'//   local maxBuffers:int = 0;
'//   glGetIntegerv(GL_MAX_COLOR_ATTACHMENTS, varptr(maxBuffers));
'//
myfbo.Bind();
myfbo.Attach(0, texture);
myfbo.unbind();
</pre><br><br>To render to our FBO, we do this:<br><pre class=code>
...
myfbo.Bind();
... rendering stuff here
myfbo.unbind();
...
</pre><br><br>When calling Bind() without parameters, the code assumes we want to use the default single attachment point 0 (GL_COLOR_ATTACHMENT0_EXT).<br><br>if you want to render to a different buffer, or multiple buffers, simply pass an array with integer indices. of course these indices should have valid textures/etc attached to them.<br><br><pre class=code>
...
myfbo.Bind( [0, 2, 3, 6] );
... rendering stuff here
myfbo.unbind();
...
</pre> <br><br></td></tr></table><br>
<a name="970869"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Armitage 1982</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Plash<br><br>I hope you don't mind if I ask you a tips on FBO since you know much more than me about openGL and Glew implementation.<br><br>I was wondering if FBO is the way to go in my problem exposed in this thread : <a href="http://blitzmax.com/Community/posts.php?topic=85762" target="_blank">http://blitzmax.com/Community/posts.php?topic=85762</a><br><br>I don't know much how to perform bliting operations in texture buffer but I suppose this would increase performance and allow many more graphic effect ?<br><br>If you could confirm this then I would dig the subject.<br>Thanks <br><br></td></tr></table><br>
<a name="970991"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jim Teeuwen</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm not Plash, but i'll throw in my 2 cents anyways after some playing with FBo's in the past few days :)<br><br>Doing complex drawing operations will slow down your app regardless of what you render to, however I find I can greatly increase the overall performance of my application by having the actual redrawing of the textures done a fixed amount of times per second, and just draw a quad with the generated texture as fast as the computer can.<br><br>In my own situation, this involves drawing UI controls. The actual controls are only drawn when they change and a maximum of 60 times per second. The part that actually draws the textures to the screen is executed as fast as possible though. This loop only has to draw textures on fixed-size quads though, so it is pretty darn fast. On my hardware, it allows me to have real framerates of 700+ while keeping the actual complex drawing to 60 times per second yet still have smooth graphics.<br><br>The general idea is this:<br><br><pre class=code>
MyUpdateFps = 60;
Interval = 1000 / MyUpdateFps; '// maximum number updates per second.

while(MainLoopRunning)
  if(Time_since_last_update &gt;= Interval) then
    check_for_input_etc();

    redraw_textures_to_fbos_if_required();
  end if

  draw_simple_quads_with_dynamic_textures_from_fbos();
wend
</pre><br><br><br>This pretty much doubled the performance of my code as opposed to recreating the entire textures every frame, or even if only when it had to be updated. Limiting it by framerate works really well, and 60 times per second is more than enough to give the appearance of smooth, continuous updates. You can, of course, change the update framerate as you see fit.<br><br>Next to that, it helps to cache as many complex calculations as possible. If it is even remotely possible to pre-calculate specific bits at program start, or even just supplying a look-up-table with pre-calculated results, it is definitely worth using it. <br><br></td></tr></table><br>
<a name="972555"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >beanage</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for sharing. Right the ref i need atm. <br><br></td></tr></table><br>
<a name="982910"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yah that's a nice idea about having a different `draw to texture` framerate separate from the `copy to backbuffer` rate. It's a pity you can't just sort of bind the texture to the backbuffer so that it doesn't need to be copied, and so that when you Flip() it will just use that fbo/texture as the source of the flip.<br><br>Note that in GL it is possible to set the front buffer to be the one drawn to, and perhaps you could wait for a vertical blank somehow and then simply copy directly from the fbo-bound texture to the front buffer - a `manual flip`, to skip the copy to the backbuffer? <br><br></td></tr></table><br>
<a name="994688"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >beanage</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> I did some reformatting of Jim T's Code (replacement of space indents with tabs, removed [Then], aligning var declarations, relaced "' //" with "'", some more white lines, removed unnecessary semicolons etc).. I seem to be a little bit strict about this^^:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Rem
	Represents an opengl Frame Buffer object (fbo).
	This object provides an easy interface for us
	to perform render-to-texture operations.
	
	- Initial FBO code by Plash
	
	- FBO class by Jim T
	
	- Some [minor] reformatting by BeAnAge
End Rem

Type TFBO Final

	Global	fboColorBuffers	:Int[]
	Field	id			:Int

	Method New()
	
		id	= -1

		'initialize colorBuffer pointer array.
		If TFbo.fboColorBuffers.length= 0

			Local	maxBuffers	:Int	= 0
			Local	idx		:Int	= GL_COLOR_ATTACHMENT0_EXT
		
			glGetIntegerv	GL_MAX_COLOR_ATTACHMENTS, Varptr maxBuffers
		
			TFbo.fboColorBuffers	= New Int[ maxBuffers ]
		
			For Local n	:Int	= 0	To maxBuffers- 1
				TFbo.fboColorBuffers[ n ]	= idx+ n
				
			Next
		
		End If

		glGenFramebuffersEXT	1, Varptr id
	
	End Method

	Method Delete()
		
		Dispose
		
	End Method
	
	Method Dispose()

		If id&lt;&gt; -1
			
			'detach all textures
			For Local n	:Int	= 0	To fboColorBuffers.length- 1
				glFramebufferTexture2DEXT	GL_FRAMEBUFFER_EXT, fboColorBuffers[ n ], GL_TEXTURE_2D, 0, 0
			
			Next
			
			'delete framebuffer
			glDeleteFramebuffersEXT	1, Varptr id
			id					= -1
			
		End If

	End Method

	Method Bind( indices:Int[]= Null ) 'render to multiple targets simultaneously

		If id&lt;&gt; -1

			glBindFramebufferEXT	GL_FRAMEBUFFER_EXT, id

			'We don't have to perform the glDrawBuffers() bit if we are
			'only dealing with GL_COLOR_ATTACHMENT0_EXT. For any other
			'buffer or multiple buffers, glDrawBuffers() is required to
			'tell opengl to also draw to these render targets.

			If ( indices&lt;&gt; Null ) And ( indices.length&gt;= 1 ) And ( indices[0]&gt; 0 )

				Local buffers	:Int[]	= New Int[ indices.length ]

				For Local n	:Int	= 0	To indices.length- 1
					buffers[ n ]	= fboColorBuffers[ indices[ n ] ];

				Next

				glDrawBuffers	buffers.length, buffers

			End If

		End If

	End Method
	
	Method Unbind()
	
		If id&lt;&gt; -1
			glBindFramebufferEXT	GL_FRAMEBUFFER_EXT, 0

		End If
	
	End Method
	
	Method Attach( texture:Int,index:Int= 0 )
		
		glFramebufferTexture2DEXT	GL_FRAMEBUFFER_EXT, fboColorBuffers[ index ], GL_TEXTURE_2D, texture, 0
		
		Assert glCheckFramebufferStatusEXT( GL_FRAMEBUFFER_EXT )= GL_FRAMEBUFFER_COMPLETE_EXT, "TFbo.Attach::glCheckFramebufferStatusEXT != GL_FRAMEBUFFER_COMPLETE_EXT"
		
	End Method
	
	Method Detach( index:Int )

		glFramebufferTexture2DEXT	GL_FRAMEBUFFER_EXT, fboColorBuffers[ index ], GL_TEXTURE_2D, 0, 0

	End Method

End Type
</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
