<!DOCTYPE html><html lang="en" ><head ><title >A straight way into openGL...and fries with that?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >A straight way into openGL...and fries with that?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=103" >OpenGL Module</a>/<a href="#bottom" >A straight way into openGL...and fries with that?</a><br><br>
<a name="1004822"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, I am ashamed to go about this in such a way, but maybe someone of you has mercy with me...<br><br>DESIRED RESULT:  drawing a single textured, bumpmapped (normalmap is fine, too) vertex (with alpha) in a scene with a light (or more) and probably just a fixed camera, if that's needed?! (without having to use a mod, that is.)<br><br>WHERE I AM: I have no idea about openGL, yet. Checked into it enough to realize that it'll take me days or weeks to crawl towards a hope to set this up without growing a brain tumor. <i>(...I consider that likely!)</i><br><br>WHAT I GIVE: My eternal gratitude, guaranteed joy and happiness as well as a promiss to use this kickstart to explore and share my discoveries along the way. <i>(...I'm a wild one at that, honestly! Love to share!)</i><br><br>Any help is greatly appreciated. I'm really wondering if there's a simple and direct approach to that or if it takes a tremendous setup. Maybe encouragement would already help enough, but a tiny code example would friggin make me dance! :}<br><br>Thanks in advance and I apologize for appearing so lazy!<br><br>WHAT I'VE GATHERED SO FAR:<br>- shaders (.frag) are written in some kind of assembler (good old "MOV" brings back memories, MAD, DP3 and the likes. I understand that well!)<br>- those have to be loaded and somehow bound to what ever they manipulate or need (geometry/vertex and what ever fragment shaders are (pixel output manipulations?))<br>- then they need to be set again? I don't think I get it, yet. <br>- eventually all of this gets rendered.<br><br>Has anybody anywhere posted a simple one .bmx + .frag example that loads one image (if it isn't created in the code) and draws one polygon, bound with a shader and one light source and renders it into the middle of the window?<br><br>Can a shader be tied to a polygon that is not loaded but simply created in the code?<br><br>I admire all your virtuosity with this stuff, I must say. Here's a bow to you and my thanks for even thinking about helping me in, too. <br><br></td></tr></table><br>
<a name="1004989"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Alright, I realize I'm asking for a lot... so I'll post a simple code to show the setup...<br>It generates a pixmap as texture for simplicity reasons.<br>Then I swirl it around a little, moving vertecies of a textured polygon. The GL stuff is somewhat ripped together from several examples I found, while I've managed to simplify it a bit.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
Framework brl.GLMax2D
SetGraphicsDriver GLMax2DDriver()

Graphics 800,600,0

Local image_size:Int = 128
Global image_pix:TPixmap = CreatePixmap(image_size,image_size,PF_RGBA8888)'LockImage(image,0)
For Local x:Float = 0 Until image_size
	For Local y:Float = 0 Until image_size
	Local hsize:Float = image_size*0.5
	Local xp:Float = (x-hsize)*(3.6+2*Sin(y*6))
	Local yp:Float = y-hsize
	Local dot:Float = 1.0-Sqr(xp*xp+yp*yp)/hsize
	If dot &lt; 0 Then dot = 0
	Local p:Int = dot^0.8*255
	WritePixel(image_pix,x,y,((p/2+127) Shl 24)|(p Shl 16)|(p Shl 8)|p)
	Next
Next

SetClsColor 30,45,65
Local poly:Float[8] 
Local i:Int = 0
Local size:Float = 64
Local pos:Float[2]
Local rot:Float[2]
Local rox:Float[2]
Local lastTex:Int = GLTexFromPixmap(image_pix)
Repeat
	If glIsEnabled( GL_TEXTURE_2D )=0 Then
		glEnable( GL_TEXTURE_2D )
	EndIf
	
	glBindTexture( GL_TEXTURE_2D, lastTex)
	
	For Local o:Int = 0 To 200
		rot[0] = Sin(i+o*10)
		rot[1] = Cos(i+o*10)
		rox[0] = rot[1]
		rox[1] = -rot[0]
		pos[0] = 400+o*2*rot[1]
		pos[1] = 300-o*2*rot[0]
		
		poly[0] = -size*rot[0]+size*rox[0]
		poly[1] = -size*rot[1]+size*rox[1]
		poly[2] = size*rot[0]+size*rox[0]
		poly[3] = size*rot[1]+size*rox[1]
		poly[4] = size*rot[0]-size*rox[0]
		poly[5] = size*rot[1]-size*rox[1]
		poly[6] = -size*rot[0]-size*rox[0]
		poly[7] = -size*rot[1]-size*rox[1]
		
		poly[0]:+pos[0]
		poly[1]:+pos[1]
		poly[2]:+pos[0]
		poly[3]:+pos[1]
		poly[4]:+pos[0]
		poly[5]:+pos[1]
		poly[6]:+pos[0]
		poly[7]:+pos[1]
		glBegin GL_QUADS
			glTexCoord2f 0,0
			glVertex2f poly[0],poly[1]
			glTexCoord2f 0,1
			glVertex2f poly[2],poly[3]
			glTexCoord2f 1,1
			glVertex2f poly[4],poly[5]
			glTexCoord2f 1,0
			glVertex2f poly[6],poly[7]
		glEnd
	Next
	i:+1
	Flip 1
	glDisable( GL_TEXTURE_2D )
	Cls
Until KeyHit(KEY_ESCAPE)
End
</textarea><br><br>My question now is, can I simply link a shader to this? Oh, no, that's not a yes/no question, HAHAHA. Any help is appreciated a lot! <br><br>Next comes lighting and then I'd be done with this...  :} <br><br></td></tr></table><br>
<a name="1005088"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >FreakForFreedom</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yea, you can simply link a shader to this.<br>Here I got a little shader-file-loading-class:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Global CurrentProgramObject:Int = 0
Global TShader_List:TList = Null
Type TShader
	Field Name:String
	Field VSObj:Int
	Field FSObj:Int
	Field ProgObj:Int
	
	Field Link:TLink
	
	Function Load:TShader(vertexshaderfile:String, fragmentshaderfile:String, instancename:String = Null)
		If Not TShader_List Then TShader_List = CreateList()
		Local Tmp:TShader = New TShader
		Tmp.Link = ListAddFirst(TShader_List, Tmp)
		
		If instancename = Null Then
			Tmp.Name = vertexshaderfile
		Else
			Tmp.Name = instancename
		End If
		
		Local file:String = vertexshaderfile
		Local file2:String = fragmentshaderfile
		
		Tmp.ProgObj = glCreateProgramObjectARB()
		Tmp.VSObj = glCreateShaderObjectARB(GL_VERTEX_SHADER_ARB)
		Tmp.FSObj = glCreateShaderObjectARB(GL_FRAGMENT_SHADER_ARB)
		
		Local fileStream:TStream = OpenFile(file)
		Local fileStream2:TStream = OpenFile(file2)
		
		Local shaderSrcVertex:Byte[FileSize(file) + 1]
		Local i:Int = 0
		While Not Eof(fileStream)
		   shaderSrcVertex[i] = ReadByte(fileStream)
		   i :+ 1
		   If i &gt; FileSize(file) Then i = FileSize(file)
		Wend
		CloseFile(fileStream)
		
		Local pointer:Byte Ptr = Varptr shadersrcVertex[0]
		Local laenge:Int = FileSize(file)
		
		glShaderSourceARB(Tmp.VSObj, 1, Varptr pointer, Varptr laenge)
		glCompileShaderARB(Tmp.VSObj)
		
		debugShader(Tmp.VSObj)
		   
		Local shaderSrcFragment:Byte[FileSize(file2) + 1]
		i = 0
		While Not Eof(fileStream2)
		   shaderSrcFragment[i] = ReadByte(fileStream2)
		   i :+ 1
		   If i &gt; FileSize(file2) Then i = FileSize(file2)
		Wend
		CloseFile(fileStream2)
		
		pointer = Varptr shadersrcFragment[0]
		laenge = FileSize(file2)
		
		glShaderSourceARB(Tmp.FSObj, 1, Varptr pointer, Varptr laenge)
		glCompileShaderARB(Tmp.FSObj)
		
		debugShader(Tmp.FSObj)
		
		glAttachObjectARB(Tmp.ProgObj, Tmp.VSObj)
		glAttachObjectARB(Tmp.ProgObj, Tmp.FSObj)
		
		glDeleteObjectARB(Tmp.VSObj)
		glDeleteObjectARB(Tmp.FSObj)
		
		glLinkProgramARB(Tmp.ProgObj)

		Return Tmp
	End Function
	
	Function debugShader(shaderObject:Int)
		Local laengedeslogs:Float
		glGetObjectParameterfvARB(shaderObject,GL_OBJECT_INFO_LOG_LENGTH_ARB, Varptr laengedeslogs)
		
		Local bytearray:Byte[] = New Byte[laengedeslogs]
		Local laenge23:Int = 0
		glGetInfoLogARB(shaderObject, laengedeslogs, Varptr(laenge23), Varptr(bytearray[0]));
		Local msg:String = ""
		For Local j:Int = 0 To laengedeslogs - 1
		   msg = msg + Chr(bytearray[j])
		Next
		DebugLog  "ShaderLog: "+msg
	End Function
	
	Method Use()
		glUseProgramObjectARB(Self.ProgObj)
	End Method
	
	Function TurnOff()
		glUseProgramObjectARB(Null)
	End Function
	
	Function DestroyAll()
		If TShader_List &lt;&gt; Null Then
			For Local Tmp:TShader = EachIn TShader_List
				Tmp.Destroy()
				Tmp = Null
			Next
			TShader_List.Clear()
			TShader_List = Null
		End If
	End Function
	
	Method Destroy()
		glDetachObjectARB(Self.ProgObj, Self.VSObj)
		glDeleteObjectARB(Self.VSObj)
		Self.VSObj = Null
		glDetachObjectARB(Self.ProgObj, Self.FSObj)
		glDeleteObjectARB(Self.FSObj)
		Self.FSObj = Null
		glDeleteObjectARB(Self.ProgObj)
		Self.ProgObj = Null
		Self.Name = Null
		RemoveLink(Self.Link)
	End Method
End Type
</textarea><br><br>What you simply need to do, is load the shader like this:<br><pre class=code>Global Dot3:TShader = TShader.Load("dot3.vert", "dot3.frag")</pre><br>then declare that you want to use it<br><pre class=code>Dot3.Use()</pre><br>You can change it at runtime, like <pre class=code>BumpMapping.Use()</pre> or turn all shaders off: <pre class=code>TShader.TurnOff()</pre><br><br>If you want a simple lighting shader:<br>heres the GLSL code (Sry, no assembler):<br><br>.frag<br><pre class=code>void main()
{
	gl_FragColor = gl_Color;
}</pre><br><br>.vert<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">void main() {
	vec3 normal, lightDir;
	vec4 diffuse;
	float NdotL;

	/* first transform the normal into eye space and normalize the result */
	normal = normalize(gl_NormalMatrix * gl_Normal);

	/* now normalize the light's direction. Note that according to the
	OpenGL specification, the light is stored in eye space. Also since 
	we're talking about a directional light, the position field is actually 
	direction */
	lightDir = normalize(vec3(gl_LightSource[0].position));

	/* compute the cos of the angle between the normal and lights direction. 
	The light is directional so the direction is constant for every vertex.
	Since these two are normalized the cosine is the dot product. We also 
	need to clamp the result to the [0,1] range. */
	NdotL = max(dot(normal, lightDir), 0.0);

	/* Compute the diffuse term */
	diffuse = gl_FrontMaterial.diffuse * gl_LightSource[0].diffuse;

	gl_FrontColor =  NdotL * diffuse;

	gl_Position = ftransform();
}</textarea><br>have fun^^ <br><br></td></tr></table><br>
<a name="1005089"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I havn't even checked it, yet, and you're already my hero! :}}}<br>I've also updated the little simple code, by the way... realized I could do the textured polygon with even less afford.<br><br>I'll post what I've learned, ASAP! <br><br>THANK YOU ONCE AGAIN a dozen times! Just awesome! <br><br></td></tr></table><br>
<a name="1005091"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> glCreateProgramObjectARB() and ShaderObject create access violations. Did I forget to import something besides pub.glew and brl.GLMax2D?<br><br>Thank god, glew isn't too heavy size wise, by the way. Almost had me worried.<br><br>Here's what I have...(I also have the files dot3.vert and dot3.frag made, so they're loading fine)<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
Framework brl.GLMax2D
Import pub.glew

SetGraphicsDriver GLMax2DDriver()
Graphics 800,600,0

Local image_size:Int = 128
Global image_pix:TPixmap = CreatePixmap(image_size,image_size,PF_RGBA8888)'LockImage(image,0)
For Local x:Float = 0 Until image_size
	For Local y:Float = 0 Until image_size
	Local hsize:Float = image_size*0.5
	Local xp:Float = (x-hsize)*(3.6+2*Sin(y*6))
	Local yp:Float = y-hsize
	Local dot:Float = 1.0-Sqr(xp*xp+yp*yp)/hsize
	If dot &lt; 0 Then dot = 0
	Local p:Int = dot^0.8*255
	WritePixel(image_pix,x,y,((p/2+127) Shl 24)|(p Shl 16)|(p Shl 8)|p)
	Next
Next

'======================================================================

Global CurrentProgramObject:Int = 0
Global TShader_List:TList = Null
Type TShader
	Field Name:String
	Field VSObj:Int
	Field FSObj:Int
	Field ProgObj:Int
	
	Field Link:TLink
	
	Function Load:TShader(vertexshaderfile:String, fragmentshaderfile:String, instancename:String = Null)
		If Not TShader_List Then TShader_List = CreateList()
		Local Tmp:TShader = New TShader
		Tmp.Link = ListAddFirst(TShader_List, Tmp)
		
		If instancename = Null Then
			Tmp.Name = vertexshaderfile
		Else
			Tmp.Name = instancename
		End If
		
		Local file:String = vertexshaderfile
		Local file2:String = fragmentshaderfile
		
		Tmp.ProgObj = glCreateProgramObjectARB()
		Tmp.VSObj = glCreateShaderObjectARB(GL_VERTEX_SHADER_ARB)
		Tmp.FSObj = glCreateShaderObjectARB(GL_FRAGMENT_SHADER_ARB)
		
		Local fileStream:TStream = OpenFile(file)
		Local fileStream2:TStream = OpenFile(file2)
		
		Local shaderSrcVertex:Byte[FileSize(file) + 1]
		Local i:Int = 0
		While Not Eof(fileStream)
		   shaderSrcVertex[i] = ReadByte(fileStream)
		   i :+ 1
		   If i &gt; FileSize(file) Then i = FileSize(file)
		Wend
		CloseFile(fileStream)
		
		Local pointer:Byte Ptr = Varptr shadersrcVertex[0]
		Local laenge:Int = FileSize(file)
		
		glShaderSourceARB(Tmp.VSObj, 1, Varptr pointer, Varptr laenge)
		glCompileShaderARB(Tmp.VSObj)
		
		debugShader(Tmp.VSObj)
		   
		Local shaderSrcFragment:Byte[FileSize(file2) + 1]
		i = 0
		While Not Eof(fileStream2)
		   shaderSrcFragment[i] = ReadByte(fileStream2)
		   i :+ 1
		   If i &gt; FileSize(file2) Then i = FileSize(file2)
		Wend
		CloseFile(fileStream2)
		
		pointer = Varptr shadersrcFragment[0]
		laenge = FileSize(file2)
		
		glShaderSourceARB(Tmp.FSObj, 1, Varptr pointer, Varptr laenge)
		glCompileShaderARB(Tmp.FSObj)
		
		debugShader(Tmp.FSObj)
		
		glAttachObjectARB(Tmp.ProgObj, Tmp.VSObj)
		glAttachObjectARB(Tmp.ProgObj, Tmp.FSObj)
		
		glDeleteObjectARB(Tmp.VSObj)
		glDeleteObjectARB(Tmp.FSObj)
		
		glLinkProgramARB(Tmp.ProgObj)

		Return Tmp
	End Function
	
	Function debugShader(shaderObject:Int)
		Local laengedeslogs:Float
		glGetObjectParameterfvARB(shaderObject,GL_OBJECT_INFO_LOG_LENGTH_ARB, Varptr laengedeslogs)
		
		Local bytearray:Byte[] = New Byte[laengedeslogs]
		Local laenge23:Int = 0
		glGetInfoLogARB(shaderObject, laengedeslogs, Varptr(laenge23), Varptr(bytearray[0]));
		Local msg:String = ""
		For Local j:Int = 0 To laengedeslogs - 1
		   msg = msg + Chr(bytearray[j])
		Next
		DebugLog  "ShaderLog: "+msg
	End Function
	
	Method Use()
		glUseProgramObjectARB(Self.ProgObj)
	End Method
	
	Function TurnOff()
		glUseProgramObjectARB(Null)
	End Function
	
	Function DestroyAll()
		If TShader_List &lt;&gt; Null Then
			For Local Tmp:TShader = EachIn TShader_List
				Tmp.Destroy()
				Tmp = Null
			Next
			TShader_List.Clear()
			TShader_List = Null
		End If
	End Function
	
	Method Destroy()
		glDetachObjectARB(Self.ProgObj, Self.VSObj)
		glDeleteObjectARB(Self.VSObj)
		Self.VSObj = Null
		glDetachObjectARB(Self.ProgObj, Self.FSObj)
		glDeleteObjectARB(Self.FSObj)
		Self.FSObj = Null
		glDeleteObjectARB(Self.ProgObj)
		Self.ProgObj = Null
		Self.Name = Null
		RemoveLink(Self.Link)
	End Method
End Type

'======================================================================
SetClsColor 30,45,65
Local poly:Float[8] 
Local i:Int = 0
Local size:Float = 64
Local pos:Float[2]
Local rot:Float[2]
Local rox:Float[2]
Local lastTex:Int = GLTexFromPixmap(image_pix)
Global Dot3:TShader = TShader.Load("dot3.vert", "dot3.frag")
'Dot3.Use()
Repeat
	If glIsEnabled( GL_TEXTURE_2D )=0 Then
		glEnable( GL_TEXTURE_2D )
	EndIf
	
	glBindTexture( GL_TEXTURE_2D, lastTex)
	
	For Local o:Int = 0 To 200
		rot[0] = Sin(i+o*10)
		rot[1] = Cos(i+o*10)
		rox[0] = rot[1]
		rox[1] = -rot[0]
		pos[0] = 400+o*2*rot[1]
		pos[1] = 300-o*2*rot[0]
		
		poly[0] = -size*rot[0]+size*rox[0]
		poly[1] = -size*rot[1]+size*rox[1]
		poly[2] = size*rot[0]+size*rox[0]
		poly[3] = size*rot[1]+size*rox[1]
		poly[4] = size*rot[0]-size*rox[0]
		poly[5] = size*rot[1]-size*rox[1]
		poly[6] = -size*rot[0]-size*rox[0]
		poly[7] = -size*rot[1]-size*rox[1]
		
		poly[0]:+pos[0]
		poly[1]:+pos[1]
		poly[2]:+pos[0]
		poly[3]:+pos[1]
		poly[4]:+pos[0]
		poly[5]:+pos[1]
		poly[6]:+pos[0]
		poly[7]:+pos[1]
		glBegin GL_QUADS
			glTexCoord2f 0,0
			glVertex2f poly[0],poly[1]
			glTexCoord2f 0,1
			glVertex2f poly[2],poly[3]
			glTexCoord2f 1,1
			glVertex2f poly[4],poly[5]
			glTexCoord2f 1,0
			glVertex2f poly[6],poly[7]
		glEnd
	Next
	i:+1
	Flip 1
	glDisable( GL_TEXTURE_2D )
	Cls
Until KeyHit(KEY_ESCAPE)
End

</textarea> <br><br></td></tr></table><br>
<a name="1005105"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >FreakForFreedom</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'll guess you forgot to initialize glut^^<br><pre class=code>glewInit()</pre> <br><br></td></tr></table><br>
<a name="1005112"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have a love/hate relationship to such situations! LOL<br><br><b>T H A N K   Y O U !!!</b> <br><br></td></tr></table><br>
<a name="1005150"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have to admit, it ain't so very easy without knowing enough. I'm currently trying to figure out how data gets exchanged with the shaders. I'm struggling to update the normals according to how I rotate the geometry, for example.<br><br>Other than that I've already managed to use a texture as normalmap and do some lighting (outside of must hope for control as of now, hehe). <br><br></td></tr></table><br>
<a name="1005173"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >FreakForFreedom</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah, I found myself messing with shaders too, not so long ago.<br>Unfortunatly, I got a bug with the direction the light is pointing at... and I just couldn't find a solution - so I gave up :/<br><br>Heres the code for my parallax mapping tryout:<br>(You will need a texturemap and a normalmap)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

Import pub.glew
Import pub.opengl

Global g_location_testTexture:Int
Global g_location_eyePos:Int
Global g_location_tangent:Int
Global g_location_normal:Int
Global g_location_binormal:Int
Global g_location_heightTexture:Int

Global g_textureID:Int = 0
Global g_heightMapID:Int = 0

Global g_bUseShaders:Int = True

Global g_fSpinX:Float = 0.0
Global g_fSpinY:Float = 0.0

Global g_quadVertices:Float[] = [ 0.0,0.0,  1.0,1.0,0.0, -1.0,-1.0,0.0 ,..
				1.0,0.0,  1.0,0.0,0.0,  1.0,-1.0,0.0 ,..
				1.0,1.0,  0.0,1.0,0.0,  1.0, 1.0,0.0 ,..
				0.0,1.0,  0.0,0.0,1.0, -1.0, 1.0,0.0  ]
				
Global g_lightpos:Float[] = [0.0, 0.0, -3.5, 0.0]

Global g_sphere:Byte Ptr

Global ScreenWidth:Int = 640
Global ScreenHeight:Int = 480
Global Screendepth:Int = 0
Global HertzPerSec:Int = 60

SetGraphicsDriver(GLGraphicsDriver())
GLGraphics(ScreenWidth, ScreenHeight, Screendepth, HertzPerSec, GRAPHICS_BACKBUFFER | GRAPHICS_DEPTHBUFFER | GRAPHICS_ACCUMBUFFER)
glewInit()
GCSetMode 1
MoveMouse((ScreenWidth / 2), (ScreenHeight / 2))
HideMouse()


glEnable(GL_TEXTURE_2D)
glEnable(GL_DEPTH_TEST)

glClearColor(0.0, 0.0, 0.0, 1.0)

glMatrixMode(GL_PROJECTION)
glLoadIdentity()
gluPerspective(45.0, 640.0 / 480.0, 0.1, 100.0)

glEnable(GL_LIGHTING)
glEnable(GL_LIGHT0)

Global ambient_light:Float[] = [0.1, 0.1, 0.1, 1.0]
Global diffuse_light:Float[] = [0.8, 0.8, 0.8, 1.0]
Global specular_light:Float[] = [0.4, 0.4, 0.4, 0.4]
Global position_light:Float[] = [0.0, 0.0, 3.0, 0.0]
Global g_Direction:Float = 0.06
Global MoveLight:Int = True

glLightfv(GL_LIGHT0, gl_ambient, Float Ptr ambient_light)
glLightfv(GL_LIGHT0, gl_diffuse, Float Ptr diffuse_light)
glLightfv(GL_LIGHT0, gl_specular, Float Ptr specular_light)

glLightf(GL_LIGHT0, GL_QUADRATIC_ATTENUATION, 2.50)
glLightf(GL_LIGHT0, GL_LINEAR_ATTENUATION, 0.25)
glLightf(GL_LIGHT0, GL_CONSTANT_ATTENUATION, 0.1)

glLightfv(GL_LIGHT0, GL_POSITION, g_lightpos)

g_sphere = gluNewQuadric()		
gluQuadricNormals(g_sphere, GL_SMOOTH)
gluQuadricTexture(g_sphere, GL_TRUE)

Global pTextureImage:TPixmap = LoadPixmap("wall1.bmp")

If pTextureImage Then
	glGenTextures(1, Varptr g_textureID)

	glBindTexture(GL_TEXTURE_2D, g_textureID)

	glTexParameteri(GL_TEXTURE_2D,GL_TEXTURE_MIN_FILTER, GL_LINEAR)
	glTexParameteri(GL_TEXTURE_2D,GL_TEXTURE_MAG_FILTER, GL_LINEAR)

	glTexImage2D(GL_TEXTURE_2D, 0, 3, pTextureImage.Width, pTextureImage.Height, 0, GL_RGB, GL_UNSIGNED_BYTE, pTextureImage.pixels)
End If
pTextureImage = Null

pTextureImage = LoadPixmap("BUMPwall1.bmp")

If pTextureImage Then
	glGenTextures(1, Varptr g_heightMapID)

	glBindTexture(GL_TEXTURE_2D, g_heightMapID)

	glTexParameteri(GL_TEXTURE_2D,GL_TEXTURE_MIN_FILTER, GL_LINEAR)
	glTexParameteri(GL_TEXTURE_2D,GL_TEXTURE_MAG_FILTER, GL_LINEAR)

	glTexImage2D(GL_TEXTURE_2D, 0, 3, pTextureImage.Width, pTextureImage.Height, 0, GL_RGB, GL_UNSIGNED_BYTE, pTextureImage.pixels)
EndIf

pTextureImage = Null

Global ParaProg:Int = loadShader("ParallaxMapping.vert", "ParallaxMapping.frag")
glUseProgramObjectARB(ParaProg)

g_location_testTexture = glGetUniformLocationARB(ParaProg, "testTexture")
g_location_eyePos = glGetUniformLocationARB(ParaProg, "Eye")
g_location_tangent = glGetUniformLocationARB(ParaProg, "Tangent")
g_location_normal = glGetUniformLocationARB(ParaProg, "Normal")
g_location_binormal = glGetUniformLocationARB(ParaProg, "BiNormal")
g_location_heightTexture = glGetUniformLocationARB(ParaProg, "HeightTexture")

Global spin:Float = 1.0

While Not KeyDown(KEY_ESCAPE)
	
	glClear(GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT)
	
	glMatrixMode(GL_MODELVIEW)
	glDisable(GL_LIGHTING)
	glLoadIdentity()
	
	spin = spin + 0.1
	
	glPushMatrix()
		glLightfv(GL_LIGHT0, GL_POSITION, g_lightpos)
		glTranslatef(g_lightpos[0], g_lightpos[1], g_lightpos[2])
		glRotatef(spin, 0.0, 1.0, 0.0)
		gluSphere(g_sphere, 0.05, 16, 16)
	glPopMatrix()
	glEnable(GL_LIGHTING)
	
	glLoadIdentity()
	glTranslatef(0.0, 0.0, -4.0)
	glRotatef(-g_fSpinY, 1.0, 0.0, 0.0)
	glRotatef(-g_fSpinX, 0.0, 1.0, 0.0)
	
	If g_bUseShaders = True Then
		glUseProgramObjectARB(ParaProg)
		
		glActiveTexture(GL_TEXTURE0)
		glBindTexture(GL_TEXTURE_2D, g_textureID)
		glActiveTexture(GL_TEXTURE1)
		glBindTexture(GL_TEXTURE_2D, g_heightMapID)
		glActiveTexture(GL_TEXTURE0)
		
		If g_location_testTexture &lt;&gt; -1 Then glUniform1iARB(g_location_testTexture, 0)
		
		If g_location_heightTexture &lt;&gt; -1 Then glUniform1iARB(g_location_heightTexture, 1)
		
		If g_location_eyePos &lt;&gt; -1 Then glUniform4fARB(g_location_eyePos, 0.0, 0.0, 0.0, 1.0)
		
		If g_location_tangent &lt;&gt; -1 Then glUniform4fARB(g_location_tangent, 1.0, 0.0, 0.0, 0.0)
		
		If g_location_normal &lt;&gt; -1 Then glUniform4fARB(g_location_normal, 0.0, 0.0, 1.0, 0.0)
		
		If g_location_binormal &lt;&gt; -1 Then glUniform4fARB(g_location_binormal, 0.0, 1.0, 0.0, 0.0)
		
		glInterleavedArrays(GL_T2F_C3F_V3F, 0, g_quadVertices)
		glDrawArrays(GL_QUADS, 0, 4)
		
		glUseProgramObjectARB(Null)
	Else
		glBindTexture(GL_TEXTURE_2D, g_textureID)
		glInterleavedArrays(GL_T2F_C3F_V3F, 0, g_quadVertices)
		glDrawArrays(GL_QUADS, 0, 4)
	End If

	Flip
	
	If KeyHit(KEY_F1) Then g_bUseShaders = Not g_bUseShaders
	If KeyDown(KEY_UP) Then g_lightpos[1] :+ 0.01
	If KeyDown(KEY_DOWN) Then g_lightpos[1] :- 0.01
	If KeyDown(KEY_LEFT) Then g_lightpos[0] :- 0.01
	If KeyDown(KEY_RIGHT) Then g_lightpos[0] :+ 0.01
	
	If KeyDown(KEY_A) Then g_fSpinY :+ 0.5
	If KeyDown(KEY_Y) Then g_fSpinY :- 0.5
Wend

glDeleteTextures(1, Varptr g_textureID)
glDeleteTextures(1, Varptr g_heightMapID)

EndGraphics()
End

Function loadShader:Int(file:String, file2:String)
   Local ProgramObject:Int
   Local VertexShaderObject:Int
   Local FragmentShaderObject:Int

   ProgramObject=glCreateProgramObjectARB()
   VertexShaderObject   = glCreateShaderObjectARB(GL_VERTEX_SHADER_ARB);
   FragmentShaderObject = glCreateShaderObjectARB(GL_FRAGMENT_SHADER_ARB);
   
   Local fileStream:TStream=OpenFile(file)
   Local fileStream2:TStream=OpenFile(file2)
   
   Local shaderSrcVertex:Byte[FileSize(file)+1]
   Local i:Int=0
   While Not Eof(fileStream)
      shaderSrcVertex[i] = ReadByte(fileStream)
      i:+1
      If i&gt;FileSize(file) Then i = FileSize(file)
   Wend
   CloseFile(fileStream)
   
   Local pointer:Byte Ptr = Varptr shadersrcVertex[0]
   Local laenge:Int=FileSize(file)
   
   glShaderSourceARB(VertexShaderObject,1,Varptr pointer,Varptr laenge)
   glCompileShaderARB(VertexShaderObject)
   
   debugShader(VertexShaderObject)
      
   Local shaderSrcFragment:Byte[FileSize(file2)+1]
   i=0
   While Not Eof(fileStream2)
      shaderSrcFragment[i] = ReadByte(fileStream2)
      i:+1
      If i&gt;FileSize(file2) Then i = FileSize(file2)
   Wend
   CloseFile(fileStream2)
   
   pointer = Varptr shadersrcFragment[0]
   laenge=FileSize(file2)
   
   glShaderSourceARB(FragmentShaderObject,1,Varptr pointer,Varptr laenge)
   glCompileShaderARB(FragmentShaderObject)
   
   debugShader(FragmentShaderObject)
   
   glAttachObjectARB(ProgramObject, VertexShaderObject)
   glAttachObjectARB(ProgramObject, FragmentShaderObject)

   glDeleteObjectARB(VertexShaderObject)
   glDeleteObjectARB(FragmentShaderObject)

   glLinkProgramARB(ProgramObject);
   
   Return ProgramObject
End Function

Function debugShader(shaderObject:Int)
   Local laengedeslogs:Float
   glGetObjectParameterfvARB(shaderObject,GL_OBJECT_INFO_LOG_LENGTH_ARB,Varptr laengedeslogs)


   Local bytearray:Byte[]=New Byte[laengedeslogs]
   Local laenge23:Int=0
   glGetInfoLogARB(shaderObject, laengedeslogs, Varptr(laenge23), Varptr(bytearray[0]));
   Local msg:String=""
   For Local j:Int=0 To laengedeslogs-1
      msg=msg+ Chr(bytearray[j])
   Next
   DebugLog "ShaderLog: "+msg
End Function
</textarea><br><br>.vert<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
varying vec2 v_texCoord;

uniform vec4 Tangent;
uniform vec4 Normal;
uniform vec4 BiNormal;

uniform vec4 Eye;
varying vec3 EyeDir;

varying vec4 pos;

void main( void )
{
    gl_Position = gl_ModelViewProjectionMatrix * gl_Vertex;
    pos = gl_ModelViewMatrix * gl_Vertex;		//wieso nicht gl_ModelViewProjectionMatrix?

    v_texCoord = gl_MultiTexCoord0.xy;

    // Tangent vectors are transformed into view-space.
    // This allows the eye position to
    // be in the same space as the vertex.
    // Please note that this is not the best way of
    // making sure that everything is in the same space.
    // The best way is to inversly transform the eye by the
    // model matrix in the application and remove the next 4 
    // lines of shader (remembering to replace the transformed 
    // vectors with the original ones).

    vec3 m_Tangent = vec3(gl_ModelViewMatrix * Tangent);
    vec3 m_Normal = vec3(gl_ModelViewMatrix * Normal);
    vec3 m_BiNormal = vec3(gl_ModelViewMatrix * BiNormal);

    // Also, transform the vertex into view-space.
    vec3 m_Vector = vec3(gl_ModelViewMatrix * gl_Vertex);

    // Calculate the direction of the Eye
    // Position from the vertex;
    vec3  dirEye   = Eye.xyz  - m_Vector;
        
    // Put the Eye Direction into tangent space
    EyeDir.x = dot(m_Tangent.xyz , dirEye);
    EyeDir.y = dot(m_BiNormal.xyz , dirEye);
    EyeDir.z = dot(m_Normal.xyz , dirEye);

}</textarea><br><br>.frag<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
uniform sampler2D testTexture;
uniform sampler2D HeightTexture;
varying vec2 v_texCoord;

varying vec3 EyeDir;

varying vec4 pos;

float distance;  // Distance between light position and fragment
float NdotL;     // Dot product of normal and lightDir
float attenuation;

vec4 finalcolor;
vec4 diffuse = vec4(8.0,8.0,8.0,1.0);

vec3 normal;
vec3 lightDir;

void main( void )
{
    
    // This is the pride of the sample.
    // Essentially what this does is shift the
    // tex coords of the fragment closer towards the 'eye'
    // depending on the fragment's 'height'.
    // Note how the offset is scaled to 0.03 and has a bias
    // of -0.015.
    // This is simple and effective, but also remember
    // that on steep 'gradients' there will be artifacts.
    // This is, after all, only an approximation.

    vec3 EyeT = normalize(EyeDir);
    vec4 Offset = texture2D(HeightTexture,v_texCoord);
    Offset = Offset * 0.04 - 0.02;
    vec2 TexCoords = Offset.xy * EyeT.xy + v_texCoord;


	//Extract normal from the NormalMap
	//R=x, G=y, B=z
	vec3 NormalTex = texture2D(HeightTexture, gl_TexCoord[0].st).xyz;
   
	//Bring the xyz normal in the -1.0 to 1.0 margin
	NormalTex = (NormalTex - 0.5) * 2.0;
		
	//Finally assign the NormalTex to the actual normal
	//to be used in our lighting
	normal=normalize(NormalTex);

	// Compute Lighting
	lightDir = normalize(vec3(gl_LightSource[0].position-pos));
	distance = length(vec3(gl_LightSource[0].position-pos));
	NdotL = max(dot(normal,lightDir),0.0);
	attenuation = 1.00 / (gl_LightSource[0].constantAttenuation +
					gl_LightSource[0].linearAttenuation * distance +
					gl_LightSource[0].quadraticAttenuation * distance * distance);





    // This also works very well with Bump Mapping.
    // just remember to use the shifted tex coords when
    // getting the normal.

    finalcolor = texture2D( testTexture, TexCoords ); 
	finalcolor *= attenuation * (diffuse * NdotL);
	
	gl_FragColor = finalcolor;

}</textarea><br><br>If you find a solution - please let me know :P <br><br></td></tr></table><br>
<a name="1005208"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'll definitely have ago at it tomorrow!<br>I got it to run real quick and use a generated set of images which showed the parallax effect on the quad when rotated. Curious stuff for sure! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
