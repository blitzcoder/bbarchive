<!DOCTYPE html><html lang="en" ><head ><title >Can't find uniforms</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Can't find uniforms</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=103" >OpenGL Module</a>/<a href="#bottom" >Can't find uniforms</a><br><br>
<a name="1092370"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sanctus</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey guys,<br><br>I've been trying to add shader support into my game written with normal Max2D.<br><br>I've written this extension for max2d:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

Import brl.GLGraphics
Import pub.glew

Private
Global blendShader:TShader
Global desaturatedShader:TShader
Global maskShader:TShader

Public

Type TShader
	Global currentShader:TShader
	Field vertCode:String
	Field fragCode:String
	Field vertProgram:Int
	Field fragProgram:Int
	Field program:Int
	
	Function Load:TShader(vertUrl:Object, fragUrl:Object)
		Local shader:TShader = New TShader
		Local vertStream:TStream = OpenStream(vertUrl)
		Local fragStream:TStream = OpenStream(fragUrl)
		If Not vertStream Then RuntimeError("Could not open file " + String(vertUrl))
		If Not fragStream Then RuntimeError("Could not open file " + String(fragUrl))
		shader.vertCode = vertStream.ReadString(vertStream.size())
		shader.fragCode = fragStream.ReadString(fragStream.size())
		shader.vertProgram = glCreateShader(GL_VERTEX_SHADER)
		shader.fragProgram = glCreateShader(GL_FRAGMENT_SHADER)
		shader.program = glCreateProgram()
		Local VS_CStr:Byte Ptr = shader.vertCode.ToCString()
		Local FS_CStr:Byte Ptr = shader.fragCode.ToCString()
		glShaderSource(shader.vertProgram, 1, Varptr(VS_CStr), Null)
		glShaderSource(shader.fragProgram, 1, Varptr(FS_CStr), Null)
		MemFree(VS_CStr)
		MemFree(FS_CStr)
		glCompileShader(shader.vertProgram)
		glCompileShader(shader.fragProgram)
		glAttachShader(shader.program, shader.vertProgram)
		glAttachShader(shader.program, shader.fragProgram)
		glLinkProgram(shader.program)
		Local linked:Int
		glGetObjectParameterivARB(shader.program, GL_OBJECT_LINK_STATUS_ARB, Varptr(linked))
		Print "Link Status " + linked
		Return shader
	End Function
	
	Method Bind()
		glUseProgram(program)
	End Method
End Type

Function BindShader(shader:TShader)
	If shader
		shader.Bind()
		TShader.currentShader = shader
	Else
		glUseProgram(0)
		TShader.currentShader = Null
	End If
End Function

Function BindImage:TGLImageFrame(image:TImage, frame:Int, slot:Int, name:String)
	Local frameHandle:TGLImageFrame = TGLImageFrame(image.Frame(frame))
	Local handle:Int = framehandle.name
	glActiveTexture(GL_TEXTURE0 + slot)
	glEnable(GL_TEXTURE_2D)
	glBindTexture(GL_TEXTURE_2D, handle)
	Local cstr:Byte Ptr = name.ToCString()
	Local location:Int = glGetUniformLocation(TShader.currentShader.program, cstr)
	MemFree(cstr)
	glUniform1i(location, slot)
	Print name + ":" + location + ":" + slot + ":" + handle
	Return frameHandle
End Function

Function DisableAdditionalTextures()
	For Local i:Int = 1 To 7
		glActiveTexture(GL_TEXTURE0 + i)
		glDisable(GL_TEXTURE_2D)
	Next
End Function

Function InitMax2DExt()
	glewInit()
	blendShader = TShader.Load("shaders/blend.vert", "shaders/blend.frag")
	desaturatedShader = TShader.Load("shaders/desaturated.vert", "shaders/desaturated.frag")
	maskShader = TShader.Load("shaders/mask.vert", "shaders/mask.frag")
End Function

Function Max2DExtBegin()
	
End Function

Function Max2DExtEnd()
	BindShader(Null)
End Function

Function DrawImagesBlend(image1:TImage, image2:TImage, image3:TImage, frame1:Int, frame2:Int, frame3:Int, x:Float, y:Float)
	BindShader(blendShader)
	BindImage(image1, frame1, 0, "tex0")
	BindImage(image2, frame2, 1, "tex1")
	BindImage(image3, frame3, 2, "tex2")
	Local gc:TMax2DGraphics = GetMax2DGraphics()
	Local x0:Float = -image1.handle_x, x1:Float = x0 + image1.width
	Local y0:Float = -image1.handle_y, y1:Float = y0 + image1.height
	Local iframe:TImageFrame = image1.Frame(frame1)
	glUniform1i(0, 1)
	glUniform1i(1, 2)
	If iframe iframe.Draw x0, y0, x1, y1, x + gc.origin_x, y + gc.origin_y, 0, 0, image1.width, image1.height
	glActiveTexture(GL_TEXTURE0)
	BindShader(Null)
End Function

Function DrawDesaturatedImage(image:TImage, frame:Int, x:Float, y:Float)
	BindShader(desaturatedShader)
	BindImage(image, frame, 0, "tex")
	Local gc:TMax2DGraphics = GetMax2DGraphics()
	Local x0:Float = -image.handle_x, x1:Float = x0 + image.width
	Local y0:Float = -image.handle_y, y1:Float = y0 + image.height
	Local iframe:TImageFrame = image.Frame(frame)
	If iframe iframe.Draw x0, y0, x1, y1, x + gc.origin_x, y + gc.origin_y, 0, 0, image.width, image.height
End Function

Function DrawMaskedImage(image:TImage, mask:TImage, frame:Int, maskFrame:Int, x:Float, y:Float)
	BindShader(maskShader)
	BindImage(image, frame, 0, "tex0")
	BindImage(mask, maskFrame, 1, "tex1")
	Local gc:TMax2DGraphics = GetMax2DGraphics()
	Local x0:Float = -image.handle_x, x1:Float = x0 + image.width
	Local y0:Float = -image.handle_y, y1:Float = y0 + image.height
	Local iframe:TImageFrame = image.Frame(frame)
	glActiveTexture(GL_TEXTURE0)
	If iframe iframe.Draw(x0, y0, x1, y1, x + gc.origin_x, y + gc.origin_y, 0, 0, image.width, image.height)
	DisableAdditionalTextures()
	glActiveTexture(GL_TEXTURE0)
	glEnable(GL_TEXTURE_2D)
	BindShader(Null)
End Function</textarea><br><br>I know it's very ugly but right now I only care about functionality.<br><br>So I've tested this in a separate app and it works great. I tested it in the game (only the desaturated shader) and it worked great. In the editor however it won't work at all.<br>Everything is set up, the max2d driver, glew, the shader is linked well but I keep getting -1 on the uniform locations and I just don't know what to do. The shader I use there is different but it doesn't matter since it won't even work with the dessaturated shader I tested in 2 other places.<br><br>Just so there is no confusion I'm gonna post the code for that shader:<br><br>Vert:<br>void main()<br>{<br>	gl_TexCoord[0] = gl_MultiTexCoord0;<br>	gl_Position = ftransform();<br>}<br><br>Frag:<br>uniform sampler2D tex;<br>void main()<br>{<br>	float4 t = texture2D(tex,gl_TexCoord[0].st);<br>	float d = (t.r+t.g+t.b)/3.0;<br>	gl_FragColor = float4(d,d,d,t.a);<br>}<br><br>Does anyone have any idea as to why this doesn't work?<br><br>I've been working with opengl for at least 5 years now and never once did I encounter such a weird thing.<br>Thanks a lot <br><br></td></tr></table><br>
<a name="1092802"></a>

<a name="1092803"></a>

<a name="1092804"></a>

<a name="1092805"></a>

<a name="1092806"></a>

<a name="1092807"></a>

<a name="1092808"></a>

<a name="1092809"></a>

<a name="1092810"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Never mind, didnt read the post properly !!! Sorry.<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1093093"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sanctus</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Found it.<br>It didn't take me this long but I was quite busy with RL things.<br>If you create a shader having set the graphics on one canvas and then try to draw using that shader on another canvas you won't find the location of the uniforms.<br>I will check on a sollution for this.<br><br>On another note, we need a new Max2D that works the same but with shader support(both ogl and dx). It's 2011 already...<br>I swear that when I'll have some free time I'll start working on one and release it for free and everything. <br><br></td></tr></table><br>
<a name="1093138"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >kfprimm</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Give it a shot with GLShareContexts().<br><br>That should fix it. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
