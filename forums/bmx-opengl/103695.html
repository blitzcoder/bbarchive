<!DOCTYPE html><html lang="en" ><head ><title >OpenGL with Blitzmax commands</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >OpenGL with Blitzmax commands</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=103" >OpenGL Module</a>/<a href="#bottom" >OpenGL with Blitzmax commands</a><br><br>
<a name="1250763"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >cgrossi</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello, guys.<br><br>Does anyone know how to join blitzmax commands with opengl commands??? I want to study opengl and couldn't find a way to use both set of commands together...<br><br>Thanks!! <br><br></td></tr></table><br>
<a name="1250814"></a>

<a name="1250815"></a>

<a name="1250816"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Do you mean like with the <a href="/posts.php?topic=41689" target="_blank">Nehe tutorials</a>? The source is quite out of date now but it's fairly simple to get the demos running. The changes are mentioned in versions.txt.<br><br>Here are those changes and some more:<br><pre class=code>Framework brl.blitzgl -&gt; Framework brl.GLGraphics
BGL_BACKBUFFER -&gt; GRAPHICS_BACKBUFFER
BGL_DEPTHBUFFER -&gt; GRAPHICS_DEPTHBUFFER
BGL_FULLSCREEN -&gt; (removed, use depth 0 or 32)
FlushMem -&gt; (removed)
bglCreateContext -&gt; GLGraphics
bglTexFromPixmap -&gt; GLTexFromPixmap
bglAdjustTexSize -&gt; GLAdjustTexSize
bglDrawText -&gt; GLDrawText
bglSwapBuffers -&gt; Flip
bglSetMouseVisible 0 -&gt; HideMouse
bglSetSwapInterval 0 -&gt; (removed, use Flip 0)
</pre> <br><br></td></tr></table><br>
<a name="1251077"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >cgrossi</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> No, I mean use blitzmax code mixed with opengl code...<br><br>Graphics 640,480<br><br>HideMouse <br><br>While Not KeyHit(KEY_ESCAPE)<br>	Cls<br>	x=MouseX()<br>	y=MouseY()<br>	DrawLine 320,240,x,y<br>	DrawLine x-2,y,x-10,y<br>	DrawLine x+2,y,x+10,y<br>	DrawLine x,y-2,x,y-10<br>	DrawLine x,y+2,x,y+10<br><br>        '************    OPENGL CODE<br>        GLDrawText "TEST",10,10<br><br>	Flip<br>Wend <br><br></td></tr></table><br>
<a name="1251234"></a>

<a name="1251235"></a>

<a name="1251236"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well you use SetGraphicsDriver GLMax2DDriver() to have GL commands and Max2d together or SetGraphicsDriver GLGraphicsDriver() if you want pure GL. <br><br></td></tr></table><br>
<a name="1251379"></a>

<a name="1251381"></a>

<a name="1251468"></a>

<a name="1251469"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Do you mean 2d, or 2d in 3d? <br><br>I have made a 2d in 3d demo which uses code from minib3d. It combines the Max2d demo with the first 8 Nehe tutorials. I've been wondering how to do this myself for a while but never got round to it so am quite pleased with it. Btw, I couldn't get blending working in demo 8 yet. <br><br>You need to save it to the Nehe_Tutorial folder and copy over spark.png from minib3d/examples/media.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' minib3d_nehe.bmx

Strict

Framework BRL.GLMax2D
Import PUB.Glew
Import brl.bmploader
Import brl.pngloader
Import brl.jpgloader
Import brl.system
Import brl.random

' Initialise Nehe
' ---------------
Global ScreenWidth:Int=800
Global ScreenHeight:Int=600
Global ScreenDepth:Int=0

Global xrot:Float=0
Global yrot:Float=0
Global zrot:Float=0
Global Filter:Int=0
Global z:Float=-5.0
Global xspeed:Float=0.0
Global yspeed:Float=0.0
Global lp:Byte=1
Global bp:Byte=1

Global Checkimage:Byte[256,256,4]
Global Checkimage7:Byte[256,256,3]
Global Checkimage8:Byte[128,128,3]
Global Texname:Int[3]
Global Texname6:Int

Global LightAmbient:Float[]=[0.5, 0.5, 0.5, 1.0]
Global LightDiffuse:Float[]=[1.0, 1.0, 1.0, 1.0]
Global LightPosition:Float[]=[0.0, 0.0, 2.0, 1.0]

Global Framecounter_time:Int,Framecounter_framerate:Int,Framecounter_counter:Int

Global rtri:Float=0.0
Global rquad:Float=0.0

TGlobal.Graphics3D 800,600,0,2
HideMouse

' Initialise Max2D
' ----------------
Global width%=800,height%=600,depth%=0,Mode%=2,rate:Int

' Create a spark type
Type spark
	Field x#,y#,z#,vy#,xd#,yd#,zd#,r#,g#,b#,alpha#
End Type

' Load spark image
Global sparki:TImage=LoadImage("spark.png")

' Set no. of sparks to be created per firework
Global no_sparks%=500

' Create spark list
Global spark_list:TList=New TList

' Load and set font
Global font:TImageFont=LoadImageFont("Arial.ttf",1)
SetImageFont font

Global test:Int = 7 ' Select Nehe lesson

Select test
	Case 6 ; LoadGlTextures6()
	Case 7 ; LoadGlTextures7()
	Case 8 ; LoadGlTextures8()
EndSelect

' Main loop
' ---------
While Not KeyHit(KEY_ESCAPE)

	InitGl()
	DisableStates()
	glDisable(GL_FOG)
	
	Select test
		Case 2 ; nehe2()
		Case 3 ; nehe3()
		Case 4 ; nehe4()
		Case 5 ; nehe5()
		Case 6 ; nehe6()
		Case 7 ; nehe7()
		Case 8 ; nehe8()
	EndSelect
	
	glDisable(GL_LIGHTING)
	glColor3f(1.0,1.0,1.0)
	
	' Framecounter
	' ------------
	Framecounter_counter=Framecounter_counter+1
	If Framecounter_time=0 Then Framecounter_time=MilliSecs()
	If Framecounter_time+1001 &lt;MilliSecs() Then
		Framecounter_framerate=Framecounter_counter
		Framecounter_counter=0
		Framecounter_time=MilliSecs()
	EndIf
	
	GLDrawText("FPS : "+Framecounter_framerate,ScreenWidth-(8*12),ScreenHeight-16-8)
	Select test
		Case 2 ; GLDrawText("Nehe lesson 2-Your First Polygon",10,24)
		Case 3 ; GLDrawText("Nehe lesson 3-Adding Color",10,24)
		Case 4 ; GLDrawText("Nehe lesson 4-Rotation",10,24)
		Case 5 ; GLDrawText("Nehe lesson 5-3D Shapes",10,24)
		Case 6 ; GLDrawText("Nehe lesson 6-Texture Mapping",10,24)
		Case 7
			GLDrawText("Nehe lesson 7-Texture Filters, Lighting &amp; Keyboard Control",10,24)
			GLDrawText("L=lighting, F=filter, PageUp/PageDn=z, Up/Down=xspeed, Left/Right=yspeed",10,44)
		Case 8
			GLDrawText("Nehe lesson 8-Blending",10,24)
			GLDrawText("L=lighting, F=filter, PageUp/PageDn=z, Up/Down=xspeed, Left/Right=yspeed",10,44)
	EndSelect
	
	glEnable(GL_FOG)
	glEnable(GL_LIGHTING)
	
	TBlitz2D.BeginMax2D()
	UpdateMax2D() ' your function
	TBlitz2D.EndMax2D()	
	
	TBlitz2D.Text MouseX(),MouseY(),"Minib3d Nehe"
	Flip
	Cls
	
Wend
End

' Extra functions
' ---------------
Function UpdateMax2D()

	' If space key pressed then create new set of sparks (new firework)

	If KeyHit(KEY_SPACE)

		Local x#=Rand(-100,100)
		Local y#=Rand(-100,100)
		Local z#=200

		Local r#=Rand(255)
		Local g#=Rand(255)
		Local b#=Rand(255)

		For Local i%=1 To no_sparks

			Local speed#=0.1

			Local ang1#=Rnd!(360)
			Local ang2#=Rnd!(360)

			Local sp:spark=New Spark
			spark_list.AddLast sp

			sp.x=x#
			sp.y=y#
			sp.z=z#

			sp.xd=Cos(ang1#)*Cos(ang2#)*speed#
			sp.yd=Cos(ang1#)*Sin(ang2#)*speed#
			sp.zd=Sin(ang1#)*speed#
	
			sp.r=r
			sp.g=g
			sp.b=b
			sp.alpha=1
	
		Next

	EndIf

	' Draw all sparks

	For Local sp:spark=EachIn spark_list

		' If spark alpha is above 0 then draw it...

		If sp.alpha&gt;0

			sp.x=sp.x+sp.xd*10.0
			sp.y=sp.y+sp.yd*10.0
			sp.z=sp.z+sp.zd*10.0
			sp.y=sp.y+sp.vy#
			sp.vy=sp.vy+0.02

			' Calculate x and y draw values based on x,y,z co-ordinates
			Local x#=(width/2.0)+((sp.x/sp.z)*500)
			Local y#=(height/2.0)+((sp.y/sp.z)*500)

			sp.alpha=sp.alpha-0.01

			SetColor sp.r#,sp.g#,sp.b#
			SetBlend LIGHTBLEND
			SetAlpha sp.alpha
			SetScale 20/sp.z,20/sp.z
			DrawImage sparki,x#,y#

		'...else remove spark from spark list

		Else

			spark_list.Remove sp

		EndIf

	Next

	SetBlend SOLIDBLEND
	SetScale 1,1
	SetColor 255,255,255
	DrawText "Press space to ignite firework",10,ScreenHeight-16-8
	
End Function

Function DisableStates()
	
	glDisableClientState(GL_VERTEX_ARRAY)
	glDisableClientState(GL_COLOR_ARRAY)
	glDisableClientState(GL_NORMAL_ARRAY)
	
End Function

' Nehe functions
' --------------
Function InitGl()	
	glClearColor(0.0, 0.0, 0.0, 0.0)
	glClearDepth 1.0
	glDepthFunc(GL_LESS)
	glEnable(GL_DEPTH_TEST)
		
	glShadeModel(GL_SMOOTH)
	glViewport(0,0,ScreenWidth,ScreenHeight)
	glMatrixMode(GL_PROJECTION)
	glLoadIdentity()
	gluPerspective(45.0,Float(ScreenWidth)/Float(ScreenHeight),1.0,100.0)
	glMatrixMode(GL_MODELVIEW)
	
	glLightfv GL_LIGHT1, GL_AMBIENT, Float Ptr LightAmbient
	glLightfv GL_LIGHT1, GL_DIFFUSE, Float Ptr LightDiffuse
	glLightfv GL_LIGHT1, GL_POSITION, Float Ptr LightPosition
	glEnable GL_LIGHT1
End Function

Function LoadGlTextures6()
	Local PointeurImg:Byte Ptr
	Local TexWidth
	Local TexHeight
	Local tex01:TPixmap=LoadPixmap("data/NeHe.bmp")
	TexWidth=tex01.Width
	TexHeight=tex01.Height
	PointeurImg=PixmapPixelPtr(tex01,0,0)
	Local pp=0,x,y
	For y=TexHeight-1 To 0 Step -1
		For x=0 To TexWidth-1
			Checkimage[y,x,0]=PointeurImg[pp]
			Checkimage[y,x,1]=PointeurImg[pp+1]
			Checkimage[y,x,2]=PointeurImg[pp+2]
			Checkimage[y,x,3]=100
			pp=pp+3
		Next
	Next
	tex01=Null
	glPixelStorei(GL_UNPACK_ALIGNMENT,1)
	glGenTextures(1, Varptr Texname6)
	glBindTexture(GL_TEXTURE_2D, Texname6)
	glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_WRAP_S, GL_REPEAT)
	glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_WRAP_T, GL_REPEAT)
	glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER, GL_NEAREST)
	glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_NEAREST)
	glTexImage2D(GL_TEXTURE_2D, 0, GL_RGBA, TexWidth, TexHeight, 0, GL_RGBA, GL_UNSIGNED_BYTE, Checkimage)
End Function

Function LoadGlTextures7()
	Local PointeurImg:Byte Ptr
	Local TexWidth
	Local TexHeight
	Local tex01:TPixmap=LoadPixmap("data/crate.bmp")
	TexWidth=tex01.Width
	TexHeight=tex01.Height
	PointeurImg=PixmapPixelPtr(tex01,0,0)
	Local pp=0,x,y
	For y=TexHeight-1 To 0 Step -1
		For x=0 To TexWidth-1
			Checkimage7[y,x,0]=PointeurImg[pp]
			Checkimage7[y,x,1]=PointeurImg[pp+1]
			Checkimage7[y,x,2]=PointeurImg[pp+2]
			pp=pp+3
		Next
	Next
	tex01=Null
	' Create Nearest Filtered Texture
	glGenTextures 3, Varptr Texname[0]
	glBindTexture GL_TEXTURE_2D,Texname[0]
	glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_MAG_FILTER,GL_NEAREST
	glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_MIN_FILTER,GL_NEAREST
	glTexImage2D GL_TEXTURE_2D, 0, 3, TexWidth, TexHeight, 0, GL_RGB, GL_UNSIGNED_BYTE, Checkimage7
	' Create Linear Filtered Texture
	glBindTexture GL_TEXTURE_2D, Texname[1]
	glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_MAG_FILTER,GL_LINEAR
	glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_MIN_FILTER,GL_LINEAR
	glTexImage2D GL_TEXTURE_2D, 0, 3, TexWidth, TexHeight, 0, GL_RGB, GL_UNSIGNED_BYTE, Checkimage7
	' Create MipMapped Texture
	glBindTexture GL_TEXTURE_2D, Texname[2]
	glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_MAG_FILTER,GL_LINEAR
	glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_MIN_FILTER,GL_LINEAR_MIPMAP_NEAREST
	gluBuild2DMipmaps GL_TEXTURE_2D, 3, TexWidth, TexHeight, GL_RGB, GL_UNSIGNED_BYTE, Checkimage7
End Function

Function LoadGlTextures8()
	Local PointeurImg:Byte Ptr
	Local TexWidth
	Local TexHeight
	Local tex01:TPixmap=LoadPixmap("data/glass.bmp")
	TexWidth=tex01.Width
	TexHeight=tex01.Height
	PointeurImg=PixmapPixelPtr(tex01,0,0)
	Local pp=0,x,y
	For y=TexHeight-1 To 0 Step -1
		For x=0 To TexWidth-1
			Checkimage8[y,x,0]=PointeurImg[pp]
			Checkimage8[y,x,1]=PointeurImg[pp+1]
			Checkimage8[y,x,2]=PointeurImg[pp+2]
			pp=pp+3
		Next
	Next
	tex01=Null
	' Create Nearest Filtered Texture
	glGenTextures 3, Varptr Texname[0]
	glBindTexture GL_TEXTURE_2D,Texname[0]
	glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_MAG_FILTER,GL_NEAREST
	glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_MIN_FILTER,GL_NEAREST
	glTexImage2D GL_TEXTURE_2D, 0, 3, TexWidth, TexHeight, 0, GL_RGB, GL_UNSIGNED_BYTE, Checkimage8
	' Create Linear Filtered Texture
	glBindTexture GL_TEXTURE_2D, Texname[1]
	glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_MAG_FILTER,GL_LINEAR
	glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_MIN_FILTER,GL_LINEAR
	glTexImage2D GL_TEXTURE_2D, 0, 3, TexWidth, TexHeight, 0, GL_RGB, GL_UNSIGNED_BYTE, Checkimage8
	' Create MipMapped Texture
	glBindTexture GL_TEXTURE_2D, Texname[2]
	glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_MAG_FILTER,GL_LINEAR
	glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_MIN_FILTER,GL_LINEAR_MIPMAP_NEAREST
	gluBuild2DMipmaps GL_TEXTURE_2D, 3, TexWidth, TexHeight, GL_RGB, GL_UNSIGNED_BYTE, Checkimage8
End Function

Function nehe2()
	glFrontFace(GL_CW)
	
	glClear GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT
	glLoadIdentity
	glTranslatef -1.5,0.0,-6.0
	glBegin %GL_POLYGON
		glVertex3f  0.0, 1.0, 0.0          ' Top
		glVertex3f  1.0,-1.0, 0.0          ' Bottom Right
		glVertex3f -1.0,-1.0, 0.0          ' Bottom Left
	glEnd
	glTranslatef 3.0,0.0,0.0
	glBegin %GL_QUADS
		glVertex3f -1.0, 1.0, 0.0          ' Top Left
		glVertex3f  1.0, 1.0, 0.0          ' Top Right
		glVertex3f  1.0,-1.0, 0.0          ' Bottom Right
		glVertex3f -1.0,-1.0, 0.0          ' Bottom Left
	glEnd	
End Function

Function nehe3()
	glFrontFace(GL_CW)
	glDisable(GL_LIGHTING)
	
	glClear GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT
	glLoadIdentity()
	glTranslatef(-1.5,0.0,-6.0)
	glBegin GL_POLYGON
		glColor3f 1.0,0.0,0.0              ' Set The Color To Red
		glVertex3f  0.0, 1.0, 0.0          ' Top
		glColor3f 0.0,1.0,0.0              ' Set The Color To Green
		glVertex3f  1.0,-1.0, 0.0          ' Bottom Right
		glColor3f 0.0,0.0,1.0              ' Set The Color To Blue
		glVertex3f -1.0,-1.0, 0.0          ' Bottom Left
	glEnd
	glTranslatef 3.0,0.0,0.0
	glColor3f 0.5,0.5,1.0                   ' // Set The Color To Blue One Time Only
	glBegin GL_QUADS
		glVertex3f -1.0, 1.0, 0.0          ' Top Left
		glVertex3f  1.0, 1.0, 0.0          ' Top Right
		glVertex3f  1.0,-1.0, 0.0          ' Bottom Right
		glVertex3f -1.0,-1.0, 0.0          ' Bottom Left
	glEnd
	
	glEnable(GL_LIGHTING)
End Function 

Function nehe4()
	glFrontFace(GL_CW)
	glDisable(GL_LIGHTING)
	
	glClear GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT
	glLoadIdentity
	glTranslatef -1.5,0.0,-6.0
	glRotatef rtri,0.0,1.0,0.0              ' Rotate The Triangle On The Y axis ( NEW )
   	glBegin %GL_POLYGON
		glColor3f 1.0,0.0,0.0              ' Set The Color To Red
		glVertex3f  0.0, 1.0, 0.0          ' Top
		glColor3f 0.0,1.0,0.0              ' Set The Color To Green
		glVertex3f  1.0,-1.0, 0.0          ' Bottom Right
		glColor3f 0.0,0.0,1.0              ' Set The Color To Blue
		glVertex3f -1.0,-1.0, 0.0          ' Bottom Left
	glEnd

	glLoadIdentity
	glTranslatef 1.5,0.0,-6.0               ' Move Right 1.5 Units
	glRotatef rquad,1.0,0.0,0.0             ' Rotate The Quad On The X axis ( New )
	glColor3f 0.5,0.5,1.0                   ' Set The Color To Blue One Time Only
	glBegin GL_QUADS
		glVertex3f -1.0, 1.0, 0.0          ' Top Left
		glVertex3f  1.0, 1.0, 0.0          ' Top Right
		glVertex3f  1.0,-1.0, 0.0          ' Bottom Right
		glVertex3f -1.0,-1.0, 0.0          ' Bottom Left
	glEnd
	
	rtri = rtri + 0.2                       '; Increase The Rotation Variable For The Triangle ( New )
	rquad = rquad + 0.15                    '; Decrease The Rotation Variable For The Quad ( New )
	
	glEnable(GL_LIGHTING)
End Function

Function nehe5()
	glDisable(GL_LIGHTING)
	
	glClear GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT
	glLoadIdentity
	glTranslatef -1.5,0.0,-6.0
	glRotatef rtri,0.0,1.0,0.0              '; Rotate The Triangle On The Y axis ( New )
	glBegin GL_POLYGON
		';Front
		glColor3f 1.0,0.0,0.0               '; Red
		glVertex3f  0.0, 1.0, 0.0           '; Top Of Triangle (Front)
		glColor3f 0.0,1.0,0.0               '; Green
		glVertex3f -1.0,-1.0, 1.0           '; Left Of Triangle (Front)
		glColor3f 0.0,0.0,1.0               '; Blue
		glVertex3f  1.0,-1.0, 1.0           '; Right Of Triangle (Front)
		';Right
		glColor3f 1.0,0.0,0.0               '; Red
		glVertex3f  0.0, 1.0, 0.0           '; Top Of Triangle (Right)
		glColor3f 0.0,0.0,1.0               '; Blue
		glVertex3f  1.0,-1.0, 1.0           '; Left Of Triangle (Right)
		glColor3f 0.0,1.0,0.0               '; Green
		glVertex3f  1.0,-1.0, -1.0          '; Right Of Triangle (Right)
		';Back
		glColor3f 1.0,0.0,0.0               '; Red
		glVertex3f  0.0, 1.0, 0.0           '; Top Of Triangle (Back)
		glColor3f 0.0,1.0,0.0               '; Green
		glVertex3f  1.0,-1.0, -1.0          '; Left Of Triangle (Back)
		glColor3f 0.0,0.0,1.0               '; Blue
		glVertex3f -1.0,-1.0, -1.0          '; Right Of Triangle (Back)
		';Left
		glColor3f 1.0,0.0,0.0               '; Red
		glVertex3f  0.0, 1.0, 0.0           '; Top Of Triangle (Left)
		glColor3f 0.0,0.0,1.0               '; Blue
		glVertex3f -1.0,-1.0,-1.0           '; Left Of Triangle (Left)
		glColor3f 0.0,1.0,0.0               '; Green
		glVertex3f -1.0,-1.0, 1.0           '; Right Of Triangle (Left)
	glEnd

	glLoadIdentity
	glTranslatef 1.5,0.0,-7.0               '; Move Right 1.5 Units And Into The Screen 1
	glRotatef rquad,1.0,1.0,1.0             '; Rotate The Quad On The X axis ( New )
	glColor3f 0.5,0.5,1.0                   '; Set The Color To Blue One Time Only
	glBegin GL_QUADS
		glColor3f 0.0,1.0,0.0               '; Set The Color To Blue
		glVertex3f  1.0, 1.0,-1.0           '; Top Right Of The Quad (Top)
		glVertex3f -1.0, 1.0,-1.0           '; Top Left Of The Quad (Top)
		glVertex3f -1.0, 1.0, 1.0           '; Bottom Left Of The Quad (Top)
		glVertex3f  1.0, 1.0, 1.0           '; Bottom Right Of The Quad (Top)

		glColor3f 1.0,0.5,0.0               '; Set The Color To Orange
		glVertex3f  1.0,-1.0, 1.0           '; Top Right Of The Quad (Bottom)
		glVertex3f -1.0,-1.0, 1.0           '; Top Left Of The Quad (Bottom)
		glVertex3f -1.0,-1.0,-1.0           '; Bottom Left Of The Quad (Bottom)
		glVertex3f  1.0,-1.0,-1.0           '; Bottom Right Of The Quad (Bottom)

		glColor3f 1.0,0.0,0.0               '; Set The Color To Red
		glVertex3f  1.0, 1.0, 1.0           '; Top Right Of The Quad (Front)
		glVertex3f -1.0, 1.0, 1.0           '; Top Left Of The Quad (Front)
		glVertex3f -1.0,-1.0, 1.0           '; Bottom Left Of The Quad (Front)
		glVertex3f  1.0,-1.0, 1.0           '; Bottom Right Of The Quad (Front)

		glColor3f 1.0,1.0,0.0               '; Set The Color To Yellow
		glVertex3f  1.0,-1.0,-1.0           '; Top Right Of The Quad (Back)
		glVertex3f -1.0,-1.0,-1.0           '; Top Left Of The Quad (Back)
		glVertex3f -1.0, 1.0,-1.0           '; Bottom Left Of The Quad (Back)
		glVertex3f  1.0, 1.0,-1.0           '; Bottom Right Of The Quad (Back)

		glColor3f 0.0,0.0,1.0               '; Set The Color To Blue
		glVertex3f -1.0, 1.0, 1.0           '; Top Right Of The Quad (Left)
		glVertex3f -1.0, 1.0,-1.0           '; Top Left Of The Quad (Left)
		glVertex3f -1.0,-1.0,-1.0           '; Bottom Left Of The Quad (Left)
		glVertex3f -1.0,-1.0, 1.0           '; Bottom Right Of The Quad (Left)

		glColor3f 1.0,0.0,1.0               '; Set The Color To Violet
		glVertex3f  1.0, 1.0,-1.0           '; Top Right Of The Quad (Right)
		glVertex3f  1.0, 1.0, 1.0           '; Top Left Of The Quad (Right)
		glVertex3f  1.0,-1.0, 1.0           '; Bottom Left Of The Quad (Right)
		glVertex3f  1.0,-1.0,-1.0           '; Bottom Right Of The Quad (Right)
	glEnd

	rtri = rtri + 0.2                       '; Increase The Rotation Variable For The Triangle ( New )
	rquad = rquad + 0.15                    '; Decrease The Rotation Variable For The Quad ( New )  
	  
	glEnable(GL_LIGHTING)
End Function 

Function nehe6()
	glClear GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT
   	glEnable(GL_TEXTURE_2D)
	glTexEnvf(GL_TEXTURE_ENV, GL_TEXTURE_ENV_MODE, GL_REPLACE)
	glBindTexture GL_TEXTURE_2D,Texname6
	
	glLoadIdentity
	glTranslatef 0.0,0.0,-5.0
	glRotatef xrot,1.0,0.0,0.0                         ' Rotate On The X Axis
	glRotatef yrot,0.0,1.0,0.0                         ' Rotate On The Y Axis
	glRotatef zrot,0.0,0.0,1.0                         ' Rotate On The Z Axis

	glBegin GL_QUADS
		' Front Face
		glTexCoord2f 0.0, 0.0; glVertex3f -1.0, -1.0, 1.0       ' Bottom Left Of The Texture And Quad
		glTexCoord2f 1.0, 0.0; glVertex3f  1.0, -1.0, 1.0       ' Bottom Right Of The Texture And Quad
		glTexCoord2f 1.0, 1.0; glVertex3f  1.0,  1.0, 1.0       ' Top Right Of The Texture And Quad
		glTexCoord2f 0.0, 1.0; glVertex3f -1.0,  1.0, 1.0       ' Top Left Of The Texture And Quad
		' Back Face
		glTexCoord2f 1.0, 0.0; glVertex3f -1.0, -1.0, -1.0      ' Bottom Right Of The Texture And Quad
		glTexCoord2f 1.0, 1.0; glVertex3f -1.0,  1.0, -1.0      ' Top Right Of The Texture And Quad
		glTexCoord2f 0.0, 1.0; glVertex3f  1.0,  1.0, -1.0      ' Top Left Of The Texture And Quad
		glTexCoord2f 0.0, 0.0; glVertex3f  1.0, -1.0, -1.0      ' Bottom Left Of The Texture And Quad
		' Top Face
		glTexCoord2f 0.0, 1.0; glVertex3f -1.0,  1.0, -1.0      ' Top Left Of The Texture And Quad
		glTexCoord2f 0.0, 0.0; glVertex3f -1.0,  1.0,  1.0      ' Bottom Left Of The Texture And Quad
		glTexCoord2f 1.0, 0.0; glVertex3f  1.0,  1.0,  1.0      ' Bottom Right Of The Texture And Quad
		glTexCoord2f 1.0, 1.0; glVertex3f  1.0,  1.0, -1.0      ' Top Right Of The Texture And Quad
		' Bottom Face
		glTexCoord2f 1.0, 1.0; glVertex3f -1.0, -1.0, -1.0      ' Top Right Of The Texture And Quad
		glTexCoord2f 0.0, 1.0; glVertex3f  1.0, -1.0, -1.0      ' Top Left Of The Texture And Quad
		glTexCoord2f 0.0, 0.0; glVertex3f  1.0, -1.0,  1.0      ' Bottom Left Of The Texture And Quad
		glTexCoord2f 1.0, 0.0; glVertex3f -1.0, -1.0,  1.0      ' Bottom Right Of The Texture And Quad
		' Right face
		glTexCoord2f 1.0, 0.0; glVertex3f  1.0, -1.0, -1.0      ' Bottom Right Of The Texture And Quad
		glTexCoord2f 1.0, 1.0; glVertex3f  1.0,  1.0, -1.0      ' Top Right Of The Texture And Quad
		glTexCoord2f 0.0, 1.0; glVertex3f  1.0,  1.0,  1.0      ' Top Left Of The Texture And Quad
		glTexCoord2f 0.0, 0.0; glVertex3f  1.0, -1.0,  1.0      ' Bottom Left Of The Texture And Quad
		' Left Face
		glTexCoord2f 0.0, 0.0; glVertex3f -1.0, -1.0, -1.0      ' Bottom Left Of The Texture And Quad
		glTexCoord2f 1.0, 0.0; glVertex3f -1.0, -1.0,  1.0      ' Bottom Right Of The Texture And Quad
		glTexCoord2f 1.0, 1.0; glVertex3f -1.0,  1.0,  1.0      ' Top Right Of The Texture And Quad
		glTexCoord2f 0.0, 1.0; glVertex3f -1.0,  1.0, -1.0      ' Top Left Of The Texture And Quad
	glEnd

	xrot = xrot + 0.3                     ' X Axis Rotation
	yrot = yrot + 0.2                     ' Y Axis Rotation
	zrot = zrot + 0.4                     ' Z Axis Rotation
	glDisable(GL_TEXTURE_2D)
End Function

Function nehe7()
	If  KeyHit(KEY_L) Then
		lp = Not lp
	EndIf
	If  lp=0 Then
		glDisable(GL_LIGHTING)
	Else
		glEnable(GL_LIGHTING)
	EndIf
	If KeyHit(KEY_F)
		Filter:+1
		If Filter&gt;2 Then Filter=0
	EndIf
	If KeyDown(KEY_PAGEUP) Then z:-0.02
	If KeyDown(KEY_PAGEDOWN) Then z:+ 0.02
	If KeyDown(KEY_UP) Then xspeed:-0.01
	If KeyDown(KEY_DOWN) Then xspeed:+0.01
	If KeyDown(KEY_RIGHT) Then yspeed:+0.01
	If KeyDown(KEY_LEFT) Then yspeed:-0.01
	
	glClear GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT
	glEnable GL_LIGHT1
	glEnable(GL_TEXTURE_2D)
	glBindTexture GL_TEXTURE_2D, Texname[Filter]
	
	glLoadIdentity
	glTranslatef 0.0,0.0,z
	glRotatef xrot,1.0,0.0,0.0
	glRotatef yrot,0.0,1.0,0.0
	glBegin GL_QUADS
		' Front Face
		glNormal3f  0.0, 0.0, 1.0
		glTexCoord2f 0.0, 0.0; glVertex3f -1.0, -1.0,  1.0
		glTexCoord2f 1.0, 0.0; glVertex3f  1.0, -1.0,  1.0
		glTexCoord2f 1.0, 1.0; glVertex3f  1.0,  1.0,  1.0
		glTexCoord2f 0.0, 1.0; glVertex3f -1.0,  1.0,  1.0
		' Back Face
		glNormal3f  0.0, 0.0,-1.0
		glTexCoord2f 1.0, 0.0; glVertex3f -1.0, -1.0, -1.0
		glTexCoord2f 1.0, 1.0; glVertex3f -1.0,  1.0, -1.0
		glTexCoord2f 0.0, 1.0; glVertex3f  1.0,  1.0, -1.0
		glTexCoord2f 0.0, 0.0; glVertex3f  1.0, -1.0, -1.0
		' Top Face
		glNormal3f  0.0, 1.0, 0.0
		glTexCoord2f 0.0, 1.0; glVertex3f -1.0,  1.0, -1.0
		glTexCoord2f 0.0, 0.0; glVertex3f -1.0,  1.0,  1.0
		glTexCoord2f 1.0, 0.0; glVertex3f  1.0,  1.0,  1.0
		glTexCoord2f 1.0, 1.0; glVertex3f  1.0,  1.0, -1.0
		' Bottom Face
		glNormal3f  0.0,-1.0, 0.0
		glTexCoord2f 1.0, 1.0; glVertex3f -1.0, -1.0, -1.0
		glTexCoord2f 0.0, 1.0; glVertex3f  1.0, -1.0, -1.0
		glTexCoord2f 0.0, 0.0; glVertex3f  1.0, -1.0,  1.0
		glTexCoord2f 1.0, 0.0; glVertex3f -1.0, -1.0,  1.0
		' Right face
		glNormal3f  1.0, 0.0, 0.0
		glTexCoord2f 1.0, 0.0; glVertex3f  1.0, -1.0, -1.0
		glTexCoord2f 1.0, 1.0; glVertex3f  1.0,  1.0, -1.0
		glTexCoord2f 0.0, 1.0; glVertex3f  1.0,  1.0,  1.0
		glTexCoord2f 0.0, 0.0; glVertex3f  1.0, -1.0,  1.0
		' Left Face
		glNormal3f -1.0, 0.0, 0.0
		glTexCoord2f 0.0, 0.0; glVertex3f -1.0, -1.0, -1.0
		glTexCoord2f 1.0, 0.0; glVertex3f -1.0, -1.0,  1.0
		glTexCoord2f 1.0, 1.0; glVertex3f -1.0,  1.0,  1.0
		glTexCoord2f 0.0, 1.0; glVertex3f -1.0,  1.0, -1.0
	glEnd

	xrot = xrot + xspeed
	yrot = yrot + yspeed
	glDisable(GL_TEXTURE_2D)
	glDisable GL_LIGHT1
End Function

Function nehe8()
	glColor4f 1.0,1.0,1.0,0.25
	glBlendFunc GL_SRC_ALPHA,GL_ONE
	
	If  KeyHit(KEY_L) Then
		lp = Not lp
	EndIf
	If lp=0 Then
		glDisable(GL_LIGHTING)
	Else
		glEnable(GL_LIGHTING)
	EndIf
	
	If KeyHit(KEY_F)
		Filter:+1
		If Filter&gt;2 Then Filter=0
	EndIf
	If KeyHit(KEY_B) Then
		Bp = Not Bp
	EndIf
	If Bp=0 Then
		glEnable GL_BLEND
		glDisable GL_DEPTH_TEST
	Else
		glDisable GL_BLEND
		glEnable GL_DEPTH_TEST
	EndIf
	
	If KeyDown(KEY_PAGEUP) Then z:-0.02
	If KeyDown(KEY_PAGEDOWN) Then z:+ 0.02
	If KeyDown(KEY_UP) Then xspeed:-0.01
	If KeyDown(KEY_DOWN) Then xspeed:+0.01
	If KeyDown(KEY_RIGHT) Then yspeed:+0.01
	If KeyDown(KEY_LEFT) Then yspeed:-0.01

	glClear GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT
	glEnable GL_LIGHT1
	glEnable(GL_TEXTURE_2D)
	glBindTexture GL_TEXTURE_2D, Texname[Filter]
	
	glLoadIdentity
	glTranslatef 0.0,0.0,z
	glRotatef xrot,1.0,0.0,0.0
	glRotatef yrot,0.0,1.0,0.0
	glBegin GL_QUADS
		' Front Face
		glNormal3f  0.0, 0.0, 1.0
		glTexCoord2f 0.0, 0.0; glVertex3f -1.0, -1.0,  1.0
		glTexCoord2f 1.0, 0.0; glVertex3f  1.0, -1.0,  1.0
		glTexCoord2f 1.0, 1.0; glVertex3f  1.0,  1.0,  1.0
		glTexCoord2f 0.0, 1.0; glVertex3f -1.0,  1.0,  1.0
		' Back Face
		glNormal3f  0.0, 0.0,-1.0
		glTexCoord2f 1.0, 0.0; glVertex3f -1.0, -1.0, -1.0
		glTexCoord2f 1.0, 1.0; glVertex3f -1.0,  1.0, -1.0
		glTexCoord2f 0.0, 1.0; glVertex3f  1.0,  1.0, -1.0
		glTexCoord2f 0.0, 0.0; glVertex3f  1.0, -1.0, -1.0
		' Top Face
		glNormal3f  0.0, 1.0, 0.0
		glTexCoord2f 0.0, 1.0; glVertex3f -1.0,  1.0, -1.0
		glTexCoord2f 0.0, 0.0; glVertex3f -1.0,  1.0,  1.0
		glTexCoord2f 1.0, 0.0; glVertex3f  1.0,  1.0,  1.0
		glTexCoord2f 1.0, 1.0; glVertex3f  1.0,  1.0, -1.0
		' Bottom Face
		glNormal3f  0.0,-1.0, 0.0
		glTexCoord2f 1.0, 1.0; glVertex3f -1.0, -1.0, -1.0
		glTexCoord2f 0.0, 1.0; glVertex3f  1.0, -1.0, -1.0
		glTexCoord2f 0.0, 0.0; glVertex3f  1.0, -1.0,  1.0
		glTexCoord2f 1.0, 0.0; glVertex3f -1.0, -1.0,  1.0
		' Right face
		glNormal3f  1.0, 0.0, 0.0
		glTexCoord2f 1.0, 0.0; glVertex3f  1.0, -1.0, -1.0
		glTexCoord2f 1.0, 1.0; glVertex3f  1.0,  1.0, -1.0
		glTexCoord2f 0.0, 1.0; glVertex3f  1.0,  1.0,  1.0
		glTexCoord2f 0.0, 0.0; glVertex3f  1.0, -1.0,  1.0
		' Left Face
		glNormal3f -1.0, 0.0, 0.0
		glTexCoord2f 0.0, 0.0; glVertex3f -1.0, -1.0, -1.0
		glTexCoord2f 1.0, 0.0; glVertex3f -1.0, -1.0,  1.0
		glTexCoord2f 1.0, 1.0; glVertex3f -1.0,  1.0,  1.0
		glTexCoord2f 0.0, 1.0; glVertex3f -1.0,  1.0, -1.0
	glEnd

	xrot = xrot + xspeed
	yrot = yrot + yspeed
	glDisable(GL_TEXTURE_2D)
	glDisable GL_LIGHT1
End Function

' Minib3d functions
' -----------------
Type TGlobal

	Function Graphics3D(w:Int,h:Int,d:Int=0,m:Int=0,r:Int=60,flags:Int=-1)	
	' SMALLFIXES added flags so that the ACCUMBUFFER can be left out (crashes parallels)

		If flags = -1 Then flags = GRAPHICS_BACKBUFFER|GRAPHICS_ALPHABUFFER|GRAPHICS_DEPTHBUFFER|GRAPHICS_STENCILBUFFER|GRAPHICS_ACCUMBUFFER
		'If flags = -1 Then flags = GRAPHICS_BACKBUFFER|GRAPHICS_DEPTHBUFFER|GRAPHICS_ACCUMBUFFER

		'mode:
		'0: windowed in debug mode, fullscreen in non-debug mode 
		'1: full-screen always 
		'2: windowed always 

		' change depth values so that Graphics will behave in the same way as Blitz3D-style Graphics3D
		Select m
			Case 0
				?debug
					d=0
				?
				?Not debug
					If d=0 Then d=16
				?
			Case 1
				If d=0 Then d=16
			Case 2
				d=0		
			Default
				d=0		
		End Select
			
		width=w
		height=h
		depth=d
		Mode=m
		rate=r
		
		SetGraphicsDriver(GLMax2DDriver()) 
		Graphics(width,height,depth,rate,flags)
	
		GraphicsInit()
								
	End Function
	
	Function GraphicsInit()
		
		glewInit() ' required for ARB funcs

		' get hardware info and set vbo_enabled accordingly
		'THardwareInfo.GetInfo()
		
		'If USE_VBO=True
		'	vbo_enabled=THardwareInfo.VBOSupport
		'EndIf

		'If USE_MAX2D=True

			' save the Max2D settings for later - by Oddball
			glPushAttrib GL_ALL_ATTRIB_BITS
			glPushClientAttrib GL_CLIENT_ALL_ATTRIB_BITS
			glMatrixMode GL_MODELVIEW
			glPushMatrix
			glMatrixMode GL_PROJECTION
			glPushMatrix
			glMatrixMode GL_TEXTURE
			glPushMatrix
			glMatrixMode GL_COLOR
			glPushMatrix
		
		'EndIf
		
		EnableStates()
		
		glLightModeli(GL_LIGHT_MODEL_COLOR_CONTROL,GL_SEPARATE_SPECULAR_COLOR)
		glLightModeli(GL_LIGHT_MODEL_LOCAL_VIEWER,GL_TRUE)

		glClearDepth(1.0)						
		glDepthFunc(GL_LEQUAL)
		glHint(GL_PERSPECTIVE_CORRECTION_HINT, GL_NICEST)

		glAlphaFunc(GL_GEQUAL,0.5)
		
	End Function
	
	Function EnableStates()
	
		glEnable(GL_LIGHTING)
   		glEnable(GL_DEPTH_TEST)
		glEnable(GL_FOG)
		glEnable(GL_CULL_FACE)
		glEnable(GL_SCISSOR_TEST)
		
		glEnable(GL_NORMALIZE)
		
		glEnableClientState(GL_VERTEX_ARRAY)
		glEnableClientState(GL_COLOR_ARRAY)
		glEnableClientState(GL_NORMAL_ARRAY)
	
	End Function

End Type

Type TBlitz2D

	Function Text(x:Int,y:Int,Text$)
	
		' set active texture to texture 0 so gldrawtext will work correctly
		'If THardwareInfo.VBOSupport 'SMALLFIXES hack to keep non vbo GFX from crashing
		'	glActiveTextureARB(GL_TEXTURE0)
		'	glClientActiveTextureARB(GL_TEXTURE0)
		'EndIf
				
		glDisable(GL_LIGHTING)
		
		glColor3f(1.0,1.0,1.0)
		
		' enable blend to hide text background
		glEnable(GL_BLEND)
		glBlendFunc GL_SRC_ALPHA,GL_ONE

		GLDrawText Text$,x,y
		
		glDisable(GL_BLEND)
		
		glEnable(GL_LIGHTING)
		
		' disable texture 2D - needed as gldrawtext enables it, but doesn't disable after use
		glDisable(GL_TEXTURE_2D)
		
	End Function

	' Function by Oddball
	Function BeginMax2D()

		glPopClientAttrib
		glPopAttrib
		glMatrixMode GL_MODELVIEW
		glPopMatrix
		glMatrixMode GL_PROJECTION
		glPopMatrix
		glMatrixMode GL_TEXTURE
		glPopMatrix
		glMatrixMode GL_COLOR
		glPopMatrix 
	
	End Function
	
	' Function by Oddball	
	Function EndMax2D()
	
		' save the Max2D settings for later
		glPushAttrib GL_ALL_ATTRIB_BITS
		glPushClientAttrib GL_CLIENT_ALL_ATTRIB_BITS
		glMatrixMode GL_MODELVIEW
		glPushMatrix
		glMatrixMode GL_PROJECTION
		glPushMatrix
		glMatrixMode GL_TEXTURE
		glPushMatrix
		glMatrixMode GL_COLOR
		glPushMatrix 
		
		TGlobal.EnableStates()
		glDisable GL_TEXTURE_2D
		
		glLightModeli(GL_LIGHT_MODEL_COLOR_CONTROL,GL_SEPARATE_SPECULAR_COLOR)
		glLightModeli(GL_LIGHT_MODEL_LOCAL_VIEWER,GL_TRUE)
	
		glClearDepth(1.0)						
		glDepthFunc(GL_LEQUAL)
		glHint(GL_PERSPECTIVE_CORRECTION_HINT, GL_NICEST)
	
		glAlphaFunc(GL_GEQUAL,0.5)
		
	End Function
	
End Type
</textarea> <br><br></td></tr></table><br>
<a name="1251545"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >cgrossi</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi, munch.<br><br>I did what you said. To use glmax2ddriver. The opengl commands are executed ok, but when I uncomment the max2d command, the screen goes blank. No errors are displayed, nothing happnes, just the screen goes blank.... I don't want to use 3D, only 2D.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

SetGraphicsDriver GLMax2DDriver()
Graphics 800,600

glViewport (0, 0, 800, 600)
glMatrixMode (GL_PROJECTION)
glLoadIdentity ()

Local pt1: Float [] = [0.5,0.0,0.75]
Local pt2: Float [] = [0.0,0.5,0.75]
Local pt3: Float [] = [0.5,0.5,-0.75]
Local offset: Float [] = [-0.75,0.0,0.5]

img:TImage=LoadImage("images\music.png")

Repeat

Cls

glBegin(GL_POLYGON)
glColor3f(1.0,0,0)
glVertex3fv (pt1)
glColor3f(0,1.0,0)
glVertex3fv (pt2)
glColor3f(0,0,1.0)
glVertex3fv (pt3)
glEnd()

glBegin(GL_POLYGON)
glColor3f(1.0,0,0)
glVertex3fv (AddVecs(pt1,offset))
glColor3f(0,1.0,0)
glVertex3fv (AddVecs(pt2,offset))
glColor3f(0,0,1.0)
glVertex3fv (AddVecs(pt3,offset))
glEnd()

'''''' max2d: DrawImage img,10,10

Flip()




Until KeyDown(KEY_ESCAPE) Or AppTerminate()

Function AddVecs:Float[](in:Float[],modd:Float[])
	Local total:Float [] = New Float[3]
	total[0] = in[0]+modd[0]
	total[1] = in[1]+modd[1]
	total[2] = in[2]+modd[2]
	Return total

End Function
</textarea> <br><br></td></tr></table><br>
<a name="1251568"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >xlsior</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I don't know if it also applies when mixing OpenGL &amp; Max2D, but try:<br><br>GLShareContexts() <br><br></td></tr></table><br>
<a name="1251573"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >cgrossi</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi, xlsior..<br><br>I got an error: Identifier GLShareContexts() not found.... I think my module is outdated... Do you know how I update it? <br><br></td></tr></table><br>
<a name="1251606"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >cgrossi</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi..<br><br>I tried GLShareContexts and didn't work....<br><br>Cristiano <br><br></td></tr></table><br>
<a name="1251624"></a>

<a name="1251625"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Okay, I adapted my previous example for your code. It still uses the same rendering "engine" from minib3d but now draws in 2d perspective. Needs spark.png.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' max2d_opengl.bmx

Strict

Framework BRL.GLMax2D
Import PUB.Glew
Import brl.bmploader
Import brl.pngloader
Import brl.jpgloader
Import brl.system
Import brl.random

' Initialise
' ----------
Global ScreenWidth:Int=800
Global ScreenHeight:Int=600
Global ScreenDepth:Int=0

Global Framecounter_time:Int,Framecounter_framerate:Int,Framecounter_counter:Int

TGlobal.Graphics3D 800,600,0,2
HideMouse

' Initialise Max2D
' ----------------

Local img:TImage=LoadImage("spark.png")

' Main loop
' ---------
While Not KeyHit(KEY_ESCAPE)

	DisableStates()
	glDisable(GL_FOG)
	
	DrawOpenGL()
	
	glDisable(GL_LIGHTING)
	glColor3f(1.0,1.0,1.0)
	
	' Framecounter
	' ------------
	Framecounter_counter=Framecounter_counter+1
	If Framecounter_time=0 Then Framecounter_time=MilliSecs()
	If Framecounter_time+1001 &lt;MilliSecs() Then
		Framecounter_framerate=Framecounter_counter
		Framecounter_counter=0
		Framecounter_time=MilliSecs()
	EndIf
	
	GLDrawText("Max2D with OpenGL using Minib3d",10,24)
	GLDrawText("FPS : "+Framecounter_framerate,ScreenWidth-(8*12),ScreenHeight-16-8)
	
	glEnable(GL_FOG)
	glEnable(GL_LIGHTING)
	
	TBlitz2D.BeginMax2D()
	SetBlend LIGHTBLEND
	DrawImage img,MouseX(),MouseY()
	TBlitz2D.EndMax2D()	
	
	TBlitz2D.Text MouseX(),MouseY(),"Max2D OpenGL"
	Flip
	Cls
	
Wend
End

' Functions
' ---------
Function AddVecs:Float[](in:Float[],modd:Float[])

	Local total:Float [] = New Float[3]
	total[0] = in[0]+modd[0]
	total[1] = in[1]+modd[1]
	total[2] = in[2]+modd[2]
	Return total

End Function

Function DrawOpenGL()

	Local pt1: Float [] = [0.5,0.0,0.75]
	Local pt2: Float [] = [0.0,0.5,0.75]
	Local pt3: Float [] = [0.5,0.5,-0.75]
	Local offset: Float [] = [-0.75,0.0,0.5]

	glFrontFace(GL_CW)
	glDisable(GL_LIGHTING)

	glClear GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT
	glMatrixMode (GL_PROJECTION)
	glLoadIdentity

	glBegin(GL_POLYGON)
	glColor3f(1.0,0,0)
	glVertex3fv (pt1)
	glColor3f(0,1.0,0)
	glVertex3fv (pt2)
	glColor3f(0,0,1.0)
	glVertex3fv (pt3)
	glEnd()

	glBegin(GL_POLYGON)
	glColor3f(1.0,0,0)
	glVertex3fv (AddVecs(pt1,offset))
	glColor3f(0,1.0,0)
	glVertex3fv (AddVecs(pt2,offset))
	glColor3f(0,0,1.0)
	glVertex3fv (AddVecs(pt3,offset))
	glEnd()
	
End Function

Function DisableStates()
	
	glDisableClientState(GL_VERTEX_ARRAY)
	glDisableClientState(GL_COLOR_ARRAY)
	glDisableClientState(GL_NORMAL_ARRAY)
	
End Function

' Minib3d functions
' -----------------
Type TGlobal

	Function Graphics3D(w:Int,h:Int,d:Int=0,m:Int=0,r:Int=60,flags:Int=-1)	
	' SMALLFIXES added flags so that the ACCUMBUFFER can be left out (crashes parallels)

		If flags = -1 Then flags = GRAPHICS_BACKBUFFER|GRAPHICS_ALPHABUFFER|GRAPHICS_DEPTHBUFFER|GRAPHICS_STENCILBUFFER|GRAPHICS_ACCUMBUFFER
		'If flags = -1 Then flags = GRAPHICS_BACKBUFFER|GRAPHICS_DEPTHBUFFER|GRAPHICS_ACCUMBUFFER

		'mode:
		'0: windowed in debug mode, fullscreen in non-debug mode 
		'1: full-screen always 
		'2: windowed always 

		' change depth values so that Graphics will behave in the same way as Blitz3D-style Graphics3D
		Select m
			Case 0
				?debug
					d=0
				?
				?Not debug
					If d=0 Then d=16
				?
			Case 1
				If d=0 Then d=16
			Case 2
				d=0		
			Default
				d=0		
		End Select
			
		'width=w
		'height=h
		'depth=d
		'Mode=m
		'rate=r
		
		SetGraphicsDriver(GLMax2DDriver()) 
		Graphics(w,h,d,r,flags)
	
		GraphicsInit()
								
	End Function
	
	Function GraphicsInit()
		
		glewInit() ' required for ARB funcs

		' get hardware info and set vbo_enabled accordingly
		'THardwareInfo.GetInfo()
		
		'If USE_VBO=True
		'	vbo_enabled=THardwareInfo.VBOSupport
		'EndIf

		'If USE_MAX2D=True

			' save the Max2D settings for later - by Oddball
			glPushAttrib GL_ALL_ATTRIB_BITS
			glPushClientAttrib GL_CLIENT_ALL_ATTRIB_BITS
			glMatrixMode GL_MODELVIEW
			glPushMatrix
			glMatrixMode GL_PROJECTION
			glPushMatrix
			glMatrixMode GL_TEXTURE
			glPushMatrix
			glMatrixMode GL_COLOR
			glPushMatrix
		
		'EndIf
		
		EnableStates()
		
		glLightModeli(GL_LIGHT_MODEL_COLOR_CONTROL,GL_SEPARATE_SPECULAR_COLOR)
		glLightModeli(GL_LIGHT_MODEL_LOCAL_VIEWER,GL_TRUE)

		glClearDepth(1.0)						
		glDepthFunc(GL_LEQUAL)
		glHint(GL_PERSPECTIVE_CORRECTION_HINT, GL_NICEST)

		glAlphaFunc(GL_GEQUAL,0.5)
		
	End Function
	
	Function EnableStates()
	
		glEnable(GL_LIGHTING)
   		glEnable(GL_DEPTH_TEST)
		glEnable(GL_FOG)
		glEnable(GL_CULL_FACE)
		glEnable(GL_SCISSOR_TEST)
		
		glEnable(GL_NORMALIZE)
		
		glEnableClientState(GL_VERTEX_ARRAY)
		glEnableClientState(GL_COLOR_ARRAY)
		glEnableClientState(GL_NORMAL_ARRAY)
	
	End Function

End Type

Type TBlitz2D

	Function Text(x:Int,y:Int,Text$)
	
		' set active texture to texture 0 so gldrawtext will work correctly
		'If THardwareInfo.VBOSupport 'SMALLFIXES hack to keep non vbo GFX from crashing
		'	glActiveTextureARB(GL_TEXTURE0)
		'	glClientActiveTextureARB(GL_TEXTURE0)
		'EndIf
				
		glDisable(GL_LIGHTING)
		
		glColor3f(1.0,1.0,1.0)
		
		' enable blend to hide text background
		glEnable(GL_BLEND)
		glBlendFunc GL_SRC_ALPHA,GL_ONE

		GLDrawText Text$,x,y
		
		glDisable(GL_BLEND)
		
		glEnable(GL_LIGHTING)
		
		' disable texture 2D - needed as gldrawtext enables it, but doesn't disable after use
		glDisable(GL_TEXTURE_2D)
		
	End Function

	' Function by Oddball
	Function BeginMax2D()

		glPopClientAttrib
		glPopAttrib
		glMatrixMode GL_MODELVIEW
		glPopMatrix
		glMatrixMode GL_PROJECTION
		glPopMatrix
		glMatrixMode GL_TEXTURE
		glPopMatrix
		glMatrixMode GL_COLOR
		glPopMatrix 
	
	End Function
	
	' Function by Oddball	
	Function EndMax2D()
	
		' save the Max2D settings for later
		glPushAttrib GL_ALL_ATTRIB_BITS
		glPushClientAttrib GL_CLIENT_ALL_ATTRIB_BITS
		glMatrixMode GL_MODELVIEW
		glPushMatrix
		glMatrixMode GL_PROJECTION
		glPushMatrix
		glMatrixMode GL_TEXTURE
		glPushMatrix
		glMatrixMode GL_COLOR
		glPushMatrix 
		
		TGlobal.EnableStates()
		glDisable GL_TEXTURE_2D
		
		glLightModeli(GL_LIGHT_MODEL_COLOR_CONTROL,GL_SEPARATE_SPECULAR_COLOR)
		glLightModeli(GL_LIGHT_MODEL_LOCAL_VIEWER,GL_TRUE)
	
		glClearDepth(1.0)						
		glDepthFunc(GL_LEQUAL)
		glHint(GL_PERSPECTIVE_CORRECTION_HINT, GL_NICEST)
	
		glAlphaFunc(GL_GEQUAL,0.5)
		
	End Function
	
End Type
</textarea> <br><br></td></tr></table><br>
<a name="1251668"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >cgrossi</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you a lot, munch. It worked very fine!!! Now I can begin my opengl studies!!<br><br>Cristiano <br><br></td></tr></table><br>
<a name="1251776"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> You're welcome, good luck!<br><br>Note that you can probably remove things like gl(En/Dis)ableClientState(GL_VERTEX_ARRAY) and gl(En/Dis)able(GL_FOG) as these shouldn't be relevant to 2d. <br><br></td></tr></table><br>
<a name="1251976"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Re: Identifier GLShareContexts() not found. I recommend using the latest version of Bmx (1.50). This error is probably because you didn't imported the right module. I reproduced it by using "Framework brl.max2d" instead of "Framework brl.glmax2d". In this case, GLShareContexts is found in BRL.GLGraphics which is imported by BRL.GLMax2D. <br><br>If you don't specify what modules to use Bmx will use most of the brl and pub modules which increases your exe size. To find where a function is, the easiest way is to hit F1 twice but to find where it is imported by other modules you can use the MaxIDE Edit&gt;Find in Files tool. Type the module name (eg. brl.max2d) and browse to the mod "scope" folder in this case "Blitzmax/mod/brl.mod". <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
