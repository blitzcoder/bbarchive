<!DOCTYPE html><html lang="en" ><head ><title >StringList</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Multipurpose StringList functions for b3d, language=bb, category=Miscellaneous'><meta name='author' content='Mahan'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=9>Miscellaneous</a>/StringList</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2381.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >StringList by Mahan</td><td align="right">2008 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Lists of strings are very useful in programming.<br><br>examples:<br>* read a complete text-file and examine line-by-line in memory.<br>* split strings by some separator and get a list of all the elements.<br>* Add strings to the list during some operation for later processing.<br>* Storing the text lines of a chat/status window.<br><br>Note: The implementation is not very optimized or advanced. It will work well for small to moderate amounts of information, but I wouldn't recommend using it for 10's or 100's of megabytes of text.<br><br><br>code examples:<br><pre class=code>

Include "strlist.bb"

;Example 1 - Make a list of the components of a path, using \ as separator
sl = CreateStrings()
AddSplitString(sl, "c:\program\test\file.txt", "\")
For i = 0 To CountStrings(sl)-1
	Print GetString(sl, i)
Next

;Example 2 - Reformat and tidy up a sentence rebuilding spaces
sl2 = CreateStrings()
AddSplitString(sl2, "    Mobilis     in   Mobile    ", " ")
DeleteString(sl2, "")
Print "Reformatted string: '" + StringFromStrings(sl2, " ") + "'"

;Example 3 - Read up text file
sl3 = CreateStrings()
If FileType("strlist.bb")=0 Then End
f=ReadFile("strlist.bb")
ReadStrings(sl3, f)
CloseFile(f)
;For i = 0 To CountStrings(sl3)-1
;	Print GetString(sl3, i)
;Next
Print "Lines in file: " + CountStrings(sl3)

FreeStrings(sl)
FreeStrings(sl2)
FreeStrings(sl3)
</pre> </td></tr><tr ><td class="cell"><pre class="code">;-------------------------------------------------
; strlist.bb - stringlist functions
;
; purpose: 
;	A very basic StringList.
;
;   Beware. Performance will degrade if used with
;   large amounts of Data. 
;
;-------------------------------------------------

Global sfHdlCounter_=-1

;INTERNAL: This is a contaner type for all strings in all stringlists.
Type TStrings
	Field hdl; this object belongs to this list-handle
	Field string$
End Type

;-------------------------------------------------
; Creates a new stringlist.
;
; Returns handle to new stringlist.
;
Function CreateStrings()
	sfHdlCounter_=sfHdlCounter_+1
	Return sfHdlCounter_
End Function

;-------------------------------------------------
; Frees (releases) a stringlist by handle.
;
; hdl - handle to previously created stringlist.
Function FreeStrings(hdl)
	For s.TStrings = Each TStrings
		If s\hdl=hdl Then Delete s
	Next
End Function

;-------------------------------------------------
; Adds a new string to the stringlist.
;
; hdl - stringlist handle
; s$ - the string to add
Function AddString(hdl, s$)
	n.TStrings = New TStrings
	n\hdl=hdl
	n\string=s
End Function

;-------------------------------------------------
; Returns the current number of strings in the
; stringlist.
;
; hdl - stringlist handle
Function CountStrings(hdl)
	cnt=0
	For s.TStrings = Each TStrings
		If s\hdl = hdl Then cnt=cnt+1
	Next
	Return cnt
End Function

;-------------------------------------------------
; Returns the string at a current index in the
; stringlist.
; 
; hdl - stringlist handle
; idx - index of a string between 0 and
;       CountStrings(hdl)-1.
;
; NOTE: Passing an invalid index will return ""
Function GetString$(hdl, idx)
	cnt=0
	For s.TStrings = Each TStrings
		If (s\hdl = hdl) And (cnt=idx) Then Return s.TStrings\String
		If s\hdl = hdl Then cnt=cnt+1
	Next
	Return ""
End Function


;-------------------------------------------------
; Splits a string based on a separator into a
; stringlist.
;
; hdl - stringlist handle
; s$ - the string to split
; sep$ - the separator
Function AddSplitString(hdl, s$, sep$)
	ls=Len(sep)
	p=Instr(s, sep)
	While p&gt;0
		AddString(hdl, Left$(s, p-1))
		;s=Right$(s, p+ls+1)
		s$=Mid$(s$, p+ls, 2000000000) ; just copy the rest of the string
		p=Instr(s, sep)
	Wend
	If Len(s) &gt; 0 Then AddString(hdl, s)
End Function

;-------------------------------------------------
; Deletes a string on a certain index in the
; stringlist.
;
; hdl - stringlist handle
; idx - index of a string between 0 and
;       CountStrings(hdl)-1.
;
; NOTE: Passing an invalid index will just waste
; cpu-cycles and do nothing.
Function DeleteStringIdx(hdl, idx)
	cnt=0
	For s.TStrings = Each TStrings
		If (s\hdl = hdl) And (cnt=idx) Then
			Delete s
			Return
		EndIf
		If s\hdl = hdl Then cnt=cnt+1
	Next
End Function


;-------------------------------------------------
; Deletes all strings equal to s$ in a stringlist.
;
; hdl - stringlist handle
; s$ - string to delete
Function DeleteString(hdl, s$)
	For st.TStrings = Each TStrings
		If (st\hdl = hdl) And (st\String=s) Then Delete st
	Next
End Function

;-------------------------------------------------
; Build a new string from all strings in the
; stringlist. A separator may be used between each
; element of the stringlist
;
; hdl - stringlist handle
; sep$ - optional separator
Function StringFromStrings$(hdl, sep$="")
	res$=""
	For s.TStrings = Each TStrings
		If (s\hdl = hdl) Then
			res=res+s\String+sep
		EndIf
	Next
	l = Len(res)
	ls = Len(sep)
	If (ls&gt;0) And (l&gt;0) Then res = Left$(res, l-Len(sep)) ;remove last separator
	Return res
End Function


;-------------------------------------------------
; Read in a whole ascii-stream into a stringlist
;
; hdl - stringlist handle
; stream - stream/file handle.
Function ReadStrings(hdl, stream)
	While Not Eof(stream)
		AddString(hdl, ReadLine(stream))
	Wend
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
