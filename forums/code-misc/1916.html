<!DOCTYPE html><html lang="en" ><head ><title >Gadget Tooltips (Win32)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Tooltips/Balloon tips for MaxGUI gadgets, language=bmx, category=Miscellaneous'><meta name='author' content='grable'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=9>Miscellaneous</a>/Gadget Tooltips (Win32)</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1916.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gadget Tooltips (Win32) by grable</td><td align="right">2007 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Tooltips &amp; Balloontips for MaxGUI gagets. <b>Win32 Only</b>.<br>This is the most basic tooltips you can get, if you want more options i suggest reading up on it on msdn.<br>feks here: <a href="http://msdn.microsoft.com/library/default.asp?url=/library/en-us/shellcc/platform/commctls/tooltip/usingtooltips.asp" target="_blank">http://msdn.microsoft.com/library/default.asp?url=/library/en-us/shellcc/platform/commctls/tooltip/usingtooltips.asp</a><br><br>Setting a tooltip<br><pre class=code>
SetGadgetTooltip( gadget, "this is the tip")
</pre><br>Removing a tooltip<br><pre class=code>
SetGadgetTooltip( gadget, Null)
</pre><br>Usage example:<br><pre class=code>
SuperStrict

Import "Tooltips.bmx"

Global window:TGadget = CreateWindow( "Main", 64,64, 256,256, Desktop(), WINDOW_TITLEBAR | WINDOW_CLIENTCOORDS)
Global button1:TGadget = CreateButton( "Hover me!", 64,64, 75,25, window)
Global button2:TGadget = CreateButton( "Hover me too!", 64 + 80,64, 75,25, window)

SetGadgetTooltip( button1, "This is standard tool-tip")
SetGadgetBalloontip( button2, "This is a balloon-tip")

While WaitEvent()
	Select CurrentEvent.ID
		Case EVENT_GADGETACTION
			' remove tooltip from clicked button
			Select CurrentEvent.Source
				Case button1	SetGadgetTooltip( button1, Null)
				Case button2	SetGadgetBalloontip( button2, Null)
			EndSelect	
		Case EVENT_WINDOWCLOSE
			If CurrentEvent.Source = window Then Exit
	EndSelect
Wend
End
</pre> </td></tr><tr ><td class="cell"><pre class="code">SuperStrict

?Win32
Import BRL.MaxGUI
Import BRL.Win32MaxGUI 
Import PUB.Win32
?

Const TTS_ALWAYSTIP:Int = 1
Const TTS_NOPREFIX:Int = 2
Const TTS_NOANIMATE:Int = $10
Const TTS_NOFADE:Int = $20
Const TTS_BALLOON:Int = $40
Const TTS_CLOSE:Int = $80	

?Win32
Const TTM_ACTIVATE:Int = WM_USER+1
Const TTM_SETDELAYTIME:Int = WM_USER+3
Const TTM_ADDTOOLA:Int = WM_USER+4
Const TTM_ADDTOOLW:Int = WM_USER+50
Const TTM_DELTOOLA:Int = WM_USER+5
Const TTM_DELTOOLW:Int = WM_USER+51
Const TTM_NEWTOOLRECTA:Int = WM_USER+6
Const TTM_NEWTOOLRECTW:Int = WM_USER+52
Const TTM_RELAYEVENT:Int = WM_USER+7
Const TTM_GETTOOLINFOA:Int = WM_USER+8
Const TTM_GETTOOLINFOW:Int = WM_USER+53
Const TTM_SETTOOLINFOA:Int = WM_USER+9
Const TTM_SETTOOLINFOW:Int = WM_USER+54
Const TTM_HITTESTA:Int = WM_USER+10
Const TTM_HITTESTW:Int = WM_USER+55
Const TTM_GETTEXTA:Int = WM_USER+11
Const TTM_GETTEXTW:Int = WM_USER+56
Const TTM_UPDATETIPTEXTA:Int = WM_USER+12
Const TTM_UPDATETIPTEXTW:Int = WM_USER+57
Const TTM_GETTOOLCOUNT:Int = WM_USER+13
Const TTM_ENUMTOOLSA:Int = WM_USER+14
Const TTM_ENUMTOOLSW:Int = WM_USER+58
Const TTM_GETCURRENTTOOLA:Int = WM_USER+15
Const TTM_GETCURRENTTOOLW:Int = WM_USER+59
Const TTM_WINDOWFROMPOINT:Int = WM_USER+16
Const TTM_TRACKACTIVATE:Int = WM_USER+17
Const TTM_TRACKPOSITION:Int = WM_USER+18
Const TTM_SETTIPBKCOLOR:Int = WM_USER+19
Const TTM_SETTIPTEXTCOLOR:Int = WM_USER+20
Const TTM_GETDELAYTIME:Int = WM_USER+21
Const TTM_GETTIPBKCOLOR:Int = WM_USER+22
Const TTM_GETTIPTEXTCOLOR:Int = WM_USER+23
Const TTM_SETMAXTIPWIDTH:Int = WM_USER+24
Const TTM_GETMAXTIPWIDTH:Int = WM_USER+25
Const TTM_SETMARGIN:Int = WM_USER+26
Const TTM_GETMARGIN:Int = WM_USER+27
Const TTM_POP:Int = WM_USER+28
Const TTM_UPDATE:Int = WM_USER+29
Const TTM_GETBUBBLESIZE:Int = WM_USER+30
Const TTM_ADJUSTRECT:Int = WM_USER+31
Const TTM_SETTITLEA:Int = WM_USER+32
Const TTM_SETTITLEW:Int = WM_USER+33
Const TTM_ADDTOOL:Int = TTM_ADDTOOLA
Const TTM_DELTOOL:Int = TTM_DELTOOLA
Const TTM_NEWTOOLRECT:Int = TTM_NEWTOOLRECTA
Const TTM_GETTOOLINFO:Int = TTM_GETTOOLINFOA
Const TTM_SETTOOLINFO:Int = TTM_SETTOOLINFOA
Const TTM_HITTEST:Int = TTM_HITTESTA
Const TTM_GETTEXT:Int = TTM_GETTEXTA
Const TTM_UPDATETIPTEXT:Int = TTM_UPDATETIPTEXTA
Const TTM_ENUMTOOLS:Int = TTM_ENUMTOOLSA
Const TTM_GETCURRENTTOOL:Int = TTM_GETCURRENTTOOLA	

Private
Type TToolInfoA
	Field cbSize:Int
	Field uFlags:Int
	Field hwnd:Int
	Field uID:Int
	Field rect:Int,_rect2:Int,_rect3:Int,_rect4:Int
	Field hInst:Int
	Field lpszText:Byte Ptr
	Field lParam:Int
EndType

Const TOOLTIPS_CLASS:String = "tooltips_class32"

Global TooltipHandle:Int
Global BalloontipHandle:Int

Extern "C"
	Function strncpy:Int( dest:Byte Ptr, src$z, length:Int)
EndExtern
Public

' automaticly initialize tooltips
InitTooltips()
?

Function InitTooltips()	
	?Win32
	' regular tooltips
	If TooltipHandle = 0 Then
		TooltipHandle = CreateWindowExA( WS_EX_TOPMOST, TOOLTIPS_CLASS, Null,..
	                            WS_POPUP | TTS_NOPREFIX | TTS_ALWAYSTIP,..
	                            CW_USEDEFAULT, CW_USEDEFAULT,..
	                            CW_USEDEFAULT, CW_USEDEFAULT,..
	                            0, Null, 0, Null)
	
		SetWindowPos( TooltipHandle, HWND_TOPMOST,0, 0, 0, 0, SWP_NOMOVE | SWP_NOSIZE | SWP_NOACTIVATE)
		SendMessageA( TooltipHandle, TTM_ACTIVATE, True, 0)
	EndIf
	' balloon tips
	If BalloontipHandle = 0 Then
		BalloontipHandle = CreateWindowExA( WS_EX_TOPMOST, TOOLTIPS_CLASS, Null,..
	                            WS_POPUP | TTS_NOPREFIX | TTS_ALWAYSTIP | TTS_BALLOON,..
	                            CW_USEDEFAULT, CW_USEDEFAULT,..
	                            CW_USEDEFAULT, CW_USEDEFAULT,..
	                            0, Null, 0, Null)
	
		SetWindowPos( BalloontipHandle, HWND_TOPMOST,0, 0, 0, 0, SWP_NOMOVE | SWP_NOSIZE | SWP_NOACTIVATE)
		SendMessageA( BalloontipHandle, TTM_ACTIVATE, True, 0)	
	EndIf
	?
EndFunction

Function SetGadgetTooltip( gadget:TGadget, text:String, flags:Int = 0, id:Int = 0)
	If Not gadget Then Return
	?Win32
	Local ti:TToolInfoA = New TToolInfoA
	ti.cbSize = SizeOf(TToolInfoA)
	ti.uFlags = TTF_SUBCLASS | flags 
	ti.hwnd = gadget.Query( QUERY_HWND)
	ti.uID = id
	ti.rect = 0
	ti._rect2 = 0
	ti._rect3 = gadget.Width
	ti._rect4 = gadget.Height	
	If text.Length &gt; 0 Then		
		Local buff:Byte[text.Length+1]
		strncpy( buff, text, buff.Length)	
		ti.lpszText = buff
		SendMessageA( TooltipHandle, TTM_ADDTOOL, 0, Int Byte Ptr ti)	
	Else
		SendMessageA( TooltipHandle, TTM_DELTOOL, 0, Int Byte Ptr ti)	
	EndIf
	?
EndFunction

Function SetGadgetBalloontip( gadget:TGadget, text:String, flags:Int = TTF_CENTERTIP, id:Int = 0)
	If Not gadget Then Return
	?Win32
	Local ti:TToolInfoA = New TToolInfoA
	ti.cbSize = SizeOf(TToolInfoA)
	ti.uFlags = TTF_SUBCLASS | flags
	ti.hwnd = gadget.Query( QUERY_HWND)
	ti.uID = id
	ti.rect = 0
	ti._rect2 = 0
	ti._rect3 = gadget.Width
	ti._rect4 = gadget.Height	
	If text.Length &gt; 0 Then
		Local buff:Byte[text.Length+1]
		strncpy( buff, text, buff.Length)	
		ti.lpszText = buff
		SendMessageA( BalloontipHandle, TTM_ADDTOOL, 0, Int Byte Ptr ti)	
	Else
		SendMessageA( BalloontipHandle, TTM_DELTOOL, 0, Int Byte Ptr ti)	
	EndIf
	?
EndFunction</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>2007</font></td></tr></table></td></tr><tr ><td class="posttext"> Updated with required module imports, and added some more ?Win32 defines <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >FreakForFreedom</td><td align="right"><font class=tiny>2007</font></td></tr></table></td></tr><tr ><td class="posttext"> Very good, <br>but how can I write more than one line in a tooltip ? <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Perturbatio</td><td align="right"><font class=tiny>2007</font></td></tr></table></td></tr><tr ><td class="posttext"> you will send the max string width to the tooltip using SendMessageA and TTM_SETMAXTIPWIDTH (the specifics I'll leave up to you). <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
