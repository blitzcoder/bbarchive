<!DOCTYPE html><html lang="en" ><head ><title >Simple work break timer</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='A lightweight timer utility for taking breaks from work, includes intervals like 30 minutes, 1 hour etc., language=bmx, category=Miscellaneous'><meta name='author' content='Kryzon'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=9>Miscellaneous</a>/Simple work break timer</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="3232.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Simple work break timer by Kryzon</td><td align="right">2015 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> A simple timer utility that flashes its taskbar button when the time has passed.<br>It's supposed to be used for taking breaks from work.<br><br><img src="http://s16.postimg.org/tmtcb9s05/break_Timer.png"><br><br>Right now it's Windows only. This has been written with 'lightweightness' in mind.<br>Since this is MaxGUI, the form layout may be messed up depending on your DPI settings -- so make sure to test it and change the coordinates and sizes of the controls if there's anything wrong.<br><br>You can add a value in minutes to the command line. Right-click the executable and select Send To -&gt; Desktop (create shortcut). <br>Then right-click the shortcut in your desktop and, after the executable name (outside of the quotes), add a value like "24" to the command line. When you run it through this shortcut, the 24 minute option will already be selected for you to use with the timer.<br>This allows you to choose your favourite waiting time and just run the program and click the start button quicker, without having to select a preset time in the list.<br><br>You want to work, not be clicking stuff.<br><br>EDIT: Added the starting and ending time to the status bar text, it's interesting to know about.<br><br>EDIT 2: The timer is working great and is very useful! I added a command-line parsing so you can add a number to the command line and the program starts with that number selected in the list. Read the description above. </td></tr><tr ><td class="cell"><pre class="code">'WORK BREAK TIMER UTILITY.
'Nov. 2015.

SuperStrict

Framework maxgui.win32maxguiex
Import brl.eventqueue


GCSetMode( 2 ) 'MANUAL GC MODE.


'Import some Win API functions.

Extern 

	Function SetTimer:Int( hWND:Int, nIDEvent:Int, elapse:Int, timerProc( hWND:Int, uMSG:Int, idEvent:Int, dwTime:Int ) ) "win32"
	Function KillTimer:Int( hWND:Int, uIDEvent:Int ) "win32"
	
	Function FlashWindowEx:Int( flashInfoStructure:Byte Ptr ) "win32"
	
	Function GetModuleFileNameA:Int( hModule:Int, name:Byte Ptr, nSize:Int ) "win32"

End Extern


'Windows taskbar flash FLASHINFO structure.

Type FLASHINFO

	Rem  
	  UINT  cbSize;
	  HWND  hwnd;
	  DWORD dwFlags;
	  UINT  uCount;
	  DWORD dwTimeout;
	EndRem

	Field cbSize:Int
	Field hWND:Int
	Field dwFlags:Int
	Field uCount:Int
	Field dwTimeout:Int

End Type



Global minutesToWait:Int = 0
Global timerID:Int = 0


'Main window.

Const width:Int = 270
Global mainWindow:TGadget = CreateWindow( "(OFF) Break Timer", 0, 0, width, 160, Null, ..
WINDOW_TITLEBAR | WINDOW_RESIZABLE | WINDOW_STATUS | WINDOW_CENTER )

	Global minutesCombo:TGadget = CreateComboBox( 20, 10, 150, 32, mainWindow )
	AddGadgetItem( minutesCombo, "20 minutes", 0, -1, "", "20" )
	AddGadgetItem( minutesCombo, "30 minutes", 0, -1, "", "30" )
	AddGadgetItem( minutesCombo, "40 minutes", GADGETITEM_DEFAULT, -1, "", "40" )
	AddGadgetItem( minutesCombo, "1 hour", 0, -1, "", "60" )
	AddGadgetItem( minutesCombo, "1 hour 30 minutes", 0, -1, "", "90" )
	AddGadgetItem( minutesCombo, "2 hours", 0, -1, "", "120" )
	
	Global startButton:TGadget = CreateButton( "Start", 20, 60, 80, 32, mainWindow )
	Local exitButton:TGadget = CreateButton( "Exit", 120, 60, 80, 32, mainWindow )


'Taskbar flash settings.

Global fInfo:FLASHINFO = New FLASHINFO
fInfo.cbSize = 4 * 5
fInfo.hWND = QueryGadget( mainWindow, QUERY_HWND )
fInfo.dwFlags = $2 | $C 'FLASHW_TRAY | FLASHW_TIMERNOFG
fInfo.uCount = 0 'Flashes forever, until the window is clicked to foreground.
fInfo.dwTimeout = 0 'Default flashing rate (milliseconds).


'See if there's any default user-minutes in the executable name.

parseCommandLine()
GCCollect()


Repeat
	WaitEvent()
	
	Select EventID()
		
		Case EVENT_APPTERMINATE
			quit()
			
		Case EVENT_WINDOWCLOSE
			If EventSource() = mainWindow Then quit()
									
		Case EVENT_GADGETACTION
		
			Select EventSource()
			
				Case exitButton
					quit()
					
				Case startButton
					toggleWinTimer()
		
			End Select
								
	End Select

Forever


Function quit()
	
	_stopWinTimer()
	GCCollect() '_stopWinTimer() already does this, but anyway.
	
	End

End Function 


Function parseCommandLine()

	'The first argument is always the path to the executable file.
	'If there's more than one argument, test if the second argument is a number.

	If AppArgs.length &gt; 1 Then
		
		Local tempMinutes:Int = Int( AppArgs[1] )
		If Chr( AppArgs[1][0] ) = "-" Then tempMinutes = Int( AppArgs[1][ 1 .. ] ) 'Also allow values preceded by "-".
		
		Const MAX_MINUTES:Int = 999999
		If tempMinutes &gt; 0 And tempMinutes &lt;= MAX_MINUTES Then
			AddGadgetItem( minutesCombo, String( tempMinutes ) + " minutes (default)", GADGETITEM_DEFAULT, -1, "", String( tempMinutes ) )
			SelectGadgetItem( minutesCombo, CountGadgetItems( minutesCombo ) - 1 )		
		EndIf
	EndIf
		
End Function


Function getMinutesToWait()
	
	minutesToWait = Int( String( GadgetItemExtra( minutesCombo, SelectedGadgetItem( minutesCombo ) ) ) )
	
End Function


Function toggleWinTimer()

	If timerID Then
		_stopWinTimer()
	Else
		_startWinTimer()
	EndIf

End Function


Function _startWinTimer()

	_stopWinTimer() 'Make sure it's off.

	getMinutesToWait() 'Get the user choice.
	
	SetGadgetText( mainWindow, "(ON) Break Timer" )
	Local statusText:String	= "Start: " + CurrentTime()
	SetStatusText( mainWindow, statusText )
	MinimizeWindow( mainWindow )
	
	SetGadgetText( startButton, "Stop" )

	timerID = SetTimer( 0, 0, minutesToWait * 60 * 1000, _processWinTimer )

End Function


Function _processWinTimer( hWND:Int, uMSG:Int, idEvent:Int, dwTime:Int )

	'The timer has ticked.
	'Windows timers created with 'SetTimer' are periodic, so we destroy it in this first tick.

	FlashWindowEx( fInfo )
	
	_stopWinTimer()

End Function


Function _stopWinTimer()
	
	SetGadgetText( mainWindow, "(OFF) Break Timer" )

	If timerID &lt;&gt; 0 Then
		Local statusText:String	= WindowStatusText( mainWindow )
		SetStatusText( mainWindow, statusText[ .. 15 ] + "~t End: " + CurrentTime() )

		SetGadgetText( startButton, "Restart" )

		KillTimer( 0, timerID )
		timerID = 0
	EndIf

	GCCollect()

End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
