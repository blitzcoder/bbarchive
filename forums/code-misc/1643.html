<!DOCTYPE html><html lang="en" ><head ><title >Simple system tray module</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='System tray module, language=bmx, category=Miscellaneous'><meta name='author' content='ozak'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=9>Miscellaneous</a>/Simple system tray module</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1643.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Simple system tray module by ozak</td><td align="right">2006 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Simple tray module that allows you to place an icon and hook it up to mouse messages. <br>Win32 only! You can call the functions on other platforms and they will simple do nothing. That way you can support multiple platforms while still having trayapp support on windows.<br>Thanks to grable for the base trayapp code :)<br><br>Here's an example of hiding app window on pressing close button and showing it when pressing the tray icon.<br><br><pre class=code>
SuperStrict

Include "trayappmodule.bmx"

'
' EXAMPLE
'
Local window:TGadget = CreateWindow( "Window", 80,80,640,480)
Local panel:TGadget = CreatePanel( 0,0, 0,0, window, PANEL_ACTIVE)

RegisterTrayIcon(panel, "Click to activate app", "beholder.ico")

Repeat
	WaitEvent()
	
	Select EventID()
		Case EVENT_MOUSEMOVE
			If EventSource() = panel Then
				Select EventX()
'					Case WM_MOUSEMOVE		Print "mouse move"
'					Case WM_LBUTTONDOWN	Print "left down"
					Case WM_LBUTTONUP		ShowGadget(window)'; Shell_NotifyIcon( NIM_DELETE, nid)
					Case WM_LBUTTONDBLCLK	ShowGadget(window)'; Shell_NotifyIcon( NIM_DELETE, nid)
'					Case WM_RBUTTONDOWN	Print "right down"
'					Case WM_RBUTTONUP		Print "right up"
'					Case WM_RBUTTONDBLCLK	Print "right doucle click"
'					Case WM_MBUTTONDOWN	Print "middle down"
'					Case WM_MBUTTONUP		Print "middle up"
'					Case WM_MBUTTONDBLCLK	Print "middle double click"
					Default
'						Print "unknown " + EventX()
				EndSelect
			EndIf
		Case EVENT_WINDOWCLOSE
			'Shell_NotifyIcon( NIM_ADD, nid)		
			HideGadget(window)
	EndSelect
Forever

' unregister tray icon
RemoveTrayIcon()

End
</pre> </td></tr><tr ><td class="cell"><pre class="code">' Tray minimize win32 application

?Win32
' tray icons 
'
Const NIM_ADD:Int			= 0
Const NIM_MODIFY:Int		= 1
Const NIM_DELETE:Int		= 2
Const NIM_SETFOCUS:Int		= 3
Const NIM_SETVERSION:Int	= 4

Const NIF_MESSAGE:Int	= $00000001
Const NIF_ICON:Int		= $00000002
Const NIF_TIP:Int		= $00000004
Const NIF_STATE:Int	= $00000008
Const NIF_INFO:Int		= $00000010
Const NIF_GUID:Int		= $00000020

Type TNotifyIconData
	Field Size:Int
	Field HWND:Int
	Field id:Int
	Field Flags:Int
	Field CallbackMessage:Int
	Field Icon:Int 				' HICON	
	Field Tip:Long				' array [0..63] of AnsiChar;
	Field Tip2:Long
	Field Tip3:Long
	Field Tip4:Long
	Field Tip5:Long
	Field Tip6:Long
	Field Tip7:Long
	Field Tip8:Long
EndType

Extern "WIN32"
	Function Shell_NotifyIcon:Int( message:Int, notifyicondata:Byte Ptr) = "Shell_NotifyIconA@8"
EndExtern

Function SetNotifyIconDataTip( nid:TNotifyIconData, s:String)
	MemClear( Varptr nid.Tip, 64)
	If s.length &gt; 0 Then
		Local p:Byte Ptr = s.ToCString()
		If s.length &lt; 64 Then
			MemCopy( Varptr nid.Tip, p, s.length)
		Else			
			MemCopy( Varptr nid.Tip, p, 63)			
		EndIf
		MemFree( p)
	EndIf
EndFunction

'
' window messages (allso used by tray icon)
'
Const WM_MOUSEMOVE:Int        = $0200
Const WM_LBUTTONDOWN:Int      = $0201
Const WM_LBUTTONUP:Int        = $0202
Const WM_LBUTTONDBLCLK:Int    = $0203
Const WM_RBUTTONDOWN:Int      = $0204
Const WM_RBUTTONUP:Int        = $0205
Const WM_RBUTTONDBLCLK:Int    = $0206
Const WM_MBUTTONDOWN:Int      = $0207
Const WM_MBUTTONUP:Int        = $0208
Const WM_MBUTTONDBLCLK:Int    = $0209

'
' icon resources
'
Const IMAGE_BITMAP:Int      = 0
Const IMAGE_ICON:Int        = 1
Const IMAGE_CURSOR:Int      = 2
Const IMAGE_ENHMETAFILE:Int = 3

Const LR_DEFAULTSIZE:Int      = 64
Const LR_DEFAULTCOLOR:Int     = 0
Const LR_MONOCHROME:Int       = 1
Const LR_COLOR:Int            = 2
Const LR_COPYRETURNORG:Int    = 4
Const LR_COPYDELETEORG:Int    = 8
Const LR_LOADFROMFILE:Int     = 16
Const LR_LOADTRANSPARENT:Int  = 32
Const LR_LOADREALSIZE:Int     = 128
Const LR_LOADMAP3DCOLORS:Int  = 4096
Const LR_CREATEDIBSECTION:Int = 8192
Const LR_COPYFROMRESOURCE:Int = $4000 ' 0x4000
Const LR_SHARED:Int      	  = 32768           
	
Global nid:TNotifyIconData

Extern "WIN32"
	Function LoadImage_:Int( Instance:Int, Name$z, Type_:Int, DesiredX:Int, DesiredY:Int, Load:Int) = "LoadImageA@24"
EndExtern

Function LoadIcon:Int( filename:String, width:Int=16,Height:Int=16, Flags:Int=LR_SHARED)
	Return LoadImage_( 0, filename, IMAGE_ICON, width,Height, LR_LOADFROMFILE| Flags)
EndFunction

?

' Register tray icon
Function RegisterTrayIcon(window:TGadget, toolTip:String, iconFile:String)

?Win32
	nid = New TNotifyIconData
	nid.Size = SizeOf(TNotifyIconData)
	nid.hwnd = QueryGadget(window, QUERY_HWND)
	nid.id = 0
	nid.CallbackMessage = WM_MOUSEMOVE
	nid.Icon = LoadIcon( iconFile)
	nid.Flags = NIF_MESSAGE | NIF_TIP | NIF_ICON
	SetNotifyIconDataTip( nid, toolTip )
	Shell_NotifyIcon( NIM_ADD, nid)
?
End Function   

'Remove tray icon
Function RemoveTrayIcon()    
?Win32
	Shell_NotifyIcon(NIM_DELETE, nid)
?
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >xlsior</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> Nice! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CASO</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> Nice!<br><br>Questions:<br>1. Is there a way to get one of those little boxes with options in it like "Open","Close","Pause"...<br>2. Is there a way to change th icon and/or tooltip in the middle of the program. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>2008</font></td></tr></table></td></tr><tr ><td class="posttext"> This code is a modification of post #1. <br>It was provided by Seb and can <b>ONLY</b> be used with MAXGUI<u>Ex</u>!<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Framework MaxGUI.Win32MaxGUIEx 
Import BRL.EventQueue
Import "../src/maxide/maxicons.o"

AppTitle = "Shell_NotifyIcon Sample Program"

Global window:TGadget = CreateWindow( AppTitle, 80, 80, 640, 480, Null, WINDOW_TITLEBAR|WINDOW_RESIZABLE|WINDOW_STATUS|WINDOW_MENU|WINDOW_CLIENTCOORDS )
Global panel:TGadget = CreatePanel( 0, 0, 0, 0, window, 0 )

Repeat
	Select WaitEvent()
		
		Case EVENT_MOUSEDOWN
			
			Select EventSource()
				Case panel
					ShowGadget(window)
					RemoveTrayIcon()
			EndSelect
		
		Case EVENT_WINDOWCLOSE
		
                 RegisterTrayIcon(panel, "Click to activate app!" )
			HideGadget(window)
			
	EndSelect
	
Forever

' unregister tray icon
RemoveTrayIcon()

End


' Register tray icon
Function RegisterTrayIcon( window:TGadget, toolTip:String )

	?Win32
	nid = New TNotifyIconData
	nid.Size = SizeOf(TNotifyIconData)
	nid.hwnd = QueryGadget(window, QUERY_HWND)
	SetWindowLongW(nid.hwnd,GWL_WNDPROC,Int Byte Ptr SysTrayWndProc)
	nid.id = 0
	nid.CallbackMessage = WM_APP
	nid.Icon = LoadIconW(GetModuleHandleW(Null),Short Ptr(101))
	nid.Flags = NIF_MESSAGE | NIF_TIP | NIF_ICON
	SetNotifyIconDataTip( nid, toolTip )
	Shell_NotifyIcon( NIM_ADD, nid)
	?
	
End Function

'WndProc NotifyIcon -&gt; Blitz Event
Function SysTrayWndProc%(hwnd%,msg%,wp%,lp%) "win32"
	?Win32
	Select msg
		Case WM_APP
			Local owner:TGadget = TWindowsGadget(TWindowsGUIDriver.GadgetMap.ValueForKey(String(hwnd)))
			If owner Then
				Select lp
					Case WM_MOUSEMOVE
						PostGuiEvent( EVENT_MOUSEMOVE, owner )
					Case WM_LBUTTONDOWN, WM_LBUTTONDBLCLK
						PostGuiEvent( EVENT_MOUSEDOWN, owner, MOUSE_LEFT )
					Case WM_LBUTTONUP
						PostGuiEvent( EVENT_MOUSEUP, owner, MOUSE_LEFT )
					Case WM_RBUTTONDOWN, WM_RBUTTONDBLCLK
						PostGuiEvent( EVENT_MOUSEDOWN, owner, MOUSE_RIGHT )
					Case WM_RBUTTONUP
						PostGuiEvent( EVENT_MOUSEDOWN, owner, MOUSE_RIGHT )
					Case WM_MBUTTONDOWN, WM_MBUTTONDBLCLK
						PostGuiEvent( EVENT_MOUSEDOWN, owner, MOUSE_MIDDLE )
					Case WM_MBUTTONUP
						PostGuiEvent( EVENT_MOUSEDOWN, owner, MOUSE_MIDDLE )
					Case WM_MOUSELEAVE
						PostGuiEvent( EVENT_MOUSELEAVE, owner )
				EndSelect
			EndIf
	EndSelect
	Return TWindowsGUIDriver.ClassWndProc(hwnd,msg,wp,lp)
	?
EndFunction

'Remove tray icon
Function RemoveTrayIcon()    
	?Win32
	Shell_NotifyIcon(NIM_DELETE, nid)
	?
End Function

Function SetNotifyIconDataTip( nid:TNotifyIconData, s:String)
	MemClear( Varptr nid.Tip, 64)
	If s.length &gt; 0 Then
		Local p:Byte Ptr = s.ToCString()
		If s.length &lt; 64 Then
			MemCopy( Varptr nid.Tip, p, s.length)
		Else			
			MemCopy( Varptr nid.Tip, p, 63)			
		EndIf
		MemFree( p)
	EndIf
EndFunction



?Win32
' tray icons 
'
Const NIM_ADD:Int			= 0
Const NIM_MODIFY:Int		= 1
Const NIM_DELETE:Int		= 2
Const NIM_SETFOCUS:Int		= 3
Const NIM_SETVERSION:Int	= 4

Const NIF_MESSAGE:Int		= $1
Const NIF_ICON:Int		= $2
Const NIF_TIP:Int			= $4
Const NIF_STATE:Int		= $8
Const NIF_INFO:Int		= $10
Const NIF_GUID:Int		= $20

Global nid:TNotifyIconData

Type TNotifyIconData
	Field Size:Int
	Field HWND:Int
	Field id:Int
	Field Flags:Int
	Field CallbackMessage:Int
	Field Icon:Int 				' HICON	
	Field Tip:Long				' array [0..63] of AnsiChar;
	Field Tip2:Long
	Field Tip3:Long
	Field Tip4:Long
	Field Tip5:Long
	Field Tip6:Long
	Field Tip7:Long
	Field Tip8:Long
EndType

Extern "win32"
	Function Shell_NotifyIcon:Int( message:Int, notifyicondata:Byte Ptr) = "Shell_NotifyIconA@8"
EndExtern
?
</textarea><br><br>Reference: <a href="http://www.blitzbasic.com/Community/posts.php?topic=82310" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=82310</a> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tani</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> I made this version of the code, which is updated to work with version 1.36 of MaxGUI (thanks to SebHoll for the fix).<br><br>It also gives an example of how to add a popup menu to the system tray and other useful details. :3<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

'__/ Framework \_______________________________________________________________________________________________________________________________________________________
'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
Import MaxGUI.Win32MaxGUIEx			'Required for the System Tray feature and using the MaxGUI (use instead of MaxGUI.Drivers)
Import BRL.EventQueue				'Required for the MaxGUI event handling
'Import Pub.Win32				'Supposedly required for the System Tray feature (seems to work fine without importing it though)

'__/ Includes \__________________________________________________________________________________________________________________________________________________________
'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
Extern "WIN32"					'Adds program feature "hacks"
	Function Shell_NotifyIcon:Int(message:Int,notifyicondata:Byte Ptr)="Shell_NotifyIconA@8"
	Function LoadImage_:Int(Instance:Int,Name$z,Type_:Int,DesiredX:Int,DesiredY:Int,Load:Int)="LoadImageA@24"
EndExtern

'__/ Constants \_________________________________________________________________________________________________________________________________________________________
'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
'**[ System Tray ]******************************************************************************************************************************************************
Const NIM_ADD:Int			= 0
Const NIM_MODIFY:Int			= 1
Const NIM_DELETE:Int			= 2
Const NIM_SETFOCUS:Int			= 3
Const NIM_SETVERSION:Int		= 4

Const NIF_MESSAGE:Int			= $1
Const NIF_ICON:Int			= $2
Const NIF_TIP:Int			= $4
Const NIF_STATE:Int			= $8
Const NIF_INFO:Int			= $10
Const NIF_GUID:Int			= $20

Const LR_LOADFROMFILE:Int		= 16
Const LR_SHARED:Int			= 32768

'**[ Menu Options ]***************************************************************************************************************************************************
Const MENU_END:Byte			=101	'Exit Option
Const MENU_RES:Byte			=102	'Restore Option

'__/ Object Declaration \________________________________________________________________________________________________________________________________________________
'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
Type SysTrayIcon
	Field Size:Int
	Field HWND:Int
	Field ID:Int
	Field Flags:Int
	Field CallbackMessage:Int
	Field Icon:Int 				'HICON	
	Field Tip:Long				'Array [0..63] of AnsiChar;
	Field Tip2:Long
	Field Tip3:Long
	Field Tip4:Long
	Field Tip5:Long
	Field Tip6:Long
	Field Tip7:Long
	Field Tip8:Long
EndType

'__/ Program Initialization \_____________________________________________________________________________________________________________________________________________
'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
'**[ Globals ]**********************************************************************************************************************************************************
Global tNID:SysTrayIcon

'__/ Setup Gadgets \____________________________________________________________________________________________________________________________________________________
'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
'**[ Main Window ]***************************************************************************************************************************************************
Local winMain:TGadget=CreateWindow(AppTitle,False,False,800,600)

'**[ Panels ]***********************************************************************************************************************************************************
'System Tray link object
Global panSysTray:TGadget=CreatePanel(0,0,0,0,winMain)

'__/ Setup Menu \______________________________________________________________________________________________________________________________________________________
'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
'**[ Popup Menus ]***************************************************************************************************************************************************
Local muPop:TGadget=CreateMenu("popup",0,Null)
CreateMenu("Restore",MENU_RES,muPop)
CreateMenu("",False,muPop)
CreateMenu("Exit",MENU_END,muPop)

'__/ Event Handling \___________________________________________________________________________________________________________________________________________________
'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
Repeat
	Select WaitEvent()
	Case EVENT_MENUACTION
		Select EventData()
		Case MENU_END
			Exit
		Case MENU_RES
			RestoreApp(winMain)
		EndSelect
	Case EVENT_MOUSEDOWN
		Select EventSource()
		Case panSysTray			'Double left click restores app, single right click shows popup menu
			If EventData()=WM_LBUTTONDBLCLK
				RestoreApp(winMain)
			ElseIf EventData()=WM_RBUTTONDOWN
				PopupWindowMenu(winMain,muPop)
		EndIf;EndSelect
	Case EVENT_WINDOWCLOSE			'Minimizes program to system tray
		RegisterTrayIcon(panSysTray,AppTitle)
		HideGadget(winMain)
EndSelect;Forever

'__/ Program Cleanup \_________________________________________________________________________________________________________________________________________________
'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
FreeGadget(muPop)				'Removes popup gadget
FreeGadget(panSysTray)				'Removes system tray link panel
FreeGadget(winMain)				'Removes main window
RemoveTrayIcon()				'In case the program is still running in the system tray

End

'__/ Functions \_________________________________________________________________________________________________________________________________________________________
'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
'**[ System Tray Icon ]*************************************************************************************************************************************************
' Register tray icon
Function RegisterTrayIcon(gRef:TGadget,sTTip:String,sIcon:String="")
	?Win32
	tNID=New SysTrayIcon
	tNID.Size=SizeOf(SysTrayIcon)
	tNID.HWND=QueryGadget(gRef,QUERY_HWND)
	SetWindowLongW(tNID.HWND,GWL_WNDPROC,Int Byte Ptr SysTrayEvent)
	tNID.ID=0
	tNID.CallbackMessage=WM_APP
	tNID.Icon=LoadIcon(sIcon)
	tNID.Flags=NIF_MESSAGE|NIF_TIP|NIF_ICON
	SetSysTrayTTip(tNID,sTTip)
	Shell_NotifyIcon(NIM_ADD,tNID)
	?
EndFunction

'Removes tray icon
Function RemoveTrayIcon()
	?Win32
	Shell_NotifyIcon(NIM_DELETE,tNID)
	?
EndFunction

'Restores the program from the system tray
Function RestoreApp(gRef:TGadget)
	ShowGadget(gRef)
	RemoveTrayIcon()
EndFunction

'Sets the icon to be displayed in the system tray
Function LoadIcon:Int( filename:String, width:Int=16,Height:Int=16,Flags:Int=LR_SHARED)
	Return LoadImage_( 0,filename,IMAGE_ICON,Width,Height,LR_LOADFROMFILE|Flags)
EndFunction

'Sets the tooltip text when hovering over the system tray icon
Function SetSysTrayTTip(objRef:SysTrayIcon,sTTip:String="")
	MemClear( Varptr objRef.Tip,64)
	If sTTip.length&gt;0 Then
		Local bNum:Byte Ptr=sTTip.ToCString()
		If sTTip.length&lt;64 Then
			MemCopy(Varptr objRef.Tip,bNum,sTTip.length)
		Else			
			MemCopy(Varptr objRef.Tip,bNum,63)			
		EndIf
		MemFree(bNum)
EndIf;EndFunction

'Handles the system tray events
Function SysTrayEvent:Int(iHWND:Int,iMSG:Int,iWP:Int,iLP:Int)
	?Win32
	Select iMSG
	Case WM_APP
		Local gItem:TGadget=TWindowsGUIDriver.GadgetFromHwnd(iHWND)
		If gItem Then
			Select iLP
			Case WM_MOUSEMOVE					'Mouse is being moved over tray icon area
				PostGuiEvent(EVENT_MOUSEMOVE,gItem)
			Case WM_LBUTTONDOWN,WM_RBUTTONDOWN,WM_MBUTTONDOWN	'Mouse button is held down
				PostGuiEvent(EVENT_MOUSEDOWN,gItem,iLP)
			Case WM_LBUTTONDBLCLK,WM_RBUTTONDBLCLK,WM_MBUTTONDBLCLK	'Mouse button is doubleclicked
				PostGuiEvent(EVENT_MOUSEDOWN,gItem,iLP)
			Case WM_LBUTTONUP,WM_RBUTTONUP,WM_MBUTTONUP		'Mouse button is released
				PostGuiEvent(EVENT_MOUSEUP,gItem,iLP)
			Case WM_MOUSELEAVE					'Mouse leaves tray icon area
				PostGuiEvent(EVENT_MOUSELEAVE,gItem)
	EndSelect;EndIf;EndSelect
	Return TWindowsGUIDriver.ClassWndProc(iHWND,iMSG,iWP,iLP)
	?
EndFunction</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> The last example doesn't do anything, and the one before it won't compile.<br><br>The first example doesn't do anything when the system tray icon is clicked. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tani</td><td align="right"><font class=tiny>2011</font></td></tr></table></td></tr><tr ><td class="posttext"> It does work for me still, so no updates should have broken it since last time... o.O Though I didn't add any icon to the example if that might cause confusion - so it ends up as an empty box in the system tray that can be right clicked for a pop-up menu and double-clicked to be restored. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
