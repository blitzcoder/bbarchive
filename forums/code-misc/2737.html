<!DOCTYPE html><html lang="en" ><head ><title >Vector2 Lib</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='A 2D Vector Library, language=bb, category=Miscellaneous'><meta name='author' content='Sauer'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=9>Miscellaneous</a>/Vector2 Lib</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2737.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Vector2 Lib by Sauer</td><td align="right">2010 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This lib does various operations on 2D vectors, which have hundreds of applications in game programming.<br><br>If you have any suggestions on functionality to improve the lib, please comment and let me know. </td></tr><tr ><td class="cell"><pre class="code">;----------------------------
;  Vector2 Library
;----------------------------

Type Vector2
	Field x#,y#
End Type

Function init.Vector2(x,y)
	;easy way to init a new vector
	tmp.Vector2=New Vector2
	tmp\x=x
	tmp\y=y
	Return tmp
End Function

Function copy.Vector2(v.Vector2)
	;makes a unique instance copying another vector
	tmp.Vector2=New Vector2
	tmp\x=v\x
	tmp\y=v\y
	Return tmp
End Function 

Function dot#(v.Vector2,w.Vector2)
	;returns the dot product of two vectors, returned value is a scalar
	Return (v\x#*w\x#)+(v\y#*w\y#)
End Function

Function length#(v.Vector2)
	;returns the length (magnitude) of a vector
	Return Sqr(dot(v.Vector2,v.Vector2))
End Function 

Function angle_between#(v.Vector2,w.Vector2)
	;finds the angle, in degrees, between two vectors
	Return ACos(dot(normalize(v),normalize(w)))
End Function 

Function rotate_vector2.Vector2(v.Vector2,deg#)
	;counter clockwise rotation, in degrees
	tmp.Vector2=New Vector2
	tmp\x#=(Cos#(deg)*v\x#)-(Sin#(deg)*v\y#)
	tmp\y#=(Sin#(deg)*v\x#)+(Cos#(deg)*v\y#)
	Return tmp
End Function 

Function cross.Vector2(v.Vector2)
	;returns a vector perpendicular to v
	tmp.Vector2=New Vector2
	tmp\x=v\y*1
	tmp\y=-v\x*1
	Return tmp
End Function 

Function normalize.Vector2(v.Vector2)
	;creates a vector with length one in the same direction as original
	tmp.Vector2=New Vector2
	leng#=length#(v)
	tmp\x#=v\x#/leng#
	tmp\y#=v\y#/leng#
	Return tmp
End Function 

Function mul.Vector2(v.Vector2,mag#)
	;multiply a vector by a scalar
	tmp.Vector2=New Vector2
	tmp\x#=v\x#*mag#
	tmp\y#=v\y#*mag#
	Return tmp
End Function  

Function add.Vector2(v.Vector2,w.Vector2)
	;adds two vectors
	tmp.Vector2=New Vector2
	tmp\x#=v\x#+w\x#
	tmp\y#=v\y#+w\y#
	Return tmp
End Function

Function sub.Vector2(v.Vector2,w.Vector2)
	;subtracts two vectors
	tmp.Vector2=New Vector2
	tmp\x#=v\x#-w\x#
	tmp\y#=v\y#-w\y#
	Return tmp 
End Function  

Function ref.Vector2(v.Vector2,w.Vector2,through=False)
	;a reflection vector, v being direction of object and w being surface.  Through means through wall or bounce off wall (default)
	If through=False
		n.Vector2=copy(w)
	Else
		n.Vector2=cross(w)
	EndIf 
	l.Vector2=normalize(v)
	n=normalize(n)
	q.Vector2=mul(n,dot(l,n))
	r.Vector2=sub(mul(q.Vector2,2),l)
	r=normalize(r)
	Return r
End Function 

Function random.Vector2()
	;returns a vector pointing in a random direction
	tmp.Vector2=init(Rnd(-10,10),Rnd(10,10))
	tmp=normalize(tmp)
	Return tmp
End Function 

Function print_vector2(v.Vector2,name$="-")
	;prints a vector with an optional name
	Print "Vector2 '"+name$+"' X: "+v\x#+" Y: "+v\y#
End Function 

Function draw_vector2(v.Vector2,w.Vector2)
	;draws a line representing a vector, taking a vector v as an origin and w as direction/magnitude
	Line v\x#,v\y#,v\x#+w\x#,v\y#+w\y#
End Function 
	
Function circle_circle(v.Vector2,w.Vector2,dist#)
	;simple circle to circle collision using vectors
	If dist(v,w)&lt;dist#*dist#
		Return True
	Else
		Return False
	EndIf
End Function 

Function dist(v.Vector2,w.Vector2)
	;returns the distance squared between two vectors
	Return (v\x-w\x)*(v\x-w\x)+(v\y-w\y)*(v\y-w\y)
End Function
	

Function circle_vector2(v.Vector2,w.Vector2,w1.Vector2,dist#)
	;determines if circle (with center v and radius dist) and vector w are colliding, where w is direction/magnitude and w1 is origin
	Local v1.Vector2=sub(v,w1)
	Local tmp.Vector2=normalize(w)
	Local proj.Vector2=mul(tmp,dot(tmp,v1)) 
	Local a=circle_circle(v1,proj,dist#)
	Local leng#=length#(w)+dist#
	leng#=(leng#*leng#)
	Return a And dist(proj,init(0,0))&lt;=leng#+dist# And dist(proj,w)&lt;=leng#+dist#
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sauer</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> Here are some applications of vectors:<br><br>Bubble Shooter:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
AppTitle "Bubble Shooter"
Graphics 640,480,32,2

Include "vector2.bb"

Type bullet
	Field pos.Vector2
	Field dir.Vector2
End Type

Type enemy
	Field pos.Vector2
	Field dir.Vector2
	Field level
End Type

Global turret_aim.Vector2=init(0,-20)
Global turret_base.Vector2=init(320,480)

Global x_.Vector2=init(1,0)
Global y_.Vector2=init(0,1)

Global ticker=100
Global score=0

Global e.enemy

Global t=CreateTimer(60)

SetBuffer BackBuffer()
While Not KeyHit(1)
	WaitTimer(t)
	Cls
	updateTurret()
	updateBullet()
	updateEnemy()
	updateLevel()
	HUD()
	info()
	Flip
Wend
End 

Function info()
	;print_vector2(add(turret_base,turret_aim))
	;Text 0,0,ticker
End Function

Function HUD()
	Color 128,128,128
	Rect 0,0,640,20,1
	Color 255,255,25
	Text 10,0,"Score: "+score
End Function  


Function updateTurret()
	Color 0,0,255
	Oval turret_base\x-10,turret_base\y-10,20,20,0
	draw_vector2(turret_base,turret_aim)
	
	If KeyDown(203) And angle_between(x_,turret_aim)&lt;170
		turret_aim=rotate_vector2(turret_aim,-2)
	EndIf
	If KeyDown(205) And angle_between(x_,turret_aim)&gt;10
		turret_aim=rotate_vector2(turret_aim,2)
	EndIf
	If KeyHit(57)
		b.bullet=New bullet
		b\pos=add(turret_base,turret_aim)
		b\dir=copy(turret_aim)
	EndIf 
End Function

Function updateBullet()
	Color 255,0,0
	For b.bullet=Each bullet
		Oval b\pos\x-4,b\pos\y-2,4,4,0
		b\pos=add(b\pos,b\dir)
		If b\pos\x&lt;=0 Or b\pos\x&gt;=640
			b\dir\x=b\dir\x*-1
		EndIf
		If b\pos\y&lt;=20
			Delete b
		EndIf 
		If b&lt;&gt; Null
			For e.enemy=Each enemy
				If e&lt;&gt;Null And b&lt;&gt;Null 
					If circle_circle(e\pos,b\pos,e\level*8+2)
						If e\level&gt;1
							For xx =1 To e\level
								e1.enemy=New enemy
								e1\pos=copy(e\pos)
								Repeat
									e1\dir=init(Rnd(-1,1),Rnd(-1,1))
								Until(e1\dir\x&lt;&gt;0 And e1\dir\y&lt;&gt;0)
								e1\level=e\level-1
							Next
						EndIf
						score=score+4-e\level 
						Delete e
						Delete b
					EndIf
				EndIf 
			Next
		EndIf 
	Next
End Function

Function updateEnemy()
	Color 0,255,0
	For e.enemy=Each enemy
		Oval e\pos\x-e\level*8,e\pos\y-e\level*8,e\level*16,e\level*16,0
		e\dir=rotate_vector2(e\dir,Rand(-e\level,e\level))
		e\pos=add(e\pos,mul(e\dir,4-e\level))
		If e\pos\x&lt;=e\level*8 Or e\pos\x&gt;=640-e\level*8
			e\dir\x=e\dir\x*-1
		EndIf
		If e\pos\y&lt;=e\level*8
			e\dir\y=e\dir\y*-1
		EndIf  
		If e\pos\y&gt;=480-e\level*8
			Delete e
		EndIf
	Next
End Function 

Function createEnemy()
	e.enemy=New enemy
	e\pos=init(Rand(100,540),25)
	e\dir=init(0,1)
	e\level=3
End Function 

Function updateLevel()
	ticker=ticker-1
	If ticker&lt;=0
		createEnemy()
		ticker=100
	EndIf
End Function</textarea><br><br>Mini Golf:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
AppTitle "Mini Golf"
Graphics 640,480,32,2

Include "vector2.bb"

Type wall
	Field base.Vector2
	Field dir.Vector2
End Type

Type ball
	Field pos.Vector2
	Field dir.Vector2
	Field speed#
End Type


Global w.wall
Global b.ball=New ball
b\pos=init(0,0)
b\dir=init(0,0)

Global mouse.Vector2=init(0,0)
Global h.Vector2=init(0,0)
Global pstart.Vector2=init(-10,-10)
Global power.Vector2=init(0,0)

Global strokes=0
Global lowstrokes=10

loadHole(1)
SetBuffer BackBuffer()
ClsColor 0,100,0
While Not KeyHit(1)
	Cls
	HUD()
	updateMouse()
	updateBall()
	drawHole()
	Flip
Wend 

Function HUD()
	Color 255,255,0
	Text 10,0,"Strokes: "+strokes
	Text 10,12,"Low Strokes: "+lowstrokes
End Function 

Function updateMouse()
	mouse\x=MouseX()
	mouse\y=MouseY()
	
	If MouseDown(2)
		loadHole(1)
	EndIf 
	
	If MouseDown(1)
		If pstart\x=-10 And pstart\y=-10 And circle_circle(mouse,b\pos,9)
			pstart=copy(mouse)
		EndIf 
		power=sub(pstart,mouse)
		Color 255,0,0
		draw_vector2(pstart,power)
		Text pstart\x+10,pstart\y+10,Int(length(power)/2)
	EndIf 
	If Not MouseDown(1)
		If pstart\x&lt;&gt;-10 And pstart\y&lt;&gt;-10 And length(power)&gt;0
			pstart\x=-10
			pstart\y=-10
			b\dir=normalize(power)
			b\speed=length(power)/2
			strokes=strokes+1
		EndIf
	EndIf 
	Color 255,0,0
	Oval mouse\x-2,mouse\y-2,4,4,1 
End Function

Function updateBall()
	Color 255,255,255
	Oval b\pos\x-4,b\pos\y-4,8,8,1
	If b\speed&gt;20
		b\speed=20
	EndIf 
	b\pos=add(b\pos,mul(b\dir,b\speed))
	b\speed=b\speed-.5
	If b\speed&lt;=0
		b\speed=0
	EndIf
	
	For w.wall=Each wall
		If circle_vector2(b\pos,w\dir,w\base,10)
			;b\speed=0
			b\dir=ref(b\dir,w\dir)
		EndIf
	Next
	
	If circle_circle(b\pos,h,18)
		If b\speed&gt;10
			b\dir=random()
		Else
			If strokes&lt;lowstrokes
				lowstrokes=strokes
			EndIf
			strokes=0
			loadHole(1)
		EndIf
	EndIf 
			
	 
End Function 
	

Function drawHole()
	Color 0,255,0
	For w.wall=Each wall
		draw_vector2(w\base,w\dir)
	Next
	Color 0,0,0
	Oval h\x-5,h\y-5,10,10,1
End Function 

Function loadHole(hole)
	Delete Each wall 
	Select hole
		Case 1
			Restore hole1
	End Select
	Read x,y
	b\pos\x=x
	b\pos\y=y
	b\dir\x=0
	b\dir\y=0
	Read x,y
	h\x=x
	h\y=y
	Read numwalls
	For x=1 To numwalls
		Read a,bb,c,d
		w.wall=New wall
		w\base=init(a,bb)
		w\dir=init(c,d)
	Next
End Function 

.hole1
Data 120,380 ;ball coords
Data 380,120 ;hole coords
Data 5;number of walls
Data 100,100 ;wall origin
Data 300,0   ;wall dir

Data 400,100
Data 0,300

Data 400,400
Data -300,0

Data 100,400
Data 0,-300

Data 200,200
Data 100,100</textarea> <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
