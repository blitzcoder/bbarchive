<!DOCTYPE html><html lang="en" ><head ><title >Game state engine</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Makes it easier to split your game into different states, language=bmx, category=Miscellaneous'><meta name='author' content='deps'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=9>Miscellaneous</a>/Game state engine</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1468.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Game state engine by deps</td><td align="right">2005 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> You can use this code to split the game into different states.<br>Extend GenericState into things like "StatePlay", "StateMenu", "StateGamePaused", etc and just call "GameState.Switch( new StatePlay )" when you want to jump from one state to another.<br><br>Here is a quick example:<br><pre class=code>
Strict 

' Include the goodies
Include "gamestate.bmx"


Graphics 800,600,0

' Switch to the menu state
GameState.Switch( New StateMenu )

' Go!
GameState.Run()

End ' We're done now.


'------------------------------------------------------------
'-------------------- Our extended states
'------------------------------------------------------------

' Create a new kind of state
Type StateMenu Extends GenericState
	Method startup()
		Print "Loading menu..."
	EndMethod
	
	Method update()
	
		If KeyHit( KEY_1 ) Then 
			GameState.switch( New StatePlay ) ' Player wants to play a game
		EndIf
		
		If KeyHit( KEY_2 ) Then GameState.ExitState() ' Exit this state.
	
	EndMethod
		
	Method draw()
		Cls
		
		DrawText "Game menu 2000 ultra power", 50,50
		
		DrawText "1.  Play the game", 50,80
		DrawText "2.  Quit", 50,100
		
		Flip ; FlushMem
	EndMethod
		
	Method shutdown()
		Print "Removing menu stuff..."
	EndMethod

	Method on_hold()
		Print "Menu is paused while another state is running."
	EndMethod
	
	Method resume()
		Print "The menu is back in bussiness!"
	EndMethod	
EndType


Type StatePlay Extends GenericState

	Field x,y, xv,yv
	Field paused_time

	Method startup()
		Print "Loading game..."
		
		x = Rand(0,800)
		y = Rand(0,600)
		xv = Rand(0,1)
		yv = Rand(0,1)
		
	EndMethod
	
	Method update()
	
		If KeyHit( KEY_ESCAPE ) Then GameState.ExitState() ' Player doesn't want to play anymore...
		If KeyHit( KEY_P ) Then GameState.Switch( New StatePause ) ' Pause the game
	
		If xv Then
			x:+1
		Else
			x:-1
		EndIf
		
		If yv Then
			y:+1
		Else
			y:-1
		EndIf
		
		If x &lt; 0 Or x &gt; 800 Then xv = Not xv
		If y &lt; 0 Or y &gt; 600 Then yv = Not yv
	
	EndMethod
		
	Method draw()
		Cls
		
		DrawRect( x-5,y-5,11,11 )
		
		DrawText "Press Escape to quit, P to pause the game", 0,0
		
		Flip ; FlushMem
	EndMethod
		
	Method shutdown()
		Print "Game is no more..."
	EndMethod

	Method on_hold()
		Print "Game paused while another state is running."
		' Just for fun, save the millisecs()
		paused_time = MilliSecs()
	EndMethod
	
	Method resume()
		Print "Game resumed"
		' For how long was we on hold?
		Print "Your little intermission lasted for "+(MilliSecs()-paused_time)+" milliseconds."
	EndMethod	
EndType



Type StatePause Extends GenericState
	Method startup()
		Print "Game paused."
		
		SetBlend ALPHABLEND
		SetColor 0,0,0
		SetAlpha 0.5
		DrawRect( 0,0, 800,600 )
		SetAlpha 1
		SetColor 255,255,255
		
		' Draw the paused message here, only need to draw it once.
		DrawText "Tea time, eh? Press P to resume.", 100,100
		Flip ; FlushMem

		FlushKeys()
		
	EndMethod
	
	Method update()
		' Just wait for the right key to be pressed
	
		If KeyHit( KEY_P ) Then GameState.ExitState() ' Exit the pause state and go back to the play state
	
	EndMethod

				
	Method draw()
		' No need to draw anything. It was done during startup
	EndMethod
		
	Method shutdown()
		Print "Unpaused."
	EndMethod

	Method on_hold()
		Print "Pause state paused? Funny that..."
		' Not that crazy after all. Many games have a game menu that can be reached during a paused game
	EndMethod
	
	Method resume()
		Print "The pause state is resumed."
	EndMethod	
EndType
</pre><br><br>And here you have gamestate.bmx: </td></tr><tr ><td class="cell"><pre class="code">' make a new type that extends this one. Overload the methods.
Type GenericState

	' Overload this one and put startup code here (loading images, sounds, etc)
	Method startup()
	EndMethod
	
	
	' No drawing here! Only user input and other behind-the-scene stuff
	Method update()
	EndMethod
		
	
	' Draw things in this one. But no user input or logic code!
	Method draw()
	EndMethod
		
	
	' Save highscore/nullify variables/scream like a banshee
	Method shutdown()
	EndMethod


	' Called when the engine switches to a new state.
	Method on_hold()
	EndMethod
	
	
	' Called when the engine is done with the other state and switches back t this one
	Method resume()
	EndMethod


EndType



Type GameState

	Global state_list:TList
	Global current_state:GenericState

	Function Switch( new_state:GenericState )
		If Not state_list Then state_list = CreateList()
		
		state_list.addlast( new_state )
		If current_state Then current_state.on_hold()
		new_state.startup()
		current_state = new_state

	EndFunction
	
	' Called by the running state when it doesn't want to play anymore
	Function ExitState()
	
		If Not state_list Then RuntimeError("ExitState should only be called by running states!")
	
		current_state.shutdown()
		
		state_list.remove( current_state )
		
		If state_list.Count() &gt; 0 Then
			current_state = GenericState( state_list.last() )
		
			current_state.resume()		
		EndIf
	
	EndFunction
	
	
	Function Run()
	
		If Not state_list Then RuntimeError("Switch to a new game state before calling GameState.Run()")
	
		While state_list.Count() &gt; 0
	
			current_state.update()
			current_state.draw()
	
		Wend
	
	EndFunction
	
	

EndType</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >deps</td><td align="right"><font class=tiny>2005</font></td></tr></table></td></tr><tr ><td class="posttext"> The logic and drawing code is separated in the states so that you could implement frame skipping in GameState.Run().<br><br>If you want to use delta time then just do it in the update() method of your states.<br><br>Hope you will find this useful. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >deps</td><td align="right"><font class=tiny>2005</font></td></tr></table></td></tr><tr ><td class="posttext"> Oops, double post. deleted. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
