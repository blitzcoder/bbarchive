<!DOCTYPE html><html lang="en" ><head ><title >Nasty Tetris</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='this tetris always give you most unconvenient figure, try to beat it and fail miserably, language=bmx, category=Miscellaneous'><meta name='author' content='Matt Merkulov'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=9>Miscellaneous</a>/Nasty Tetris</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2969.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nasty Tetris by Matt Merkulov</td><td align="right">2012 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> try to beat it and fail miserably!<br><br>controls:<br>arrows - move figure<br>q - rotate figure right<br>w - rotate figure right </td></tr><tr ><td class="cell"><pre class="code">SuperStrict

Framework brl.glmax2d
Import brl.random

SeedRnd Millisecs()

Const SquareSize:Int = 24

Global World:Int[ 21, 23 ]
Global TempWorld:Int[ 21, 23 ]
Global Figure:Int[ 7, 4, 4, 4 ]
Global Phases:Int[ 7 ]
Global XShift:Int[][] = [ [ 0 ], [ 0, 0 ], [ 0, 0 ], [ 1, 0 ], [ 0, 1, 0, 0 ], [ 0, 1, 0, 0 ], [ 0, 1, 0, 0 ] ]
Global YShift:Int[][] = [ [ 2 ], [ 2, 2 ], [ 3, 1 ], [ 4, 2 ], [ 2, 3, 2, 3 ], [ 2, 3, 3, 1 ], [ 2, 3, 2, 2 ] ]

Local M:Int = 0
For Local S:String = Eachin "ABEF,AEFJEFBC,IEFBABFG,BFJNEFGH,AEFGCBFJEFGKIJFB,EFGCBFJKIEFGABFJ,EFBGBFGJEFGJBEFJ".Split(",")
	Phases[ M ] = S.Length / 4 - 1
	For Local I:Int = 0 To Phases[ M ]
		For Local J:Int = 0 To 3
			Local A:Int = S[ I * 4 + J ] - 65
			Figure[ M, I, A &amp; 3, A Shr 2 ] = True
		Next
	Next
	M :+ 1
Next

Graphics SquareSize * 14, SquareSize * 21 + 16

For Local I:Int = 0 To 20
	World[ 1, I ] = True
	World[ 12, I ] = True
	World[ I, 20 ] = True
Next

Local X:Int = 0, Y:Int = 19, R:Int = 0, T:Int = 0, N:Int = 0, Score:Int = 0

Repeat
	Cls
	For Local I:Int = 0 To 20
		For Local J:Int = 1 To 12
			If World[ J, I ] Then DrawRect J * SquareSize, I * SquareSize, SquareSize, SquareSize
		Next
	Next
	
	For Local I:Int = 0 To 3 
		For Local J:Int = 0 To 3
			If Figure[ N, R, I, J ] Then DrawRect ( X + I ) * SquareSize, ( Y + J ) * SquareSize, SquareSize, SquareSize
		Next
	Next
	
	DrawText "Score: " + Score, SquareSize * 7 - 4 * ( 7 + String( Score ).Length ), SquareSize * 21
	
	Flip
	
	Local A:Int = X + KeyHit( KEY_RIGHT ) - KeyHit( KEY_LEFT )
	Local B:Int = Y + KeyDown( KEY_DOWN )
	Local Q:Int = ( R + KeyHit( KEY_W ) - KeyHit( KEY_Q ) ) &amp; Phases[ N ]
	
	IF Not CheckPosition( A, B, N, Q ) Then
		X = A
		Y = B
		R = Q
	End If
	
	Local M:Int = Millisecs()
	
	If M &gt; T Then
		T = M + 999
		
		If CheckPosition( X, Y + 1, N, R ) Then
			If Y = 0 Then End
			
			For Local I:Int = 0 To 3
				For Local J:Int = 0 To 3
					If Figure[ N, R, I, J ] Then World[ X + I, Y + J ] = True
				Next
			Next
			
			Local Counter:Int = 0
			Local D:Int = 0
			For Local J:Int = 19 To 0 Step -1
				Local Quantity:Int = 0
				For Local I:Int = 2 To 11
					If World[ I, J ] Then Quantity :+ 1
					World[ I, J + D ] = World[ I, J ]
				Next
				If Quantity &gt; 9 Then
					D :+ 1
					Counter :+ 1
					Score :+ Counter
				End If
			Next
			
			X = 5
			Y =  - 1
			N = Decide()
		EndIf
		
		Y :+ 1
	End If
Until KeyHit( KEY_ESCAPE )

Function CheckPosition:Int( X:Int, Y:Int, N:Int, R:Int )
	For Local I:Int = 0 To 3
		For Local J:Int = 0 To 3
			If Figure[ N, R, I, J ] * World[ I + X, J + Y ] Then Return True
		Next
	Next
EndFunction

Function Decide:Int()
	Local CurrentScore:Int = 0
	Local CurrentN:Int = 0
	For Local N:Int = 0 To 6
		Local Score:Int = 999
		For Local R:Int = 0 To Phases[ N ]
			For Local X:Int = 2 To 11
				For Local Y:Int = 4 To 19
					If Not World[ X, Y ] And World[ X, Y + 1 ] Then
						Local XX:Int = X - XShift[ N ][ R ]
						Local YY:Int = Y + 1 - YShift[ N ][ R ]
						If CheckPosition( XX, YY, N, R ) Then Continue
						Score = Min( Score, Calculate( XX, YY, N, R ) )
					End If
				Next
			Next
		Next
		
		If Score &gt;= CurrentScore Then
			If Score = CurrentScore Then If Rand( 0, 1 ) Then Continue
			CurrentScore = Score
			CurrentN = N
		End If
	Next
	
	Return CurrentN
End Function



Function Calculate:Int( X:Int, Y:Int, N:Int, R:Int )
	For Local X:Int = 2 To 11
		For Local Y:Int = 0 To 20
			TempWorld[ X, Y ] = World[ X, Y ]
		Next
	Next
	
	For Local I:Int = 0 To 3
		For Local J:Int = 0 To 3
			If Figure[ N, R, I, J ] Then TempWorld[ X + I, Y + J ] = True
		Next
	Next

	Local Score:Int = 0
	
	Local CurrentHeight:Int
	For Local I:Int = 2 To 11
		For Local J:Int = 0 To 20
			If TempWorld[ I, J ] Then
				If I &gt; 2 Then Score :+ Abs( J - 1 - CurrentHeight )
				CurrentHeight = J - 1
				Exit
			End If
		Next
		
		For Local J:Int = 1 To 19
			If Not TempWorld[ I, J ] And TempWorld[ I, J - 1 ] Then Score :+ 16
		Next
	Next
	
	Return Score
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>2012</font></td></tr></table></td></tr><tr ><td class="posttext"> I hate it. :). <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt Merkulov</td><td align="right"><font class=tiny>2012</font></td></tr></table></td></tr><tr ><td class="posttext"> tell how much score you gained <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>2012</font></td></tr></table></td></tr><tr ><td class="posttext"> Id didn't even look at the score.  I think I got 3 rows through out the whole game. <br><br>one thing though, and I forgot to mention it the the first time, I had to modifie the window size. I don't think it's what it's supposed to be. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>2012</font></td></tr></table></td></tr><tr ><td class="posttext"> Argh! 13 was my highest score, not helped by the fact it doesn't allow sliding under.<br><br>Tidy little implementation though. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt Merkulov</td><td align="right"><font class=tiny>2012</font></td></tr></table></td></tr><tr ><td class="posttext"> it allows, you just have to drop down figure to the place as closer as possible then wait the right moment to shift it <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >xlsior</td><td align="right"><font class=tiny>2012</font></td></tr></table></td></tr><tr ><td class="posttext"> Got to 19, twice. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt Merkulov</td><td align="right"><font class=tiny>2012</font></td></tr></table></td></tr><tr ><td class="posttext"> congratulations, it's quite good result! <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
