<!DOCTYPE html><html lang="en" ><head ><title >MAXGUI - ScrollArea</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='A Fake ScrollArea TGadget, language=bmx, category=Miscellaneous'><meta name='author' content='REDi'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=9>Miscellaneous</a>/MAXGUI - ScrollArea</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1610.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >MAXGUI - ScrollArea by REDi</td><td align="right">2006 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Here is a lil faked ScrollArea TGadget for MaxGUI. Use it like any other MaxGUI gadget.<br><br>WARNING: Don't use the ScrollArea TGadget as a parent for the Gadgets you want to add to the ScrollArea, You must call GetScrollAreaClient() to retrieve the gadet to use as the parent. </td></tr><tr ><td class="cell"><pre class="code">' FAKE SCROLLAREA TGADGET BY CLIFF

Rem
bbdoc: Create a ScrollArea gadget.
about:
A ScrollArea generates an EVENT_GADGETACTION #brl.event.Event whenever it is changed.&lt;p&gt;
You can't create gadgets using this object as a parent.
To add child gadgets to the ScrollArea you must call #GetScrollAreaClient and use the returned TGadget as the parent.
&lt;p&gt;
See Also:  #SetScrollAreaSize and #GetScrollAreaClient.
EndRem
Function CreateScrollArea:TGadget(x,y,w,h,group:TGadget)
	Local This:TScrollArea = New TScrollArea
	This.innerwidth = w-15
	This.innerheight= h-15
	This.Parent = Group
	This.Group = Group
	This.Create()
	This.SetShape(x,y,w,h)
	This.Update
	Return This
EndFunction

Rem
bbdoc: Set the size of the inner client panel of a ScrollArea gadget.
See Also: #CreateScrollArea and #GetScrollAreaClient.
EndRem
Function SetScrollAreaSize(ScrollArea:TGadget,width,height)
	Local Gadget:TScrollArea = TScrollArea(ScrollArea)
	If Gadget
		Gadget.innerwidth = width
		Gadget.innerheight= height
		Gadget.Update
	EndIf
EndFunction

Rem
bbdoc: Return the TGadget object of the inner client panel
about:
Use the returned TGadget object to add child gadgets to.&lt;p&gt;
&lt;p&gt;
See Also: #CreateScrollArea and #GetScrollAreaClient.
EndRem
Function GetScrollAreaClient:TGadget(ScrollArea:TGadget)
	Local Gadget:TScrollArea = TScrollArea(ScrollArea)
	If Gadget
		Return Gadget.ClientPanel
	EndIf
EndFunction

' ---------------------------------------------------------------------------------------

Type TScrollArea Extends TGadget

	Field group:TGadget
	Field backpanel:TGadget
	Field vslider:TGadget
	Field hslider:TGadget
	Field Panel:TGadget
	Field ClientPanel:TGadget
	Field innerwidth
	Field innerheight

	Method Create()
		backpanel:TGadget = CreatePanel(xpos,ypos,width,height,group)
		VSlider = CreateSlider(width-15,0,15,height-15,backpanel)
		VSlider.SetLayout(0,1,1,1)
		HSlider = CreateSlider(0,height-15,width-15,15,backpanel,SLIDER_HORIZONTAL)
		HSlider.SetLayout(1,1,0,1)
		Panel = CreatePanel(0,0,width-15,height-15,backpanel,Panel_Border)
		Panel.SetLayout(1,1,1,1)
		ClientPanel = CreatePanel(0,0,innerwidth,innerheight,Panel)
		ClientPanel.SetLayout(1,0,1,0)
		AddHook EmitEventHook,eventhook,Self
	EndMethod
	
	Method Free()
		RemoveHook EmitEventHook,eventhook
		FreeGadget(backpanel)
	EndMethod
	
	Method SetLayout(Left,Right,Top,Bottom)
		backpanel.SetLayout(Left,Right,Top,Bottom)
	EndMethod
	
	Method SetShape(x,y,w,h)
		backpanel.SetShape(x,y,w,h)
	EndMethod

	Method SetEnabled(bool)
		ClientPanel.SetEnabled(bool)
		VSlider.SetEnabled(bool)
		HSlider.SetEnabled(bool)
	End Method

	Method SetColor(r,g,b)
		Panel.SetColor(r,g,b)
		ClientPanel.SetColor(r,g,b)
	End Method

	Method Update()
		Local x,y, cw = .ClientWidth(Panel), ch = .ClientHeight(Panel)
		SetSliderRange(HSlider,cw,innerwidth) ; SetSliderRange(VSlider,ch,innerheight)
		If innerwidth&lt;cw Then x=(cw-innerwidth)/2 Else x=-SliderValue(HSlider)
		If innerheight&lt;ch Then y=(ch-innerheight)/2 Else y=-SliderValue(VSlider)
		ClientPanel.SetShape(x,y,innerwidth,innerheight)
	EndMethod

	' ----------------------------------------------------------------------------------
	
	Function eventhook:Object(id,data:Object,context:Object)
		If TScrollArea(context) Then TScrollArea(context).OnEvent TEvent(data)
		Return data	
	EndFunction
	
	Method OnEvent(event:TEvent)
		Select event.id
			Case EVENT_WINDOWSIZE
				Update
				
			Case EVENT_GADGETACTION
				Select Event.Source
					Case HSlider
						Update
					Case VSlider
						Update
				EndSelect
		EndSelect
	EndMethod

EndType</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >REDi</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> Demo usage...<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Type TApp

	Field Window:TGadget = CreateWindow("",0,0,640,480)
	Field ScrollArea:TGadget = CreateScrollArea(5,75,ClientWidth(window)-10,ClientHeight(window)-80,window)
	Field Button:TGadget = CreateButton("BUTTON",0,0,200,200,GetScrollAreaClient(ScrollArea))
	
	Method New()
		AddHook EmitEventHook,eventhook,Self
		Button.SetLayout(1,0,1,0)
		ScrollArea.SetLayout(1,1,1,1)
		SetGadgetColor(ScrollArea,200,200,200)
		SetScrollAreaSize(ScrollArea,200,200)
	EndMethod	
	
	Method Free()
		RemoveHook(EmitEventHook,eventhook)
		FreeGadget(Window)
	EndMethod
	
	Function eventhook:Object(id,data:Object,context:Object)
		If TApp(context) Then TApp(context).OnEvent TEvent(data)
		Return data	
	EndFunction
	
	Method OnEvent(event:TEvent)
		Select event.id
		
			Case EVENT_WINDOWCLOSE
				Select Event.Source
					Case window
						Free
						End
				End Select
				
			Case EVENT_GADGETACTION
				Select Event.Source
					Case Button
						Notify "You pressed the button"
				EndSelect
			
		EndSelect
	EndMethod
	
EndType


New TApp
While True
	WaitEvent()
Wend
</textarea> <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
