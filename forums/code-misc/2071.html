<!DOCTYPE html><html lang="en" ><head ><title >[MaxGUI/Win32]: Transparent Window Gadgets</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Transparent / Alpha Window Gadgets, language=bb, category=Miscellaneous'><meta name='author' content='grable'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=9>Miscellaneous</a>/[MaxGUI/Win32]: Transparent Window Gadgets</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2071.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >[MaxGUI/Win32]: Transparent Window Gadgets by grable</td><td align="right">2007 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Example:<br><pre class=code>
SuperStrict

Import "LayeredGadgets.bmx"

Global window:TGadget = CreateWindow( "Window", 64,64, 256,256, Null, WINDOW_TITLEBAR | WINDOW_RESIZABLE | WINDOW_CLIENTCOORDS)
Global panel:TGadget = CreatePanel( 64, 64,  128,128, window)

SetPanelColor panel, 255,0,0

SetGadgetLayered window, True
SetGadgetLayeredAttribs window, $FF, 128

While WaitEvent()	
	If EventID() = EVENT_WINDOWCLOSE Then Exit
Wend
End
</pre><br><br>Hackish code to get it working inside windows (by using a second window)<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Import "LayeredGadgets.bmx"

Global window:TGadget = CreateWindow( "Window", 64,64, 256,256, Null, WINDOW_TITLEBAR | WINDOW_RESIZABLE | WINDOW_CLIENTCOORDS)
Global panel:TGadget = CreatePanel( 64, 64,  128,128, window)

' the fake window with the controls
Global window2:TGadget = CreateWindow( "Window2", 32,32, 64,64, window, WINDOW_CLIENTCOORDS)
Global button:TGadget = CreateButton( "Button", 4,4, 56,25, window2)

SetPanelColor panel, 255,0,0

SetGadgetLayered window2, True
SetGadgetAlpha window2, 0.5

' set the timer to make sure the window stays where we tell it to
CreateTimer 1000
AddHook EmitEventHook, move_window_hook

While WaitEvent()	
	If EventID() = EVENT_WINDOWCLOSE Then Exit
Wend
End


Extern "Win32"
	Function ClientToScreen:Int( hwnd:Int, point:Long Var)
EndExtern

Function move_window_hook:Object( id:Int, data:Object, context:Object)
	Local ev:TEvent = TEvent( data)
	If ev Then
		If ev.ID = EVENT_TIMERTICK Then
			' get the position of main window, and keep the child window in place
			Local point:Long	
			ClientToScreen( window.Query(QUERY_HWND), point)
			Local x:Int = Int Ptr( Varptr point)[0]
			Local y:Int = Int Ptr( Varptr point)[1]
			SetGadgetShape( window2, x+64,y+64, GadgetWidth(window2),GadgetHeight(window2))
			Return Null
		EndIf
	EndIf
	Return data
EndFunction
</textarea> </td></tr><tr ><td class="cell"><pre class="code">Rem
	transparent windows in windows ;)
	
	author: grable
	email : grable0@gmail.com
EndRem

SuperStrict

Import MaxGUI.Drivers

?MacOS
DebugLog "LayeredGadgets.bmx doesnt support MacOS"
?Linux
DebugLog "LayeredGadgets.bmx doesnt support Linux"
?

Private

?Win32
Extern "Win32"
	Const GWL_EXSTYLE:Int = -20
	Const GWL_STYLE:Int = -16
	
	Const WS_EX_TRANSPARENT:Int = $20
	Const WS_EX_LAYERED:Int = $80000
	
	Const LWA_COLORKEY:Int = $1
	Const LWA_ALPHA:Int = $2

	Function GetWindowLong:Int( hwnd:Int, index:Int) = "GetWindowLongA@8"
	Function SetWindowLong:Int( hwnd:Int, index:Int, value:Int) = "SetWindowLongA@12"
	Function SetLayeredWindowAttributes:Int( hwnd:Int, ckey:Int, alpha:Byte, flags:Int)

'	typedef struct _BLENDFUNCTION {
'	  	Byte     BlendOp;
'	  	Byte     BlendFlags;
'	  	Byte     SourceConstantAlpha;
'	  	Byte     AlphaFormat;
'	}BLENDFUNCTION, *PBLENDFUNCTION, *LPBLENDFUNCTION;
'	Function 	UpdateLayeredWindow:Int( hwnd:Int, destdc:Int, destpoint:Long Var, size:Long Var, srcdc:Int, srcpoint:Long Var, ckey:Int, blendfunc:Byte Ptr, flags:Int)
EndExtern
?

Public

Function SetGadgetLayered( gadget:TGadget, layered:Int, transparent:Int = False)
	?Win32
	Local handle:Int = QueryGadget( gadget, QUERY_HWND)
	Local style:Int = GetWindowLong( handle, GWL_EXSTYLE)
	If layered Then 
		style :| WS_EX_LAYERED
	Else
		style :&amp; ~WS_EX_LAYERED
	EndIf
	If transparent  Then 
		style:| WS_EX_TRANSPARENT
	Else
		style :&amp; ~WS_EX_TRANSPARENT
	EndIf
	SetWindowLong( handle, GWL_EXSTYLE, style)
	?
EndFunction

Function SetGadgetColorKey( gadget:TGadget, ckey:Int)
	?Win32
	Local handle:Int = QueryGadget( gadget, QUERY_HWND)
	SetLayeredWindowAttributes( handle, ckey, 0, LWA_COLORKEY)
	?
EndFunction

?Win32
Function SetGadgetAlpha( gadget:TGadget, alpha:Float)
	Local handle:Int = QueryGadget( gadget, QUERY_HWND)
	SetLayeredWindowAttributes( handle, 0, Byte(alpha * 255), LWA_ALPHA)
EndFunction
?

Function SetGadgetLayeredAttribs( gadget:TGadget, ckey:Int, alpha:Byte)
	?Win32	
	Local handle:Int = QueryGadget( gadget, QUERY_HWND)
	SetLayeredWindowAttributes( handle, ckey, alpha, LWA_COLORKEY | LWA_ALPHA)
	?	
EndFunction</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CASO</td><td align="right"><font class=tiny>2007</font></td></tr></table></td></tr><tr ><td class="posttext"> I tried this with the CreatePanel example, set the colored panel and button to 0.5, and.... NOTHING HAPPENED. Is it me?<br><br>PS: I'm using XP <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>2007</font></td></tr></table></td></tr><tr ><td class="posttext"> As far as i know this wont work inside a window, only on windows them self, sorry :/<br><br>Ive corrected the name/title to reflect this, and added an example.<br><br>EDIT: There is a way to get it to work, but its a bit hackish. see the example above. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CASO</td><td align="right"><font class=tiny>2007</font></td></tr></table></td></tr><tr ><td class="posttext"> I tried the example. THAT IS AWESOME! I don't really know what to do with it right now, but IT'S AWESOME. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
