<!DOCTYPE html><html lang="en" ><head ><title >Mesh surface Packer Example</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='2d Example how you can turn a mesh of blocks(map) into bigger blockw., language=bb, category=Miscellaneous'><meta name='author' content='Pakz'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=9>Miscellaneous</a>/Mesh surface Packer Example</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="3157.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mesh surface Packer Example by Pakz</td><td align="right">2014 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> <img src="http://i57.tinypic.com/5la33k.jpg"><br><br>Here a video with more maps and time taken (debugger on on 250 dollar laptop) : <a href="http://youtu.be/rmlrJO1-h3c" target="_blank">http://youtu.be/rmlrJO1-h3c</a><br><br>This is a example of how I am trying to optimize a random 3d map with blocks like minecraft and create bigger surfaces where possible.<br><br>In this example a small 2d map is generated and a routine goes from fitting big to small blocks into the map. If it fits then the space is added into a rects list and the map space is flagged occupied in a other array.<br><br>You end up with a list of coordinates and width and height for placing and resizing the meshes.  I want to try and use this with a randomly created minecraft like map.<br><br>In this example it does not use 2*2 blocks. I still need to figure out how to do that. Edit - I added 2 4x4 block to. </td></tr><tr ><td class="cell"><pre class="code">; Big Surfaces Maker

Graphics 800,600,32,2
SetBuffer BackBuffer()
SeedRnd 1

AppTitle "Press escape to end."

Global mapw = 50
Global maph = 50
Dim map(mapw,maph)

Type r
	Field x,y,w,h
End Type
Dim  cmap(mapw,maph)
initmap()
ms = MilliSecs()
initsurfaces()	
ms = MilliSecs() - ms

timer = CreateTimer(60)
While KeyDown(1) = False
	WaitTimer timer
	Cls
	drawmap()
	drawrects()
	;
	If cnt &gt; 60*3
		initmap()
		ms = MilliSecs()		
		initsurfaces()	
		ms = MilliSecs() - ms
		cnt=0
	End If
	cnt=cnt+1
	Text GraphicsWidth()-196,10,"Took:"+ms+" ms"
	Flip
Wend
End

Function initsurfaces()
	Delete Each r
	For y=0 To maph
	For x=0 To mapw
		cmap(x,y) = 0
	Next
	Next
	;
	; Pass 1 - Fit increasingly smaller blocks into the space and add to list
	;
	cnt=0
	exitloop = False
	rad = mapw
	x1 = 0
	y1 = 0
	While exitloop = False
		fits = True
		x2 = -rad
		y2 = -rad
		If map(x1,y1) = 1
			For y2=-rad To rad
			For x2=-rad To rad
				x3 = x1+x2
				y3 = y1+y2
				If RectsOverlap(x3,y3,1,1,0,0,mapw+1,maph+1) = True
						If map(x3,y3) = 0 Then fits = False:Exit
						If cmap(x3,y3) = 1 Then fits = False:Exit
					Else
					fits = False :Exit
				EndIf
			Next
			Next
		
			If fits = True Then
				For y2 = -rad To rad
				For x2 = -rad To rad
					x3 = x1 + x2
					y3 = y1 + y2
					cmap(x3,y3) = 1
				Next
				Next
				r1.r = New r			
				r1\x = x1-rad
				r1\y = y1-rad			
				r1\w = rad*2
				r1\h = rad*2
			End If
		End If
		x1 = x1 + 1
		If x1 &gt; mapw
			y1 = y1 + 1
			x1 = 0
		End If
		If x1 =&gt; mapw And y1=&gt;maph Then 
			x1 = 0
			y1 = 0
			rad = rad - 1
		End If
		If rad &lt; 0 Then exitloop = True
	Wend	
	;
	; Pass 2 - check the list for 4x4 rectangles to create one off
	;
	For y=0 To maph
	For x=0 To mapw
		If map(x,y) = 1
			For this.r = Each r
				If this\x = x And this\y = y And this\w = 0
					aset = False
					For a.r = Each r
						If a\x = this\x+1 And a\y = this\y And a\w = 0 Then aset = True 
					Next
					bset = False
					For b.r = Each r
						If b\x = this\x And b\y = this\y+1 And b\w = 0 Then bset = True 
					Next
					cset = False
					For c.r = Each r
						If c\x = this\x+1 And c\y = this\y+1 And c\w = 0 Then cset = True 
					Next
					If aset = True And bset = True And cset = True
						that.r = New r
						that\x = this\x
						that\y = this\y
						that\w = 1
						that\h = 1
						For a.r = Each r
							del = False
							If a\x = this\x+1 And a\y = this\y And a\w = 0 Then Del = True
							If a\x = this\x And a\y = this\y+1 And a\w = 0 Then del = True
							If a\x = this\x+1 And a\y = this\y+1 And a\w = 0 Then del = True
							If del = True Then Delete a
						Next
						Delete this
					End If
				End If			
			Next
		End If
	Next
	Next
End Function

Function drawrects()
	Color 255,255,255
	For this.r = Each r
		Rect this\x*10,this\y*10,(this\w+1)*10,(this\h+1)*10,False
	Next
End Function

Function drawmap()
	For y=0 To maph
	For x=0 To mapw
		Select map(x,y)
			Case 0:Color 0,0,0
			Case 1:Color 100,100,100
		End Select
		Rect x*10,y*10,10,10,True
	Next
	Next
End Function

Function initmap()
	For y=0 To maph
	For x=0 To mapw
		map(x,y) = 0
	Next
	Next	

	exitloop = False
	While exitloop = False
		x1 = Rand(mapw)
		y1 = Rand(maph)
		rad = Rand(3,6)
		For y2 = -rad To rad
		For x2 = -rad To rad
			x3 = x1+x2
			y3 = y1+y2
			If x3 =&gt; 0 And y3 &gt;= 0 And x3 =&lt; mapw And y3 &lt;= maph
				map(x3,y3) = map(x3,y3) + 1
				If map(x3,y3) &gt; 10 Then exitloop = True				
			End If			
		Next
		Next
	Wend	
	For y = 0 To maph
	For x = 0 To mapw
		If map(x,y) &lt; 5 Then map(x,y) = 0
		If map(x,y) &gt; 4 Then map(x,y) = 1
	Next
	Next
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
