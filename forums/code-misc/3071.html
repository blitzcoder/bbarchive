<!DOCTYPE html><html lang="en" ><head ><title >Deck of playing cards</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Deck and card classes, language=bmx, category=Miscellaneous'><meta name='author' content='Pineapple'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=9>Miscellaneous</a>/Deck of playing cards</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="3071.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Deck of playing cards by Pineapple</td><td align="right">2013 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Pretty simple. Decks are treated like dequeue of cards. Check the methods and describing comments, it shouldn't be any trouble to pick up. </td></tr><tr ><td class="cell"><pre class="code">' 	--+-----------------------------------------------------------------------------------------+--
'	  |   This code was originally written by Sophie Kirschner (sophiek@pineapplemachine.com)   |  
' 	  | It is released as public domain. Please don't interpret that as liberty to claim credit |  
' 	  |   that isn't yours, or to sell this code when it could otherwise be obtained for free   |  
'	  |                because that would be a really shitty thing of you to do.                |
' 	--+-----------------------------------------------------------------------------------------+--


SuperStrict

Import brl.random



' Example code

Rem

SeedRnd MilliSecs()

Local d:deck=deck.Create(1)

Print "The first 5 cards on the deck are:"

For Local i%=1 To 5
	Print d.pop().tostring()
Next

EndRem



Const SUIT_SPADES%=0
Const SUIT_HEARTS%=1
Const SUIT_CLUBS%=2
Const SUIT_DIAMONDS%=3
Const NUM_ACE%=1
Const NUM_JACK%=11
Const NUM_QUEEN%=12
Const NUM_KING%=13
Const NUM_JOKER%=14

Type deck
	Field cards:TList=CreateList()
	Field count%=0
	Function Create:deck(shuffle%=False)
		Local n:deck=New deck
		n.init
		If shuffle n.shuffle()
		Return n
	End Function
	Method init()
		For Local suit%=0 Until 4
			For Local num%=NUM_ACE To NUM_KING
				Local c:card=New card
				c.suit=suit
				c.num=num
				cards.addlast c
			Next
		Next
		count=52
	End Method
	Method shuffle() ' fewer operations, results in a pretty well-randomized deck.
		Local nlist:TList=CreateList()
		For Local i%=0 Until count
			Local j%=Rand(0,count-i-1)
			Local link:TLink
			If Rand(0,1)
				link=cards._head._succ
				While j
					link=link._succ
					j:-1
				Wend
			Else
				link=cards._head._pred
				While j
					link=link._pred
					j:-1
				Wend
			EndIf
			nlist.addlast link._value
			link.remove
		Next
		cards=nlist
	End Method
	Method shuffle_2(iterations%=60) ' more operations, but shuffles like a human might.
		?debug
		Local icount%=count
		?
		For Local i%=0 Until iterations
			Local link:TLink
			Local slink:TLink,elink:TLink
			Local index%=Rand(1,count-1)
			Local move%=Rand(0,count-index-1)
			Local dir%=Rand(0,1)
			If dir
				link=cards._head._succ
				Local j%=0
				While j&lt;index
					link=link._succ
					j:+1
				Wend
				slink=link
				j=0
				While j&lt;move
					link=link._succ
					j:+1
				Wend
				elink=link
				slink._pred._succ=elink._succ
				elink._succ._pred=slink._pred
				elink._succ=cards._head
				slink._pred=cards._head._pred
				cards._head._pred._succ=slink
				cards._head._pred=elink
			Else
				link=cards._head._pred
				Local j%=0
				While j&lt;index
					link=link._pred
					j:+1
				Wend
				elink=link
				j=0
				While j&lt;move
					link=link._pred
					j:+1
				Wend
				slink=link
				slink._pred._succ=elink._succ
				elink._succ._pred=slink._pred
				slink._pred=cards._head
				elink._succ=cards._head._succ
				cards._head._succ._pred=elink
				cards._head._succ=slink
			EndIf
			?debug
			Assert cards.count()=icount,"Shuffling the deck fucked up a list link somewhere."
			?
		Next
	End Method
	Method tostring$()
		Local str$=""
		For Local c:card=EachIn cards
			str:+c.tostring()+" "
		Next
		Return str
	End Method
	Method pop:card() ' draw a card from the top
		If count=0 Return Null
		count:-1
		Return card(cards.removefirst())
	End Method
	Method popbottom:card() ' draw a card from the bottom
		If count=0 Return Null
		count:-1
		Return card(cards.removelast())
	End Method
	Method push:TLink(c:card) ' put the card on top
		count:+1
		Return cards.addfirst(c)
	End Method
	Method pushbottom:TLink(c:card) ' put the card on bottom
		count:+1
		Return cards.addlast(c)
	End Method
End Type

Type card
	Field suit%
	Field num%
	Global suitname$[]=["spades","hearts","clubs","diamonds"]
	Global suitchar$[]=["S","H","C","D"]
	Global numname$[]=["error","ace","two","three","four","five","six","seven","eight","nine","ten","jack","queen","king","joker"]
	Global numplural$[]=["errors","aces","twos","threes","fours","fives","sixes","sevens","eights","nines","tens","jacks","queens","kings","jokers"]
	Global numchar$[]=["?","A","2","3","4","5","6","7","8","9","10","J","Q","K","@"]
	Method tostring$()
		Return numchar[num]+suitchar[suit]
	End Method
End Type</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
