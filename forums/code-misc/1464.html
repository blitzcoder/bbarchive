<!DOCTYPE html><html lang="en" ><head ><title >Create Process (windows)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Process spawning class for windows, language=bmx, category=Miscellaneous'><meta name='author' content='Lomat'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=9>Miscellaneous</a>/Create Process (windows)</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1464.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Create Process (windows) by Lomat</td><td align="right">2005 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> My first posting to the blitzmax code archive so i hope it works :) Comments and suggestions welcome.<br><br>This class will allow you to spawn processes from within you application (under windows) and allow you to keep you blitz max application running in the background.<br><br>Example usage:<br><br>Local p:Process = New Process<br>p.setExe("c:\windows\notepad.exe")<br>p.setArgs("C:\Program Files\BlitzMax\doc\home.html")<br>p.create()<br>While p.isRunning()<br>	Print "Process is running..."<br>	Delay(1000)<br>Wend<br>Print "Process finished with return value " + p.getReturnValue() </td></tr><tr ><td class="cell"><pre class="code">Extern "Win32"
	Function WaitForSingleObject:Int(hHandle:Int, dwMilliseconds:Int)
	Function GetExitCodeProcess:Int(hProcess:Int, lpExitCode:Int)
	Function CreateProcessA:Int(lpApplicationName:Byte Ptr, lpCommandLine:Byte Ptr, lpProcessAttributes:Int, lpThreadAttributes:Int, bInheritHandles:Int, dwCreationFlags:Int, lpEnvironment:Int, lpCurrentDirectory:Byte Ptr, lpStartupInfo:ProcessStartUpInfo, lpProcessInformation:ProcessInformation)
	Function CloseHandle:Int(hProcess:Int)
	Function GetActiveWindow:Int()
	Function SetForegroundWindow(hWnd:Int)
	Function SetActiveWindow(hWnd:Int)
	Function OpenProcess:Int(dwDesiredAccess:Int, bInheritHandle:Int, dwProcessId:Int)
	Function keybd_event(bVk:Byte, bScan:Byte, dwFlags:Int, dwExtraInfo:Int)
End Extern

Type ProcessStartUpInfo
   Field cb:Int
   Field lpReserved:String
   Field lpDesktop:String
   Field lpTitle:String
   Field dwX:Int
   Field dwY:Int
   Field dwXSize:Int
   Field dwYSize:Int
   Field dwXCountChars:Int
   Field dwYCountChars:Int
   Field dwFillAttribute:Int
   Field dwFlags:Int
   Field wShowWindow:Int
   Field cbReserved2:Int
   Field lpReserved2:Int
   Field hStdInput:Int
   Field hStdOutput:Int
   Field hStdError:Int
End Type

Type ProcessInformation
   Field hProcess:Int
   Field hThread:Int
   Field dwProcessID:Int
   Field dwThreadID:Int
End Type

Type Process

	Field exe:String
	Field args:String
	Field proc:ProcessInformation
	Field returnValue:Int
	Field pHandle:Int
	Field myhWnd:Int
	
	Method New()
		myHwnd = GetActiveWindow()
	End Method
	
	Method setExe (appPath:String)
		exe = appPath
	End Method

	Method setArgs (argString:String)
		args = argString
	End Method
	
	Method getReturnValue:Int ()
		Return returnValue
	End Method
	
	Method create()
		' change to exe directory
		ChangeDir(ExtractDir(exe))
		' spawn process...
		Local cmd:String  = "~q" + exe + "~q" + " " + "~q" + args + "~q"
		Print "Run process: " + cmd
		Local start:ProcessStartUpInfo = New ProcessStartUpInfo
		proc = New ProcessInformation
		CreateProcessA(Null, cmd.ToCString(), 0, 0, 1, 0, 0, Null, start, proc)
		pHandle     = OpenProcess($100000, -1, proc.hProcess)
		returnValue = Null
	End Method
	
	Method isRunning:Int()
		Local v:Int = WaitForSingleObject(pHandle, 0)
		Select v
			Case 0
				returnValue = GetExitCodeProcess(pHandle, v)
				CloseHandle(pHandle)
				Print "Process finished with return value: " + returnValue
				' change back to this app dir.
				ChangeDir(LaunchDir$)
				' Set focus back to this application.
				SetForegroundWindow(myHWnd)
				SetActiveWindow(myHWnd)
				
				' Since were a full screen app Windows does not like
				' makign you go back into full screen mode so we have
				' to emulate the user pressing Alt + Enter :)
				
				' Add a little delay
				Delay(100)
				' send alt + enter to get back full screen window :)
				keybd_event(KEY_ALT, 0, 0, 0)
				keybd_event(KEY_RETURN, 0, 0, 0)
				' we have to let go of the keys :)
				keybd_event(KEY_RETURN, 0, 2, 0)
				keybd_event(KEY_ALT, 0, 2, 0)
				' app should now be back in focus and on the screen.
				Return 0
			Case -1
				' We normally get this if the process failed to launch.
				Print "WaitForSingleObject failed."
				CloseHandle(pHandle)
				Return 0
			Default
				' Process is running.
				Return 1				
		End Select
	End Method
	
End Type</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Lomat</td><td align="right"><font class=tiny>2005</font></td></tr></table></td></tr><tr ><td class="posttext"> For getting back to full screen sending ALT+TAB instead of ALT+ENTER might work better. YMMV so see which one works for you. :) <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
