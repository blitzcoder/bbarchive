<!DOCTYPE html><html lang="en" ><head ><title >registry access functions</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='functions that allow reading/writing to the registry, language=bb, category=Miscellaneous'><meta name='author' content='gman'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=9>Miscellaneous</a>/registry access functions</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1136.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >registry access functions by gman</td><td align="right">2004 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> a small library implementing some functions and constants that work with the windows registry.<br><br>** NOTES **<br>- messing with your registry can really muck with your machine so use at your own risk.<br>- this library requires <a href="/codearcs/codearcs.php?code=1135" >advapi32.dll decls for registry access</a> in your userlibs folder.<br>- supports REG_SZ, REG_DWORD, and REG_BINARY types<br>- any errors that occur are stored in a global variable called "reg_lasterr"<br><br>functions:<br>reg_openkey - opens a registry key and returns the handle<br>reg_closekey - closes a registry key opened with reg_openkey or reg_createkey<br>reg_createkey - creates a key if it doesnt exist and opens it, returning the handle<br>reg_deletekey - deletes a key from the registry<br>reg_setvalue - stores a value in the registry<br>reg_getvalue - returns a value from the registry as a string.  can optionally return a delimited string with the data type preceding the value.<br>reg_deletevalue - deletes a value from the registry<br>reg_enumkeys - returns a delimited string of the keys contained under the passed key.<br>reg_enumvalues - returns a delimited string containing stored values (both name and value).  can optionally return type preceding the name.<br><br>a few use examples (written in B+):<br><br>i find it good to define a constant housing your application root.<br><pre class=code>
Const REG_APPROOT$ = "SOFTWARE\My Application"
</pre><br>test to see if a key exists<br><pre class=code>
If Not reg_iskey(HKEY_LOCAL_MACHINE,REG_APPROOT$)
	Notify("reg_iskey: Application Key Does Not Exist")
EndIf
</pre><br>try to open a key<br><pre class=code>
hKey=reg_openkey(HKEY_LOCAL_MACHINE,REG_APPROOT$)
If hKey&lt;&gt;-1
	Notify("reg_openkey: Key opened, returned handle "+hKey)
	reg_closekey(hKey)
Else
	Notify("reg_openkey: Application Key Does Not Exist")
EndIf
</pre><br>create a key and open it or open if it exists<br><pre class=code>
hKey=reg_createkey(HKEY_LOCAL_MACHINE,REG_APPROOT$)
If hKey&lt;&gt;-1
	Notify("reg_createkey: Key created, returned handle "+hKey)
	reg_closekey(hKey)
Else
	Notify("reg_createkey: Key creation failed, check req_lasterr.")
EndIf
</pre><br>set some values<br><pre class=code>
; string
reg_setvalue(HKEY_LOCAL_MACHINE,REG_APPROOT$,"test1","1212")
; integer
reg_setvalue(HKEY_LOCAL_MACHINE,REG_APPROOT$,"test2",3344,REG_DWORD)
</pre><br>get the values<br><pre class=code>
Notify("test1: "+reg_getvalue(HKEY_LOCAL_MACHINE,REG_APPROOT$,"test1"))
Notify("test2 with type prepended: "+reg_getvalue(HKEY_LOCAL_MACHINE,REG_APPROOT$,"test2","",True))
</pre><br>enumeration of values<br><pre class=code>
; without type
Notify(reg_enumvalues(HKEY_LOCAL_MACHINE,REG_APPROOT$))
; with type prepended
Notify(reg_enumvalues(HKEY_LOCAL_MACHINE,REG_APPROOT$,"|",True))
</pre><br>delete a value<br><pre class=code>
reg_deletevalue(HKEY_LOCAL_MACHINE,REG_APPROOT$,"test1")
</pre><br>delete the key<br><pre class=code>
reg_deletekey(HKEY_LOCAL_MACHINE,REG_APPROOT$)
</pre> </td></tr><tr ><td class="cell"><pre class="code">; registry roots
Const HKEY_CLASSES_ROOT			= -2147483648
Const HKEY_CURRENT_USER			= -2147483647
Const HKEY_LOCAL_MACHINE		= -2147483646
Const HKEY_USERS			= -2147483645

; return value constants
Const ERROR_SUCCESS 			= 0
Const ERROR_EOF				= 259  	; no more entries in key

; data types For keys
Const REG_SZ				= 1	; Data String
Const REG_BINARY			= 3	; Binary Data in any form.
Const REG_DWORD				= 4 	; A 32-bit number.

; global var holding last error #
Global reg_lasterr			= ERROR_SUCCESS

; enumerates the keys contained in the passed subkey and returns them as a delimited string in 
; the format: KEY=VALUE|KEY=VALUE|KEY=VALUE
Function reg_enumvalues$(RegKey%,SubKey$,delim$="|",types=False)
	Local cRetVal$="",key$="",val$=""
	Local keybank=CreateBank(100),keybanksize=CreateBank(4),valbank=CreateBank(100),valbanksize=CreateBank(4),typebank=CreateBank(4)
	Local char=0,nIdx=0,nType=0

	; open the key 
	Local hKey=reg_openkey(RegKey,SubKey$)
	If hKey&lt;&gt;-1	
	
		; read in the values
		Repeat
			; init the banks
			PokeInt(typebank,0,0)
			PokeInt(valbanksize,0,100)
			PokeInt(keybanksize,0,100)
		
			; clear out the temp values
			key$=""
			val$=""
			
			If RegEnumValue(hKey,nIdx,keybank,keybanksize,0,typebank,valbank,valbanksize)&lt;&gt;ERROR_EOF
				nType=PeekInt(typebank,0)
				
				; tack on the delimiter
				If cRetVal$&lt;&gt;""
					cRetVal$=cRetVal$+delim$
				EndIf 

				; build the key name
				For char=0 To PeekInt(keybanksize,0)-1
					If PeekByte(keybank,char)=0 Then Exit
					key$=key$+Chr(PeekByte(keybank,char))
				Next

				Select nType
					; read in a string or binary value
					Case REG_SZ, REG_BINARY
						; build the value
						For char=0 To PeekInt(valbanksize,0)-1
							If PeekByte(valbank,char)=0 Then Exit
							val$=val$+Chr(PeekByte(valbank,char))
						Next
					; read in an integer
					Case REG_DWORD
						val$=PeekInt(valbank,0)						
				End Select

				If types
					cRetVal$=(cRetVal$+PeekInt(typebank,0)+";"+key$+"="+val$)
				Else
					cRetVal$=(cRetVal$+key$+"="+val$)
				EndIf
			Else
				Exit
			EndIf			
			
			nIdx=nIdx+1
		Forever
		reg_closekey(hKey)
	EndIf
	
	FreeBank typebank
	FreeBank valbank
	FreeBank valbanksize
	FreeBank keybank
	FreeBank keybanksize
	
	Return cRetVal$
End Function

; enumerates the keys contained in the passed subkey and returns them as a delimited string in 
; the format: KEY|KEY|KEY
Function reg_enumkeys$(RegKey%,SubKey$,delim$="|")
	Local cRetVal$=""
	Local keybank=CreateBank(100)
	Local nIdx=0

	; open the key first	
	Local hKey=reg_openkey(RegKey,SubKey$)
	If hKey&lt;&gt;-1	
		Repeat			
			If RegEnumKey(hKey,nIdx,keybank,BankSize(keybank))&lt;&gt;ERROR_EOF			
				; tack on the delimiter
				If cRetVal$&lt;&gt;""
					cRetVal$=cRetVal$+delim$
				EndIf 
				
				For char=0 To BankSize(keybank)-1
					If PeekByte(keybank,char)=0 Then Exit
					cRetVal$=cRetVal$+Chr(PeekByte(keybank,char))
				Next
			Else
				Exit
			EndIf
			
			nIdx=nIdx+1
		Forever
		reg_closekey(hKey)
	EndIf

	FreeBank keybank
	Return cRetVal$
End Function

; deletes a value from the registry.  returns True/False.
Function reg_deletevalue%(RegKey%,SubKey$,ValueName$)
	Local hKey=reg_openkey(RegKey,SubKey$)
	Local lRetVal=False
	If hKey&lt;&gt;-1
		Local nRslt=RegDeleteValue(hKey,ValueName$)		
		If (nRslt=ERROR_SUCCESS)
			lRetVal=True
		Else
			reg_lasterr=nRslt
		EndIf
		reg_closekey(hKey)
	EndIf
	Return lRetVal
End Function

; gets a value from the registry and returns it as a string.  will return the passed default
; if the value requested is not found in the registry.
Function reg_getvalue$(RegKey%,SubKey$,ValueName$,Dflt$="",types=False)
	Local cRetVal$=Dflt$
	Local hKey=reg_openkey(RegKey,SubKey$)
	Local char=0,nType=0
	
	; open the key 
	If hKey&lt;&gt;-1
		Local valbank=CreateBank(100),valbanksize=CreateBank(4),typebank=CreateBank(4)
	
		; init the banks
		PokeInt(typebank,0,0)
		PokeInt(valbanksize,0,100)
	
		Local nRslt=RegQueryValueEx(hKey,ValueName$,0,typebank,valbank,valbanksize)
		If (nRslt=ERROR_SUCCESS)
			cRetVal$=""
		
			nType=PeekInt(typebank,0)
			
			; build the value
			Select nType
				; read in a string or binary value
				Case REG_SZ, REG_BINARY
					; build the value
					For char=0 To PeekInt(valbanksize,0)-1
						If PeekByte(valbank,char)=0 Then Exit
						cRetVal$=cRetVal$+Chr(PeekByte(valbank,char))
					Next
				; read in an integer
				Case REG_DWORD
					cRetVal$=PeekInt(valbank,0)						
			End Select
			
			; tack on the type if requested
			If types
				cRetVal$=nType+";"+cRetVal$
			EndIf
		Else
			reg_lasterr=nRslt
		EndIf
		reg_closekey(hKey)
	EndIf
	Return cRetVal$
End Function

; sets a value in the registry.  defaults to type string, but can pass REG_DWORD and REG_BINARY.
; returns True/False.
Function reg_setvalue%(RegKey%,SubKey$,ValueName$,Value$,nType=REG_SZ)
	Local hKey=reg_openkey(RegKey,SubKey$)
	Local lRetVal=False
	If hKey&lt;&gt;-1
		Local valbank
		
		; create a bank to hold the info
		Select nType
			Case REG_SZ, REG_BINARY
				valbank=CreateBank(Len(Value$))				
				For i=1 To Len(Value$)
					PokeByte(valbank,i-1,Asc(Mid(Value$,i,1)))
				Next				
			Case REG_DWORD
				valbank=CreateBank(4)
				PokeInt(valbank,0,Int(Value$))
		End Select
	
		Local nRslt=RegSetValueEx(hKey,ValueName$,0,nType,valbank,BankSize(valbank))
		If (nRslt=ERROR_SUCCESS)
			lRetVal=True
		Else
			reg_lasterr=nRslt
		EndIf
		reg_closekey(hKey)
	EndIf
	Return lRetVal
End Function

; deletes the passed key from the registry.  returns True/False.
Function reg_deletekey%(RegKey%,KeyName$)
	reg_lasterr=ERROR_SUCCESS
	Local nRslt=RegDeleteKey(RegKey,KeyName$)
	
	If nRslt&lt;&gt;ERROR_SUCCESS
		reg_lasterr=nRslt
	EndIf
	
	Return (nRslt=ERROR_SUCCESS)
End Function

; returns the registry handle or -1 if failed.
Function reg_createkey%(RegKey%,KeyName$)
	reg_lasterr=ERROR_SUCCESS
	Local regbank=CreateBank(4)
	Local hKey=-1

	Local nRslt=RegCreateKey(RegKey%,KeyName$,regbank)

	If (nRslt=ERROR_SUCCESS)
		hKey=PeekInt(regbank,0)
	Else
		reg_lasterr=nRslt
	EndIf

	FreeBank regbank	
	Return hKey
End Function 

; returns the registry handle or -1 if failed
Function reg_openkey%(RegKey%,KeyName$)
	reg_lasterr=ERROR_SUCCESS
	Local regbank=CreateBank(4)
	Local hKey=-1

	Local nRslt=RegOpenKey(RegKey%,KeyName$,regbank)

	If (nRslt=ERROR_SUCCESS)
		hKey=PeekInt(regbank,0)
	Else
		reg_lasterr=nRslt
	EndIf

	FreeBank regbank
	Return hKey
End Function 

; closes the registry key.  returns True/False.
Function reg_closekey%(RegKey%)
	reg_lasterr=ERROR_SUCCESS
	Local nRslt=RegCloseKey(RegKey)
	
	If nRslt&lt;&gt;ERROR_SUCCESS
		reg_lasterr=nRslt
	EndIf
	
	Return (nRslt=ERROR_SUCCESS)
End Function

; returns true if the key exists
Function reg_iskey%(RegKey%,KeyName$)
	Local hKey=reg_openkey(RegKey,KeyName$)
	Local lRetVal=False

	If hKey&lt;&gt;-1
		reg_closekey(hKey)
		lRetVal=True
	EndIf
	
	Return lRetVal
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >cyberseth</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> This is a great set of routines, but the reg_deletekey function does not delete registry entries with multiple subkeys (on Windows NT/2000 or above). This can be remedied by putting the following code at the beginning of the reg_deletekey function:<br><br><pre class=code>
	subkeys$ = reg_enumkeys(RegKey%, KeyName$, Chr(13))
	While Len(subkeys$)&gt;0
		If Instr(subkeys$,Chr(13)) Then
			reg_deletekey(RegKey%, KeyName$+"\"+Left(subkeys$,Instr(subkeys$,Chr(13))-1))
			subkeys$ = Mid(subkeys$,Instr(subkeys$,Chr(13))+1)
		Else
			reg_deletekey(RegKey%, KeyName$+"\"+subkeys$)
			Exit
		End If
	Wend
</pre> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> Cyberseth's correction not ony wont compile, but can cause infinite loops.<br>A fix that works: (Entire Function here)<br><pre class=code>
; deletes the passed key from the registry.  returns True/False.
Function reg_deletekey%(RegKey%,KeyName$)
	reg_lasterr=ERROR_SUCCESS
	Local nRslt=RegDeleteKey(RegKey,KeyName$)
	
	If nRslt&lt;&gt;ERROR_SUCCESS
		reg_lasterr=nRslt
	EndIf
	
subkeys$ = reg_enumkeys(RegKey%, KeyName$)
	While Len(subkeys$)
		If Instr(subkeys$,Chr(13)) Then
			reg_deletekey(RegKey%, KeyName$+"\"+Left(subkeys$,Instr(subkeys$,Chr(13))-1))
			subkeys$ = Mid(subkeys$,Instr(subkeys$,Chr(13))+1)
		Else
			reg_deletekey(RegKey%, KeyName$+"\"+subkeys$)
			Exit
		End If
	Wend
	Return (nRslt=ERROR_SUCCESS)
End Function
</pre><br><br>With this additional update to reg_enumkeys (Again, full function here)<br><pre class=code>
; enumerates the keys contained in the passed subkey and returns them as a delimited string in 
; the format: KEY|KEY|KEY
Function reg_enumkeys$(RegKey%,SubKey$,delim$="|")
	Local cRetVal$=""
	Local keybank=CreateBank(100)
	Local nIdx=0
Local ExitLoop%=False

	; open the key first	
	Local hKey=reg_openkey(RegKey,SubKey$)
	If hKey&lt;&gt;-1	
		While (Not(ExitLoop%))			
			If RegEnumKey(hKey,nIdx,keybank,BankSize(keybank))&lt;&gt;ERROR_EOF			
				; tack on the delimiter
				If cRetVal$&lt;&gt;""
					cRetVal$=cRetVal$+delim$
				EndIf 
				
				For char=0 To BankSize(keybank)-1
					If (Not(PeekByte(keybank,char))) 
ExitLoop%=True
Exit
					cRetVal$=cRetVal$+Chr(PeekByte(keybank,char))
				Next
			Else
				Exit
			EndIf
			
			nIdx=nIdx+1
		Wend
		reg_closekey(hKey)
	EndIf

	FreeBank keybank
	Return cRetVal$
End Function</pre><br><br><br>[/code] <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
