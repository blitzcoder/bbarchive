<!DOCTYPE html><html lang="en" ><head ><title >String comparison to pattern using wildcards such as ? and *</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Support for both greedy and lazy quantifiers, language=bmx, category=Miscellaneous'><meta name='author' content='Pineapple'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=9>Miscellaneous</a>/String comparison to pattern using wildcards such as ? and *</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="3176.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >String comparison to pattern using wildcards such as ? and * by Pineapple</td><td align="right">2015 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Supports these operators in a pattern:<br><br>? - Matches one of any character<br>* - Matches zero or more of any character (greedy)<br>. - Matches zero or more of any character (lazy)<br>+ - Matches one or more of any character (greedy)<br>% - Matches one or more of any character (lazy)<br><br>Special characters can be escaped (for sake of exact character match) by preceding the character with a backslash ("\"). A literal backslash must also be escaped ("\\").<br><br>An example demonstrating the difference between greedy and lazy quantifiers: The pattern "foo*bar" will match the string "foo bar bar". The pattern "foo.bar" will not. </td></tr><tr ><td class="cell"><pre class="code">'   --+-----------------------------------------------------------------------------------------+--
'     |   This code was originally written by Sophie Kirschner (sophiek@pineapplemachine.com)   |  
'     | It is released as public domain. Please don't interpret that as liberty to claim credit |  
'     |   that isn't yours, or to sell this code when it could otherwise be obtained for free   |  
'     |                because that would be a really shitty thing of you to do.                |
'   --+-----------------------------------------------------------------------------------------+--

SuperStrict



' Example code

Rem

Function metatest( pattern:String, str:String, expected:Int, caseSensitive:Int = True )
    Local result:Int = matchWild( pattern, str, caseSensitive )
    Local failtext:String = ""
    If result &lt;&gt; expected failtext = " (FAILED!)"
    Print "pattern: ~q"+pattern+"~q string: ~q"+str+"~q result: "+result+" expected: "+expected+failtext
End Function

Print;Print "Ordinary strings"
metatest( "test", "test", 1 )
metatest( "test", "toast", 0 )

Print;Print "Wildcard ?"
metatest( "t?st", "test", 1 )
metatest( "t?st", "toot", 0 )

Print;Print "Wildcard *"
metatest( "*", "teeeeeeEEEESSSt", 1 )
metatest( "te*st", "test", 1 )
metatest( "t*st", "teeest", 1 )
metatest( "t*st", "test test test", 1 )
metatest( "one*two*three*four", "onebbbdddtwobbthreefour", 1 )
metatest( "*yes", "testyes", 1 )
metatest( "test*", "testyes", 1 )
metatest( "*testyes*", "testyes", 1 )
metatest( "t*st", "testno", 0 )

Print;Print "Wildcard ."
metatest( ".", "teeeeeeEEEESSSt", 1 )
metatest( "te.st", "test", 1 )
metatest( "t.st", "teeest", 1 )
metatest( "t.st", "test test test", 0 )
metatest( "t.st", "testno", 0 )

Print;Print "Wildcard +"
metatest( "+", "teeeeeeEEEESSSt", 1 )
metatest( "te+st", "test", 0 )
metatest( "t+t", "test", 1 )
metatest( "te+st", "test test test", 1 )
metatest( "t+st", "testno", 0 )
metatest( "testno+", "testno", 0 )
metatest( "test+", "testyes", 1 )

Print;Print "Wildcard %"
metatest( "%", "teeeeeeEEEESSSt", 1 )
metatest( "te%st", "test", 0 )
metatest( "t%t", "test", 1 )
metatest( "te%st", "test test test", 0 )
metatest( "testno%", "testno", 0 )
metatest( "test%", "testyes", 1 )

Print;Print "Blank strings"
metatest( "*", "", 1 )
metatest( ".", "", 1 )
metatest( "+", "", 0 )
metatest( "%", "", 0 )

Print;Print "Case sensitivity"
metatest( "test", "TEST", 0 )
metatest( "test", "TEST", 1, False )

Print;Print "Escaped chars"
metatest( "escape\?\?", "escape??", 1 )
metatest( "a*\*", "a*", 1 )
metatest( "esc*\?\?", "escape??", 1 )
metatest( "esc*b\?\?", "escape??", 0 )

EndRem



Const wildcardAscEsc:Int = Asc( "\" )
Const wildcardAsc1:Int = Asc( "?" )
Const wildcardAscPgreedy:Int = Asc( "+" )
Const wildcardAscNgreedy:Int = Asc( "*" )
Const wildcardAscPlazy:Int = Asc( "%" )
Const wildcardAscNlazy:Int = Asc( "." )



Rem

Fairly simple checking whether a string conforms to a pattern.
The following are various wildcards. Anything else in the pattern must be an exact match to the string.
In the pattern, escape a special character by preceding it with a \. Escape a \ by writing \\.

    ?       Match any one character
    +       Greedily match one or more of any character.
    *       Greedily match zero or more of any character.
    %       Lazily match one or more of any character.
    .       Lazily match zero or more of any character.
    
EndRem

Function matchWild:Int( pattern:String, str:String, caseSensitive:Int = True, initx:Int = 0, inity:Int = 0 )

    ' Match a single character
    Function matchChar:Int( pattern:String, y:Int, str:String, x:Int )
        'print "[matching: "+disp(pattern,y) + " , " + disp(str,x)+"]" ' Debug
        If pattern[y] = wildcardAscEsc And y+1 &lt; pattern.length
            If pattern[y+1] = str[x]
                Return 2
            Else
                Return 0
            EndIf
        ElseIf      (pattern[y] = str[x] Or pattern[y] = wildcardAsc1) And ..
                    Not(pattern[y] = wildcardAscPgreedy Or pattern[y] = wildcardAscNgreedy ..
                    Or pattern[y] = wildcardAscPlazy Or pattern[y] = wildcardAscNlazy)
            Return 1
        EndIf
    End Function
    
    ' Good for debugging
    Function disp:String( str:String, p:Int )
        Return str[..p] + "[" + Chr(str[p]) + "]" + str[p+1..] 
    End Function

    ' Essential vars
    Local x:Int = initx, y:Int = inity, ch:Int
    
    'print "[call: "+disp(pattern,y) + " , " + disp(str,x)+"]" ' Debug
    
    ' Handle case insensitivity
    If Not caseSensitive
        str = str.ToLower()
        pattern = pattern.ToLower()
    EndIf
    
    ' Loop
    Repeat
        
        ' Check for termination of string and/or pattern
        If y &gt;= pattern.length
            Return x &gt;= str.length
        ElseIf x &gt;= str.length
            Return y+1 &gt;= pattern.length And (pattern[y] = wildcardAscNlazy Or pattern[y] = wildcardAscNgreedy)
        EndIf
        
        ' Check for exact match for non-special character
        ch = matchChar( pattern, y, str, x )
        If ch
            x :+ 1; y :+ ch
            
        ' Evaluate .
        ElseIf pattern[y] = wildcardAscNlazy
            y :+ 1
            If y &gt;= pattern.length Return True
            While x &lt; str.length And y &lt; pattern.length
                ch = matchChar( pattern, y, str, x )
                If ch Exit Else x :+ 1
            Wend
            
        ' Evaluate %
        ElseIf pattern[y] = wildcardAscPlazy
            y :+ 1; x :+ 1
            If y &gt;= pattern.length Return True
            While x &lt; str.length And y &lt; pattern.length
                ch = matchChar( pattern, y, str, x )
                If ch Exit Else x :+ 1
            Wend
        
        ' Evaluate *
        ElseIf pattern[y] = wildcardAscNgreedy
            y :+ 1
            If y &gt;= pattern.length Return True
            While x &lt; str.length And y &lt; pattern.length
                ch = matchChar( pattern, y, str, x )
                If ch And matchWild( pattern, str, True, x, y ) Return True Else x :+ 1
            Wend
        
        ' Evaluate +
        ElseIf pattern[y] = wildcardAscPgreedy
            y :+ 1; x :+ 1
            If y &gt;= pattern.length Return True
            While x &lt; str.length And y &lt; pattern.length
                ch = matchChar( pattern, y, str, x )
                If ch And matchWild( pattern, str, True, x, y ) Return True Else x :+ 1
            Wend
            
        ' Not a match
        Else
            Return False
            
        EndIf
        
    Forever
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >virtlands</td><td align="right"><font class=tiny>2015</font></td></tr></table></td></tr><tr ><td class="posttext"> [bookmarked] <img src="http://i37.photobucket.com/albums/e73/VirtLands/emoticons/MSN%20Emoticons/Happy/Happy_1044.gif"><br>This could be quite useful, thanks. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
