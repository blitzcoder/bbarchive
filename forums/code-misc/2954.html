<!DOCTYPE html><html lang="en" ><head ><title >Rectangle</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='find intersections and minimum bounding boxes of rectangles, language=bmx, category=Miscellaneous'><meta name='author' content='Pineapple'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=9>Miscellaneous</a>/Rectangle</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2954.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rectangle by Pineapple</td><td align="right">2012 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> find intersections and minimum bounding boxes of rectangles </td></tr><tr ><td class="cell"><pre class="code">' 	--+-----------------------------------------------------------------------------------------+--
'	  |   This code was originally written by Sophie Kirschner (sophiek@pineapplemachine.com)   |  
' 	  | It is released as public domain. Please don't interpret that as liberty to claim credit |  
' 	  |   that isn't yours, or to sell this code when it could otherwise be obtained for free   |  
'	  |                because that would be a really shitty thing of you to do.                |
' 	--+-----------------------------------------------------------------------------------------+--

SuperStrict

'example code
Rem
Graphics 512,512
SetBlend alphablend

Global r:rectangle[8],ms%=0

	r[0]=rectangle.Create(100,40,150,310)
	r[1]=rectangle.Create(50,30,160,190)
	r[2]=rectangle.Create(20,90,300,150)

Repeat
	Cls
	drs
	drr
	If MouseHit(1) Then ms:+1;ms=ms Mod 3
	Flip
Until AppTerminate() Or KeyDown(27)

Function drs()
	r[ms].x=MouseX()
	r[ms].y=MouseY()
	r[3]=r[0].intersection(r[1])
	r[4]=r[1].intersection(r[2])
	r[5]=r[2].intersection(r[0])
	r[6]=r[3].intersection(r[4])
	r[7]=r[0].container(r[1])
End Function
Function drr()
	SetAlpha 1
	For Local x%=0 To 6
		SetColor 255*(x=0 Or x=3 Or x=5),255*(x=1 Or x=3 Or x=4),255*(x=2 Or x=4 Or x=5)
		If x=6 SetColor 255,255,255
		dr r[x]
	Next
	SetColor 192,192,192
	SetAlpha 0.5
	dr r[7]
End Function
Function dr(r:rectangle)
	If Not(r And r.exists()) Then Return
	DrawRect r.x,r.y,r.w,r.h
End Function
EndRem 

Type rectangle
	Field x%,y%,w%,h%
	' return a new rectangle with the specified dimensions
	Function Create:rectangle(x%,y%,w%,h%)
		Local n:rectangle=New rectangle
		n.x=x
		n.y=y
		n.w=w
		n.h=h
		Return n
	End Function
	' return a new rectangle with the same dimensions as this one
	Method copy:rectangle()
		Return rectangle.Create(x,y,w,h)
	End Method
	' return the rectangle that is the intersection of this and another
	Method intersection:rectangle(r2:rectangle)
		Local g:rectangle=r2.copy()
		If g.x&lt;x Then g.x=x;g.w:-(x-r2.x)
		If g.y&lt;y Then g.y=y;g.h:-(y-r2.y)
		If g.x&gt;x+w Then g.x=x+w
		If g.y&gt;y+h Then g.y=y+h
		If g.x+g.w&gt;x+w Then g.w:-(g.x+g.w)-(x+w)
		If g.y+g.h&gt;y+h Then g.h:-(g.y+g.h)-(y+h)
		If g.x+g.w&lt;x Then g.w=0
		If g.y+g.h&lt;y Then g.h=0
		Return g
	End Method
	' return the smallest rectangle that contains both this and another (aka minimum bounding box)
	Method container:rectangle(r2:rectangle)
		Local g:rectangle=r2.copy()
		If g.x&gt;x Then g.x=x
		If g.y&gt;y Then g.y=y
		If g.x+g.w&lt;x+w Then g.w=w+(x-g.x)
		If g.y+g.h&lt;y+h Then g.h=h+(y-g.y)
		If g.x+g.w&lt;r2.x+r2.w Then g.w=r2.w+(r2.x-g.x)
		If g.y+g.h&lt;r2.y+r2.h Then g.h=r2.h+(r2.y-g.y)
		Return g
	End Method
	' does the rectangle exist?
	Method exists%()
		Return w&gt;0 And h&gt;0
	End Method
	' return the area of the rectangle
	Method area%()
		Return w*h
	End Method
	' return the perimeter of the rectangle
	Method perimeter%()
		Return w+w+h+h
	End Method
	' return the length of the line from one corner of the rectangle to its opposite
	Method diagonal#()
		Return ((w*w)+(h*h))^.5
	End Method
	' returns 1 if a rectangle is entirely inside this one, 0 otherwise
	Method containsrect%(r:rectangle)
		If intersection(r).equals(r) Then Return 1
		Return 0
	End Method
	' returns 1 if a point is inside this rectangle, 0 otherwise
	Method containspoint%(px%,py%)
		If (px&gt;=x And py&gt;=y And px&lt;x+w And py&lt;y+h) Then Return 1
		Return 0
	End Method
	' returns 1 if this rectangle overlaps another, 0 otherwise
	Method overlaps%(o:rectangle)
		If x+w&gt;o.x And x&lt;o.x+o.w And y+h&gt;o.y And y&lt;o.y+o.h Then Return 1
		Return 0
	End Method
	' returns 1 if this rectangle has the same dimensions as another, 0 otherwise
	Method equals%(o:rectangle)
		Return x=o.x And y=o.y And w=o.w And h=o.h
	End Method
End Type</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
