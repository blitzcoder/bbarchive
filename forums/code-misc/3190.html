<!DOCTYPE html><html lang="en" ><head ><title >Escape unsafe characters in strings</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Turns things like " into \" or ~q and back again, language=bmx, category=Miscellaneous'><meta name='author' content='Pineapple'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=9>Miscellaneous</a>/Escape unsafe characters in strings</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="3190.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Escape unsafe characters in strings by Pineapple</td><td align="right">2015 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Turns things like " into \" or ~q and back again </td></tr><tr ><td class="cell"><pre class="code">'   --+-----------------------------------------------------------------------------------------+--
'     |   This code was originally written by Sophie Kirschner (sophiek@pineapplemachine.com)   |  
'     | It is released as public domain. Please don't interpret that as liberty to claim credit |  
'     |   that isn't yours, or to sell this code when it could otherwise be obtained for free   |  
'     |                because that would be a really shitty thing of you to do.                |
'   --+-----------------------------------------------------------------------------------------+--

superstrict



' Example code

rem

framework brl.standardio

local str$ = "tab: ~t~n double quote: ~q~n single quote: '~n tilde: ~~~n backslash: \"

print "test string looks like this:"
print str

print "c escapes:"
local cescape:stringescape = new stringescape.c()
local cresult$ = cescape.escape( str )
print cresult
assert cescape.unescape( cresult ) = str

print "json escapes:"
local jsonescape:stringescape = new stringescape.json()
local jsonresult$ = jsonescape.escape( str )
print jsonresult
assert jsonescape.unescape( jsonresult ) = str

print "blitzmax escapes:"
local bmaxescape:stringescape = new stringescape.bmax()
local bmaxresult$ = bmaxescape.escape( str )
print bmaxresult
assert bmaxescape.unescape( bmaxresult ) = str

endrem



type stringescape

    field escapechar$
    field escapes$[][]
    
    ' intialize with C escapes
    method c:stringescape()
        escapechar = "\"
        escapes = [ ..
            [ "~q", "~q" ], [ "/", "/" ], ..
            [ "'", "'" ], [ "?", "?" ], ..
            [ chr($08), "b" ], .. ' backspace
            [ chr($0C), "f" ], .. ' form feed
            [ chr($0A), "n" ], .. ' newline
            [ chr($0D), "r" ], .. ' carriage return
            [ chr($09), "t" ], .. ' tab
            [ chr($0B), "v" ], .. ' vertical tab
            [ chr($07), "a" ]  .. ' alarm
        ]
        return self
    end method
    
    ' initialize with javascript escapes
    method js:stringescape()
        escapechar = "\"
        escapes = [ ..
            [ "~q", "~q" ], [ "'", "'" ], ..
            [ chr($08), "b" ], .. ' backspace
            [ chr($0C), "f" ], .. ' form feed
            [ chr($0A), "n" ], .. ' newline
            [ chr($0D), "r" ], .. ' carriage return
            [ chr($09), "t" ]  .. ' tab
        ]
        return self
    end method
    
    ' initialize with json escapes
    method json:stringescape()
        escapechar = "\"
        escapes = [ ..
            [ "~q", "~q" ], ..
            [ chr($08), "b" ], .. ' backspace
            [ chr($0C), "f" ], .. ' form feed
            [ chr($0A), "n" ], .. ' newline
            [ chr($0D), "r" ], .. ' carriage return
            [ chr($09), "t" ], .. ' tab
            [ chr($0B), "v" ]  .. ' vertical tab
        ]
        return self
    end method
    
    ' initialize with blitzmax escapes
    method bmax:stringescape()
        escapechar = "~~"
        escapes = [ ..
            [ chr($00), "0" ], .. ' null
            [ chr($09), "t" ], .. ' tab
            [ chr($0D), "r" ], .. ' return
            [ chr($0A), "n" ], .. ' newline
            [ chr($22), "q" ]  .. ' quote
        ]
        return self
    end method
    
    ' initialize with regex escapes
    method regex:stringescape()
        escapechar = "\"
        escapes = [ ..
            [ "/", "/" ], [ "^", "^" ], ..
            [ "$", "$" ], [ ".", "." ], ..
            [ "|", "|" ], [ "?", "?" ], ..
            [ "*", "*" ], [ "+", "+" ], ..
            [ "(", "(" ], [ ")", ")" ], ..
            [ "[", "[" ], [ "]", "]" ], ..
            [ "{", "{" ], [ "}", "}" ]  ..
        ]
        return self
    end method
    
    ' escape unsafe characters in input string
    method escape$( str$ )
        local out$ = ""
        for local i% = 0 until str.length
            local char$ = chr( str[i] )
            local needesc% = (char = escapechar)
            local escchar$
            if not needesc
                for local j% = 0 until escapes.length
                    if char = escapes[j][0]
                        needesc = true
                        escchar = escapes[j][1]
                        exit
                    endif
                next
            endif
            if needesc
                out :+ escapechar
                if escchar
                    out :+ escchar
                else
                    out :+ char
                endif
            else
                out :+ char
            endif
        next
        return out
    end method
    ' get escaped string with unsafe chars not escaped
    method unescape$( str$ )
        local out$ = ""
        for local i% = 0 until str.length
            local char$ = chr( str[i] )
            if char = escapechar and i-1 &lt; str.length
                local escchar$ = str[i+1]
                local subchar$ = escchar
                for local j% = 0 until escapes.length
                    if char = escapes[j][1]
                        subchar = escapes[j][0]
                        exit
                    endif
                next
                out :+ subchar
                i :+ 1
            else
                out :+ char
            endif
        next
        return out
    end method
    
end type</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pineapple</td><td align="right"><font class=tiny>2015</font></td></tr></table></td></tr><tr ><td class="posttext"> 23 Feb 2015: Added a default escape constructor for regex characters. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
