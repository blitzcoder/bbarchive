<!DOCTYPE html><html lang="en" ><head ><title >Taxi Game</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='The basics for a Taxi game..., language=bb, category=Miscellaneous'><meta name='author' content='JA2'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=9>Miscellaneous</a>/Taxi Game</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2065.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taxi Game by JA2</td><td align="right">2007 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This is just some basic code for a Crazy Taxi type of game. It can be configured to allow the player to pickup between 1 and 99 passengers at once :)<br><br>Control the orange dot with the arrow keys. Collect the people (blue squares) and take them to their destination. Press space to add new people to the map. </td></tr><tr ><td class="cell"><pre class="code">Graphics3D 640,480,16,2
SetBuffer BackBuffer()
SeedRnd MilliSecs()

;-------------------;
; Types and Globals ;-----------------------------------------------------
;-------------------;

Type Person

	Field Start,Dest,X,Y,Pos
	
End Type

Type Location

	Field Num,X,Y,Ppl,MaxPpl,Name$,Pos[99]
	
End Type

Type Player

	Field X,Y,Ppl,MaxPpl,Dest[99],Score
	
End Type

;--------;
; Create ;----------------------------------------------------------------
;--------;

Function CreateLocation (Num,X,Y,MaxPpl,Name$)

Local LN.Location

LN.Location = New Location
LN\Num		= Num
LN\X		= X
LN\Y		= Y
LN\MaxPpl	= MaxPpl
LN\Name		= Name

End Function

Function CreatePerson (Location)

Local PN.Person,LN.Location,A

PN.Person 	= New Person
PN\Start	= Location

Repeat

	PN\Dest = Rand (1,5)
	
Until PN\Dest &lt;&gt; PN\Start

For LN.Location = Each Location

	If PN\Start = LN\Num
	
		LN\Ppl 	= LN\Ppl + 1
		PN\X	= LN\X
		PN\Y	= LN\Y + 10
		
		For A = 0 To LN\MaxPpl
		
			If LN\Pos[A] = 0
			
				LN\Pos[A]	= 1
				PN\Pos		= A
				
				Exit				
			
			EndIf
		
		Next
	
	EndIf

Next

End Function

Function CreatePlayer (X,Y,Max)

Local PR.Player

PR.Player 	= New Player
PR\X		= X
PR\Y		= Y
PR\MaxPpl	= Max

End Function

;--------;
; Update ;----------------------------------------------------------------
;--------;

Function UpdateGame()

UpdatePlayers()
UpdatePeople()
UpdateLocations()

End Function

Function UpdatePlayers()

Local PR.Player,PN.Person,LN.Location,A

For PR.Player = Each Player

	PR\X = PR\X + KeyDown(205)-KeyDown(203)
	PR\Y = PR\Y + KeyDown(208)-KeyDown(200)

	Color 250,200,0
	Oval PR\X-3,PR\Y-3,6,6,True
	Color 255,255,0
	
	For A = 0 To PR\MaxPpl-1
	
		Text 550,A*15+40,PR\Dest[A],True,True
	
	Next
	
	Text 550,20,"Score: "+PR\Score,True,True
	
	For PN.Person = Each Person
	
		If RectsOverlap (PN\X-2+PN\Pos*8,PN\Y-2,4,4,PR\X-3,PR\Y-3,6,6)
		
			If PR\Ppl &lt; PR\MaxPpl
			
				PR\Ppl = PR\Ppl + 1
				
				For A = 0 To PR\MaxPpl
				
					If PR\Dest[A] = 0
					
						PR\Dest[A] = PN\Dest
						
						For LN.Location = Each Location
						
							If LN\Num = PN\Start
							
								LN\Ppl			= LN\Ppl - 1
								LN\Pos[PN\Pos]	= 0
							
							EndIf
						
						Next
						
						Delete PN
						
						Exit
					
					EndIf
				
				Next
			
			EndIf
		
		EndIf
	
	Next

	For LN.Location = Each Location
	
		If RectsOverlap (LN\X-5,LN\Y-5,10,10,PR\X-3,PR\Y-3,6,6)
		
			For A = 0 To PR\MaxPpl
			
				If PR\Dest[A] = LN\Num
				
					PR\Score	= PR\Score + 100
					PR\Ppl		= PR\Ppl - 1
					PR\Dest[A] 	= 0
				
				EndIf
			
			Next
		
		EndIf
	
	Next
	
Next

End Function

Function UpdateLocations()

Local LN.Location

For LN.Location = Each Location

	Color 255,0,0
	Rect LN\X-5,LN\Y-5,10,10,True
	Color 0,255,0
	Text LN\X,LN\Y-30,LN\Name+"("+LN\Num+")",True,True
	Text LN\X,LN\Y-15,LN\Ppl+"/"+LN\MaxPpl,True,True

Next

End Function

Function UpdatePeople()

Local PN.Person

For PN.Person = Each Person

	Color 0,0,255
	Rect PN\X-2+PN\Pos*8,PN\Y-2,4,4,True
	Color 0,255,255
	Text PN\X+PN\Pos*8,PN\Y+10,PN\Dest,True,False
	
Next

End Function

;-----------;
; Game Loop ;-------------------------------------------------------------
;-----------;

Setup()

Function Setup()

CreateLocation (01,090,050,02,"Church")
CreateLocation (02,320,100,12,"School")
CreateLocation (03,400,400,05,"Beach")
CreateLocation (04,600,420,03,"Bridge")
CreateLocation (05,120,380,08,"Grocery")

CreatePerson (01)
CreatePerson (02)
CreatePerson (02)
CreatePerson (02)
CreatePerson (03)
CreatePerson (03)
CreatePerson (05)
CreatePerson (05)
CreatePerson (05)

CreatePlayer (320,240,9)

Game()

End Function

Function Game()

Local LN.Location,PN.Person,PR.Player,A,NewLocation

Repeat
Cls

UpdateGame()

If KeyHit (57)

	NewLocation = Rand (1,5)

	For LN.Location = Each Location
	
		If LN\Num = NewLocation
		
			If LN\Ppl &lt; LN\MaxPpl
			
				CreatePerson (LN\Num)
			
			EndIf
		
		EndIf
	
	Next

EndIf

Flip
Until KeyDown (1)
End

End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
