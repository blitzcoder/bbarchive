<!DOCTYPE html><html lang="en" ><head ><title >Animating Fields</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Code to animate the fields of objects over time., language=bmx, category=Miscellaneous'><meta name='author' content='N'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=9>Miscellaneous</a>/Animating Fields</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2567.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Animating Fields by N</td><td align="right">2009 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This code isn't going to be fast, it's mostly just for the sake of prototyping stuff (which is why I wrote it, I needed to do something very quickly and execution speed was not an issue).  Its main use is in animating the properties of objects - say you have an object with an Alpha field you want to set to 0 (its starting value being whatever was in there when the animation started) over 5 seconds, you can do that using <i>Animate(theObject, "Alpha", 0, 5000)</i>.<br><br>For example, here's a quick program I wrote to visualize a timeline.  It's not pretty code, but it works.<br><br><i>timeline.bmx</i><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict

Type Entry
	Field Year%
	Field Month%
	Field Day%
	Field Title$
	Field Description$
	Field Scale!=0
	Field Alpha!=0
	
	Method Number:Int()
		Return year*10000+month*100+day
	End Method
	
	Method Compare:Int(other:Object)
		Local na, nb
		na = Number()
		nb = Entry(other).Number()
		Return (na&lt;nb)*-1+(na &gt; nb)*1
	End Method
	
	Method ToString$()
		Return ",".Join([String(year), String(month), String(day), title, description])
	End Method
End Type

SetGraphicsDriver(GLMax2DDriver())
Graphics(1440, 450, 0, 0)

Global Entries:TList = New TList

'#region Read entries

Local istream:TStream = ReadFile("entries.csv")
While Not istream.Eof()
	Local entry:Entry = New Entry
	Local values$[] = istream.ReadLine().Split(",")
	If values.Length &gt;= 1 Then entry.Year = values[0].ToInt()
	If values.Length &gt;= 2 Then entry.Month = values[1].ToInt()
	If values.Length &gt;= 3 Then entry.Day = values[2].ToInt()
	If values.Length &gt;= 4 Then entry.Title = values[3]
	If values.Length &gt;= 5 Then entry.Description = values[4]
	Entries.AddLast(entry)
	DebugLog entry.ToString()
	Animate(entry, "Scale", .5, 1000)
	Animate(entry, "Alpha", .4, 500)
Wend
istream.Close();istream=Null
Entries.Sort()

'#endregion

Local range:Int[] = [Entry(Entries.First()).Year, Entry(Entries.Last()).Year]
Local left:Int = 200
Local right:Int = 80
Local height:Int = 16
Local entryheight:Int = 24

SetBlend(MASKBLEND)

Local selected:TLink = Entries.FirstLink()
Animate(selected.Value(), "Scale", 1, 500)

left = TextWidth(Entry(selected.Value()).Title)*2

Animation.EnableAutoUpdate()

'buildopt:gui
While Not AppTerminate() And Not KeyHit(KEY_ESCAPE)
	
	If KeyHit(KEY_LEFT) Then
		Animate(selected.value(), "Scale", .5, 200)
		Animate(selected.value(), "Alpha", .4, 200)
		selected = selected.PrevLink()
		If selected = Null Then
			selected = Entries.LastLink()
		EndIf
		Animate(selected.Value(), "Scale", 1, 200)
		Animate(selected.value(), "Alpha", 1, 200)
	ElseIf KeyHit(KEY_RIGHT) Then
		Animate(selected.value(), "Scale", .5, 200)
		Animate(selected.value(), "Alpha", .4, 200)
		selected = selected.NextLink()
		If selected = Null Then
			selected = Entries.FirstLink()
		EndIf
		Animate(selected.Value(), "Scale", 1, 200)
		Animate(selected.value(), "Alpha", 1, 200)
	EndIf
	
	Local sx:Int = left
	Local sy:Int = GraphicsHeight()-100
	Local size:Int = GraphicsWidth()-(left+right)
	
	Cls
	
	SetScale(1,1)
	SetHandle(0,0)
	
	SetRotation(0)
	
	SetAlpha(1)
	SetColor(90,10,2)
	DrawRect(sx, sy, size, height)
	
	For Local e:Entry = EachIn Entries
		Local ox!
		ox = sx + size*(Double(e.year-range[0])/Double(range[1]-range[0]))
		
		SetColor(255,240,33)
		SetScale(1,1)
		SetLineWidth(5*e.Scale)
		SetHandle(0,0)
		SetRotation(0)
		SetAlpha(1)
		DrawLine(ox, sy+height+4, ox, sy-entryheight-((e.Scale-.5)*2)*24)
		
		Local scale! = Sin(e.Scale*90)*(e.Scale*2)
		SetScale(scale, scale)
		SetRotation(20*((e.Scale-.5)*2)-6*(1.0-(e.Scale-.5)*2))
		SetHandle((TextWidth(e.title)+4)*((e.Scale-.5)*2), TextHeight(e.title)*.5)
		Local oy! = sy-entryheight-((e.Scale-.5)*2)*24
		
		'#region shadow
		SetColor(0,0,0)
		DrawText(e.title, ox-2, oy-2)
		DrawText(e.title, ox-2, oy+2)
		DrawText(e.title, ox+2, oy-2)
		DrawText(e.title, ox+2, oy+2)
		'#endregion
		SetColor(e.Alpha*255, e.Alpha*255, e.Alpha*255)
		DrawText(e.title, ox, oy)
		SetHandle(-4, TextHeight(e.year)*.5)
		
		oy = sy+height+4
		
		'#region shadow
		SetColor(0,0,0)
		DrawText(e.year, ox-2, oy-2)
		DrawText(e.year, ox-2, oy+2)
		DrawText(e.year, ox+2, oy-2)
		DrawText(e.year, ox+2, oy+2)
		'#endregion
		SetColor(e.Alpha*255, e.Alpha*255, e.Alpha*255)
		DrawText(e.year, ox, oy)
		
	Next
	
	Flip
	
Wend

EndGraphics()
</textarea><br><br><i>entries.csv</i><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">1498,,,Vasco da Gama Arrives in Calicut,
1468,,,Sonni Ali siezes Timbuktu from Berber,
1492,,,Christopher Columbus's First Voyage to the Americas,
1503,,,Last Voyages of Columbus,
1519,,,Beginning of Spanish Conquest of Mexico,
1522,,,End of Spanish Conquest of Mexico,
1531,,,Beginning of Pizarro's Conquest of the Inkas,
1536,,,Ending of Pizarro's Conquest of the Inkas,</textarea><br><br>And now I go to sleep, because I have had a very stressful day, my eyes are bright red, and I'm exhausted.  I'm really just not cut out for anything... </td></tr><tr ><td class="cell"><pre class="code">Strict

Type Animation
	?Threaded
	Global AnimationsLock:TMutex = TMutex.Create()
	?
	Global Animations:TList = New TList
	Global AnimationTimer:TTimer = TTimer.Create(60, Null)
	
	Field o:Object		' Object being animated, used to keep pointer to object in scope during animation
	Field f:TField
	Field duration:Double
	Field stime:Int
	Field start:Double
	Field finish:Double
	Field fn:Double(s:Double,f:Double,t:Double)
'	Field last:Double ' check to see if the value has changed since last time
	
	Method Update(ctime%=-1)
		If ctime=-1 Then ctime = Millisecs()
		Local time! = (ctime-stime)/duration
		If time &gt;= 1! Then
			?Threaded
			AnimationsLock.Lock
			?
			Animations.Remove(Self)
			?Threaded
			AnimationsLock.Unlock
			?
			If Not fn Then f.SetDouble(o, finish)
		Else
			Local nv!
			If fn Then
				nv = fn(start, finish, Min(time,1))
			Else
				nv = start+((finish-start)*Min(time,1))
			EndIf
			f.SetDouble(o, nv)
		EndIf
	End Method
	
	Function UpdateAnimations()
		?Threaded
		AnimationsLock.Lock
		?
		If Not Animations.IsEmpty() Then
			Local ctime% = Millisecs()
			Local anims:TList = Animations.Copy()
			For Local a:Animation = EachIn Animations
				a.Update(ctime)
			Next
		EndIf
		?Threaded
		AnimationsLock.Unlock
		?
	End Function
	
	Function tick_UpdateAnimations:Object(id%, data:Object, ctx:Object)
		Local event:TEvent = TEvent(data)
		If event And event.id = EVENT_TIMERTICK And event.source = AnimationTimer Then
			UpdateAnimations()
			Return Null ' event handled
		EndIf
		
		Return data
	End Function
	
	Function EnableAutoUpdate()
		AddHook(EmitEventHook, Animation.tick_UpdateAnimations, Null, 1000)
	End Function
	
	Function DisableAutoUpdate()
		RemoveHook(EmitEventHook, Animation.tick_UpdateAnimations, Null)
	End Function
End Type

Function Animate(obj:Object, value$, newvalue!, duration!=5000, fn:Double(start:Double, finish:Double, time:Double)=Null)
	Local a:Animation
	
	?Threaded
	AnimationsLock.Lock
	?
	
	If Not Animation.Animations.IsEmpty() Then
		For a = EachIn Animation.Animations
			If a.o = obj And a.f.Name().ToLower() = value.ToLower() Then
				a.start = a.f.GetDouble(obj)
				a.finish = newvalue
				a.duration = duration
				a.stime = Millisecs()
				?Threaded
				AnimationsLock.Unlock
				?
				Return
			EndIf
		Next
	EndIf
	
	a = New Animation
	a.o = obj
	a.f = TTypeID.ForObject(obj).FindField(value)
	a.duration = duration
	a.stime = Millisecs()
	a.start = a.f.GetDouble(obj)
	a.finish = newvalue
	a.fn = fn
	Animation.Animations.AddLast(a)
	
	?Threaded
	AnimationsLock.Unlock
	?
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
