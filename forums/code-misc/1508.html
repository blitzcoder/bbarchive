<!DOCTYPE html><html lang="en" ><head ><title >Simple Event Handling</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='BlitzMax port of Gamedev.net tutorial source, language=bmx, category=Miscellaneous'><meta name='author' content='deps'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=9>Miscellaneous</a>/Simple Event Handling</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1508.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Simple Event Handling by deps</td><td align="right">2005 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Found a simple event handling tutorial and wanted to try it out in BlitzMax.<br><br>The original one can be found here: <a href="http://www.gamedev.net/reference/articles/article2141.asp" target="_blank">http://www.gamedev.net/reference/articles/article2141.asp</a><br>Another one here: http://gpwiki.org/index.php/Programming_Techniques:Events<br><br>Usage example:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Include "events.bmx"

' Some events
Const E_NEWGAME = 0
Const E_PAUSEGAME = 1
Const E_RESUMEGAME = 2
Const E_STOPGAME = 3
Const E_APPCLOSE = 4
Const E_INCSCORE = 5
Const E_POWERUP = 6




Type A Extends EventHandler

	Method EventHandler( e:Tevent )
		Select e.t
		
			Case E_NEWGAME
				Print "A: New game started"
			Case E_PAUSEGAME
				Print "A: Tea time"
			Case E_RESUMEGAME
				Print "A: No tea for you!"
			Case E_STOPGAME
				Print "A: *Chicken noises*"
			Case E_APPCLOSE
				Print "A: Shutdown"				
							
		EndSelect	
	EndMethod

EndType


Type B Extends EventHandler

	Method EventHandler( e:Tevent )
		Select e.t
		
			Case E_PAUSEGAME
				Print "B: Tea time"
			Case E_RESUMEGAME
				Print "B: No tea for you!"
			Case E_INCSCORE							
				Print "B: Yay! Score! Got "+e.arg1+" points!"
			Case E_POWERUP
				Print "B: Powerup taken"
		EndSelect	
	EndMethod

EndType


Type C Extends EventHandler

	Method EventHandler( e:Tevent )
		Select e.t
		
			Case E_PAUSEGAME
				Print "C: Tea time"
			Case E_RESUMEGAME
				Print "C: No tea for you!"
		EndSelect	
	EndMethod

EndType



Print "- Creating A and B"
Local a1:A = New A ; a1.init()
Local b1:B = New B ; b1.init()

Print "- Sending events"
EventDispatcher.SendEvent( E_NEWGAME )
EventDispatcher.SendEvent( E_INCSCORE, 1000 )
EventDispatcher.SendEvent( E_PAUSEGAME )

Print "- Creating C"
Local c1:C = New C ; c1.init()

Print "- Sending some more events"
EventDispatcher.SendEvent( E_RESUMEGAME )
EventDispatcher.SendEvent( E_POWERUP )
</textarea> </td></tr><tr ><td class="cell"><pre class="code">Type Tevent
	Field t:Int
	Field arg1:Int, arg2:Int
EndType


Type EventHandler

	' Overload this
	Method EventHandler( e:Tevent ) 
	EndMethod


	Field _nexthandler:EventHandler

	Method init()
		EventDispatcher.RegisterHandler(Self)
	EndMethod
	

	Method GetNextHandler:EventHandler()
		Return _nexthandler
	EndMethod
	

	Method SetNextHandler( n:EventHandler )
		_nexthandler = n
	EndMethod
	
	
	Method SendEvent( event_type:Int, arg1:Int = 0, arg2:Int = 0 )
		EventDispatcher.SendEvent( event_type, arg1, arg2 )
	EndMethod

EndType




Type EventDispatcher

	Global _devicelist:EventHandler

	Function RegisterHandler( device:EventHandler )
		device.SetNextHandler( _devicelist )
		_devicelist = device
	EndFunction
	
	Function SendEvent( event_type:Int, arg1:Int = 0, arg2:Int = 0 )
		Local e:Tevent = New Tevent
		e.t = event_type
		e.arg1 = arg1
		e.arg2 = arg2
		Local curDevice:EventHandler = _deviceList
		While curDevice
			curDevice.EventHandler(e)
			curDevice = CurDevice.GetNextHandler()
		Wend
	EndFunction


EndType</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Macguffin</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> Thanks!  Adapting this to the needs of my game - much appreciated. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
