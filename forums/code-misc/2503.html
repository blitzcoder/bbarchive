<!DOCTYPE html><html lang="en" ><head ><title >Number Class Cluster</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Type for boxing primitive types as objects (int, float, double, etc.), language=bmx, category=Miscellaneous'><meta name='author' content='N'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=9>Miscellaneous</a>/Number Class Cluster</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2503.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Number Class Cluster by N</td><td align="right">2009 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This is probably going to win me the Most Useless Code award, but being able to have a list or map of numbers can be handy at times, however infrequently those times seem to come up.<br><br>Anyway, this is based off the same sort of idea for NSNumber, only the implementation is far simpler (if you've ever seen the source for GNUstep, you'd probably understand).<br><br>The TNumber subclasses are hidden to completely ensure you're not going to modify the _value field of the object, since the object is supposed to be considered immutable. </td></tr><tr ><td class="cell"><pre class="code">SuperStrict

Public

Const DOUBLE_DELTA! = 4.94065645841247e-324!
Const FLOAT_DELTA# = 1.4e-45#

' Used mainly for comparing types
Const TYPE_INVALID:Int = -1
Const TYPE_BOOL:Int = 0
Const TYPE_BYTE:Int = 1
Const TYPE_SHORT:Int = 2
Const TYPE_INT:Int = 3
Const TYPE_LONG:Int = 4
Const TYPE_FLOAT:Int = 5
Const TYPE_DOUBLE:Int = 6

Type TNumber Abstract
	Function ForDouble:TNumber(v!)
		Type TDouble Extends TNumber
			Field _value:Double

			Method InitWithDouble:TDouble(v!)
				_value = v
				Return Self
			End Method

			Method DoubleValue!()
				Return _value
			End Method

			Method FloatValue#()
				Return Float(_value)
			End Method

			Method ByteValue@()
				Return Byte(_value)
			End Method

			Method ShortValue@@()
				Return Short(_value)
			End Method

			Method IntValue%()
				Return Int(_value)
			End Method

			Method LongValue:Long()
				Return Long(_value)
			End Method

			Method BoolValue:Int()
				Return Int(_value)&gt;0
			End Method

			Method GetType:Int()
				Return TYPE_DOUBLE
			End Method

			Method ToString:String()
				Return String(_value)
			End Method
		End Type
		
		Return New TDouble.InitWithDouble(v)
	End Function

	Function ForFloat:TNumber(v#)
		Type TFloat Extends TNumber
			Field _value:Float

			Method InitWithFloat:TFloat(v#)
				_value = v
				Return Self
			End Method

			Method DoubleValue!()
				Return Double(_value)
			End Method

			Method FloatValue#()
				Return Float(_value)
			End Method

			Method ByteValue@()
				Return Byte(_value)
			End Method

			Method ShortValue@@()
				Return Short(_value)
			End Method

			Method IntValue%()
				Return Int(_value)
			End Method

			Method LongValue:Long()
				Return Long(_value)
			End Method

			Method BoolValue:Int()
				Return Int(_value)&gt;0
			End Method

			Method GetType:Int()
				Return TYPE_FLOAT
			End Method

			Method ToString:String()
				Return String(_value)
			End Method
		End Type
		
		Return New TFloat.InitWithFloat(v)
	End Function

	Function ForByte:TNumber(v@)
		Type TByte Extends TNumber
			Field _value:Byte

			Method InitWithByte:TByte(v:Byte)
				_value = v
				Return Self
			End Method

			Method DoubleValue!()
				Return Double(_value)
			End Method

			Method FloatValue#()
				Return Float(_value)
			End Method

			Method ByteValue@()
				Return _value
			End Method

			Method ShortValue@@()
				Return Short(_value)
			End Method

			Method IntValue%()
				Return Int(_value)
			End Method

			Method LongValue:Long()
				Return Long(_value)
			End Method

			Method BoolValue:Int()
				Return _value&gt;0
			End Method

			Method GetType:Int()
				Return TYPE_BYTE
			End Method

			Method ToString:String()
				Return String(_value)
			End Method
		End Type
		
		Return New TByte.InitWithByte(v)
	End Function

	Function ForShort:TNumber(v@@)
		Type TShort Extends TNumber
			Field _value:Short

			Method InitWithShort:TShort(v:Short)
				_value = v
				Return Self
			End Method

			Method DoubleValue!()
				Return Double(_value)
			End Method

			Method FloatValue#()
				Return Float(_value)
			End Method

			Method ByteValue@()
				Return Byte(_value)
			End Method

			Method ShortValue@@()
				Return _value
			End Method

			Method IntValue%()
				Return Int(_value)
			End Method

			Method LongValue:Long()
				Return Long(_value)
			End Method

			Method BoolValue:Int()
				Return _value&gt;0
			End Method

			Method GetType:Int()
				Return TYPE_SHORT
			End Method

			Method ToString:String()
				Return String(_value)
			End Method
		End Type
		
		Return New TShort.InitWithShort(v)
	End Function

	Function ForInt:TNumber(v%)
		Type TInt Extends TNumber
			Field _value:Int

			Method InitWithInt:TInt(v:Int)
				_value = v
				Return Self
			End Method

			Method DoubleValue!()
				Return Double(_value)
			End Method

			Method FloatValue#()
				Return Float(_value)
			End Method

			Method ByteValue@()
				Return Byte(_value)
			End Method

			Method ShortValue@@()
				Return Short(_value)
			End Method

			Method IntValue%()
				Return _value
			End Method

			Method LongValue:Long()
				Return Long(_value)
			End Method

			Method BoolValue:Int()
				Return _value&gt;0
			End Method

			Method GetType:Int()
				Return TYPE_INT
			End Method

			Method ToString:String()
				Return String(_value)
			End Method
		End Type
		
		Return New TInt.InitWithInt(v)
	End Function

	Function ForLong:TNumber(v:Long)
		Type TLong Extends TNumber
			Field _value:Long

			Method InitWithLong:TLong(v:Long)
				_value = v
				Return Self
			End Method

			Method DoubleValue!()
				Return Double(_value)
			End Method

			Method FloatValue#()
				Return Float(_value)
			End Method

			Method ByteValue@()
				Return Byte(_value)
			End Method

			Method ShortValue@@()
				Return Short(_value)
			End Method

			Method IntValue%()
				Return Int(_value)
			End Method

			Method LongValue:Long()
				Return _value
			End Method

			Method BoolValue:Int()
				Return _value&gt;0
			End Method

			Method GetType:Int()
				Return TYPE_LONG
			End Method

			Method ToString:String()
				Return String(_value)
			End Method
		End Type
		
		Return New TLong.InitWithLong(v)
	End Function

	Function ForBool:TNumber(b:Int)
		Type TBool Extends TNumber
			Field _value:Int

			Method InitWithBool:TBool(v:Int)
				_value = v&gt;0
				Return Self
			End Method

			Method DoubleValue!()
				Return Double(_value)
			End Method

			Method FloatValue#()
				Return Float(_value)
			End Method

			Method ByteValue@()
				Return Byte(_value)
			End Method

			Method ShortValue@@()
				Return Short(_value)
			End Method

			Method IntValue%()
				Return _value
			End Method

			Method LongValue:Long()
				Return Long(_value)
			End Method

			Method BoolValue:Int()
				Return _value
			End Method

			Method ToString:String()
				Return String(_value)
			End Method

			Method GetType:Int()
				Return TYPE_BOOL
			End Method
		End Type
		
		Return New TBool.InitWithBool(b)
	End Function

	Method DoubleValue!() Abstract
	Method FloatValue#() Abstract
	Method ByteValue@() Abstract
	Method ShortValue@@() Abstract
	Method IntValue%() Abstract
	Method LongValue:Long() Abstract
	Method BoolValue:Int() Abstract
	Method ToString:String() Abstract

	Method GetType:Int() Abstract

	Method Compare:Int(other:Object)
		Local n:TNumber = TNumber(other)
		If n Then
			Local _type:Int, t2:Int
			_type = GetType()
			t2 = GetType()

			If _type = TYPE_INVALID Or t2 = TYPE_INVALID Then
				Throw "Attempt to compare invalid number"
			EndIf

			If t2 &gt; _type Then
				_type = t2
			EndIf

			Select _type
				Case TYPE_BOOL
					Local b1%, b2%
					b1 = n.BoolValue()
					b2 = n.BoolValue()
					If b1 = b2 Then
						Return 0
					ElseIf b1 Then
						Return 1
					EndIf
					Return -1
				Case TYPE_DOUBLE
					Local d! = DoubleValue()-n.DoubleValue()
					If d &lt; -DOUBLE_DELTA Then
						Return -1
					ElseIf d &gt; DOUBLE_DELTA then
						Return 1
					EndIf
					Return 0
				Case TYPE_FLOAT
					Local f! = FloatValue()-n.FloatValue()
					If f &lt; -FLOAT_DELTA Then
						Return -1
					ElseIf f &gt; FLOAT_DELTA then
						Return 1
					EndIf
					Return 0
				Case TYPE_LONG
					Local l1:Long, l2:Long
					l1 = LongValue()
					l2 = n.LongValue()
					If l1 = l2 Then
						Return 0
					ElseIf l1 &lt; l2 Then
						Return -1
					EndIf
					Return 1
				Default ' int and under
					Local i1%, i2%
					i1 = IntValue()
					i2 = n.IntValue()
					If i1 = i2 Then
						Return 0
					ElseIf i1 &lt; i2 Then
						Return -1
					EndIf
					Return 1
			End Select
		EndIf
		Return Super.Compare(other)
	End Method
End Type</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warpy</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> Interesting use of DOUBLE_DELTA and FLOAT_DELTA in the compare method, I wouldn't've thought of that.<br><br>I prefer to have modified list/map types which take numbers directly, and mark seems happy enough to cast to strings, but horses for courses I suppose. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >N</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> Suppose I should mention that I just added a sort of bug fix, since previously bools would only be equal if both were true.  Why I thought this was correct when I wrote it is beyond me, but that's fixed. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Otus</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> Something looks wrong here:<br><pre class=code>			Local _type:Int, t2:Int
			_type = GetType()
			t2 = GetType()</pre><br><br>Am I missing something: why is TNumber not Abstract? <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >N</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> It's not abstract so that you can, for whatever reason you would want to, do New TNumber.InitWithDouble(x).<br><br>Thinking about it now though, I'll probably just remove those... <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Zakk</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> I usually use bytes for my bools :) <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
