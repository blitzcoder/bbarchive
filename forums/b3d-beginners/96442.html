<!DOCTYPE html><html lang="en" ><head ><title >Grabbing an Image over the web</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Grabbing an Image over the web</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Grabbing an Image over the web</a><br><br>
<a name="1116041"></a>

<a name="1116042"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CodeOrc</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi All,<br><br> Been in the code archs, the forums, if I missed it please point me in the right direction.<br><br>What I wanna' do is pull an image from my server upon game boot<br><br>I have this that don't work.<br><br><pre class=code>
 filein$="www.myserver.com/dir1/image1.jpg"

ad_image=LoadImage(filein$)
</pre><br><br>anyone doing this or know what to do?<br><br>thanx in advance<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1116049"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> The reason that wont work as it stands is because Blitz (via its windows framework) cannot access files over the internet directly, any filepath beginning "http:\\" or "www." will fail since Load needs a path localised (LoadImage("../Image.bmp")), on a specified Harddrive path (LoadImage("C:\Folder\Image.bmp")) or a local LAN Server (LoadImage"\\Server\Share\Image.bmp") Otherwise, it's just not a path that Blitz can follow.<br><br>Before you can use the LoadImage command, you need to actually Downloa the image to somewhere, then Load it in from that dowload location.<br><br>Have a look in the CoeArcs for "BlitzGet" ( BlitzGetDeluxe?) or similar which is a great and very simple means to downloa web-based files<br><br>If you're still stuck, post back and I'l try top find the link and show an example :) <br><br></td></tr></table><br>
<a name="1116051"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CodeOrc</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you Malice for the info, looking at it now.<br><br><br>Looks like I should be able to figure this out, but if not then yep, I'll post some code with a question ^^ <br><br></td></tr></table><br>
<a name="1116057"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> This should be the sport of thing you need... It's basically just a complete copy of the BlitzGetDeluxe code with the example details changed a little to suit your requirements above (I also made sure it downloaded to the CurrentDir() rather than in the windows folder so it's easier to read the downloaded file back)<br><br>It's kinda messy overall, but at least you only really need to call the BlitzGet() function to achieve what you want.<br>If you feel more confident, of course, you can tinker with the functions, but mostly it shouldn't be necessary aside from ensuring your game isn't cluttered with the BlitzGet "Print" messages etc.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics 640, 480, 0, 2
SetBuffer BackBuffer ()

	download$	 = "http:\\www.myserver.com/dir1/image1.jpg"
; SAVE LOCATION: Windows Temp folder:
	downloadDir$ = CurrentDir() 

If BlitzGet (download$, downloadDir$, "")
	TESTIMAGE=LoadImage(downloaddir+"image1.jpg")
	DrawImage TESTIMAGE,0,0
	Flip
	WaitKey()
	End	
Else
	RuntimeError"Download error!"
EndIf

; Temporary, For quick results...

Global header$
Global bytesToRead
Global date$
Global server$
Global contentType$
Global initialReply$

; -----------------------------------------------------------------------------
; File download function
; -----------------------------------------------------------------------------
; webFile$  -- file to download
; saveDir$  -- directory to download into
; saveFile$ -- filename to save as (use "" to use name of downloaded file automatically)
; -----------------------------------------------------------------------------
; Note that if you just provide a web server address, the document downloaded will
; be named "Unknown file.txt"
; -----------------------------------------------------------------------------

Function BlitzGet (webFile$, saveDir$, saveFile$)

	.requestfile; NEW/MODIFIED 2009 (re-direct support)
	
	; RE-OPEN STREAM!
	
	DebugLog webFile$
	
	; -------------------------------------------------------------------------
	; Strip "http://" if provided
	; -------------------------------------------------------------------------
	If Left (webFile$, 7) = "http://" Then webFile$ = Right (webFile$, Len (webFile$) - 7)

	; -------------------------------------------------------------------------
	; Split into hostname and path/filename to download
	; -------------------------------------------------------------------------
	slash = Instr (webFile$, "/")
	If slash
		webHost$ = Left (webFile$, slash - 1)
		webFile$ = Right (webFile$, Len (webFile$) - slash + 1)
	Else
		webHost$ = webFile$
		webFile$ = "/"
	EndIf
	
	DebugLog webHost$
	DebugLog webFile$
	
	If Not www ; NEW/MODIFIED 2009 (re-direct support)

		; -------------------------------------------------------------------------
		; Add trailing slash to download dir if not given
		; -------------------------------------------------------------------------
		If Right (saveDir$, 1) &lt;&gt; "\" Then saveDir$ = saveDir$ + "\"
	
		; -------------------------------------------------------------------------
		; Save filename -- get from webFile$ if not provided
		; -------------------------------------------------------------------------
		If saveFile$ = ""
			If webFile = "/"
				saveFile$ = "Unknown file.txt"
			Else
				For findSlash = Len (webFile$) To 1 Step - 1
					testForSlash$ = Mid (webFile$, findSlash, 1)
					If testForSlash$ = "/"
						saveFile$ = Right (webFile$, Len (webFile$) - findSlash)
						Exit
					EndIf
				Next
				If saveFile$ = "" Then saveFile$ = "Unknown file.txt"
			EndIf
		EndIf

	EndIf
	
	; DEBUG
	; RuntimeError "Web host: " + webHost$ + Chr (10) + "Web file: " + webFile$ + Chr (10) + "Save dir: " + saveDir$ + Chr (10) + "Save file: " + saveFile$

	www = OpenTCPStream (webHost$, 80)

	If www
	
		WriteLine www, "GET " + webFile$ + " HTTP/1.1" ; GET / gets default page...
		WriteLine www, "Host: " + webHost$
		WriteLine www, "User-Agent: BlitzGet Deluxe"
		WriteLine www, "Accept: */*"
		WriteLine www, ""
		
		; ---------------------------------------------------------------------
		; NEW/MODIFIED 2009 (re-direct support)
		; ---------------------------------------------------------------------
		; Get HTTP response (200 = OK, 404 = not found, etc)...
		; ---------------------------------------------------------------------

		response$ = ReadLine (www)

		DebugLog "Server response: " + response$
		
		If Left (response$, 5) = "HTTP/"
			pos = Instr (response$, " ")
			replycode$ = Mid (response$, pos + 1, 3)
		EndIf

		; ---------------------------------------------------------------------
		; Find blank line after header data, where the action begins...
		; ---------------------------------------------------------------------
		
		; Store various information along the way...
		
		Repeat
	
			Cls
			
			header$ = ReadLine (www)
	
			reply$ = ""
			pos = Instr (header$, ": ")
			If pos
				reply$ = Left (header$, pos + 1)
			EndIf
	
			Select Lower (reply$)
				Case "content-length: "
					bytesToRead = ReplyContent (header$, reply$)
				Case "date: "
					date$ = ReplyContent (header$, reply$)
				Case "server: "
					server$ = ReplyContent (header$, reply$)
				Case "content-type: "
					contentType$ = ReplyContent (header$, reply$)
				Case "location: " ; NEW/MODIFIED 2009 (re-direct support)
					location$ = ReplyContent (header$, reply$) ; NEW/MODIFIED 2009 (re-direct support)
				Default
					If gotReply = 0 Then initialReply$ = header$: gotReply = 1
			End Select
	
			DisplayResponse ()
	
			Flip
			
		Until header$ = "" Or (Eof (www))

		; ---------------------------------------------------------------------
		; NEW/MODIFIED 2009 (re-direct support)
		; ---------------------------------------------------------------------
		
		downloadfile = 0

		Select replycode$

			Case "200" ; File found...

				downloadfile = True
				
			Case "404" ; File not found...
			
				Goto skipDownLoad
			
			Case "301" ; File permanently moved...
			
				webFile$ = location$
				
				CloseTCPStream www
				www = 0 ; NEW/MODIFIED 2009 (re-direct support)

				Goto requestfile
	
			Case "302" ; File temporarily moved...
	
				webFile$ = location$

				CloseTCPStream www
				www = 0 ; NEW/MODIFIED 2009 (re-direct support)

				Goto requestfile
			
			Case "303" ; File moved...

				webFile$ = location$

				CloseTCPStream www
				www = 0 ; NEW/MODIFIED 2009 (re-direct support)

				Goto requestfile

			Case "307" ; Naughty...

				webFile$ = location$

				CloseTCPStream www
				www = 0 ; NEW/MODIFIED 2009 (re-direct support)

				Goto requestfile
				
		End Select

		; ---------------------------------------------------------------------

		If Not downloadfile Then Goto skipDownLoad ; NEW/MODIFIED 2009 (re-direct support)
		
		If bytesToRead = 0 Then Goto skipDownLoad
		
		; ---------------------------------------------------------------------
		; Create new file to write downloaded bytes into
		; ---------------------------------------------------------------------
		save = WriteFile (saveDir$ + saveFile$)
		If Not save Then Goto skipDownload

		; ---------------------------------------------------------------------
		; Incredibly complex download-to-file routine...
		; ---------------------------------------------------------------------

		For readWebFile = 1 To bytesToRead
		
			If Not Eof (www) Then WriteByte save, ReadByte (www)
			
			; Call BytesReceived with position and size every 100 bytes (slows down a LOT with smaller updates)
			
			tReadWebFile = readWebFile
			If tReadWebFile Mod 100 = 0 Then BytesReceived (readWebFile, bytesToRead)

		Next

		CloseFile save
		
		; Fully downloaded?
		If (readWebFile - 1) = bytesToRead
			success = 1
		EndIf
		
		; Final update (so it's not rounded to nearest 100 bytes!)
		BytesReceived (bytesToRead, bytesToRead)
		
		.skipDownload ; NEW/MODIFIED 2009 (re-direct support)
		CloseTCPStream www
		www = 0 ; NEW/MODIFIED 2009 (re-direct support)
		
	Else
	
		RuntimeError "Failed to connect"
		
	EndIf
	
	Return success
	
End Function

; -----------------------------------------------------------------------------
; User-defined update function, called every 100 bytes of download -- alter to suit!
; -----------------------------------------------------------------------------
; TIP: Pass a user-defined type instead, with all data (this stuff plus URL, local filename, etc)
; -----------------------------------------------------------------------------
Function BytesReceived (posByte, totalBytes)
	; Example update code...
	Cls
	Text 0, 10, "Downloading file -- please wait..."
	Text 0, 30, "Received: " + posByte + "/" + totalBytes + " bytes (" + Percent (posByte, totalBytes) + "%)"
	DisplayResponse ()
	Flip
End Function

; -----------------------------------------------------------------------------
; Handy percentage function
; -----------------------------------------------------------------------------
Function Percent (part#, total#)
	Return Int (100 * (part / total))
End Function

Function ReplyContent$ (header$, reply$)
	Return Right (header$, Len (header$) - Len (reply$))
End Function

; Temporary, for quick results...

Function DisplayResponse ()
	Text 0, 80, "Header: " + initialReply$
	Text 0, 100, "Date: " + date$
	Text 0, 120, "Server: " + server$
	Text 0, 140, "Content-Type: " + contentType$
	Text 0, 160, "Content-Length: " + bytesToRead
End Function

</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
