<!DOCTYPE html><html lang="en" ><head ><title >IDE bug?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >IDE bug?</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >IDE bug?</a><br><br>
<a name="1114421"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I didn't want to post in the bug forums because this may not be specifically B|litz related.<br><br>The issue is, as you can see by running the code (Please run in Debug Mode and wait until the STOP cmmand halts the debugging)<br><br>The offending code is just under the Stop command:<br><br><pre class=code>

	Stop	
	
	Local Found.BioForm
	For Found = Each BioForm
		If (Not(Found\Category)) ; Found/CATEGORY IS ZERO So why is the IF skipped????????????
			If (((Found\PositionX)&lt;XMax) And ((Found\PositionX)&gt;XMin) And ((Found\PositionY)&lt;YMax) And ((Found\PositionX)&gt;YMin) )
				;Double Check Range?
				If (Distance2D(Found\PositionX,Found\PositionY,X,Y)&lt;Range)
					Exit
				End If
			End If	
		End If	
	Next
</pre><br><br>I was wondering if this occus for opther people, and if so, does anyone have any insight as to WHY it's not working right?<br><br>The complete code to run is here:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics 1024,768,32
SetBuffer BackBuffer()

Const INITIAL_POPULATION_FOOD=100

; RED ARE STRONG, SLOW AND SHORT-LIVED. THEY CONSUME FOOD, DIVIDE AND DESTROY THEIR ENEMIES RAPIDLY.
Const INITIAL_POPULATION_RED=15
Const INITIAL_HEALTH_RED#=15			; Initial Health
Const HUNGER_RATE_RED=4500				; Millisecond duration since last meal after which Bioform will lose Health. Currently 1/5 of DIVISION RATE or 1/20 LIFESPAN = 1/Health
Const DIVISION_RATE_RED=22500			; Millisecond Rate at which Bioform will divide currently 1/4 of LIFESPAN
Const LIFESPAN_RED=90000 				; Milliseconds from creation until death will occur naturally
Const DIVISION_DELAY_RED=10000			; Milliseconds Period of time after division or birth during which Bioform is immobile
Const SENSE_RED#=25.0						; Distance at which senses detect food, friend or predators
Const INITIAL_SPEED_RED#=1.25			; Initial Distance travelled each frame
Const SPEED_INCREASE_RED#=0.075			; Increase in Speed gained from Eating
Const HEALTH_INCREASE_RED#=1.25			; Increase in Health gained from Eating (Also Rate at which Food is devoured)
Const DAMAGE_RED#=1.5					; Damage Inflicted Per Attack#


; GREEN GUYS ARE MODERATE IN ALL CONSIDERATION
Const INITIAL_POPULATION_GREEN=10
Const INITIAL_HEALTH_GREEN#=10			; Initial Health
Const HUNGER_RATE_GREEN=6000			; Millisecond duration since last meal after which Bioform will lose Health. Currently 1/5 of DIVISION RATE or 1/20 LIFESPAN = 1/Health
Const DIVISION_RATE_GREEN=30000			; Millisecond Rate at which Bioform will divide currently 1/4 of LIFESPAN
Const LIFESPAN_GREEN=120000 			; Milliseconds from creation until death will occur naturally
Const DIVISION_DELAY_GREEN=1000			; Milliseconds Period of time after division or birth during which Bioform is immobile
Const SENSE_GREEN#=50.0					; Distance at which senses detect food, friend or predators
Const INITIAL_SPEED_GREEN#=1.5			; Initial Distance travelled each frame
Const SPEED_INCREASE_GREEN#=0.1			; Increase in Speed gained from Eating
Const HEALTH_INCREASE_GREEN#=1.0		; Increase in Health gained from Eating (Also Rate at which Food is devoured). Health Loss through lack of food is proportional to HALF this amount.
Const DAMAGE_GREEN#=1.0					; Damage Inflicted Per Attack#


; BLUE ARE WEAK, INEFFECTIVE AGAINST ENEMIES, BUT ARE FAST, PROLIFIC AND LONG-LIVED
Const INITIAL_POPULATION_BLUE=5
Const INITIAL_HEALTH_BLUE#=5			; Initial Health
Const HUNGER_RATE_BLUE=1500				; Millisecond duration since last meal after which Bioform will lose Health. Currently 1/5 of DIVISION RATE or 1/20 LIFESPAN = 1/Health
Const DIVISION_RATE_BLUE=7500			; Millisecond Rate at which Bioform will divide currently 1/4 of LIFESPAN
Const LIFESPAN_BLUE=30000 				; Milliseconds from creation until death will occur naturally
Const DIVISION_DELAY_BLUE=500			; Milliseconds Period of time after division or birth during which Bioform is immobile
Const SENSE_BLUE#=75.0						; Distance at which senses detect food, friend or predators
Const INITIAL_SPEED_BLUE#=1.75			; Initial Distance travelled each frame
Const SPEED_INCREASE_BLUE#=0.15			; Increase in Speed gained from Eating
Const HEALTH_INCREASE_BLUE#=0.5			; Increase in Health gained from Eating (Also Rate at which Food is devoured)
Const DAMAGE_BLUE#=0.5					; Damage Inflicted Per Attack#









Const INITIAL_NUMBER_FOOD=20

Const FOOD_INITIAL_FOOD_HEALTH#=10.0		; This is the health level for initially spawned FOOD only. Bioforms that are killed leave their Category Health level of Edible Remains.
Const FOOD_LIFETIME_RATIO#=1.0			; Food lasts this ratio * Original Matter Health Minutes

Const CATEGORY_FOOD=0
Const CATEGORY_RED=1
Const CATEGORY_GREEN=2
Const CATEGORY_BLUE=3




; The fun part...

Const NO_ENEMY=0
Const RED_ENEMY=CATEGORY_BLUE
Const GREEN_ENEMY=CATEGORY_RED
Const BLUE_ENEMY=CATEGORY_GREEN




Const FLAG_DEAD=0				; No more activity
Const FLAG_SEEKING=1			; Seeking food
Const FLAG_FOUND=2				; Found Food!
Const FLAG_EATING=4				; Eating Food
Const FLAG_ATTACKING=8			; Attacking an Enemy
Const FLAG_ATTACKED=16			; Under Attack
Const FLAG_HELPING=32			; Attempting to Help a Friend!
Const FLAG_FLEEING=64			; Attempting to Flee an ENemy
Const FLAG_DIVIDING=128			; Currently Immobile due to division process

Const TOTAL_CATSTATS=10

Const CATSTATS_INITIAL_HEALTH#=0
Const CATSTATS_HEALTH_INCREASE=1
Const CATSTATS_HUNGER_RATE=2

Const CATSTATS_INITIAL_SPEED=3
Const CATSTATS_SPEED_INCREASE=4

Const CATSTATS_DIVISION_RATE=5
Const CATSTATS_LIFESPAN=6
Const CATSTATS_DIVISION_DELAY=7

Const CATSTATS_SENSE=8

Const CATSTATS_DAMAGE=9

Const CATSTATS_PREDATOR=10

Global EPOCH

Initialise

While ((MilliSecs()-(EPOCH-5000))&lt;1)
	Cls
	Text GraphicsWidth()Shr True,GraphicsHeight()Shr True, ("INITIALISING..."+Int(Floor((Abs(MilliSecs()-EPOCH)) Mod 5000)*0.002)),True,True
	Flip
Wend
Cls

While Not KeyDown(1)
	Cls
	Update
	Flip False
Wend

Type BioForm
	Field FriendlyName$
	
	Field Category
	Field CategoryStats#[TOTAL_CATSTATS]
	
	Field LastDivisionTime%
	Field LastMealTime%
	
	Field Health#
	Field Birthdate%
	Field Lifetime%
	
	; Cannot combine as Single DWord due to Floating Point accuracy required for Speed#
	Field PositionX#
	Field PositionY#
	
	Field Direction#
	
	Field SensoryFlags
	
	Field Speed#
	
	Field TargetFood.BioForm	
	Field TargetFriend.BioForm	
	Field TargetEnemy.BioForm
End Type

Function Initialise();		INITIALISE Settings and Defaults
	
	EPOCH=MilliSecs()+10000 ;DELAY
	
	Local CreateBioforms
	Local InitialDirection#
	
	Local B.BioForm
	Local RandomiseDivisionTime%
	
	For CreateBioforms=1 To INITIAL_NUMBER_FOOD
		InitialDirection#=Rnd(0.0,360.0)
		B=Object.BioForm(GenerateNewBioform(Rand(GraphicsWidth()*0.1,GraphicsWidth()*0.9),Rand(GraphicsHeight()*0.1,GraphicsHeight()*0.9),CATEGORY_FOOD,InitialDirection,EPOCH))
		B\LastDivisionTime=999999
	Next
	
	For CreateBioforms=1 To INITIAL_POPULATION_RED
		InitialDirection=Rnd(0.0,360.0)
		B=Object.BioForm(GenerateNewBioform(Rand(GraphicsWidth()*0.1,GraphicsWidth()*0.9),Rand(GraphicsHeight()*0.1,GraphicsHeight()*0.9),CATEGORY_RED,InitialDirection,EPOCH))
		B\LastDivisionTime=Rand(EPOCH,EPOCH+(B\CategoryStats[CATSTATS_DIVISION_RATE]))
	Next
	
	For CreateBioforms=1 To INITIAL_POPULATION_GREEN
		InitialDirection=Rnd(0.0,360.0)
		B=Object.BioForm(GenerateNewBioform(Rand(GraphicsWidth()*0.1,GraphicsWidth()*0.9),Rand(GraphicsHeight()*0.1,GraphicsHeight()*0.9),CATEGORY_GREEN,InitialDirection,EPOCH))
		B\LastDivisionTime=Rand(EPOCH,EPOCH+(B\CategoryStats[CATSTATS_DIVISION_RATE]))
	Next
	
	For CreateBioforms=1 To INITIAL_POPULATION_BLUE
		InitialDirection=Rnd(0.0,360.0)
		B=Object.BioForm(GenerateNewBioform(Rand(GraphicsWidth()*0.1,GraphicsWidth()*0.9),Rand(GraphicsHeight()*0.1,GraphicsHeight()*0.9),CATEGORY_BLUE,InitialDirection,EPOCH))
		B\LastDivisionTime=Rand(EPOCH,EPOCH+(B\CategoryStats[CATSTATS_DIVISION_RATE]))
	Next
End Function	

Function AI(B.BioForm);	PROCESS Aritifical Intelligence
	
	Action(B)
	
	Sense(B)
	
	Respond(B)
	
	Move(B)
	
End Function	

Function Respond(B.BioForm)
	If (B=Null)
		Return
	End If
	If (B\Category=CATEGORY_FOOD)
		Return
	End If
	
	If (CheckFlag(B,FLAG_FOUND Or FLAG_EATING))
		If (B\TargetFood&lt;&gt;Null)
			If (Distance2D(B\PositionX,B\PositionY,B\TargetFood\PositionX,B\TargetFood\PositionY)&lt;(B\Health*2))
				Confront(B,B\TargetFood)
			End If
		End If
	End If	
	
	If (CheckFlag(B,FLAG_FLEEING Or FLAG_ATTACKED Or FLAG_ATTACKING))
		If (B\TargetEnemy&lt;&gt;Null)
			If (Distance2D(B\PositionX,B\PositionY,B\TargetEnemy\PositionX,B\TargetEnemy\PositionY)&lt;(B\Health*2))
				Confront(B,B\TargetEnemy)
			End If
		End If
	End If	
	
End Function	

Function Update();			REFRESH Data
	Local B.BioForm
	For B=Each BioForm
		
		If KeyDown(1) Exit
		
		AI(B)
		Draw(B)
	Next
End Function

Function Sense(B.BioForm);	AI Senses
	If (B=Null)
		Return
	End If
	
	If (Not(B\Category))
		; FOOD Has no senses
		ClearAllFlags(B,False)
		Return
	End If
	
	; Prioritise
	
	Local BHandle=Handle(B)
	
	If (Not(CheckFlag(B,0)))
		SetFlag(B,FLAG_SEEKING)
	End If	
	
	Local Friend.BioForm=SenseForCategory(B\Category,B\PositionX,B\PositionY,B\Direction,B\CategoryStats[CATSTATS_SENSE])
	Local Food.BioForm=SenseForCategory(CATEGORY_FOOD,B\PositionX,B\PositionY,B\Direction,B\CategoryStats[CATSTATS_SENSE])
	Local Predator.BioForm=SenseForCategory(B\CategoryStats[CATSTATS_PREDATOR],B\PositionX,B\PositionY,B\Direction,B\CategoryStats[CATSTATS_SENSE])
	
	SenseClosestFood(B,Food)
	SenseClosestFriend(B,Friend)
	SenseClosestPredator(B,Predator)
	
	
End Function		

Function SenseClosestFriend(B.BioForm,Friend.BioForm)
	If (B\TargetFriend=Null)
		If (Friend=Null)
			ClearFlag(B,FLAG_HELPING)
			
			DebugLog(B\FriendlyName+" is lonely")
			
			Return
		Else
			;Just in case!
			If (Friend\Category=B\Category)
				If (Friend\TargetEnemy&lt;&gt;Null)
					If (UpdatePredator(Friend\TargetEnemy))
						If ((CheckFlag(Friend,FLAG_ATTACKED Or FLAG_ATTACKING)))
							DebugLog(B\FriendlyName+" decided to help "+Friend\FriendlyName+" in the struggle versus "+Friend\TargetEnemy\FriendlyName)
							SetFlag(B,FLAG_HELPING,Handle(Friend))
						Else
							
							If (B\TargetFood=Null) Or (CheckFlag(B,FLAG_SEEKING))
								If (Friend\TargetFood&lt;&gt;Null)
									If (CheckFlag(Friend,FLAG_EATING Or FLAG_FOUND))
										DebugLog(B\FriendlyName+" noticed that "+Friend\FriendlyName+" has found food")
										SetFlag(B,FLAG_FOUND,Handle(Friend\TargetFood))
									End If
								Else
									ClearFlag(Friend,FLAG_FOUND)
									ClearFlag(Friend,FLAG_EATING)
									DebugLog(B\FriendlyName+" noticed that "+Friend\FriendlyName+" no longer has food")				
								End If	
							End If
							ClearFlag(Friend,FLAG_ATTACKED)
							ClearFlag(Friend,FLAG_ATTACKING)
							ClearFlag(Friend,FLAG_FLEEING)
							DebugLog(B\FriendlyName+" noticed that "+Friend\FriendlyName+" is no longer in difficulty")
						End If
					End If
				End If	
			End If
		End If
	Else
		
		
		If (B\TargetFriend\Category&lt;&gt;B\Category)
			DebugLog(B\FriendlyName+" witnessed the demise of a fellow")
			Return
		End If
		
		If (Friend&lt;&gt;Null)
			
			If (Friend=B\TargetFriend)
				
				B\TargetFriend=GetClosest(B,B\TargetFriend,Friend)
				DebugLog(B\FriendlyName+" is surrounded by friends and family")
				
				If (UpdatePredator(B\TargetFriend\TargetEnemy))
					If ((CheckFlag(B\TargetFriend,FLAG_ATTACKED Or FLAG_ATTACKING)))
						DebugLog(B\FriendlyName+" decided to help "+B\TargetFriend\FriendlyName+" in the struggle versus "+B\TargetFriend\TargetEnemy\FriendlyName)
						
						SetFlag(B,FLAG_HELPING,Handle(B\TargetFriend))
					Else
						
						If (B\TargetFood=Null) Or (CheckFlag(B,FLAG_SEEKING))
							If (B\TargetFriend\TargetFood&lt;&gt;Null)
								If (CheckFlag(B\TargetFriend,FLAG_EATING Or FLAG_FOUND))
									DebugLog(B\FriendlyName+" noticed that "+B\TargetFriend\FriendlyName+" has found food")
									SetFlag(B,FLAG_FOUND,Handle(B\TargetFriend\TargetFood))
								End If
							Else
								ClearFlag(B\TargetFriend,FLAG_FOUND)
								ClearFlag(B\TargetFriend,FLAG_EATING)
								DebugLog(B\FriendlyName+" noticed that "+B\TargetFriend\FriendlyName+" no longer has food")				
							End If	
						End If
						
					End If
				Else
					ClearFlag(B\TargetFriend,FLAG_ATTACKED)
					ClearFlag(B\TargetFriend,FLAG_ATTACKING)
					ClearFlag(B\TargetFriend,FLAG_FLEEING)
					DebugLog(B\FriendlyName+" noticed that "+B\TargetFriend\FriendlyName+" is no longer in difficulty")
				End If
			Else
				
				If (UpdatePredator(B\TargetFriend\TargetEnemy))
					If ((CheckFlag(B\TargetFriend,FLAG_ATTACKED Or FLAG_ATTACKING)))
						DebugLog(B\FriendlyName+" decided to help "+B\TargetFriend\FriendlyName+" in the struggle versus "+B\TargetFriend\TargetEnemy\FriendlyName)
						SetFlag(B,FLAG_HELPING,Handle(B\TargetFriend))
					Else
						
						If (B\TargetFood=Null) Or (CheckFlag(B,FLAG_SEEKING))
							If (B\TargetFriend\TargetFood&lt;&gt;Null)
								If (CheckFlag(B\TargetFriend,FLAG_EATING Or FLAG_FOUND))
									DebugLog(B\FriendlyName+" noticed that "+B\TargetFriend\FriendlyName+" has found food")
									SetFlag(B,FLAG_FOUND,Handle(B\TargetFriend\TargetFood))
								End If
							Else
								ClearFlag(B\TargetFriend,FLAG_FOUND)
								ClearFlag(B\TargetFriend,FLAG_EATING)
								DebugLog(B\FriendlyName+" noticed that "+B\TargetFriend\FriendlyName+" no longer has food")				
							End If	
						End If
						
					End If
					
				Else
					ClearFlag(B\TargetFriend,FLAG_ATTACKED)
					ClearFlag(B\TargetFriend,FLAG_ATTACKING)
					ClearFlag(B\TargetFriend,FLAG_FLEEING)
					DebugLog(B\FriendlyName+" noticed that "+B\TargetFriend\FriendlyName+" is no longer in difficulty")
				End If
			End If
		End If
	End If
End Function

Function SenseClosestPredator(B.BioForm,Predator.BioForm)
	If ((UpdatePredator(Predator)))
		If (B\TargetEnemy&lt;&gt;Null)
			If (B\TargetEnemy&lt;&gt;Predator)
				If (Not(UpdatePredator(B\TargetEnemy)))
					DebugLog(B\FriendlyName+" senses a new predator nearby: "+Predator\FriendlyName)
					SetFlag(B,FLAG_FLEEING,Handle(Predator))
				Else
					B\TargetEnemy=GetClosest(B,B\TargetEnemy,Predator)
					DebugLog(B\FriendlyName+" is more threatened by the proximmity of "+B\TargetEnemy\FriendlyName)
				End If	
			Else
				If (Not(UpdatePredator(B\TargetEnemy)))
					DebugLog(B\FriendlyName+" doesn't sense any predators nearby")
					ClearFlag(B,FLAG_FLEEING)
					ClearFlag(B,FLAG_ATTACKED)
					Return
				Else
					DebugLog(B\FriendlyName+" is still threatened by "+B\TargetEnemy\FriendlyName)
				End If	
			End If
			
			SetFlag(B,FLAG_FLEEING,Handle(B\TargetEnemy))
			
		Else
			DebugLog(B\FriendlyName+" senses a new predator nearby: "+Predator\FriendlyName)
			SetFlag(B,FLAG_FLEEING,Handle(Predator))
		End If
	Else
		If (Not(UpdatePredator(B\TargetEnemy)))
			DebugLog(B\FriendlyName+" doesn't sense any predators nearby")
			ClearFlag(B,FLAG_FLEEING)
			ClearFlag(B,FLAG_ATTACKED)
		Else
			DebugLog(B\FriendlyName+" is still threatened by "+B\TargetEnemy\FriendlyName)
			SetFlag(B,FLAG_FLEEING,Handle(B\TargetEnemy))	
		End If	
	End If
End Function

Function UpdatePredator(Predator.BioForm)
	;Just in case Predator dies ior decays in the meantime...
	If (Predator=Null)Then Return False
	Return (Predator\Category&gt;0)
End Function		

Function SenseClosestFood(B.BioForm,Food.BioForm)
	If (B\TargetFood&lt;&gt;Null)
		If ((Food&lt;&gt;Null) And (Food&lt;&gt;B\TargetFood))
			B\TargetFood=GetClosest(B,B\TargetFood,Food)
			DebugLog(B\FriendlyName+" is spoilt for menu choice, but "+B\TargetFood\FriendlyName+" is closer")
		Else
			If (Food=B\TargetFood)
				DebugLog(B\FriendlyName+" is still hungry for "+B\TargetFood\FriendlyName)
			End If
		End If
		SetFlag(B,FLAG_FOUND,Handle(B\TargetFood))
	Else
		If (Food&lt;&gt;Null)
			DebugLog(B\FriendlyName+" senses something nice to eat: "+Food\FriendlyName)
			SetFlag(B,FLAG_FOUND,Handle(Food))
		Else
			DebugLog(B\FriendlyName+" wonders where their next meal might come from")
			ClearFlag(B,FLAG_FOUND)
		End If
	End If	
End Function	

Function GetClosest.BioForm(B.BioForm,Bio1.BioForm,Bio2.BioForm)	
	Local Distance1#=Distance2D(B\PositionX,B\PositionY,Bio1\PositionX,Bio1\PositionY)
	Local Distance2#=Distance2D(B\PositionX,B\PositionY,Bio1\PositionX,Bio1\PositionY)
	
	If(Distance1&lt;=Distance2) 
		Return Bio1
	Else
		Return Bio2
	End If	
End Function	

Function Angle2D#( x1#,y1#,x2#,y2# )
	Return 180-ATan2(x2-x1,y2-y1)
End Function

Function SenseForCategory.BioForm(Category,X#,Y#, Direction#,Range#)
	
	Local SgnX#=Sin(Direction)
	Local SgnY#=Cos(Direction)
	
	If (Abs(SgnX)&lt;0.25)
		SgnX=SgnX+(0.5*Sgn(SgnX))
	End If	
	
	
	If (Abs(SgnY)&lt;0.25)
		SgnY=SgnY+(0.5*Sgn(SgnY))
	End If	
	
	Local XMin#
	Local XMax#
	Local YMin#
	Local YMax#
	
	Local TMin#
	Local TMax#
	
	
	If (SgnX&lt;0)
		XMin=(X-(Range*SgnX))
		XMax=(X+(Range*SgnX))
	End If	
	If (SgnX&gt;=0)
		XMin=(X-(SgnX*Range))
		XMax=(X+(SgnX*Range))
	End If
	
	If (SgnY&lt;0)
		YMin=(Y-(Range*SgnY))
		YMax=(Y+(Range*SgnY))
	End If	
	If (SgnY&gt;=0)
		YMin=(Y-(SgnY*Range))
		YMax=(Y+(SgnY*Range))
	End If
	
	TMin=((XMin&lt;XMax)*XMin)+((XMax&lt;=XMin)*XMax)
	TMax=((XMin&gt;XMax)*XMin)+((XMax=&gt;XMin)*XMax)
	
	XMin=TMin
	XMax=TMax
	
	TMin=((YMin&lt;YMax)*YMin)+((YMax&lt;=YMin)*YMax)
	TMax=((YMin&gt;YMax)*YMin)+((YMax=&gt;YMin)*YMax)
	
	YMin=TMin
	YMax=TMax
	
	Stop	
	
	Local Found.BioForm
	For Found = Each BioForm
		If (Not(Found\Category)) ; Found/CATEGORY IS ZERO So why is the IF skipped????????????
			If (((Found\PositionX)&lt;XMax) And ((Found\PositionX)&gt;XMin) And ((Found\PositionY)&lt;YMax) And ((Found\PositionX)&gt;YMin) )
				;Double Check Range?
				If (Distance2D(Found\PositionX,Found\PositionY,X,Y)&lt;Range)
					Exit
				End If
			End If	
		End If	
	Next
	Return Found
End Function	

Function Distance2D#(x#,y#,x2#,y2#)
	Return Sqr(((x#-x2#)*(x#-x2#))+((y#-y2#)*(y#-y2#)))
End Function

Function Action(B.BioForm)
	;Prioritize
	
	;Ageing:
	If (GetAge(B)&gt;B\Lifetime)
		If (B\Category)
			Die(B)
			Return
		Else
			; Food will not leave any remains once its rotted away
			DebugLog(B\FriendlyName+" rots, all nutrition has gone")
			Destroy(B)
			Return
		End If	
	End If
	
	; Hungry
	If ((MilliSecs()-B\LastMealTime)&gt;B\CategoryStats[CATSTATS_HUNGER_RATE])
		If (B\Category)
			DebugLog(B\FriendlyName+" is starving")
		Else
			DebugLog(B\FriendlyName+" rots")
		End If	
		Starve(B)
	End If
	
	; Dividing
	If (B\Category)
		;Food does not reproduce!
		If ((MilliSecs()-B\LastDivisionTime)&gt;B\CategoryStats[CATSTATS_DIVISION_RATE])
			Divide(B)
		End If	
	End If	
	
End Function

Function Move(B.BioForm)
	If (B=Null)
		Return
	End If	
	
	If (B\Category=CATEGORY_FOOD)
		; Dead Matter doesn't move!
		Return
	End If
	
	If (CheckFlag(B,FLAG_DIVIDING))
		If (MilliSecs()-(B\LastDivisionTime)&lt;B\CategoryStats[CATSTATS_DIVISION_DELAY])
			SetFlag(B,FLAG_DIVIDING)
			DebugLog B\FriendlyName+" is busy dividing and cannot move"
			Return
		End If
		ClearFlag(B,FLAG_DIVIDING)
		DebugLog B\FriendlyName+" has completed dividing and can move again"
		
	End If
	
	If (CheckFlag(B,FLAG_EATING))
		If (B\TargetFood&lt;&gt;Null)
			
			; Just to make sure
			SetFlag(B,FLAG_FOUND,Handle(B\TargetFood))
			ClearFlag(B,FLAG_SEEKING)
			
			If (Distance2D(B\PositionX,B\PositionY,B\TargetFood\PositionX,B\TargetFood\PositionY)&lt;(B\Health*2.0))
				; No need to move!
				DebugLog(B\FriendlyName+" refuses to eat on the move so stays put to munch "+B\TargetFood\FriendlyName)
				Return
			Else
				ClearFlag(B,FLAG_EATING)
			End If
		End If
	End If	
	
	If (CheckFlag(B,FLAG_FOUND))
		If (B\TargetFood=Null)
			ClearFlag(B,FLAG_FOUND)
			SetFlag(B,FLAG_SEEKING)
		Else
			B\Direction=Angle2D(B\PositionX,B\PositionY,B\TargetFood\PositionX,B\TargetFood\PositionY)
		EndIf	
	End If
	
	If (CheckFlag(B,FLAG_SEEKING))
		If (B\TargetFood&lt;&gt;Null)
			B\TargetFood=Null
		End If
		
		ClearFlag(B,FLAG_FOUND)
		ClearFlag(B,FLAG_EATING)
		
		Local NewDirection#=Sin(Handle(B))*B\Speed*12
		NewDirection=NewDirection+(Cos(B\LastMealTime)*B\Speed*12)
		Stop
		B\Direction=B\Direction+NewDirection 
		
	End If
	
	Local X#=B\PositionX
	Local Y#=B\PositionY
	
	Local XX#=Sin(B\Direction)*B\Speed
	Local YY#=Cos(B\Direction)*B\Speed
		
	If (((XX+X)&gt;=0) And ((XX+X)&lt;=GraphicsWidth()))
		B\PositionX=X+XX
	Else
		DebugLog(B\FriendlyName+" Hit the edge of the screen at "+Str(Int(XX+X))+" and rebounded")
		B\PositionX=X-XX
		B\Direction=(180+B\Direction) Mod 360.0
	End If
	
	If (((YY+Y)&gt;=0) And ((YY+Y)&lt;=GraphicsHeight()))
		B\PositionY=Y+YY
	Else
		DebugLog(B\FriendlyName+" Hit the edge of the screen at "+Str(Int(YY+Y))+" and rebounded")
		B\PositionY=Y-YY
		B\Direction=(180.0+B\Direction) Mod 360.0
	End If
	
End Function

Function Draw(B.BioForm)
	If (B=Null)
		Return
	End If	
	
	DrawBioform(B\PositionX,B\PositionY,B\Category,B\Direction, B\Health)
End Function	

Function DrawBioform(X#,Y#,Category,Direction#,Health)
	
	Local XX#
	Local YY#
	
	Select Category
		Case CATEGORY_FOOD:
			Color 128,128,128
			Circle(X,Y,Health,True)
			
		Case CATEGORY_RED:
			Color 255,0,0
			
			XX=(Sin(Direction)*Health)*0.5
			YY=(Cos(Direction)*Health)*0.5
			
			Line X-XX,Y-YY,X+XX,Y+YY
			Circle(X+XX,Y+YY,Health*0.3)
			
			
		Case CATEGORY_GREEN:
			Color 0,255,0
			Circle(X,Y,Health,False)
		Case CATEGORY_BLUE:	
			Local Spindle#
			
			Color 0,0,255
			
			
			For Spindle=Direction-30 To Direction+30 Step 10
				XX=X-(Sin(Spindle)*Health*2.0)
				YY=Y-(Cos(Spindle)*Health*2.0)
				
				Line X,Y,XX,YY
			Next
			
	End Select
End Function

Function Starve(B.BioForm)
	B\Health=B\Health-(B\CategoryStats[CATSTATS_HEALTH_INCREASE]*0.5)
	DebugLog(B\FriendlyName+" is wasting away to "+Str(B\Health)+" health remaining")
	
	; Yes, it's not a meal, but this means the starvation routine only fires at Hunger_Rate if
	; There's no food in between!
	B\LastMealTime=MilliSecs()
	
	If (B\Health&lt;=0)
		Die(B)
	End If
End Function	

Function Divide(B.BioForm)
	
	If (Not(B\Category))
		ClearAllFlags(B,False)
		; Just in case! Food does not reproduce!
		Return
	End If
	
	B\LastDivisionTime=MilliSecs()
	
	Local NewDirection#=(B\Direction+180.0)
	NewDirection=((NewDirection-(45.0+(Rnd(45.0,90.0)))) + 360) Mod 360.0
	Local SpawnHandle=GenerateNewBioform(B\PositionX,B\PositionY, B\Category, NewDirection, B\LastDivisionTime)
	SetFlag(B,FLAG_DIVIDING)
	
	Local S.BioForm=Object.BioForm(SpawnHandle)
	
	DebugLog(B\FriendlyName+" divides in two, producing "+S\FriendlyName)
End Function	

Function GenerateNewBioform(PositionX#, PositionY#, Category, Direction#, Time)
	Local B.BioForm=New BioForm
	
	PopulateCategoryStatistics(B,Category)
	
	B\Category=Category
	B\LastDivisionTime=Time
	B\Birthdate=Time
	B\LastMealTime=Time
	
	;Add some randomnisity
	B\Health=Rnd(B\CategoryStats[CATSTATS_INITIAL_HEALTH]*0.8,B\CategoryStats[CATSTATS_INITIAL_HEALTH]*1.1)
	
	;Add some randomnisity
	B\Lifetime=Rnd(B\CategoryStats[CATSTATS_INITIAL_HEALTH]*0.7,B\CategoryStats[CATSTATS_LIFESPAN]*1.2)
	
	;Add some randomnisity
	B\Speed=Rnd(B\CategoryStats[CATSTATS_INITIAL_SPEED]*0.9,B\CategoryStats[CATSTATS_INITIAL_SPEED]*1.1)
	
	B\PositionX=PositionX#
	B\PositionY=PositionY#
	
	B\Direction=Direction Mod 360.0
	
	; First order of the day...
	If (B\Category)
		
		SetFlag(B,FLAG_SEEKING)
		
	End If
	
	Return Handle(B)
	
End Function	

Function PopulateCategoryStatistics(B.BioForm,Category)
	
	Select (Category)
			
		Case CATEGORY_RED:
			B\FriendlyName="Red #"+Str(Handle(B))
			
			B\CategoryStats[CATSTATS_INITIAL_HEALTH]=INITIAL_HEALTH_RED
			B\CategoryStats[CATSTATS_HEALTH_INCREASE]=HEALTH_INCREASE_RED
			B\CategoryStats[CATSTATS_HUNGER_RATE]=HUNGER_RATE_RED
			
			B\CategoryStats[CATSTATS_INITIAL_SPEED]=INITIAL_SPEED_RED
			B\CategoryStats[CATSTATS_SPEED_INCREASE]=SPEED_INCREASE_RED
			
			B\CategoryStats[CATSTATS_DIVISION_RATE]=DIVISION_RATE_RED
			B\CategoryStats[CATSTATS_LIFESPAN]=LIFESPAN_RED
			B\CategoryStats[CATSTATS_DIVISION_DELAY]=DIVISION_DELAY_RED
			
			B\CategoryStats[CATSTATS_SENSE]=SENSE_RED
			B\CategoryStats[CATSTATS_DAMAGE]=DAMAGE_RED
			
			B\CategoryStats[CATSTATS_PREDATOR]=RED_ENEMY
			
			
		Case CATEGORY_GREEN:
			B\FriendlyName="Green #"+Str(Handle(B))
			
			B\CategoryStats[CATSTATS_INITIAL_HEALTH]=INITIAL_HEALTH_GREEN
			B\CategoryStats[CATSTATS_HEALTH_INCREASE]=HEALTH_INCREASE_GREEN
			B\CategoryStats[CATSTATS_HUNGER_RATE]=HUNGER_RATE_GREEN
			
			B\CategoryStats[CATSTATS_INITIAL_SPEED]=INITIAL_SPEED_GREEN
			B\CategoryStats[CATSTATS_SPEED_INCREASE]=SPEED_INCREASE_GREEN
			
			B\CategoryStats[CATSTATS_DIVISION_RATE]=DIVISION_RATE_GREEN
			B\CategoryStats[CATSTATS_LIFESPAN]=LIFESPAN_GREEN
			B\CategoryStats[CATSTATS_DIVISION_DELAY]=DIVISION_DELAY_GREEN
			
			B\CategoryStats[CATSTATS_SENSE]=SENSE_GREEN
			B\CategoryStats[CATSTATS_DAMAGE]=DAMAGE_GREEN
			
			B\CategoryStats[CATSTATS_PREDATOR]=GREEN_ENEMY
			
		Case CATEGORY_BLUE:
			B\FriendlyName="Blue #"+Str(Handle(B))
			
			B\CategoryStats[CATSTATS_INITIAL_HEALTH]=INITIAL_HEALTH_BLUE
			B\CategoryStats[CATSTATS_HEALTH_INCREASE]=HEALTH_INCREASE_BLUE
			B\CategoryStats[CATSTATS_HUNGER_RATE]=HUNGER_RATE_BLUE
			
			B\CategoryStats[CATSTATS_INITIAL_SPEED]=INITIAL_SPEED_BLUE
			B\CategoryStats[CATSTATS_SPEED_INCREASE]=SPEED_INCREASE_BLUE
			
			B\CategoryStats[CATSTATS_DIVISION_RATE]=DIVISION_RATE_BLUE
			B\CategoryStats[CATSTATS_LIFESPAN]=LIFESPAN_BLUE
			B\CategoryStats[CATSTATS_DIVISION_DELAY]=DIVISION_DELAY_BLUE
			
			B\CategoryStats[CATSTATS_SENSE]=SENSE_BLUE
			B\CategoryStats[CATSTATS_DAMAGE]=DAMAGE_BLUE
			
			B\CategoryStats[CATSTATS_PREDATOR]=BLUE_ENEMY
			
		Default:
			
			B\FriendlyName="Food #"+Str(Handle(B))
			
			;INITIAL DEAD MATTER PROPERTIES ONLY
			B\CategoryStats[CATSTATS_INITIAL_HEALTH]=INITIAL_HEALTH_GREEN
			B\CategoryStats[CATSTATS_HEALTH_INCREASE]=0.0
			B\CategoryStats[CATSTATS_HUNGER_RATE]=9999999
			
			B\CategoryStats[CATSTATS_INITIAL_SPEED]=0.0
			B\CategoryStats[CATSTATS_SPEED_INCREASE]=0.0
			
			B\CategoryStats[CATSTATS_DIVISION_RATE]=9999999
			B\CategoryStats[CATSTATS_LIFESPAN]=9999999
			B\CategoryStats[CATSTATS_DIVISION_DELAY]=9999999
			
			B\CategoryStats[CATSTATS_SENSE]=0.0
			B\CategoryStats[CATSTATS_DAMAGE]=0.0
			
			B\CategoryStats[CATSTATS_PREDATOR]=NO_ENEMY
			
	End Select		
End Function
	

Function GetAge(B.BioForm)
	Return MilliSecs()-B\Birthdate
End Function

Function SetFlag(B.BioForm,Flag,TargetHandle=0)
	Local Target.BioForm=Null
	If (TargetHandle)
		Target.BioForm=Object.BioForm(TargetHandle)
	End If
	
	Select(Flag)
		Case FLAG_SEEKING:
			B\TargetFood=Null
			B\TargetEnemy=Null
		Case FLAG_FOUND:
			; Overrides Seeking food
			ClearFlag(B,FLAG_SEEKING)
			B\TargetFood=Target
		Case FLAG_EATING:
			; Overrides Seeking Food 
			ClearFlag(B,FLAG_SEEKING)
		Case FLAG_ATTACKING:
			; Overrides Seeking, Under Attack and Eating
			ClearFlag(B,FLAG_SEEKING)
			ClearFlag(B,FLAG_ATTACKED)
			B\TargetFood=Target
		Case FLAG_ATTACKED:
			; Overrides Seeking Food, Fleeing or Eating
			ClearFlag(B,FLAG_SEEKING)
			ClearFlag(B,FLAG_FLEEING)
			ClearFlag(B,FLAG_EATING)
			B\TargetEnemy=Target
		Case FLAG_HELPING:
			; Overrides Seeking Food or Eating
			ClearFlag(B,FLAG_SEEKING)
			ClearFlag(B,FLAG_EATING)
			B\TargetFriend=Target
			B\TargetEnemy=Target\TargetEnemy
		Case FLAG_FLEEING:
			; Overrides Seeking Food, Eating or attacking
			ClearFlag(B,FLAG_SEEKING)
			ClearFlag(B,FLAG_EATING)
			ClearFlag(B,FLAG_ATTACKING)
			
			B\TargetEnemy=Target
	End Select
	
	B\SensoryFlags=SetBit(B\SensoryFlags,Flag)
	
End Function

Function ClearFlag(B.BioForm,Flag)
	Select Flag
		Case FLAG_FOUND:
			B\TargetFood=Null
		Case FLAG_EATING:
			B\TargetFood=Null
		Case FLAG_ATTACKING:
			B\TargetEnemy=Null
		Case FLAG_ATTACKED:
			B\TargetEnemy=Null	
		Case FLAG_HELPING:
			If (B\TargetFriend&lt;&gt;Null)
				If (B\TargetFriend\TargetEnemy=B\TargetEnemy)
					B\TargetEnemy=Null	
				End If
				B\TargetFriend=Null		
			End If	
		Case FLAG_FLEEING:
			B\TargetEnemy=Null
	End Select
	
	B\SensoryFlags=UnSetBit(B\SensoryFlags,Flag)			
End Function				

Function CheckFlag(B.BioForm,Flag, TargetHandle=0)
	
	If (B=Null)
		Return False
	End If	
	
	Local Target.BioForm=Null
	If (TargetHandle)
		Target.BioForm=Object.BioForm(TargetHandle)	
	End If
	
	If ((B\SensoryFlags And Flag)=Flag)
		If (Target&lt;&gt;Null)
			Select Flag
				Case FLAG_FOUND:
					Return (B\TargetFood=Target)
				Case FLAG_EATING:
					Return (B\TargetFood=Target)
				Case FLAG_ATTACKING:
					Return (B\TargetEnemy=Target)
				Case FLAG_ATTACKED:
					Return (B\TargetEnemy=Target)
				Case FLAG_HELPING:
					Return (B\TargetFriend=Target)
				Case FLAG_FLEEING:
					Return (B\TargetEnemy=Target)
			End Select
		End If
		Return True
	End If
	Return False
End Function						

Function ClearAllFlags(B.BioForm, Ignore_DividedBit=True)
	B\SensoryFlags=(B\SensoryFlags And (FLAG_DIVIDING*Ignore_DividedBit))
End Function	

Function SetBit(Value,BitValue)
	If ((Value And BitValue)&lt;&gt;BitValue)
		Value=Value+BitValue
	End If	
	
	Return Value
	
End Function

Function UnSetBit(Value,BitValue)
	If ((Value And BitValue)=BitValue)
		Value=Value-BitValue
	End If
End Function

Function Circle(X#,Y#,Radius#,Filled=True)
	X=X-(Radius*0.5)
	Y=Y-(Radius*0.5)
	Oval X,Y,Radius,Radius,Filled
End Function	

	
	


Function Disturbed(B.BioForm)	; Returns TRUE if Attacking, Under Attack, Fleeing, Dividing, Helping a Friend - Essentially not convenient to Eat
	Return CheckFlag(B,FLAG_ATTACKING+FLAG_ATTACKED+FLAG_HELPING+FLAG_FLEEING,Handle(B))
End Function

Function Eat(B.BioForm, Food.BioForm)
	If (B=Null)
		Return
	End If	
	
	If (Food=Null)
		DebugLog(B\FriendlyName+"'s food is all gone")
		B\TargetFood=Null
		ClearFlag(B,FLAG_EATING)
		SetFlag(B,FLAG_SEEKING)
		Return
	End If
	
	B\LastMealTime=MilliSecs()
	
	Local FoodHandle=Handle(Food)
	
	SetFlag(B,FLAG_EATING,FoodHandle)
	
	Local hAmount#=B\CategoryStats[CATSTATS_HEALTH_INCREASE]
	
	; Make sure only what's available can be eaten!
	If (Food\Health&lt;hAmount)
		hAmount=Food\Health
	End If
	
	Food\Health=Food\Health-hAmount
	B\Health=B\Health+hAmount
	
	B\Speed=B\CategoryStats[CATSTATS_SPEED_INCREASE]
	
	DebugLog(B\FriendlyName+" took a bite of "+Food\FriendlyName+" to gain "+Str(hAmount)+" health and "+Str(B\CategoryStats[CATSTATS_HEALTH_INCREASE])+" speed")
	
	If (Food\Health&lt;=0)
		OnZeroHealth(Food,Handle(B))
	End If
End Function

Function Confront(B.BioForm, Other.BioForm)
	If (B\Category)
		If (B\Category&lt;&gt;Other\Category)
			If (Other\Category)
				Local Resolve.BioForm=RelativeConflict(B,Other)
				If (Resolve=B)
					Attack(B,Other)
				Else
					Attack(Other,B)
				End If
			Else
				If (Not(Disturbed(B)))
					Eat(B,Other)
				End If			
			End If
		End If	
	End If
End Function

Function RelativeConflict.BioForm(Attacker.BioForm,Defender.BioForm)
	
		; Disturbed
	If  ((CheckFlag(Defender,FLAG_EATING) )Or ( (CheckFlag(Defender,FLAG_DIVIDING))))
		ClearFlag(Defender,FLAG_EATING)
		SetFlag(Attacker,FLAG_ATTACKING,Handle(Defender))
		SetFlag(Defender,FLAG_ATTACKED,Handle(Attacker))
		Return Attacker
	End If	
	If  ((CheckFlag(Attacker,FLAG_EATING) )Or ( (CheckFlag(Attacker,FLAG_DIVIDING) ) ))
		ClearFlag(Attacker,FLAG_EATING)
		SetFlag(Attacker,FLAG_ATTACKING,Handle(Defender))
		SetFlag(Defender,FLAG_ATTACKED,Handle(Attacker))
		Return Attacker
	End If	
	
		; Tried to flee, but has been caught
	If (CheckFlag(Defender,FLAG_FLEEING,Handle(Attacker)))
		SetFlag(Attacker,FLAG_ATTACKING,Handle(Defender))
		SetFlag(Defender,FLAG_ATTACKED,Handle(Attacker))
		Return Attacker
	End If
	
	If (CheckFlag(Attacker,FLAG_FLEEING,Handle(Defender)))
		SetFlag(Defender,FLAG_ATTACKING,Handle(Attacker))
		SetFlag(Attacker,FLAG_ATTACKED,Handle(Defender))
		Return Defender
	End If
	
		; Already in a scrap
	If  ((CheckFlag(Defender,FLAG_ATTACKED,Handle(Attacker)) Or (CheckFlag(Attacker,FLAG_ATTACKING,Handle(Defender)))))
		SetFlag(Attacker,FLAG_ATTACKING,Handle(Defender))
		SetFlag(Defender,FLAG_ATTACKED,Handle(Attacker))
		Return Attacker
	End If
	
	If ((CheckFlag(Attacker,FLAG_ATTACKED,Handle(Defender)))  Or (CheckFlag(Defender,FLAG_ATTACKING,Handle(Attacker))))
		SetFlag(Defender,FLAG_ATTACKING,Handle(Attacker))
		SetFlag(Attacker,FLAG_ATTACKED,Handle(Defender))
		Return Defender
	End If
	
		; NOT previously engaged in combat - See who has the upper hand:	
	
	Local APoints	=	Attacker\Health+GetAge(Attacker)
	APoints	=APoints	/((MilliSecs()-Attacker\LastMealTime)/Attacker\CategoryStats[CATSTATS_HUNGER_RATE])
	
	Local DPoints	=	Defender\Health+GetAge(Defender)
	DPoints=DPoints		/((MilliSecs()-Defender\LastMealTime)/Attacker\CategoryStats[CATSTATS_HUNGER_RATE])
	
	If (APoints&gt;DPoints)
		SetFlag(Attacker,FLAG_ATTACKING,Handle(Defender))
		SetFlag(Defender,FLAG_ATTACKED,Handle(Attacker))
		
		Return Attacker
	End If	
	
	If (DPoints&gt;APoints)
		SetFlag(Defender,FLAG_ATTACKING,Handle(Attacker))
		SetFlag(Attacker,FLAG_ATTACKED,Handle(Defender))
		Return Defender
	End If
End Function	

Function Attack(Attacker.BioForm,Defender.BioForm)
	
	Local AttackerHandle=Handle(Attacker)
	Local DefenderHandle=Handle(Defender)
	
	Defender\Health=Defender\Health-Attacker\CategoryStats[CATSTATS_DAMAGE]
	
	DebugLog(Attacker\FriendlyName+" attacked "+Defender\FriendlyName+" and inflicted "+Str(Attacker\CategoryStats[CATSTATS_DAMAGE])+" wounds leaving "+Str(Defender\Health)+" health remaining")
	
	If (Defender\Health&lt;=0)
		SetFlag(Attacker,FLAG_FOUND,DefenderHandle)
		ClearFlag(Attacker,FLAG_ATTACKING)
		
		DebugLog(Attacker\FriendlyName+" slew "+Defender\FriendlyName)
		OnZeroHealth(Defender,AttackerHandle)
	Else	
		DebugLog(Defender\FriendlyName+" decides to flee attacks from "+Attacker\FriendlyName)
		
		; After being damaged, Defender attempts to flee
		SetFlag(Defender,FLAG_FLEEING,AttackerHandle)
	End If	
End Function

Function OnZeroHealth(B.BioForm, KillerHandle=0)
	If (B=Null)
		Return
	End If	
	
	If (B\Category)
		Die(B)
	Else
		Destroy(B.BioForm)
		Return
	End If
End Function	

Function Destroy(B.BioForm)
	If (B=Null)
		Return
	End If	
	
	DebugLog("the remains of "+B\FriendlyName+" were consumed")
	Delete B.BioForm
End Function

Function Die(B.BioForm)
	If (B=Null)
		Return
	End If	
	
	
	ClearAllFlags(B)	
	
	; Just in case!
	If (Not(B\Category))
		Destroy(B)
	End If	
	
	Local NewNumber=Handle(B)
	
	B\Category=CATEGORY_FOOD
	B\Health=B\CategoryStats[CATSTATS_INITIAL_HEALTH]*0.5
	B\Lifetime=FOOD_LIFETIME_RATIO*B\Health*60000
	
	B\CategoryStats[CATSTATS_HUNGER_RATE]=B\Lifetime/B\Health
	
	B\CategoryStats[CATSTATS_SPEED_INCREASE]=0
	B\CategoryStats[CATSTATS_DIVISION_RATE]=999999
	B\CategoryStats[CATSTATS_DIVISION_DELAY]=99999
	B\CategoryStats[CATSTATS_INITIAL_SPEED]=0
	B\CategoryStats[CATSTATS_DAMAGE]=0
	B\CategoryStats[CATSTATS_INITIAL_SPEED]=0
	B\CategoryStats[CATSTATS_SENSE]=0.0
	
	B\TargetFood=Null
	B\TargetFriend=Null
	B\TargetEnemy=Null
	
	B\LastMealTime=MilliSecs()
	B\Speed=0
	
	Local NewName$="Food #"+Str(NewNumber)
	
	DebugLog(B\FriendlyName+" became edible remains: "+NewName)
	
	B\FriendlyName=NewName
	
End Function
</textarea><br>PS. It's late, I'm tired, and I'm probably missing something obvious ... :S <br><br></td></tr></table><br>
<a name="1114422"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Doesn't skip on that line for me, but on the next one, as it should.<br><br>Are you experiencing the "slipping Debug line" problem (where the highlighted line isn't the one the code's actually stopped on)? Try copying the code from the codebox back into a new file yourself, as the website will have corrected the newlines. <br><br></td></tr></table><br>
<a name="1114461"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks, Yasha - That seemed to be what was happening.<br>I copied and pasted directly back from here into a new file and it worked correctly!<br><br><br>Incidentally, this also helped me spot another little issue:<br><br><pre class=code>If (((Found\PositionX)&lt;XMax) And ((Found\PositionX)&gt;XMin) And ((Found\PositionY)&lt;YMax) And ((Found\PositionX)&gt;YMin) )
</pre><br><br>Should have used Found\PositionY at the end, not Found\PositionX <br><br><pre class=code>...((Found\PositionY)&lt;YMax) And ((Found\PositionY)&gt;YMin) )</pre><br><br>Thanks again! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
