<!DOCTYPE html><html lang="en" ><head ><title >Drak's Waypoint Example?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Drak's Waypoint Example?</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Drak's Waypoint Example?</a><br><br>
<a name="1124770"></a>

<a name="1124772"></a>

<a name="1124773"></a>

<a name="1124774"></a>

<a name="1124775"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi all, a LONG, LONG time ago, Drak had made a waypoint example. Since I'm making a level editor and needed waypoints to write to the final game script, I decided to try and add an extra mode for the enemies, called "Roaming". What Roaming SHOULD do, is turn the enemy blue (for now), and wait a couple seconds before roaming close to its waypoint. BUT. If the player gets TOO close, the enemy will go into "Pursue" mode again, and start pursuing the player again. If the player gets away, the enemy returns to its waypoint, waits 1 to 5 seconds, and then starts roaming randomly again, keeping nearby its waypoint. NOW.... I am HORRIBLE with enemy roaming math, so can someone PLEASE help fix this? :) Also, one small glitch, when you are chased by a couple of enemies for a while, the game crashes at this line: <br><br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Local dist_home# = EntityDistance(e\mesh,e\return_waypoint)	;get the distance home</textarea><br><br><br><br>In:<br><br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Case 5</textarea><br><br><br><br>Here's the code: <br><br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">;Waypoint Example By Drak, 3/8/2010

;************************************************************
;THIS PROGRAM WILL:											*
;Create a player mesh (a square)							*
;Create some enemies (some spheres)							*
;Make on-the-fly waypoints for the enemies to return to		*
;Return the enemies to the waypoints						*
;Also shows some some rudimentary AI						*
;Shows some very simple particle effects as well			*
;************************************************************

AppTitle "Drak's Waypoint Example"

;-------------------------------------------------------------------------------------------------------
;INITIALIZATION
;-------------------------------------------------------------------------------------------------------
Graphics3D 800,600,0,2
SetBuffer BackBuffer()



AmbientLight 255, 255, 255



SeedRnd MilliSecs()



Global type_player 	= 1
Global type_obj 	= 2
Global type_land 	= 3



Collisions type_player, type_land, 2, 3
Collisions type_obj, type_obj, 2, 3
Collisions type_obj, type_land, 2, 3



Global player = CreateCube()	;create a cube for the player
EntityColor player, 0,0,200		;we'll make the player blue
EntityType player, type_player



Global player_hp = 10			;give the player 10 hit points
Global player_alive = 1			;player is indeed alive



Global camera = CreateCamera()	;create a camera
PositionEntity camera, 0,1,0	;raise it up in the air



;-------------------------------------------------------------------------------------------------------
;TYPES
;-------------------------------------------------------------------------------------------------------



Type enemy
	Field mesh				;will hold the actual mesh
	Field return_waypoint	;will hold the handle of the return waypoint
	Field status			;holds a # referring to the enemies status
	Field speed#			;this will hold the speed of the enemy
End Type



Type particle				
	Field mesh				;holds mesh
	Field alpha#			;holds alpha
	Field speed_x#			;speed x
	Field speed_y#			;same
	Field speed_z#			;same...
	Field fade_speed#		;fade_speed
End Type



;-------------------------------------------------------------------------------------------------------
;LETS CREATE SOME PRIMITIVES TO USE LATER	
;-------------------------------------------------------------------------------------------------------

Global spark = CreateCube()
ScaleEntity spark, .25,.25,.25
EntityColor spark, 255,168,0
EntityType spark,type_obj
HideEntity spark

Global waypoint = CreateSphere()
ScaleEntity waypoint, .5,.5,.5
EntityColor waypoint, 0,200,0
HideEntity waypoint

;-------------------------------------------------------------------------------------------------------
;CREATE SOME ENEMIES
;-------------------------------------------------------------------------------------------------------
For num = 1 To 5
	e.enemy = New enemy				;new enemy
	e\mesh = CreateSphere(16)		;create a sphere and store it in e\mesh
	e\speed = .1					;set the speed of each enemy
	e\status = 1					;set status to 1
	PositionEntity e\mesh, Rand(-20,20),0,Rand(-20,20)	;place the enemies randomly
	EntityAlpha e\mesh, .5
	EntityType e\mesh, type_obj
Next

;-------------------------------------------------------------------------------------------------------
;MAIN LOOP
;-------------------------------------------------------------------------------------------------------
While Not KeyHit(1)
	
	
	
	get_input()
	
	
	
	If waypoint&lt;&gt;0
		run_enemies()
		run_particles()
		check_hp()
	EndIf
	
	
	
	UpdateWorld()
	RenderWorld()
	
	
	
	gui()
	
	
	
	FlushKeys()
	
	
	
	FlushMouse()
	
	
	
    ;SuperCam(cam,ent,cspeed#,dist#,hite#,xrot#,tilt#)
	SuperCam(camera,player,.01,8,3,0,0)
	
	
	
	Flip
	
	
	
Wend



ClearWorld(1, 1, 1)



End



;-------------------------------------------------------------------------------------------------------
;END MAIN LOOP
;-------------------------------------------------------------------------------------------------------


;-----------------------------------------------------------------------------------------------------
;FUNCTION GET_INPUT &lt;--- GRABS INPUT FOR THE PLAYER
;-----------------------------------------------------------------------------------------------------
Function get_input()
	If player_alive = 1
		MoveEntity player, 0, 0, (KeyDown(17)-KeyDown(31) Or KeyDown(200)-KeyDown(208))*.15
		TurnEntity player, 0, (KeyDown(30)-KeyDown(32) Or KeyDown(203)-KeyDown(205))*1, 0
	End If 
End Function 


;-----------------------------------------------------------------------------------------------------
;FUNCTION RUN_ENEMIES   &lt;---EXOCUTES ACTIONS FOR EACH ENEMY
;-----------------------------------------------------------------------------------------------------
Function run_enemies()
	For e.enemy = Each enemy	;for each enemy
		;regardless of case, the distance to the player must be known, so we'll check it here
		Local dist# = EntityDistance(e\mesh,player)
		Select e\status			;select it's status
				
				
				
			Case 1 ;ON GUARD
				EntityColor e\mesh, 200,200,0
				If dist# &lt;= 10		;if the distance is less than 10 or equal to it
					If player_alive = 1
						e\status = 6	;switch to state 2
					End If
				End If
				
				
				
			Case 2 ;DROP NEW WAYPOINT
				Local x# = EntityX(e\mesh)	;grab the enemies' coordinates
				Local y# = EntityY(e\mesh)
				Local z# = EntityZ(e\mesh)
				
				;place the waypoint at the location
				e\return_waypoint = CopyEntity(waypoint)
				If e\return_waypoint&lt;&gt;0 Then PositionEntity e\return_waypoint,x#,y#,z#
				
				e\status = 3	;then switch to case 3, which is pursue
				
				
				
			Case 3 ;PURSUE THE PLAYER
				EntityColor e\mesh, 200,0,0	;red
				If dist# &lt; 1	;then attack!
					e\status = 4;change to attack
					Exit 		;exit the loop
				End If 
				
				If dist# &lt; 12 And player_hp&gt;0	;first, check to see if we're 12 units away or less....
					PointEntity e\mesh, player		;point enemy at the player
					MoveEntity e\mesh, 0,0,e\speed	;then make the enemy move in the pointed direction
				Else
					e\status = 5	;set to status 5, which is return to waypoint
				End If
				
				
				
			Case 4 ;ATTACK
				player_hp = player_hp - 1	;take 1 hp from the player
				If player_hp &lt; 1 Then e\status = 5				;now return to the waypoint
				
				If player_hp &gt; 1
				Local s_x# = EntityX(player)
				Local s_y# = EntityY(player)
				Local s_z# = EntityZ(player)
				Local num = Rand(3,8)
						;create some effects
				create_particles(num,s_x,s_y,s_z)
				
			EndIf
			
			
			
			Case 5
				
				
				
				EntityColor e\mesh, 200,0,200
				Local dist_home# = EntityDistance(e\mesh,e\return_waypoint)	;get the distance home
				If dist_home# &gt; .2							;If we're further than .2 away...
					PointEntity e\mesh, e\return_waypoint	;point at home waypoint
					MoveEntity e\mesh, 0,0,e\speed			;move them forward
				Else If dist_home# &lt;= .2
					FreeEntity e\return_waypoint			;get rid of the waypoint mesh
					e\status = 1							;and return to idle again
				End If
				
				
				
			Case 6
				
				
				
				;If the distance from the player to the waypoint is GREATER than or EQUAL to 12
				
				
				
				If dist# &gt;= 12
					
					
					
				;Color the enemy blue and start roaming around randomly
					
					
				
					EntityColor e\mesh, 68, 207, 252
					
					
					
				;Randomly allow the enemy to roam
				
				
				
				For x1 = 0 To Rnd(100)
					
					
					MoveEntity e\mesh, 0, 0, x1
					
					
				Next
				
				
			;Otherwise
				
				
				
			Else
				
				
				
				;Player got too close - Pursue the player
				
				
				
				e\status = 3
				
				
				
			EndIf
				
				
				
			Default
				e\status = 1	;if somehow e\status = anything else, we'll set it to 1
				
		End Select
	Next
End Function 

;-----------------------------------------------------------------------------------------------------
;GRAPHIC READOUTS   &lt;---This function prints readouts to the screen
;-----------------------------------------------------------------------------------------------------
Function gui()
	Text 0,0, "Use WASD keys to move around."
	Text 0,10,"Don't let the spheres touch you or you'll lose hitpoints."
	Text 0,20,"Remaining HP: " + player_hp
	
	If player_alive = 0
		Text 350,290,"You are DEAD!"
	End If 
End Function 


;-----------------------------------------------------------------------------------------------------
;RUN PARTICLES	   &lt;---This function runs the particles if there are any
;-----------------------------------------------------------------------------------------------------
Function run_particles()
	For p.particle = Each particle
		p\alpha = p\alpha - p\fade_speed	;take fade speed from alpha
		EntityAlpha p\mesh, p\alpha			;set alpha to p\alpha
		If p\alpha &lt;= 0					;if alpha is &lt;= 0
			FreeEntity p\mesh			;free it
			Delete p				
			Exit						;exit the loop
		Else
			p\speed_y = p\speed_y - .01							;subtract from y speed
			MoveEntity p\mesh, p\speed_x, p\speed_y,p\speed_z	;move the particle
		End If
		
		
	Next
End Function  


;-----------------------------------------------------------------------------------------------------
;CREATE PARTICLES	&lt;--- THis is called when we need to create particles
;-----------------------------------------------------------------------------------------------------
Function create_particles(num,x#,y#,z#)
	For s = 1 To num
		p.particle = New particle
		p\mesh = CopyEntity(spark)
		p\speed_x# = Rnd(-.2,.2)
		p\speed_y# = Rnd(.3)
		p\speed_z# = Rnd(-.2,.2)
		p\alpha = 1
		p\fade_speed# = Rnd(.1)
		PositionEntity p\mesh, x,y,z
	Next
End Function 

;-----------------------------------------------------------------------------------------------------
;CHECK HP	&lt;---This checks the player's HEALTH each frame
;-----------------------------------------------------------------------------------------------------
Function check_hp()
	If player_hp &lt;= 0
		player_hp = 0
		If player_alive = 1
			Local s_x# = EntityX(player)
			Local s_y# = EntityY(player)
			Local s_z# = EntityZ(player)
			Local num = Rand(15)
		;create some effects
			create_particles(num,s_x,s_y,s_z)
			player_alive = 0
		End If
	End If 
End Function 



Function SuperCam(cam,ent,cspeed#,dist#,hite#,xrot#,tilt#)
	
	TFormPoint 0,hite#,-dist#,ent,0
	
	cx#=(TFormedX()-EntityX(cam))*cspeed#
	cy#=(TFormedY()-EntityY(cam))*cspeed#
	cz#=(TFormedZ()-EntityZ(cam))*cspeed#
	
	TranslateEntity cam,cx,cy,cz
	PointEntity cam,ent
	RotateEntity cam,xrot#,EntityYaw(cam),tilt#
	
End Function</textarea><br><br><br><br>Pay PARTICULAR attention to THIS part of code: <br><br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">			Case 6
				
				
				
				;If the distance from the player to the waypoint is GREATER than or EQUAL to 12
				
				
				
				If dist# &gt;= 12
					
					
					
				;Color the enemy blue and start roaming around randomly
					
					
				
					EntityColor e\mesh, 68, 207, 252
					
					
					
				;Randomly allow the enemy to roam
				
				
				
				For x1 = 0 To Rnd(100)
					
					
					MoveEntity e\mesh, 0, 0, x1
					
					
				Next
				
				
			;Otherwise
				
				
				
			Else
				
				
				
				;Player got too close - Pursue the player
				
				
				
				e\status = 3
				
				
				
			EndIf</textarea><br><br><br><br>Thank you for all help! :)<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1124829"></a>

<a name="1124830"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> It works absolutely perfect as I wrote it.  That's not my original code.  You added a bunch of stuff to it, and it's the stuff you added that's screwing up MY original code.<br><br>MY original code here works without glitch or hiccup:<br><a href="http://www.blitzbasic.com/Community/posts.php?topic=89386" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=89386</a><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1124831"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Anyhow, if you actually read and understand the code it's a very simple program.  ONLY when the "enemy" targets and chases the player does a "waypoint" get created.  The enemy return to this "waypoint" when a)the player gets too far away, or b) when the enemy hits the player.  It then returns to the "waypoint" and deletes it until it goes into pursue mode again in which it creates a NEW "waypoint".  That't it.  There is no actual pathfinding in this example.<br><br>Now, if you want the enemy to wander around that could very, very, easily be done without all that other stuff you added. <br><br></td></tr></table><br>
<a name="1124841"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, how is roaming done? <br><br></td></tr></table><br>
<a name="1124859"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Instead of returning the enemy to the waypoint when the player gets too far away, place it into a wander state instead.  In that wander state, simply keep checking the distance to the player from the enemy, and if that distance is too small, switch back to pursue state.  Also, in the wander state, just run a simple timer to control when the enemy switches back from wander, to return to waypoint. <br><br></td></tr></table><br>
<a name="1124863"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Now, the reason you're getting that error code is because you're bypassing the state where the enemy drops a waypoint.  The return waypoint does not exist until they move through state 2, which you switched into state 6.  Therefore, you'll always get that Object does not Exist error because they return waypoint does not exist in your code. <br><br></td></tr></table><br>
<a name="1124877"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> With only a few simple lines of code wandering can be implemented. <br><br>Try this:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Waypoint Example By Drak, 3/8/2010
;EDITED 2/3/12
;************************************************************
;THIS PROGRAM WILL:											*
;Create a player mesh (a square)							*
;Create some enemies (some spheres)							*
;Make on-the-fly waypoints for the enemies to return to		*
;Return the enemies to the waypoints						*
;Also shows some some rudimentary AI						*
;Shows some very simple particle effects as well			*
;************************************************************

AppTitle "Drak's Waypoint Example"

;-------------------------------------------------------------------------------------------------------
;INITIALIZATION
;-------------------------------------------------------------------------------------------------------
Graphics3D 800,600
SetBuffer BackBuffer()
SeedRnd MilliSecs()

Global player = CreateCube()	;create a cube for the player
EntityColor player, 0,0,200		;we'll make the player blue

Global player_hp = 10			;give the player 10 hit points
Global player_alive = 1			;player is indeed alive

Global camera = CreateCamera()	;create a camera
PositionEntity camera, 0,40,0	;raise it up in the air
RotateEntity camera, 90,0,0		;point it down


;-------------------------------------------------------------------------------------------------------
;TYPES
;-------------------------------------------------------------------------------------------------------
Type enemy
	Field mesh				;will hold the actual mesh
	Field return_waypoint	;will hold the handle of the return waypoint
	Field status			;holds a # referring to the enemies status
	Field speed#			;this will hold the speed of the enemy
	Field timer				;2/3/12 This will be a counter
	Field timer_max			;2/3/12 This is the counter's maximum
	Field steer_angle#		;2/3/12 This holds the enemy's steering angle
End Type

Type particle				
	Field mesh				;holds mesh
	Field alpha#			;holds alpha
	Field speed_x#			;speed x
	Field speed_y#			;same
	Field speed_z#			;same...
	Field fade_speed#		;fade_speed
End Type

;-------------------------------------------------------------------------------------------------------
;LETS CREATE SOME PRIMITIVES TO USE LATER	
;-------------------------------------------------------------------------------------------------------

Global spark = CreateCube()
	ScaleEntity spark, .25,.25,.25
	EntityColor spark, 255,168,0
	HideEntity spark

Global waypoint = CreateSphere()
	ScaleEntity waypoint, .5,.5,.5
	EntityColor waypoint, 0,200,0
	HideEntity waypoint

;-------------------------------------------------------------------------------------------------------
;CREATE SOME ENEMIES
;-------------------------------------------------------------------------------------------------------
For num = 1 To 5
	e.enemy = New enemy				;new enemy
	e\mesh = CreateSphere(16)		;create a sphere and store it in e\mesh
	e\speed = .1					;set the speed of each enemy
	e\status = 1					;set status to 1
	e\timer = 0						;2/3/12	set timer to 0
	e\timer_max = Rand(400,1000)	;2/3/12	pick a maximum timer
	PositionEntity e\mesh, Rand(-20,20),0,Rand(-20,20)	;place the enemies randomly
	EntityAlpha e\mesh, .5
	RotateEntity e\mesh, 0,Rand(360),0
Next

;-------------------------------------------------------------------------------------------------------
;MAIN LOOP
;-------------------------------------------------------------------------------------------------------
While Not KeyHit(1)
	get_input()
	run_enemies()
	run_particles()
	check_hp()
	
	
	UpdateWorld
	RenderWorld
	
	gui()
	Flip
	
Wend
;-------------------------------------------------------------------------------------------------------
;END MAIN LOOP
;-------------------------------------------------------------------------------------------------------


;-----------------------------------------------------------------------------------------------------
;FUNCTION GET_INPUT &lt;--- GRABS INPUT FOR THE PLAYER
;-----------------------------------------------------------------------------------------------------
Function get_input()
	If player_alive = 1
	;W key
	If KeyDown(17) Then TranslateEntity player, 0,0,.15	;move it +z
	
	;A key
	If KeyDown(30) Then TranslateEntity player, -.15,0,0;move it -x
	
	;S key
	If KeyDown(31) Then TranslateEntity player, 0,0,-.15;move it -z
	
	;D key
	If KeyDown(32) Then TranslateEntity player, .15,0,0 ;move it +x
	End If 
End Function 

	
;-----------------------------------------------------------------------------------------------------
;FUNCTION RUN_ENEMIES   &lt;---EXOCUTES ACTIONS FOR EACH ENEMY
;-----------------------------------------------------------------------------------------------------
Function run_enemies()
	For e.enemy = Each enemy	;for each enemy
		;regardless of case, the distance to the player must be known, so we'll check it here
		Local dist# = EntityDistance(e\mesh,player)
			Select e\status			;select it's status
				
				Case 1 ;ON GUARD
					EntityColor e\mesh, 200,200,0
					If dist# &lt;= 10		;if the distance is less than 10 or equal to it
						If player_alive = 1
							e\status = 2	;switch to state 2
						End If
					Else e\timer = e\timer + 1
						If e\timer &gt;= e\timer_max
							e\status = 6 ;switch to quick wander
						End If
					End If 
				
				Case 2 ;DROP NEW WAYPOINT
					Local x# = EntityX(e\mesh)	;grab the enemies' coordinates
					Local y# = EntityY(e\mesh)
					Local z# = EntityZ(e\mesh)
					
						;place the waypoint at the location
						e\return_waypoint = CopyEntity(waypoint)
						PositionEntity e\return_waypoint,x#,y#,z#
						
						e\status = 3	;then switch to case 3, which is pursue
				
				Case 3 ;PURSUE THE PLAYER
					EntityColor e\mesh, 200,0,0	;red
					If dist# &lt; 1	;then attack!
						e\status = 4;change to attack
						Exit 		;exit the loop
					End If 
					
					If dist# &lt; 12	;first, check to see if we're 12 units away or less....
						PointEntity e\mesh, player		;point enemy at the player
						MoveEntity e\mesh, 0,0,e\speed	;then make the enemy move in the pointed direction
					Else
						e\status = 5	;set to status 5, which is return to waypoint
					End If 	
		
				Case 4 ;ATTACK
					player_hp = player_hp - 1	;take 1 hp from the player
					e\status = 5				;now return to the waypoint
					Local s_x# = EntityX(player)
					Local s_y# = EntityY(player)
					Local s_z# = EntityZ(player)
					Local num = Rand(3,8)
						;create some effects
						create_particles(num,s_x,s_y,s_z)
				
				Case 5	;RETURN TO WAYPOINT
					EntityColor e\mesh, 200,0,200
					Local dist_home# = EntityDistance(e\mesh,e\return_waypoint)	;get the distance home
						If dist_home# &gt; .2							;If we're further than .2 away...
							PointEntity e\mesh, e\return_waypoint	;point at home waypoint
							MoveEntity e\mesh, 0,0,e\speed			;move them forward
						Else If dist_home# &lt;= .2
							FreeEntity e\return_waypoint			;get rid of the waypoint mesh
							e\status = 1							;and return to idle again
						End If
				
				;CASE 6 ADDED ON 2/3/12
				Case 6	;WANDER
					EntityColor e\mesh, 0,200,0	;turn it green
						;KEEP THE WANDERING SIMPLE!!!! DON'T OVERCOMPLICATE THIS!!!!!
						e\steer_angle = e\steer_angle + Rnd(-.1,.1)
							If e\steer_angle &gt; 1 Then e\steer_angle = 1
							If e\steer_angle &lt; -1 Then e\steer_angle = -1
						TurnEntity e\mesh, 0,e\steer_angle,0
						MoveEntity e\mesh, 0,0,e\speed/5
					;keep checking to see if the player is close enough to follow
						If dist# &lt;= 10		;if the distance is less than 10 or equal to it
							If player_alive = 1
								e\status = 2	;switch to state 2
							End If
						End If 

						
				Default
					e\status = 1	;if somehow e\status = anything else, we'll set it to 1
			
			End Select
	Next
End Function 

;-----------------------------------------------------------------------------------------------------
;GRAPHIC READOUTS   &lt;---This function prints readouts to the screen
;-----------------------------------------------------------------------------------------------------
Function gui()
	Text 0,0, "Use WASD keys to move around."
	Text 0,10,"Don't let the spheres touch you or you'll lose hitpoints."
	Text 0,20,"Remaining HP: " + player_hp
	
	If player_alive = 0
		Text 350,290,"You are DEAD!"
	End If 
End Function 


;-----------------------------------------------------------------------------------------------------
;RUN PARTICLES	   &lt;---This function runs the particles if there are any
;-----------------------------------------------------------------------------------------------------
Function run_particles()
For p.particle = Each particle
	p\alpha = p\alpha - p\fade_speed	;take fade speed from alpha
	EntityAlpha p\mesh, p\alpha			;set alpha to p\alpha
		If p\alpha &lt;= 0					;if alpha is &lt;= 0
			FreeEntity p\mesh			;free it
			Delete p				
			Exit						;exit the loop
		Else
			p\speed_y = p\speed_y - .01							;subtract from y speed
			MoveEntity p\mesh, p\speed_x, p\speed_y,p\speed_z	;move the particle
		End If
		
		
Next
End Function  


;-----------------------------------------------------------------------------------------------------
;CREATE PARTICLES	&lt;--- THis is called when we need to create particles
;-----------------------------------------------------------------------------------------------------
Function create_particles(num,x#,y#,z#)
	For s = 1 To num
		p.particle = New particle
		p\mesh = CopyEntity(spark)
		p\speed_x# = Rnd(-.2,.2)
		p\speed_y# = Rnd(.3)
		p\speed_z# = Rnd(-.2,.2)
		p\alpha = 1
		p\fade_speed# = Rnd(.1)
		PositionEntity p\mesh, x,y,z
	Next
End Function 

;-----------------------------------------------------------------------------------------------------
;CHECK HP	&lt;---This checks the player's HEALTH each frame
;-----------------------------------------------------------------------------------------------------
Function check_hp()
If player_hp &lt;= 0
	player_hp = 0
	If player_alive = 1
	Local s_x# = EntityX(player)
	Local s_y# = EntityY(player)
	Local s_z# = EntityZ(player)
	Local num = Rand(15)
		;create some effects
		create_particles(num,s_x,s_y,s_z)
	player_alive = 0
	End If
End If 
End Function 
</textarea> <br><br></td></tr></table><br>
<a name="1124942"></a>

<a name="1124946"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Is there a way u can make it so that the enemies when in "Wander" mode, are on a timer, and they randomly stop wandering for a couple of seconds, then start wandering again? :)<br><br><br><br>Thanks, Drak! :)<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1124955"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >(tu) ENAY</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Drak, be careful of this guy ok. Unless you want to write his entire game for him. <br><br></td></tr></table><br>
<a name="1124956"></a>

<a name="1124979"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Tu, stop hijacking my thread. -.- U have NO right to be in here! Get away from my thread. -.-<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1124997"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>Drak, be careful of this guy ok. Unless you want to write his entire game for him. <br><br> <br></div><br>I'm aware of Rez's behavior in the past.  I do truly like coding though and hopefully he learns something from this.  I have no intention of writing a whole game for anyone.  :)<br><br><div class="quote"> <br>Is there a way u can make it so that the enemies when in "Wander" mode, are on a timer, and they randomly stop wandering for a couple of seconds, then start wandering again? :)<br><br> <br></div><br>Yes I can easily do that.  Why don't you give it a try first though.<br><br><div class="quote"> <br><br>Tu, stop hijacking my thread. -.- U have NO right to be in here! Get away from my thread. -.-<br><br> <br></div><br>Rez, everyone has a right to be here.  This is a public forum.  Enay was adressing me anyway, not you. <br><br></td></tr></table><br>
<a name="1125000"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> I did, Drak. Here's what I tried so far: <br><br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">				;CASE 6 ADDED ON 2/3/12
			Case 6	;WANDER
				EntityColor e\mesh, 0,200,0	;turn it green
						;KEEP THE WANDERING SIMPLE!!!! DON'T OVERCOMPLICATE THIS!!!!!
				If MilliSecs()&gt;startTime+3000 Then
					e\steer_angle = e\steer_angle + Rnd(-.1,.1)
				If e\steer_angle &gt; 1 Then e\steer_angle = 1
				If e\steer_angle &lt; -1 Then e\steer_angle = -1
				TurnEntity e\mesh, 0,e\steer_angle,0
				MoveEntity e\mesh, 0,0,e\speed/5
			EndIf</textarea> <br><br></td></tr></table><br>
<a name="1125001"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's what I would do:<br><br>1. Wander for a few seconds.<br>2. Stop.<br>3. Turn in a random direction.<br>4. Go back to wander mode.<br>5. All while checking to see if the player is in range. Repeat.<br><br><br>You'll need to add new states for some of these actions. <br><br></td></tr></table><br>
<a name="1125004"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> I don't know how to do timed movements. only regular movements... <br><br></td></tr></table><br>
<a name="1125005"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> The code I posted doesn't use timed movements.  It uses a counter, adding 1 each frame until the threshold is reached. <br><br></td></tr></table><br>
<a name="1125006"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well, could u show me a countdown timer so I can attempt to solve this problem easily? Im better with countDOWN timers, as opposed to count UP timers. <br><br></td></tr></table><br>
<a name="1125007"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's what I have so far: <br><br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">				;CASE 6 ADDED ON 2/3/12
			Case 6	;WANDER
				EntityColor e\mesh, 0,200,0	;turn it green
						;KEEP THE WANDERING SIMPLE!!!! DON'T OVERCOMPLICATE THIS!!!!!
				;If 3 seconds have passed, activate random roaming, and wait another 3 seconds by reseting the timer
				If MilliSecs()&gt;startTime+3000 Then
					e\steer_angle = e\steer_angle + Rnd(-.1,.1)
				If e\steer_angle &gt; 1 Then e\steer_angle = 1
				If e\steer_angle &lt; -1 Then e\steer_angle = -1
				TurnEntity e\mesh, 0,e\steer_angle,0
				MoveEntity e\mesh, 0,0,e\speed/5
				startTime = MilliSecs()
			EndIf</textarea> <br><br></td></tr></table><br>
<a name="1125008"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> Read my code. It's VERY simple.<br><br>Under the enemy TYPE there is:<br><pre class=code>
Field timer
Field timer_max
</pre><br><br>When the enemy is created timer is set to 0 and timer_max is randomly set at a number between 400 and 1000.  <br>Each frame:<br><pre class=code>
e\timer = e\timer + 1
</pre><br>until e\timer is greater or equal to e\timer_max.  If it is, then move on to the next step and reset e\timer to 0.  <br><br>Read the code, it's all in there. <br><br></td></tr></table><br>
<a name="1125009"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, I've TRIED. keyword... TRIED to understand how it works, but it's still stumping me....<br><br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">				;CASE 6 ADDED ON 2/3/12
			Case 6	;WANDER
				EntityColor e\mesh, 0,200,0	;turn it green
						;KEEP THE WANDERING SIMPLE!!!! DON'T OVERCOMPLICATE THIS!!!!!
				;If 3 seconds have passed, activate random roaming, and wait another 3 seconds by reseting the timer
				If e\timer &gt;= 3
					e\steer_angle = e\steer_angle + Rnd(-.1,.1)
				If e\steer_angle &gt; 1 Then e\steer_angle = 1
				If e\steer_angle &lt; -1 Then e\steer_angle = -1
				TurnEntity e\mesh, 0,e\steer_angle,0
				MoveEntity e\mesh, 0,0,e\speed/5
				e\timer = 0
			EndIf</textarea> <br><br></td></tr></table><br>
<a name="1125010"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> You still don't understand what my counter is.  EVERY FRAME it adds 1 to the count.  Not 1 per second.  ONE per FRAME.  Now, try and undertand what is happening here.<br><br>I added it with 4 VERY SHORT lines of code.  It took me about 2 minutes.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Waypoint Example By Drak, 3/8/2010
;UPDATED 2/4/2012
;************************************************************
;THIS PROGRAM WILL:											*
;Create a player mesh (a square)							*
;Create some enemies (some spheres)							*
;Make on-the-fly waypoints for the enemies to return to		*
;Return the enemies to the waypoints						*
;Also shows some some rudimentary AI						*
;Shows some very simple particle effects as well			*
;************************************************************

AppTitle "Drak's Waypoint Example"

;-------------------------------------------------------------------------------------------------------
;INITIALIZATION
;-------------------------------------------------------------------------------------------------------
Graphics3D 800,600
SetBuffer BackBuffer()
SeedRnd MilliSecs()

Global player = CreateCube()	;create a cube for the player
EntityColor player, 0,0,200		;we'll make the player blue

Global player_hp = 10			;give the player 10 hit points
Global player_alive = 1			;player is indeed alive

Global camera = CreateCamera()	;create a camera
PositionEntity camera, 0,40,0	;raise it up in the air
RotateEntity camera, 90,0,0		;point it down


;-------------------------------------------------------------------------------------------------------
;TYPES
;-------------------------------------------------------------------------------------------------------
Type enemy
	Field mesh				;will hold the actual mesh
	Field return_waypoint	;will hold the handle of the return waypoint
	Field status			;holds a # referring to the enemies status
	Field speed#			;this will hold the speed of the enemy
	Field timer				;2/3/12 This will be a counter
	Field timer_max			;2/3/12 This is the counter's maximum
	Field steer_angle#		;2/3/12 This holds the enemy's steering angle
End Type

Type particle				
	Field mesh				;holds mesh
	Field alpha#			;holds alpha
	Field speed_x#			;speed x
	Field speed_y#			;same
	Field speed_z#			;same...
	Field fade_speed#		;fade_speed
End Type

;-------------------------------------------------------------------------------------------------------
;LETS CREATE SOME PRIMITIVES TO USE LATER	
;-------------------------------------------------------------------------------------------------------

Global spark = CreateCube()
	ScaleEntity spark, .25,.25,.25
	EntityColor spark, 255,168,0
	HideEntity spark

Global waypoint = CreateSphere()
	ScaleEntity waypoint, .5,.5,.5
	EntityColor waypoint, 0,200,0
	HideEntity waypoint

;-------------------------------------------------------------------------------------------------------
;CREATE SOME ENEMIES
;-------------------------------------------------------------------------------------------------------
For num = 1 To 5
	e.enemy = New enemy				;new enemy
	e\mesh = CreateSphere(16)		;create a sphere and store it in e\mesh
	e\speed = .1					;set the speed of each enemy
	e\status = 1					;set status to 1
	e\timer = 0						;2/3/12	set timer to 0
	e\timer_max = Rand(400,1000)	;2/3/12	pick a maximum timer
	PositionEntity e\mesh, Rand(-20,20),0,Rand(-20,20)	;place the enemies randomly
	EntityAlpha e\mesh, .5
	RotateEntity e\mesh, 0,Rand(360),0
Next

;-------------------------------------------------------------------------------------------------------
;MAIN LOOP
;-------------------------------------------------------------------------------------------------------
While Not KeyHit(1)
	get_input()
	run_enemies()
	run_particles()
	check_hp()
	
	
	UpdateWorld
	RenderWorld
	
	gui()
	Flip
	
Wend
;-------------------------------------------------------------------------------------------------------
;END MAIN LOOP
;-------------------------------------------------------------------------------------------------------


;-----------------------------------------------------------------------------------------------------
;FUNCTION GET_INPUT &lt;--- GRABS INPUT FOR THE PLAYER
;-----------------------------------------------------------------------------------------------------
Function get_input()
	If player_alive = 1
	;W key
	If KeyDown(17) Then TranslateEntity player, 0,0,.15	;move it +z
	
	;A key
	If KeyDown(30) Then TranslateEntity player, -.15,0,0;move it -x
	
	;S key
	If KeyDown(31) Then TranslateEntity player, 0,0,-.15;move it -z
	
	;D key
	If KeyDown(32) Then TranslateEntity player, .15,0,0 ;move it +x
	End If 
End Function 

	
;-----------------------------------------------------------------------------------------------------
;FUNCTION RUN_ENEMIES   &lt;---EXOCUTES ACTIONS FOR EACH ENEMY
;-----------------------------------------------------------------------------------------------------
Function run_enemies()
	For e.enemy = Each enemy	;for each enemy
		;regardless of case, the distance to the player must be known, so we'll check it here
		Local dist# = EntityDistance(e\mesh,player)
			Select e\status			;select it's status
				
				Case 1 ;ON GUARD
					EntityColor e\mesh, 200,200,0
					If dist# &lt;= 10		;if the distance is less than 10 or equal to it
						If player_alive = 1
							e\status = 2	;switch to state 2
						End If
					Else e\timer = e\timer + 1
						If e\timer &gt;= e\timer_max
							e\status = 6 ;switch to quick wander
							e\timer = 0	 ;reset the timer to 0
						End If
					End If 
				
				Case 2 ;DROP NEW WAYPOINT
					Local x# = EntityX(e\mesh)	;grab the enemies' coordinates
					Local y# = EntityY(e\mesh)
					Local z# = EntityZ(e\mesh)
					
						;place the waypoint at the location
						e\return_waypoint = CopyEntity(waypoint)
						PositionEntity e\return_waypoint,x#,y#,z#
						
						e\status = 3	;then switch to case 3, which is pursue
				
				Case 3 ;PURSUE THE PLAYER
					EntityColor e\mesh, 200,0,0	;red
					If dist# &lt; 1	;then attack!
						e\status = 4;change to attack
						Exit 		;exit the loop
					End If 
					
					If dist# &lt; 12	;first, check to see if we're 12 units away or less....
						PointEntity e\mesh, player		;point enemy at the player
						MoveEntity e\mesh, 0,0,e\speed	;then make the enemy move in the pointed direction
					Else
						e\status = 5	;set to status 5, which is return to waypoint
					End If 	
		
				Case 4 ;ATTACK
					player_hp = player_hp - 1	;take 1 hp from the player
					e\status = 5				;now return to the waypoint
					Local s_x# = EntityX(player)
					Local s_y# = EntityY(player)
					Local s_z# = EntityZ(player)
					Local num = Rand(3,8)
						;create some effects
						create_particles(num,s_x,s_y,s_z)
				
				Case 5	;RETURN TO WAYPOINT
					EntityColor e\mesh, 200,0,200
					Local dist_home# = EntityDistance(e\mesh,e\return_waypoint)	;get the distance home
						If dist_home# &gt; .2							;If we're further than .2 away...
							PointEntity e\mesh, e\return_waypoint	;point at home waypoint
							MoveEntity e\mesh, 0,0,e\speed			;move them forward
						Else If dist_home# &lt;= .2
							FreeEntity e\return_waypoint			;get rid of the waypoint mesh
							e\status = 1							;and return to idle again
						End If
				
				;CASE 6 ADDED ON 2/3/12
				Case 6	;WANDER
					EntityColor e\mesh, 0,200,0	;turn it green
						;KEEP THE WANDERING SIMPLE!!!! DON'T OVERCOMPLICATE THIS!!!!!
						
						e\timer = e\timer + 1
						If e\timer &lt;= e\timer_max
							e\steer_angle = e\steer_angle + Rnd(-.1,.1)
								If e\steer_angle &gt; 1 Then e\steer_angle = 1
								If e\steer_angle &lt; -1 Then e\steer_angle = -1
							TurnEntity e\mesh, 0,e\steer_angle,0
							MoveEntity e\mesh, 0,0,e\speed/5
						Else 
							e\timer = 0
							RotateEntity e\mesh, 0,Rand(360),0
							e\status = 1
						End If 
					
					;keep checking to see if the player is close enough to follow
						If dist# &lt;= 10		;if the distance is less than 10 or equal to it
							If player_alive = 1
								e\status = 2	;switch to state 2
							End If
						End If 

						
				Default
					e\status = 1	;if somehow e\status = anything else, we'll set it to 1
			
			End Select
	Next
End Function 

;-----------------------------------------------------------------------------------------------------
;GRAPHIC READOUTS   &lt;---This function prints readouts to the screen
;-----------------------------------------------------------------------------------------------------
Function gui()
	Text 0,0, "Use WASD keys to move around."
	Text 0,10,"Don't let the spheres touch you or you'll lose hitpoints."
	Text 0,20,"Remaining HP: " + player_hp
	
	If player_alive = 0
		Text 350,290,"You are DEAD!"
	End If 
End Function 


;-----------------------------------------------------------------------------------------------------
;RUN PARTICLES	   &lt;---This function runs the particles if there are any
;-----------------------------------------------------------------------------------------------------
Function run_particles()
For p.particle = Each particle
	p\alpha = p\alpha - p\fade_speed	;take fade speed from alpha
	EntityAlpha p\mesh, p\alpha			;set alpha to p\alpha
		If p\alpha &lt;= 0					;if alpha is &lt;= 0
			FreeEntity p\mesh			;free it
			Delete p				
			Exit						;exit the loop
		Else
			p\speed_y = p\speed_y - .01							;subtract from y speed
			MoveEntity p\mesh, p\speed_x, p\speed_y,p\speed_z	;move the particle
		End If
		
		
Next
End Function  


;-----------------------------------------------------------------------------------------------------
;CREATE PARTICLES	&lt;--- THis is called when we need to create particles
;-----------------------------------------------------------------------------------------------------
Function create_particles(num,x#,y#,z#)
	For s = 1 To num
		p.particle = New particle
		p\mesh = CopyEntity(spark)
		p\speed_x# = Rnd(-.2,.2)
		p\speed_y# = Rnd(.3)
		p\speed_z# = Rnd(-.2,.2)
		p\alpha = 1
		p\fade_speed# = Rnd(.1)
		PositionEntity p\mesh, x,y,z
	Next
End Function 

;-----------------------------------------------------------------------------------------------------
;CHECK HP	&lt;---This checks the player's HEALTH each frame
;-----------------------------------------------------------------------------------------------------
Function check_hp()
If player_hp &lt;= 0
	player_hp = 0
	If player_alive = 1
	Local s_x# = EntityX(player)
	Local s_y# = EntityY(player)
	Local s_z# = EntityZ(player)
	Local num = Rand(15)
		;create some effects
		create_particles(num,s_x,s_y,s_z)
	player_alive = 0
	End If
End If 
End Function 
</textarea> <br><br></td></tr></table><br>
<a name="1125011"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, so you first initiate the timer. Then if the FRAME count is &lt; or equal to the max frame count, then start roaming randomly, otherwise, reset the frame count to 0, and rotate the enemy so that it's yaw is a random rotation within 360 degrees, then set their "case" back to 1.<br><br><br>Am I correct? :) <br><br></td></tr></table><br>
<a name="1125020"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes, each enemies' e\timer actually counts the frames that have passed, correct.  If that number is less than the maximum, they roam. (remember the maximum was set randomly between 400 and 1000 at each enemies creation)<br>When it reaches the max, they go back to status 1, which is idle.  While idle the counter continues to count and if they sit too long, they go back into wander mode, and repeat, and repeat, until they see the player.  So yes, you are correct. <br><br></td></tr></table><br>
<a name="1125023"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#23">[#23]</a></td></tr></table></td></tr><tr ><td class="posttext"> YAY! I finally learned something! =D I think o.o <br><br></td></tr></table><br>
<a name="1125034"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#24">[#24]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey Drak, u might wanna update ur tutorial 1 more time so other ppl can see it without having to search for this thread :) <br><br></td></tr></table><br>
<a name="1125124"></a>

<a name="1125125"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#25">[#25]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've taken it to the next level. I changed the enemies to Dwarves. I added player to object collision, player to land collision, and object to object collision. I fixed a rotation glitch when the enemy returns to its waypoint, due to collision.<br><br><br><br>PROBLEM is, the enemies wont pursue the player anymore....<br><br><br><br>Here's the project: <br><br><br><br><a href="http://www.mediafire.com/?do051b8fw2h5tyj" target="_blank">http://www.mediafire.com/?do051b8fw2h5tyj</a><br><br><br><br>Thanks for any help! :)<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1125163"></a>

<a name="1125164"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#26">[#26]</a></td></tr></table></td></tr><tr ><td class="posttext"> The enemies did pursue the player in my example.  If they don't now, then something you added or changed modified the enemies' behavior.<br><br>Without any code no one can help.<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1125174"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#27">[#27]</a></td></tr></table></td></tr><tr ><td class="posttext"> Why don't you remove your changes, implement them one by one, and deduct what change you made that's causing the "bug". <br><br></td></tr></table><br>
<a name="1125194"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#28">[#28]</a></td></tr></table></td></tr><tr ><td class="posttext"> I DID give u the source... It's in the mediafire file <br><br></td></tr></table><br>
<a name="1125212"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#29">[#29]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think he means post it in a codebox here? <br><br></td></tr></table><br>
<a name="1125214"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#30">[#30]</a></td></tr></table></td></tr><tr ><td class="posttext"> ooooo! my bad! sorry bro! :)<br><br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">;Waypoint Example By Drak, 3/8/2010
;UPDATED 2/4/2012
;************************************************************
;THIS PROGRAM WILL:											*
;Create a player mesh (a square)							*
;Create some enemies (some spheres)							*
;Make on-the-fly waypoints for the enemies to return to		*
;Return the enemies to the waypoints						*
;Also shows some some rudimentary AI						*
;Shows some very simple particle effects as well			*
;************************************************************



;-------------------------------------------------------------------------------------------------------
;INITIALIZATION
;-------------------------------------------------------------------------------------------------------
Graphics3D 800,600,0,2
SetBuffer BackBuffer()
SeedRnd MilliSecs()

AmbientLight 255, 255, 255

Global type_player 	= 1
Global type_land 	= 2
Global type_obj 	= 3

Collisions type_player, type_land, 2, 3
Collisions type_player, type_obj, 2, 2
Collisions type_obj, type_obj, 2, 3

Global player = LoadAnimMesh("models\dwarf1.b3d")	;create a cube for the player
EntityColor player, 0,0,200		;we'll make the player blue
ScaleEntity player, .1, .1, .1
EntityType player, type_player

Global player_hp = 10			;give the player 10 hit points
Global player_alive = 1			;player is indeed alive

Global camera = CreateCamera()	;create a camera
CameraRange camera, .1, 999999999

Global sky = CreateCube()
ScaleEntity sky, 10000, 10000, 10000
EntityColor sky, 102, 102, 255
FlipMesh sky

Global Land = CreatePlane()
EntityColor Land, 0, 64, 0
EntityType Land, type_land

;-------------------------------------------------------------------------------------------------------
;TYPES
;-------------------------------------------------------------------------------------------------------
Type enemy
	Field mesh				;will hold the actual mesh
	Field return_waypoint	;will hold the handle of the return waypoint
	Field status			;holds a # referring to the enemies status
	Field speed#			;this will hold the speed of the enemy
	Field timer				;2/3/12 This will be a counter
	Field timer_max			;2/3/12 This is the counter's maximum
	Field steer_angle#		;2/3/12 This holds the enemy's steering angle
End Type

Type particle				
	Field mesh				;holds mesh
	Field alpha#			;holds alpha
	Field speed_x#			;speed x
	Field speed_y#			;same
	Field speed_z#			;same...
	Field fade_speed#		;fade_speed
End Type

;-------------------------------------------------------------------------------------------------------
;LETS CREATE SOME PRIMITIVES TO USE LATER	
;-------------------------------------------------------------------------------------------------------

Global spark = CreateCube()
ScaleEntity spark, .25,.25,.25
EntityColor spark, 255,168,0
HideEntity spark

Global waypoint = CreateSphere()
ScaleEntity waypoint, .5,.5,.5
EntityColor waypoint, 0,200,0
HideEntity waypoint

;-------------------------------------------------------------------------------------------------------
;CREATE SOME ENEMIES
;-------------------------------------------------------------------------------------------------------
For num = 1 To 5
	e.enemy = New enemy				;new enemy
	e\mesh = LoadAnimMesh("models\dwarf1.b3d") ;CreateSphere(16)		;create a sphere and store it in e\mesh
	e\speed = .1					;set the speed of each enemy
	e\status = 1					;set status to 1
	e\timer = 0						;2/3/12	set timer to 0
	e\timer_max = Rand(400,1000)	;2/3/12	pick a maximum timer
	PositionEntity e\mesh, Rand(-20,20),0,Rand(-20,20)	;place the enemies randomly
	;EntityAlpha e\mesh, .5
	RotateEntity e\mesh, 0,Rand(360),0
	ScaleEntity e\mesh, .1, .1, .1
	EntityType e\mesh, type_obj
Next

Global turnonanim = False

;-------------------------------------------------------------------------------------------------------
;MAIN LOOP
;-------------------------------------------------------------------------------------------------------
While Not KeyHit(1)
	For e.enemy = Each enemy
		If turnonanim  = True Then Animate e\mesh, 1, .1, 1, 15
	Next
	SuperCam(camera,player,.02,200,4,0,0)
	py# = EntityY#(player, True)
	If py# &lt; 1.0 Then py# = 1.0
	If py# &gt; 1.0 Then gravity# = gravity# - .1
	PositionEntity player, EntityX#(player, True), py#+gravity#, EntityZ#(player, True)
	get_input()
	run_enemies()
	run_particles()
	check_hp()
	
	
	UpdateWorld
	RenderWorld
	
	gui()
	Flip
	
Wend
;-------------------------------------------------------------------------------------------------------
;END MAIN LOOP
;-------------------------------------------------------------------------------------------------------


;-----------------------------------------------------------------------------------------------------
;FUNCTION GET_INPUT &lt;--- GRABS INPUT FOR THE PLAYER
;-----------------------------------------------------------------------------------------------------
Function get_input()
	If player_alive = 1
		MoveEntity player, 0, 0, (KeyDown(17)-KeyDown(31) Or KeyDown(200)-KeyDown(208))*.5
		TurnEntity player, 0, (KeyDown(30)-KeyDown(32) Or KeyDown(203)-KeyDown(205))*1, 0
	End If 
End Function 


;-----------------------------------------------------------------------------------------------------
;FUNCTION RUN_ENEMIES   &lt;---EXOCUTES ACTIONS FOR EACH ENEMY
;-----------------------------------------------------------------------------------------------------
Function run_enemies()
	For e.enemy = Each enemy	;for each enemy
		;regardless of case, the distance to the player must be known, so we'll check it here
		Local dist# = EntityDistance(e\mesh,player)
		Select e\status			;select it's status
				
			Case 1 ;ON GUARD
				EntityColor e\mesh, 200,200,0
				If dist# &lt;= 10		;if the distance is less than 10 or equal to it
					If player_alive = 1
						e\status = 2	;switch to state 2
					End If
					Else e\timer = e\timer + 1
					If e\timer &gt;= e\timer_max
						e\status = 6 ;switch to quick wander
						e\timer = 0	 ;reset the timer to 0
					End If
				End If 
				
			Case 2 ;DROP NEW WAYPOINT
				Local x# = EntityX(e\mesh)	;grab the enemies' coordinates
				Local y# = EntityY(e\mesh)
				Local z# = EntityZ(e\mesh)
				
						;place the waypoint at the location
				e\return_waypoint = CopyEntity(waypoint)
				PositionEntity e\return_waypoint,x#,y#,z#
				
				e\status = 3	;then switch to case 3, which is pursue
				
			Case 3 ;PURSUE THE PLAYER
				EntityColor e\mesh, 200,0,0	;red
				If dist# &lt; 1	;then attack!
					e\status = 4;change to attack
					Exit 		;exit the loop
				End If 
				
				If dist# &lt; 12	;first, check to see if we're 12 units away or less....
					PointEntity e\mesh, player		;point enemy at the player
					MoveEntity e\mesh, 0,0,e\speed	;then make the enemy move in the pointed direction
				Else
					e\status = 5	;set to status 5, which is return to waypoint
				End If 	
				
			Case 4 ;ATTACK
				player_hp = player_hp - 1	;take 1 hp from the player
				Local s_x# = EntityX(player)
				Local s_y# = EntityY(player)
				Local s_z# = EntityZ(player)
				Local num = Rand(3,8)
						;create some effects
				create_particles(num,s_x,s_y,s_z)
				e\status = 5				;now return to the waypoint
				
			Case 5	;RETURN TO WAYPOINT
				EntityColor e\mesh, 200,0,200
				Local dist_home# = EntityDistance(e\mesh,e\return_waypoint)	;get the distance home
				If dist_home# &gt; .2							;If we're further than .2 away...
					PointEntity e\mesh, e\return_waypoint	;point at home waypoint
					MoveEntity e\mesh, 0,0,e\speed			;move them forward
				Else If dist_home# &lt;= .2
					RotateEntity e\mesh, 0, 0, 0
					FreeEntity e\return_waypoint			;get rid of the waypoint mesh
					e\status = 1							;and return to idle again
				End If
				
				;CASE 6 ADDED ON 2/3/12
			Case 6	;WANDER
				EntityColor e\mesh, 0,200,0	;turn it green
						;KEEP THE WANDERING SIMPLE!!!! DON'T OVERCOMPLICATE THIS!!!!!
				
				e\timer = e\timer + 1
				If e\timer &lt;= e\timer_max
					e\steer_angle = e\steer_angle + Rnd(-.1,.1)
					If e\steer_angle &gt; 1 Then e\steer_angle = 1
					If e\steer_angle &lt; -1 Then e\steer_angle = -1
					TurnEntity e\mesh, 0,e\steer_angle,0
					MoveEntity e\mesh, 0,0,e\speed/5
				Else 
					e\timer = 0
					RotateEntity e\mesh, 0,Rand(360),0
					e\status = 1
				End If 
				
					;keep checking to see if the player is close enough to follow
				If dist# &lt;= 10		;if the distance is less than 10 or equal to it
					If player_alive = 1
						e\status = 2	;switch to state 2
					End If
				End If 
				
				
			Default
				e\status = 1	;if somehow e\status = anything else, we'll set it to 1
				
				
		End Select
	Next
End Function 

;-----------------------------------------------------------------------------------------------------
;GRAPHIC READOUTS   &lt;---This function prints readouts to the screen
;-----------------------------------------------------------------------------------------------------
Function gui()
	Text 0,0, "Use WASD keys to move around."
	Text 0,10,"Don't let the spheres touch you or you'll lose hitpoints."
	Text 0,20,"Remaining HP: " + player_hp
	
	If player_alive = 0
		Text 350,290,"You are DEAD!"
	End If 
End Function 


;-----------------------------------------------------------------------------------------------------
;RUN PARTICLES	   &lt;---This function runs the particles if there are any
;-----------------------------------------------------------------------------------------------------
Function run_particles()
	For p.particle = Each particle
		p\alpha = p\alpha - p\fade_speed	;take fade speed from alpha
		EntityAlpha p\mesh, p\alpha			;set alpha to p\alpha
		If p\alpha &lt;= 0					;if alpha is &lt;= 0
			FreeEntity p\mesh			;free it
			Delete p				
			Exit						;exit the loop
		Else
			p\speed_y = p\speed_y - .01							;subtract from y speed
			MoveEntity p\mesh, p\speed_x, p\speed_y,p\speed_z	;move the particle
		End If
		
		
	Next
End Function  


;-----------------------------------------------------------------------------------------------------
;CREATE PARTICLES	&lt;--- THis is called when we need to create particles
;-----------------------------------------------------------------------------------------------------
Function create_particles(num,x#,y#,z#)
	For s = 1 To num
		p.particle = New particle
		p\mesh = CopyEntity(spark)
		p\speed_x# = Rnd(-.2,.2)
		p\speed_y# = Rnd(.3)
		p\speed_z# = Rnd(-.2,.2)
		p\alpha = 1
		p\fade_speed# = Rnd(.1)
		PositionEntity p\mesh, x,y,z
	Next
End Function 

;-----------------------------------------------------------------------------------------------------
;CHECK HP	&lt;---This checks the player's HEALTH each frame
;-----------------------------------------------------------------------------------------------------
Function check_hp()
	If player_hp &lt;= 0
		player_hp = 0
		If player_alive = 1
			Local s_x# = EntityX(player)
			Local s_y# = EntityY(player)
			Local s_z# = EntityZ(player)
			Local num = Rand(15)
		;create some effects
			create_particles(num,s_x,s_y,s_z)
			player_alive = 0
		End If
	End If 
End Function 



Function SuperCam(cam,ent,cspeed#,dist#,hite#,xrot#,tilt#)
	
	TFormPoint 0,hite#,-dist#,ent,0
	
	cx#=(TFormedX()-EntityX(cam))*cspeed#
	cy#=(TFormedY()-EntityY(cam))*cspeed#
	cz#=(TFormedZ()-EntityZ(cam))*cspeed#
	
	TranslateEntity cam,cx,cy,cz
	PointEntity cam,ent
	RotateEntity cam,xrot#,EntityYaw(cam),tilt#
	
End Function</textarea> <br><br></td></tr></table><br>
<a name="1125300"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#31">[#31]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, without looking through the code... do the enemies change to the proper color even though they don't follow?  That is the reason I had the enemies' color change, so it's easy to see what state they are in.  If they don't change color, then they're not switching to the proper state.  If they do switch color but don't follow, then there is another problem. <br><br></td></tr></table><br>
<a name="1125305"></a>

<a name="1125307"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#32">[#32]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well I took out the media (the dwarf) so I could run the program.  I couldn't replicate the problem.  The enemies still pursue fine.<br><br>Oh, and CameraRange(1,999999999)?  Seriously? 999 million units?<br>The default is CameraRange(1,1000) and you don't even have to type it in.  You can't see things 999 million units away let alone 1000.<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1125319"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#33">[#33]</a></td></tr></table></td></tr><tr ><td class="posttext"> Problem with that camerarange also, is your seriously reducing the resolution of the world. Remember it's stored as a 32 bit number. <br><br></td></tr></table><br>
<a name="1125338"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#34">[#34]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yea, they switch colors. <br><br></td></tr></table><br>
<a name="1125442"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#35">[#35]</a></td></tr></table></td></tr><tr ><td class="posttext"> If they switch colors then they are changing states correctly.  Now, as I said before I couldn't replicate the problem with the code that you posted.  The only change I did was replace the dwarf with a primitive. <br><br></td></tr></table><br>
<a name="1125447"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#36">[#36]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well the problem its having is with collisions. <br><br></td></tr></table><br>
<a name="1125568"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#37">[#37]</a></td></tr></table></td></tr><tr ><td class="posttext"> Can u get this to work with collisions?<br><br><br><br>Thanks, Drak! :) <br><br></td></tr></table><br>
<a name="1125592"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#38">[#38]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm not following why you need collisions with this code example.  I took care of the collisions by saying if:<br><pre class=code>
;psuedocode:
If distance between enemy mesh and player mesh &lt; 1
DamagePlayer()
End if
</pre> <br><br></td></tr></table><br>
<a name="1125606"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#39">[#39]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well, because I don't want the players to walk through an enemy NPC. <br><br></td></tr></table><br>
<a name="1125619"></a>

<a name="1125621"></a>

<a name="1125622"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#40">[#40]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is an example on controlling types using states. It is not a collision tutorial.  It's not meant to be made into a different game, either.  In this example code, the player is meant to avoid the enemies.  The purpose of a tutorial is to use it to learn so you can implement things into your (original) code.  If you're having trouble with collisions that's a whole different subject than the thread title.<br><br><font class="tiny">Last edited 2012</font><br><div class="quote"> <br>Can u get this to work with collisions?<br> <br></div><br>Yes I can, but this again is a tutorial on controlling npcs using types.  So, no I will not.<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1125705"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#41">[#41]</a></td></tr></table></td></tr><tr ><td class="posttext"> ok, fine.. -.- <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
