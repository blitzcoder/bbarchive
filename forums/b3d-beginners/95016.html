<!DOCTYPE html><html lang="en" ><head ><title >Bezier Waypoint creator</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Bezier Waypoint creator</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Bezier Waypoint creator</a><br><br>
<a name="1092695"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi all, can someone help me work on a waypoint creator, and make it read all the coordinates from data statements?<br><br>Here's a modified version of the 3D Bezier object waypoint code: <br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Global Path_x#, Path_y#, Path_z#

Type tWayPoint

	Field PathId	; with path it belongs
	Field Entity	; a pivot with position and orientation
	Field cp1		; control point1
	Field cp2		; control point2
	Field nSteps	; number of interpolated steps to the next waypoint
	Field CurrStep	; current step interpolated (from 1 to nSteps)
	Field Pitch#, Yaw#, Roll#
	

End Type

Type tPath

	Field PathId		;id 
	Field pFirst		;handle to the first on this list
	Field pLast			;same for last

	Field WPCount		;quantity of waypoints on this path
	Field CurrentWP		;handlw of the current waypoint, in whic we are
	Field CurrPos		;pivot wiht pos and orientation of the step in the path
						;acrodingly with CurrentStep of the CurrentWP

End Type

Global maxwaypoints = 5

;wp functions

Function NextWP.tWayPoint(wp.tWayPoint)
	wpTemp.tWayPoint = After wp
	If wpTemp = Null
		wpTemp = FirstWP()
	EndIf
	Return wpTemp
End Function

Function PrevWP.tWayPoint(wp.tWayPoint)
	wpTemp.tWayPoint = Before wp
	If wpTemp = Null
		wpTemp = LastWP()
	EndIf
	Return wpTemp
End Function

Function FirstWP.tWayPoint()
	Return First tWayPoint
End Function


Function LastWP.tWayPoint()
	Return Last tWayPoint
End Function

Function NewWP.tWayPoint(Path.tPath, x#, y#, z#, nSteps=100, pitch#=0, yaw#=0, roll#=0)

	wp.tWayPoint = New tWayPoint

	wp\PathID = Path\PathID
	
	Path\WPCount = Path\WPCount+1
	If Path\pFirst=0 Then
		Path\pFirst = Handle(wp)
		Path\CurrentWP = Path\pFirst
		PositionEntity Path\CurrPos,x,y,z
		RotateEntity Path\CurrPos,pitch,yaw,roll
	End If
	Path\pLast = Handle(wp)
	wp\Entity = CreatePivot()
	wp\cp1 = CreatePivot(wp\Entity)
	wp\cp2 = CreatePivot(wp\Entity)
	PositionEntity wp\Entity,x,y,z
	wp\nSteps = nSteps
	wp\CurrStep = 0
	wp\pitch = pitch
	wp\yaw = yaw
	wp\roll = roll
	
	Return wp

End Function

;path functions


Function PathNextWP.tWayPoint(Path.tPath)
	Local wp.tWayPoint
	If Path\CurrentWP = Path\pLast Then
		Path\CurrentWP = Path\pFirst
	Else
		wp = After PathCurrWP(Path)
		Path\CurrentWP = Handle(wp)
	End If
	Return PathCurrWP(Path)
End Function

Function PathPrevWP.tWayPoint(Path.tPath)
	Local wp.tWayPoint
	If Path\CurrentWP = Path\pFirst Then
		Path\CurrentWP = Path\pLast
	Else
		wp = Before PathCurrWP(Path)
		Path\CurrentWP = Handle(wp)
	End If
	Return PathCurrWP(Path)
End Function

Function PathFirstWP.tWayPoint(Path.tPath)
	Path\CurrentWP = Path\pFirst
	Return PathCurrWP(Path)
End Function

Function PathLastWP.tWayPoint(Path.tPath)
	Path\CurrentWP = Path\pLast
	Return PathCurrWP(Path)
End Function

Function PathCurrWP.tWayPoint(Path.tPath)
	Return Object.tWayPoint(Path\CurrentWP)
End Function

Function NewPath.tPath()

	Path.tPath = New tPath
	Path\PathId = Handle(Path)
	Path\CurrPos = CreatePivot()
	
	Return Path
		
End Function


Function InitPath(Path.tPath, Save=False)

	;precalculates 2 control points for each WayPoint
	;they are needed for the InterpolatePos function based on a Bezier curve
	
	If Save Then
		CurrentWP = Path\CurrentWP
	End If

	CurrWP.tWayPoint = PathFirstWP(Path)

	For i=1 To Path\WPCount+1
	
		PrevWP.tWayPoint = PathPrevWP(Path)	;previous	-1
		CurrWP.tWayPoint = PathNextWP(Path)	;actual		 0
		NextWP.tWayPoint = PathNextWP(Path)	;next		+1, so in each loop advance one element

		If save Then
			If Handle(CurrWP)&lt;&gt;Path\CurrentWP Then CurrWP\CurrStep=0
		Else
			CurrWP\CurrStep = 0
		End If

		NextAux = CopyEntity(NextWP\Entity)
		PrevAux = CopyEntity(PrevWP\Entity)
		PointEntity CurrWP\Entity,NextAux
		PointEntity PrevAux,NextAux
		
		RotateEntity CurrWP\cp1, EntityPitch(PrevAux,True), EntityYaw(PrevAux,True), EntityRoll(PrevAux,True),True
		Dist# = D3D(CurrWP\Entity,NextAux)
		PositionEntity CurrWP\cp1,0,0,0
		MoveEntity CurrWP\cp1,0,0,-Dist/3.0
		PositionEntity PrevWp\cp2, EntityX(CurrWP\cp1,True), EntityY(CurrWP\cp1,True), EntityZ(CurrWP\cp1,True), True
		MoveEntity CurrWP\cp1,0,0,2*Dist/3
		FreeEntity NextAux
		FreeEntity PrevAux

	Next
	
	If Save Then
		Path\CurrentWP = CurrentWP
	Else
		wp.tWayPoint = PathFirstWP(Path)
		PositionEntity Path\CurrPos, EntityX(wp\Entity,True), EntityY(wp\Entity,True), EntityY(wp\Entity,True)
	End If


End Function

Function InitPaths(Save=True)

	For Path.tPath = Each tPath
		
		InitPath(Path,Save)
		
	Next
End Function

Function PathNextPos(Path.tPath, SignalEnd=False)

	Local CurrWP.tWayPoint, NextWP.tWayPoint
	
	CurrWP = PathCurrWP(Path)

	If CurrWP\CurrStep=CurrWP\nSteps Then
		CurrWP\CurrStep=0
		CurrWP = PathNextWP(Path)
		If Path\CurrentWP = Path\pFirst Then
			If SignalEnd Then Stop=True
		End If
	End If

	If Not(Stop) Then
	
		NextWP = PathNextWP(Path)
		CurrWP = PathPrevWP(Path)
		
		t# = (1.0*(Float(CurrWP\CurrStep))) / (Float(CurrWP\nSteps))
		
		InterpolatePos( t, CurrWP\Entity, CurrWP\cp1, CurrWP\cp2, NextWP\Entity)
		
		Pitch# = Interpolate(t,CurrWP\Pitch,NextWP\Pitch)
		Yaw#   = Interpolate(t,CurrWP\Yaw  ,NextWP\Yaw  )
		Roll#  = Interpolate(t,CurrWP\Roll ,NextWP\Roll )
		
		PositionEntity Path\CurrPos, Path_x, Path_y, Path_z
		RotateEntity Path\CurrPos, Pitch, Yaw, Roll
	
		CurrWP\CurrStep = CurrWP\CurrStep + 1
	
		
		Return Path\CurrPos
	Else
		Return -1
	End If

End Function

Function PathCurrPos(Path.tPath)
	Return Path\CurrPos
End Function

Function PathsNextPos(SignalEnd=False)
	For Path.tPath = Each tPath
		PathNextPos(Path,SignalEnd)
	Next
End Function

Function Interpolate#(t#, v1#, v2#)
	Return v1 + (v2-v1)*t
End Function

Function InterpolatePos( t#, p1, p2, p3, p4)

	x1# = EntityX(p1,True)
	y1# = EntityY(p1,True)
	z1# = EntityZ(p1,True)
	
	x2# = EntityX(p2,True)
	y2# = EntityY(p2,True)
	z2# = EntityZ(p2,True)

	x3# = EntityX(p3,True)
	y3# = EntityY(p3,True)
	z3# = EntityZ(p3,True)
	
	x4# = EntityX(p4,True)
	y4# = EntityY(p4,True)
	z4# = EntityZ(p4,True)
	
	Path_x = x1 * (1-t)^3 + 3 * x2 * (1-t)^2 * t + 3 * x3 * (1-t) * t^2 + x4 * t^3 
	Path_y = y1 * (1-t)^3 + 3 * y2 * (1-t)^2 * t + 3 * y3 * (1-t) * t^2 + y4 * t^3 
	Path_z = z1 * (1-t)^3 + 3 * z2 * (1-t)^2 * t + 3 * z3 * (1-t) * t^2 + z4 * t^3 

	
End Function


Function PlotPath(camera, Path.tPath)

	SaveCurrent = Path\CurrentWP

	Color 200,200,200
	
	LockBuffer()
	
	CurrWP.tWayPoint = PathFirstWP(Path)
	
	For wp=1 To Path\WPCount
	
		CurrWP.tWayPoint = PathCurrWP(Path)
		NextWP.tWayPoint = PathNextWP(Path)
	
		t# = 1.0 / CurrWP\nSteps
		tinc# = t	
	
		For i=1 To CurrWP\nSteps
		
			InterpolatePos( t, CurrWP\Entity, CurrWP\cp1, CurrWP\cp2, NextWP\Entity)
			
			CameraProject( camera, Path_x, Path_y, Path_z)
			x=ProjectedX()
			y=ProjectedY()
			If x&gt;0 And x&lt;GraphicsWidth() And y&gt;0 And y&lt;GraphicsHeight()
				WritePixelFast ProjectedX(), ProjectedY(), $FFFFFFFF
			End If
			t = t + tinc
		
		Next

	Next
	
	UnlockBuffer()

	Path\CurrentWP = SaveCurrent

End Function

Function PlotPaths(Camera)
	For Path.tPath = Each tPath
		PlotPath(Camera, Path)
	Next
End Function

Function D3D#(obj1, obj2)
	x# = EntityX(obj1) - EntityX(obj2)
	y# = EntityY(obj1) - EntityY(obj2)
	z# = EntityZ(obj1) - EntityZ(obj2)
	Return Sqr(x*x + y*y + z*z)
End Function

Function Example()
	;
	;	Path following system
	;
	;	
	
	Graphics3D 800,600,0,2
	SetBuffer BackBuffer()
	
	pivotCam=CreatePivot()
	camera = CreateCamera(pivotcam)
	light = CreateLight(2)
	PositionEntity light,20,10,-10
	LightRange light,50
	PositionEntity camera,0,0,-10
	
	Viajero = CreateCone()	;viajero : voyager
	RotateMesh Viajero,90,0,0
	EntityColor Viajero,255,255,0
	Temp = CreateCube()
	ScaleEntity Temp,2,.2,.5
	EntityColor Temp,255,0,0
	EntityParent Temp,Viajero
	
	
	; Usage
	;
	;Create some paths	: 
	;						Path_N.tPaht = NewPath()
	;
	
	;Add WayPoints 		:
	;						WP.tWayPoint = NewWP(Path_n.tPath, x#, y#, z#, steps, pitch#, yaw#, roll#)	
	;
	;
	
	; define at least 3 points: 
	;		x,y,z, pitch, yaw, roll	(position and orientation of the waypoint)
	;		Steps are the number of interpolations the system will do to reach the next WayPoint
	;
	;
	
	;Initialize the path:
	;					InitPath(Path_n.tPath), or InitPaths() for init all defined path's
	;
	;InitPath precalculate 2 control points for each waypoint defined on the path, they are needed for the InterpolatePos function
	;wich is based on a Bezier curve.
	
	;
	;To move to the next position: PathNextPos(Path_N), or PathsNextPos() for all
	;
	;this function returns the handle to a pivot that has the newly calculated position and orientation
	;
	
	
	;in this demo:
	;
	;
	;Path1 and Path2 are identical: defined by the same waypoints
	;Path3 and Path4 are identical
	;
	;in the demo, i use Path1 and Path2 for the same object. Path2 is one step ahead Path1 so, we know the next position
	;and force the object to point to the next step.
	;
	;Path3 and Path4 has associated diferent objects and one of them is automatically stepped, the other is stepped under
	;user control (pressing the "A" key)
	;
	
	Restore WayPointDefinition
	
	
		Path1.tPath = NewPath()		;create Path1
	
		EntityParent viajero,Path1\CurrPos
		
		For nPoint = 1 To maxwaypoints			;define some waypoints
			Read  x# 
			Read  y# 
			Read  z#
			Read Steps
			Read Pitch#
			Read yaw#
			Read roll
	
			wp.tWayPoint = NewWP(Path1, x,y,z, steps, pitch, yaw, roll)
			;create a visible cube for each way point
			temp = CreateCube(wp\entity)
			PositionEntity temp,0,0,0
			RotateEntity temp,0,0,0
			ScaleEntity temp,.1,.1,.1
			EntityColor temp,255,255,0
			
		Next	
		
		Path3.tPath = NewPath()		;create Path3 from a diferent set of points
		temp = CreateCube(Path3\CurrPos)
		ScaleEntity temp,.3,.3,.3
		EntityColor temp,0,0,255
		
		For nPoint = 1 To maxwaypoints
			Read  x# 
			Read  y# 
			Read  z# 
			Read Pasos
			Read Pitch#
			Read yaw#
			Read roll
	
			wp.tWayPoint = NewWP(Path3, x,y,z, pasos, pitch, yaw, roll)
			
			;create a visible cube for each way point
			temp = CreateCube(wp\entity)
			PositionEntity temp,0,0,0
			RotateEntity temp,0,0,0
			ScaleEntity temp,.1,.1,.1
			EntityColor temp,0, 255,255
	
			
		Next
	
	Restore WayPointDefinition	;create 2 aditional path from the same set of points
	
		Path2.tPath = NewPath()
		
		For nPoint = 1 To maxwaypoints
			Read  x# 
			Read  y# 
			Read  z# 
			Read Pasos
			Read Pitch#
			Read yaw#
			Read roll
	
			wp.tWayPoint = NewWP(Path2, x,y,z, pasos, pitch, yaw, roll)
			
		Next	
	
		Path4.tPath = NewPath()
		temp = CreateCube(Path4\CurrPos)
		ScaleEntity temp,.3,.3,.3
		EntityColor temp,0,255,0
		
		For nPoint = 1 To maxwaypoints
			Read  x# 
			Read  y# 
			Read  z# 
			Read Pasos
			Read Pitch#
			Read yaw#
			Read roll
	
			wp.tWayPoint = NewWP(Path4, x,y,z, pasos, pitch, yaw, roll)
			
		Next
		
	;at this moment, we have 4 paths, Path1 and Path2 are clones of each other and se same for Path3 and Path4
	
	
	;Initialize each path
	InitPaths()	;after initialization all paths are on The first waypoint and in the step 0
	
	
	Piv1 = PathCurrPos(Path1)	;fist position of Path1
	Piv2 = PathNextPos(Path2)	;second position of Path2
	PointEntity Piv1,Piv2,EntityRoll(piv1)	;point the Voyager to the next step in the path
	
	ShowPaths = True	;controls the plot of the interpolated steps
	
	
		
	
	Repeat
	
		If KeyDown(203) Then TurnEntity pivotCam, 0,+.5,0
		If KeyDown(205) Then TurnEntity pivotCam, 0,-.5,0
		If KeyDown(30)	Then PathNextPos(Path4)	;only stepped if A key is held down
		
		If KeyHit(57) Then
			ShowPaths = Not(ShowPaths)
		End If
	
		SuperCam(pivotCam,Viajero,.025,8,3,0,0) 

		RenderWorld
		
		If ShowPaths Then
			PlotPath(Camera,Path1)
			PlotPath(Camera,Path3)
		End If
		
		;the path system not only interpolate position from one way point to the next via a bezier curve
		;plus, it interpolate pitch, yaw and roll from WayPoint form the next lineary.
		
		;in this demo, i use the Roll interpolated by the system but i prefere to do the voyager to point to the next waypoint.
		Piv1 = PathNextPos(Path1)	;both path are equal but, Path2 is one step ahead of Path1
		Piv2 = PathNextPos(Path2)	;used to force the entity to Point to the next step
		R# = EntityRoll(Piv1)		;the calculated roll is saved and used to stablish the en roll of the object
		PointEntity Piv1,Piv2
		RotateEntity Piv1, EntityPitch(Piv1), EntityYaw(Piv1), R
	
		;for the boxes, the system use the pitch, yaw and roll interpolated by the system		
		temp = PathNextPos(Path3,True)
		If temp=-1 Then
			DebugLog "end of Path3"
		End If
			
		Text 10,10,"Space to show/hide paths"
		Text 10,30,"&lt;- -&gt; tu turn camera"
		Text 10,50,"A to step Path4"
		Text 10,70,TrisRendered()
		
		Flip
	
	Until KeyHit(1)
	

End Function

Example()

End 

;x#, y#, z#, Steps, Pitch#, yaw#, roll#

;Step = distance between 2 dots / points
;Smaller step is, farther dots between each other are

.WayPointDefinition
;Path 1

Data -120, 10, 10, 100, 0, 0, 0
Data -124, 20, 20, 100, 0, 0, 0
Data -136, 30, 30, 100, 0, 0, 0
Data -148, 40, 40, 100, 0, 0, 0
Data -160, 50, 50, 100, 0, 0, 0

Data -160, 50, 50, 150, 0, 0, 0
Data -172, 60, 60, 150, 0, 0, 0
Data -181, 70, 70, 150, 0, 0, 0
Data -193, 80, 80, 150, 0, 0, 0
Data -205, 90, 90, 150, 0, 0, 0

;Path 1
;	Data -3, 0, 0, 100, 0, 0,  0 ;wp 1
;	Data -1, 2,-5, 150, 0, 0, 50 ;wp 2
;	Data  1, 1, 0, 100, 0, 0,  0 ;wp 3
;	Data  3,-2,+4, 100, 0, 0,-30 ;wp 4
;Path 2
;	Data -3, 0, 4,  50,  60,   0,   0
;	Data -1, 3, 5, 100,   0,   0,  60
;	Data  1, 1, 6,  50,   0, -50,   0
;	Data  3,-3, 2, 100, -20,   0, -60
	
	Function SuperCam(cam,ent,cspeed#,dist#,hite#,xrot#,tilt#) 
 
TFormPoint 0,hite#,-dist#,ent,0 
 
cx#=(TFormedX()-EntityX(cam))*cspeed# 
cy#=(TFormedY()-EntityY(cam))*cspeed# 
cz#=(TFormedZ()-EntityZ(cam))*cspeed# 
 
TranslateEntity cam,cx,cy,cz 
PointEntity cam,ent 
RotateEntity cam,xrot#,EntityYaw(cam),tilt# 
 
End Function
</textarea><br><br>Thanks! :) <br><br></td></tr></table><br>
<a name="1092988"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Charrua</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> hi<br><br>the example of the Path Following System i've posted does exactly this.<br>As i see, the only modification you did in the example is to incorporate a camera that follows the "Viajero" and changed the x,y,z values for the waypoints. <br><br>So, if the example READS all the coordinates from DATA statments already, what are you asking for?<br><br>btw it should be good that you mention the autor of the code you use/post, it's ok, is from the Code Archives and is Public Domain, but i think that isn't much to ask. I'm pleased if that code fullfill your needs.<br><br>regards<br><br>Juan <br><br></td></tr></table><br>
<a name="1093028"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> What im asking for is a waypoint system i can press a button, it creates a waypoint, and adds an interpolation bezier line to 2 connected waypoints. which then allows for you to save or load them, and the player follows them. Kinda like a drag and drop system :) <br><br></td></tr></table><br>
<a name="1093044"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Charrua</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> building your drag'n drop system isn't imposible but i gess that it will be better, at first, for you to use some World Editor/Creator to place objects that you will then use as WayPoints. If you want, download AWC (follow my signature) and see the "awc demo4.bb" wich loads "awc path demo.awc", using the editor and pressing F7 you can see the WayPoints linked with lines as a helper function, then you can use the Path Following System to interpolate between them (see the demo)<br><br>regards<br><br>Juan <br><br></td></tr></table><br>
<a name="1093053"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> how do i create waypoints with this and save them to a file? <br><br></td></tr></table><br>
<a name="1093069"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nvm, thanks Charrua. This editor works like a beauty! =D <br><br></td></tr></table><br>
<a name="1093076"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Charrua</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> i'been out for a while, thank's<br><br>simply without any selected objects, RightClick and Select Path<br>Choose a name for it, and place the first WayPoint, then move the mouse pointer and Ctrl+C to place as many WP as needed, Press F7 if you want to see Red Lines between the WayPoints.<br><br>Read Page 6 of the companion document, but i think i'm late.<br><br>Juan <br><br></td></tr></table><br>
<a name="1093077"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> thanks ALOT, Charrua. Ur a lifesaver! =D <br><br></td></tr></table><br>
<a name="1093110"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> I get a memory access violation when I try to paste a waypoint using Control-V :( <br><br></td></tr></table><br>
<a name="1093118"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Charrua</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> me to!<br>a bug, sorry<br><br>try this:<br><a href="https://sites.google.com/site/odriozolajuanignacio/awcexe.zip?attredirects=0&amp;d=1" target="_blank">https://sites.google.com/site/odriozolajuanignacio/awcexe.zip?attredirects=0&amp;d=1</a><br><br>i updated only the exe<br><br>Juan <br><br></td></tr></table><br>
<a name="1093119"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> YAY! It works! =D<br><br>Is there a way I can move a waypoint with the keys rather than the mouse for more precise positioning? <br><br></td></tr></table><br>
<a name="1093125"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Charrua</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> no, but i think you have "Snap to grid" on, deactivate it (press space) or use the text input on the properties window to manually set the position.<br><br>Juan <br><br></td></tr></table><br>
<a name="1093126"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> thanks, and just a tip, i think it would make it MUCH easier on most ppl if you could A) make it so that keys can move waypoints and objects (arrow keys), and B) NOT resetting the snap to grid function every time you make a new object by copying an old object and pasting it (i noticed it went from Freely back to Snap to grid), other than that, its WONDERFUL! =D <br><br></td></tr></table><br>
<a name="1093131"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Charrua, ALSO, WITH the above, how would I detect which waypoint my player is at? So I can use a command to make him stop for a minute, think, then walk to the next waypoint? <br><br></td></tr></table><br>
<a name="1093136"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Also, there's a Memory Access Violation when I hold Control and Press A (Select All) :( <br><br></td></tr></table><br>
<a name="1093143"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Charrua</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> updated "awc exe.zip", same link of post #10<br><br>the function PathNextPos() is the one that recalculates the new position in the path, you can evaluate if it's necesari to call or not this function.<br>Each tWayPoint object may include some extra fields to meet your needs, you can add an wpNumber or Name (that matches with te one you give inside the editor)<br>then in the main loop you can call PathCurrWP to get the Current tWayPoint object and retrieve from it the Number, Name or whatever you added.<br>Conditionally you may sept the path or not...<br><br>Juan <br><br></td></tr></table><br>
<a name="1093144"></a>

<a name="1093145"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> awesome! Works like a charm! Thanks =D<br><br>Also, you wouldnt happen to know the formula to position an object away from a certain object using random math to randomize the X &amp; Z value of the objects around the object?<br><br>Example: <br><br>I don't want trees randomly generating in my castle, what can I do so this won't happen?<br><br>tx# = rnd(-1700, 1700)<br>tz# = rnd(-1700, 1700)<br>ty# = terrainy(blah)<br><br>if tx# &gt; -1700-meshwidth(castle)? then tx# = -1700-meshwidth(castle)?<br><br>if tz# &gt; -1700-meshdepth(castle)? then tz# = -1700-meshwidth(castle)?<br><br>if tx# &lt; 1700+meshwidth(castle)? then tx# = 1700+meshwidth(castle)?<br><br>if tz# &lt; 1700+meshdepth(castle)? then tz# = 1700+meshdepth(castle)?<br><br>Something like the above. I've tried the above already, but it's still creating trees sometimes INSIDE my castle. i want it to create trees AROUND the castle :)<br><br>Thanks, Charrua! =D<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1093146"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Charrua</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> MeshWidht, Height and Depth reports correct values if you scale the Castle with ScaleMesh, if not any comparation you made with them may result in incorrect or unespected results. <br><br><br><br>Juan <br><br></td></tr></table><br>
<a name="1093154"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Charrua, how do I load a heightmap into the editor WITHOUT it being flat? <br><br></td></tr></table><br>
<a name="1093157"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Charrua</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> normally you have to scale the heightmap in the Y axis: ScaleY <br><br></td></tr></table><br>
<a name="1093177"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LineOf7s</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yep, Rez is back alright.  *sigh*<br><br><br>Thanks Charrua for your generosity.  I plan on trying to use your code to generate splined attack paths for a shoot-em-up.  Given I've never done anything like that before I look forward to trying to work it out.   A bit of brain-burning for me to look forward to, I think.  :) <br><br></td></tr></table><br>
<a name="1093178"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yup, I'z back :P<br><br>And yes, thanks Charrua! :) <br><br></td></tr></table><br>
<a name="1093182"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Charrua</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#23">[#23]</a></td></tr></table></td></tr><tr ><td class="posttext"> @LineOf7s <br>one thing you should may consider, if interpolation wastes to much time, is use the system to precalculate interpolated points and store them in Arrays or typed variables and then use the precalculated values instead of calling the system to do so.<br><br>juan <br><br></td></tr></table><br>
<a name="1093319"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LineOf7s</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#24">[#24]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for the tip, Charrua, that sounds like a totally sensible thing to do if the paths never change (like the sort of paths enemies might take in a shoot-em-up). <br><br></td></tr></table><br>
<a name="1142254"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Blitzplotter</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#25">[#25]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br><br>updated "awc exe.zip", same link of post #10<br><br>the function PathNextPos() is the one that recalculates the new position in the path, you can evaluate if it's necesari to call or not this function.<br>Each tWayPoint object may include some extra fields to meet your needs, you can add an wpNumber or Name (that matches with te one you give inside the editor)<br>then in the main loop you can call PathCurrWP to get the Current tWayPoint object and retrieve from it the Number, Name or whatever you added.<br>Conditionally you may sept the path or not...<br><br> <br></div><br><br>Oh yep, truly awesome! Going to have a play today ;) <br><br></td></tr></table><br>
<a name="1153319"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#26">[#26]</a></td></tr></table></td></tr><tr ><td class="posttext"> Can someone make it so you can drag/drop waypoint, drag/drop the bezier line, add new waypoints, connect 2 or more waypoints together?<br><br>Thank You! :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
