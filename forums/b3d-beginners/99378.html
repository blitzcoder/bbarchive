<!DOCTYPE html><html lang="en" ><head ><title >Snake movement, Body &amp; tail follow head.</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Snake movement, Body &amp; tail follow head.</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Snake movement, Body &amp; tail follow head.</a><br><br>
<a name="1166260"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Aussie</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey there everyone, <br>I decided to have a go at making a simple 3d snake style game &amp; have run into trouble with the body &amp; tail part of the snake following the head.<br>The way I was thinking of doing it was to use types to store the info of the position &amp; rotation of the head when a key is pressed so when the body &amp; tail = the position of the marker then the body &amp; tail rotate to match the rotation of the head at that position, then delete the marker after the body parts &amp; the tail have passed the position.<br>What’s happening is both the body &amp; tail are rotating as soon as the key is pressed. I’m not 100% sure why but I think it is to do with the body &amp; tail reading the rotation of the head instead of reading it from the marker.<br>Could some one take a look &amp; see what’s going on.<br>Also would this be the right way about doing this?<br>Cheers.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800,600,0,2

SetBuffer BackBuffer()

Const head = 1
Const body = 2
Const tail = 3

Global Snake.Snake
Global Mark.Marker
Global Turnhead# = 0
Global Segments% = 2

Dim Sbody(3)






Light= CreateLight()
	RotateEntity light,90,0,0
camera = CreateCamera()
	PositionEntity camera,0,50,0
	RotateEntity camera,90,0,0
plane = CreatePlane()
	EntityColor plane,0,0,255
	

Create_player()
	
While Not KeyDown (1)


	Update_player()

	
UpdateWorld
RenderWorld

Text 10,12,"Head rotation" + Turnhead

Flip

Wend

End


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Type Snake 
	Field speed# 	
	Field xpos# 
	Field zpos# 
	Field yrot#
	Field Headrot#
	Field snaketype
End Type

Function Create_player()
	
	Snake.Snake = New Snake
		Snake\speed = .05
		Snake\xpos = 0
		Snake\zpos = 0
		Snake\Headrot = 0
		Snake\snaketype = head
		
		Snake.Snake = New Snake
		Snake\speed = .05
		Snake\xpos = 0
		Snake\zpos = -2
		Snake\yrot = 0
		Snake\snaketype = body
		
		Snake.Snake = New Snake
		Snake\speed = .05
		Snake\xpos = 0
		Snake\zpos = -4
		Snake\yrot = 0
		Snake\snaketype = tail
		
		Dim Sbody(3)
		Sbody(1) = CreateCube()
					EntityColor Sbody(1),255,0,0
		Sbody(2) = CreateCube()
					EntityColor Sbody(2),0,255,0
		Sbody(3) = CreateCone()
					RotateMesh Sbody(3),-90,0,0
					ScaleEntity Sbody(3),.5,.5,1
					EntityColor Sbody(3),0,255,0
					
		
		For Snake.Snake = Each Snake
		    PositionEntity Sbody(Snake\snaketype),0,1,Snake\zpos
		Next
		
End Function



Function Update_player()

	For Snake.Snake = Each Snake
		
			
		MoveEntity Sbody(Snake\snaketype),0,0,Snake\speed
		
		Snake\xpos = EntityX(Sbody(Snake\snaketype))
		Snake\zpos = EntityZ(Sbody(Snake\snaketype))
				
		If KeyDown(203) Then
			Turnhead = Turnhead + 1
			Create_Marker()
		End If
			
		If KeyDown(205) Then
			Turnhead = Turnhead - 1
			Create_Marker()
		EndIf
				
		Snake\Headrot = Turnhead
		RotateEntity Sbody(1),0,Snake\Headrot#,0
		RotateEntity Sbody(1),0,Snake\Headrot#,0
			
		
		For Mark.Marker = Each Marker

			If Mark\Mx = Snake\xpos And Mark\Mz = Snake\zpos Then
				RotateEntity Sbody(2),0,Mark\Mrot,0
				RotateEntity Sbody(3),0,Mark\Mrot,0
				Mark\Count = Mark\Count + 1
			End If
					
			If Mark\Count = Segments Then
				FreeEntity Mark\temp
				Delete Mark
			End If
		
		Next
	Next
	
End Function

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Type marker
	Field Mx#
	Field My#
	Field Mz#
	Field Mrot#
	Field Count%
	Field Temp
End Type

Function Create_Marker()

	Mark.Marker = New Marker
		Mark\Mx = EntityX(Sbody(1))
		Mark\My = 1
		Mark\Mz = EntityZ(Sbody(1))
		Mark\Mrot = Turnhead
		Mark\Count = 0
		
		; used for visual representation
		Mark\temp = CreateCube()
		PositionEntity Mark\temp,Mark\Mx,Mark\My,Mark\Mz
		RotateEntity Mark\temp,0,Mark\Mrot,0
		ScaleEntity Mark\temp,.25,.25,.25
		
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1166293"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mikorians</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Weak suggestion...  You might try setting the body parts as children of their respective parent entities, thus their motion would be relative to the position of the root entity. <br><br></td></tr></table><br>
<a name="1166323"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Aussie</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for the suggestion Mikorians but later on i want to be able to add more body segments to the snake to make it longer, trying to have a go at getting a 3d snake game up &amp; going. <br>Once i can get it up &amp; running i want to try &amp; implement it into a movement around a sphere i was playing around with a while ago. <br><br></td></tr></table><br>
<a name="1166330"></a>

<a name="1166331"></a>

<a name="1166334"></a>

<a name="1166335"></a>

<a name="1166339"></a>

<a name="1166347"></a>

<a name="1166348"></a>

<a name="1166351"></a>

<a name="1166352"></a>

<a name="1166353"></a>

<a name="1166354"></a>

<a name="1166355"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I know the problem from our train project. One segment should follow the segment ahead.<br><br>The solution is not as simple... You have to think about "onthe fly railway creation". <br><br>Each segment has a head point and a tail point. Only the head point of the first one can be manipulated by the player. The following segments always keep the connection to the tail point of the segment ahead.<br><br>So you have to calculate the tail point after a movement and then change the head point of its follower to this. Now you have to calculate his tail point and so on....<br><br>As I see, you already have a snake segment type TYPE SNAKE. The segments have to be part of the type. What about adding a field ENTITY as a part of the typ for the entities, instead of using Sbody(1..3)?<br><br>And your idea of using waypoints is excellent. But there for you have to set a new Waypoint Array as a field into your type. The idea: The first segment sets a new marker which becomes part of its type. It collect some waypoint and sends the oldest values to the following segment. So each segment inherits its position and direction from the waypoint list of the segment ahead each update step.<br><br><pre class=code>Graphics3D 800,600,0,2

SetBuffer BackBuffer()


Global H_Snake.Snake


Light= CreateLight()
	RotateEntity light,90,0,0
camera = CreateCamera()
	PositionEntity camera,0,50,0
	RotateEntity camera,90,0,0
plane = CreatePlane()
	EntityColor plane,0,0,255
	

Create_player()
	
While Not KeyDown (1)


	Update_player()
	UpdateWorld
	RenderWorld

	Flip
Wend

End


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Type Snake 
	Field xpos# 
	Field zpos# 

;some more Fields:	
	Field Entity
	Field WayX#[11],WayZ#[11],WayR#[11]
End Type

Function Create_player()
	
	;the first entity is GLOBAL, because we will need it again individual:
	H_Snake.Snake = New Snake
		H_Snake\Entity = CreateCube()
		EntityColor H_Snake\Entity,0,255,0
		

	; the others have only a few proerties fixed now:
		For i%=0 To 10
			Snake.Snake = New Snake
			Snake\Entity = CreateCube()
		Next

End Function


Function Update_player()
	Local locX#,locZ#, locRot#

		;speed of the snake 0.25
		MoveEntity H_Snake\Entity, 0 , 0 , 0.25
		If KeyDown(203) Then
			TurnEntity H_Snake\Entity,0,2,0
	
		ElseIf KeyDown(205) Then
			TurnEntity H_Snake\Entity,0,-2,0
		EndIf
	
	For Snake.Snake = Each Snake
		;this is important: A FILO-stack 
		;new waypoint will "sink down" during 10 Updates
		For i%=10 To 1 Step -1
			Snake\WayZ[i]=Snake\WayZ[i-1]
			Snake\WayX[i]=Snake\WayX[i-1]
			Snake\WayR[i]=Snake\WayR[i-1]
		Next

		; following segments will get there values from the segment before:
		If Snake&lt;&gt;H_Snake	
			PositionEntity Snake\Entity,locX,0,locZ
			RotateEntity Snake\Entity,0,locRot,0
		EndIf

		;new position will become new waypoint on top:
		Snake\WayZ[0]=EntityZ(Snake\Entity,True)
		Snake\WayX[0]=EntityX(Snake\Entity,True)
		Snake\WayR[0]=EntityYaw(Snake\Entity,True)

		; keep oldest waypoint of this segment for the next segments
		locZ=Snake\WayZ[10]
		locX=Snake\WayX[10]
		locRot=Snake\WayR[10]
					
	Next
	
End Function

</pre><br><br>The system is that every segment has a stack with 10 waypoints. The newest is on WayX[0], the oldest on WayX[10]. Alle waypoints are "below" the segment, only the oldest waypoint is even "behind" it. So it is ideal for to set position of the next segment.<br><br>In this sample the speed of the snake is 0.25 and the size of a segment is 2. So after 10 waypoints the oldest is 10x0.25 = 2.5 behind the current position.<br><br>If you need a slower speed (f.e.0.1) you have to store more waypoints to reach the tail of your segment with the last one:<br><br><pre class=code>
Type Snake 
...
Field WayX[21]
....
Function Update_player()

	Local locX#,locZ#, locRot#

		MoveEntity H_Snake\Entity,0,0,0.1

....
	For Snake.Snake = Each Snake
		For i%=20 To 1 Step -1
			Snake\WayZ[i]=Snake\WayZ[i-1]
....
		locZ=Snake\WayZ[20]
		locX=Snake\WayX[20]
		locRot=Snake\WayR[20]
					
	Next
....
</pre><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1166375"></a>

<a name="1166376"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mikorians</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Half awake...  Groan...  Ah.  I loved snakebyte.<br>We used to use an array of previous positions organized thus.<br>oox=ox:ooy=oy:ox=x:oy=y:x=x+dx:y=y+dy<br>or<br>for t=0 to 4:x(t)=x(t+1):y(t)=y(t+1):next<br>I guess in the case of the train you'd need dummy points, so increase the array accordingly...?  I know there's more to it...  Listen to midimaster...<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1166443"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Aussie</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks heaps Midimaster.<br>Just a couple of questions. <br>What is a FILO stack?<br>What is the difference between Snake\WayZ[0] &amp; Snake\WayZ(0) the [] () part?<br><br>Going to have a play at adding more parts now. :)<br><br>I have added a tail part.<br><br><pre class=code>
Function Create_player()
	
	;the first entity is GLOBAL, because we will need it again individual:
	H_Snake.Snake = New Snake
		H_Snake\Entity = CreateCube()
		EntityColor H_Snake\Entity,0,255,0
		

	; the others have only a few proerties fixed now:
		For i%=0 To 7
			Snake.Snake = New Snake
			Snake\Entity = CreateCube()
		Next
		For i%=8 To 8
			Snake.Snake = New Snake
			Snake\Entity = CreateCube()
			EntityColor Snake\Entity,255,0,0
		Next

End Function
</pre><br><br>Next I want to add more body parts.<br>The way I am thinking about this is to create a variable for the segments. Something like<br><pre class=code>
Segments = ??
Tail = ??
                For i%=0 To Segments
			Snake.Snake = New Snake
			Snake\Entity = CreateCube()
		Next
		For i%= Tail To Tail
			Snake.Snake = New Snake
			Snake\Entity = CreateCube()
			EntityColor Snake\Entity,255,0,0
		Next
</pre><br>&amp; then have to do something similar for the waypoints. <br><br></td></tr></table><br>
<a name="1166499"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Aussie</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok I have added more body parts &amp; am now a bit stuck in regards how to go about replacing the last part of the snake (the tail) &amp; changing it to a body part when a new tail part is created<br>Here is what I have.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800,600,0,2

SetBuffer BackBuffer()


Global H_Snake.Snake
Global X% = 21
Global seg% = 7

Light= CreateLight()
	RotateEntity light,90,0,0
camera = CreateCamera()
	PositionEntity camera,0,50,0
	RotateEntity camera,90,0,0
plane = CreatePlane()
	EntityColor plane,0,0,255
	

Create_player()
	
While Not KeyDown (1)


	Update_player()
	UpdateWorld
	RenderWorld
Text 200,100,"Press space bar to add another segment to the body"
	Flip
Wend

End


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Type Snake 
	Field xpos# 
	Field zpos#
	Field Speed#
	Field Turnspeed# 

;some more Fields:
		
	Field Entity
	Field Waycount%
	Field WayX#[21],WayZ#[21],WayR#[21]
End Type

Function Create_player()
	
	;the first entity is GLOBAL, because we will need it again individual:
	H_Snake.Snake = New Snake
		H_Snake\Entity = CreateCube()
		EntityColor H_Snake\Entity,0,255,0
		

	; the others have only a few proerties fixed now:
		For i%=0 To seg% 
			Snake.Snake = New Snake
			Snake\Entity = CreateSphere()
		Next
		For i%=8 To 8
			Snake.Snake = New Snake
			Snake\Entity = CreateCone()
			RotateMesh Snake\Entity,-90,0,0
			EntityColor Snake\Entity,255,0,0
		Next
		H_Snake\Speed = .1
		H_Snake\Turnspeed = 3
		Snake\Waycount% = 20

End Function


Function Update_player()
	Local locX#,locZ#, locRot#

		;speed of the snake 0.25
		MoveEntity H_Snake\Entity,0,0,H_Snake\Speed
		If KeyDown(203) Then
			TurnEntity H_Snake\Entity,0,H_Snake\Turnspeed,0
	
		ElseIf KeyDown(205) Then
			TurnEntity H_Snake\Entity,0,-H_Snake\Turnspeed,0
		EndIf
		
		
		
		
		;add a new segment to the snake body if space bar is hit
		If KeyHit(57)
			;For i%=seg To seg
			;	FreeEntity Snake\Entity
			;	Snake\Entity = CreateSphere()
			;Next
			
				seg = seg + 1
				
			For i%=seg To seg 
				Snake.Snake = New Snake
				Snake\Entity = CreateCone()
					RotateMesh Snake\Entity,-90,0,0
				EntityColor Snake\Entity,255,0,0

			Next
		EndIf
	
	For Snake.Snake = Each Snake
		;this is important: A FILO-stack 
		;new waypoint will "sink down" during 10 Updates
		;If KeyHit(57)
		;	x% = x% + 2
		;EndIf
		
		Snake\Waycount% = x%
		For i% = Snake\Waycount% To 1 Step -1
			Snake\WayZ[i]=Snake\WayZ[i-1]
			Snake\WayX[i]=Snake\WayX[i-1]
			Snake\WayR[i]=Snake\WayR[i-1]
		Next

		; following segments will get there values from the segment before:
		If Snake&lt;&gt;H_Snake	
			PositionEntity Snake\Entity,locX,0,locZ
			RotateEntity Snake\Entity,0,locRot,0
		EndIf

		;new position will become new waypoint on top:
		Snake\WayZ[0]=EntityZ(Snake\Entity,True)
		Snake\WayX[0]=EntityX(Snake\Entity,True)
		Snake\WayR[0]=EntityYaw(Snake\Entity,True)

		; keep oldest waypoint of this segment for the next segments
		locZ=Snake\WayZ[Snake\Waycount%]
		locX=Snake\WayX[Snake\Waycount%]
		locRot=Snake\WayR[Snake\Waycount%]
					
	Next
	
	
End Function
</textarea><br><br>Going to add pickups &amp; collisions now. :) <br><br></td></tr></table><br>
<a name="1166509"></a>

<a name="1166510"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >gradualcheetah</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> <b>here you go.</b><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800,600,0,2

SetBuffer BackBuffer()


Global H_Snake.Snake
Global X% = 21
Global seg% = 7

Light= CreateLight()
	RotateEntity light,90,0,0
camera = CreateCamera()
	PositionEntity camera,0,50,0
	RotateEntity camera,90,0,0
plane = CreatePlane()
	EntityColor plane,0,0,255
	

Create_player()
	
While Not KeyDown (1)


	Update_player()
	UpdateWorld
	RenderWorld
Text 200,100,"Press space bar to add another segment to the body"
	Flip
Wend

End


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Type Snake 
	Field xpos# 
	Field zpos#
	Field Speed#
	Field Turnspeed# 

;some more Fields:
		
	Field Entity
	Field Bodytype
	Field posx#,posy#,posz#
	Field Waycount%
	Field WayX#[21],WayZ#[21],WayR#[21]
End Type

Function Create_player()
	
	;the first entity is GLOBAL, because we will need it again individual:
	H_Snake.Snake = New Snake
		H_Snake\Entity = CreateCube()
		EntityColor H_Snake\Entity,0,255,0
		

	; the others have only a few proerties fixed now:
		For i%=0 To seg% 
			Snake.Snake = New Snake
			Snake\Bodytype = 2
			Snake\Entity = CreateSphere()
		Next
		For i%=8 To 8
			Snake.Snake = New Snake
			Snake\Bodytype = 3
			Snake\Entity = CreateCone()
			RotateMesh Snake\Entity,-90,0,0
			EntityColor Snake\Entity,255,0,0
		Next
		H_Snake\Speed = .1
		H_Snake\Turnspeed = 3
		Snake\Waycount% = 20

End Function


Function Update_player()
	Local locX#,locZ#, locRot#

		;speed of the snake 0.25
		MoveEntity H_Snake\Entity,0,0,H_Snake\Speed
		If KeyDown(203) Then
			TurnEntity H_Snake\Entity,0,H_Snake\Turnspeed,0
	
		ElseIf KeyDown(205) Then
			TurnEntity H_Snake\Entity,0,-H_Snake\Turnspeed,0
		EndIf
		
		
		
		
		
		;add a new segment to the snake body if space bar is hit
		If KeyHit(57)
			;For i%=seg To seg
			;	FreeEntity Snake\Entity
			;	Snake\Entity = CreateSphere()
			;Next
			
				seg = seg + 1
			;this deletes the old tail and replaces it then moves the new body part to the old tails location 	
				For Snake.Snake = Each Snake
			Local nx#,ny#,nz#	
				    If Snake\Bodytype = 3
	
				 		FreeEntity Snake\Entity
				        Snake\Entity = CreateSphere()
				PositionEntity Snake\Entity ,posx#,posy#,posz#
				 Snake\Bodytype = 2
				    EndIf
				
				Next
				
			For i%=seg To seg 
				Snake.Snake = New Snake
				Snake\Entity = CreateCone()
				Snake\Bodytype = 3
					RotateMesh Snake\Entity,-90,0,0
				EntityColor Snake\Entity,255,0,0

			Next
		EndIf
	
	For Snake.Snake = Each Snake
	
	    ;this saves the position of each segement 
	    Snake\posx# = EntityX(Snake\Entity)
	    Snake\posy# = EntityY(Snake\Entity)
	    Snake\posz# = EntityZ(Snake\Entity)
	
	
		;this is important: A FILO-stack 
		;new waypoint will "sink down" during 10 Updates
		;If KeyHit(57)
		;	x% = x% + 2
		;EndIf
		
		Snake\Waycount% = x%
		For i% = Snake\Waycount% To 1 Step -1
			Snake\WayZ[i]=Snake\WayZ[i-1]
			Snake\WayX[i]=Snake\WayX[i-1]
			Snake\WayR[i]=Snake\WayR[i-1]
		Next

		; following segments will get there values from the segment before:
		If Snake&lt;&gt;H_Snake	
			PositionEntity Snake\Entity,locX,0,locZ
			RotateEntity Snake\Entity,0,locRot,0
		EndIf

		;new position will become new waypoint on top:
		Snake\WayZ[0]=EntityZ(Snake\Entity,True)
		Snake\WayX[0]=EntityX(Snake\Entity,True)
		Snake\WayR[0]=EntityYaw(Snake\Entity,True)

		; keep oldest waypoint of this segment for the next segments
		locZ=Snake\WayZ[Snake\Waycount%]
		locX=Snake\WayX[Snake\Waycount%]
		locRot=Snake\WayR[Snake\Waycount%]
					
	Next
	
	
End Function
 </textarea><br><br>i tried to kinda comment what i did in there. basically all i did was add the field "bodytype" to the type Snake. when you hit spacebar it checks for the bodytype "3" (2=body,3=tail) and once it finds "3"(the tail) it deletes it, makes it into a bodytype of 2 and makes it a circle. then i just went where you made the new tail on space hit and made it so that when you add a tail the tail mesh gets the bodytype of 3.<br><br>hope i was of at least some help!<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1166517"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sorry I did a typo... It has to be "FIFO"<br><br><br>FIFO<br><br>a FIFO ("First In First Out") stack is a stack where things, that came in new, get served first like in a queue. <br><br>The values enter the array at position 0 and then "sink" down to position 10. This is the moment, when they get out.<br><br><br><br>ARRAY () VERSUS []<br><br>The "normal" Array...<br> <pre class=code>Dim a%(9)
Print a(3)</pre><br>...does not work inside types. You have to use a special "BlitzArray" with different brackets:<br><br><pre class=code>Typ TPlayer
     Field a%[9]
End Type
P.TPlayer =New TPlayer
Print P\a[3]</pre><br><br><br><br><br><br><br>CRITICS ON YOUR CODE<br><br>At first your code works well, so there is no need to change what I now write. But some considerations:<br><br>You added some new fields:<br><pre class=code>Type Snake 
	Field xpos# 
	Field zpos#
	Field Speed#
	Field Turnspeed# 
</pre><br>This implements, that each segment has the chance to have a individual speed and turnspeed. But in reality only the whole snake has one common speed. So the parameter Speed# and TurnSpeed# should be GLOBAL:<br><br><pre class=code>
Global Speed#, TurnSpeed#
Type Snake 
	Field xpos# 
	Field zpos#
</pre><br><br>Or do you plan to have more than one snake on the screen? <br><br></td></tr></table><br>
<a name="1166540"></a>

<a name="1166541"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Aussie</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks heaps guys, got it all working nicely for now.<br><br>Thanks Midimaster for the info &amp; there will only be the one snake.<br><br>Just got a question to do with the FIFO array &amp; a bug to sort out.<br><br>I have set the a waycount at 20<br><br><pre class=code>
x%=20
…..
…..
…..
For Snake.Snake = Each Snake
	;this is important: A FIFO-stack 
	;new waypoint will "sink down" during 10 Updates
		
		
	Snake\Waycount% = x%
		For i% = Snake\Waycount% To 1 Step -1
			Snake\WayZ[i]=Snake\WayZ[i-1]
			Snake\WayX[i]=Snake\WayX[i-1]
			Snake\WayR[i]=Snake\WayR[i-1]
		Next

		; following segments will get there values from the segment before:
		If Snake&lt;&gt;H_Snake	
			PositionEntity Snake\Entity,locX,0,locZ
			RotateEntity Snake\Entity,0,locRot,0
		EndIf

		;new position will become new waypoint on top:
		Snake\WayZ[0]=EntityZ(Snake\Entity,True)
		Snake\WayX[0]=EntityX(Snake\Entity,True)
		Snake\WayR[0]=EntityYaw(Snake\Entity,True)

		; keep oldest waypoint of this segment for the next segments
		locZ=Snake\WayZ[Snake\Waycount%]
		locX=Snake\WayX[Snake\Waycount%]
		locRot=Snake\WayR[Snake\Waycount%]
					
	Next
	
 </pre><br><br>My question is, is this creating 20 waypoints behind the head of the snake so the body segments know the position &amp; rotation or is it creating 20 waypoints for each body segment of the snake? I am struggling to get my head around this part &amp; why if I have say, 60 body segments they keep following the head.<br><br>The bug I have is when I collect a pickup both a tail part &amp; body segment quickly flash up at 0,0,0 then appear in the body of the snake.<br><br>Going to keep playing around with it &amp; once I have it all working nicely I will start adding some bells &amp; whistles. :)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800,600,0,2

SetBuffer BackBuffer()


Global H_Snake.Snake
Global Snake.Snake
Global a.arena
Global Player_col = 1
Global Arena_col = 2
Global Body_col = 3
Global X% = 21
Global seg% = 1
Global pickup_avalable = 0
Global gamestate% = 0
Global speed# = .1
Global Turnspeed# = 3
Global Score% = 0




Collisions Player_col,Arena_col,2,1
Collisions Player_Col,Body_col,1,1




Light= CreateLight()
	RotateEntity light,90,0,0
camera = CreateCamera()
	PositionEntity camera,25,20,-25
	RotateEntity camera,45,45,0
plane = CreatePlane()
	EntityColor plane,0,0,255
	PositionEntity plane,0,-1,0
		



Create_player()
Create_arean()
	



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;main loop;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;	
While Not KeyDown (1)

If KeyHit(57) gamestate = 1

	Update_player()
	Update_pickup()
	
	UpdateWorld
	RenderWorld
	
	
Text 10,10,"Score: = " + Score%


	Flip
	
Wend

End


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Type Snake 
	Field xpos# 
	Field zpos#
	Field Entity
	Field Bodytype
	Field posx#,posy#,posz#
	Field Waycount%
	Field WayX#[21],WayZ#[21],WayR#[21]
End Type


Function Create_player()
	
	;the first entity is GLOBAL, because we will need it again individual:
	H_Snake.Snake = New Snake
		H_Snake\Entity = CreateCube()
		EntityType H_Snake\Entity,1
		EntityColor H_Snake\Entity,0,255,0
		

	; the others have only a few proerties fixed now:
		For i%=0 To seg% 
			Snake.Snake = New Snake
			Snake\Bodytype = 2
			Snake\Entity = CreateSphere()
			EntityRadius Snake\Entity,1
			EntityType Snake\Entity,3
		Next
		For i%=seg% + 1 To seg% + 1
			Snake.Snake = New Snake
			Snake\Bodytype = 3
			Snake\Entity = CreateCone()
			RotateMesh Snake\Entity,-90,0,0
			EntityColor Snake\Entity,255,0,0
			EntityRadius Snake\Entity,1
			EntityType Snake\Entity,3
		Next
		;Waycount% = 20

End Function


Function Update_player()
	Local locX#,locZ#, locRot#

		;speed of the snake 0.25
		MoveEntity H_Snake\Entity,0,0,Speed
		If KeyDown(203) Then
			TurnEntity H_Snake\Entity,0,Turnspeed,0
	
		ElseIf KeyDown(205) Then
			TurnEntity H_Snake\Entity,0,-Turnspeed,0
		EndIf
		
		If EntityCollided(H_Snake\Entity,Arena_col) Then
			End
		End If
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;                             
	
		;For snake.snake = Each snake		
		If EntityCollided(H_Snake\Entity,Body_col) Then
			End
		End If
		;Next
	
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;		
		
		
	For Snake.Snake = Each Snake
		;this is important: A FIFO-stack 
		;new waypoint will "sink down" during 10 Updates
		
		
		Snake\Waycount% = x%
		For i% = Snake\Waycount% To 1 Step -1
			Snake\WayZ[i]=Snake\WayZ[i-1]
			Snake\WayX[i]=Snake\WayX[i-1]
			Snake\WayR[i]=Snake\WayR[i-1]
		Next

		; following segments will get there values from the segment before:
		If Snake&lt;&gt;H_Snake	
			PositionEntity Snake\Entity,locX,0,locZ
			RotateEntity Snake\Entity,0,locRot,0
		EndIf

		;new position will become new waypoint on top:
		Snake\WayZ[0]=EntityZ(Snake\Entity,True)
		Snake\WayX[0]=EntityX(Snake\Entity,True)
		Snake\WayR[0]=EntityYaw(Snake\Entity,True)

		; keep oldest waypoint of this segment for the next segments
		locZ=Snake\WayZ[Snake\Waycount%]
		locX=Snake\WayX[Snake\Waycount%]
		locRot=Snake\WayR[Snake\Waycount%]
					
	Next
	
	
End Function



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
Type pickup
	Field P_mesh
	Field x#
	Field z#
	Field y#
End Type



;creates the pickup witin a playing area
	
Function Create_pickup()

	p.pickup = New pickup
	p\P_mesh = CreateSphere()
		EntityColor p\P_mesh,0,255,0
	p\x = Rnd(-22,22)
	p\z = Rnd(-22,22)
	p\y = 0
		
	PositionEntity p\P_mesh,p\x,p\y,p\z
	
	pickup_avalable = 1
	
End Function




;checks to see if a pickup has been colected &amp; adds a new segment to the snake body
		
Function Update_pickup()

	If pickup_avalable = 0 Then
	Create_pickup()
	End If
	For p.pickup = Each pickup
		If EntityDistance (p\P_mesh,H_Snake\Entity) &lt; 1.5 Then
		FreeEntity p\P_mesh
		Delete p
		pickup_avalable = 0
		
		Score = Score + 10
		seg = seg + 1
				
;this deletes the old tail and replaces it then moves the new body part to the old tails location 	
		For Snake.Snake = Each Snake
		
			Local nx#,ny#,nz#
				
			If Snake\Bodytype = 3
				FreeEntity Snake\Entity
				Snake\Entity = CreateSphere()
				PositionEntity Snake\Entity ,posx#,posy#,posz#
				Snake\Bodytype = 2
				EntityRadius Snake\Entity,1
				EntityType Snake\Entity,3
			EndIf
			
		Next
				
		For i%=seg To seg 
			Snake.Snake = New Snake
			Snake\Entity = CreateCone()
			Snake\Bodytype = 3
			RotateMesh Snake\Entity,-90,0,0
			EntityColor Snake\Entity,255,0,0
			EntityRadius Snake\Entity,1
			EntityType Snake\Entity,3

			Next
		End If
	Next
	
End Function
	




;;;;;;;;;;;;;;;;;;;;;;;;	
Type arena
	Field mesh
End Type

; creating the play area
Function Create_arean()
	
	a.arena = New arena
		a\mesh = CreateCube()
		ScaleEntity a\mesh,25,5,1
		EntityColor a\mesh,255,0,0
		PositionEntity a\mesh,0,0,25
		EntityType a\mesh,2
		
	a.arena = New arena
		a\mesh = CreateCube()
		ScaleEntity a\mesh,25,5,1
		EntityColor a\mesh,255,0,0
		PositionEntity a\mesh,0,0,-25
		EntityType a\mesh,2
		
	a.arena = New arena
		a\mesh = CreateCube()
		ScaleEntity a\mesh,1,5,26
		EntityColor a\mesh,255,0,0
		PositionEntity a\mesh,25,0,0
		EntityType a\mesh,2
		
	a.arena = New arena
		a\mesh = CreateCube()
		ScaleEntity a\mesh,1,5,26
		EntityColor a\mesh,255,0,0
		PositionEntity a\mesh,-25,0,0
		EntityType a\mesh,2
		
End Function	

</textarea><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1166543"></a>

<a name="1166544"></a>

<a name="1166545"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> experiment 1:<br>use 10 or 30 waypoints instead of 20 and you will see how they are working :-)<br><br>Each segment has a list of 20 waypoints and the last is given to the following segment. So if you use less or more, you will see segments to closed together or empty room between the segments .<br><br>So you can add as many segments as you like. You will never get problems with the waypoints.<br><br>experiment 2:<br>it needs one update until the newest segment is added to the snake. Meanwhile you see it at position 0,0,0. Solution: In the moment when you create a new segment, position the entity (f.e. the cube) behind your camera. When the next FLIP comes it will be "unvisible", but when the next update comes it will be positioned correct.<br><br><font class="tiny">Last edited 2012</font><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1166664"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >psychicbottle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> I like where you are going with this, you  should add a high score system into it. It,s fun already! <br><br></td></tr></table><br>
<a name="1166682"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Aussie</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Midimaster, I think I understand how the FIFO works. Would I be correct in saying that the head takes 20 steps in a direction &amp; each step is stored in the FIFO once the 20 steps have been completed it then passes the first one created to the next segment in the snake &amp; so on?<br><br>Thanks psychicbottle, that’s one of the next things I will be doing. Going to start on my own models &amp; images over the next few days. <br><br></td></tr></table><br>
<a name="1166764"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> yes <br><br></td></tr></table><br>
<a name="1166836"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >psychicbottle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Aussie, start a work log, i know this is a small project but i think we would all like to see you come along on it. <br><br></td></tr></table><br>
<a name="1166897"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Aussie</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Good idea psychicbottle. <br>As of where I am with the code now, I will start the work log. Will see this one through to the end. <br><br></td></tr></table><br>
<a name="1166923"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >psychicbottle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> Glad to hear it, i will keep up to date on your progress! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
