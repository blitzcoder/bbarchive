<!DOCTYPE html><html lang="en" ><head ><title >Returning Entities assigned to Type Fields</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Returning Entities assigned to Type Fields</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Returning Entities assigned to Type Fields</a><br><br>
<a name="1180110"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Shredster7</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello there!<br>I'm getting started on the code for my biggest game yet, and (big surprise) I've run into an issue with the earliest stages of the code.<br><br>What I want to do is to make a function (CREATE_DUMMY) that creates a new instance of a Type called "CHAR_DUMMY" and assigns a sprite to the CHAR_DUMMY instance's "mesh" field via CopyEntity.<br><br>However, it seems that the CREATE_DUMMY function won't return any values assigned to the CHAR_DUMMY instance I've created. <br><br>Here's the part of the code that's giving me trouble:<br><br><pre class=code>
;;;;;;;;;;;;;;;;;;;;
;LOAD SPRITE SHEETS;
;;;;;;;;;;;;;;;;;;;;
Global sprite_generic = LoadTexture("stand.png", 2)

.CREATE_SPRITE
Function CREATE_SPRITE(name, sprite_sheet)
	character = CreateSprite()
	EntityTexture character, sprite_generic
	NameEntity character, name
	Return name
End Function

.X

.CHAR_DUMMY
;;;;;;;;;;;;;;;;;;;
;"DUMMY" CHARACTER;
;;;;;;;;;;;;;;;;;;;

;Type variables
Type CHAR_DUMMY
	Field ID%
	Field damage
	Field lives
	Field xpos#
	Field ypos#
	Field direction
	Field airborne
	Field attacking
	Field stunned
	Field velx#
	Field vely#
	Field shield
	Field mesh
End Type

;Creation function
Function CREATE_DUMMY.CHAR_DUMMY(player, dummy1)

	Local player1.CHAR_DUMMY = New CHAR_DUMMY
	
	player1\lives = 5
	player1\direction = DIR_LEFT
	player1\mesh = CopyEntity(dummy1)
	
	Return player1.CHAR_DUMMY
End Function

	
;Update function
Function UPDATE_DUMMY.CHAR_DUMMY(character.CHAR_DUMMY)

	If KeyDown(KEY_LEFT)
		character\xpos = character\xpos - 2
	ElseIf KeyDown(KEY_RIGHT)
		character\xpos = character\xpos + 2
	EndIf
	
	PositionEntity character\mesh, character\xpos, character\ypos, 0
	
End Function

CREATE_SPRITE(dummy1, 0)
CREATE_DUMMY(one, dummy1)

While Not KeyDown(KEY_ESC)
	WaitTimer FRAMERATE

	UPDATE_DUMMY(player1.CHAR_DUMMY)
	
	UpdateWorld
	RenderWorld
	Flip
	
Wend
	
End
</pre><br><br><b> Thanks for your time! </b> <br><br></td></tr></table><br>
<a name="1180148"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> To be honest there are alot of issues with your code.  <br><br>You aren't actually assigning a variable to what the CREATE_DUMMY function is returning.  You really need to understand the variable types and use better naming conventions before you go much further or you'll be in a world of pain later.  <br><br>For example, the function below requires a name parameter which presumably should be a string ( $ ) in order for the entity to then be named.  As it stands you pass in dummy1 as the name, which is an integer converts to 0.  You return name so return 0.  You should be returning the character variable as this is the entity.  Even then, you don't actually have a variable to store what is returned!! <br><br><pre class=code>
Function CREATE_SPRITE(name, sprite_sheet)
	character = CreateSprite()
	EntityTexture character, sprite_generic
	NameEntity character, name
	Return name
End Function
</pre><br><br>If you are going to be reusing the sprite in each dummy_type created then it's best to create a global reference to the sprite, hide it and just copyentity the global version.<br><br>Anywho, I made some modification which may make more sense to you ..<br><br><pre class=code>
;;;;;;;;;;;;;;;;;;;;
;LOAD SPRITE SHEETS;
;;;;;;;;;;;;;;;;;;;;
Global TEXTURE = LoadTexture("stand.png", 2)

;create a global version of the sprite for re-use
Global SPRITE = CREATE_SPRITE( "This is the Name", 0 )

;Type variables
Type CharType
	Field ID%
	Field damage
	Field lives
	Field xpos#
	Field ypos#
	Field direction
	Field airborne
	Field attacking
	Field stunned
	Field velx#
	Field vely#
	Field shield
	Field mesh
End Type

;create a dummy and assign it to a global variable Player1 for later reference
Global Player1.CharType = CREATE_CHARACTER()

While Not KeyDown( KEY_ESC )

	WaitTimer FRAMERATE

	UPDATE_CHARACTER( Player1 )
	
	UpdateWorld
	RenderWorld
	Flip
	
Wend
	
End

;============================================================
;============================================================
;============================================================

Function CREATE_SPRITE( Name$, sprite_sheet = 0 )

	Local sprite = CreateSprite()
	
	EntityTexture sprite, TEXTURE
	NameEntity sprite, Name
	HideEntity sprite

	Return sprite

End Function

;============================================================
;============================================================
;============================================================

;Creation function
Function CREATE_CHARACTER.CharType( Player = 0, Dummy1 = 0 )

	Local this.CharType = New CharType
	
	this\lives = 5
	this\direction = DIR_LEFT
	this\mesh = CopyEntity( SPRITE )
	
	Return this

End Function

;============================================================
;============================================================
;============================================================
	
;Update function
Function UPDATE_CHARACTER( this.CharType )

	If KeyDown(KEY_LEFT)
		this\xpos = this\xpos - 2
	ElseIf KeyDown(KEY_RIGHT)
		this\xpos = this\xpos + 2
	EndIf
	
	PositionEntity this\mesh, this\xpos, this\ypos, 0
	
End Function
</pre> <br><br></td></tr></table><br>
<a name="1180348"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Shredster7</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've been playing around with that code you posted, and I have a much better idea of what I need to do now. I'm also going to have to study up on other suffixes (or whatever they're called) other than just '#' and '$'.<br><br>But anyways, thanks so much for helping me out! I'll remember to list you in the opening credits for the game. :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
