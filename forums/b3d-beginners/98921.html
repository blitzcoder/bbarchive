<!DOCTYPE html><html lang="en" ><head ><title >YawToEntity Function</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >YawToEntity Function</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >YawToEntity Function</a><br><br>
<a name="1158902"></a>

<a name="1158903"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Aussie</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am having some strange issues with this function.<br><br>If i move towards the enemy the enemy turns away from the player on the south side of the sphere. If i chase the enemy the enemy will eventually start to  point towards the player. <br><br>I am wondering if it has something to do with the playing area being a sphere.<br><br>Here is the code.<br>If you hold down the up arrow straight away you will see what happens when the player gets close to the enemy.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

; The number of logic updates per second.
Const C_LOGIC_FREQUENCY = 50

; The interval (in milliseconds) between logic updates.
Const C_LOGIC_INTERVAL# = 1000.0 / C_LOGIC_FREQUENCY ; Unused.

Const C_SMART_MISSILE_TEST_DISTANCE# = 10.0
Const C_SMART_MISSILE_TEST_ANGLE# = 60.0
Const C_SMART_MISSILE_NO_TURN_DISTANCE# = 15.0

Global timer = CreateTimer( C_LOGIC_FREQUENCY )



Graphics3D 1024,768,32,1

SetBuffer BackBuffer()


P_VEL#  = 0
P_YAW# = 0
	
light = CreateLight()
	RotateEntity light,180,0,0

camera = CreateCamera()
	CameraRange camera,.1,5000
	PositionEntity camera,0,0,0
	RotateEntity camera,090,0,0
	

masterpiv = CreatePivot()

sphere = CreateSphere(18)
	EntityColor sphere,0,150,255
	ScaleEntity sphere,550,550,550
	UpdateNormals sphere
	EntityPickMode sphere,2
	EntityRadius sphere,550



player = CreateCone()
	PositionEntity player,0,590,0
	RotateMesh player,90,0,0
	RotateEntity player,0,180,0
	ScaleEntity player,5,5,5
	EntityColor player,0,255,0
	
enemy = CreateCone()
	PositionEntity enemy,0,-590,0
	RotateMesh enemy,90,0,0
	RotateEntity enemy,180,0,0
	ScaleEntity enemy,5,5,5
	EntityColor enemy,255,0,0
	
	
	
campiv = CreatePivot()
	EntityParent campiv,player
	PositionEntity campiv,0,90,0
	pivx# = EntityX(campiv,True)
	pivy# = EntityY(campiv,True)
	pivz# = EntityZ(campiv,True)
	PositionEntity camera,pivx#,pivy#,pivz#

		
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
While Not KeyDown (1)



If KeyDown (203)
	P_YAW# = P_YAW# + .25
	ElseIf KeyDown (205)
	P_YAW# = P_YAW# - .25
End If

If P_YAW# &gt; 4
	P_YAW# = 4
ElseIf P_YAW# &lt; -4
	P_YAW# = -4
End If



				
If KeyDown (200)
	P_VEL# = P_VEL# + .5
	create_flame(player,Rnd(-.5,.5),0,-1,Rnd(10,20))
End If




If P_VEL# &gt; 5 P_VEL# = 5


If P_VEL# &gt; 0
P_VEL# = P_VEL# - .1
If P_VEL# &lt; 0 P_VEL# = 0
End If


If P_YAW# &gt; 0
   P_YAW# = P_YAW# - .05
  If P_YAW# &lt; 0 P_YAW# = 0             
ElseIf P_YAW# &lt; 0
   P_YAW# = P_YAW# + .05
  If P_YAW# &gt; 0 P_YAW# = 0
ElseIf P_YAW# = 0
   P_YAW# = 0
End If


MoveEntity player,0,0,P_VEL#
TurnEntity player,0,P_YAW#,0


MoveEntity enemy,0,0,2
create_flame(enemy,Rnd(-.5,.5),0,-1,Rnd(10,20))

If EntityDistance (enemy,player) &lt; 250	
	YawToEntity( enemy, player, 3 )	
End If

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;Tform &amp; Picking;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

TFormVector 0,-90,0,player,0
EntityPickMode sphere,1
LinePick (EntityX(player,True),EntityY(player,True),EntityZ(player,True),TFormedX(),TFormedY(),TFormedZ())
AlignToVector player,PickedNX#(),PickedNY#(),PickedNZ#(),2,1

TFormVector 0,-90,0,enemy,0
LinePick (EntityX(enemy,True),EntityY(enemy,True),EntityZ(enemy,True),TFormedX(),TFormedY(),TFormedZ())
AlignToVector enemy,PickedNX#(),PickedNY#(),PickedNZ#(),2,1
EntityPickMode sphere,0


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


update_flame()



pivx# = EntityX(campiv,True)
pivy# = EntityY(campiv,True)
pivz# = EntityZ(campiv,True)
PositionEntity camera,pivx#,pivy#,pivz#

vx# = EntityX( player ) - EntityX( camera )
vy# = EntityY( player ) - EntityY( camera )
vz# = EntityZ( player ) - EntityZ( camera )

AlignToVector camera,vx,vy,vz,3,1

UpdateWorld


RenderWorld



Flip
LimitFrameRate(38)
Wend
End

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Function LimitFrameRate(FPS = 65)
	If FrameTime = 0 Then
		Period = 1000 / FPS
		FrameTime = MilliSecs()
	EndIf

	While (FrameTime + Period) &gt; MilliSecs()
			Delay 1
	Wend
	FrameTime = MilliSecs()
End Function 


Type flame
	Field life%
	Field scale#
	Field x#,y#,z#
	Field obj
	Field alpha#
	Field R#,G#,B#
End Type

Function create_flame(entity,x#,y#,z#,life#)
	f.flame = New flame
		f\obj = CreateSphere(10,entity)
		f\R# = 0
		f\G# = 155
		f\B# = 255
		EntityColor f\obj,f\R,f\G,f\B
		EntityFX f\obj,1
		EntityBlend f\obj,3
		f\scale# = Rand(1,4)
		f\alpha# = 1
		f\life = life#
	PositionEntity f\obj,x,y,z
	EntityParent f\obj,0
End Function

Function update_flame()
	For f.flame = Each flame
		f\G = f\G - Rand(0,10)
		EntityColor f\obj,f\R,f\G,f\B
		f\alpha = f\alpha - .1
		ScaleEntity f\obj,f\scale,f\scale,f\scale
		EntityAlpha f\obj,f\alpha
		f\life = f\life - 1
		If f\life &lt; 0 Then
			FreeEntity f\obj
			Delete f
		End If
	Next
End Function

Function YawToEntity( src_entity, dest_entity, rate# )
; Turns 'src_entity' to point at 'dest_entity' at the rotation rate specified by 'rate#'.

	Local target_yaw# = DeltaYaw( src_entity, dest_entity )
	
	; If the required correction amount is less than the correction amount to be applied...
	If Abs( target_yaw# ) &lt; rate#
		; Point 'src_entity' directly at 'dest_entity' to prevent jittering.
		TurnEntity src_entity, 0.0, target_yaw#, 0.0
	Else
		; Turn 'src_entity' gradually towards 'dest_entity'.
		TurnEntity src_entity, 0.0, rate# * Sgn(  target_yaw# ), 0.0
	EndIf
End Function
</textarea><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1158904"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> It's not entirely clear to me what should be happening. But you probably want to turn around the world Y axis, meaning set the global flag to True when you turn the enemy. Near the end of the code that would be<br><pre class=code>	; If the required correction amount is less than the correction amount to be applied...
	If Abs( target_yaw# ) &lt; rate#
		; Point 'src_entity' directly at 'dest_entity' to prevent jittering.
		TurnEntity src_entity, 0.0, target_yaw#, 0.0, True
	Else
		; Turn 'src_entity' gradually towards 'dest_entity'.
		TurnEntity src_entity, 0.0, rate# * Sgn(  target_yaw# ), 0.0, True
	EndIf</pre><br>You can find my description of DeltaYaw <a href="/posts.php?topic=76602#856629" target="_blank">here</a>.<br>That was four and a half years ago and I was just posting something I wrote  many years earlier. So I don't remember much about it. <br><br></td></tr></table><br>
<a name="1158908"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Aussie</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey there Floyd,<br>What should be happening is when the played gets close to the enemy the enemy should point at the player to give chase.<br>Zethrax pointed me to his Function YawToEntity <a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=2258" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=2258</a> in another thread.<br>I have set the global flag to true &amp; i still get the same thing happening. <br><br></td></tr></table><br>
<a name="1158942"></a>

<a name="1159074"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Okay, I hadn't really thought about the spherical world.<br><br>The correct approach is the opposite of what I was thinking. Instead of looking at this globally do it locally, from the point of view of the enemy. The only decision is which way to turn.<br><br>The enemy entity has his own coordinate system. Y+ is up, Z+ is forward and X+ and X- are right and left. We need only decide whether the target ( the player ) is to the right or left as seen by the enemy. The player is always at 0,0,0 in his own space so we transform that to the enemy's space. Then TFormedX() tells us which side the player is on. If this value is positive then the player is to the right of the enemy, and the enemy must turn clockwise as seen from above. That means a negative change to the Yaw angle. Thus the sign of the Yaw change is the opposite of the sign of TFormedX().<br><br>With that in mind the last part of YawToEntity becomes<br><br><pre class=code>		; Turn 'src_entity' gradually towards 'dest_entity'.
		TFormPoint 0,0,0,  dest_entity, src_entity
		TurnEntity src_entity, 0.0, rate# * -Sgn( TFormedX() ), 0.0</pre><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1159025"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> The enemy entity has his own coordinate system. Y+ is up, Z+ is forward and X+ and X- are right and left. We need only decide whether the target ( the player ) is to the right or left as seen by the enemy. <br></div><br>Hi Floyd, I've implemented your suggestion as a simple AI following the player - <a href="/posts.php?topic=98905#1159024" target="_blank">link to post</a>. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
