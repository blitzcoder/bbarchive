<!DOCTYPE html><html lang="en" ><head ><title >Problems with types</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Problems with types</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Problems with types</a><br><br>
<a name="998000"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Valgar</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello everyone<br>I have a problem with my code and i can't figure out whats wrong.<br>Basically i have types for enemy,player,bullets,enemy bullets.<br>The way i create the types is the following:<br>1)player is created OUTSIDE main loop (a typical while ---&gt; wend) once,and its coordinates are mousex and y<br>2)if i press a button an enemy is created<br>3)i update player position<br>4)i update enemy position<br>5)main loop is finished if i press esc<br><br>Everything is working fine until i create an enemy....<br>I get a debugger warning telling me that a variable doesnt exist.<br>That variable is the one(within the enemy type) that store the player coordinates(i do this cause i have some code that send the enemy toward the position that the player had when the enemy was created)<br>If i put the create player code inside the main loop everything works fine,but the problem is that i create an instance of the player type everytime the main loop is repeated,causing a row of player images,cause i just call the function without any condition (unlike the enemy,which is created based on a keypress).<br>I try to write down some code to make it clear...<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
createplayer(100,100)	;must be out of the main loop otherwise its called multiple times per second automatically
;M A I N 
While Not KeyHit(1)
	Cls
	If KeyHit(2) Then create_enemy(10,baddie,1,300,300,1,4,1)
	If KeyHit(3) Then create_enemy(10,baddie,2,100,100,1,8,1)
	If KeyHit(4) Then create_enemy(10,baddie,3,100,100,1,6,1)
	displayPlayer()
	displayEnemy()
	Flip(1)
Wend
End
;END M A I N
</textarea><br>If i replace the variables that store the enemy coordinates with simple Mousex() and Mousey() everything works ok,but its not what i want because i use the mouse coordinates at the moment for debugging purposes(i replace mouse coordinates with proper variables when everything works fine...).<br>So i suspect that if i create a type outside the main loop its variables doesnt get updated inside the main loop....correct me if i am wrong!<br>Here is the section of the code that create the enemy:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Function create_enemy(energia,immagine,moto,posizx,posizy,power,speed,weapon)
	enemy.enemy = New enemy
	enemy\energy=energia	;hit points of enemies
	enemy\image=immagine	;the actual enemy graphic
	enemy\lifespan=100	;the lifespan if untouched,so it doesnt stay on screen forever
	enemy\motion=moto	;the type of motion enemy use
	enemy\posx=posizx	;the starting position
	enemy\posy=posizy	;the starting position
	enemy\power=power	;the power
	enemy\speed=speed	;the enemy speed
	enemy\weapon=weapon	;the type of weapon the enemy use...useful for changing bullet shapes and so on
	;adding the 'homing move' code here to see if its better to have it type-related...
	enemy\px=player\posx					;player position
	enemy\py=player\posy					;player position
	enemy\pa#=Rand(-180,180)		;player angle
	enemy\ex=enemy\posx				;enemy starting position
	enemy\ey=enemy\posy				;enemy starting position
	enemy\ps#=0.1				;precision approximation of the player position
	enemy\bs=speed		;enemy speed....try changin this in the function call
;calculate routine
	enemy\an#=ATan2(enemy\ey-enemy\py,enemy\ex-enemy\px)		;used to calculate from 'coordinate system relative' to 'player relative'
	enemy\p#=-enemy\pa-enemy\an						
	enemy\e#=ASin((Sin(enemy\p)/enemy\bs*enemy\ps))		;    the main formula....
;get enemy parameters from stuff
	enemy\ba#=-enemy\an+180+enemy\e			;enemy angle: calculation return to system relative coords
	enemy\bx#=enemy\ex					;enemy coordinates
	enemy\by#=enemy\ey					;enemy coordinates
	enemy\direzione=enemy\speed	;this is just a variable to "hold" a direction on the x axis for some enemy movement
	enemy\borderdx=posizx+100	;this is the right border of some movement limit
	enemy\bordersx=posizx-100	;this is the left border of some movement limit
End Function
</textarea><br>The lines that give me the error are "enemy\px=player\posx" and "enemy\py=player\posy" ......<br>If anyone has any suggestion i can stop hitting the wall with my head... :P<br>Thanks in advance! <br><br></td></tr></table><br>
<a name="998012"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >lo-tekk</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Maybe you forgot to make your player globally accessible then ?<br><br>Something like:<br><br><pre class=code>
Global player.PlayerType

player=createplayer(100,100)

</pre><br><br>You might change your createplayer() function to return an instance of the created player. <br><br></td></tr></table><br>
<a name="998017"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Valgar</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello.<br>No the type is globally accessible.<br>What do you mean by changing the function to return an instance? <br><br></td></tr></table><br>
<a name="998074"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> By changing the function to return an instance, means essentially:<br><br><pre class=code>
Type MyType
Field Whatever%
End Type

Function ReturnType.MyType()
Local CreateType.MyType= New NewType
CreateType\Whatever%=1
Return CreateType\MyType
End Function

</pre><br><br>So that when the enemey instance of the type is created, you can 'do stuff' to that particular enemy:<br><br><pre class=code>
Local NewBaddie.MyType
If (If KeyHit(2)) Then NewBaddie.MyType=ReturnType.MyType()
</pre> <br><br></td></tr></table><br>
<a name="998078"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oops sorry for double-post---<br><br><div class="quote"> So i suspect that if i create a type outside the main loop its variables doesnt get updated inside the main loop....correct me if i am wrong!<br> <br></div><br><br>This is why you would ened to access the ACTUAL INSTANCE of the type.<br><br>For example, in your DisplayPlayer() and DisplayEnemy() functions I would guess that you aren't selecting types but instead using variables (maybe from the Type Fields) that are currently un-declared.<br><br>Could you post teh code to these functions?<br><br>From what I can assume, they should be something like:<br><br><pre class=code>

Function DisplayPlayer()
Local GetPlayer.PlayerType = First Player
DrawImage GetPlayer\Immagin%,MouseX(),MouseY()
End Functionm

Function DisplayEnemy()
Local GetEnemy.EnemyType
For GetEnemy.EnemyType = Each EnemyType
DrawImage GetEnemy\Immagin%,GetEnemy\posizx,GetEnemy\poiszy
Next
End Function

</pre> <br><br></td></tr></table><br>
<a name="998171"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Valgar</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello<br>Sorry for delay,but i was too busy sleeping... :P<br>I post the whole code here,cause i think i just dont understand how to use types (and thats a basic thing to understand...).<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;simple shooter demo

;initialising the graphic display
Graphics 800,600	;for now its windowed for test purposes
SetBuffer BackBuffer()	;this tell that all the drawing is done in the backbuffer for double buffering purposes
HidePointer

;loadind all the graphic
Global hero=LoadImage("player.png")
Global bullet=LoadImage("bullet.png")
Global baddie=LoadImage("enemy.png")
Global enemybullet=LoadImage("ebullet.png")

MaskImage hero,0,128,128
MaskImage baddie,0,128,128

;now we create types
Type player
	Field posx,posy,power,hitpoints,bombtype,bombpower
End Type

Type enemy
	Field posx,posy,image,energy,power,motion,weapon,speed,lifespan,name
	;adding some calculating fields
	Field bx#,by#,ba#,bs#,px#,py#,pa#,ex#,ey#,ps#,an#,p#,e#,direzione,borderdx,bordersx
End Type

Type explosion
	Field posx,posy,image
End Type

Type playerbullet
	Field posx,posy,power,image,speed,life
End Type

Type enemybullet
	Field posx,posy,power,image,speed
End Type

Global player.player
Global enemy.enemy
Global pbullet.playerbullet
Global ebullet.enemybullet

;now we create some functions

Function createplayer(xpos,ypos)
	player.player=New player
	player\posx=xpos
	player\posy=ypos
	player\power=1	;starting power
	player\hitpoints=10	;starting hit points
End Function

Function create_enemy(energia,immagine,moto,posizx,posizy,power,speed,weapon)
	enemy.enemy = New enemy
	enemy\energy=energia	;hit points of enemies
	enemy\image=immagine	;the actual enemy graphic
	enemy\lifespan=100	;the lifespan if untouched,so it doesnt stay on screen forever
	enemy\motion=moto	;the type of motion enemy use
	enemy\posx=posizx	;the starting position
	enemy\posy=posizy	;the starting position
	enemy\power=power	;the power
	enemy\speed=speed	;the enemy speed
	enemy\weapon=weapon	;the type of weapon the enemy use...useful for changing bullet shapes and so on
	;adding the 'homing move' code here to see if its better to have it type-related...
	enemy\px=player\posx					;player position
	enemy\py=player\posy					;player position
	enemy\pa#=Rand(-180,180)		;player angle
	enemy\ex=enemy\posx				;enemy starting position
	enemy\ey=enemy\posy				;enemy starting position
	enemy\ps#=0.1				;precision approximation of the player position
	enemy\bs=speed		;enemy speed....try changin this in the function call
;calculate routine
	enemy\an#=ATan2(enemy\ey-enemy\py,enemy\ex-enemy\px)		;used to calculate from 'coordinate system relative' to 'player relative'
	enemy\p#=-enemy\pa-enemy\an						
	enemy\e#=ASin((Sin(enemy\p)/enemy\bs*enemy\ps))		;    the main formula....
;get enemy parameters from stuff
	enemy\ba#=-enemy\an+180+enemy\e			;enemy angle: calculation return to system relative coords
	enemy\bx#=enemy\ex					;enemy coordinates
	enemy\by#=enemy\ey					;enemy coordinates
	enemy\direzione=enemy\speed	;this is just a variable to "hold" a direction on the x axis for some enemy movement
	enemy\borderdx=posizx+100	;this is the right border of some movement limit
	enemy\bordersx=posizx-100	;this is the left border of some movement limit
End Function

Function createPbullet()
	pbullet.playerbullet=New playerbullet
	pbullet\image=bullet
	pbullet\posx=player\posx
	pbullet\posy=player\posy+10
	pbullet\speed=8
	pbullet\power=1
	pbullet\life=50
End Function

Function createEbullet()
	ebullet.enemybullet=New enemybullet
	ebullet\image=enemybullet
	ebullet\posx=enemy\posx
	ebullet\posy=enemy\posy+10
	ebullet\speed=8
	ebullet\power=enemy\power
End Function

Function makexplosion()
	;its empty for now....
End Function

Function displayPlayer()
	For player.player=Each player
		player\posx=MouseX()
		player\posy=MouseY()
		DrawImage hero,player\posx,player\posy
	Next
End Function

Function displayEnemy()
	For enemy.enemy=Each enemy
		
		Select enemy\motion	;we choose different coordinates based on the type of motion we choose
			Case 1 
				straight()
			Case 2
				homing()
			Case 3
				zigzag()
			Case 4 
				circle()
				
		End Select
		DrawImage enemy\image,enemy\posx,enemy\posy
		enemy\lifespan=enemy\lifespan-1	;we decrease the lifespan by 1 unit
		If enemy\lifespan=&lt;0 Then 
			Delete enemy	;when lifespan reach 0 its deleted
		ElseIf enemy\energy =&lt;0 Then 
			Delete enemy	;when hit points reach 0 its deleted
		EndIf
		
	Next
End Function

Function updatepbullet()
	For pbullet.playerbullet=Each playerbullet
		pbullet\posx=player\posx
		pbullet\posy=pbullet\posy-10;10;player\posy+pbullet\speed
		DrawImage bullet,pbullet\posx,pbullet\posy
		pbullet\life=pbullet\life-1
		If pbullet\life=&lt;0 Then
			Delete pbullet
		EndIf
	Next
End Function

Function homing()
	enemy\bx=enemy\bx+Cos(enemy\ba)*enemy\bs;*.1		;update enemy according to calculated angle
	enemy\by=enemy\by-Sin(enemy\ba)*enemy\bs;*.1
	enemy\posx=enemy\bx	;x coord updated
	enemy\posy=enemy\by	;y coord updated
	enemy\name=2
End Function

Function straight()
	enemy\posy=enemy\posy+enemy\speed	;we update just the y coordinates of enemies
	enemy\name=1
End Function

Function zigzag()
	enemy\name=3
	enemy\posx=enemy\posx+enemy\direzione	;this initiate the movement on the x axis
	enemy\posy=enemy\posy+enemy\speed/3	;this initiate the movement on the y axis half the speed of the one on the x axis
	If enemy\posx=&gt;enemy\borderdx	;if the enemy reach its right border limit
		enemy\posx=enemy\posx-enemy\speed	;this tell the enemy to start to move to the left
		enemy\direzione=-enemy\speed	;important! this tell the enemy that its direction is the opposite now
	EndIf
	If enemy\posx=&lt;enemy\bordersx	;if the enemy reach its left border limit
		enemy\posx=enemy\posx+enemy\speed	;this tell the enemy to start move to the right
		enemy\direzione=+enemy\speed	;important! this tell the enemy that its direction is the opposite now
	EndIf
End Function

Function circle()
	;nothing for now
End Function

createplayer(100,100)	;must be out of the main loop otherwise its called multiple times per second automatically
;M A I N 
While Not KeyHit(1)
	Cls
	player\posx=MouseX()
	player\posy=MouseY()
	If MouseHit(2) Then createPbullet() 
	If KeyHit(2) Then create_enemy(10,baddie,1,300,300,1,4,1)
	If KeyHit(3) Then create_enemy(10,baddie,2,100,100,1,8,1)
	If KeyHit(4) Then create_enemy(10,baddie,3,100,100,1,6,1)
	displayPlayer()
	displayEnemy()
	updatepbullet()
	Flip(1)
Wend
End
;END M A I N
</textarea><br>I think i cant grasp the concept of returning an instance so i can do stuff with it.<br>Thanks in advance <br><br></td></tr></table><br>
<a name="998189"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Valgar</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I just noticed that i posted an updated version of the code sorry...<br>In that code what doesnt work is the "updatebullet()" function...and the enemies are updated just because i have declared into the main loop the variables that store the player coordinates as mousex() and mousey()...<br>I am stuck,i have tried everything i know to make it work. <br><br></td></tr></table><br>
<a name="998201"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I was 'scared' of using ypes for a long time, until somehow they suddenly finally made sense!<br><br>Looking at your code<br><br>The first thing I noticed was the lines:<br><br>Global player.player<br>Global enemy.enemy<br><br>Every time that you refer to a Type with the "label.type" syntax you are telling Blitz that out of any created 'instances' of a Type, (instances are created whenever you use 'New ' ) you are referring specifically to a particular one. In the cases above, a specific instance of the 'player' type which you have labelled 'player' and a specific instance of the 'enemy' type which you have labelled 'enemy'<br><br>Since you will need to deal with more than one enemy, I removed the<br>Global enemy.enemy<br>since we don't NEED to deal with any one in particular.<br><br>Assuming there will be only one player, then it doesn't matter that we have player.player used throughout and the instance of player.player is global.<br><br>Back to the 'enemies', in the creation function:<br><pre class=code>
Function create_enemy(energia,immagine,moto,posizx,posizy,power,speed,weapon)
</pre><br><br>You have identified <br><pre class=code>	enemy.enemy = New enemy</pre><br><br>This is part of the main problem.<br>As enemy.enemy was global, then you are ALWAYS making the same instance (enemy.enemy) = the NEW instance created.<br><br>Instead, I've added in:<br><br><pre class=code>
Local enemy.enemy = New enemy</pre><br>[/code]<br>So the enemy.enemy is JUST FOR THE NEW instance we are creating with the function.<br><br><br>When you come to DisplayEnemy, because previously you had a single global label for enemy.enemy, when you called For / Each, the label was removed from the New enemy created ion order for it to apply to the 'For/Each pointer', so your enemies would not ever be found correctly.<br><br>However, with a LOCAL enemy.enemy, For/Each works perfectly and should iterate through all the enemies correctly.<br><br><pre class=code>

Function displayEnemy()
	Local enemy.enemy
	For enemy.enemy=Each enemy

</pre><br><br>Now, all that remains is to ensure that the remaining functions, such as zigzag, create ebullet homing etc. know WHICH enemy to deal with.<br><br>There's two ways to approach this:<br><br>1 - Process EVERY enemy in a For/Each loop<br>2 - Ensure the function accepts the enemy Type instance as a parameter to identify which enemy it's dealing with.<br><br>This largely depends on where the code is called from, but here's examples:<br><br><pre class=code>
Function createEbullet(ShootingEnemy.enemy)
	ebullet.enemybullet=New enemybullet
	ebullet\image=enemybullet
	ebullet\posx=ShootingEnemy\posx
	ebullet\posy=ShootingEnemy\posy+10
	ebullet\speed=8
	ebullet\power=ShootingEnemy\power
End Function
</pre><br><br><pre class=code>
Function homing()
Local enemy.enemy
For enemy.enemy=Each enemy
;ADDED TO ENSURE UPDATES TO PLAYER POSITION ARE NOTICED BY THE ENEMY
	enemy\px=player\posx					;player position
	enemy\py=player\posy					;player position
	enemy\pa#=Rand(-180,180)		;player angle
	enemy\bx=enemy\bx+Cos(enemy\ba)*enemy\bs;*.1		;update enemy according to calculated angle
	enemy\by=enemy\by-Sin(enemy\ba)*enemy\bs;*.1
	enemy\posx=enemy\bx	;x coord updated
	enemy\posy=enemy\by	;y coord updated
	enemy\name=2 ; not sure what this is for........
Next
End Function
</pre><br><br><br><br><br>For Createpbullet, because player.player is global, and hopefully tthere's only one layer, then it will know that player.player refers to the ionly instance we created.<br><br>_____________________________________________________________________<br><br>That should go some way to get your code working better.<br><br>As for types generally, I'll see if I can describe them in a way that makes sense:<br><br><br>1. A Type is a 'collection' of data<br>2. The data in Each Type is stored as INSTANCES.<br>3. Each INSTANCE contains information according to FIELDS.<br>4. FIELDS can contain various types of data such as strings, integers and object handles, or floating points. (They can also contain pointers to other Type instances or arrays, but that's not important here)<br><br>Example:<br><br>To set up a Type containing information about pets, I first need to declare the Type and what Fields it will have. I know I want to have the pets' names, the type of animal it is, and how old each pet is. The first two fields, then (Name$ and Animal$) will be strings, the third, Age% will be a number, so an Integer should do)<br><pre class=code>
Type MyPets
Field Name$
Field Animal$
Field Age%
End Type
</pre><br>Now I need to add in the data. To start with, I need to put in my pet dog, Rufus. Rufus is 7 years old.<br><br>Because there's no entries at all yet, and specifically, no entries for Rufus, I need to make a NEW INSTANCE.<br>This means using the 'New' command.<br>However, to let Blitz know when I am filling out the data for Rufus, that I am dealing with this NEW INSTANCE, I have to give the Instance a label.<br><br>The label I will use is "MyInstance"<br><pre class=code>
Local MyInstance.MyPets = New MyPets
</pre><br><br>Note that the "MyInstance" label for this NEW INSTANCE is Local. Unlike variables, though the actual data that will be stored in the INSTANCE will be retained within the Type (The Type will retain the data for ALL the instances created within it) what it means having a Local label, is that once I've finished dealing with the New INSTANCE, I can re-use the label elsewhere, it will no longer be relevant to Rufus' INSTANCE.<br><br>Let's carry on  though, and fill out the data for Rufus (this current NEW INSTANCE).<br><pre class=code>
MyInstance\Name$="Rufus"
MyInstance\Animal$="Dog"
MyInstance\Age%=7
</pre><br>That's it. Rufus now hasan INSTANCE in the Type!<br><br>We can add more pets, re-using the "MyInstance" label because it was a Local one...<br><br><pre class=code>
MyInstance.MyPets = New MyPets
MyInstance\Name$="Church"
MyInstance\Animal$="Cat"
MyInstance\Age%=666

MyInstance.MyPets = New MyPets
MyInstance\Name$="Nemo"
MyInstance\Animal$="Fish"
MyInstance\Age%=1
</pre><br><br>Now what happens when we want to find information about the pets?<br><br>We can do this in two ways, one is to pick out a specific pet, (a specific instance) OR to search through ALL the instances (all the pets)<br><br>In order to identify any INSTANCES of the Type, we need a label to do so. We can still use the "MyInstance" label if we wish. <br><br>*Re-Using the same label when it's Local doesn't have any particular consequences, aside from that once you exit the function where it's used, or re-assign the label to another Instance, then it no longer has any meaning relating to the prior instance.<br><br>So, let's see if we can pick out "Church" the cat from the Type.<br>Knowing it was the second of three INSTANCES made, I can find "Church"'s INSTANCE by the use of BEFORE, AFTER, FIRST and LAST commands (being #2 of 3, means it's the INSTANCE AFTER #1(First) and the INSTANCE BEFORE 3#(Last) i.e.:<br><br><pre class=code>
MyInstance.MyPets = After First MyPets
MyInstance.MyPets = Before Last MyPets
</pre><br><br>These would both result in Church's INSTANCE<br><br>But what if I didn't know how many pets there were, or whereabouts Church was?<br><br>Then, I'd have to search through all instances and Pick the one for Church from the list:<br><br><pre class=code>
For MyInstance.MyPets = Each MyPets
</pre><br><br>Luckily, we know Church's name so we can distinguish his INSTANCE from another:<br><pre class=code>
If MyInstance\Name$="Church" Then
</pre><br><br>We might need further clarification, just in case we had a dog called "Church", too or something<br><pre class=code>
If ((MyInstance\Name$="Church") And (MyInstance\Animal$="Cat")) Then
</pre><br><br>Either way, we have identified Church from all the others. If we used the For/Each loop method, we ought to ensure the loop doesn't carry on now we have the Instance we want...<br><br><pre class=code>
For MyInstance.MyPets = Each MyPets
If ((MyInstance\Name$="Church") And (MyInstance\Animal$="Cat")) Then Exit
Next
</pre><br><br>Now, we need to think about modifying or editing some of the values given to the fields of these instances. As an example, I'm gonna look at 'Age'<br><br>The above code has selected a specific INSTANCE for us. Thanks to the 'Exit' command, just after the 'Next' we can carry on, knowing that "MyInstance.MyPets" is a label that refers to the Instance where the field Name$="Church" and the field Animal$="Cat".<br>So if the next line was<br><pre class=code>Print Str(MyInstance\Age%)</pre><br>It would print Church's Age!<br><br>If it was Church's birthday, I could simply alter the value too...<br><pre class=code>
MyInstance\Age%=((MyInstance\Age%)+1)
</pre><br><br>Let's say, that today IS Church's birthday - he's gonna be 667 !<br>So as we already determined how to separate Church from the rest, now all we need to do is update the value of that INSTANCE.<br><br>BUT! I don't want to have to program every obne again each birthday for each pet, so wouldn't it be great if I had a single function that could do it - perhaps on their birthdays (which is recorded elsewhere for simplicity's sake) there's a function called "Birthday()" Which then does the work...<br><br>To make sure we get the right pert's birthday though, we'll give the Birthday() function two parameters, Name and Animal.<br><br><pre class=code>
Function Birthday(Name$,Animal$)
</pre><br><br>Our birthday function will use the ideas already presented above all together:<br>So the first theing the Birthday function needs to do, is declare the Local label (because, being in a function, any previous uses will now have been 'forgotten', and we want to let Blitz know that we are labelling the MyPets Type instances)<br><pre class=code>
Function Birthday(Name$,Animal$)
Local MyInstance.MyPets
</pre><br>However, we're not making any new Instances, only updating the existing ones, so we'll need that For/Each loop...<br><pre class=code>
Function Birthday(Name$,Animal$)
Local MyInstance.MyPets
For MyInstance.MyPets = Each MyPets
</pre><br><br>And again, we need to identify the precise instance accordng to our parameters of Name$ and Animal$<br><br>So...<br><br><pre class=code>
Function Birthday(Name$,Animal$)
Local MyInstance.MyPets
For MyInstance.MyPets = Each MyPets
If ((MyInstance\Name$="Church") And (MyInstance\Animal$="Cat")) Then Exit
Next
</pre><br>Now, we have the right Instance, we can add a year to their current age, and print it, to confirm the change!<br><br><pre class=code>
Function Birthday(Name$,Animal$)
Local MyInstance.MyPets
For MyInstance.MyPets = Each MyPets
If ((MyInstance\Name$="Church") And (MyInstance\Animal$="Cat")) Then Exit
Next
MyInstance\Age%=((MyInstance\Age%)+1)
Print "Happy Birthday! "+Str(MyInstance\Age%)+" Today!"
</pre><br><br>SO now we can idenitify, retrieve and modiify individual instances. But one thing may seem a liittle annoying....<br>Every time the birthday function is run, it has to search through EVERY instanec of the type? <br>Go through ALL THE PETS just to find which one's birthday it is???<br><br>At some point, yes. A search will need to be made. HOWEVER - <br>If that particular instance is ever identified with a label, it can be passed to a function on it's own, OR maybe stored with a Global label.<br><br>Let's just say (you may have guessed) that Church, the cat is my favourite pet. So I'm gonna honour him with the status of having a Global label :)<br><br><pre class=code>
Global FavouritePet.MyPets
</pre><br><br>Way back when I was entering the data, and I gave Church's information as:<br><pre class=code>
MyInstance.MyPets = New MyPets
MyInstance\Name$="Church"
MyInstance\Animal$="Cat"
MyInstance\Age%=666
</pre><br>We were using the Local, re-usable label of "MyInstance". After thee lines for the Cat, I shall make sure that the Global FavouritePet points to this particular Instance.<br><pre class=code>
MyInstance.MyPets = New MyPets
MyInstance\Name$="Church"
MyInstance\Animal$="Cat"
MyInstance\Age%=666
Global FavouritePet.MyPets = MyInstance
</pre><br><br>There. Now, even if the label MyInstance changes (as in, changes which Instance it refers to), FavouritePet.MyPets as always going to point to the Instance for Church. (UNLESS I ever use FacouritePet.MyPet= ..... again! )<br><br>Having a specific instance can be handy for functions that deal with Instances rather than each of these functions needing to search through the entire Types.<br><br>For ecample, the birthday function could be:<br><br><pre class=code>
Function Birthday(BirthdayInstance.MyPets)
BirthdayInstance\Age%=((BirthdayInstance\Age%)+1)
Print "Happy Birthday! "+Str(BirthdayInstance\Age%)+" Today!"
End Function
</pre><br><br>And as a special treat...<br><br><pre class=code>
Function Birthday(BirthdayInstance.MyPets)
BirthdayInstance\Age%=((BirthdayInstance\Age%)+1)
Print "Happy Birthday! "+Str(BirthdayInstance\Age%)+" Today!"
If BirthdayInstace.MyPets = FavouritePet.MyPets Then Print "Have Some Cake!"
End Function
</pre><br><br>Lastly, What if I wanted that function to get hold of the Instance so I knew when it was a Pet s birthday, and whose birthday it was?<br><br>For now, we need to assume another Field was added to the Type:<br><pre class=code>
Field DateOfBirth$
</pre><br><br>And for simplicity's sake, the DateOfBirth$ field contains dates in the format "01/Jan/1999" as a string. (The same as CurrentDate() returns).<br><br>So here's where our search of all the pets comes in...<br>Instead of checking for names, though, we're looking for whether the date of birth matches the CurrentDate()<br><br>Technically, this is (shortening each date to ignore the year, it's only day and month we need)<br><pre class=code>
((Left$(MyInstance\DateOfBirth$,Len(MyInstance\DateOfBirth$)-5))=(Left$(CurrentDate$(),Len(CurrentDate$()-5)))
</pre><br><br><pre class=code>
Function CheckDate()
Local MyInstance.MyPets
For MyInstance.MyPets=Each MyPets
If 
((Left$(MyInstance\DateOfBirth$,Len(MyInstance\DateOfBirth$)-5))=(Left$(CurrentDate$(),Len(CurrentDate$()-5)))

</pre><br>However - unlike before, when we were searching for name$ and/or animal$, this time, we need to account for the fact that it's possible that more than one pet could have a birthday on the same date!<br>This means, we no longer can just 'Exit' the loop here, but must allow the loop to continue until ALL the pets have been iterated through.<br><br>But this - you may have guessed - will give us problems. How will we know which Pet is which after the loop has iterated because MyInstance at the end of the For/Each loop will only = whatever "Last MyPets" is.<br><br>That's where it comes in handy, that we had changed the Birthday Function to accept an Instance as a parameter. We have the IF line in our CheckDate function to identify the valid ce(s) so can pass that to the Birthday Function, then continue the loop as normal...<br><br><pre class=code>
Function CheckDate()
Local MyInstance.MyPets
For MyInstance.MyPets=Each MyPets
If 
((Left$(MyInstance\DateOfBirth$,Len(MyInstance\DateOfBirth$)-5))=(Left$(CurrentDate$(),Len(CurrentDate$()-5))) Then Birthday(MyInstance.MyPets)
Next
End Function
</pre><br><br>Lastly, Let's look at returning an Instance from a function:<br><br>We know how to accept an instance as a parameter to a function with <pre class=code>Function Birthday(BirthdayInstance.Mypets)</pre><br><br>BirthdayInstance.MyPets is the Instance passed IN to the function Birthday()<br><br>Just as, say, for <pre class=code>
KeyDown(1)</pre><br><br>1 is the value passed IN to the function KeyDown()<br><br>Tghe difference, of course, is that BirthdayInstance.MyPets has the label "BirthdayInstance" specifying which instance of MyPets is being used, because just using "MyPets" would not clarify exactly which instancve out of the whole MyPets Type you meant.<br><br>In the KeyDown example, 1 is just that, the number, without any labels or handles or anything, because there's nothing else that 1 can represent.<br><br>So, to get an Instance OUT of a function, we need a label attached to the function to specify the particular instance:<br><br><pre class=code>
Function GetInstance.MyPets()
</pre><br><br>This ensures for us that whenever we call "GetInstance()" we should expect the result to be a Type instance of MyPets.<br><br>We can use this as a step in our birthday program:<br>Note the Return line returns a labelled Instance like the Function name.<br><pre class=code>Function GetInstance.MyPets(Date$)
Local MyInstance.MyPets
For MyInstance.MyPets = Each MyPets
((Left$(MyInstance\DateOfBirth$,Len(MyInstance\DateOfBirth$)-5))=(Left$(CurrentDate$(),Len(CurrentDate$()-5))) Then Return MyInstance.MyPets
Next
End Function
</pre><br> Which can operate with a modified CheckDate() Function:<br><pre class=code>
Function CheckDate()
Local MyDate$=CurerentDate()
Birthday(GetInstance.MyPets(MyDate$))
End Function</pre><br><br>So from start to finish, all our Functions dealing with  Types:<br><br><pre class=code>
Type MyPets
	Field Name$
	Field Animal$
	Field Age%
	Field DateOfBirth$
End Type

Local MyInstance.MyPets = New MyPets
MyInstance\Name$="Rufus"
MyInstance\Animal$="Dog"
MyInstance\Age%=7
MyInstance\DateOfBirth$="3/Mar/2002"
MyInstance.MyPets = New MyPets

MyInstance\Name$="Church"
MyInstance\Animal$="Cat"
MyInstance\Age%=666
MyInstance\DateOfBirth$="6/Jun/1343"
Global FavouritePet.MyPets = MyInstance.MyPets

MyInstance.MyPets = New MyPets
MyInstance\Name$="Nemo"
MyInstance\Animal$="Fish"
MyInstance\Age%=1
MyInstance\DateOfBirth$="16/Aug/2008"

Function Birthday(BirthdayInstance.MyPets)
	BirthdayInstance\Age%=((BirthdayInstance\Age%)+1)
	Print "Happy Birthday! "+Str(BirthdayInstance\Age%)+" Today!"
	If BirthdayInstace.MyPets = FavouritePet.MyPets Then Print "Have Some Cake!"
End Function

Function GetInstance.MyPets(Date$)
	Local MyInstance.MyPets
	For MyInstance.MyPets = Each MyPets
	((Left$(MyInstance\DateOfBirth$,Len(MyInstance\DateOfBirth$)-5))=(Left$(CurrentDate$(),Len(CurrentDate$()-5)))) Then Return MyInstance.MyPets
Next
End Function

Function CheckDate()
	Local MyDate$=CurerentDate()
	Birthday(GetInstance.MyPets(MyDate$))
End Function</pre> <br><br></td></tr></table><br>
<a name="998206"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Valgar</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you very much!<br>Now i am carefully studying what you just wrote.<br>I knew the problem was with types and how i was using them....it seems that i lacked the basics.<br>I really appreciate your help,hope someday i could be of help somehow :) <br><br></td></tr></table><br>
<a name="998238"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Valgar</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have made some modification in the code to work locally.<br>Apparently now i am able to use the single player i create outside the main loop but the enemies now act very wierd,with increased speed and movements that i havent made.<br>It seems the variables that store the positions are all mixing things up.<br>The code now is without images and can be run if "copy&amp;paste":<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;simple shooter demo

;initialising the graphic display
Graphics 800,600	;for now its windowed for test purposes
SetBuffer BackBuffer()	;this tell that all the drawing is done in the backbuffer for double buffering purposes
HidePointer
;now we create types
Type player
	Field posx,posy,power,hitpoints,bombtype,bombpower
End Type

Type enemy
	Field posx,posy,image,energy,power,motion,weapon,speed,lifespan,name
	;adding some calculating fields
	Field bx#,by#,ba#,bs#,px#,py#,pa#,ex#,ey#,ps#,an#,p#,e#,direzione,borderdx,bordersx
End Type

;now we create some functions

Function createplayer(xpos,ypos)
	Local player.player=New player
	player\posx=xpos
	player\posy=ypos
	player\power=1	;starting power
	player\hitpoints=10	;starting hit points
End Function

Function create_enemy(energia,moto,posizx,posizy,power,speed)
	Local enemy.enemy = New enemy
	Local player.player
	enemy\energy=energia	;hit points of enemies
	enemy\lifespan=100	;the lifespan if untouched,so it doesnt stay on screen forever
	enemy\motion=moto	;the type of motion enemy use
	enemy\posx=posizx	;the starting position
	enemy\posy=posizy	;the starting position
	enemy\power=power	;the power
	enemy\speed=speed	;the enemy speed
	;adding the 'homing move' code here to see if its better to have it type-related...
	For player.player=Each player
		enemy\px=player\posx					;player position
		enemy\py=player\posy					;player position
	Next
	enemy\pa#=Rand(-180,180)		;player angle
	enemy\ex=enemy\posx				;enemy starting position
	enemy\ey=enemy\posy				;enemy starting position
	enemy\ps#=0.1				;precision approximation of the player position
	enemy\bs=speed		;enemy speed....try changin this in the function call
;calculate routine
	enemy\an#=ATan2(enemy\ey-enemy\py,enemy\ex-enemy\px)		;used to calculate from 'coordinate system relative' to 'player relative'
	enemy\p#=-enemy\pa-enemy\an						
	enemy\e#=ASin((Sin(enemy\p)/enemy\bs*enemy\ps))		;    the main formula....
;get enemy parameters from stuff
	enemy\ba#=-enemy\an+180+enemy\e			;enemy angle: calculation return to system relative coords
	enemy\bx#=enemy\ex					;enemy coordinates
	enemy\by#=enemy\ey					;enemy coordinates
	enemy\direzione=enemy\speed	;this is just a variable to "hold" a direction on the x axis for some enemy movement
	enemy\borderdx=posizx+100	;this is the right border of some movement limit
	enemy\bordersx=posizx-100	;this is the left border of some movement limit
End Function

Function displayPlayer()
	Local player.player
	For player.player=Each player
	player\posx=MouseX()
	player\posy=MouseY()
	Oval player\posx,player\posy,20,20,True
	Next
End Function

Function displayEnemy()
	Local enemy.enemy
	For enemy.enemy=Each enemy
		
		Select enemy\motion	;we choose different coordinates based on the type of motion we choose
			Case 1 
				straight()
			Case 2
				homing()
			Case 3
				zigzag()
		End Select
		Rect enemy\posx,enemy\posy,20,20,True
		enemy\lifespan=enemy\lifespan-1	;we decrease the lifespan by 1 unit
		If enemy\lifespan=&lt;0 Then 
			Delete enemy	;when lifespan reach 0 its deleted
		ElseIf enemy\energy =&lt;0 Then 
			Delete enemy	;when hit points reach 0 its deleted
		EndIf
		
	Next
End Function

Function homing()
	Local enemy.enemy
	For enemy.enemy=Each enemy
		enemy\bx=enemy\bx+Cos(enemy\ba)*enemy\bs;*.1		;update enemy according to calculated angle
		enemy\by=enemy\by-Sin(enemy\ba)*enemy\bs;*.1
		enemy\posx=enemy\bx	;x coord updated
		enemy\posy=enemy\by	;y coord updated
	Next
End Function

Function straight()
	Local enemy.enemy
	For enemy.enemy=Each enemy
		enemy\posy=enemy\posy+enemy\speed	;we update just the y coordinates of enemies
	Next
End Function

Function zigzag()
	Local enemy.enemy
	For enemy.enemy=Each enemy
		enemy\posx=enemy\posx+enemy\direzione	;this initiate the movement on the x axis
		enemy\posy=enemy\posy+enemy\speed/3	;this initiate the movement on the y axis half the speed of the one on the x axis
		If enemy\posx=&gt;enemy\borderdx	;if the enemy reach its right border limit
			enemy\posx=enemy\posx-enemy\speed	;this tell the enemy to start to move to the left
			enemy\direzione=-enemy\speed	;important! this tell the enemy that its direction is the opposite now
		EndIf
		If enemy\posx=&lt;enemy\bordersx	;if the enemy reach its left border limit
			enemy\posx=enemy\posx+enemy\speed	;this tell the enemy to start move to the right
			enemy\direzione=+enemy\speed	;important! this tell the enemy that its direction is the opposite now
		EndIf
	Next
End Function

createplayer(100,100)	;must be out of the main loop otherwise its called multiple times per second automatically
;M A I N 
While Not KeyHit(1)
	Cls
	If KeyHit(2) Then create_enemy(10,1,300,300,1,4)
	If KeyHit(3) Then create_enemy(10,2,200,200,1,8)
	If KeyHit(4) Then create_enemy(10,3,100,100,1,6)
	displayPlayer()
	displayEnemy()
	Flip(1)
Wend
End
;END M A I N
</textarea><br>Maybe i am iterating trough the instances in the wrong way? <br><br></td></tr></table><br>
<a name="998290"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Valgar</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am sure the error lies in the way i iterate trough the instances in the 3 movement functions....and i think the error is cause i use a "for enemy=each enemy" loop instead of sorting the type based on the movement "flag"....<br>but even after carefully studying all the examples (i admit now i know more than before) i cant figure out how to do it. <br><br></td></tr></table><br>
<a name="998510"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Valgar</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Can't make it working.<br>After various trying it seems i can't get the grasp of it.<br>What give me the problem is that i have a main function that make enemies ( a type called "enemy")and one that display all enemies("displayenemy()");inside this function i call 3 others functions based on a type field ("enemy\motion")....<br>If the field is 1 i call the "straight" function<br>If its 2 i call the "homing" one<br>If its 3 i call the "zigzag" one<br>Those 3 functions deals with the enemy movement,so i dont have to type all the movement code everytime i call the displayenemy() (not that it matters...cause i call that just once! but its just to understand how to use functions inside functions that deals with types...).<br>If i type the code inside the main display function everything works fine,but if i use separate functions everything get jerky with all sort of funny results(like enemies stopping or going twice the speed)!<br>I am sure the error lies in the 3 sub functions....and how i try to access a particular type instance..but i definitely cant understand why,cause the code i write kind of make sense.<br>The main function is:<br><pre class=code>
Function displayEnemy()
	Local enemy.enemy
	For enemy.enemy=Each enemy
		Select enemy\motion	;we choose different coordinates based on the type of motion we choose
			Case 1 
				Color 255,0,0
				;enemy\posy=enemy\posy+enemy\speed	;we update just the y coordinates of enemies
				straight()
			Case 2
				Color 255,255,0
				;enemy\bx=enemy\bx+Cos(enemy\ba)*enemy\bs;*.1		;update enemy according to calculated angle
				;enemy\by=enemy\by-Sin(enemy\ba)*enemy\bs;*.1
				;enemy\posx=enemy\bx	;x coord updated
				;enemy\posy=enemy\by	;y coord updated
				homing()
			Case 3
				Color 0,255,255
				;enemy\posx=enemy\posx+enemy\direzione	;this initiate the movement on the x axis
				;enemy\posy=enemy\posy+enemy\speed/3	;this initiate the movement on the y axis half the speed of the one on the x axis
				;If enemy\posx=&gt;enemy\borderdx	;if the enemy reach its right border limit
				;	enemy\posx=enemy\posx-enemy\speed	;this tell the enemy to start to move to the left
				;	enemy\direzione=-enemy\speed	;important! this tell the enemy that its direction is the opposite now
				;EndIf
				;If enemy\posx=&lt;enemy\bordersx	;if the enemy reach its left border limit
				;	enemy\posx=enemy\posx+enemy\speed	;this tell the enemy to start move to the right
				;	enemy\direzione=+enemy\speed	;important! this tell the enemy that its direction is the opposite now
				;EndIf
				zigzag()
		End Select
		Rect enemy\posx,enemy\posy,20,20,True
		enemy\lifespan=enemy\lifespan-1	;we decrease the lifespan by 1 unit
		If enemy\lifespan=&lt;0 Then Delete enemy	;when lifespan reach 0 its deleted
	Next
End Function
</pre><br>I have commented the lines of code that are part of the 3 sub functions just to show the difference.<br>Here is the 3 sub functions:<br>1)<br><pre class=code>
Function homing()
	Color 0,0,255
	Local enemy.enemy
	For enemy.enemy=Each enemy
		If enemy\motion=2 Then Exit
	Next
	enemy\bx=enemy\bx+Cos(enemy\ba)*enemy\bs;*.1		;update enemy according to calculated angle
	enemy\by=enemy\by-Sin(enemy\ba)*enemy\bs;*.1
	enemy\posx=enemy\bx	;x coord updated
	enemy\posy=enemy\by	;y coord updated
End Function
</pre><br>2)<br><pre class=code>
Function straight()
	Color 255,0,0
	Local enemy.enemy
	For enemy.enemy=Each enemy
		If enemy\motion=1 Then Exit
	Next
	enemy\posy=enemy\posy+enemy\speed	;we update just the y coordinates of enemies
End Function
</pre><br>3)<br><pre class=code>
Function zigzag()
	Color 0,255,0
	Local enemy.enemy
	For enemy.enemy=Each enemy
		If enemy\motion=3 Then Exit
	Next
		enemy\posx=enemy\posx+enemy\direzione	;this initiate the movement on the x axis
		enemy\posy=enemy\posy+enemy\speed/3	;this initiate the movement on the y axis half the speed of the one on the x axis
		If enemy\posx=&gt;enemy\borderdx	;if the enemy reach its right border limit
			enemy\posx=enemy\posx-enemy\speed	;this tell the enemy to start to move to the left
			enemy\direzione=-enemy\speed	;important! this tell the enemy that its direction is the opposite now
		EndIf
		If enemy\posx=&lt;enemy\bordersx	;if the enemy reach its left border limit
			enemy\posx=enemy\posx+enemy\speed	;this tell the enemy to start move to the right
			enemy\direzione=+enemy\speed	;important! this tell the enemy that its direction is the opposite now
		EndIf
End Function
</pre><br><br>Anyone have any idea how i can make the 3 sub functions work properly? <br><br></td></tr></table><br>
<a name="998523"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> This should work.  <br><br>You need to pass the instance of the enemy type to the movement functions so that the function knows which one you want to move.  You do not need to iterate through every instance within the function as you already know it!!<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Function displayEnemy()
	Local enemy.enemy
	For enemy.enemy=Each enemy
		Select enemy\motion	;we choose different coordinates based on the type of motion we choose
			Case 1 
				straight( enemy )
			Case 2
				homing( enemy )
			Case 3
				zigzag( enemy )
		End Select
		Rect enemy\posx,enemy\posy,20,20,True
		enemy\lifespan=enemy\lifespan-1	;we decrease the lifespan by 1 unit
		If enemy\lifespan=&lt;0 Then Delete enemy	;when lifespan reach 0 its deleted
	Next
End Function

Function homing( e.enemy )

	Color 0,0,255
	e\bx=e\bx+Cos(e\ba)*e\bs;*.1		;update enemy according to calculated angle
	e\by=e\by-Sin(e\ba)*e\bs;*.1
	e\posx=e\bx	;x coord updated
	e\posy=e\by	;y coord updated

End Function

Function straight( e.enemy )

	Color 255,0,0
	e\posy=e\posy+e\speed	;we update just the y coordinates of enemies

End Function

Function zigzag( e.enemy )

	Color 0,255,0
	e\posx=e\posx+e\direzione	;this initiate the movement on the x axis
	e\posy=e\posy+e\speed/3	;this initiate the movement on the y axis half the speed of the one on the x axis
	If e\posx=&gt;e\borderdx	;if the enemy reach its right border limit
		e\posx=e\posx-e\speed	;this tell the enemy to start to move to the left
		e\direzione=-e\speed	;important! this tell the enemy that its direction is the opposite now
	EndIf
	If e\posx=&lt;e\bordersx	;if the enemy reach its left border limit
		e\posx=e\posx+e\speed	;this tell the enemy to start move to the right
		e\direzione=+e\speed	;important! this tell the enemy that its direction is the opposite now
	EndIf

End Function
</textarea><br><br>Alternatively, you could contain everything in a single update function ..<br><br><pre class=code>
Function UpdateEnemy()

	Local e.enemy

	For e.enemy = Each enemy

		Select e\motion
	
			Case 1	;straight
			
			Color 255,0,0
				e\posy=e\posy+e\speed	;we update just the y coordinates of enemies
			
			Case 2	;homing
			
			Color 0,0,255
				e\bx=e\bx+Cos(e\ba)*e\bs;*.1		;update enemy according to calculated angle
				e\by=e\by-Sin(e\ba)*e\bs;*.1
				e\posx=e\bx	;x coord updated
				e\posy=e\by	;y coord updated
			
			Case 3	;zigzag
			
				Color 0,255,0
				e\posx=e\posx+e\direzione	;this initiate the movement on the x axis
				e\posy=e\posy+e\speed/3	;this initiate the movement on the y axis half the speed of the one on the x axis
				If e\posx=&gt;e\borderdx	;if the enemy reach its right border limit
					e\posx=e\posx-e\speed	;this tell the enemy to start to move to the left
					e\direzione=-e\speed	;important! this tell the enemy that its direction is the opposite now
				EndIf
				If e\posx=&lt;e\bordersx	;if the enemy reach its left border limit
					e\posx=e\posx+e\speed	;this tell the enemy to start move to the right
					e\direzione=+e\speed	;important! this tell the enemy that its direction is the opposite now
				EndIf

		End Select
	
		Rect e\posx,e\posy,20,20,True
		e\lifespan=e\lifespan-1	;we decrease the lifespan by 1 unit
		If e\lifespan=&lt;0 Then Delete e	;when lifespan reach 0 its deleted

	Next

End Function
</pre><br><br>I'm not sure if deleting a type instance when iterating through them is a good idea - although I guess it depends on what you are doing with them.  <br><br>Hope this helps. <br><br></td></tr></table><br>
<a name="998527"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Valgar</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Wow thanks a lot!<br>That work perfectly now.<br>I quite didn't understood the mechanics to pass an instance of a type trough a function....and honestly i still can't grasp it properly i am afraid....i have to spend more time doing simple examples.<br><br>I delete the type instance because i dont want the enemy to exist forever even if i dont destroy it(to free up resources).<br>Do you think its better to put the delete code outside the "for----next" loop? <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
