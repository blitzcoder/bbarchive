<!DOCTYPE html><html lang="en" ><head ><title >Handling Data</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Handling Data</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Handling Data</a><br><br>
<a name="1087606"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kirkkaf13</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi everyone,<br><br>What is the best way to handle data for all meshes in my level/map?<br><br>My simple level only consists of a small amount of meshes which so far I am placed by hard coding there x, y, z. I know this isn't ideal for lots of meshes/objects in games but data handling is not my strong point.<br><br>How does world editors or game engines handle the placment of meshes and can someone show me a simple example of whether its an array or another technique.<br><br>Thanks. <br><br></td></tr></table><br>
<a name="1087681"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Maybe you could look into these 'dropper' applications?<br>I found a thread about this here: <a href="http://blitzbasic.com/Community/posts.php?topic=80635" target="_blank">http://blitzbasic.com/Community/posts.php?topic=80635</a><br>Gile[s] is mentioned, which is now freeware:<br><a href="http://www.frecle.net/index.php?show=giles.download" target="_blank">http://www.frecle.net/index.php?show=giles.download</a><br>There is a blitz3d sample further down the page, under 'extras'. <br><br></td></tr></table><br>
<a name="1087701"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kirkkaf13</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi thanks for your post however I don't think my original question was clear enough. I am aware of Giles and programs similar I would like to know how Giles and other world editors handle the data.<br><br>So how does it store the data of the meshes coordinates on screen and other information it may need? What technique is used and can someone show a simple example? <br><br></td></tr></table><br>
<a name="1087715"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I see. Well, positional data, such as location, orientation and scale could be stored into the Entity itself. Accessible via EntityX, EntityY, EntityZ etc. <br>Having said that, a user type should be a good way to store information.<br>A Type could be used when you need to store a certain set of structured data (struct) for each object, and the number of these objects is not predetermined.<br>Here is an example of a dropper:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;type should hold all objects
Type TObject
	Field x#,y#,z#
	Field name$
	Field red%,green%,blue%
	Field kind%
	Field entity%
End Type

;set graphics mode	
Graphics3D 800, 600, 0, 2
SetBuffer BackBuffer()

;create camera
cam = CreateCamera()
PositionEntity cam, 0, 5, -15

;create floor
plane = CreatePlane()
EntityPickMode plane, 2

;just a counter, for naming purposes
counter% = 0

;create two meshes that will be used with CopyEntity()
originalCube = CreateCube()
HideEntity originalCube

originalCone = CreateCone()
HideEntity originalCone

;main loop
Repeat

	;if mouse is clicked
	If MouseHit(1) Then
		;pick a point under the cursor
		CameraPick cam, MouseX(), MouseY()
		;if user clicked on the floor
		If PickedEntity()
			;create new TObject instance
			obj.TObject = New TObject
			;fill in some fields
			obj\x = PickedX() ;position
			obj\y = PickedY()+1
			obj\z = PickedZ()
			obj\name$ = "Cube" + counter ;name
			counter = counter + 1
			obj\red = Rand(255) ;color
			obj\green = Rand(255)
			obj\blue = Rand(255)
			
			obj\kind = Rand(1, 2) ;select a random number, used for selecting shape (cube/cone)
						
			If obj\kind = 1 Then cube = CopyEntity(originalCube) ;respond to this number
			If obj\kind = 2 Then cube = CopyEntity(originalCone)
			
			PositionEntity cube, obj\x, obj\y, obj\z ;position new entity at selected position
			EntityColor cube, obj\red, obj\green, obj\blue ;give it selected color

			obj\entity = cube ;keep a reference to the entity inside the TObject instace
		End If		
	End If
	
	;S = save
	If KeyHit(31)
		;write a new file
		ff = WriteFile("level.dat")
		;loop through all TObjects
		For obj.TObject = Each TObject
			;write current TObjects data
			WriteLine ff, obj\name$
			WriteFloat ff, obj\x
			WriteFloat ff, obj\y
			WriteFloat ff, obj\z
			WriteInt ff, obj\red
			WriteInt ff, obj\green
			WriteInt ff, obj\blue
			WriteInt ff, obj\kind
		Next ;repeat for all other TObjects
		;close file
		CloseFile ff
	End If
	
	;L = load
	If KeyHit(38)
		;clear level first
		For obj.TObject = Each TObject
			FreeEntity obj\entity ;free entity inside TObject
		Next
		Delete Each TObject ;remove all TObjects

		;if file exists!
		If FileType("level.dat") = 1		
			;open it up
			ff = ReadFile("level.dat")
			;repeat the following until "End Of File" (eof)
			While Not(Eof(ff))
				;create a new object
				obj = New TObject
				;read object properties directly from file
				obj\name$ = ReadLine(ff) ;name
				obj\x = ReadFloat(ff) ;position
				obj\y = ReadFloat(ff)
				obj\z = ReadFloat(ff)
				obj\red = ReadInt(ff) ;color
				obj\green = ReadInt(ff)
				obj\blue = ReadInt(ff)
				obj\kind = ReadInt(ff) ;number to select shape
				
				;use this data to create a new entity
				If obj\kind = 1 Then cube = CopyEntity(originalCube)
				If obj\kind = 2 Then cube = CopyEntity(originalCone)
				;use this data to place and color the new entity
				PositionEntity cube, obj\x, obj\y, obj\z
				EntityColor cube, obj\red, obj\green, obj\blue
				;keep a reference to this new entity inside the TObject
				obj\entity = cube
			Wend
			;close file
			CloseFile ff
		End If
	End If

	;N = new
	If KeyHit(49)
		;clear level first
		For obj.TObject = Each TObject
			FreeEntity obj\entity ;free entity inside TObject
		Next
		Delete Each TObject ;delete all TObjects
	End If

	;render frame	
	RenderWorld
	
	;show help text
	Text 0, 0, "Click mouse to place object"
	Text 0, 20, "S=Save L=Load N=New"
	Flip
	
Until KeyHit(1) ;ESC=exit

End</textarea><br>You could also use an array, a Blitz array or a Bank, but I think it is more of a hassle to do so. <br><br></td></tr></table><br>
<a name="1087723"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Do you mean how that data is saved to disk?<br><br>If so, other than "it's written to a file", there is no standard way to store it. You have several options; the easiest is probably to have all the actual media (textures, images, meshes and whatnot) in separate files, as it would have been before (arranged however you like), and then store your level as a file containing references to these and instructions for setting it up. You'd then pass the level filename to a "level builder" function, which opens the file, reads the names of media to load, and positions them according to the data it finds in the level file associated with that object.<br><br>There are several possible ways to store data in a file. You could just dump the data in a very linear way (write the name of each mesh and dump the floats for its position and rotation), but that's really inflexible and will also make it very hard to spot errors. Several formats are listed here: <a href="http://en.wikipedia.org/wiki/Comparison_of_data_serialization_formats" target="_blank">http://en.wikipedia.org/wiki/Comparison_of_data_serialization_formats</a><br><br>The section on human-readable formats has some examples that might give you some ideas, but you'll have to either write, or find, parsers for the different formats.<br><br>In my opinion, the absolute simplest way to store data is with an INI file ( <a href="http://en.wikipedia.org/wiki/INI_file" target="_blank">http://en.wikipedia.org/wiki/INI_file</a> ). It doesn't let you nest data blocks, but it's unbelievably simple, having only three rules (one of which is comments!). Then you can do something like this (hypothetical example):<br><br><pre class=code>; Blah blah blah
[LevelMain]
baseMesh = \Meshes\Areas\Level5.b3d
gravity = 0.98
enemyCount = 2
enemies = Dave, Mitchell

;Just faces forwards
[Dave]
mesh = \Meshes\Enemies\Dave.b3d
texture = \Texs\Enemies\Dave.png
position = 10, 5, 10.2

; Doesn't have a texture yet
[Mitchell]
mesh = \Meshes\Enemies\Mitchell.b3d
position = 5, 6, 22.1
rotation = 0, 45, 0
</pre><br><br>The data for each "thing" is put into a named block, and each piece of data is associated with a named key. This makes it easy to search, and also really easy to see what's going on if you choose to edit or examine the file by hand. The loose structure also means you can potentially make up and add new sections or properties, or leave them out, without affecting the structure of the whole data file (e.g. Dave in the example has no rotation, because it faces forwards, while Mitchell has no texture yet so doesn't bother to store it).<br><br>You can parse INI files like the one above with the following code snippet:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Function ReadINIValue$(fHandle,key$,section$="",sepChr$="=")		;Get a value from a standard INI filestream, by key and optionally section
	Local fLine$,value$,cPtr,inQuote;,esc
	Local filePosTemp=FilePos(fHandle)
	
	section=Lower(section)
	key=Lower(key)
	
	SeekFile fHandle,0
	If section&lt;&gt;""		;Move pointer to start of section
		While Not Eof(fHandle)
			fLine=Trim(ReadLine(fHandle))
			If Left(fLine,1)="["
				fLine=Trim(Mid(fLine,2))
			EndIf
			If Lower(Left(fLine,Len(section)))=section Then Exit
		Wend
	EndIf
	
	While Not Eof(fHandle)		;Find the key
		fLine=Trim(ReadLine(fHandle))
		If Lower(Left(fLine,Len(key)))=key		;Read value from key
			fLine=Trim(Mid(fLine,Len(key)+1))
			If Left(fLine,1)=sepChr
				fLine=Trim(Mid(fLine,2))
				While cPtr&lt;Len(fLine)
					cPtr=cPtr+1
					If Mid(fLine,cPtr,1)=Chr(34); And esc=False		;" - start of a string
						inQuote=Not(inQuote)
					;ElseIf Mid(fLine,cPtr,1)="\" And esc=False	;\ - escape character
					;	esc=True
					ElseIf Mid(fLine,cPtr,1)=";" And inQuote=False; And esc=False
						Exit
					Else
						value=value+Mid(fLine,cPtr,1)
					;	esc=False
					EndIf
				Wend
			EndIf
			Exit
		ElseIf section&lt;&gt;"" And Left(fLine,1)="["	;Start of next section, stop searching and return ""
			Exit
		EndIf
	Wend
	
	SeekFile fHandle,filePosTemp
	Return Trim(value)
End Function

Function StrToVec$(s$,vfield,sep$=",")	;Extract a vector field from a string
	Local i
	
	For i=1 To vfield
		s=Trim(Mid(s,Instr(s,sep)+1))
	Next
	
	If Instr(s,sep) Then Return Trim(Left(s,Instr(s,sep)-1)):Else Return Trim(s)
End Function
</textarea><br>I'm sure there are much better (and faster!) functions for this in the archives, I just cranked those out quickly. The ReadINIValue function accepts a file handle (obviously), property name and optional section (if you don't include the section it just searches from the top for the first matching key, ignoring sections); the StrToVec function grabs a substring from a comma-separated list.<br><br>So then you could do something like...<br><pre class=code>levelFileName$ = ReadINIValue (level, "baseMesh", "LevelMain")
If levelFileName &lt;&gt; "" Then LevelMesh = LoadMesh(levelFileName)
noEnemies = Int(ReadINIValue (level, "enemyCount", LevelMain"))
allEnemies$ = ReadINIValue(level, "enemies", "LevelMain")

For e = 0 to noEnemies - 1
    enemy.Enemy = New Enemy
    enemy\Name = StrToVec(allEnemies, e)
    enemy\Mesh = LoadAnimMesh(ReadINIValue(level, "mesh", enemy\Name))
    positionStr$ = ReadINIValue(level, "position", enemy\Name)
    If positionStr &lt;&gt; "" Then PositionEntity enemy\Mesh, StrToVec(positionStr, 0), StrToVec(positionStr, 1), StrToVec(positionStr, 2)
    ;DO the same for rotation... you get the idea
Next

;etc.
</pre><br><br>...and you'd be setting up a level based on data read from a file. <br><br></td></tr></table><br>
<a name="1087842"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Charrua</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> hi<br>i developed a wordl assembler and use a file format not so good, but not so bad. Probably you can watch the Loader and see if it work for you. I changed the file format recently to a completely new one but haven't updated already. Now i'm using the Chunk's method of the .b3d file formtat itself<br><br>b3d file specs:<br><a href="http://blitzbasic.com/sdkspecs/sdkspecs/b3dfile_specs.txt" target="_blank">http://blitzbasic.com/sdkspecs/sdkspecs/b3dfile_specs.txt</a><br>b3d sample code:<br><a href="http://blitzbasic.com/sdkspecs/sdkspecs/b3dfile_utils.zip" target="_blank">http://blitzbasic.com/sdkspecs/sdkspecs/b3dfile_utils.zip</a><br><br>AWC is distributed with many .bb files see: "awc loader.bb"<br><a href="http://blitzbasic.com/toolbox/toolbox.php?tool=247" target="_blank">http://blitzbasic.com/toolbox/toolbox.php?tool=247</a><br><br>Juan <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
