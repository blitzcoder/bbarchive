<!DOCTYPE html><html lang="en" ><head ><title >Please help: collisions</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Please help: collisions</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Please help: collisions</a><br><br>
<a name="864942"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >olo</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> i am working on a "game" but i have some problems. Here is my code so far, it isnt complete and i havnt added the text that displays the score. Dont laugh at my bad coding ;)<br><br><pre class=code>

welcome()

Function welcome()
	Graphics 1024, 768
	screen=LoadImage("ice_escape1.bmp")
	DrawImage screen, 0,0
	While Not KeyDown(1)
	
		If KeyDown(28) Then Return
		If KeyDown(57) Then Return 
		
	Wend
	
	End Function
	
	

.start
Graphics3D 1280,1024
SetBuffer BackBuffer()

SeedRnd MilliSecs()

;collisions
type_player=1
type_obstacle=2
type_sphere=3
type_ring=4

;camera
camera=CreateCamera()
PositionEntity camera, -360.132,5,692.037
RotateEntity camera, 0,180, 0
EntityType camera, type_player
EntityRadius camera, 2


CameraClsColor camera, 0,0,0

;sound
sound=LoadSound("phaser.wav")

;lights
light=CreateLight()
RotateEntity light,90,0,0


;heightmap and the terrain
maze=LoadTerrain("heightmap6.bmp")
ScaleEntity maze, 5,100,5
PositionEntity maze, -500,0,-500
tex1=LoadTexture("ice1.jpg")
ScaleTexture tex1, 50,50
EntityTexture maze, tex1

;penguins
Dim spheres(10000)
For s=0 To 100
spheres(s)=LoadMesh("penguin.3ds")
ScaleEntity spheres(s), 0.05,0.05,0.05
PositionEntity spheres(s), Rand(500,-500), 3,Rand(500,-500)
EntityColor spheres(s), 95,95,95

EntityType spheres(s), type_sphere
EntityRadius spheres(s), 1
RotateEntity spheres(s), 0,-80,0
Next

;making rings(gems)
Dim rings(1000)
For r=0 To 100
rings(r)=LoadMesh("ring1.3ds")
ScaleEntity rings(r), 0.05,0.05,0.05
PositionEntity rings(r), Rand(500,-500), 3,Rand(500,-500)
EntityColor rings(r), 255,255,26

EntityType rings(r), type_ring
EntityRadius rings(r), 0.3
Next


;destination uncone...
cone=CreateCone()
PositionEntity cone, -407, 15, -16.5
EntityColor cone, 255,0,0
ScaleEntity cone, 5,5,5
EntityType maze, type_obstacle
EntityRadius maze, 0.1

;collisions
Collisions type_player, type_obstacle,2,1

Collisions type_player,type_sphere,2,1

Collisions type_sphere,type_player,2,1

Collisions type_player, type_ring,2,1

Collisions type_ring, type_player,2,1

;the timer
starttime=MilliSecs()

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;makes game run
While Not KeyDown(1)

If KeyDown (72)=True Then velocity#=velocity#+0.02
If KeyDown (76)=True Then velocity#=velocity#-0.02
If KeyDown (77)=True Then TurnEntity camera, 0,-1,0
If KeyDown (75)=True Then TurnEntity camera, 0,1,0
MoveEntity camera, 0,0,velocity#

;pausing
If KeyDown(25) Then 
Cls
pause=LoadImage ("ice_pause.bmp")
	DrawImage pause,0,0
	Flip
	WaitKey()
	
	EndIf

	If KeyHit(57) Then PlaySound sound

;making spheres move randomly
For i=0 To 100
MoveEntity spheres(i), Rnd(-0.5,0.5), 0, Rnd(-1.2,1.2)
 
Next

;ending game
For b=0 To 100
If timeleft &lt; 0 Or crash#=1 Then 
     Cls
Graphics 1024,768  
End=LoadImage ("ice_end.bmp")
     DrawImage End,0,0
     Flip
     WaitKey() 
     If KeyDown(1) Or (57) Or (28) Then End
     If KeyHit (57) Then Goto start
     EndIf

If CountCollisions (spheres(b))
            crash=CollisionEntity (spheres(b),1)
    		HideEntity crash
		crash#=crash#+1
EndIf



If CountCollisions (rings(b))
		ring=CollisionEntity (rings(b),2)
		HideEntity ring
	score#=score#+1
EndIf
Next

UpdateWorld
RenderWorld

;the time
CurrentTime = MilliSecs() 
	timeleft= 240000 - ((CurrentTime - starttime))
	Text 800,10,"Time Remaining: "+TimeLeft /1000



Flip

Wend

End 

</pre><br><br>What i would like is that when the player(camera) touches a ring, the ring dissapears and the score goes up by one. i've made some penguins which you cant touch , if you do then you lose. The problem is that i do not know how to make the collision with the rings and the score thing. <br><br>When i tried, the collisions with the rings acted exactly the same as with the penguins. In other words, when i touched a ring, i lost...<br><br>Please help me.. i have been trying for too long and need some advice.<br><br>thanks Olo <br><br></td></tr></table><br>
<a name="864945"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Have you tried using EntityCollided ( entity,type ) to get what entity has collided? Then you can determine whether it's a ring or a penguin.<br><br><pre class=code>
If EntityCollided( player, type_ring) &gt; 0 then
   ; An entity with the specified collision type, has collided with the player. You can identify this entity by querying EntityCollided(player,type_ring)
End if
</pre> <br><br></td></tr></table><br>
<a name="864950"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >olo</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> ok thanx for posting some help but i do not understand :(<br><br>here is the game (what is want it to be):<br><br>-it is a maze<br><br>-there are penguins which you cannot touch and you also cannot touch the walls<br><br>-there are rings..when you touch one, you get one point and the ring dissapears<br><br>problems (many):<br><br>-when i touch the wall, the whole terrain dissapears<br><pre class=code>If timeleft &lt; 0 Or crash#=1 Then 
     Cls
Graphics 1024,768  
End=LoadImage ("ice_end.bmp")
     DrawImage End,0,0
     Flip
     WaitKey() 
     If KeyDown(1) Or (57) Or (28) Then End
     If KeyHit (57) Then Goto start
     EndIf

If CountCollisions (camera)
            crash=CollisionEntity (camera,1)
    		HideEntity crash
		crash#=crash#+1
EndIf
</pre><br><br>-when i touch a penguin it also dissapears and nothing else happens (using same code as above)<br><br>-the only thing i have that i want is the dissapearing rings..when i touch them<br><br>i would really apreciate it you could help me solve any of these problems i have. Thanks again and please try to help me.<br><br>Olo <br><br></td></tr></table><br>
<a name="864951"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Don't use count collisions at all :o) There is no need.<br><br>Do you store your rings in types? Or arrays? <br><br></td></tr></table><br>
<a name="864952"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >olo</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> arrays.. what would i use insted of countcollisions? <br><br></td></tr></table><br>
<a name="864955"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >olo</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> please help me, i really need some suggestions or advice...anyone<br><br>just give me some suggestions or things i could try <br><br></td></tr></table><br>
<a name="864956"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> EntityCollided(). It returns whether an entity was collided with.<br><br>It would be a bit easier to use types to store your rings.<br><br>But, instead of blitz collisions, for the rings, i'd say it would be easier to use entitydistance() to check for collisions with coins.<br><br>What i do notice is your placing your coins but only storing the entity in the array. If you use types, you can store more information about your entities alot easier. You can do this with arrays, but it's not as readable i don't think.<br><br>I'll knock something together. <br><br></td></tr></table><br>
<a name="864957"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >olo</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> ok thank you so much<br><br>i really appreciate your help ;) <br><br></td></tr></table><br>
<a name="864960"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >olo</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> i have come up with something although it is not exactly what i was looking for..<br><br>Now whenever i touch a ring it dissapears but when i touch a penguin or the walls nothing happens. The collision is there, not allowing me to go through but i dont get the 'you lose screen'. <br><br>Thanks to you, Ross C, i solved one problem, a couple to go. <br><br></td></tr></table><br>
<a name="864962"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >olo</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> i still have some probelms :(<br><br>Thanks to Ross C, i have made the disapearing rings but whenever i crash into a penguin or the wall nothing happens except that i stop due to the collision. What i was aiming for is: <br><br>-i hit a penguin<br>-there is a collision detection<br>-the 'game over' screen pops up<br><br>i had it before but when i added the rings it stopped working<br><br>here is the code that i am using..Please correct any mistakes:<br><pre class=code>
If timeleft &lt; 0 Or crash#=1 Then 
     Cls
Graphics 1024,768  
End=LoadImage ("ice_end.bmp")
     DrawImage End,0,0
     Flip
     WaitKey() 
     If KeyDown(1) Or (57) Or (28) Then End
     If KeyHit (57) Then Goto start
     EndIf

If EntityCollided (camera, type_sphere) &gt; 0 Then
            crash=CollisionEntity (camera,1)
    				crash#=crash#+1
EndIf
</pre><br><br>thanks again, Olo <br><br></td></tr></table><br>
<a name="864964"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> There is a good few things wrong here.<br><br>For instance:<br><br><pre class=code>

;ending game
For b=0 To 100
If timeleft &lt; 0 Or crash#=1 Then 
     Cls
     Graphics 1024,768  
     End=LoadImage ("ice_end.bmp")
     DrawImage End,0,0
     Flip
     WaitKey() 
     If KeyDown(1) Or (57) Or (28) Then End
     If KeyHit (57) Then Goto start
EndIf
</pre><br><br>Just won't work. "END" is a also a blitz command. You can assign things to it and use it to draw, but to avoid confusion, you should probably change this to something else. Also, try and avoid loading images whilst the game has started. Preload as much as possible.<br><br><pre class=code>
     If KeyDown(1) Or (57) Or (28) Then End
     If KeyHit (57) Then Goto start
</pre><br><br>Your program will never go back to the beginning, as the first IF statement will catch the spacebar being pressed first and end the application.<br><br>The penguins are disappearing because:<br><br><pre class=code>
If CountCollisions (spheres(b))
            crash=CollisionEntity (spheres(b),1)
    		HideEntity crash
		crash#=crash#+1
EndIf
</pre><br><br>Again, confused about the coding used :o)<br><br>Your assigning the entity that has crashed, to the "crash" variable, then hiding it, THEN setting it to 1 more and making the number a floating point number with the #.<br><br>Firstly, when you set an entity to a variable, the variable will hold a handle (presumably a memory location) of the entity. This will usually be quite a large number (eg. 1034293). So adding one to this, doesn't make much sense...<br><br>Anyway, the reason the penguin is disappearing, is because you saying:<br><br><pre class=code>
            crash=CollisionEntity (spheres(b),1)
    		HideEntity crash
</pre><br><br>Get the entity that has collided with the player. Then hide it. Also, you NEED to set your variables to GLOBAL before you run the main loop that runs the game. That way you can access them from within functions if need be, and they will always retain their values.<br><br>Again, remember the # after a variable is used to making the variable into a float. Only use this if you actually need a floating point value. Else, stick to integers.<br><br>So, i have recoded some parts, moved some parts into functions to keep the main loops cleaner and easier to understand. I have used distance checking on the rings instead of blitz collisions. Should work better. I don't have the media, but it should work fine :o)<br><br><pre class=code>
welcome()

.start
Graphics3D 1280,1024
SetBuffer BackBuffer()

SeedRnd MilliSecs()

;collisions
Const type_player=1
Const type_penguin=2
Const type_sphere=3
Const type_ring=4


;Set up camera
Global camera = CreateCamera()
PositionEntity camera, -360.132,5,692.037
RotateEntity camera, 0,180, 0
CameraClsColor camera, 0,0,0
EntityType camera, type_player
EntityRadius camera, 2


;sound
Global sound=LoadSound("phaser.wav")

;lights
Global light=CreateLight()
RotateEntity light,90,0,0


;heightmap and the terrain
Global maze=LoadTerrain("heightmap6.bmp")
ScaleEntity maze, 5,100,5
PositionEntity maze, -500,0,-500

Global tex1=LoadTexture("ice1.jpg")
ScaleTexture tex1, 50,50
EntityTexture maze, tex1

;destination uncone...
Global cone=CreateCone()
PositionEntity cone, -407, 15, -16.5
EntityColor cone, 255,0,0
ScaleEntity cone, 5,5,5
EntityType maze, type_obstacle
EntityRadius maze, 0.1

; set up the rings

Type ring
	Field entity
	Field size ; use this for distance checking
End Type

For loop = 0 To 100
	r.ring = New ring
		r\entity = LoadMesh("ring1.3ds")
		r\size = 0.3
		ScaleEntity r\entity, 0.05, 0.05, 0.05
		PositionEntity r\entity, Rand(-500,500), 3, Rand(-500,500)
		EntityColor r\entity, 255,255,26
		
		EntityType r\entity, type_ring
		EntityRadius r\entity, 0.3
Next

; Set up the penguins

Type penguin
	Field entity
End Type

For loop = 0 To 100
	p.penguin = New penguin
		p\entity = LoadMesh("penguin.3ds")

		ScaleEntity p\entity, 0.05, 0.05, 0.05
		PositionEntity p\entity, Rand(-500,500), 3, Rand(-500,500)
		EntityColor p\entity, 95,95,95
		RotateEntity p\entity,0,-80,0
		
		EntityType p\entity, type_penguin
		EntityRadius p\entity, 1
Next

Global end_image = LoadImage("ice_end.bmp")
Global pause = LoadImage ("ice_pause.bmp")


;collisions
Collisions type_player, type_obstacle,2,1

Collisions type_player,type_sphere,2,1

Collisions type_sphere,type_player,2,1

Collisions type_player, type_ring,2,1 

;Collisions type_ring, type_player,2,1 ; no need to set up this collision boths ways, since the rings don't move.

;the timer and other variables
Global starttime=MilliSecs()
Global crash = 0
Global score = 0
Global velocity# = 0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;makes game run
While Not KeyDown(1)

	If KeyDown (72)=True Then velocity#=velocity#+0.02
	If KeyDown (76)=True Then velocity#=velocity#-0.02
	If KeyDown (77)=True Then TurnEntity camera, 0,-1,0
	If KeyDown (75)=True Then TurnEntity camera, 0,1,0
	MoveEntity camera, 0,0,velocity#

	;pausing
	If KeyDown(25) Then 
		Cls
		DrawImage pause,0,0
		Flip
		WaitKey()	
	EndIf

	If KeyHit(57) Then PlaySound sound

	move_penguins()
	check_ring_collisions()
	check_penguin_collisions()
	
	If timeleft &lt; 0 Or crash = 1 Then 
		end_game()
	End If

	UpdateWorld
	RenderWorld

	;the time
	CurrentTime = MilliSecs() 
	timeleft= 240000 - ((CurrentTime - starttime))
	Text 800,10,"Time Remaining: "+TimeLeft /1000

	Flip
Wend
End

Function welcome()
	Graphics 1024, 768
	screen=LoadImage("ice_escape1.bmp")
	DrawImage screen, 0,0
	While Not KeyDown(1)
	
		If KeyDown(28) Then Return
		If KeyDown(57) Then Return 
		
	Wend
	
End Function

Function end_game()
 
    Cls
	Graphics 1024,768  
	DrawImage end_image,0,0
    Flip
	; keep repeating until one of the keys is pressed below.
    Repeat
	    WaitKey()
		If KeyHit (57) Then Goto start
	Until KeyHit(1) Or KeyHit(28)
	End
	
  
 
End Function

; move the penguins randomly.
Function move_penguins()

	For p.penguin = Each penguin
	
		MoveEntity p\entity, Rnd(-0.5,0.5), 0, Rnd(-1.2,1.2)
		
	Next
	
End Function

; Use distance checking, instead of blitz collisions. Quicker i say :o) And cleaner.
Function check_ring_collisions()

	For r.ring = Each ring
	
		If EntityDistance#(camera,r\entity) &lt; r\size Then
		
			FreeEntity r\entity
			Delete r.ring
			score = score + 1
			
		End If
		
	Next
	
End Function

Function check_penguin_collisions()

	Local temp_entity = EntityCollided(camera,type_penguin)

	If temp_entity &gt; 0 Then ; if you have touched a penguin, then
	
		crash = 1 ; set the crash variable.
		
	End If
	
End Function

</pre> <br><br></td></tr></table><br>
<a name="864966"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >olo</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> thanks so much, it works now. i really appreciate you writing so much to help me. Thanks again, Olo <br><br></td></tr></table><br>
<a name="864980"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> No worries man :o) <br><br></td></tr></table><br>
<a name="865022"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >olo</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> ------------------------------------------------------------<br><br>i have got everything how i wanted it to be and everything is great (thanks to Ross) but i would like to add one more thing to my "game". <br><br>Right now whenever i touch the wall or a 'penguin' then i automatically lose. I would like to add some "lives" for the player so that he can hit the wall severall times before he loses. <br><br>here is the code but it doesnt work :(..please correct the mistakes<br><br><pre class=code>
crash=5
If timeleft &lt; 0 Or crash=0 Then 
     Cls
Graphics 1024,768  
End=LoadImage ("ice_end.bmp")
     DrawImage End,0,0
     Flip
     WaitKey() 
     If KeyDown(1) Or (57) Or (28) Then End
     If KeyHit (57) Then Goto start
     EndIf

If EntityCollided (camera, type_sphere) &gt; 0 Then
            sphere=CollisionEntity (camera,1)    				
crash=crash-1
EndIf

If EntityCollided (camera, type_obstacle) &gt; 0 Then
		wall=CollisionEntity (camera,1)
	crash=crash-1
EndIf
</pre><br><br>and here i display the amount of lives left, so that the player can see how many times he can touch the wall or 'penguin' again:<br><pre class=code>
Text 450,10, "Lives Left: "+crash
</pre><br><br><br>So the problem is that whenever i touch the wall or penguin, the amount of lives goes down by 1. The bad thing is that when i move away from the object, my amount of lives go back up.  :( <br><br>please help...anyone <br><br></td></tr></table><br>
<a name="865040"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >olo</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> here is an example i made which is basically the same problem- I would like that when the sphere touches the cube the life goes down by one and stays that way. <br>PLEASE TRY THIS YOURSELF BY COPYING THIS CODE TO YOUR B3D<br><br>---use the left and right arrow keys to move sphere---<br><br><pre class=code>
Graphics3D 800,600

  type_cube=1
  type_sphere=2

  SetBuffer BackBuffer()

  camera=CreateCamera()
  CameraViewport camera,0,0,800,600
  PositionEntity camera,0,0,-5

  light=CreateLight()

  cube=CreateCube()
  PositionEntity cube,-5,0,5
  EntityColor cube,70,80,190
  EntityType cube,type_cube

  sphere=CreateSphere(12)
  PositionEntity sphere,5,0,5
  EntityColor sphere,170,80,90
  EntityType sphere,type_sphere

  Collisions type_sphere,type_cube,3,1

  While Not KeyHit(1)

If KeyDown(205)=True Then MoveEntity sphere, 0.5,0,0
If KeyDown(203)=True Then MoveEntity sphere, -0.5,0,0

lives=5
 	
If EntityCollided(sphere,type_cube)&gt; 0 Then
 		ball=CollisionEntity (sphere, 1) 
	lives=lives-1
EndIf

 UpdateWorld
  RenderWorld

  

  Text 335,500,"Lives Left: "+lives
  Flip

  Wend

  End
</pre> <br><br></td></tr></table><br>
<a name="865098"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >mtnhome3d</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> remove lives=5 from your loop and put where you load your camera and such. currently you are reseting lives to 5 every time the code gets to that point and then you are subtracting from that. <br><br></td></tr></table><br>
<a name="865210"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >olo</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks so much man, :D I have been trying for ages <br><br></td></tr></table><br>
<a name="865261"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >KillerX</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> You Should Replace<br><pre class=code>
For loop = 0 To 100
	p.penguin = New penguin
		p\entity = LoadMesh("penguin.3ds")

		ScaleEntity p\entity, 0.05, 0.05, 0.05
		PositionEntity p\entity, Rand(-500,500), 3, Rand(-500,500)
		EntityColor p\entity, 95,95,95
		RotateEntity p\entity,0,-80,0
		
		EntityType p\entity, type_penguin
		EntityRadius p\entity, 1
Next

</pre><br>with<br><pre class=code>

Penguin = LoadMesh("penguin.3ds")

		ScaleEntity Penguin, 0.05, 0.05, 0.05

		EntityColor Penguin, 95,95,95
		RotateEntity Penguin,0,-80,0
		
		EntityType Penguin, type_penguin
		EntityRadius Penguin, 1

For loop = 0 To 100
	p.penguin = New penguin
	
p\entity = CopyEntity(penguin)
PositionEntity p\entity, Rand(-500,500), 3, Rand(-500,500)
Next
FreeEntity Penguin
</pre><br>It will run faster. <br><br></td></tr></table><br>
<a name="865263"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >olo</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> oh thanks, my game was starting to get a bit slow..thanks again :D <br><br></td></tr></table><br>
<a name="865282"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yep sorry, bad programming on my part there! Copyentity is a dam sight faster! Good spot. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
