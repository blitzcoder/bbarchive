<!DOCTYPE html><html lang="en" ><head ><title >A Quick Question About GrabImage</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >A Quick Question About GrabImage</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >A Quick Question About GrabImage</a><br><br>
<a name="1174863"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Newbie31</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Heyo, I've just started looking at the grabimage command, and am having a little trouble. I'm trying to load 2 images one background and one foreground and when the mouse passes over an area it displays the background image. I know it can be done cause years ago I created such and effect, but I forgot exactly what I did.<br><br>This is what my current code looks like and it kind of works but it not updating correctly, I was wondering if the problem might be a graphics card issue?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

;Set 2d Graphics
Graphics 800,600,32,2
;SetBuffer BackBuffer()

;_________________________________________________
;______________Set Global Constraints_____________
;_________________________________________________

;Global Load Background Image
gfx_bg=LoadImage("_Images\bg_image_01x.jpg");Load the Background Image
gfx_fg=LoadImage("_Images\bg_image_02x.jpg");Load the Foreground Image

;_________________________________________________
;_________________________________________________
;_________________________________________________

SetBuffer ImageBuffer(gfx_bg)

SetBuffer BackBuffer()

;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;
;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp; MAIN GAME AREA &amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;
;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;
While Not KeyHit( 1 )
	
	;DRAW IMAGES
	DrawImage gfx_bg,0,0	;Draws The Background Image
	DrawImage gfx_fg,0,0	;Draws The Foreground Image
	
	;Draw Image Grab
	gfxgrab = CreateImage(300,300)
	GrabImage gfxgrab,MouseX()+10,MouseY()+10
	DrawImage gfxgrab,MouseX(),MouseY()
	
	Rect MouseX(),MouseY(),300,300,0
	
;--------------------------------------------------------
;---------------- ONSCREEN TEXT AND HUD -----------------
;--------------------------------------------------------
	
	;Display My Testing Text
	Text 0,120,"Mouse X Pos: " + MouseX() + " Mouse Y Pos: " + MouseY()
	
	
	Flip 
Wend

End 


</textarea> <br><br></td></tr></table><br>
<a name="1174890"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> [bbcode]<br>While Not KeyHit( 1 )<br>	<br>	;DRAW IMAGES<br>	DrawImage gfx_bg,0,0	;Draws The Background Image<br>	DrawImage gfx_fg,0,0	;Draws The Foreground Image<br>	<br>	;Draw Image Grab<br>	gfxgrab = CreateImage(300,300) ;**********[/bbcode]You have a creation function inside a loop, that's very dangerous. If you let your program running, it'll eventually crash due to lack of available memory. <br>Every single loop cycle you're creating a new 'gfxgrab' image, and this isn't necessary.<br><br>You see, when you create an image, you can replace its content and the image wil still be usable by Blitz. Since you want a dynamic image (one that changes its content throughout the program), you can do it with just a single image.<br><br>About the GrabImage(). According to <a href="/b3ddocs/command.php?name=GrabImage&amp;ref=2d_cat" target="_blank">its documentation</a> (something you can always consult when you're having trouble with a specific command), this function "allows you to grab a portion of the current drawing buffer".<br>Since the current drawing buffer is the BackBuffer, whenever you call GrabImage it will capture whatever's drawn there. And if you follow your program's flow...<br>[bbcode]<br>	;DRAW IMAGES<br>	DrawImage gfx_bg,0,0	;Draws The Background Image<br>	DrawImage gfx_fg,0,0	;Draws The Foreground Image<br>	<br>	;Draw Image Grab<br>	GrabImage gfxgrab,MouseX()+10,MouseY()+10[/bbcode]... you're drawing both backgrounds first, then grabbing. You're going to end up capturing the foreground (which just overwrote the background).<br><br>If you want to capture only the background image, you should change the order to:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics 800,600,32,2
SetBuffer BackBuffer()

;Load screen layer images.
gfx_bg=LoadImage("_Images\bg_image_01x.jpg")
gfx_fg=LoadImage("_Images\bg_image_02x.jpg")

;Create standard image to be used dynamically.
gfxgrab = CreateImage(300,300)

While Not KeyHit( 1 )
 	Cls ;Start with a clean BackBuffer.
	
	DrawImage gfx_bg,0,0 ;Draw background image.
	GrabImage gfxgrab,MouseX()+10,MouseY()+10 ;Capture part of the background image (current drawing buffer is the BackBuffer).

	DrawImage gfx_fg,0,0 ;Draw foreground image.
	
	;Overlay the previously captured 'background only' image and a rectangular border.
	DrawImage gfxgrab,MouseX(),MouseY()
	Rect MouseX(),MouseY(),300,300,0

	;Display My Testing Text
	Text 0,120,"Mouse X Pos: " + MouseX() + " Mouse Y Pos: " + MouseY()
	
	Flip 
Wend</textarea> <br><br></td></tr></table><br>
<a name="1176373"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Newbie31</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Heyo, me again.<br><br>I was wondering if there is a way to change the shape of the image buffer to a circle. I thought maybe could be done with alpha channels(masks) in the created image but I didn't have any luck with that. Can it be done? <br><br></td></tr></table><br>
<a name="1176467"></a>

<a name="1176469"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> All images start with a color mask of [0,0,0]. That's black.<br>If you plot any black pixels on the buffer of an image masked with that same black, they'll be transparent.<br><br>All image buffers are rectangular. If you use masking in a smart way you can make it look like you have a circular buffer.<br><br>To do this:<br><br>1) Create a 300x300 image that'll be your "dynamic image".<br><br>2) Create another 300x300 image that'll be your "stencil image". Fill it with black and then paint a white, filled circle perfectly inscribed in the image.<br><br>3) Create another 300x300 image that'll be your "dynamic masked result image".<br><br>-----<br>4) Now, per render cycle, grab a piece of the BackBuffer to your "dynamic image" as discussed in the previous posts.<br><br>5) Plot on the "dynamic masked result image" the bitwise AND comparison results between the pixel values you read from the "dynamic" and the "stencil" images.<br><br>What will happen is this: if the pixel value you read from the stencil image is black (so it's part of the background, outside of the white filled circle) it's going to make the bitwise AND result in 0, giving you a black pixel.<br>If the pixel value you read from the stencil image is white (inside the circle), it's going to make the bitwise AND result in whatever pixel color you picked from the dynamic image.<br><br>In the end, the "dynamic masked result image" will have a black background and a circular section of the dynamic image overlayed on it. <br>When you draw it with DrawImage, it'll have a circular shape because of masking. <br><br></td></tr></table><br>
<a name="1176470"></a>

<a name="1176471"></a>

<a name="1176472"></a>

<a name="1176473"></a>

<a name="1176474"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> The above is a more intuitive way, using comparisons.<br>But you can use another more mathematical way, specific for this, so it's cheaper on memory and CPU.<br><br>These two steps every loop cycle:<br><br>1) Grab your "dynamic image" from the BackBuffer like usual.<br><br>2) Process the image with the following function:<br><pre class=code>;Only admits SQUARE images, where width and height are equal.

Function CircularMask( image )
	Local width = ImageWidth(image)-1
	Local height = width
	Local halfWidth# = width/2.0
	
	LockBuffer ImageBuffer(image)
		For X = 0 To width
			For Y = 0 To height
			
				;Pythagorean theorem without the square root.
				;If the pixel is distanced from the center of the image by more 
				;than the image's "radius" (half its width or height), it's 
				;outside of what should be visible.
				If ( Abs((X-halfWidth) * (X-halfWidth)) + Abs((Y-halfWidth) * (Y-halfWidth)) ) &gt; (halfWidth*halfWidth) Then WritePixelFast X,Y,0,ImageBuffer(image)
			
			Next
		Next	
	UnlockBuffer ImageBuffer(image)
End Function</pre><br>What it does is compare the distance of each pixel against the center of the square image. <br>If it's bigger than the image's inscribed circle's "radius" (half the width or half the height), then the pixel is outside the circle and is colored black. <br><br></td></tr></table><br>
<a name="1176476"></a>

<a name="1176478"></a>

<a name="1176480"></a>

<a name="1176481"></a>

<a name="1176482"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Addi</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> You can also use this Code:<br><br><pre class=code>
;Bild runden =)
Function MaskImgRound(Pic, MX, MY, DX, DY, R = 0, G = 0, B = 0)
	
	Local CC = 255*$1000000+R*$10000+G*100+B
	Local ColorCode = 0
	
	;ImageBuffer einstellen um auf dem Bild malen zu können	
	SetBuffer ImageBuffer(Pic)	
		
	;Auf dem geladenen Bild and MX und MY (Kreismittelpunkt) ein Kreis mit DX/2 und DY/2 als Radius 
	Color R, G, B
	Oval MX-(DX/2), MY-(DY/2), DX, DY, 0
	
	;Buffer für ReadPixelFast und WritePixelFast sperren
	LockBuffer ImageBuffer(Pic)
	
	;Alle Pixel auserhalb des Kreis werden unsichtbar gemacht
	For Y=0 To ImageHeight(Pic)-1
		For X = 0 To MX
			ColorCode = ReadPixelFast(X,Y)	
			
			If ColorCode = CC Then Exit
			WritePixelFast X, Y, CC	
		Next
		
		For X = ImageWidth(Pic)-1 To MX Step -1
			ColorCode = ReadPixelFast(X,Y)	
									
			If ColorCode = CC Then Exit
			WritePixelFast X, Y, CC
		Next
	Next
	
	;Bild maskieren
	MaskImage Pic,R,G,B
	
	;Buffer freigeben und den BackBuffer setzen
	UnlockBuffer ImageBuffer(Pic)
	SetBuffer BackBuffer()
End Function</pre><br><br>Pararmeter:<br>pic = handle of the pic you want to shape<br>mx  = the x-position of the middle point of the circle<br>my  = the y-position of the middle point of the circle<br>dx   = the width of the circle<br>dy   = the height of the circle<br>r, g, b = the rgb values for MaskImage<br><br>Example:<br><a href="https://dl.dropbox.com/u/48089972/MaskImgRound.rar" target="_blank">https://dl.dropbox.com/u/48089972/MaskImgRound.rar</a><br><br>Problem:<br>If the circle goes out of the picture<br><br>Edit:<br>This function works for each picture <br><br></td></tr></table><br>
<a name="1176736"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Newbie31</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Heyo, I'm back again.<br><br>I tried that "Function CircularMask( image )", and that works but it seems to have problems with refreshing(kind of flickers between the 2 images, and there seems to be a little displacement when moving). I've had problems like this before, I always assumed it was a lack in processing power problem. <br><br>Also I haven't had much practice with drawing into image that is created in blitz, I usually just draw in paint. Could you recommend any tutorials I should look at regarding this? <br><br></td></tr></table><br>
<a name="1176940"></a>

<a name="1176957"></a>

<a name="1176958"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> You need to share your exact code so we can see if there's anything wrong. Especially because that CircularMask() function is quite fast and shouldn't be flickering.<br><br>If you want to learn about the 2D part of Blitz3D (originally a standalone product itself, called Blitz Basic), there's no better place for that than the archived BlitzCoder tutorials. It's how I learned most of what I know:<br><br>http://web.archive.org/web/20050102090428/http://www.blitzcoder.com/<br><br>Everything there should be of use to you. One of those tutorials in particular is "How to create a simple image editor in Blitz2D", and while it doesn't explain to the fullest detail how the Front\Back\ImageBuffer() functions work, it does make use of them.<br>You need to ally those articles with the habit of consulting the documentation for any doubts you have about the language. <br>If you can, prefer to use the online documentation you can find here, in the official website. It includes a few user comments about the commands and functions, and some times it's very valuable info. <br><br></td></tr></table><br>
<a name="1219433"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Newbie31</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Heyo,  <br><br>I have been going over some of my older code and I found this Imagegrab tests that the people here helped me with. I've learned a little bit more since then so I figured I'd give dynamic and stencil images a shot like kryzon suggested. It took me a while me a few tries to figure it out and get it working but I've accounted a problem, it is really slow at updating.<br><br>I was wondering if some of the guru's could have a looksy' and tell me how I could make it work faster. Thanks.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">


.restart
;Set 2d Graphics
Graphics 800,600,32,2
SetBuffer BackBuffer()

Global setlev$ = Input("Select a level [a-z]?")

;_________________________________________________
;______________Set Global Constraints_____________
;_________________________________________________
Global imX = 0
Global imY = 0


;Global Load Background Image
gfx_bg=LoadImage("_Images2\bg_image_"+setlev$+"01.png");Load the Background Image
gfx_fg=LoadImage("_Images2\bg_image_"+setlev$+"02.png");Load the Foreground Image

;Load Images for Dynamic Masking

Global gfxgrab1 = LoadImage("_Images2\Img1.png")	;Stencil

Global gfxgrab2 = CreateImage(200,200)				;Dynamic1

Global gfxgrab3 = CreateImage(200,200)				;Dynamic2

Global gfxgrab4 = CreateImage(200,200)				;Dynamic Masked Result



;Create Array To hold pixel color data
Dim pixstencil$(ImageWidth(gfxgrab1),ImageHeight(gfxgrab1))
;For i=0 To ImageWidth(gfxgrab1)
;	For j=0 To ImageHeight(gfxgrab1)
;		pixstencil$(i,j) = ReadPixel(i,j,ImageBuffer(gfxgrab1))
;	Next
;Next

Dim pixdynamic1$(ImageWidth(gfxgrab2),ImageHeight(gfxgrab2))
;For i=0 To ImageWidth(gfxgrab2)
;	For j=0 To ImageHeight(gfxgrab2)
;		pixdynamic1$(i,j) = ReadPixel(i,j,ImageBuffer(gfxgrab2))
;	Next
;Next

Dim pixdynamic2$(ImageWidth(gfxgrab3),ImageHeight(gfxgrab3))
;For i=0 To ImageWidth(gfxgrab3)
;	For j=0 To ImageHeight(gfxgrab3)
;		pixdynamic2$(i,j) = ReadPixel(i,j,ImageBuffer(gfxgrab3))
;	Next
;Next

;_________________________________________________
;___________________ Set Image Masks _____________
;_________________________________________________

;MaskImage gfxgrab1 ,255,255,255
;MaskImage gfxgrab2 ,255,255,255
;MaskImage gfxgrab3 ,255,255,255


;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;
;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp; MAIN GAME AREA &amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;
;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;

Repeat
	Cls;Start with a clean BackBuffer.
	If setlev$ = "exit" Or setlev$ = "Exit" Or setlev$ = "EXIT" Or setlev$ = "close" Or setlev$ = "Close" Or setlev$ = "CLOSE"
		End
	End If
	
	If KeyHit(1)
		End
	EndIf
	
	If KeyHit( 19 ) = True
		;End
		Cls
		Goto restart
	EndIf	
	
	
	;DRAW IMAGES
	DrawImage gfx_bg,imX,imY	;Draws The Background Image
	GrabImage gfxgrab2,MouseX()-100,MouseY()-100 ;Capture part of the background image (current drawing buffer is the BackBuffer).
	DrawImage gfx_fg,imX,imY	;Draws The Foreground Image
	GrabImage gfxgrab3,MouseX()-100,MouseY()-100 ;Capture part of the background image (current drawing buffer is the BackBuffer).
	
	;CircularMask( gfxgrab2 )
	DynamicMask1( gfxgrab1,gfxgrab2,gfxgrab3,gfxgrab4 )
	
	;Draw Combined Image
	;DrawImage gfxgrab2,MouseX()-100,MouseY()-100
	DrawImage gfxgrab4,MouseX()-100,MouseY()-100
	
	Rect MouseX()-100,MouseY()-100,200,200,0
	Line MouseX()-50,MouseY(),MouseX()+50,MouseY()
	Line MouseX(),MouseY()-50,MouseX(),MouseY()+50
	
;-----------------------------------------------------------------
;-------------- SET KEYBOARD AND MOUSE FUNCTIONS -----------------
;-----------------------------------------------------------------
	
	If KeyDown( 205 ) Or MouseX() &gt; 650
		If imX&gt;= -ImageWidth(gfx_bg) + GraphicsWidth() + 5
			imX=imX-10 ;Right Arrow1
		EndIf
	ElseIf KeyDown( 203 ) Or MouseX() &lt; 150
		If imX&lt;= -1
			imX=imX+10 ;Left Arrow
		EndIf
	ElseIf KeyDown( 208 ) Or MouseY() &gt; 450
		If imY&gt;= -ImageHeight(gfx_bg) + GraphicsHeight() + 5
			imY=imY-10 ;Up Arrow
		EndIf
	ElseIf KeyDown( 200 ) Or MouseY() &lt; 150
		If imY&lt;= -1 
			imY=imY+10 ;Down Arrow
		EndIf
	EndIf
	
;--------------------------------------------------------
;---------------- ONSCREEN TEXT AND HUD -----------------
;--------------------------------------------------------
	
	Color 0,0,0	
	;Display My Testing Text
	Text 0,120,"Mouse X Pos: " + MouseX() + " Mouse Y Pos: " + MouseY()
	
	Flip 
Until KeyDown(1)=1 ;End program when ESC is pressed
;Wend

End 


;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;
;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp; END OF GAME AREA &amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;
;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;&amp;

;------------- CREATE GAME FUNCTIONS -----------------


Function CircularMask( image )
	Local width = ImageWidth(image)-1
	Local height = width
	Local halfWidth# = width/2.0
	
	LockBuffer ImageBuffer(image)
	
	For X = 0 To width
		For Y = 0 To height
			If ( Abs((X-halfWidth) * (X-halfWidth)) + Abs((Y-halfWidth) * (Y-halfWidth)) ) &gt; (halfWidth*halfWidth) Then WritePixelFast x,y,0,ImageBuffer(image)
		Next
	Next	
	UnlockBuffer ImageBuffer(image)
	
End Function


Function DynamicMask1( stencil,dynamic1,dynamic2,result )
	
	Local width = ImageWidth(stencil)
	Local height = ImageHeight(stencil)
	
	For i=0 To ImageWidth(stencil)
		For j=0 To ImageHeight(stencil)
			pixstencil$(i,j) = ReadPixel(i,j,ImageBuffer(stencil))
			pixdynamic1$(i,j) = ReadPixel(i,j,ImageBuffer(dynamic1))
			pixdynamic2$(i,j) = ReadPixel(i,j,ImageBuffer(dynamic2))
		Next
	Next
	
	For X = 0 To width
		For Y = 0 To height
			If Hex$(pixstencil$(x,y)) = "FFFFFFFF"	;WHITE
				WritePixel x,y,pixdynamic1$(x,y),ImageBuffer(result)
			ElseIf Hex$(pixstencil$(x,y)) = "FF000000"	;BLACK
				WritePixel x,y,pixdynamic2$(x,y),ImageBuffer(result)
			EndIf
		Next
	Next	
	
End Function



</textarea><br><br>Complete files(156kbytes) : <a href="http://www.sendspace.com/file/8azgxh" target="_blank">http://www.sendspace.com/file/8azgxh</a> <br><br></td></tr></table><br>
<a name="1221584"></a>

<a name="1221586"></a>

<a name="1221588"></a>

<a name="1221589"></a>

<a name="1221590"></a>

<a name="1221591"></a>

<a name="1221592"></a>

<a name="1221593"></a>

<a name="1221594"></a>

<a name="1221595"></a>

<a name="1221596"></a>

<a name="1221597"></a>

<a name="1221641"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >fox95871</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm not a guru, but I think in general the 2d commands are considered to be relatively slow. It's just one of the downsides of a language that's otherwise very good. But considering the new Direct x update, maybe a fix for this might be down the road too? I heard Blitz3d's coming back from the dead, afterall. Anyway, try doing the same thing with fullscreen animated image planes. I've found you can work around a lot of the 2d slowness with simulated 2d. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
