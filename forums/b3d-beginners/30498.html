<!DOCTYPE html><html lang="en" ><head ><title >uhh how do I do ths</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >uhh how do I do ths</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >uhh how do I do ths</a><br><br>
<a name="325019"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Agamer</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>
Function buy()
Print"what do you want to buy"
Print "you have " +money#+ "money"
item$=Input()
If money#=money#-item$+"#" =&gt;0
	item$=item$+1
	money#=money#-item$+"#"
Else 
	Print"you do not have anoth money!
EndIf 
End Function
</pre><br><br>so the problem is item$ will be eqaul to a global with the amount it costs stored in it! but It won't let em do it is there a way to get around it if you understand <br><br></td></tr></table><br>
<a name="325029"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Agamer</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> ohh please help <br><br></td></tr></table><br>
<a name="325047"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RetroBooster</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> 1. this code is gibberish it's doing stuff that's not even remotely possible...<br>2. You need to work on your written/verbal skills agamer, there's no way we can help you if you post descriptions that could be written by an 11 year old. <br><br></td></tr></table><br>
<a name="325049"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Agamer</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am only 12 so haa<br><br>yes it is possible i know what is possible but this is only one function.I am only goign about it the wrong way<br>I would apreciate if you would not insult my understanding of what it can do maybe you could say what it can't do. Uhh<br><br>I know I could say something like this <br><pre class=code>
if item$ = chair
    money#=money#-chair#
endif
</pre>but I have loads of items so I would rather do it deffriently <br><br></td></tr></table><br>
<a name="325058"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Agamer</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> so would some one please like to give some constructive advice <br><br></td></tr></table><br>
<a name="325060"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CodeD</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I didn't check this but, I think that's a better way than you're trying. A better way than this would be to use types and read from a data file but, this is just off the top of my head.<br><br>Function buy()<br><br>Print "what do you want to buy"<br>print "you have" + money# + "money"<br><br>item$ = input()<br>checkcost(item$)<br><br>End Function<br><br>Function Checkcost(item$)<br>if item$ = "sword" then cost = "100"<br>if money# =&gt; cost<br>item$ = item$ + 1<br>mone# = money# - cost<br>Else<br>Print "get outta here you bum!"<br>end if<br>End Function <br><br></td></tr></table><br>
<a name="325061"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Agamer</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> umm could do it this way or I could do iut the other wayb I listed so umm both the same it is just I have about 40 items so it's a very long way round but thanks or the help <br><br></td></tr></table><br>
<a name="325068"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >soja</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> You could have all your items in custom types, perhaps defined like this:<br><pre class=code>
Type Item
	Field ID%	; Unique identifier for this item
	Field Name$	; Name of item
	Field Desc$	; Description of item
	Field Cost#	; Cost of item $
End Type
</pre><br>You could then have Data statements that hold all your items.  (Alternatively, you could use an external config file too.)  Something like this might work:<br><pre class=code>
.Items
Data 3 ; Number of total items to be initialized
Data 1, "Helmet", "Ultra-sturdy dwarven helm", 1.99
Data 2, "Sword", "Saber of Blitz Programming +2", 5.50
Data 3, "Apple", "For when yo uget hungry", .25
</pre><br>Then you could use a For..Next loop and all the Data commands (look them up under Read and Restore) to read in all the data, something like this:<br><pre class=code>
; (psuedocode below)
; restore .items
; for 0 to (Read int)
;   create new Item
;   item\id = (read int)
;   item\name = (read string)
;   item\desc = (read string)
;   item\cost = (read float)
; next
</pre><br>After that you'll have a whole list of items that you didn't have to enter in one by one.  Now when the user enters an item by name, just search down the Item type list until it finds the item's name, and then you can get the cost of it.  (If you've got a ton of items, you can make the user enter the ID, and have an array of types indexed by that ID to make it so you don't have to search down the list, but you probably don't have to worry about that right now.)<br><br>Follow?  Keep asking questions if this doesn't make sense. <br><br></td></tr></table><br>
<a name="325075"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Agamer</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> ohhh this makes more sense now thanks is it better than this system i just started<br><br><pre class=code>
Function buy()
Print"what do you want to buy"
Print "you have " +money#+ "money"
item$=Input()
If Lower(item$)= "Desk"
	If money#=&gt;desk#
		money#=money#-desk#
	EndIf
If Lower(item$)= "computer"
	If money#=&gt;computer#
		money#=money#-computer#
	EndIf	
If Lower(item$)= "dvdplayer"
	If money#=&gt;dvdplayer#
		money#=money#-dvdplayer#
	EndIf	
If Lower(item$)= "dvd"
	If money#=&gt;dvd#
		money#=money#-dvd#
	EndIf	
If Lower(item$)= "cddrive"
	If money#=&gt;cddrive#
		money#=money#-cddrive#
	EndIf	
If Lower(item$)= "cdrewriter"
	If money#=&gt;cdrewriter#
		money#=money#-cdrewriter#
	EndIf	
If Lower(item$)= "scanner"
	If money#=&gt;scanner#
		money#=money#-scanner#
	EndIf	
If Lower(item$)= "Mic"
	If money#=&gt;Mic#
		money#=money#-Mic#
	EndIf	
If Lower(item$)= "printer"
	If money#=&gt;printer#
		money#=money#-printer#
	EndIf
If Lower(item$)= "netcard"
	If money#=&gt;netcard#
		money#=money#-netcard#
	EndIf
If Lower(item$)= "nethub"
	If money#=&gt;nethub#
		money#=money#-nethub#
	EndIf
If Lower(item$)= "netcable"
	If money#=&gt;netcable#
		money#=money#-netcable#
	EndIf
If Lower(item$)= "modem"
	If money#=&gt;modem#
		money#=money#-modem#
	EndIf
If Lower(item$)= "adslmodem"
	If money#=&gt;adslmodem#
		money#=money#-adslmodem#
	EndIf
If Lower(item$)= "vr"
	If money#=&gt;vr#
		money#=money#-vr#
	EndIf
If Lower(item$)= "cad"
	If money#=&gt;cad#
		money#=money#-cad#
	EndIf
If Lower(item$)= "buidings"
	If money#=&gt;buidings#
		money#=money#-buidings#
	EndIf
If Lower(item$)= "buildingm"
	If money#=&gt;buildingm#
		money#=money#-buildingm#
	EndIf
If Lower(item$)= "buildingm"
	If money#=&gt;buildingm#
		money#=money#-buildingL#
	EndIf
If Lower(item$)= "light"
	If money#=&gt;light#
		money#=money#-light#
	EndIf
If Lower(item$)= "digicamlow"
	If money#=&gt;digicamlow#
		money#=money#-digicamlow#
	EndIf
If Lower(item$)= "digicamhigh"
	If money#=&gt;digicamhigh#
		money#=money#-digicamhigh#
	EndIf
If Lower(item$)= "digivid"
	If money#=&gt;digivid#
		money#=money#-digivid#
	EndIf
If Lower(item$)= "webcam"
	If money#=&gt;webcam#
		money#=money#-webcam#
	EndIf
If Lower(item$)= "laptop"
	If money#=&gt;laptop#
		money#=money#-laptop#
	EndIf
If Lower(item$)= "floppy"
	If money#=&gt;floppy#
		money#=money#-floppy#
	EndIf
If Lower(item$)= "clock"
	If money#=&gt;clock#
		money#=money#-clock#
	EndIf
If Lower(item$)= "chair"
	If money#=&gt;chair#
		money#=money#-chair#
	EndIf
If Lower(item$)= "fillingcab"
	If money#=&gt;fillingcab#
		money#=money#-fillingcab#
	EndIf
If Lower(item$)= "tvsmall"
	If money#=&gt;tvsmall#
		money#=money#-tvsmall#
	EndIf
If Lower(item$)= "tvlarge"
	If money#=&gt;tvlarge#
		money#=money#-tvlarge#
	EndIf
If Lower(item$)= "shelves"
	If money#=&gt;shelves#
		money#=money#-shelves#
	EndIf
If Lower(item$)= "paper"
	If money#=&gt;paper#
		money#=money#-paper#
	EndIf
If Lower(item$)= "pen"
	If money#=&gt;pen#
		money#=money#-pen#
	EndIf
If Lower(item$)= "picture"
	If money#=&gt;picture#
		money#=money#-picture#
	EndIf
If Lower(item$)= "Displaycase"
	If money#=&gt;Displaycase#
		money#=money#-Displaycase#
	EndIf
If Lower(item$)= "dsiaplaycaserotate"
	If money#=&gt;dsiaplaycaserotate#
		money#=money#-dsiaplaycaserotate#
	EndIf
If Lower(item$)= "sterob"
	If money#=&gt;sterob#
		money#=money#-sterob#
	EndIf
If Lower(item$)= "sterog"
	If money#=&gt;sterog#
		money#=money#-sterog#
	EndIf
If Lower(item$)= "cashreg"
	If money#=&gt;cashreg#
		money#=money#-cashreg#
	EndIf
If Lower(item$)= "photocopier"
	If money#=&gt;photocopier#
		money#=money#-photocopier#
	EndIf
If Lower(item$)= "phone"
	If money#=&gt;phone#
		money#=money#-phone#
	EndIf
If Lower(item$)= "tablel"
	If money#=&gt;tablel#
		money#=money#-tablel#
	EndIf
If Lower(item$)= "tables"
	If money#=&gt;tables#
		money#=money#-tables#
	EndIf
If Lower(item$)= "drinkmachine"
	If money#=&gt;drinkmachine#
		money#=money#-drinkmachine#
	EndIf
If Lower(item$)= "changinroom"
	If money#=&gt;changinroom#
		money#=money#-changinroom#
	EndIf
If Lower(item$)= "buglaralarm"
	If money#=&gt;buglaralarm#
		money#=money#-buglaralarm#
	EndIf
Else 
	Print"you do not have anoth money!
EndIf 
End Function
</pre> <br><br></td></tr></table><br>
<a name="325077"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Agamer</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> ohhh one question do I need to use all of them <br><br></td></tr></table><br>
<a name="325078"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Agamer</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> plus then I had o have all of htesse<br><br>;compnay's money<br>Global money#=0<br>Global in$<br><br>;list of bought items<br>Global nDesk#=0<br>Global ncomputer#=0<br>Global ndvdplayer#=0<br>Global ndvd=0<br>Global ncddrive#=0<br>Global ncdrewriter#=0<br>Global nscanner#=0<br>Global nMic#=1<br>Global nprinter#=0<br>Global nnetcard#=0<br>Global nnethub#=0<br>Global nnetcable#=0<br>Global nmodem#=0<br>Global nadslmodem#=0<br>Global nvr#=0<br>Global ncad#=0<br>Global nbuidings#=0<br>Global nbuildingm#=0<br>Global nbuildingL#=0<br>Global nlight#=0<br>Global ndigicamlow#=0<br>Global ndigicamhigh#=0<br>Global ndigivid#=0<br>Global nwebcam#=0<br>Global nlaptop#=0<br>Global nfloppy#=0<br>Global nclock#=0<br>Global nchair#=0<br>Global nfillingcab#=0<br>Global ntvsmall#=0<br>Global ntvlarge#=0<br>Global nshelves#=0<br>Global npaper#=0<br>Global npen#=0<br>Global npicture#=0<br>Global nDisplaycase#=0<br>Global ndsiaplaycaserotate#=0<br>Global nsterob#=0<br>Global nsterog#=0<br>Global ncashreg#=0<br>Global nphotocopier#=0<br>Global nphone#=0<br>Global ntablel#=0<br>Global ntables#=0<br>Global ndrinkmachine#=0<br>Global nchanginroom#=0<br>Global nbuglaralarm#=0<br>;end of list<br><br><br><br>;list of buyble items<br>Const Desk#=200<br>Const computer#=1000<br>Const dvdplayer#=50<br>Const dvd=100<br>Const cddrive#=10<br>Const cdrewriter#=30<br>Const scanner#=50<br>Const Mic#=10<br>Const printer#=100<br>Const netcard#=10<br>Const nethub#=50<br>Const netcable#=5<br>Const modem#=30<br>Const adslmodem#=50<br>Const vr#=10000<br>Const cad#=100<br>Const buidings#=50000<br>Const buildingm#=100000<br>Const buildingL#=200000<br>Const light#=10<br>Const digicamlow#=100<br>Const digicamhigh#=300<br>Const digivid#=500<br>Const webcam#=100<br>Const laptop#=1500<br>Const floppy#=5<br>Const clock#=5<br>Const chair#=100<br>Const fillingcab#=200<br>Const tvsmall#=100<br>Const tvlarge#=400<br>Const shelves#=50<br>Const paper#=5<br>Const pen#=2<br>Const picture#=30<br>Const Displaycase#=400<br>Const dsiaplaycaserotate#=1000<br>Const sterob#=100<br>Const sterog#=1000<br>Const cashreg#=200<br>Const photocopier#=1000<br>Const phone#=20<br>Const tablel#=100<br>Const tables#=50<br>Const drinkmachine#=2000<br>Const changinroom#=5000<br>Const buglaralarm#=100<br>;end of buyable items <br><br></td></tr></table><br>
<a name="325083"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Agamer</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> can data statements be acseessed from within functions <br><br></td></tr></table><br>
<a name="325091"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >WolRon</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> If money#=money#-item$+"#" =&gt;0 <br></div>What the hell does this mean?<br><br>I have NO IDEA how the compiler resolves that.<br><br>Why are you adding "#" to item$?<br><br>Does the compiler add "#" to item$ before or after item$ is subtracted from money#?  And would it make a difference?  If I'm not mistaken the order that the arguments are handled can be in any order unless strictly specifying the order with parentheses.  What I am getting at is that the compiler may add "#" to item$ OR it may add "#" to the result of (money# - item$).  Which one do you want? (AND WHY?).  There is no guarantee on how the compiler will resolve that unless you specifically add parentheses<br>i.e.<pre class=code>money# - (item$ + "#")</pre>Comparing (money# - item$ + "#") to whether it's greater than or equal to 0 (zero) will result in a 1 or a 0,<br>SO your initial comparison of (IF money# =) will be comparing it to either 1 or 0 which I doubt is what you want.<br><br>I don't think you have a clue what you are doing. <br><br></td></tr></table><br>
<a name="325096"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Agamer</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> yep that is true but I have forgot that methord hre is what I have got now<br><br>I have replaced some of my other functions<br><br><pre class=code>
ype Item
	Field ID%	; Unique identifier for this item
	Field Name$	; Name of item
	Field Cost#	; Cost of item $
	Field Desc$	; Description of item
End Type

Function listitems()
	For T = 1 To 3
	Read ID%
	Read Name$ 
	Read Cost# 
	  Next 
End Function

.Items
Data 1,computer,1000
Data 2,dvdplayer,50
Data 3,dvd,100
Data 4,cddrive,10
Data 5,cdrewriter,30
Data 6,scanner,50
Data 7,Mic,10
Data 8,printer,100
Data 9,netcard,10
Data 10,nethub,50
Data 11,netcable,5
Data 12,modem,30
Data 13,adslmodem,50
Data 14,vr,10000
Data 15,cad,100
Data 16,buidings,50000
Data 17,buildingm,100000
Data 18,buildingL,200000
Data 19,light,10
Data 20,digicamlow,100
Data 21,digicamhigh,300
Data 22,digivid,500
Data 23,webcam,100
Data 24,laptop,1500
Data 25,floppy,5
Data 26,clock,5
Data 27,chair,100
Data 28,fillingcab,200
Data 29,tvsmall,100
Data 30,tvlarge,400
Data 31,shelves,50
Data 32,paper,5
Data 33,pen,2
Data 34,picture,30
Data 35,Displaycase,400
Data 36,dsiaplaycaserotate,1000
Data 37,sterob,100
Data 38,sterog,1000
Data 39,cashreg,200
Data 40,photocopier,1000
Data 41,phone,20
Data 42,tablel,100
Data 43,tables,50
Data 44,drinkmachine,2000
Data 45,changinroom,5000
Data 46,buglaralarm,100
Data 47,Desk,200

</pre> <br><br></td></tr></table><br>
<a name="325099"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >WolRon</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> BTW, declaring costants for the prices of your items means that you will never be able to change the prices later if you wanted to. <br><br></td></tr></table><br>
<a name="325102"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Agamer</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> kk well I never would want to but can you change the number in a data stament<br><br>it's really weird when I run the above cod e(my last post before this)<br><br>it asks to open a file how weird <br><br></td></tr></table><br>
<a name="325105"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Agamer</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> ahh types are so confusing <br><br></td></tr></table><br>
<a name="325113"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RetroBooster</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's an implementation that's a bit more elegant, btw. with saying you where doing things that weren't even remotely possible I meanth this:<br><br>money#=money#-item$+"#"<br><br>You where substracting a string from a float and then adding another string to that, that is impossible.<br><br>Sorry if I sounded rude, I wasn't aware you where still so young, welcome to the club in any case, I personaly started coding around age 10-11.<br><br>This should keep you busy for a while...<br>Copy paste and run, then take a look at the iner workings :)<br><br>-edit- Updated code in new post! <br><br></td></tr></table><br>
<a name="325117"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jfk EO-11110</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> Maybe it just wants to save the source?<br><br>BTW you should not write<br>Data 1,computer,1000<br>Data 2,dvdplayer,50<br><br>instead write<br>Data 1,"computer",1000<br>Data 2,"dvdplayer",50<br><br><br><br><br>you really shouldn't make this more complicated than it has to be.<br><br>Did you consider to use simple arrays instead?<br><br>Here's an example:<br><pre class=code>

num=47
dim product$(num)
dim price#(num)

restore items
for i=1 to num
 read dummy
 read product$(i)
 read price#(i)
next

money#=1000.0

while upper$(in$)&lt;&gt;"EXIT"
 print "(Enter EXIT to finish)"
 in$=input$("please enter product name")
 for i=1 to num
  if upper$(in$) = upper$(product$(i)) ; product name found?
   print "Product "+ product$(i)+" costs "+price#(i)
   if money# &gt;= price#(i) then
    money#=money#-price#(i)
    print "you have bought one. Now you still have "+ money# +" Dollars"
   else
    print "You haven't got enough money to buy this."
   endif
  else
   print "The product name you entered was not found in the database. Please try again"
  endif
 next
wend

end

.Items
Data 1,"computer",1000
Data 2,"dvdplayer",50
Data 3,"dvd",100
Data 4,"cddrive",10
Data 5,"cdrewriter",30
Data 6,"scanner",50
Data 7,"Mic",10
Data 8,"printer",100
Data 9,"netcard",10
Data 10,"nethub",50
Data 11,"netcable",5
Data 12,"modem",30
Data 13,"adslmodem",50
Data 14,"vr",10000
Data 15,"cad",100
Data 16,"buidings",50000
Data 17,"buildingm",100000
Data 18,"buildingL",200000
Data 19,"light",10
Data 20,"digicamlow",100
Data 21,"digicamhigh",300
Data 22,"digivid",500
Data 23,"webcam",100
Data 24,"laptop",1500
Data 25,"floppy",5
Data 26,"clock",5
Data 27,"chair",100
Data 28,"fillingcab",200
Data 29,"tvsmall",100
Data 30,"tvlarge",400
Data 31,"shelves",50
Data 32,"paper",5
Data 33,"pen",2
Data 34,"picture",30
Data 35,"Displaycase",400
Data 36,"dsiaplaycaserotate",1000
Data 37,"sterob",100
Data 38,"sterog",1000
Data 39,"cashreg",200
Data 40,"photocopier",1000
Data 41,"phone",20
Data 42,"tablel",100
Data 43,"tables",50
Data 44,"drinkmachine",2000
Data 45,"changinroom",5000
Data 46,"buglaralarm",100
Data 47,"Desk",200
</pre> <br><br></td></tr></table><br>
<a name="325191"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CodeD</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> When you finish the shop code you should post it in the code gallery for other people!! <br><br></td></tr></table><br>
<a name="325243"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Agamer</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> yeh this is for a weird text game which will be freeware I am planning to make it multiplayer but it is like tatergy so if you can help it would be nice <br><br></td></tr></table><br>
<a name="325250"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Agamer</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> @RetroBooster I acept your apolgy and <br><br>i lve your solution but  am at a lost woth theesse types and your code would you mind documenting it! <br><br></td></tr></table><br>
<a name="325254"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RetroBooster</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#23">[#23]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'll comment it some more so you can understand it better :) plus I just saw I did the loader code wrong, silly slip up. New version up soon. <br><br></td></tr></table><br>
<a name="325263"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Agamer</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#24">[#24]</a></td></tr></table></td></tr><tr ><td class="posttext"> kk <br><br></td></tr></table><br>
<a name="325280"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RetroBooster</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#25">[#25]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's the new version:<br><br><pre class=code>
Global Money# = 100 ;The players money
Global ItemFile$ = "Items.dat"	;Stores all the items you save
Global GameFile$ = "Game.dat"	;Stores all the items and their current availability in stores
								;as well as how many the player has of them and how much money the player has
Type Item
Field name$		;Name of the item
Field desc$		;Description of the item
Field cost#		;Price of the item
Field ammount	;How many items of this type are available
Field owned		;How many of this item the player posesses
End Type

;DEMO CODE:
Graphics 800,600,0,2

;Make some items
CreateItem("Coffee","Yummy beverage with extra cafein!",10)
CreateItem("Tea","A nice drink from china",5)
CreateItem("Chocolate milk","Hot chocolate with extra milk and sugar",15)
CreateItem("Water","Natural spring water",2)

;Store them for later
SaveItems()

;Offer some items for sale
AddItem("Coffee",3)
AddItem("Tea",10)
AddItem("Chocolate milk",6)
AddItem("Water",100)

;Buy some items
If BuyItem("Coffee",3)
	Print "bought 3 Cups of Coffee!"
Else
	Print "Not enough money!"
EndIf

Print "Money Left: " + Money

If BuyItem("Tea",9)
	Print "bought 9 Cups of Tea!"
Else
	Print "Not enough money!"
EndIf

Print "Money Left: " + Money

If BuyItem("Chocolate milk",1)
	Print "bought 1 Cup of Nice choc!"
Else
	Print "Not enough money!"
EndIf

Print "Money Left: " + Money

If BuyItem("Water",40)
	Print "bought 40 Cups of water!"
Else
	Print "Not enough money!"
EndIf

Print "Money Left: " + Money

;Display all items
PrintItems()

;Save the game!
SaveGame()

;Exit
WaitKey()

;Creates a new item
Function CreateItem(Name$,Desc$,cost#)
	;Make a new item
	I.Item = New Item

	;Set up the stats of the item
	I\name = name
	I\desc = desc
	I\cost = cost
End Function

;Deletes an item
Function FreeItem(Name$)
	;Loop trough all items
	For I.Item = Each Item
		;Check if this is the right one
		If I\name = name
			;Remove it and stop looping trough the items, we're done
			Delete I
			Exit
		EndIf
	Next
End Function

;Saves all items
Function SaveItems()
	;Open ItemFile for writing
	file = WriteFile(ItemFile)

		;Loop trough all items
		For I.Item = Each Item
			;Write the items to the file
			WriteString(file,I\name)
			WriteString(file,I\desc)
			WriteFloat(file,I\cost)
		Next
	;Close the file
	CloseFile(file)
End Function
	
;Loads all items
Function LoadItems()
	;Open ItemFile for reading
	file = ReadFile(ItemFile)

		;Repeat until the file ends
		While Not Eof(file)
			;Create the item stored in the file
			I.Item = New Item
			I\name = ReadString(file)
			I\desc = ReadString(file)
			I\cost = ReadFloat(file)
		Wend
	;Close the file
	CloseFile(file)
End Function

;Adds a certain ammount of an item to the store
Function AddItem(Name$,ammount)
	;Loop trough all items
	For I.Item = Each Item
		;Check if this is the right one
		If I\name = name
			;Add ammount to the amount of this item being offered for sale
			I\Ammount = I\Ammount + ammount
			Exit
		EndIf
	Next
End Function

;Removes a certain ammount of items from the store
Function RemoveItem(Name$,ammount)
	;Loop trough all items
	For I.Item = Each Item
		;Check if this is the right one
		If I\name = name
			;Substract ammount from the amount of this item being offered for sale and make sure it's not under zero
			I\Ammount = I\Ammount - ammount
			If I\Ammount &lt; 0 Then I\Ammount = 0
			Exit
		EndIf
	Next
End Function

;Buys an item
Function BuyItem(name$,ammount = 1)
	;Loop trough all items
	For I.Item = Each Item
		;Check if this is the right one
		If I\name = name
			;Calculate the cost for this purchase
			cost# = ammount * I\Cost

			;Check if we can afford it
			If Money &lt; cost#
				Return False	;you can't pay it silly
			Else
				;Remove the items bought from the store
				I\Ammount = I\Ammount - ammount
				;Add the items bought to the ammount of these the player owns
				I\Owned = Owned + ammount
				;Pay the cost
				Money = Money - cost
				Return True		;All paid, you now own your new stuff
			EndIf
		EndIf
	Next
End Function

;Prints the info on an item
Function PrintItem(name$)
	;Loop trough all items	
	For I.Item = Each Item
		;Check if this is the right one
		If I\name = name
			;Print info on the item
			Print "-----------------------------"
			Print "Item: " + I\Name
			Print "Desc: " + I\Desc
			Print "Cost: " + I\Cost
			If I\Ammount = 0
				Print "This item is out of stock"
			ElseIf I\Ammount = 1
				Print "Only one " + I\Name + " is in store"
			Else
				Print "There are: " + I\Ammount + " of these item in store"
			EndIf
			If I\Owned = 0
				Print "You don't own any of these"
			Else
				Print "You have " + I\owned + " of these"
			EndIf
		EndIf
	Next
End Function

;Prints the info on an all items
Function PrintItems()
	;Loop trough all items
	For I.Item = Each Item
		;Print info on the item
		Print "-----------------------------"
		Print "Item: " + I\Name
		Print "Desc: " + I\Desc
		Print "Cost: " + I\Cost
		If I\Ammount = 0
			Print "This item is out of stock"
		ElseIf I\Ammount = 1
			Print "Only one " + I\Name + " is in store"
		Else
			Print "There are: " + I\Ammount + " of these item in store"
		EndIf
		If I\Owned = 0
			Print "You don't own any of these"
		Else
			Print "You have " + I\owned + " of these"
		EndIf
	Next
End Function

;Saves all game info
Function SaveGame()
	;Open GameFile for writing
	file = WriteFile(GameFile)
		;Write the amount of money we have
		WriteInt(file,Money)
		;Loop trough all items
		For I.Item = Each Item
			;Write the items to the file as well as the amount the store and player own!
			WriteString(file,I\name)
			WriteString(file,I\desc)
			WriteFloat(file,I\cost)
			WriteInt(file,I\Ammount)
			WriteInt(file,I\Owned)
		Next
	;Close the file	
	CloseFile(file)
End Function
	
;Loads all game info
Function LoadGame()
	;Open GameFile for reading
	file = ReadFile(GameFile)
	
		Money = ReadInt(file)
		;Repeat until the file ends
		While Not Eof(file)
			;Restore the item stored in the file
			I.Item = New Item
			I\name		= ReadString(file)
			I\desc		= ReadString(file)
			I\cost		= ReadFloat(file)
			I\Ammount	= ReadInt(file)
			I\Owned		= ReadInt(file)
		Wend

	;Close the file
	CloseFile(file)
End Function
</pre> <br><br></td></tr></table><br>
<a name="325291"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Agamer</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#26">[#26]</a></td></tr></table></td></tr><tr ><td class="posttext"> thx will hav a look at it later today thx <br><br></td></tr></table><br>
<a name="325452"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Agamer</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#27">[#27]</a></td></tr></table></td></tr><tr ><td class="posttext"> thx I have now looked at it<br><br>May I please mod it and use it in my free ware game please! <br><br></td></tr></table><br>
<a name="325822"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RetroBooster</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#28">[#28]</a></td></tr></table></td></tr><tr ><td class="posttext"> sure, go ahead. <br><br></td></tr></table><br>
<a name="325860"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Agamer</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#29">[#29]</a></td></tr></table></td></tr><tr ><td class="posttext"> thanks so much <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
