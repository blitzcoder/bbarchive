<!DOCTYPE html><html lang="en" ><head ><title >first try at creating avatar</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >first try at creating avatar</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >first try at creating avatar</a><br><br>
<a name="1237122"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >bluatigro</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> i m trying to create a simple avatar<br>the blitz3d comands do not work as expected<br><pre class=code>
Include "shapes.bb"

Graphics3D 640,480
SetBuffer BackBuffer()

camera=CreateCamera()
PositionEntity camera,0,0,-200

light=CreateLight()
RotateEntity light,90,0,0

;body pivots
body = CreatePivot( 0 )
setbox( 0,0,0 , 20,30,10 )
lijf = createbox()
EntityParent lijf , body

sholder_l = CreatePivot( body )
PositionEntity sholder_l , 30,30,0
setbox( 0,-10,0 , 5,10,5 )
arm = createbox()
EntityParent arm , sholder_l

elbow_l = CreatePivot( sholder_l )
PositionEntity elbow_l , 0,-20,0
uarm = createbox()
EntityParent uarm , elbow_l

wrist_l = CreatePivot( elbow_l )
PositionEntity wrist_l , 0,0,-20
setbox( 0,-7,0 , 3,7,7 )
hand = createbox()
EntityParent hand , wrist_l

neck = CreatePivot( body )
bekken_l = CreatePivot( body )
knee_l = CreatePivot( bekken_l )
enkle_l = CreatePivot( knee_l )


While Not KeyDown( 1 )

  TurnEntity body , 0 , 1 , 0

  RotateEntity sholder_l , 30 , 0 , 0

  UpdateWorld
  RenderWorld

  Flip

Wend
</pre><br>shapes.bb :<br><pre class=code>
Type tbox
  Field mx#
  Field my#
  Field mz#
  Field dx#
  Field dy#
  Field dz#
End Type

Global box.tbox = New tbox

Function setbox( mx# , my# , mz# , dx# , dy# , dz# )
  box\mx = mx
  box\my = my
  box\mz = mz
  box\dx = dx
  box\dy = dy
  box\dz = dz
End Function

Function CreateBox()
  mesh = CreateCube()
  PositionEntity mesh , box\mx , box\my , box\mz
  ScaleEntity mesh , box\dx , box\dy , box\dz
  Return mesh
End Function

</pre> <br><br></td></tr></table><br>
<a name="1237151"></a>

<a name="1237152"></a>

<a name="1237160"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> All the functions that you have used work properly so it is your code which has errors.<br><br>Try to use scalemesh(), positionmesh(), rotatemesh(), when building your meshes, because if you use scaleentity(), the childs (pivot or mesh) can be affected by the scale of the parent. <br><br></td></tr></table><br>
<a name="1237184"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >bluatigro</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> i solved the error :<br>- i switched positionmesh and scalemesh in createbox()<br><br>update :<br>- try at a torus<br><br>new error :<br>- j is not a function [ in createtorus ]<br>- [ look at REMed code ]<br><br>question :<br>- how do i create a avatar-function out of this<br>- so i can create more avatars<br><br>main :<br><pre class=code>
Include "shapes.bb"

Graphics3D 640,480,16,1
SetBuffer BackBuffer()

camera=CreateCamera()
PositionEntity camera,0,0,-200

light=CreateLight()
RotateEntity light,90,0,0

;body pivots + parts
body = CreatePivot( 0 )
lijf = createbox( 0,0,0 , 20,30,5 )
EntityParent lijf , body

sholder_l = CreatePivot( body )
PositionEntity sholder_l , 30,30,0
arm = createbox( 0,-15,0 , 5,10,5 )
EntityParent arm , sholder_l , False

elbow_l = CreatePivot( sholder_l )
PositionEntity elbow_l , 0,-30,0
uarm = createbox( 0,-15,0 , 5,10,5 )
EntityParent uarm , elbow_l , False

wrist_l = CreatePivot( elbow_l )
PositionEntity wrist_l , 0,-30,0
hand = createbox( 0,-7,0 , 3,7,7 )
EntityParent hand , wrist_l , False

sholder_r = CreatePivot( body )
PositionEntity sholder_r , -30,30,0
arm = createbox( 0,-15,0 , 5,10,5 )
EntityParent arm , sholder_r , False

elbow_r = CreatePivot( sholder_r )
PositionEntity elbow_r , 0,-30,0
uarm = createbox( 0,-15,0 , 5,10,5 )
EntityParent uarm , elbow_r , False

wrist_r = CreatePivot( elbow_r )
PositionEntity wrist_r , 0,-30,0
hand = createbox( 0,-7,0 , 3,7,7 )
EntityParent hand , wrist_r , False

neck = CreatePivot( body )
PositionEntity neck , 0,50,0
;setbox( 0,0,0 , 10,10,10 )
;head = createtorus()
head = createbox( 0,0,0 , 10,10,10 )
EntityParent head , neck , False

bekken_l = CreatePivot( body )
PositionEntity bekken_l , 15,-30,0
leg = createbox( 0,-15,0 , 5,10,5 )
EntityParent leg , bekken_l , False

knee_l = CreatePivot( bekken_l )
PositionEntity knee_l , 0,-30,0
uleg = createbox( 0,-15,0 , 5,10,5 )
EntityParent uleg , knee_l , False

enkle_l = CreatePivot( knee_l )
PositionEntity enkle_l , 0,-30,0
foot = createbox( 0,-5,-7  , 5,5,10 )
EntityParent foot , enkle_l , False

bekken_r = CreatePivot( body )
PositionEntity bekken_r, -15,-30,0
leg = createbox( 0,-15,0 , 5,10,5 )
EntityParent leg , bekken_r, False

knee_r= CreatePivot( bekken_r)
PositionEntity knee_r , 0,-30,0
uleg = createbox( 0,-15,0 , 5,10,5 )
EntityParent uleg , knee_r, False

enkle_r = CreatePivot( knee_r)
PositionEntity enkle_r, 0,-30,0
foot = createbox( 0,-5,-7  , 5,5,10 )
EntityParent foot , enkle_r, False


angle = 0

While Not KeyDown( 1 )

  TurnEntity body , 0 , 1 , 0

  RotateEntity sholder_l , pend( angle , 30 ) , 0 , 0
  RotateEntity elbow_l , 30 , 0 , 0
  RotateEntity bekken_l , pend( angle + 180 , 30 ) , 0 , 0
  RotateEntity knee_l , pend( angle + 90 , 30 ) - 30 , 0 , 0
  RotateEntity sholder_r , pend( angle + 180 , 30 ) , 0 , 0
  RotateEntity elbow_r , 30 , 0 , 0
  RotateEntity bekken_r , pend( angle , 30 ) , 0 , 0
  RotateEntity knee_r , pend( angle - 90 , 30 ) - 30 , 0 , 0
  angle = angle + 5

  UpdateWorld
  RenderWorld

  Flip

Wend
</pre><br>shapes.bb :<br><pre class=code>
Type tbox
  Field mx#
  Field my#
  Field mz#
  Field dx#
  Field dy#
  Field dz#
End Type

Global box.tbox = New tbox

Function setbox( mx# , my# , mz# , dx# , dy# , dz# )
  box\mx = mx
  box\my = my
  box\mz = mz
  box\dx = dx
  box\dy = dy
  box\dz = dz
End Function

Function CreateBox( mx# , my# , mz# , dx# , dy# , dz# )
  mesh = CreateCube()
  ScaleMesh mesh , dx , dy , dz
  PositionMesh mesh , mx , my , mz
  Return mesh
End Function

Function createtorus()
  mesh = CreateMesh()
  surface = CreateSurface( mesh )
  mx = box\mx
  my = box\my
  mz = box\mz
  dx = box\dx
  dy = box\dy
  dz = box\dz
;  For i = -180 To 180 Step 360 / 12
;    i2 = i + 360 / 12
;    For j = -180 To 180 Step 360 / 12
;      j2 = j + 360 / 12
;      x = mx + ( dx + dy * Cos( i ) ) * Cos( j )
;      y = my + ( dx + dy * Cos( i ) ) * Sin( j )
;      z = mz + Sin( i ) * dz
;      v0 = AddVertex( surface , x , y , z )
;      x = mx + ( dx + dy * Cos( i ) ) * Cos( j2 )
;      y = my + ( dx + dy * Cos( i ) ) * Sin( j2 )
;      z = mz + Sin( i ) * dz
;      v1 = AddVertex( surface , x , y , z )
;      x = mx + ( dx + dy * Cos( i2 ) ) * Cos( j2 )
;      y = my + ( dx + dy * Cos( i2 ) ) * Sin( j2 )
;      z = mz + Sin( i ) * dz
;      v2 = AddVertex( surface , x , y , z )
;      x = mx + ( dx + dy * Cos( i2 ) ) * Cos( j )
;      y = my + ( dx + dy * Cos( i2 ) ) * Sin( j )
;      z = mz + Sin( i ) * dz
;      v3 = AddVertex( surface , x , y , z )
;      AddTriangle( suface , v0 , v1 , v2 )
;      AddTriangle( suface , v0 , v2 , v3 )
;    Next j
;  Next i
  Return mesh
End Function

Function pend#( fase# , amp# )
  Return Sin( fase ) * amp
End Function
</pre> <br><br></td></tr></table><br>
<a name="1237196"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bobysait</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> you've got some errors in the syntax<br>"next" is the end of the "For/Next" statement. You can't iterate the variable after<br>take care of your variable names : Surface is mispelled on the Addtriangle (suface)<br><br>And I don't know how you intend to get a torus with this, but anyway, here is a valid function.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Type tbox
  Field mx#
  Field my#
  Field mz#
  Field dx#
  Field dy#
  Field dz#
End Type

Global box.tbox = New tbox

Function setbox( mx# , my# , mz# , dx# , dy# , dz# )
  box\mx = mx
  box\my = my
  box\mz = mz
  box\dx = dx
  box\dy = dy
  box\dz = dz
End Function

Function CreateBox( mx# , my# , mz# , dx# , dy# , dz# )
  Local mesh = CreateCube()
  ScaleMesh mesh , dx , dy , dz
  PositionMesh mesh , mx , my , mz
  Return mesh
End Function

Function createtorus ( segmentRadius%=8, segmentPerimeter%=12, outerRadius#=1.0, innerRadius#=0.1, height#=1.0, pParent = 0 )
	Local mesh = CreateMesh ( pParent );
	Local s = CreateSurface(mesh);
	
	Local radius# = (outerRadius - innerRadius)*.5;
	Local pathR# = outerRadius - radius;
	Local j, i
	For j = 0 To segmentPerimeter
		
		Local dx# = Cos(j*360.0/segmentPerimeter);
		Local dz# = Sin(j*360.0/segmentPerimeter);
		
		Local cx# = dx * pathR;
		Local cz# = dz * pathR;
		
		For i = 0 To segmentRadius
			Local x_# = Cos(i*360.0/segmentRadius)*radius;
			Local y # = Sin(i*360.0/segmentRadius)*height;
			Local nx# = x_*dx;
			Local nz# = x_*dz;
			Local ny# = y;
			Local x# = cx + nx;
			Local z# = cz + nz;
			Local v = AddVertex(s, x,y,z, Float(j)/segmentPerimeter, Float(i)/segmentRadius);
			Local n# = 1.0/Sqr(nx*nx+ny*ny+nz*nz);
			VertexNormal(s, v, nx*n, ny*n, nz*n);
			If i*j&gt;0
				AddTriangle(s, v-segmentRadius-2, v-segmentRadius-1, v);
				AddTriangle(s, v-segmentRadius-2, v, v-1);
			End If
		Next
	Next
	
	Return mesh;
End Function

Function createtorus_()
  Local mesh = CreateMesh()
  Local surface = CreateSurface( mesh )
  Local mx# = box\mx
  Local my# = box\my
  Local mz# = box\mz
  Local dx# = box\dx
  Local dy# = box\dy
  Local dz# = box\dz
  Local x#,y#,z#, i,j, i2, j2
  Local v0,v1,v2,v3
  For i = -180 To 180 Step 360 / 12
    i2 = i + 360 / 12
    For j = -180 To 180 Step 360 / 12
      j2 = j + 360 / 12
      x = mx + ( dx + dy * Cos( i ) ) * Cos( j )
      y = my + ( dx + dy * Cos( i ) ) * Sin( j )
      z = mz + Sin( i ) * dz
      v0 = AddVertex( surface , x , y , z )
      x = mx + ( dx + dy * Cos( i ) ) * Cos( j2 )
      y = my + ( dx + dy * Cos( i ) ) * Sin( j2 )
      z = mz + Sin( i ) * dz
      v1 = AddVertex( surface , x , y , z )
      x = mx + ( dx + dy * Cos( i2 ) ) * Cos( j2 )
      y = my + ( dx + dy * Cos( i2 ) ) * Sin( j2 )
      z = mz + Sin( i ) * dz
      v2 = AddVertex( surface , x , y , z )
      x = mx + ( dx + dy * Cos( i2 ) ) * Cos( j )
      y = my + ( dx + dy * Cos( i2 ) ) * Sin( j )
      z = mz + Sin( i ) * dz
      v3 = AddVertex( surface , x , y , z )
      AddTriangle( surface , v0 , v1 , v2 )
      AddTriangle( surface , v0 , v2 , v3 )
    Next; j
  Next; i
  UpdateNormals mesh
  Return mesh
End Function

Function pend#( fase# , amp# )
  Return Sin( fase ) * amp
End Function

Graphics3D 640,480,0,2
SetBuffer BackBuffer()

;Local torus = createtorus()

Local piv = CreatePivot()
Local pit = CreatePivot(piv)
camera=CreateCamera(pit)
PositionEntity camera,0,0,-200

light=CreateLight(2)
PositionEntity light,90,100,-50

;body pivots + parts
body = CreatePivot( 0 )
lijf = CreateBox( 0,0,0 , 20,30,5 )
EntityParent lijf , body

sholder_l = CreatePivot( body )
PositionEntity sholder_l , 30,30,0
arm = CreateBox( 0,-15,0 , 5,10,5 )
EntityParent arm , sholder_l , False

elbow_l = CreatePivot( sholder_l )
PositionEntity elbow_l , 0,-30,0
uarm = CreateBox( 0,-15,0 , 5,10,5 )
EntityParent uarm , elbow_l , False

wrist_l = CreatePivot( elbow_l )
PositionEntity wrist_l , 0,-30,0
hand = CreateBox( 0,-7,0 , 3,7,7 )
EntityParent hand , wrist_l , False

sholder_r = CreatePivot( body )
PositionEntity sholder_r , -30,30,0
arm = CreateBox( 0,-15,0 , 5,10,5 )
EntityParent arm , sholder_r , False

elbow_r = CreatePivot( sholder_r )
PositionEntity elbow_r , 0,-30,0
uarm = CreateBox( 0,-15,0 , 5,10,5 )
EntityParent uarm , elbow_r , False

wrist_r = CreatePivot( elbow_r )
PositionEntity wrist_r , 0,-30,0
hand = CreateBox( 0,-7,0 , 3,7,7 )
EntityParent hand , wrist_r , False

neck = CreatePivot( body )
PositionEntity neck , 0,50,0
setbox( 0,0,0 , 10,10,10 )
Local head = createtorus(8,12,10,6,2)
;head = CreateBox( 0,0,0 , 10,10,10 )
EntityParent head , neck , False

bekken_l = CreatePivot( body )
PositionEntity bekken_l , 15,-30,0
leg = CreateBox( 0,-15,0 , 5,10,5 )
EntityParent leg , bekken_l , False

knee_l = CreatePivot( bekken_l )
PositionEntity knee_l , 0,-30,0
uleg = CreateBox( 0,-15,0 , 5,10,5 )
EntityParent uleg , knee_l , False

enkle_l = CreatePivot( knee_l )
PositionEntity enkle_l , 0,-30,0
foot = CreateBox( 0,-5,-7  , 5,5,10 )
EntityParent foot , enkle_l , False

bekken_r = CreatePivot( body )
PositionEntity bekken_r, -15,-30,0
leg = CreateBox( 0,-15,0 , 5,10,5 )
EntityParent leg , bekken_r, False

knee_r= CreatePivot( bekken_r)
PositionEntity knee_r , 0,-30,0
uleg = CreateBox( 0,-15,0 , 5,10,5 )
EntityParent uleg , knee_r, False

enkle_r = CreatePivot( knee_r)
PositionEntity enkle_r, 0,-30,0
foot = CreateBox( 0,-5,-7  , 5,5,10 )
EntityParent foot , enkle_r, False


angle = 0
;HideEntity body

While Not KeyDown( 1 )
	
	TurnEntity piv, 0,-MouseXSpeed(),0
	TurnEntity pit, +MouseYSpeed(),0,0
	
  TurnEntity body , 0 , 1 , 0

  RotateEntity sholder_l , pend( angle , 30 ) , 0 , 0
  RotateEntity elbow_l , 30 , 0 , 0
  RotateEntity bekken_l , pend( angle + 180 , 30 ) , 0 , 0
  RotateEntity knee_l , pend( angle + 90 , 30 ) - 30 , 0 , 0
  RotateEntity sholder_r , pend( angle + 180 , 30 ) , 0 , 0
  RotateEntity elbow_r , 30 , 0 , 0
  RotateEntity bekken_r , pend( angle , 30 ) , 0 , 0
  RotateEntity knee_r , pend( angle - 90 , 30 ) - 30 , 0 , 0
  angle = angle + 5

  UpdateWorld
  RenderWorld

  Flip

Wend
</textarea><br><br><br>by the way : nice job on the procedural animation :) <br><br></td></tr></table><br>
<a name="1237256"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >bluatigro</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> update :<br>- avatar function<br>[ now i can create lots of avatars ]<br>- animation function<br><br>main :<br><pre class=code>
Include "shapes.bb"

Graphics3D 640,480,16,1
SetBuffer BackBuffer()

camera=CreateCamera()
PositionEntity camera,0,0,-200

light=CreateLight()
RotateEntity light,90,0,0

human1.human = New human
createhuman( human1 )
PositionEntity human1\body , -50,0,0
human2.human = New human
createhuman( human2 )
PositionEntity human2\body , 50,0,0


angle = 0

While Not KeyDown( 1 )

  TurnEntity human1\body , 0 , 1 , 0

  human_walk( human1 , angle * 4 , 30 )
  human_walk( human2 , angle * 5 , 30 )

  angle = ( angle + 1 ) Mod 360

  UpdateWorld
  RenderWorld

  Flip

Wend
</pre><br>shapes.bb<br><pre class=code>
Type tbox
  Field mx#
  Field my#
  Field mz#
  Field dx#
  Field dy#
  Field dz#
End Type

Global box.tbox = New tbox

Function setbox( mx# , my# , mz# , dx# , dy# , dz# )
  box\mx = mx
  box\my = my
  box\mz = mz
  box\dx = dx
  box\dy = dy
  box\dz = dz
End Function

Function CreateBox( mx# , my# , mz# , dx# , dy# , dz# )
  mesh = CreateCube()
  ScaleMesh mesh , dx , dy , dz
  PositionMesh mesh , mx , my , mz
  Return mesh
End Function

Function createtorus()
  local mesh = CreateMesh()
  local surface = CreateSurface( mesh )
  local mx# = box\mx
  local my# = box\my
  local mz# = box\mz
  local dx# = box\dx
  local dy# = box\dy
  local dz# = box\dz
;  For i = -180 To 180 Step 360 / 12
;    local i2 = i + 360 / 12
;    For j = -180 To 180 Step 360 / 12
;      local j2 = j + 360 / 12
;      local x# = mx + ( dx + dy * Cos( i ) ) * Cos( j )
;      local y# = my + ( dx + dy * Cos( i ) ) * Sin( j )
;      local z# = mz + Sin( i ) * dz
;      local v0 = AddVertex( surface , x , y , z )
;      x = mx + ( dx + dy * Cos( i ) ) * Cos( j2 )
;      y = my + ( dx + dy * Cos( i ) ) * Sin( j2 )
;      z = mz + Sin( i ) * dz
;      local v1 = AddVertex( surface , x , y , z )
;      x = mx + ( dx + dy * Cos( i2 ) ) * Cos( j2 )
;      y = my + ( dx + dy * Cos( i2 ) ) * Sin( j2 )
;      z = mz + Sin( i ) * dz
;      local v2 = AddVertex( surface , x , y , z )
;      x = mx + ( dx + dy * Cos( i2 ) ) * Cos( j )
;      y = my + ( dx + dy * Cos( i2 ) ) * Sin( j )
;      z = mz + Sin( i ) * dz
;      local v3 = AddVertex( surface , x , y , z )
;      AddTriangle( surface , v0 , v1 , v2 )
;      AddTriangle( surface , v0 , v2 , v3 )
;    Next ; j
;  Next ; i
  Return mesh
End Function

Function pend#( fase# , amp# )
  Return Sin( fase ) * amp
End Function

Type human
  Field body
  Field sholder_l , sholder_r
  Field elbow_l , elbow_r
  Field wrist_l , wrist_r
  Field neck
  Field bekken_l , bekken_r
  Field knee_l , knee_r
  Field enkle_l , enkle_r
End Type

Function createhuman( h.human )

  h\body = CreatePivot( 0 )
  lijf = createbox( 0,0,0 , 20,30,5 )
  EntityParent lijf , h\body

  h\sholder_l = CreatePivot( h\body )
  PositionEntity h\sholder_l , 30,30,0
  arm = createbox( 0,-15,0 , 5,10,5 )
  EntityParent arm , h\sholder_l , False

  h\elbow_l = CreatePivot( h\sholder_l )
  PositionEntity h\elbow_l , 0,-30,0
  uarm = createbox( 0,-15,0 , 5,10,5 )
  EntityParent uarm , h\elbow_l , False

  h\wrist_l = CreatePivot( h\elbow_l )
  PositionEntity h\wrist_l , 0,-30,0
  hand = createbox( 0,-7,0 , 3,7,7 )
  EntityParent hand , h\wrist_l , False

  h\sholder_r = CreatePivot( h\body )
  PositionEntity h\sholder_r , -30,30,0
  arm = createbox( 0,-15,0 , 5,10,5 )
  EntityParent arm , h\sholder_r , False

  h\elbow_r = CreatePivot( h\sholder_r )
  PositionEntity h\elbow_r , 0,-30,0
  uarm = createbox( 0,-15,0 , 5,10,5 )
  EntityParent uarm , h\elbow_r , False

  h\wrist_r = CreatePivot( h\elbow_r )
  PositionEntity h\wrist_r , 0,-30,0
  hand = createbox( 0,-7,0 , 3,7,7 )
  EntityParent hand , h\wrist_r , False

  h\neck = CreatePivot( h\body )
  PositionEntity h\neck , 0,50,0
  ;setbox( 0,0,0 , 10,10,10 )
  ;head = createtorus()
  head = createbox( 0,0,0 , 10,10,10 )
  EntityParent head , h\neck , False

  h\bekken_l = CreatePivot( h\body )
  PositionEntity h\bekken_l , 15,-30,0
  leg = createbox( 0,-15,0 , 5,10,5 )
  EntityParent leg , h\bekken_l , False

  h\knee_l = CreatePivot( h\bekken_l )
  PositionEntity h\knee_l , 0,-30,0
  uleg = createbox( 0,-15,0 , 5,10,5 )
  EntityParent uleg , h\knee_l , False

  h\enkle_l = CreatePivot( h\knee_l )
  PositionEntity h\enkle_l , 0,-30,0
  foot = createbox( 0,-5,-7  , 5,5,10 )
  EntityParent foot , h\enkle_l , False

  h\bekken_r = CreatePivot( h\body )
  PositionEntity h\bekken_r, -15,-30,0
  leg = createbox( 0,-15,0 , 5,10,5 )
  EntityParent leg , h\bekken_r, False

  h\knee_r= CreatePivot( h\bekken_r)
  PositionEntity h\knee_r , 0,-30,0
  uleg = createbox( 0,-15,0 , 5,10,5 )
  EntityParent uleg , h\knee_r, False

  h\enkle_r = CreatePivot( h\knee_r)
  PositionEntity h\enkle_r, 0,-30,0
  foot = createbox( 0,-5,-7  , 5,5,10 )
  EntityParent foot , h\enkle_r, False

End Function

Function human_walk( h.human , angle# , a# )

  RotateEntity h\sholder_l , pend( angle , a ) , 0 , 0
  RotateEntity h\elbow_l , a , 0 , 0
  RotateEntity h\bekken_l , pend( angle + 180 , a ) , 0 , 0
  RotateEntity h\knee_l , pend( angle + 90 , a ) - a , 0 , 0
  RotateEntity h\sholder_r , pend( angle + 180 , a ) , 0 , 0
  RotateEntity h\elbow_r , a , 0 , 0
  RotateEntity h\bekken_r , pend( angle , a ) , 0 , 0
  RotateEntity h\knee_r , pend( angle - 90 , a ) - a , 0 , 0

End Function
</pre> <br><br></td></tr></table><br>
<a name="1237264"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >bluatigro</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> update :<br>- now whit torus function<br><br>error :<br>- where are my normals ?<br><br>main :<br><pre class=code>
Include "shapes.bb"

Graphics3D 640,480,16,1
SetBuffer BackBuffer()

camera=CreateCamera()
PositionEntity camera,0,0,-200

light=CreateLight()
RotateEntity light,90,0,0

human1.human = New human
createhuman( human1 )
PositionEntity human1\body , -70,0,0
human2.human = New human
createhuman( human2 )
PositionEntity human2\body , 70,0,0

setbox( 0,0,0 , 15,5,5 )
torus = createtorus()


angle = 0

While Not KeyDown( 1 )

  TurnEntity human1\body , 0 , 1 , 0

  human_walk( human1 , angle * 4 , 30 )
  human_walk( human2 , angle * 5 , 30 )

  angle = ( angle + 1 ) Mod 360

  TurnEntity torus , 1 , 0 , 0

  UpdateWorld
  RenderWorld

  Flip

Wend
</pre><br>shapes.bb :<br><pre class=code>
Type tbox
  Field mx#
  Field my#
  Field mz#
  Field dx#
  Field dy#
  Field dz#
End Type

Global box.tbox = New tbox

Function setbox( mx# , my# , mz# , dx# , dy# , dz# )
  box\mx = mx
  box\my = my
  box\mz = mz
  box\dx = dx
  box\dy = dy
  box\dz = dz
End Function

Function CreateBox( mx# , my# , mz# , dx# , dy# , dz# )
  mesh = CreateCube()
  ScaleMesh mesh , dx , dy , dz
  PositionMesh mesh , mx , my , mz
  Return mesh
End Function

Function createtorus()
  Local mesh = CreateMesh()
  local surface = CreateSurface( mesh )
  local mx# = box\mx
  local my# = box\my
  local mz# = box\mz
  local dx# = box\dx
  local dy# = box\dy
  local dz# = box\dz
  For i = -180 To 180 Step 360 / 12
    Local i2 = i + 360 / 12
    For j = -180 To 180 Step 360 / 12
      Local j2 = j + 360 / 12
      Local x# = mx + ( dx + dy * Cos( i ) ) * Cos( j )
      Local y# = my + ( dx + dy * Cos( i ) ) * Sin( j )
      Local z# = mz + Sin( i ) * dz
      Local v0 = AddVertex( surface , x , y , z )
      x = mx + ( dx + dy * Cos( i ) ) * Cos( j2 )
      y = my + ( dx + dy * Cos( i ) ) * Sin( j2 )
      z = mz + Sin( i ) * dz
      Local v1 = AddVertex( surface , x , y , z )
      x = mx + ( dx + dy * Cos( i2 ) ) * Cos( j2 )
      y = my + ( dx + dy * Cos( i2 ) ) * Sin( j2 )
      z = mz + Sin( i2 ) * dz
      Local v2 = AddVertex( surface , x , y , z )
      x = mx + ( dx + dy * Cos( i2 ) ) * Cos( j )
      y = my + ( dx + dy * Cos( i2 ) ) * Sin( j )
      z = mz + Sin( i2 ) * dz
      Local v3 = AddVertex( surface , x , y , z )
      AddTriangle( surface , v0 , v1 , v2 )
      AddTriangle( surface , v0 , v2 , v3 )
    Next ; j
  Next ; i
  Return mesh
End Function

Function pend#( fase# , amp# )
  Return Sin( fase ) * amp
End Function

Type human
  Field body
  Field sholder_l , sholder_r
  Field elbow_l , elbow_r
  Field wrist_l , wrist_r
  Field neck
  Field bekken_l , bekken_r
  Field knee_l , knee_r
  Field enkle_l , enkle_r
End Type

Function createhuman( h.human )

  h\body = CreatePivot( 0 )
  lijf = createbox( 0,0,0 , 20,30,5 )
  EntityParent lijf , h\body

  h\sholder_l = CreatePivot( h\body )
  PositionEntity h\sholder_l , 30,30,0
  arm = createbox( 0,-15,0 , 5,10,5 )
  EntityParent arm , h\sholder_l , False

  h\elbow_l = CreatePivot( h\sholder_l )
  PositionEntity h\elbow_l , 0,-30,0
  uarm = createbox( 0,-15,0 , 5,10,5 )
  EntityParent uarm , h\elbow_l , False

  h\wrist_l = CreatePivot( h\elbow_l )
  PositionEntity h\wrist_l , 0,-30,0
  hand = createbox( 0,-7,0 , 3,7,7 )
  EntityParent hand , h\wrist_l , False

  h\sholder_r = CreatePivot( h\body )
  PositionEntity h\sholder_r , -30,30,0
  arm = createbox( 0,-15,0 , 5,10,5 )
  EntityParent arm , h\sholder_r , False

  h\elbow_r = CreatePivot( h\sholder_r )
  PositionEntity h\elbow_r , 0,-30,0
  uarm = createbox( 0,-15,0 , 5,10,5 )
  EntityParent uarm , h\elbow_r , False

  h\wrist_r = CreatePivot( h\elbow_r )
  PositionEntity h\wrist_r , 0,-30,0
  hand = createbox( 0,-7,0 , 3,7,7 )
  EntityParent hand , h\wrist_r , False

  h\neck = CreatePivot( h\body )
  PositionEntity h\neck , 0,50,0
  ;setbox( 0,0,0 , 10,5,5 )
  ;head = createtorus()
  head = createbox( 0,0,0 , 10,10,10 )
  EntityParent head , h\neck , False

  h\bekken_l = CreatePivot( h\body )
  PositionEntity h\bekken_l , 15,-30,0
  leg = createbox( 0,-15,0 , 5,10,5 )
  EntityParent leg , h\bekken_l , False

  h\knee_l = CreatePivot( h\bekken_l )
  PositionEntity h\knee_l , 0,-30,0
  uleg = createbox( 0,-15,0 , 5,10,5 )
  EntityParent uleg , h\knee_l , False

  h\enkle_l = CreatePivot( h\knee_l )
  PositionEntity h\enkle_l , 0,-30,0
  foot = createbox( 0,-5,-7  , 5,5,10 )
  EntityParent foot , h\enkle_l , False

  h\bekken_r = CreatePivot( h\body )
  PositionEntity h\bekken_r, -15,-30,0
  leg = createbox( 0,-15,0 , 5,10,5 )
  EntityParent leg , h\bekken_r, False

  h\knee_r= CreatePivot( h\bekken_r)
  PositionEntity h\knee_r , 0,-30,0
  uleg = createbox( 0,-15,0 , 5,10,5 )
  EntityParent uleg , h\knee_r, False

  h\enkle_r = CreatePivot( h\knee_r)
  PositionEntity h\enkle_r, 0,-30,0
  foot = createbox( 0,-5,-7  , 5,5,10 )
  EntityParent foot , h\enkle_r, False

End Function

Function human_walk( h.human , angle# , a# )

  RotateEntity h\sholder_l , pend( angle , a ) , 0 , 0
  RotateEntity h\elbow_l , a , 0 , 0
  RotateEntity h\bekken_l , pend( angle + 180 , a ) , 0 , 0
  RotateEntity h\knee_l , pend( angle + 90 , a ) - a , 0 , 0
  RotateEntity h\sholder_r , pend( angle + 180 , a ) , 0 , 0
  RotateEntity h\elbow_r , a , 0 , 0
  RotateEntity h\bekken_r , pend( angle , a ) , 0 , 0
  RotateEntity h\knee_r , pend( angle - 90 , a ) - a , 0 , 0

End Function

</pre> <br><br></td></tr></table><br>
<a name="1237268"></a>

<a name="1237269"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> You can use the [ codebox ] [ /codebox ] tags to create a scrollable text area that displays your code. <br>This way it's easier to follow the thread. <br><br></td></tr></table><br>
<a name="1237274"></a>

<a name="1237275"></a>

<a name="1237276"></a>

<a name="1237277"></a>

<a name="1237278"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> There is another way to create rigged, skinned (only with a 100% influence of a joint over a mesh), animated, characters in code :<br>First step : create a skeleton with joints (pivots)<br> -&gt;There must be one root joint which will be parent of all others joints<br> -&gt;Each other joint must be a child of the root joint or of another joint<br> -&gt;each body part (mesh) will be child of the root joint or of another joint<br> -&gt;The root joint will then be used to turn/move the entity in the world and all others child joints and child meshes will turn/move with it.<br><br>Second step : create the different body parts (meshes) and set each body part as a child of a joint<br><br>Third step : create an animation :<br> -&gt;each animation is composed of several animation frames (poses)<br> -&gt;to prepare an animation frame, you can use rotateentity/positionentity to rotate/position each joint as you want<br> -&gt;to save the animation frame you can use SetAnimKey(RootJoint,FrameId)<br> -&gt;when you have finished to create all animation frames, you can use AddAnimSeq(RootJoint,FramesCount)<br><br>Fourth Step : extract an animation<br> -&gt;you can use AnimationX = extractanimseq(RootJoint,StartFrameId,EndFrameId)<br><br>Fifth step : play/stop the animation<br> -&gt;you can use Animate(RootJoint,Mode%,Speed#,AnimationX)<br><br>Then you can also use CharacterCopy = copyentity(RooJoint) to create many others characters.<br><br>I have an old example somewhere on a dvd-r i will try to find it. <br><br></td></tr></table><br>
<a name="1237296"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bobysait</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Use UpdateNormals (torus), it will calculate them for you.<br>Else, manually use VertexNormal(surface, vertexIndex, nx,ny,nz) for each vertices to set the normals. <br><br></td></tr></table><br>
<a name="1237398"></a>

<a name="1237399"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Example of what i meant :<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D(640,480,32,2)
SetBuffer(BackBuffer())

SeedRnd(MilliSecs())

Origine = CreateCube()
ScaleMesh(Origine,0.01/2,0.01/2,0.01/2)
EntityColor(Origine,255,000,255)

Camera = CreateCamera()
CameraRange(Camera,0.1,100)

;Create a skeleton with joints
JointRoot = CreatePivot()
PositionEntity(JointRoot,0,0,0)
MoveEntity(JointRoot,0,0,0)
NameEntity(JointRoot,"JRoot")

JointHipM = CreatePivot()
PositionEntity(JointHipM,0,0.9,0)
EntityParent(JointHipM,JointRoot,True)
NameEntity(JointHipM,"JHipM")
Debug_JointHipM = CreateSphere(8)
ScaleMesh(Debug_JointHipM,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointHipM,EntityX(JointHipM,True),EntityY(JointHipM,True),EntityZ(JointHipM,True))
EntityParent(Debug_JointHipM,JointHipM,True)

JointHipL = CreatePivot()
PositionEntity(JointHipL,-0.125,0.85,0)
EntityParent(JointHipL,JointHipM,True)
NameEntity(JointHipL,"JHipL")
Debug_JointHipL = CreateSphere(8)
ScaleMesh(Debug_JointHipL,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointHipL,EntityX(JointHipL,True),EntityY(JointHipL,True),EntityZ(JointHipL,True))
EntityParent(Debug_JointHipL,JointHipL,True)

JointKneeL = CreatePivot()
PositionEntity(JointKneeL,-0.125,0.45,0)
EntityParent(JointKneeL,JointHipL,True)
NameEntity(JointKneeL,"JKneeL")
Debug_JointKneeL = CreateSphere(8)
ScaleMesh(Debug_JointKneeL,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointKneeL,EntityX(JointKneeL,True),EntityY(JointKneeL,True),EntityZ(JointKneeL,True))
EntityParent(Debug_JointKneeL,JointKneeL,True)

JointAnkleL = CreatePivot()
PositionEntity(JointAnkleL,-0.125,0.075,0)
EntityParent(JointAnkleL,JointKneeL,True)
NameEntity(JointAnkleL,"JAnkleL")
Debug_JointAnkleL = CreateSphere(8)
ScaleMesh(Debug_JointAnkleL,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointAnkleL,EntityX(JointAnkleL,True),EntityY(JointAnkleL,True),EntityZ(JointAnkleL,True))
EntityParent(Debug_JointAnkleL,JointAnkleL,True)

JointHipR = CreatePivot()
PositionEntity(JointHipR,0.125,0.85,0)
EntityParent(JointHipR,JointHipM,True)
NameEntity(JointHipR,"JHipR")
Debug_JointHipR = CreateSphere(8)
ScaleMesh(Debug_JointHipR,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointHipR,EntityX(JointHipR,True),EntityY(JointHipR,True),EntityZ(JointHipR,True))
EntityParent(Debug_JointHipR,JointHipR,True)

JointKneeR = CreatePivot()
PositionEntity(JointKneeR,0.125,0.45,0)
EntityParent(JointKneeR,JointHipR,True)
NameEntity(JointKneeR,"JKneeR")
Debug_JointKneeR = CreateSphere(8)
ScaleMesh(Debug_JointKneeR,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointKneeR,EntityX(JointKneeR,True),EntityY(JointKneeR,True),EntityZ(JointKneeR,True))
EntityParent(Debug_JointKneeR,JointKneeR,True)

JointAnkleR = CreatePivot()
PositionEntity(JointAnkleR,0.125,0.075,0)
EntityParent(JointAnkleR,JointKneeR,True)
NameEntity(JointAnkleR,"JAnkleR")
Debug_JointAnkleR = CreateSphere(8)
ScaleMesh(Debug_JointAnkleR,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointAnkleR,EntityX(JointAnkleR,True),EntityY(JointAnkleR,True),EntityZ(JointAnkleR,True))
EntityParent(Debug_JointAnkleR,JointAnkleR,True)

JointVertebraM = CreatePivot()
PositionEntity(JointVertebraM,0,1.1,0)
EntityParent(JointVertebraM,JointHipM,True)
NameEntity(JointVertebraM,"JVertebraM")
Debug_JointVertebraM = CreateSphere(8)
ScaleMesh(Debug_JointVertebraM,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointVertebraM,EntityX(JointVertebraM,True),EntityY(JointVertebraM,True),EntityZ(JointVertebraM,True))
EntityParent(Debug_JointVertebraM,JointVertebraM,True)

JointShoulderL = CreatePivot()
PositionEntity(JointShoulderL,-0.175,1.45,0)
EntityParent(JointShoulderL,JointVertebraM,True)
NameEntity(JointShoulderL,"JShoulderL")
Debug_JointShoulderL = CreateSphere(8)
ScaleMesh(Debug_JointShoulderL,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointShoulderL,EntityX(JointShoulderL,True),EntityY(JointShoulderL,True),EntityZ(JointShoulderL,True))
EntityParent(Debug_JointShoulderL,JointShoulderL,True)

JointElbowL = CreatePivot()
PositionEntity(JointElbowL,-0.175,1.2,0)
EntityParent(JointElbowL,JointShoulderL,True)
NameEntity(JointElbowL,"JElbowL")
Debug_JointElbowL = CreateSphere(8)
ScaleMesh(Debug_JointElbowL,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointElbowL,EntityX(JointElbowL,True),EntityY(JointElbowL,True),EntityZ(JointElbowL,True))
EntityParent(Debug_JointElbowL,JointElbowL,True)

JointWristL = CreatePivot()
PositionEntity(JointWristL,-0.175,0.95,0)
EntityParent(JointWristL,JointElbowL,True)
NameEntity(JointWristL,"JWristL")
Debug_JointWristL = CreateSphere(8)
ScaleMesh(Debug_JointWristL,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointWristL,EntityX(JointWristL,True),EntityY(JointWristL,True),EntityZ(JointWristL,True))
EntityParent(Debug_JointWristL,JointWristL,True)

JointShoulderR = CreatePivot()
PositionEntity(JointShoulderR,0.175,1.45,0)
EntityParent(JointShoulderR,JointVertebraM,True)
NameEntity(JointShoulderR,"JShoulderR")
Debug_JointShoulderR = CreateSphere(8)
ScaleMesh(Debug_JointShoulderR,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointShoulderR,EntityX(JointShoulderR,True),EntityY(JointShoulderR,True),EntityZ(JointShoulderR,True))
EntityParent(Debug_JointShoulderR,JointShoulderR,True)

JointElbowR = CreatePivot()
PositionEntity(JointElbowR,0.175,1.2,0)
EntityParent(JointElbowR,JointShoulderR,True)
NameEntity(JointElbowR,"JElbowR")
Debug_JointElbowR = CreateSphere(8)
ScaleMesh(Debug_JointElbowR,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointElbowR,EntityX(JointElbowR,True),EntityY(JointElbowR,True),EntityZ(JointElbowR,True))
EntityParent(Debug_JointElbowR,JointElbowR,True)

JointWristR = CreatePivot()
PositionEntity(JointWristR,0.175,0.95,0)
EntityParent(JointWristR,JointElbowR,True)
NameEntity(JointWristR,"JWristR")
Debug_JointWristR = CreateSphere(8)
ScaleMesh(Debug_JointWristR,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointWristR,EntityX(JointWristR,True),EntityY(JointWristR,True),EntityZ(JointWristR,True))
EntityParent(Debug_JointWristR,JointWristR,True)

JointNeck = CreatePivot()
PositionEntity(JointNeck,0,1.45,0)
EntityParent(JointNeck,JointVertebraM,True)
NameEntity(JointNeck,"JNeck")
Debug_JointNeck = CreateSphere(8)
ScaleMesh(Debug_JointNeck,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointNeck,EntityX(JointNeck,True),EntityY(JointNeck,True),EntityZ(JointNeck,True))
EntityParent(Debug_JointNeck,JointNeck,True)

JointHead = CreatePivot()
PositionEntity(JointHead,0,1.6,0)
EntityParent(JointHead,JointNeck,True)
NameEntity(JointHead,"JHead")
Debug_JointHead = CreateSphere(8)
ScaleMesh(Debug_JointHead,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointHead,EntityX(JointHead,True),EntityY(JointHead,True),EntityZ(JointHead,True))
EntityParent(Debug_JointHead,JointHead,True)

;Create body parts and set each body part as a child of a joint

;Pelvis

;UpperLegL

;LowerLegL

;FootL

;UpperLegR

;LowerLegR

;FootR

;StomachLowerBack

;ChestUpperBack

;UpperArmL

;LowerArmL

;HandL

;UpperArmR

;LowerArmR

;HandR

;Neck

;Head

;Create an animation of the human character with animation frames/poses
FrameId% = 0

;Animation StandIdle
;new frame : 
FrameId = FrameId + 1 
;turn move the joints : 
RotateEntity(JointRoot,0,0,0)
RotateEntity(JointHipM,0,0,0)
RotateEntity(JointHipL,0,0,0) 
RotateEntity(JointKneeL,0,0,0)
RotateEntity(JointAnkleL,0,0,0)
RotateEntity(JointHipR,0,0,0)
RotateEntity(JointKneeR,0,0,0)
RotateEntity(JointAnkleR,0,0,0)
RotateEntity(JointVertebraM,0,0,0)
RotateEntity(JointShoulderL,0,0,0) 
RotateEntity(JointElbowL,0,0,0) 
RotateEntity(JointWristL,0,0,0) 
RotateEntity(JointShoulderR,0,0,0)
RotateEntity(JointElbowR,0,0,0) 
RotateEntity(JointWristR,0,0,0) 
RotateEntity(JointNeck,0,0,0)
RotateEntity(JointHead,0,0,0)
;capture the frame :  
SetAnimKey(JointRoot,FrameId) 
SetAnimKey(JointHipM,FrameId)
SetAnimKey(JointHipL,FrameId)
SetAnimKey(JointKneeL,FrameId)
SetAnimKey(JointAnkleL,FrameId)
SetAnimKey(JointHipR,FrameId)
SetAnimKey(JointKneeR,FrameId)
SetAnimKey(JointAnkleR,FrameId)
SetAnimKey(JointVertebraM,FrameId) 
SetAnimKey(JointShoulderL,FrameId)
SetAnimKey(JointElbowL,FrameId)
SetAnimKey(JointWristL,FrameId)
SetAnimKey(JointShoulderR,FrameId)
SetAnimKey(JointElbowR,FrameId)
SetAnimKey(JointWristR,FrameId)
SetAnimKey(JointNeck,FrameId)
SetAnimKey(JointHead,FrameId)

;Animation WalkForward
;new frame :
FrameId = FrameId + 1 
;turn move the joints : 
RotateEntity(JointRoot,0,0,0)
RotateEntity(JointHipM,0,-10,-5)
RotateEntity(JointHipL,33.75,0,0) 
RotateEntity(JointKneeL,22.5,0,0)
RotateEntity(JointAnkleL,11.25,0,0)
RotateEntity(JointHipR,-45,0,0)
RotateEntity(JointKneeR,0,0,0)
RotateEntity(JointAnkleR,0,0,0)
RotateEntity(JointVertebraM,0,-12,5)
RotateEntity(JointShoulderL,-33.75,0,0) 
RotateEntity(JointElbowL,-45,0,0) 
RotateEntity(JointWristL,-5,0,0) 
RotateEntity(JointShoulderR,22.75,0,0)
RotateEntity(JointElbowR,0,0,0) 
RotateEntity(JointWristR,0,0,0) 
RotateEntity(JointNeck,0,5,-2.5)
RotateEntity(JointHead,0,-10,0)
;capture the frame : 
SetAnimKey(JointRoot,FrameId) 
SetAnimKey(JointHipM,FrameId)
SetAnimKey(JointHipL,FrameId)
SetAnimKey(JointKneeL,FrameId)
SetAnimKey(JointAnkleL,FrameId)
SetAnimKey(JointHipR,FrameId)
SetAnimKey(JointKneeR,FrameId)
SetAnimKey(JointAnkleR,FrameId)
SetAnimKey(JointVertebraM,FrameId) 
SetAnimKey(JointShoulderL,FrameId)
SetAnimKey(JointElbowL,FrameId)
SetAnimKey(JointWristL,FrameId)
SetAnimKey(JointShoulderR,FrameId)
SetAnimKey(JointElbowR,FrameId)
SetAnimKey(JointWristR,FrameId)
SetAnimKey(JointNeck,FrameId)
SetAnimKey(JointHead,FrameId)
;new frame :
FrameId = FrameId + 1 
;turn move the joints :
RotateEntity(JointRoot,0,0,0)
RotateEntity(JointHipM,0,0,0)
RotateEntity(JointHipL,0,0,0) 
RotateEntity(JointKneeL,0,0,0)
RotateEntity(JointAnkleL,0,0,0)
RotateEntity(JointHipR,0,0,0)
RotateEntity(JointKneeR,0,0,0)
RotateEntity(JointAnkleR,0,0,0)
RotateEntity(JointVertebraM,0,0,0)
RotateEntity(JointShoulderL,0,0,0) 
RotateEntity(JointElbowL,0,0,0) 
RotateEntity(JointWristL,0,0,0) 
RotateEntity(JointShoulderR,0,0,0)
RotateEntity(JointElbowR,0,0,0) 
RotateEntity(JointWristR,0,0,0) 
RotateEntity(JointNeck,0,0,0)
RotateEntity(JointHead,0,0,0)
;capture the frame :  
SetAnimKey(JointRoot,FrameId) 
SetAnimKey(JointHipM,FrameId)
SetAnimKey(JointHipL,FrameId)
SetAnimKey(JointKneeL,FrameId)
SetAnimKey(JointAnkleL,FrameId)
SetAnimKey(JointHipR,FrameId)
SetAnimKey(JointKneeR,FrameId)
SetAnimKey(JointAnkleR,FrameId)
SetAnimKey(JointVertebraM,FrameId) 
SetAnimKey(JointShoulderL,FrameId)
SetAnimKey(JointElbowL,FrameId)
SetAnimKey(JointWristL,FrameId)
SetAnimKey(JointShoulderR,FrameId)
SetAnimKey(JointElbowR,FrameId)
SetAnimKey(JointWristR,FrameId)
SetAnimKey(JointNeck,FrameId)
SetAnimKey(JointHead,FrameId)
;new frame :
FrameId = FrameId + 1 
;turn move the joints : 
RotateEntity(JointRoot,0,0,0)
RotateEntity(JointHipM,0,10,5)
RotateEntity(JointHipL,-45,0,0) 
RotateEntity(JointKneeL,0,0,0)
RotateEntity(JointAnkleL,0,0,0)
RotateEntity(JointHipR,33.75,0,0)
RotateEntity(JointKneeR,22.5,0,0)
RotateEntity(JointAnkleR,11.25,0,0)
RotateEntity(JointVertebraM,0,-10,-5)
RotateEntity(JointShoulderL,22.75,0,0) 
RotateEntity(JointElbowL,0,0,0) 
RotateEntity(JointWristL,0,0,0) 
RotateEntity(JointShoulderR,-33.75,0,0)
RotateEntity(JointElbowR,-45,0,0) 
RotateEntity(JointWristR,-5,0,0) 
RotateEntity(JointNeck,0,-5,-2.5)
RotateEntity(JointHead,0,10,0)

;capture the frame :  
SetAnimKey(JointRoot,FrameId) 
SetAnimKey(JointHipM,FrameId)
SetAnimKey(JointHipL,FrameId)
SetAnimKey(JointKneeL,FrameId)
SetAnimKey(JointAnkleL,FrameId)
SetAnimKey(JointHipR,FrameId)
SetAnimKey(JointKneeR,FrameId)
SetAnimKey(JointAnkleR,FrameId)
SetAnimKey(JointVertebraM,FrameId) 
SetAnimKey(JointShoulderL,FrameId)
SetAnimKey(JointElbowL,FrameId)
SetAnimKey(JointWristL,FrameId)
SetAnimKey(JointShoulderR,FrameId)
SetAnimKey(JointElbowR,FrameId)
SetAnimKey(JointWristR,FrameId)
SetAnimKey(JointNeck,FrameId)
SetAnimKey(JointHead,FrameId)
;new frame : 
FrameId = FrameId + 1 
;turn move the joints : 
RotateEntity(JointRoot,0,0,0)
RotateEntity(JointHipM,0,0,0)
RotateEntity(JointHipL,0,0,0) 
RotateEntity(JointKneeL,0,0,0)
RotateEntity(JointAnkleL,0,0,0)
RotateEntity(JointHipR,0,0,0)
RotateEntity(JointKneeR,0,0,0)
RotateEntity(JointAnkleR,0,0,0)
RotateEntity(JointVertebraM,0,0,0)
RotateEntity(JointShoulderL,0,0,0) 
RotateEntity(JointElbowL,0,0,0) 
RotateEntity(JointWristL,0,0,0) 
RotateEntity(JointShoulderR,0,0,0)
RotateEntity(JointElbowR,0,0,0) 
RotateEntity(JointWristR,0,0,0) 
RotateEntity(JointNeck,0,0,0)
RotateEntity(JointHead,0,0,0)
;capture the frame :  
SetAnimKey(JointRoot,FrameId) 
SetAnimKey(JointHipM,FrameId)
SetAnimKey(JointHipL,FrameId)
SetAnimKey(JointKneeL,FrameId)
SetAnimKey(JointAnkleL,FrameId)
SetAnimKey(JointHipR,FrameId)
SetAnimKey(JointKneeR,FrameId)
SetAnimKey(JointAnkleR,FrameId)
SetAnimKey(JointVertebraM,FrameId) 
SetAnimKey(JointShoulderL,FrameId)
SetAnimKey(JointElbowL,FrameId)
SetAnimKey(JointWristL,FrameId)
SetAnimKey(JointShoulderR,FrameId)
SetAnimKey(JointElbowR,FrameId)
SetAnimKey(JointWristR,FrameId)
SetAnimKey(JointNeck,FrameId)
SetAnimKey(JointHead,FrameId)

;Save the captured frames in an animation sequence : 
AddAnimSeq(JointRoot,FrameId) 

;Load the animation sequences
AnimationStandIdle = ExtractAnimSeq(JointRoot,0,0)  
AnimationWalkForward = ExtractAnimSeq(JointRoot,1,5)  

PositionEntity(Camera,1.5,0.875,1.5)
RotateEntity(Camera,0,135,0)

While(KeyDown(1)&lt;&gt;1)
 
 If(KeyDown(30)=1)
  TurnEntity(Camera,0,1.5,0)
 ElseIf(KeyDown(32)=1)
  TurnEntity(Camera,0,-1.5,0)
 EndIf
 If(KeyDown(17)=1)
  MoveEntity(Camera,0,0,0.075)
 ElseIf(KeyDown(31)=1)
  MoveEntity(Camera,0,0,-0.075)
 EndIf

 ;animate the skeleton (the root joint)
 If(KeyDown(57)=0)
  If(Not Animating(JointRoot))
   Animate(JointRoot,3,0.1,AnimationStandIdle)
  EndIf 
 ElseIf(KeyDown(57)=1)
  If(Not Animating(JointRoot))
   Animate(JointRoot,3,0.1,AnimationWalkForward)
  EndIf 
 EndIf

 UpdateWorld()

 If(KeyDown(2)=0)
  Wireframe(False)
 ElseIf(KeyDown(2)=1)
  Wireframe(True)
 EndIf 

 RenderWorld()

 Flip(1)

Wend

End()
</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
