<!DOCTYPE html><html lang="en" ><head ><title >TPS camera with mouse and collision control</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >TPS camera with mouse and collision control</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >TPS camera with mouse and collision control</a><br><br>
<a name="664425"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mike0101</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here is my code, I hope somebody  modify for better<br><br><br><br>;*************<br>;by Mike0101<br>;I'm sure somebody makes  better this weak code<br>;but this code works right<br>;*************<br>;System<br>Global screen_width% = 1024<br>Global screen_height% = 768<br>Global screen_halfwidth% = screen_width/2<br>Global screen_halfheight% = screen_height/2<br>Global camHeight#=4 ; default height of the camera behind the player<br>Global camDist#=-7 ; default distance of the camera behind the player<br>Global dest_xang# <br>Global dest_yang# <br><br>;CONST<br>Const C_PLAYER=1, C_LEVEL=2<br><br>AppTitle "TPS Camera"<br>HidePointer<br><br><br>;Screen<br>Graphics3D screen_width,screen_height,32,2<br>SetBuffer BackBuffer()<br><br>;Light<br>light=CreateLight()<br><br>;Plan<br>plan=CreatePlane()<br>EntityColor plan,255,0,0<br>EntityType plan, C_LEVEL<br><br>;OBJECT<br>cube1=CreateCube()<br>ScaleEntity cube1,10,10,10<br>PositionEntity cube1,13,0,13<br>EntityType cube1,C_LEVEL<br><br><br>;CAMERA<br>camera =CreateCamera()<br><br><br>;Player Pivot<br>plpivot=CreatePivot()<br>EntityType plpivot, C_PLAYER<br>EntityRadius plpivot,1.8,3<br>PositionEntity plpivot, 0, 1,0<br><br><br>;Player, now a cube but you can change for a nice model<br>Player=CreateCube(plpivot)<br>ScaleEntity player,1,3,1<br>EntityColor Player,255,0,255<br><br>;I'm using 2 pivots for camera controlling<br><br>;Pivot for the camera controlling<br>;One Front of the player,<br>FrontPivot=CreatePivot(plpivot)<br>PositionEntity Frontpivot,0,4,10<br><br>;One behind the player<br>;This pivot controlls the collisions and "finds" the right position of the camera behind player<br>Backpivot=CreatePivot(plpivot)<br>EntityRadius Backpivot,1<br>EntityType BackPivot, C_PLAYER<br><br><br>Collisions C_PLAYER, C_LEVEL ,2,2<br><br>While Not KeyHit(1)<br> <br>  UpdateCamera(plpivot,MouseXSpeed(),MouseYSpeed(), Frontpivot, Backpivot, Camera)<br><br>  UpdateWorld<br>  RenderWorld<br>  ; Crosshair :)<br>  Text screen_halfwidth%, screen_halfheight%, "+"<br>   Flip<br>Wend<br><br>End<br><br><br><br>Function updatecamera(ent,mxs#, mys#, pivot1, pivothatso, camera)<br>;ent : Player pivot<br>;mxs: MouseXmove<br>;mys: MouseYmove<br>;pivot1: pivot before player<br>;pivothatso: pivot front player<br>;Camera: ....What do you think:)<br>       MoveMouse screen_halfwidth%, screen_halfheight%<br>  <br>     ; Limit of the "look up", "look down"  <br>       If dest_xang + mys &gt; 120 Or dest_xang + mys &lt; -45 Then ;<br>        mys=0 <br>       EndIf<br><br>      dest_xang# = dest_xang + mys<br><br>      ; When the player looks up, the front pivot moves UP, when looks down the back pivot moves down<br>      TranslateEntity pivot1,0,-MYS#*.15,0<br> <br>       angle#=  Sin(EntityPitch(camera))*10<br>      tempkkammag#=(camheight + angle)<br>      tempkkamtav#=   camdist - Abs( angle) <br><br>      dest_yang# = dest_yang - (mxs*.5)<br> <br>       RotateEntity ent, 0, dest_yang,0<br>     <br>       ;Velocity of camera movement and position according of collision<br>            dx#=(EntityX(pivothatso,True)-EntityX(camera,True)) * .1<br>	     dy#=(EntityY(pivothatso,True)-EntityY(camera,True)) *.9<br>            dz#=(EntityZ(pivothatso,True)-EntityZ(camera,True)) *.1<br>	<br>	      TranslateEntity camera,dx,dy,dz<br>        PointEntity camera,pivot1<br>	      PositionEntity pivothatso,0,0,0<br>            ResetEntity pivothatso<br>       	PositionEntity pivothatso,0,tempkkammag,tempkkamtav<br>End Function <br><br></td></tr></table><br>
<a name="664452"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sir Gak</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Code seems fine.  When posting code, though, it is best to use the code brackets before and after your code, as follows (but without the spaces):<br>[ code ]<br>[ /code ] <br><br></td></tr></table><br>
<a name="664455"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mike0101</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> More smooth with modified paramerets<br><br><br><br><pre class=code>

;*************
;by Mike0101
;I'm sure somebody makes  better this weak code
;but this code works right
;*************
;System
Global screen_width% = 1024
Global screen_height% = 768
Global screen_halfwidth% = screen_width/2
Global screen_halfheight% = screen_height/2
Global camHeight#=4 ; default height of the camera behind the player
Global camDist#=-8 ; default distance of the camera behind the player
Global dest_xang# 
Global dest_yang# 

;CONST
Const C_PLAYER=1, C_LEVEL=2

AppTitle "TPS Camera"
HidePointer


;Screen
Graphics3D screen_width,screen_height,32,2
SetBuffer BackBuffer()

;Light
light=CreateLight()

;Plan
plan=CreatePlane()
EntityColor plan,255,0,0
EntityType plan, C_LEVEL

;OBJECT
cube1=CreateCube()
ScaleEntity cube1,10,10,10
PositionEntity cube1,13,0,13
EntityType cube1,C_LEVEL


;CAMERA
camera =CreateCamera()


;Player Pivot
plpivot=CreatePivot()
EntityType plpivot, C_PLAYER
EntityRadius plpivot,1.8,3
PositionEntity plpivot, 0, 1,0


;Player, now a cube but you can change for a nice model
Player=CreateCube(plpivot)
ScaleEntity player,1,3,.7
EntityColor Player,255,0,255

;I'm using 2 pivots for camera controlling

;Pivot for the camera controlling
;One Front of the player,
FrontPivot=CreatePivot(plpivot)
PositionEntity Frontpivot,0,4,10

;One behind the player
;This pivot controlls the collisions and "finds" the right position of the camera behind player
Backpivot=CreatePivot(plpivot)
EntityRadius Backpivot,1
EntityType BackPivot, C_PLAYER


Collisions C_PLAYER, C_LEVEL ,2,2

While Not KeyHit(1)
 
  UpdateCamera(plpivot,MouseXSpeed(),MouseYSpeed(), Frontpivot, Backpivot, Camera)

  UpdateWorld
  RenderWorld
  ; Crosshair :)
  Text screen_halfwidth%, screen_halfheight%, "+"
   Flip
Wend

End



Function updatecamera(ent,mxs#, mys#, pivot1, pivothatso, camera)
;ent : Player pivot
;mxs: MouseXmove
;mys: MouseYmove
;pivot1: pivot before player
;pivothatso: pivot front player
;Camera: ....What do you think:)
       MoveMouse screen_halfwidth%, screen_halfheight%
  
     ; Limit of the "look up", "look down"  
       If dest_xang + mys &gt; 120 Or dest_xang + mys &lt; -45 Then ;
        mys=0 
       EndIf

      dest_xang# = dest_xang + mys

      ; When the player looks up, the front pivot moves UP, when looks down the back pivot moves down
      TranslateEntity pivot1,0,-MYS#*.15,0
 
       angle#=  Sin(EntityPitch(camera))*6
      tempkkammag#=(camheight + angle)
      tempkkamtav#=   camdist + Abs( angle) 

      dest_yang# = dest_yang - (mxs*.5)
 
       RotateEntity ent, 0, dest_yang,0
     
       ;Velocity of camera movement and position according of collision
            dx#=(EntityX(pivothatso,True)-EntityX(camera,True)) * .1
	     dy#=(EntityY(pivothatso,True)-EntityY(camera,True)) *.9
            dz#=(EntityZ(pivothatso,True)-EntityZ(camera,True)) *.1
	
	      TranslateEntity camera,dx,dy,dz
        PointEntity camera,pivot1
	      PositionEntity pivothatso,0,0,0
            ResetEntity pivothatso
       	PositionEntity pivothatso,0,tempkkammag,tempkkamtav
End Function
</pre> <br><br></td></tr></table><br>
<a name="664980"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chad</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok Mike, what do you need help with on collisions.. I'll do my best :-D <br><br></td></tr></table><br>
<a name="665438"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chad</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Mike, when you put in code to move the box, the camera doesn't move with the box..<br><br>For example, I put this code to test it and it didn't work.<br><pre class=code>
While Not KeyHit(1)

If KeyHit(2) Then ;The 1 button on keyboard
MoveEntity Player,.2,.2,.2
End If

UpdateCamera(plpivot,MouseXSpeed(),MouseYSpeed(), Frontpivot, Backpivot, Camera)

  UpdateWorld
  RenderWorld
  ; Crosshair :)
  Text screen_halfwidth%, screen_halfheight%, "+"
   Flip
Wend

End
</pre><br><br>Do you have a solution to that??<br><br>It's overall a good little camera, and I quite like it and modelled my camera more like yours after playin around with this..  But just this little move entity is what gets me..<br><br>Thanks,<br>Chad <br><br></td></tr></table><br>
<a name="665445"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >mindstorms</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> The way he has got it set up, the pivots are positioned "absolutly".  For it to be able to move, the pivots have to be positioned according to the player's coordinates....Maybe make the player the parent of the pivots instead of the the pivots the parent to the player... <br><br></td></tr></table><br>
<a name="665456"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mike0101</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Chad<br><br>you must move the plpivot object instead of the player (cube).<br><br>Change this:<br><br>If KeyHit(2) Then ;The 1 button on keyboard<br>MoveEntity Player,.2,.2,.2<br>End If<br><br>for this<br><br>If KeyHit(2) Then ;The 1 button on keyboard<br>MoveEntity Plpivot,.2,.2,.2<br>End If<br><br><br>By the way I will send a new sample. <br><br></td></tr></table><br>
<a name="665458"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mike0101</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Move WSAD, jump SPACE or RMB<br><br><br><pre class=code>

;*************
;3rd person camera control with move, jump, collison
;by Mike0101
;I'm sure somebody makes  better this weak code
;but this code works right
;*************
Type Timer
	Field start
	Field timeOut
	Field vel_start#;=90
	Field vel_down#;=-2
End Type

;System
Global AppFPS =45
Global screen_width% = 1024
Global screen_height% = 768
Global screen_halfwidth% = screen_width/2
Global screen_halfheight% = screen_height/2
Global camHeight#=4.5  ; default height of the camera behind player
Global camDist#=-7 ; default distance of the camera behind player
Global dest_xang# 
Global dest_yang# 
Global pl_gravity#=-.5
Global pl_jump=0
Global Jump_Move#=0
Global Move_For_Back#
Global Move_Left_Right#
Global Move_Slow#=.1
;CONST
Const C_PLAYER=1, C_LEVEL=2

AppTitle "TPS Camera movement"
HidePointer

;Screen
Graphics3D screen_width,screen_height,32,1
SetBuffer BackBuffer()

CreateWorld()

;CAMERA
camera =CreateCamera()

;Player Pivot
plpivot=CreatePivot()
EntityType plpivot, C_PLAYER
EntityRadius plpivot,1.8,3
PositionEntity plpivot, 0, 3,0

;Player, now a cube but you can change for a nice model
Player=CreateCube(plpivot)
ScaleEntity player,1,3,.7
MoveEntity player,0,1,0
EntityColor Player,255,0,255

;I'm using 2 pivots for camera controlling
;Pivot for the camera controlling
;One Front of the player,
FrontPivot=CreatePivot(plpivot)
PositionEntity Frontpivot,0,4,10

;One behind player
;This pivot controlls the collisions and "finds" the right position of the camera behind player
Backpivot=CreatePivot(plpivot)
EntityRadius Backpivot,1
EntityType BackPivot, C_PLAYER


Collisions C_PLAYER, C_LEVEL ,2,2


; -------------
; Main loop
; -------------
FramePeriod = 1000 / AppFPS
FrameTime = MilliSecs () - FramePeriod
While Not KeyHit(1)
 
; --------------------------
	; Frame rate limiting
	; --------------------------
	Repeat
		FrameElapsed = MilliSecs () - FrameTime
	Until FrameElapsed

	FrameTicks = FrameElapsed / FramePeriod
	FrameTween# = Float (FrameElapsed Mod FramePeriod) / Float (FramePeriod)

	; -----------------------
	; Update tweening
	; -----------------------
	For FrameLimit = 1 To FrameTicks
		If FrameLimit = FrameTicks Then
			CaptureWorld
		EndIf
		FrameTime = FrameTime + FramePeriod
		
              UpdateCamera(plpivot,MouseXSpeed(),MouseYSpeed(), Frontpivot, Backpivot, Camera)
              UpdatePosition(plpivot)
              UpdateWorld

      Next
     
  RenderWorld FrameTween
  ; Crosshair :)
  Text screen_halfwidth%, screen_halfheight%, "+"

  Flip 0

Wend

End


Function updatecamera(ent,mxs#, mys#, pivot1, pivothatso, camera)
;ent : Player pivot
;mxs: MouseXmove
;mys: MouseYmove
;pivot1: pivot before player
;pivothatso: pivot behind player
;Camera: 

    MoveMouse screen_halfwidth%, screen_halfheight%
  
    ; Limit of the "look up", "look down"  
    If dest_xang + mys &gt; 120 Or dest_xang + mys &lt; -45 Then ;
        mys=0 
    EndIf


      dest_xang# = dest_xang + mys

      ; When the player looks up, the front pivot moves UP, when looks down the back pivot moves down
      TranslateEntity pivot1,0,-MYS#*.15,0
 
     angle#=  Sin(EntityPitch(camera))*6
      tempkkammag#=camheight + (angle*.8)
      tempkkamtav#=   camdist  + Abs( angle) 

      dest_yang# = dest_yang - (mxs*.5)
 
       RotateEntity ent, 0, dest_yang,0
     
        dx#=(EntityX(pivothatso,True)-EntityX(camera,True)) *.1
	dy#=(EntityY(pivothatso,True)-EntityY(camera,True)) *.9
	dz#=(EntityZ(pivothatso,True)-EntityZ(camera,True)) *.1
	
	TranslateEntity camera,dx,dy,dz
        PointEntity camera,pivot1
	PositionEntity pivothatso,0,0,0
	ResetEntity pivothatso

       	PositionEntity pivothatso,0,tempkkammag,tempkkamtav
End Function


Function UpdatePosition(ent)
;MOVEMENT
If pl_jump=0
  If KeyDown(200) Or KeyDown(17) Then 
	Move_For_Back=.3
  End If
  If KeyDown (208) Or KeyDown(31) Then
	Move_For_Back=-.2
  End If
  If KeyDown(203) Or KeyDown(30) Then 
      Move_Left_Right=-.2
  End If
  If KeyDown (205) Or KeyDown(32) Then
       Move_Left_Right=.2
  End If
EndIf

If ( MouseHit(2)  Or KeyHit(57))  And pl_jump=0
     t.Timer = JumpTimer (1500)
    ;When jumps slides in the air doesn't stop quickly
     Move_Slow#=.999
     pl_jump=1
EndIf

If jump_timeout()=1
    pl_jump=0
   ;when goes stops quickly
    Move_Slow#=.1
EndIf

   MoveEntity ent, Move_Left_Right, Jump_Move, Move_For_Back
  TranslateEntity ent,0,pl_gravity,0

  ;Smooth movement
  move_left_right = move_left_right * move_slow
  move_for_back = move_for_back  * move_slow

End Function

Function JumpTimer.timer (timeOut)
	t.Timer = New Timer
	t\start   = MilliSecs ()
	t\timeOut = t\start + timeOut
        t\vel_start#=90
        t\vel_down#=-2
	Return t
End Function

; Check for timeout:
Function Jump_TimeOut ()
	For test.timer= Each timer
		If test\timeOut &lt; MilliSecs ()
 		       Jump_move=0
			Delete test
			Return 1
	      Else
	           Jump_Move=Sin(test\vel_start#)
	            test\vel_start=test\vel_start#- test\vel_down#
        	   Return 0
       	EndIf
     Next		
End Function


Function CreateWorld()
;Light
light=CreateLight(1)
PositionEntity light ,0,50,0

;Plan
plan=CreatePlane()
EntityColor plan,255,0,0
EntityType plan, C_LEVEL

;OBJECT
cube1=CreateCube()
ScaleEntity cube1,10,10,10
PositionEntity cube1,13,0,13
EntityType cube1,C_LEVEL

cube2=CreateCube()
ScaleEntity cube2,20,20,20
PositionEntity cube2,33,0,33
EntityType cube2,C_LEVEL

;bridge right side
cube3=CreateCube()
ScaleEntity cube3,20,20,15
PositionEntity cube3,83,0,33
EntityType cube3,C_LEVEL

;bridge
cube4=CreateCube()
ScaleEntity cube4,5,2,20
PositionEntity cube4,83,18,63
EntityType cube4,C_LEVEL

cube5=CreateCube()
ScaleEntity cube5,30,15,35
PositionEntity cube5,153,0,33
EntityType cube5,C_LEVEL

;Bridge left side
cube6=CreateCube()
ScaleEntity cube6,20,20,15
PositionEntity cube6,83,0,93 
EntityType cube6,C_LEVEL
End Function
</pre> <br><br></td></tr></table><br>
<a name="665547"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >mindstorms</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> That's neat!  <br><br>Sorry about my misleading advice, buggy :( <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
