<!DOCTYPE html><html lang="en" ><head ><title >write b3d files from code</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >write b3d files from code</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >write b3d files from code</a><br><br>
<a name="1246453"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Prym</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I don't want to use logiciels . <br>I want to save a created mesh into a b3d file .  <br>Is there someone who can give me a complete example ( with b3d utils ) <br>Their examples don't show all I need . <br>I don't find how to define textures and brushes . <br>My mesh, writen with "Function WriteBB3D( f_name$,mesh )", can not be load with the loadmesh() blitz3d command . <br><br>Thank you for your help . <br><br></td></tr></table><br>
<a name="1246660"></a>

<a name="1246729"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Charrua</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> hi, i work a little with b3d file format when doing an editor<br><br>i started isolating the most important part of my save_b3d<br><br>and finally get an stand alone .bb<br><br>hope it helps<br><br>you need blitz3D+.dll (which is included in the zip file)<br><br><br><a href="https://dl.dropboxusercontent.com/u/78894295/saveb3d.zip" target="_blank">https://dl.dropboxusercontent.com/u/78894295/saveb3d.zip</a><br><br>or (as i did) you have to keep track of each texture, brush applied to each surface, so, when scanning the mesh you has all the info to be saved...<br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;======================================================================================================================
;	WriteB3D by Charrua
;======================================================================================================================
;	it uses Blitz3D+.dll
;
;	you need to place Blit3D+.decls on blitzdir\userlibs
;	and the .dll must be on your executable folder
;
;	this version writes more mesh info: vertex normal, vertex color, uv sets (for textures)	
;	
;	handles the hard part: textures chunk, brush chunk with correct textures indexes
;
;	you need to keep track of each texture used on your mesh, for each texture you have to create a tTexture object
;	
;	
;	WriteBB3D scan your mesh and save info of the first 3 texture layers. If you specify a lightmap texture, then is asumed to be on layer 3
;	if a light map is specifyed then it is asumed that the second uv set must be used for it.
;
;	it took me a while to make this to work, so if you use, please give me some credit!
;
;	enjoy
;


Include "b3dfile.bb"

Graphics3D 640,480


;camera and light
;------------------------------------------------
camera=CreateCamera()
PositionEntity camera,0,0,-3

light=CreateLight()
TurnEntity light,45,45,0


; create a mesh to be saved
mesh=CreateSphere( 32 )


;load a texture for it
texName$ = "tex.jpg"
texFlags = 1
tex=LoadTexture(texName,texFlags)
;create an tTexture object to keep track of some aditional info for each texture used
texobj.tTexture=New tTexture
texobj\file  = texName
texobj\flags = texFlags

;create a brush							
bru = CreateBrush(255,128,64)
BrushTexture bru,tex

;paint the mesh
PaintMesh  mesh,bru


;write it to disk
WriteBB3D( "temp.b3d",mesh )

;show the mesh
While Not KeyHit(57)
	TurnEntity mesh,.1,.2,.3
	UpdateWorld
	RenderWorld
	Text 10,10,"hit space to continue..."
	Flip
Wend

FreeEntity mesh

;load the saved mesh
mesh=LoadMesh( "temp.b3d" )


;show it
While Not KeyHit(1)
	TurnEntity mesh,.1,.2,.3
	UpdateWorld
	RenderWorld
	Text 10,10,"hit escape to exit..."
	Flip
Wend

End


;-------------------------------------------------------------------------
Type tTexture
	Field file$
	Field flags
	Field b3dTexIdx
End Type


Function SearchTexture.tTexture(filename$)
	Local tex.tTexture
	DebugLog "looking for: "+filename
	For tex = Each tTexture
		If tex\file=filename Then Return tex
	Next
	DebugLog "tex not fuound"
	Return Null
End Function

Function StripPath$(path$)
	; Returns the file from the specifed path 
	Local a, byte$
    For a = Len(path$) To 1 Step -1 
        byte$ = Mid(path$,a,1) 
        If byte$ = "\" 
            Return Right(path$,Len(path$)-a) 
        EndIf 
    Next 
    Return path$ 
End Function
;------------------------------------------------------------------------

Function WriteB3DInfo(mesh, LightMapTex$="")
	
	Local SurfIdx, Surface
	Local TexCount, TexIdx, Tex.tTexture
	Local b3dTexIdx
	
	;---------------------------------------------------------------------------------
	b3dBeginChunk( "TEXS" ) ; list all textures used by the mesh
	;
	
	
	b3dTexIdx = 0	;reset tex idx
	
	For SurfIdx=1 To CountSurfaces(mesh)

		Surface = GetSurface(mesh,SurfIdx)
		
		bru = GetSurfaceBrush(Surface)
		
		
		If Bru&lt;&gt;0 Then
		
			For TexIdx=0 To 3
				
					
				texhandle = GetBrushTexture(bru, TexIdx)
				texName$ = strippath(TextureName(texhandle))
				
				If texname&lt;&gt;"" Then
					
					tex = SearchTexture(texName)
					
					If tex&lt;&gt;Null Then
						
						tex\b3dTexIdx = b3dTexIdx	;store for future use (in brush chunk!)
						b3dTexIdx = b3dTexIdx + 1
						
						b3dWriteString( texName )
						b3dWriteInt( tex\flags )	;flags manually set when texture was loaded
						b3dWriteInt( BP_GetTextureBlend(texhandle) )		;blend
						b3dWriteFloat( BP_GetTexturePositionU(texhandle) )	;x in tex 0 (hu?)
						b3dWriteFloat( BP_GetTexturePositionV(texhandle) )	;y in tex 0
						b3dWriteFloat( BP_GetTextureScaleU(texhandle) )		;x scale 1
						b3dWriteFloat( BP_GetTextureScaleV(texhandle) )		;y scale 1
						b3dWriteFloat( BP_GetTextureRotation(texhandle) )	;rotation 0
					End If
					
				End If
					
			Next
		End If
	Next
	
	;el idx del lm es el ultimo de la lista
	If LightMapTex&lt;&gt;"" Then
		b3dWriteString( LightMapTex )
		b3dWriteInt( 9 + $10000)			;flags	+$10000 para que use el segundo set de coordenadas
		b3dWriteInt( 2 )					;blend
		b3dWriteFloat( 0 )					;x in tex 0 (hu?)
		b3dWriteFloat( 0 )					;y in tex 0
		b3dWriteFloat( 1 )					;x scale 1
		b3dWriteFloat( 1 )					;y scale 1
		b3dWriteFloat( 0 )					;rotation 0
	End If
	
	
	b3dEndChunk()	;end of TEXS chunk
	;---------------------------------------------------------------------------------
	
	
	For SurfIdx = 1 To CountSurfaces(mesh)
		b3dBeginChunk( "BRUS" )
		Surface = GetSurface(mesh,SurfIdx)
		
		GetSurfaceBrush(Surface)
		
		If Bru&lt;&gt;0 Then
			TexCount = 0
			For TexIdx=0 To 2	;capa 3 es para lightmap
				texhandle = GetBrushTexture(bru, TexIdx)
				texName$ = strippath(TextureName(texhandle))
				If texName&lt;&gt;"" Then TexCount=TexCount+1
			Next
			
			If LightMapTex&lt;&gt;"" Then TexCount=TexCount+1
			
			b3dWriteInt(TexCount)				;textures count
			b3dWriteString( "bru" )				;brushname, if you need, you may manually set a brsh name for each brush....
			b3dWriteFloat( Float(BP_GetBrushColorR(bru))/255.0 )		;red
			b3dWriteFloat( Float(BP_GetBrushColorG(bru))/255.0 ) 		;green
			b3dWriteFloat( Float(BP_GetBrushColorB(bru))/255.0 )		;blue
			b3dWriteFloat( BP_GetBrushAlpha(bru) )			;alpha
			b3dWriteFloat( BP_GetBrushShininess(bru) )		;shininess
			b3dWriteInt( BP_GetBrushBlend(bru) )			;blendmode
			b3dWriteInt( BP_GetBrushFX(bru) )				;FX
			
			If TexCount Then
			
				For TexIdx = 0 To 3
					texhandle = GetBrushTexture(bru, TexIdx)
					texName$ = strippath(TextureName(texhandle))
						
					If texName&lt;&gt;"" Then
						
						Tex = SearchTexture(texName)

						If Tex&lt;&gt;Null Then
							b3dWriteInt( Tex\b3dTexIdx )
						Else
							DebugLog "tex null"
						End If
					End If
				Next
			End If
			
			If LightMapTex&lt;&gt;"" Then b3dWriteInt( b3dTexIdx )	;LM_DEFAULTLAYER ojo piñon fijo!!
			
		Else
			b3dWriteInt(0)	;no tiene brush, menos tiene texturas
			b3dWriteString( "DummyBrush" )	;brushname
			b3dWriteFloat( 1 )		;red
			b3dWriteFloat( 1 )		;green
			b3dWriteFloat( 1 )		;blue
			b3dWriteFloat( 1 )		;alpha
			b3dWriteFloat( 0 )		;shininess
			b3dWriteInt( 1 )		;blendmode
			b3dWriteInt( 0 )		;FX
		End If
		b3dEndChunk()			;end of BRUS chunk
	Next
	
	b3dBeginChunk( "NODE" )
	b3dWriteString( "Exported_with_charruas_code" )	;je place whatever you want here!
	b3dWriteFloat( 0 )	;x_pos
	b3dWriteFloat( 0 )	;y_pos
	b3dWriteFloat( 0 )	;y_pos
	
	b3dWriteFloat( 1 )	;x_scale
	b3dWriteFloat( 1 )	;y_scale
	b3dWriteFloat( 1 )	;z_scale
	
	b3dWriteFloat( 1 )	;rot_w
	b3dWriteFloat( 0 )	;rot_x
	b3dWriteFloat( 0 )	;rot_y
	b3dWriteFloat( 0 )	;rot_z
	
	WriteMESH( mesh )
	
	b3dEndChunk()	;end of NODE chunk
	
End Function


Function WriteBB3D( f_name$, mesh, LightMapTex$="" )
	
	Local file
	
	file=WriteFile( f_name$ )
	
	DebugLog f_name+" : "+file
	
	b3dSetFile( file )
	DebugLog "b3d_file: "+b3d_file
	
	b3dBeginChunk( "BB3D" )
	
	b3dWriteInt( 1 )	;version
	
	WriteB3DInfo(mesh, LightMapTex )	;lo que tengo que salvar com mesh info!
	
	b3dEndChunk()	;end of BB3D chunk
	
	CloseFile file
	
End Function


Function WriteMESH( mesh, moreinfo=True )
	
	Local n_surfs, k, n_verts, surf, first_vert, n_tris, j
	
	n_surfs=CountSurfaces( mesh )
	
	b3dBeginChunk( "MESH" )
	b3dWriteInt( -1 )				;no 'entity' brush -1
	

	b3dBeginChunk( "VRTS" )

	If moreinfo Then
		b3dWriteInt( 3 )			;flags - 0=nothin, normals=1, rgba=2
		b3dWriteInt( 2 )			;number of tex_coord sets (using 2 anyway here)
		b3dWriteInt( 2 )			;descriptors per coordinate (u,v,w?) usually 2 for uv
	Else
		b3dWriteInt( 0 )			;flags - 0=nothin, normals=1, rgba=2
		b3dWriteInt( 0 )			;number of tex_coord sets (using 2 anyway here)
		b3dWriteInt( 0 )			;descriptors per coordinate (u,v,w?) usually 2 for uv
	End If
	
	For k=1 To n_surfs
		surf=GetSurface( mesh,k )
		n_verts=CountVertices( surf )-1
		
		For j=0 To n_verts
			
			b3dWriteFloat( VertexX( surf,j ) )
			b3dWriteFloat( VertexY( surf,j ) )
			b3dWriteFloat( VertexZ( surf,j ) )
			
			If moreinfo Then
				b3dWriteFloat( VertexNX( surf,j ) )
				b3dWriteFloat( VertexNY( surf,j ) )
				b3dWriteFloat( VertexNZ( surf,j ) )
				
				
				b3dWriteFloat( VertexRed( surf,j ) ) ; r
				b3dWriteFloat( VertexGreen( surf,j ) ) ; g
				b3dWriteFloat( VertexBlue( surf,j ) ) ; b
				b3dWriteFloat( VertexAlpha( surf,j ) ) ; a
				
				b3dWriteFloat( VertexU#( surf,j,0 ) ) ; uv set 0
				b3dWriteFloat( VertexV#( surf,j,0 ) )
				b3dWriteFloat( VertexU#( surf,j,1 ) ) ; uv set 1
				b3dWriteFloat( VertexV#( surf,j,1 ) )
			End If
						
		Next
	Next
	
	b3dEndChunk()	;end of VRTS chunk
	
	first_vert=0
	For k=1 To n_surfs
		
		surf=GetSurface( mesh,k )
		n_tris=CountTriangles( surf )-1
		b3dBeginChunk( "TRIS" )
		b3dWriteInt( k-1 )		;brush for these triangles (surf -1 !!!)
		
		For j=0 To n_tris
			b3dWriteInt( first_vert+TriangleVertex( surf,j,0 ) )
			b3dWriteInt( first_vert+TriangleVertex( surf,j,1 ) )
			b3dWriteInt( first_vert+TriangleVertex( surf,j,2 ) )
		Next
		
		b3dEndChunk()	;end of TRIS chunk
		
		first_vert=first_vert+CountVertices( surf )
		
	Next
	
	b3dEndChunk()	;end of MESH chunk
	
End Function

</textarea> <br><br></td></tr></table><br>
<a name="1246706"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Prym</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Charrua Hello and thank you for your reply.<br>I hope I do not owe you anything.<br>I am low and I can not fully exploit your example. Thank you anyway.<br>I did a lot of testing on my side , without success.<br>I answered you to ask yourself some questions:<br><br>Block of code from b3d.utils :<br><br>Function WriteBB3D( f_name$,mesh )<br>	file=WriteFile( f_name$ )<br>	b3dSetFile( file )<br>	b3dBeginChunk( "BB3D" )<br>		b3dWriteInt( 1 );version<br>		b3dBeginChunk( "BRUS" )<br>			b3dWriteInt( 0 );0 textures per brush<br>			b3dWriteString( "Shiny Red" )		;name<br>			b3dWriteFloat( 1 );red<br>			b3dWriteFloat( 0 );green<br>			b3dWriteFloat( 0 );blue<br>			b3dWriteFloat( 1 );alpha<br>			b3dWriteFloat( .75 );shininess<br>			b3dWriteInt( 1 );blend<br>			b3dWriteInt( 0 );FX<br>		b3dEndChunk();end of BRUS chunk<br>		b3dBeginChunk( "NODE" )<br>			b3dWriteString( "entity_name_here!" )<br>etc ...<br>End Function<br><br>- How have the block textures ?<br>- The textures are they still attached to brushes ?<br>- Can you load a " .b3d " without texture , then the texture after ?<br>- So how to write ? <br><br></td></tr></table><br>
<a name="1246728"></a>

<a name="1246730"></a>

<a name="1246731"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Charrua</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> well, i'm not an expert on the subject...<br><br>a entity has surfaces<br>each surface has a brush<br>each brush has color, blend, bright many other properties and up to 8 textures, each with it's own properties...<br><br>a .b3d file has some mandatory chunks (or sections) and other that may or may not be. <br><br>if the object has no textures, then should be at least one brush but no textures chunk (the example that cames with b3d_util has no textures for instance).<br><br>The function i wrote, scans the mesh, scan each surface, get each brush and scans each texture it has (if any), count how much textures are in the whole mesh and for each brush and then writes the list of textures and brushes, each brush has to include how many texture it has if any and at the end, the list of texture indexes as they appear on the TEXS chunk.<br><br>i know it is not easy at first (and after a while ...)<br><br>for example, you may load a mesh, texture it and afterwards save the mesh with the textures, but, you have to tell my function about the texture you applied:<br><br><pre class=code>
;load a texture for it
texName$ = "tex.jpg"
texFlags = 1
tex=LoadTexture(texName,texFlags)
;create an tTexture object to keep track of some aditional info for each texture used
texobj.tTexture=New tTexture
texobj\file  = texName
texobj\flags = texFlags
</pre><br><br>this fragment, loads a texture, which will be applied to abrush (BrushTexture bru,tex) and then applied to the mesh (PaintMesh  mesh,bru)<br><br>the sentence:<br><pre class=code>
texobj.tTexture=New tTexture
</pre><br><br>creates an object to keep track of each texture<br>i need to know each texture the object has to properly place the texture index afterwards.<br><br>this save_b3d works only if you has the complete list of textures linked to tTexture objects if not it will fail.<br><br>blitz3d keeps trak of many properties not exposed via functions, for that reason i use blitz3d+.dll, to get some extra info about brush and textures<br><br>i correct a bug i found on the first version, and updated the demo code, now it creates two entities, textures them with 2 different textures, then makes one mesh and save to disk as one b3d file.<br><br>new file:<br><a href="https://dl.dropboxusercontent.com/u/78894295/saveb3d.zip" target="_blank">https://dl.dropboxusercontent.com/u/78894295/saveb3d.zip</a><br><br>juan <br><br></td></tr></table><br>
<a name="1246844"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Blitzplotter</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> This looks very interesting, I've been wanting to do some mesh manipulation for a while but its been eluding me until now ;) <br><br></td></tr></table><br>
<a name="1247424"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Prym</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you Charrua ,<br>I will explore these ideas and try to understand the architecture of " b3d ."<br>Hold the beginning of my address space game " <a href="http://prymprog.wikeo.net" target="_blank">http://prymprog.wikeo.net</a> ." It is not very developed, but I little by little rule . <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
