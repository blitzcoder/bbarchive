<!DOCTYPE html><html lang="en" ><head ><title >Collision(jumping) help</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Collision(jumping) help</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Collision(jumping) help</a><br><br>
<a name="1191508"></a>

<a name="1191509"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >codermax</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi.<br><br>I'm kind of stuck on this whole jumping thing. The code basically goes like this:<br>1. when players wants to jump, press space<br>2. player will fly up for a bit, then fall down<br>3. player will fall to the ground, then stop<br>4. stopping the fall is based on zeroing the fall velocity<br><br>I've tried various methods from the forums to get this working perfect. LinePick doesn't work. It took me a couple tries to get the internal collisions to work. The problem is, when I set "playerisonground=false", the character will fall whenever the player leaves the ground. Two problems exist here:<br><br>1. the player will fall through the ground no matter what<br><br>And if I happen to fix that, then:<br><br>1. the game will check and say that the player has collided with the ground when they reach the peak of their jump, so they'll stop falling in mid-air and can no longer jump<br><br>The funny thing is, linepick USED to work, but it was quite finnicky, so I decided to use the internal collisions, which seemed to work for a bit. But as I added unrelated code, the jumping failed to work and I'm kinda stuck. :/<br><br>The collision code is based on my understanding of entity scaling and entity boxes.<br><br>Just copy, paste and run.<br><br>camera.bb<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Initializes the machine's 3D Graphics card.
Graphics3D 800,600,0,2
;STATICSCENE = collision type assigned to your level
Const STATICSCENE=1 
Const SPHERE_COL=2
Const FLOORSCENE=3
;sets the drawing focus to the program's backbuffer.
  SetBuffer BackBuffer()
	HidePointer()
	
	;player speed.
	Global PlayerSpeed#=8
	Global Difference#=0
	Global Col$=""
	;player turn speed.
	Global PlayerTurnSpeed#=8
	Global PlayerIsOnGround=True 
	Global PlayerIsJumping=False
	Global PlayerEntityHandle$
	Global PlayerYVelocity#=0
	Global AccelerationDueToGravity#=-0
	Global LowestGravityAcceleration#=-0.0003
	
	;old Y position for player.
	Global OldY#=0
	
	Global cameracube=CreateCube()
	;set up an ambient light, which shines on everything equally.
	AmbientLight 50,50,50
	
	;create a camera for viewing.
	Global camera=CreateCamera(cameracube)
	CameraViewport camera,0,0,800,600
	CameraRange camera,0.7,100
	CameraZoom camera,1.7
	
	;create a light object, to shine upon the area. The brightness of the light is partially determined by the ambient light.
	light=CreateLight(1,camera)
;change the light's color.
	LightColor light,155,155,190
	
	Global LevelCube.Cube=New Cube
	LevelCube\Shape=CreateCube()
	PositionEntity LevelCube\Shape,0,6,0
	ScaleEntity LevelCube\Shape,9,1,9
	EntityType LevelCube\Shape,FLOORSCENE
	;EntityType LevelCube\Shape,STATICSCENE
	EntityPickMode LevelCube\Shape,3 
	;width, height and depth offset for scaled entities in relation to their entitybox:
	;X Pos-Entity X Scale, Y Pos+Entity Y Scale, Z Pos-Entity Z Scale and
	;X Scale x 2, Y Scale x 2, Z Scale x 2
	EntityBox LevelCube\Shape,EntityX(LevelCube\Shape)-9,EntityY(LevelCube\Shape)+2,EntityZ(LevelCube\Shape)-9,18,3,18
	
	Global LevelCube2.Cube=New Cube
	LevelCube2\Shape=CreateCube()
	PositionEntity LevelCube2\Shape,0,7,5
	ScaleEntity LevelCube2\Shape,1,1,1
	EntityType LevelCube2\Shape,STATICSCENE
	EntityPickMode LevelCube2\Shape,3 
	;width, height and depth offset for scaled entities in relation to their entitybox:
	;X Pos-Entity X Scale, Y Pos+Entity Y Scale, Z Pos-Entity Z Scale and
	;X Scale x 2, Y Scale x 2, Z Scale x 2
	EntityBox LevelCube2\Shape,-1,-1,-1,2,2,2
	
	EntityType cameracube,SPHERE_COL
	;EntityPickMode cameracube,3
	PositionEntity cameracube,0,9,1
	PositionEntity camera,EntityX(cameracube)-6,EntityY(cameracube)+10,EntityZ(cameracube)
	;EntityBox cameracube,-1,-1,-1,2,2,2
	EntityRadius cameracube,0.9
	EntityColor cameracube, 0,190,0
	;EntityAlpha cameracube, 0
	;position and turn the camera.
	TurnEntity cameracube,0.0,1.0,0.0
	
	Collisions SPHERE_COL,STATICSCENE,3,3
	Collisions SPHERE_COL,FLOORSCENE,3,1
	
;main while loop
	;ends the game by pressing the escape key.
	While Not KeyHit(1)
		;TurnEntity LevelCube\Shape,0,0.3,0
;rotate the entity.
	CheckInput()
	CheckMouse()
	
	;update all objects.
	UpdateWorld()
	CheckGravity()
	
;renders the objects with camera.
	RenderWorld()
	
;draw text onto the screen.
	Text 320,460,"Player: "+EntityX(cameracube)+", "+EntityY(cameracube)+", "+EntityZ(cameracube)
	Text 320,480,"Cube: "+EntityX(LevelCube\Shape)+", "+EntityY(LevelCube\Shape)+", "+EntityZ(LevelCube\Shape)
	Text 320,500,"Difference: "+PlayerIsOnGround
	Text 320,520,PlayerYVelocity#
;flip BackBuffer contents onto the front buffer.
	Flip
	
;end of the loop.
Wend
;exits program.
  End
	
Function CheckInput()
	Local TotalSpeed#=PlayerSpeed*0.05
	Local TotalTurnSpeed#=PlayerTurnSpeed*0.1
	Local NoMove=0
	;UP key.
	If KeyDown(200) =True
		If PlayerIsJumping=True
				MoveEntity cameracube,0,0,TotalSpeed/3
			EndIf
			If PlayerIsJumping=False
				MoveEntity cameracube,0,0,TotalSpeed/7.6
		EndIf
		
EndIf
	
	;DOWN key.
	If KeyDown(208) Then
	
		For  p=0 To 4 Step +1
			If LinePick(EntityX(cameracube)+(-0.8+(p*0.35)),EntityY(cameracube), EntityZ(cameracube)-0.8, 0,0,-0.5,0.35)&gt;0
				NoMove=1 ;MoveEntity cameracube,0,0,TotalSpeed/7.6/5
			EndIf
		Next
		
		;If NoMove=0
			MoveEntity cameracube,0,0,-TotalSpeed/7.6
		;EndIf
	EndIf
	
	;LEFT key.
	If KeyDown(203) Then
		For  p=0 To 4 Step +1
			If LinePick(EntityX(cameracube)-0.5,EntityY(cameracube), EntityZ(cameracube)+(-0.8+(p*0.35)), -0.5,0,0,0.35)&gt;0
				NoMove=1
			EndIf
		Next
		
		;If NoMove=0
			MoveEntity cameracube,-TotalSpeed/7.6,0,0
		;EndIf
	EndIf
	
	;RIGHT key.
	If KeyDown(205) Then
		For p=0 To 4 Step +1
			If LinePick(EntityX(cameracube)+0.8,EntityY(cameracube), EntityZ(cameracube)+(-0.8+(p*0.35)), 0.5,0,0,0.35)&gt;0
				NoMove=1 	;MoveEntity cameracube,-TotalSpeed/7.6/5,0,0
			EndIf
		Next
		
		;If NoMove=0
			MoveEntity cameracube,TotalSpeed/7.6,0,0
		;EndIf
		EndIf
	
	;execute jumping.
	If KeyDown(57) And PlayerIsJumping=False And PlayerIsOnGround=True 
		PositionEntity cameracube, EntityX(cameracube),EntityY(cameracube)+0.5,EntityZ(cameracube)
		PlayerIsJumping=True
		PlayerIsOnGround=False 
		AccelerationDueToGravity#=1
	EndIf
	
End Function

Function CheckMouse()
	;MOUSELOOK FUNCTION 
	TurnEntity cameracube, 0, -MouseXSpeed()/20.0, 0					;rotate player Pivot according to mouse X movement
	TurnEntity camera, MouseYSpeed()/20.0, 0, 0					;rotate camera up/down according to mouse Y 
		
		MoveMouse GraphicsWidth()/2, GraphicsHeight()/2
		If EntityPitch(camera) &lt; -55								;don't allow camera to look above 55 degrees
			RotateEntity camera, -55, EntityYaw(camera), EntityRoll(camera)
		EndIf 
 	 	
		If EntityPitch(camera) &gt; 79									;don't allow camera to look below 80 degrees
			RotateEntity camera, 79, EntityYaw(camera), EntityRoll(camera)
		EndIf
		
		;THIS STRAIGHTENS THE CAMERA IF IT GETS OUT OF ROLL (TILT LEFT OR RIGHT)
		Local roll = EntityRoll(camera)
		If roll &lt;&gt; 0
			TurnEntity camera, 0,0, - roll
		EndIf 
End Function

Function CheckGravity()
	
	;TFormVector 0,0,1,cameracube, 0
	If AccelerationDueToGravity#&gt;LowestGravityAcceleration
		AccelerationDueToGravity#=AccelerationDueToGravity#-0.1
	EndIf
	PlayerYVelocity#=PlayerYVelocity#+AccelerationDueToGravity#
	;PlayerIsOnGround=False 
	If EntityCollided(cameracube, FLOORSCENE)=LevelCube\Shape 
		PlayerIsOnGround=True ;End
	EndIf
	
	If PlayerIsJumping=True
		;highest jump height.
		If PlayerYVelocity#&gt;0.5
			PlayerYVelocity#=0.5
		EndIf
		;fastest fall height.
		If PlayerYVelocity#&lt;-0.3
			PlayerYVelocity#=-0.3
		EndIf
	EndIf
	;=====================
	;if player is not jumping.
	If PlayerIsJumping=False
		If PlayerYVelocity#&lt;-0.3
		PlayerYVelocity#=-0.3
	EndIf
	EndIf
;AccelerationDueToGravity=some negative value
	;if the player is on the ground.
		If PlayerIsOnGround=True
		PlayerYVelocity#=0
		PlayerIsJumping=False
	EndIf
	
	TranslateEntity cameracube,0,PlayerYVelocity#,0
End Function

Type Cube
	Field X#,Y#,Z#,Width#,Height#,Depth#,Shape
End Type

</textarea> <br><br></td></tr></table><br>
<a name="1191528"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here is an example :<br><a href="http://blitzbasic.com/codearcs/codearcs.php?code=2933" target="_blank">http://blitzbasic.com/codearcs/codearcs.php?code=2933</a><br><br>However this is a really simple example. Please note that if you want to be able to detect collision against walls and against the ground there are several more complex ways to do it.<br><br>One would be to use only a collider sphere for the player and seperate the ground collider and the wall collider for the environment<br><br>One would be to use one collider sphere for the player to check the collision against the walls and use linepick to check the distance between the player feet and the ground. <br><br></td></tr></table><br>
<a name="1191701"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Okay, I haven't looked in real depth yet, but I did find a small issue:<br><br>In the  CheckInput function, you have the following:<br><br><pre class=code>
;UP key.
	If KeyDown(200) =True
		If PlayerIsJumping=True
			MoveEntity cameracube,0,0,TotalSpeed/3
		EndIf
		If PlayerIsJumping=False
			MoveEntity cameracube,0,0,TotalSpeed/7.6
		EndIf
		
	EndIf
</pre><br>With similar code for other directions too.<br>This seems a little counter-intuitive to me, and may be better if you consider that while JUMPING the player is only going to ACCELERATE DOWNWARDS due to gravity, and when in the air, no keypresses will make a difference to the movement.<br><br>You then have a CheckGravity function in which your AccelerationDueToGravity CHANGES, yet the modification to the PlayerYVelocity is going to quickly become a constant -0.3 or constant +0.5<br><br>Overall it seems that at some point, the code has gotten so confusing that the essential principle of what you tried to achieve has become lost in the jumble!<br><br>I would recommend simplifying the code in the following way:<br><br><br>1) Acceleration due to Gravity is constant (Okay, it's not TRULY in reality, it varies according to the mass of the objects, but for a game, a constant acceleration downwards should be the value for the effect of gravity).<br><br>2) When pressing jump (provided player is not already jumping (see later) then an impulse of velocity UPWARDS is given to the player - this upwards momentum will naturally lessen due to the constant effect of gravity - no more need be done) <br><br>3) When checking keys, first, check to see if player is on the ground (With LinePick or the methods mentioned above). If so, then check for keypresses. If not, Apply Gravity (Ideally, gravity would be applied every loop, but to make things easier on the CPU, we only really need to do so if the player is in the air)<br><br><br>____<br><br><br>I really hope this makes sense.<br><br>Some brief example code here for each part I mentioned, If I have time, I will see if I can incorporate this into your code, if you need it :)<br><pre class=code>

;The Values below should be chosen according to the scale/units you use for your game.
Const AccelerationDueToGravity#=-0.5
Const MoveSpeed#=1.0 ;Standard Movement Rate
Const JumpSpeed#=2.0 ;Standard Jump Rate

Global MoveXSpeed#;Remembers speed for Momentum in X dimension
Global MoveZSpeed#;Remembers speed for Momentum in Z dimension
Global MoveYSpeed#;Remembers speed for Momentum in Y dimension

While (Loop)
;Main Loop Stuff
	Loop=(Not(KeyHit(1)))
	GetInput
	UpdateMovement
Wend
End

Function GetInput()
	If (PlayerOnGround())
		MoveXSpeed=(KeyDown(205)-KeyDown(203))*MoveSpeed
		MoveZSpeed=(KeyDown(208)-KeyDown(200))*MoveSpeed
		MoveYSpeed=(KeyHit(57))*JumpSpeed
	Else
		;Apply Gravity, Player is in air
		;NOTE We ADD AccelerationDueToGravity, since the Value is NEGATIVE
		MoveYSpeed=MoveYSpeed+AccelerationDueToGravity;This will require some collision checking or similar to ensure that (EntityY(Player,True)+MoveYSpeed)&gt;=GroundLevel and if not, MoveYSpeed=GroundLevel-(EntityY(Player,True)) etc.
	End If
End Function

Function UpdateMovement()
	;Simple Collision Detecting
	If (MoveYSpeed&lt;0)
		If (PlayerOnGround())
			;Here is where any falling-induced damage can occur, MoveYSpeed will hold a value that should indicate magnitude of damage in arbitrary units of MoveSpeed and AccelerationDueToGravity
			MoveYSpeed=0
		End If
	End If
	
	MoveEntity Player,MoveXSpeed,MoveYSpeed,MoveZSpeed		
		
End Function

Function PlayerOnGround()
	If (DoLinePickOrWhatever())
	;Result = Player Touching (OR BENEATH!) Ground
		Return True
	End If
	;Result = Player Not Touching Ground - i.e. jumping, in-air or falling
	Return False
End Function





</pre> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
