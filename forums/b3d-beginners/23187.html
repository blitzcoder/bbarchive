<!DOCTYPE html><html lang="en" ><head ><title >B+: Use TAB to switch between text fields..?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >B+: Use TAB to switch between text fields..?</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >B+: Use TAB to switch between text fields..?</a><br><br>
<a name="238877"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BachFire</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey,<br><br>I made an app with B+, which includes several text fields. I would like to move the curser to the next text field, when TAB is pressed.<br><br>I can't figure this out.. :( I have tried working with the 'HotKeyEvent' command, but have a hard time seeing how it works..<br><br>Could someone help me, please? <br><br></td></tr></table><br>
<a name="238953"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Oldefoxx</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> One of the best ways is to create arrays for values of x, y, and z (column,row,length) for each field.  Your index then increments to point to the next field, and decrements to point to the previous field.  If your index exceeds the maximum number of entries allowed in your arrays (goes beyond the last field), just have it start again at the first one, and if the index decrements back before the first field, just set it to the last one.  You can use the LOCATE command to position to the column and row, but the<br>big problem is limiting the input to the length that you want the field to be.  In addition, you may want to limit what characters are valid in that field (Only digits in a phone number field, for instance).  Look in the code archives and in the forum elsewhere to find efforts to code routines to supplement INPUT to accomodate these requirements. <br><br></td></tr></table><br>
<a name="238967"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >EOF</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here is a tabber example I done a while back with HotKeyEvent.<br>I make the TAB and Shift+TAB generate and event of $9000.<br><br>Note: This example has a userlib entry for selecting the values in the textboxes for editing.<br>If you just want to try out this example without having to set up the userlib just comment out the SendMessage command.<br><br><pre class=code>; Textboxes - adding 2 values
; with TABBING support and text 'selection'

; userlib
; ************************
; .lib "user32.dll"
; SendMessage%(hwnd,msg,wParam,mParam):"SendMessageA"
; ************************

Const EM_SETSEL=$b1 ;  select a range of characters in an edit control

Dim gad(4) : tab=1
Gosub CreateGUI

Repeat
	ev=WaitEvent()
	DebugLog "Event = $"+Right$(Hex$(ev),4) + "   "+EventSource() + "   "+EventData()
	Select ev
		Case $803 ; close [X]
			Exit
		Case $401 ;gadgethit
				total#=Float(TextFieldText(gad(1)))+Float(TextFieldText(gad(2)))
				SetGadgetText gad(3),Str$(total)
				lastgad=EventSource()
		Case $9000 ; TAB key event
				If lastgad=gad(1) tab=2
				If lastgad=gad(2) tab=1
				ActivateGadget gad(tab) : lastgad=gad(tab)
				SendMessage QueryObject(gad(tab),1),EM_SETSEL,0,-1 ; select all characters
	End Select
Forever

End

.CreateGUI
	win = CreateWindow("Adder",176,193,284,123,Desktop(),1)
	gad(1) = CreateTextField(20,20,100,20,win,0)
	gad(2) = CreateTextField(150,20,100,20,win,0)
	gad(3) = CreateTextField(106,56,100,20,win,0)
	CreateLabel("+",133,21,9,15,win,0)
	CreateLabel("=",80,57,10,16,win,0)
	HotKeyEvent 15,0,$9000,+1              ; TAB forward
	HotKeyEvent 15,1,$9000,-1              ; TAB back
	HotKeyEvent 1,0,$803                   ; ESC
	ActivateGadget gad(1)
Return</pre> <br><br></td></tr></table><br>
<a name="239050"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >soja</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's one I wrote.  It doesn't rely on you to actively keep track of the focus, and it is very easy to add/subtract/rearrange items in the tab cycle order, so that might make it a little easier.  But, it doesn't have the text selection stuff built-in like Syntax Error's, and there are other problems, but I think they're Blitz issues.<br><pre class=code>
;------------------------------
; USERLIB DECLARATIONS 
; 
; .lib "user32.dll"
; GetFocus%():"GetFocus"

;------------------------------
; CONSTS

Const evWindowClose = $803	; "Real" Blitz event
Const evTab 		= $104	; "Made up" event for HotKeyEvent
Const vkTab			= 15	; Scancode for Tab key

;------------------------------
; TYPES

Type TabCycleGadget
	Field hgadget% ; Windows handle
	Field gadget% ; Blitz handle
End Type

;------------------------------
; GLOBALS

Global wnd%, txt%, txt2%, button%, txtarea%, listbox%, tree%, button2%

;------------------------------
; MAIN PROGRAM

InitGUI()
InitTabCycle()

While WaitEvent()
	Select EventID()
		Case evWindowClose : End
		Case evTab : PerformTabCycle(GetFocus(), EventData())
	End Select
Wend

;------------------------------
; FUNCTIONS

Function InitGUI()
	; Create Window
	width% = 320 : height% = 240
	x% = (ClientWidth(Desktop()) - width) / 2
	y% = (ClientHeight(Desktop()) - height) / 2
	wnd = CreateWindow("Tab Cycle Demo", x, y, width, height, 0, 1)

	; Create Gadgets
	txt = CreateTextField(5, 5, 200, 20, wnd,2)
	txt2 = CreateTextField(5, 30, 200, 20, wnd)
	button = CreateButton("Button", 5, 55, 80, 25, wnd)
	txtarea = CreateTextArea(5, 85, 200, 95, wnd)
	listbox = CreateListBox(210, 5, 100, 75, wnd)
	tree = CreateTreeView(210, 85, 100, 65, wnd)
	button2 = CreateButton("Button2", 210, 155, 80, 25, wnd)
	
	; Configure Gadgets
	AddGadgetItem listbox, "ListItem1"
	AddGadgetItem listbox, "ListItem2"
	SelectGadgetItem listbox, 1
	AddTreeViewNode("Node1", TreeViewRoot(tree))
	AddTreeViewNode("Node2", TreeViewRoot(tree))
End Function

Function InitTabCycle()
	; Set up Forward Tab Hotkey 
	HotKeyEvent vkTab, 0, evTab

	; Set up Backward Tab Hotkey 
	HotKeyEvent vkTab, 1, evTab, 1

	; Set up tab cycling in this order (empty list is ok)
	; i.e. to change tab order, just cut and paste!
	CreateTabCycleGadget(txt)
	CreateTabCycleGadget(txt2)
	CreateTabCycleGadget(button)
	CreateTabCycleGadget(txtarea)
	CreateTabCycleGadget(listbox)
	CreateTabCycleGadget(tree)
	CreateTabCycleGadget(button2)
End Function

Function CreateTabCycleGadget(g%)
	t.TabCycleGadget = New TabCycleGadget
	t\gadget = g
	t\hgadget = QueryObject(g, 1)
End Function

Function PerformTabCycle(g%, bBackwards%)
	Local t.TabCycleGadget = First TabCycleGadget
	If g &lt;&gt; 0 And t &lt;&gt; Null Then
		; Find out if current focused gadget is in tab cycle list
		While (g &lt;&gt; t\hgadget) And (t &lt;&gt; Last TabCycleGadget) t = After t : Wend
		; Change focus
		If t &lt;&gt; Null Then
			; Change from current focused gadget
			If bBackwards Then
				; Go backwards in cycle (SHIFT-TAB)
				If t = First TabCycleGadget Then
					t = Last TabCycleGadget
					ActivateGadget(t\gadget)
				Else
					t = Before t
					ActivateGadget(t\gadget)
				EndIf
			Else					
				; Go forwards in cycle (normal TAB)
				If t = Last TabCycleGadget Then
					t = First TabCycleGadget
					ActivateGadget(t\gadget)
				Else
					t = After t
					ActivateGadget(t\gadget)
				EndIf
			EndIf
		Else
			; Activate first gadget in cycle (no current focused gadget)
			t = First TabCycleGadget
			ActivateGadget(t\gadget)
		EndIf 
	EndIf
End Function
</pre> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
