<!DOCTYPE html><html lang="en" ><head ><title >7 Years Bad Luck?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >7 Years Bad Luck?</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >7 Years Bad Luck?</a><br><br>
<a name="1097165"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I was toying with the idea of a reflective "mirror" based on a quad with a 2ndary camera render as its texture.<br><br>I managed to get it working too, but then I tried modifying it and somehow, now it doesn't seem to work at all :/<br><br>Unfortunately, I've overwtritten any backups of the working version too, and I can't see what's causing the problem.<br><br>It's clear thpough, that something is preventing the 2ndary camera ("Reflection") from rendering a visible scene.<br><br>The code is in two parts, the first is just initialising and a main loop, the second is a dependancy keeping the "mirror-specific" functions and constants separate.<br><br>If anyone can offer any suggestions as to what might be the problem, I'd be really grateful, thank you!<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Include "Mirror.bb"

Graphics3D 1024,768,32,6
SetBuffer BackBuffer()

AmbientLight 160,160,160

Global Camera=CreateCamera()
Global Sun=CreateLight()
Global Plane=CreatePlane()
TexturePlane(Plane)

Global Sphere=CreateSphere()
EntityColor Sphere,192,64,64
MoveEntity Sphere,-2,1,10

Global Cube=CreateCube()
EntityColor Cube,64,192,64
MoveEntity Cube,2,1,5

MoveEntity Sun,-10,10,-10
MoveEntity Camera,0,1,-5

MoveMouse GraphicsWidth() Shr 1,GraphicsHeight() Shr 1

HidePointer

; HERE's THE MIRROR STUFF!
Global Reflect=CreateMirrorMesh(4)
PositionEntity Reflect,0,1,7.5
;TurnEntity Reflect,0,Rand(360),0,True

While Not KeyDown(1)
	
	CameraMovement(Camera)
	UpdateMirror(Reflect,Camera)
	
	UpdateWorld
	RenderWorld
	
	Text 0,0,TrisRendered()
	Text 0,20,Str(TotalVidMem()-AvailVidMem())+" / "+Str(TotalVidMem())
	
	Flip
Wend	

Function CameraMovement(EntityCamera)
	TurnEntity EntityCamera,0,0-MouseXSpeed(),0,True
	RotateEntity EntityCamera,EntityPitch(EntityCamera,True),EntityYaw(EntityCamera,True),0,True
	MoveEntity EntityCamera,KeyDown(205)-KeyDown(203),0,KeyDown(200)-KeyDown(208)
	TranslateEntity EntityCamera,0,Sgn(MouseZSpeed()),0,False
	;If ((MouseX()&gt;GraphicsWidth()) Or(MouseX()&lt;0))
	;MoveMouse GraphicsWidth() Shr 1,GraphicsHeight() Shr 1
	;End If
	
End Function	

Function TexturePlane(EntityPlane)
	Local Texture=CreateTexture(128,128)
	SetBuffer TextureBuffer(Texture)
	ClsColor 64,64,64
	Cls
	Color 192,192,192
	Rect 0,0,63,63,True
	Rect 64,64,63,63,True
	SetBuffer BackBuffer()
	ClsColor 0,0,0
	Color 255,255,255
	EntityTexture EntityPlane,Texture
	FreeTexture Texture
End Function</textarea><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Const MIRROR_MAXIMUM_RENDERDISTANCE#=10.0
Const MIRROR_RESOLUTION_DETAIL%=7

Function CreateMirrorMesh(Segments=4,Parent=False)
	If ((Segments&lt;4) Or (Segments&gt;10000)) Then RuntimeError "Illegal number of segments"
	Local nSize=Int((Sqr(Segments)))
	Local Mesh=CreateMesh(Parent)
	;Local Brush=CreateBrush()
	Local Texture=CreateTexture(2^MIRROR_RESOLUTION_DETAIL,2^MIRROR_RESOLUTION_DETAIL)
	;BrushTexture Brush,Texture
	
	Local Surface=CreateSurface(Mesh)
	
	Local X#
	Local Y#
	For Y=1 To nSize
		For X=1 To nSize
			AddQuad(Surface,X-1,Y-1)
		Next
	Next
	
	Local Reflection=CreateReflection(Mesh)
	;PaintEntity Mesh,Brush	
	Return Mesh
End Function

Function AddQuad(Surface,X,Y)
	AddVertex(Surface,-0.5+X,0.5+Y,0,1,0)
	AddVertex(Surface,0.5+X,0.5+Y,0,0,0)
	AddVertex(Surface,-0.5+X,-0.5+Y,0,1,1)
	AddVertex(Surface,0.5+X,-0.5+Y,0,0,1)
	
	AddTriangle(Surface,0,1,2)
	AddTriangle(Surface,3,2,1)
End Function

Function CreateReflection(Mirror)
	Local Reflection=CreateCamera(Mirror)
	CameraViewport Reflection,0,0,MeshWidth(Mirror),MeshHeight(Mirror)
	
	PositionEntity Reflection,EntityX(Mirror,True),EntityY(Mirror,True),EntityZ(Mirror,True),True
	RotateEntity Reflection,EntityPitch(Mirror,True),EntityYaw(Mirror,True)+180,0
	Return Reflection
End Function

Function UpdateMirror(Mirror,RenderCamera)
	UpdateReflectionPosition(Mirror,RenderCamera)
	Local Reflection=GetReflection(Mirror)
	
;	HideEntity RenderCamera
;	ShowEntity Reflection
	;CameraProjMode RenderCamera,0
	;CameraProjMode Reflection,2
	
	UpdateWorld
	RenderWorld
	ReflectionSnapshot(Mirror)
	
;	HideEntity Reflection
;	ShowEntity RenderCamera
	
	;CameraProjMode RenderCamera,1
	;CameraProjMode Reflection,0
	
End Function
	
Function UpdateReflectionZoom(Mirror,FOVAngle#)
	Local Reflection=GetReflection(Mirror)
	Local Width=MeshWidth(Mirror)
	
	Local Zoom#=1.0 / Tan(FOVAngle#*0.5)
	CameraZoom Reflection,Zoom
End Function

Function UpdateReflectionPosition(Mirror,RenderCamera)
	Local Reflection=GetReflection(Mirror)
	PositionEntity Reflection,EntityX(Mirror,True),EntityY(Mirror,True),EntityZ(Mirror,True),True
	RotateEntity Reflection,EntityPitch(Mirror,True),EntityYaw(Mirror,True),0
	
	Local Distance#=EntityDistance(RenderCamera,Reflection)
	Local ViewAngle#=DeltaYaw(Reflection,RenderCamera)
	CameraRange Reflection,MIRROR_MAXIMUM_RENDERDISTANCE*0.001,MIRROR_MAXIMUM_RENDERDISTANCE
	
	Local Angle#=2*ATan((MeshWidth(Mirror)*0.25)/Distance)
	UpdateReflectionZoom(Mirror,Angle)
	
	PointEntity Reflection,RenderCamera,True
	TurnEntity Reflection,0,180,0
	MoveEntity Reflection,0,0,Distance
	
	RotateEntity Reflection,EntityPitch(Mirror,True),EntityYaw(Mirror,True),0
	TurnEntity Reflection,0,ViewAngle#,0,True
End Function

Function ReflectionSnapshot(Mirror)
	Local Width#=MeshWidth(Mirror)
	Local Height#=MeshHeight(Mirror)
	Local Image=CreateImage(Width,Height)
	
	Local Reflection=GetReflection(Mirror)
	
	GrabImage Image,0,0
	
;	Local Brush=GetEntityBrush(Mirror)
;	Local Texture=GetBrushTexture(Brush)
	
	Local Resolution=2^MIRROR_RESOLUTION_DETAIL	;
	Local Texture=CreateTexture(Resolution,Resolution)	;
	
	Local ScaleWidth#=0-Resolution/Width	;
	Local ScaleHeight#=Resolution/Height	;
	;Local ScaleWidth#=0-TextureWidth(Texture)/Width
	;Local ScaleHeight#=TextureHeight(Texture)/Height
	
	TFormImage Image,ScaleWidth*Width,0,0,ScaleHeight*Height
	
	Local IBuffer=ImageBuffer(Image)
	Local TBuffer=TextureBuffer(Texture)
	
	CopyRect 0,0,Resolution,Resolution,0,0,IBuffer,TBuffer	;
	;CopyRect 0,0,TextureWidth(Texture),TextureHeight(Texture),0,0,IBuffer,TBuffer
	
	EntityTexture Mirror,Texture	;
;	PaintEntity Mirror,Brush
	
	FreeImage Image
	FreeTexture Texture
;	FreeBrush Brush
End Function

Function GetReflection(Mirror)
	If (Not(Mirror)) Then RuntimeError "Entity does not exist"
	If (EntityClass(Mirror)&lt;&gt;"Mesh") Then RuntimeError "Entity is not a model"
	
	Local Child
	Local Children
	
	For Children=1 To CountChildren(Mirror)
		Child=GetChild(Mirror,Children)
		If (EntityClass(Child)="Camera")
			Return Child
			Exit
		End If
	Next
	Return 0
End Function</textarea> <br><br></td></tr></table><br>
<a name="1097172"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Whoa, that's alot of code just for creating a mirror :) I don't see a buffer set to the backbuffer or frontbuffer. So, unless your using fastextension, then I can't see how you can copy anything to a mirror?<br><br>Your copying from an imagebuffer to a texturebuffer, but that's about it... <br><br></td></tr></table><br>
<a name="1097179"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm using the CopyRect function, is that one from FastExtension? I lose track of which are native and not these days. That command takes parameters for source and destination buffers.<br><br>The only important functions are those in the second code block, and they are dealing with creating the mirror, calculating the angles and position for the reflection according to the viewer, and then the actual capturing the render.<br><br>Also, there's a couple of extra useful funcs thrown in, such as AddQuad() etc.<br><br>------------------------<br><br>If the copying from image buffer into a texture buffer IS (part of?) the problem, is there a recommended way to do this efficiently? All I can think of to be as "safe" as possible is a Read/WritePixelFast which might be too slow for realtime? <br><br></td></tr></table><br>
<a name="1097204"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I would copyrect from the buffer, rather than grab image. Copy straight from the buffer, directly to a texture. And always copy from the backbuffer(). I'll knock together a sample for ya. <br><br></td></tr></table><br>
<a name="1097309"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> did you read this:<br><br><a href="http://www.blitzbasic.com/Community/posts.php?topic=87371#990125" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=87371#990125</a><br><br>perhaps it may help.... <br><br></td></tr></table><br>
<a name="1097521"></a>

<a name="1097522"></a>

<a name="1097524"></a>

<a name="1097525"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Malice. Here is my mirror code. I've coded it using types, so you can have as many mirrors as you want. You can set size and position, and texture size too. The only thing you need to watch out for though, is because each mirror has a camera attached, it has it's own cameraclscolor. So any areas of blank will be rendered using that particular camerasclscolor. So just watch if you change the main camera's clscolor. You will need to change all the mirrors.<br><br>Z and X keys to move the cube. Arrow keys to move and turn the camera. Enjoy!<br><br>OH! And i've coded it so mirrors can't reflect other mirrors. If you want to disable that, delete the hide_all_mirrors() and show_all_mirrors() function calls. Rendering other mirrors in a mirror doesn't work very well... Oh AND don't set the texture resolution of the mirror to a dimension larger than the set graphics resolution. Doesn't work right!<br><br><pre class=code>
Const graphics_width = 800
Const graphics_height = 600

Graphics3D graphics_width,graphics_height
SetBuffer BackBuffer()


Global light = CreateLight()

Global camera = CreateCamera()
PositionEntity camera,0,0,-10

CameraClsColor camera,100,100,100

Global cube = CreateCube()
PositionEntity cube,0,0,5

ScaleEntity cube,0.2,0.2,0.2


Type mirror

	Field entity
	Field mesh
	Field surface
	Field texture
	Field texture_width
	Field texture_height
	
	Field camera
	
	Field v0,v1,v2,v3
	Field x,y,z
	
End Type

Global mirror1 = create_mirror(0,1,10,4,4,256,256,0,0,0)
Global mirror2 = create_mirror(4,1,10,4,4,256,256,0,-50,0)
Global mirror3 = create_mirror(-4,1,10,4,4,256,256,0,50,0)

While Not KeyHit(1)


	If KeyDown(200) Then MoveEntity camera,0,0,0.1
	If KeyDown(208) Then MoveEntity camera,0,0,-0.1
	If KeyDown(203) Then TurnEntity camera,0,1,0
	If KeyDown(205) Then TurnEntity camera,0,-1,0

	If KeyDown(44) Then MoveEntity cube,-0.4,0,0
	If KeyDown(45) Then MoveEntity cube,0.4,0,0

	update_mirrors()

	UpdateWorld
	RenderWorld
	Flip
Wend
End

Function update_mirrors()


	hide_all_mirrors()

	For m.mirror = Each mirror

		ShowEntity m\mesh
		ShowEntity m\camera
		HideEntity camera
		trx = DeltaPitch#(m\mesh,camera)*-1
		try = DeltaYaw#(m\mesh,camera)*-1
		

		RotateEntity m\camera, EntityPitch(m\mesh,True) + trx, EntityYaw(m\mesh,True) + try + 0 , 0,True

		RenderWorld
		CopyRect 0,0,m\texture_width,m\texture_height,0,0,BackBuffer(),TextureBuffer(m\texture)

		HideEntity m\camera
		HideEntity m\mesh
		
	Next

	show_all_mirrors()
	
	ShowEntity camera

		
End Function

Function hide_all_mirrors()

	For m.mirror = Each mirror
	
		HideEntity m\mesh
		
	Next
	
End Function

Function show_all_mirrors()

	For m.mirror = Each mirror
	
		ShowEntity m\mesh
		
	Next
	
End Function


; x - the centre x position of the mirror
; y - the centre y position of the mirror
; z - the centre z position of the mirror
;
; width - the width of the mirror
; height - the height of the mirror
;
; tex_x_res - the resolution width of the texture for the mirror
; tex_y_res - the resolution height of the texture for the mirror
;
; rot_x - the pitch of the new mirror
; rot_y - the yaw of the new mirror
; rot_z - the roll of the new mirror
;
; This function will return the actual entity, so you can modify it as you see fit.
; Remember the mirror mesh has a camera attached, which I don't think hides with the mesh.

Function create_mirror(x#,y#,z#,width#,height#,tex_x_res,tex_y_res,rotx#=0,roty#=0,rotz#=0)

	m.mirror = New mirror
	
	m\mesh = CreateMesh()
	m\surface = CreateSurface(m\mesh)

	m\v0 = AddVertex( m\surface,  - (width*0.5) ,  + (height*0.5) , 0 ,1,0)
	m\v1 = AddVertex( m\surface,  + (width*0.5) ,  + (height*0.5) , 0 ,0,0)
	m\v2 = AddVertex( m\surface,  - (width*0.5) ,  - (height*0.5) , 0 ,1,1)
	m\v3 = AddVertex( m\surface,  + (width*0.5) ,  - (height*0.5) , 0 ,0,1)
	
	AddTriangle (m\surface,m\v0,m\v1,m\v2)
	AddTriangle (m\surface,m\v2,m\v1,m\v3)
	
	UpdateNormals m\mesh
	
	PositionEntity m\mesh,x,y,z

	m\texture = CreateTexture(tex_x_res,tex_y_res)
	m\texture_width = tex_x_res
	m\texture_height = tex_y_res
	
	SetBuffer TextureBuffer(m\texture)
	Color 255,255,255
	Oval 0,0,200,200
	
	SetBuffer BackBuffer()
	
	EntityTexture m\mesh,m\texture
	
	RotateEntity m\mesh,rotx,roty,rotz
	
	m\camera = CreateCamera()
	EntityParent m\camera,m\mesh
	PositionEntity m\camera,x,y,z,True
	HideEntity m\camera
	CameraRange m\camera,0.1,100
	CameraViewport m\camera,0,0, m\texture_width, m\texture_height
	CameraClsColor m\camera,0,0,0
	
	Return m\mesh
	
End Function
</pre><br><br>I was tinkering with the idea of a disable mirror commmand, so the mirror can be truely disabled, rather than hidden. Shouldn't be too hard to do though!<br><br><font class="tiny">Last edited 2011</font><br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1097791"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> I would copyrect from the buffer, rather than grab image <br></div><br>Of course! I'd been so used to working with locking buffers that I completely ignored the possiblity of just working directly with Buffers via SetBuffer.<br><br><br>As foir your code,<br>That's excellent, Ross... thank you so much! It's really made a lot of my ideas above obsolete, I still want to play around with it some. I'll post back when I have finished :)<br><br>It's much more versatile and capable than I really thought possible too! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
