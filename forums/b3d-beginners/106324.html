<!DOCTYPE html><html lang="en" ><head ><title >Model animation and control</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Model animation and control</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Model animation and control</a><br><br>
<a name="1307608"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >collimic</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am looking for some sample code to help me load an animated .b3d file that several sequences in it. I am trying to control a player model, make it move across the screen while playing a sequence. I am using the ninja.b3d file I found with pacemaker 1.0. I can get the keys to play the animation but the model will not move at the same time. <br><br>Sample code using WASD to move the player while playing sequence 1 would be very helpful.<br><br>Thank you all in advance<br>v/r <br><br></td></tr></table><br>
<a name="1307618"></a>

<a name="1307620"></a>

<a name="1307621"></a>

<a name="1307622"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dan</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Check the blitz3d folder Samples\mak\Castle there is a demo of mario3d like model running arround a Castle.<br><br>Or click, on the native blitz3d ide, the samples link (on the Help Tab), then choose mak\Castle <br><br></td></tr></table><br>
<a name="1307649"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >collimic</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have looked at the code included with the Castle but I do not truly understand how to get the model to move and animate at the same time. <br><br>Most of the code has been written by a programming student. So his code is very ruff. <br>Any info to help the student clean up his code and make the controls and movement work would be great.<br>v/r<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 640,480,16,2
SetBuffer BackBuffer()

Const ESC_KEY = 1

;bitterman keys
Const RIGHT_KEY = 205	; Move Right
Const LEFT_KEY = 203	; Move Left
Const FOUR_KEY = 75 	; Punch
Const FIVE_KEY = 76	; Weapon
Const SIX_KEY = 77		; Jump

;ninja keys
Const Q_KEY = 16		; Move Left
Const W_KEY = 17		; Move Right
Const D_KEY = 32		; Punch
Const F_KEY = 33		; Weapon
Const G_KEY = 34		; Jump

; Create camera
camera=CreateCamera()
PositionEntity camera,0,5,-6  

; Create the light
light=CreateLight(camera)

;Creating the terrain
ground=CreateTerrain (512)
PositionEntity ground, -500,-1,-500
ScaleEntity ground, 10,15,10

;Texture terrain
fighting_tex=LoadTexture("../images/fighting_tex.jpg")
EntityTexture ground,fighting_tex 
 
;Creating the sky
sky = CreateSphere (40)
FlipMesh sky
ScaleEntity sky, 500,100,500

;Loading the Heightmap
terrain=LoadTerrain("../images/valley.jpg")
ScaleEntity terrain,5,100,5
PositionEntity terrain,-500,0,-500
tex=LoadTexture("../images/fighting_tex.jpg")
ScaleTexture tex,50,50
EntityTexture terrain,tex
PositionEntity sky, 0,50,0
sky_tex = LoadTexture("../images/sky_tex.jpg")
EntityTexture sky,sky_tex
EntityFX sky,1

Include "ninja.bb"
Include "bitterman.bb"

;This following code deals with cameras and terrain
While Not KeyDown(ESC_KEY)

;Move Ninja
If KeyDown(2)
		Animate ninja,3,.25,1,5
	ElseIf KeyHit(Q_KEY)
		TranslateEntity ninja, -1,0,0
		Animate ninja,3,.25,1 ;Walk forward
		;;;FlushKeys 
	ElseIf KeyHit(W_KEY)
		TranslateEntity ninja, 1,0,0
		Animate ninja,3,-.25,1 ; Walk backwards
		;;;FlushKeys 
	ElseIf KeyHit(D_KEY)
		Animate ninja,3,.25,3 ; Punch
		FlushKeys
	ElseIf KeyHit(F_KEY)
		Animate ninja,3,.25,5 ; Sword
		FlushKeys
	ElseIf KeyHit(G_KEY)
		Animate ninja,3,.25,9 ; Jump
		FlushKeys
EndIf

;Move bitterman
If KeyDown(3)
		Animate bitterman,3,.25,1,5
	ElseIf KeyHit(LEFT_KEY)
		Animate bitterman,3,.25,2
		FlushKeys 
	ElseIf KeyHit(RIGHT_KEY)
		Animate bitterman,3,.25,3
		FlushKeys 
	ElseIf KeyDown(FOUR_KEY)
		Animate bitterman,3,.25,4
		FlushKeys
	ElseIf KeyDown(FIVE_KEY)
		Animate bitterman,3,.25,5
		FlushKeys
	ElseIf KeyDown(SIX_KEY)
		Animate bitterman,3,.25,6
		FlushKeys
EndIf

   If KeyDown(RIGH_KEY) = True Then TurnEntity camera,0,-.15,0
   If KeyDown(LEFT_KEY) = True Then TurnEntity camera,0,.15,0
   If KeyDown(DOWN_KEY) = True Then MoveEntity camera,0,0,-.15
   If KeyDown(UP_KEY) = True Then MoveEntity camera,0,0,.15

UpdateWorld 

    RenderWorld
    Flip

Wend
End
</textarea><br><br>Bitterman.bb<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;everyting to do with loading bitterman
;Load Bitterman Model

bitterman=LoadAnimMesh("../ninja/bitterman.b3d")
ScaleEntity bitterman,.5,.5,.5
PositionEntity bitterman,5,4,5
RotateEntity bitterman,0,45,0
;;;Animate bitterman,2

ExtractAnimSeq( bitterman,1,51 ) ;anim sequence 1:	Fall forward
ExtractAnimSeq( bitterman,52,101 ) ;anim sequence 2:	Fall backwards
ExtractAnimSeq( bitterman,102,152 ) ;anim sequence 3:	Pushed Backwards (and fall down)
ExtractAnimSeq( bitterman,153,228 ) ;anim sequence 4:	Fighting pose
ExtractAnimSeq( bitterman,229,255 ) ;anim sequence 5:	Right hand punch
ExtractAnimSeq( bitterman,256,322 ) ;anim sequence 6:	Moving Forward than backwards
ExtractAnimSeq( bitterman,323,340 ) ;anim sequence 7:	Sideways, moves legs in a circle??
ExtractAnimSeq( bitterman,341,354 ) ;anim sequence 8:	Jump kick (left leg)
ExtractAnimSeq( bitterman,355,386 ) ;anim sequence 9:	Backflip
ExtractAnimSeq( bitterman,387,407 ) ;anim sequence 10:	Crouch
ExtractAnimSeq( bitterman,408,416 ) ;anim sequence 11:	Standing
ExtractAnimSeq( bitterman,417,466 ) ;anim sequence 12:	Falling Down (backwards)
ExtractAnimSeq( bitterman,467,518 ) ;anim sequence 13:	Drop kick than stay laying down
ExtractAnimSeq( bitterman,519,573 ) ;anim sequence 14:	Fall forward onto ground (from the sky)
ExtractAnimSeq( bitterman,574,678 ) ;anim sequence 15:	Starting pose
ExtractAnimSeq( bitterman,679,692 ) ;anim sequence 16:	Walking forward crouched
ExtractAnimSeq( bitterman,693,712 ) ;anim sequence 17:	Two steps forward
ExtractAnimSeq( bitterman,713,728 ) ;anim sequence 18:	Running forward
ExtractAnimSeq( bitterman,729,743 ) ;anim sequence 19:	2 Steps back
ExtractAnimSeq( bitterman,744,760 ) ;anim sequence 20:	???
ExtractAnimSeq( bitterman,761,788 ) ;anim sequence 21:	Jumping
ExtractAnimSeq( bitterman,789,827 ) ;anim sequence 22:	Back flip
ExtractAnimSeq( bitterman,828,846 ) ;anim sequence 23:	Crouch
ExtractAnimSeq( bitterman,847,921 ) ;anim sequence 24:	Backflip
ExtractAnimSeq( bitterman,922,1041 ) ;anim sequence 25:	Back stretch / fighting pose
</textarea><br><br>Ninja.bb<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;everyting to do with loading ninja
;Load Ninja Model

ninja=LoadAnimMesh("../ninja/ninja.b3d")
ScaleEntity ninja,.5,.5,.5
PositionEntity ninja,0,2,5
RotateEntity ninja,0,45,0

;;;Animate ninja,2
RotateEntity ninja,0,100,0

ExtractAnimSeq( ninja,1,14 ) ;anim sequence 1:	Walk (normal)
ExtractAnimSeq( ninja,15,30 ) ;anim sequence 2:	Stealth Walk
ExtractAnimSeq( ninja,32,44 ) ;anim sequence 3:	Punch and swipe sword
ExtractAnimSeq( ninja,45,59 ) ;anim sequence 4:	Swipe and spin sword
ExtractAnimSeq( ninja,60,68 ) ;anim sequence 5:	Overhead twohanded downswipe
ExtractAnimSeq( ninja,69,72 ) ;anim sequence 6:	Up to block position (play backwards to lower sword if you want)
ExtractAnimSeq( ninja,73,83 ) ;anim sequence 7:	Forward kick
ExtractAnimSeq( ninja,84,93 ) ;anim sequence 8:	Pick up from floor (or down to crouch at frame 87)
ExtractAnimSeq( ninja,94,102 ) ;anim sequence 9:	Jump
ExtractAnimSeq( ninja,103,111 ) ;anim sequence 10:	Jump without height (for programmer controlled jumps)
ExtractAnimSeq( ninja,112,125 ) ;anim sequence 11:	High jump to Sword Kill (Finish em off move??)
ExtractAnimSeq( ninja,126,133 ) ;anim sequence 12:	Side Kick
ExtractAnimSeq( ninja,134,145 ) ;anim sequence 13:	Spinning Sword attack (might wanna speed this up in game)
ExtractAnimSeq( ninja,146,158 ) ;anim sequence 14:	Backflip
ExtractAnimSeq( ninja,159,165 ) ;anim sequence 15:	Climb wall
ExtractAnimSeq( ninja,166,173 ) ;anim sequence 16:	Death 1 , Fall back onto ground
ExtractAnimSeq( ninja,174,182 ) ;anim sequence 17:	Death 2 , Fall forward onto ground
ExtractAnimSeq( ninja,184,205 ) ;anim sequence 18:	Idle 1 , Breathe heavily
ExtractAnimSeq( ninja,206,250 ) ;anim sequence 19:	Idle 2
ExtractAnimSeq( ninja,251,300 ) ;anim sequence 20:	Idle 3
</textarea> <br><br></td></tr></table><br>
<a name="1307793"></a>

<a name="1307794"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cocopino</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> ExtractAnimSeq does not do anything unless you store the information into a variable to use it later.<br>E.g. <br><br><pre class=code>
Graphics3D 1600,900

Local camera = CreateCamera()
PositionEntity camera,0,5,0

Local ninja = LoadAnimMesh("media/ninja.b3d")
ScaleEntity ninja,.5,.5,.5
PositionEntity ninja,0,0,10

PointEntity(camera,ninja)
EntityParent(camera,ninja)
MoveEntity	(camera,0,3,0)

Local i
For i=0 To 20
	Local cube = CreateCube()
	PositionEntity(cube,-3,0,i*10)
Next

Local anim_idle = ExtractAnimSeq( ninja,206,250 ) ;Idle 2
Local anim_walk = ExtractAnimSeq( ninja,1,14 ) ;anim sequence 1:	Walk (normal)
Local anim = 0

While Not KeyHit(1)
	
	If KeyDown(17) Then 
		MoveEntity (ninja,0,0,0.5)
		If anim&lt;&gt;anim_walk Then
			anim=anim_walk
			Animate(ninja,1,0.3,anim)
		EndIf
	ElseIf anim&lt;&gt;anim_idle Then 
		anim=anim_idle
		Animate(ninja,1,0.1,anim)
	EndIf
	
	UpdateWorld()
	RenderWorld()
	Flip()
Wend
</pre><br><br>W key moves and animates ninja. <br><br></td></tr></table><br>
<a name="1307822"></a>

<a name="1307823"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is AWESOME code guys! Although it would be nice if the Ninja could smoothly transition between multiple animation such as walking and attacking with the sword at the same time.<br><br>ninja_test.bb: <br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800, 600, 0, 2

	Local camera = CreateCamera ( )
	
		PositionEntity camera, 0, 5, 0
		
			Local ninja = LoadAnimMesh ( "ninja/ninja.b3d" )
			
				ScaleEntity ninja, .5, .5, .5
				
					PositionEntity ninja, 0, 0, 10
				
				PointEntity ( camera, ninja )

			EntityParent ( camera, ninja )

		MoveEntity ( camera, 0, 3, 0 )

	Local i

For i=0 To 20

	Local cube = CreateCube ( )

	PositionEntity ( cube, -3, 0, i * 10 )

Next

Local anim_idle = ExtractAnimSeq ( ninja, 206, 250 ) ;Idle 2

Local anim_walk = ExtractAnimSeq ( ninja, 1, 14 ) ;anim sequence | 1: Walk (normal)

Local anim = 0

	While Not KeyHit ( 1 )

		If KeyDown ( 17 ) Or KeyDown ( 200 )

			MoveEntity ( ninja, 0, 0, 0.005 )

			If anim &lt;&gt; anim_walk

				anim = anim_walk

				Animate ( ninja, 1, 0.003, anim, 0.001 )

			EndIf

		ElseIf anim &lt;&gt; anim_idle

			anim = anim_idle

			Animate ( ninja, 1, 0.001, anim, 0.001 )

		EndIf

		UpdateWorld ( )
	
		RenderWorld ( )
	
		Flip 0
	
	Wend

End
</textarea><br><br>Thanks! =)<br><br>~GF <br><br></td></tr></table><br>
<a name="1308161"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Can someone please help make this Ninja Walk &amp; attack with the Sword at the same time?<br><br>Thank You!<br><br>~GF <br><br></td></tr></table><br>
<a name="1308181"></a>

<a name="1308182"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>make this Ninja Walk &amp; attack with the Sword at the same time<br> <br></div><br>If it is not an existing animation, you would have to create one yourself by mixing the two.<br>You can use Fragmotion to do this (the positions/rotations of the lower body while walking + the positions/rotations of the upper body while attacking) However if the two animations don't use the same number of frames, you may have to tweak it a little... <br><br></td></tr></table><br>
<a name="1308274"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank You, Remi. But that's not my point. I want to be able to do this in code.<br><br>Regards, <br><br>~GF <br><br></td></tr></table><br>
<a name="1308300"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cocopino</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> What probably would work is having two invisible ninjas as well as the "real" (visible) ninja. <br><br>Do not animate the visible ninja. Instead:<br>Animate invisible ninja 1 (walk animation), copy bone position + rotation to the legs/feet of the visible ninja.<br>Animate invisible ninja 2 (sword animation), copy bone position + rotation to the arms/hands of the visible ninja.<br>(Parenting the bones may also work)<br><br>The spine rotation/position may need an average between the two animations for rotation and position to make the new animation look natural.<br><br>You may then be able to store these new bone information using SetAnimKey but I've never actually used that command. <br><br></td></tr></table><br>
<a name="1308302"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> What Cocopino suggests may work, try it ! <br><br></td></tr></table><br>
<a name="1308306"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> That's a good idea, Cocopino! I WILL try it! =)<br><br>~GF <br><br></td></tr></table><br>
<a name="1308334"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rick Nasher</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> If it works please share, I'm kinda curious to see this working too. <br><br></td></tr></table><br>
<a name="1308338"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Additionally you may want to try this : <a href="http://www.blitzbasic.com/Community/posts.php?topic=105608" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=105608</a> (post#116) <br><br></td></tr></table><br>
<a name="1308341"></a>

<a name="1308343"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cocopino</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Proof of concept below.<br>You'll need to figure out which bones are legs/feet and which ones the arms/legs instead of having a counter of course. <br><br><pre class=code>
Graphics3D 1920,1080

Local camera = CreateCamera()
PositionEntity camera,0,5,-15

Local ninja = LoadAnimMesh("media/ninja.b3d")
Local ninja_inv1 = LoadAnimMesh("media/ninja.b3d")
Local ninja_inv2 = LoadAnimMesh("media/ninja.b3d")

EntityAlpha(ninja_inv1,0.01)
EntityAlpha(ninja_inv2,0.01)

PointEntity(camera,ninja)
EntityParent(camera,ninja)
MoveEntity(camera,0,6,0)

Local anim_walk = ExtractAnimSeq( ninja_inv1,1,14 ) ;anim sequence 1:	Walk (normal)
Local anim_sword = ExtractAnimSeq( ninja_inv2,32,44 ) ;anim sequence 3:	Punch and swipe sword
Local anim = 0

Animate(ninja_inv1,1,0.3,anim_walk)
Animate(ninja_inv2,1,0.3,anim_sword)

While Not KeyHit(1)
	
	Local bone = NextChild(ninja)
	Local bone1 = NextChild(ninja_inv1)
	Local bone2 = NextChild(ninja_inv2)
	Local counter = 0
	
	While bone
		
		If bone1 And bone2 Then
			
			counter=counter+1
			
			If counter&gt;14
				PositionEntity(bone,EntityX(bone2),EntityY(bone2),EntityZ(bone2))
				RotateEntity(bone,EntityPitch(bone2),EntityYaw(bone2),EntityRoll(bone2))
			Else
				PositionEntity(bone,EntityX(bone1),EntityY(bone1),EntityZ(bone1))
				RotateEntity(bone,EntityPitch(bone1),EntityYaw(bone1),EntityRoll(bone1))
			EndIf
			
			bone=NextChild(bone)
			bone1=NextChild(bone1)
			bone2=NextChild(bone2)
		Else
			bone=0
		EndIf
		
	Wend
	
	UpdateWorld()
	RenderWorld()
	
	Text 10,10,"total children: " + counter
	Flip()
	
Wend

Function NextChild(ent)
	
	Local siblingcnt
	If CountChildren(ent)&gt;0
		Return GetChild(ent,1)
	EndIf
	
	Local foundunused=False
	Local foundent = 0, parent,sibling
	While foundunused=False And ent&lt;&gt;0
		parent = GetParent(ent)
		If parent&lt;&gt;0
			If CountChildren(parent)&gt;1
				If GetChild(parent,CountChildren(parent))&lt;&gt;ent
					For siblingcnt = 1 To CountChildren(parent)
						sibling = GetChild(parent,siblingcnt)
						If sibling=ent
							foundunused = True
							foundent = GetChild(parent,siblingcnt+1)
						EndIf
					Next
				EndIf
			EndIf
		EndIf
		ent = parent
	Wend
	Return foundent
	
End Function
</pre> <br><br></td></tr></table><br>
<a name="1308350"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> AHAHAHAHA! OMFG! Cocopino, that's HYSTERICAL! Thank you! That kicks ASS! =D<br><br>~GF <br><br></td></tr></table><br>
<a name="1308351"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rick Nasher</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Always cool to see some nice animations. lol <br><br></td></tr></table><br>
<a name="1308381"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> A better approach imo, would be to create a new animation on your character joints/bones (with positionentity, rotateentity, setanimkey, addanimSeq) by analyzing the rotations/translations of the 2 animations that you want to mix.<br>This way it will be premade and easier to use when you need it. <br><br></td></tr></table><br>
<a name="1308398"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> Imho, that would actually be a worse way because at least in code you have infinite combinations, whereas in animations you would have to make the animations one by one.<br><br>~GF <br><br></td></tr></table><br>
<a name="1308416"></a>

<a name="1308417"></a>

<a name="1308418"></a>

<a name="1308419"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> @GF&gt;&gt;I agree, but reread my post, i just suggest to create a new animation (in code) by analyzing the positions/rotations of the joints of each frame of the 2 others existing animations.<br><br>An old ugly code to demonstrate how to create an animation in code with Blitz3d :<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D(640,480,32,2)

SeedRnd(MilliSecs())

Origine = CreateCube()
ScaleMesh(Origine,0.01/2,0.01/2,0.01/2)
EntityColor(Origine,255,000,255)

Camera = CreateCamera()
CameraRange(Camera,0.1,100)

;Create a skeleton with joints
JointRoot = CreatePivot()
PositionEntity(JointRoot,0,0,0)
MoveEntity(JointRoot,0,0,0)
NameEntity(JointRoot,"JRoot")

JointHipM = CreatePivot()
PositionEntity(JointHipM,0,0.9,0)
EntityParent(JointHipM,JointRoot,True)
NameEntity(JointHipM,"JHipM")
Debug_JointHipM = CreateSphere(8)
ScaleMesh(Debug_JointHipM,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointHipM,EntityX(JointHipM,True),EntityY(JointHipM,True),EntityZ(JointHipM,True))
EntityParent(Debug_JointHipM,JointHipM,True)

JointHipL = CreatePivot()
PositionEntity(JointHipL,-0.125,0.85,0)
EntityParent(JointHipL,JointHipM,True)
NameEntity(JointHipL,"JHipL")
Debug_JointHipL = CreateSphere(8)
ScaleMesh(Debug_JointHipL,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointHipL,EntityX(JointHipL,True),EntityY(JointHipL,True),EntityZ(JointHipL,True))
EntityParent(Debug_JointHipL,JointHipL,True)

JointKneeL = CreatePivot()
PositionEntity(JointKneeL,-0.125,0.45,0)
EntityParent(JointKneeL,JointHipL,True)
NameEntity(JointKneeL,"JKneeL")
Debug_JointKneeL = CreateSphere(8)
ScaleMesh(Debug_JointKneeL,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointKneeL,EntityX(JointKneeL,True),EntityY(JointKneeL,True),EntityZ(JointKneeL,True))
EntityParent(Debug_JointKneeL,JointKneeL,True)

JointAnkleL = CreatePivot()
PositionEntity(JointAnkleL,-0.125,0.075,0)
EntityParent(JointAnkleL,JointKneeL,True)
NameEntity(JointAnkleL,"JAnkleL")
Debug_JointAnkleL = CreateSphere(8)
ScaleMesh(Debug_JointAnkleL,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointAnkleL,EntityX(JointAnkleL,True),EntityY(JointAnkleL,True),EntityZ(JointAnkleL,True))
EntityParent(Debug_JointAnkleL,JointAnkleL,True)

JointHipR = CreatePivot()
PositionEntity(JointHipR,0.125,0.85,0)
EntityParent(JointHipR,JointHipM,True)
NameEntity(JointHipR,"JHipR")
Debug_JointHipR = CreateSphere(8)
ScaleMesh(Debug_JointHipR,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointHipR,EntityX(JointHipR,True),EntityY(JointHipR,True),EntityZ(JointHipR,True))
EntityParent(Debug_JointHipR,JointHipR,True)

JointKneeR = CreatePivot()
PositionEntity(JointKneeR,0.125,0.45,0)
EntityParent(JointKneeR,JointHipR,True)
NameEntity(JointKneeR,"JKneeR")
Debug_JointKneeR = CreateSphere(8)
ScaleMesh(Debug_JointKneeR,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointKneeR,EntityX(JointKneeR,True),EntityY(JointKneeR,True),EntityZ(JointKneeR,True))
EntityParent(Debug_JointKneeR,JointKneeR,True)

JointAnkleR = CreatePivot()
PositionEntity(JointAnkleR,0.125,0.075,0)
EntityParent(JointAnkleR,JointKneeR,True)
NameEntity(JointAnkleR,"JAnkleR")
Debug_JointAnkleR = CreateSphere(8)
ScaleMesh(Debug_JointAnkleR,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointAnkleR,EntityX(JointAnkleR,True),EntityY(JointAnkleR,True),EntityZ(JointAnkleR,True))
EntityParent(Debug_JointAnkleR,JointAnkleR,True)

JointVertebraM = CreatePivot()
PositionEntity(JointVertebraM,0,1.1,0)
EntityParent(JointVertebraM,JointHipM,True)
NameEntity(JointVertebraM,"JVertebraM")
Debug_JointVertebraM = CreateSphere(8)
ScaleMesh(Debug_JointVertebraM,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointVertebraM,EntityX(JointVertebraM,True),EntityY(JointVertebraM,True),EntityZ(JointVertebraM,True))
EntityParent(Debug_JointVertebraM,JointVertebraM,True)

JointShoulderL = CreatePivot()
PositionEntity(JointShoulderL,-0.175,1.45,0)
EntityParent(JointShoulderL,JointVertebraM,True)
NameEntity(JointShoulderL,"JShoulderL")
Debug_JointShoulderL = CreateSphere(8)
ScaleMesh(Debug_JointShoulderL,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointShoulderL,EntityX(JointShoulderL,True),EntityY(JointShoulderL,True),EntityZ(JointShoulderL,True))
EntityParent(Debug_JointShoulderL,JointShoulderL,True)

JointElbowL = CreatePivot()
PositionEntity(JointElbowL,-0.175,1.2,0)
EntityParent(JointElbowL,JointShoulderL,True)
NameEntity(JointElbowL,"JElbowL")
Debug_JointElbowL = CreateSphere(8)
ScaleMesh(Debug_JointElbowL,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointElbowL,EntityX(JointElbowL,True),EntityY(JointElbowL,True),EntityZ(JointElbowL,True))
EntityParent(Debug_JointElbowL,JointElbowL,True)

JointWristL = CreatePivot()
PositionEntity(JointWristL,-0.175,0.95,0)
EntityParent(JointWristL,JointElbowL,True)
NameEntity(JointWristL,"JWristL")
Debug_JointWristL = CreateSphere(8)
ScaleMesh(Debug_JointWristL,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointWristL,EntityX(JointWristL,True),EntityY(JointWristL,True),EntityZ(JointWristL,True))
EntityParent(Debug_JointWristL,JointWristL,True)

JointShoulderR = CreatePivot()
PositionEntity(JointShoulderR,0.175,1.45,0)
EntityParent(JointShoulderR,JointVertebraM,True)
NameEntity(JointShoulderR,"JShoulderR")
Debug_JointShoulderR = CreateSphere(8)
ScaleMesh(Debug_JointShoulderR,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointShoulderR,EntityX(JointShoulderR,True),EntityY(JointShoulderR,True),EntityZ(JointShoulderR,True))
EntityParent(Debug_JointShoulderR,JointShoulderR,True)

JointElbowR = CreatePivot()
PositionEntity(JointElbowR,0.175,1.2,0)
EntityParent(JointElbowR,JointShoulderR,True)
NameEntity(JointElbowR,"JElbowR")
Debug_JointElbowR = CreateSphere(8)
ScaleMesh(Debug_JointElbowR,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointElbowR,EntityX(JointElbowR,True),EntityY(JointElbowR,True),EntityZ(JointElbowR,True))
EntityParent(Debug_JointElbowR,JointElbowR,True)

JointWristR = CreatePivot()
PositionEntity(JointWristR,0.175,0.95,0)
EntityParent(JointWristR,JointElbowR,True)
NameEntity(JointWristR,"JWristR")
Debug_JointWristR = CreateSphere(8)
ScaleMesh(Debug_JointWristR,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointWristR,EntityX(JointWristR,True),EntityY(JointWristR,True),EntityZ(JointWristR,True))
EntityParent(Debug_JointWristR,JointWristR,True)

JointNeck = CreatePivot()
PositionEntity(JointNeck,0,1.45,0)
EntityParent(JointNeck,JointVertebraM,True)
NameEntity(JointNeck,"JNeck")
Debug_JointNeck = CreateSphere(8)
ScaleMesh(Debug_JointNeck,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointNeck,EntityX(JointNeck,True),EntityY(JointNeck,True),EntityZ(JointNeck,True))
EntityParent(Debug_JointNeck,JointNeck,True)

JointHead = CreatePivot()
PositionEntity(JointHead,0,1.6,0)
EntityParent(JointHead,JointNeck,True)
NameEntity(JointHead,"JHead")
Debug_JointHead = CreateSphere(8)
ScaleMesh(Debug_JointHead,0.05/2,0.05/2,0.05/2)
PositionEntity(Debug_JointHead,EntityX(JointHead,True),EntityY(JointHead,True),EntityZ(JointHead,True))
EntityParent(Debug_JointHead,JointHead,True)

;Create body parts and set each body part as a child of a joint

;Pelvis

;UpperLegL

;LowerLegL

;FootL

;UpperLegR

;LowerLegR

;FootR

;StomachLowerBack

;ChestUpperBack

;UpperArmL

;LowerArmL

;HandL

;UpperArmR

;LowerArmR

;HandR

;Neck

;Head

;Create an animation of the human character with animation frames/poses
FrameId% = 0

;Animation StandIdle
;new frame : 
FrameId = FrameId + 1 
;turn move the joints : 
RotateEntity(JointRoot,0,0,0)
RotateEntity(JointHipM,0,0,0)
RotateEntity(JointHipL,0,0,0) 
RotateEntity(JointKneeL,0,0,0)
RotateEntity(JointAnkleL,0,0,0)
RotateEntity(JointHipR,0,0,0)
RotateEntity(JointKneeR,0,0,0)
RotateEntity(JointAnkleR,0,0,0)
RotateEntity(JointVertebraM,0,0,0)
RotateEntity(JointShoulderL,0,0,0) 
RotateEntity(JointElbowL,0,0,0) 
RotateEntity(JointWristL,0,0,0) 
RotateEntity(JointShoulderR,0,0,0)
RotateEntity(JointElbowR,0,0,0) 
RotateEntity(JointWristR,0,0,0) 
RotateEntity(JointNeck,0,0,0)
RotateEntity(JointHead,0,0,0)
;capture the frame :  
SetAnimKey(JointRoot,FrameId) 
SetAnimKey(JointHipM,FrameId)
SetAnimKey(JointHipL,FrameId)
SetAnimKey(JointKneeL,FrameId)
SetAnimKey(JointAnkleL,FrameId)
SetAnimKey(JointHipR,FrameId)
SetAnimKey(JointKneeR,FrameId)
SetAnimKey(JointAnkleR,FrameId)
SetAnimKey(JointVertebraM,FrameId) 
SetAnimKey(JointShoulderL,FrameId)
SetAnimKey(JointElbowL,FrameId)
SetAnimKey(JointWristL,FrameId)
SetAnimKey(JointShoulderR,FrameId)
SetAnimKey(JointElbowR,FrameId)
SetAnimKey(JointWristR,FrameId)
SetAnimKey(JointNeck,FrameId)
SetAnimKey(JointHead,FrameId)

;Animation WalkForward
;new frame :
FrameId = FrameId + 1 
;turn move the joints : 
RotateEntity(JointRoot,0,0,0)
RotateEntity(JointHipM,0,-10,-5)
RotateEntity(JointHipL,33.75,0,0) 
RotateEntity(JointKneeL,22.5,0,0)
RotateEntity(JointAnkleL,11.25,0,0)
RotateEntity(JointHipR,-45,0,0)
RotateEntity(JointKneeR,0,0,0)
RotateEntity(JointAnkleR,0,0,0)
RotateEntity(JointVertebraM,0,-12,5)
RotateEntity(JointShoulderL,-33.75,0,0) 
RotateEntity(JointElbowL,-45,0,0) 
RotateEntity(JointWristL,-5,0,0) 
RotateEntity(JointShoulderR,22.75,0,0)
RotateEntity(JointElbowR,0,0,0) 
RotateEntity(JointWristR,0,0,0) 
RotateEntity(JointNeck,0,5,-2.5)
RotateEntity(JointHead,0,-10,0)
;capture the frame : 
SetAnimKey(JointRoot,FrameId) 
SetAnimKey(JointHipM,FrameId)
SetAnimKey(JointHipL,FrameId)
SetAnimKey(JointKneeL,FrameId)
SetAnimKey(JointAnkleL,FrameId)
SetAnimKey(JointHipR,FrameId)
SetAnimKey(JointKneeR,FrameId)
SetAnimKey(JointAnkleR,FrameId)
SetAnimKey(JointVertebraM,FrameId) 
SetAnimKey(JointShoulderL,FrameId)
SetAnimKey(JointElbowL,FrameId)
SetAnimKey(JointWristL,FrameId)
SetAnimKey(JointShoulderR,FrameId)
SetAnimKey(JointElbowR,FrameId)
SetAnimKey(JointWristR,FrameId)
SetAnimKey(JointNeck,FrameId)
SetAnimKey(JointHead,FrameId)
;new frame :
FrameId = FrameId + 1 
;turn move the joints :
RotateEntity(JointRoot,0,0,0)
RotateEntity(JointHipM,0,0,0)
RotateEntity(JointHipL,0,0,0) 
RotateEntity(JointKneeL,0,0,0)
RotateEntity(JointAnkleL,0,0,0)
RotateEntity(JointHipR,0,0,0)
RotateEntity(JointKneeR,0,0,0)
RotateEntity(JointAnkleR,0,0,0)
RotateEntity(JointVertebraM,0,0,0)
RotateEntity(JointShoulderL,0,0,0) 
RotateEntity(JointElbowL,0,0,0) 
RotateEntity(JointWristL,0,0,0) 
RotateEntity(JointShoulderR,0,0,0)
RotateEntity(JointElbowR,0,0,0) 
RotateEntity(JointWristR,0,0,0) 
RotateEntity(JointNeck,0,0,0)
RotateEntity(JointHead,0,0,0)
;capture the frame :  
SetAnimKey(JointRoot,FrameId) 
SetAnimKey(JointHipM,FrameId)
SetAnimKey(JointHipL,FrameId)
SetAnimKey(JointKneeL,FrameId)
SetAnimKey(JointAnkleL,FrameId)
SetAnimKey(JointHipR,FrameId)
SetAnimKey(JointKneeR,FrameId)
SetAnimKey(JointAnkleR,FrameId)
SetAnimKey(JointVertebraM,FrameId) 
SetAnimKey(JointShoulderL,FrameId)
SetAnimKey(JointElbowL,FrameId)
SetAnimKey(JointWristL,FrameId)
SetAnimKey(JointShoulderR,FrameId)
SetAnimKey(JointElbowR,FrameId)
SetAnimKey(JointWristR,FrameId)
SetAnimKey(JointNeck,FrameId)
SetAnimKey(JointHead,FrameId)
;new frame :
FrameId = FrameId + 1 
;turn move the joints : 
RotateEntity(JointRoot,0,0,0)
RotateEntity(JointHipM,0,10,5)
RotateEntity(JointHipL,-45,0,0) 
RotateEntity(JointKneeL,0,0,0)
RotateEntity(JointAnkleL,0,0,0)
RotateEntity(JointHipR,33.75,0,0)
RotateEntity(JointKneeR,22.5,0,0)
RotateEntity(JointAnkleR,11.25,0,0)
RotateEntity(JointVertebraM,0,-10,-5)
RotateEntity(JointShoulderL,22.75,0,0) 
RotateEntity(JointElbowL,0,0,0) 
RotateEntity(JointWristL,0,0,0) 
RotateEntity(JointShoulderR,-33.75,0,0)
RotateEntity(JointElbowR,-45,0,0) 
RotateEntity(JointWristR,-5,0,0) 
RotateEntity(JointNeck,0,-5,-2.5)
RotateEntity(JointHead,0,10,0)

;capture the frame :  
SetAnimKey(JointRoot,FrameId) 
SetAnimKey(JointHipM,FrameId)
SetAnimKey(JointHipL,FrameId)
SetAnimKey(JointKneeL,FrameId)
SetAnimKey(JointAnkleL,FrameId)
SetAnimKey(JointHipR,FrameId)
SetAnimKey(JointKneeR,FrameId)
SetAnimKey(JointAnkleR,FrameId)
SetAnimKey(JointVertebraM,FrameId) 
SetAnimKey(JointShoulderL,FrameId)
SetAnimKey(JointElbowL,FrameId)
SetAnimKey(JointWristL,FrameId)
SetAnimKey(JointShoulderR,FrameId)
SetAnimKey(JointElbowR,FrameId)
SetAnimKey(JointWristR,FrameId)
SetAnimKey(JointNeck,FrameId)
SetAnimKey(JointHead,FrameId)
;new frame : 
FrameId = FrameId + 1 
;turn move the joints : 
RotateEntity(JointRoot,0,0,0)
RotateEntity(JointHipM,0,0,0)
RotateEntity(JointHipL,0,0,0) 
RotateEntity(JointKneeL,0,0,0)
RotateEntity(JointAnkleL,0,0,0)
RotateEntity(JointHipR,0,0,0)
RotateEntity(JointKneeR,0,0,0)
RotateEntity(JointAnkleR,0,0,0)
RotateEntity(JointVertebraM,0,0,0)
RotateEntity(JointShoulderL,0,0,0) 
RotateEntity(JointElbowL,0,0,0) 
RotateEntity(JointWristL,0,0,0) 
RotateEntity(JointShoulderR,0,0,0)
RotateEntity(JointElbowR,0,0,0) 
RotateEntity(JointWristR,0,0,0) 
RotateEntity(JointNeck,0,0,0)
RotateEntity(JointHead,0,0,0)
;capture the frame :  
SetAnimKey(JointRoot,FrameId) 
SetAnimKey(JointHipM,FrameId)
SetAnimKey(JointHipL,FrameId)
SetAnimKey(JointKneeL,FrameId)
SetAnimKey(JointAnkleL,FrameId)
SetAnimKey(JointHipR,FrameId)
SetAnimKey(JointKneeR,FrameId)
SetAnimKey(JointAnkleR,FrameId)
SetAnimKey(JointVertebraM,FrameId) 
SetAnimKey(JointShoulderL,FrameId)
SetAnimKey(JointElbowL,FrameId)
SetAnimKey(JointWristL,FrameId)
SetAnimKey(JointShoulderR,FrameId)
SetAnimKey(JointElbowR,FrameId)
SetAnimKey(JointWristR,FrameId)
SetAnimKey(JointNeck,FrameId)
SetAnimKey(JointHead,FrameId)

;Save the captured frames in an animation sequence : 
AddAnimSeq(JointRoot,FrameId) 

;Load the animation sequences
AnimationStandIdle = ExtractAnimSeq(JointRoot,0,0)  
AnimationWalkForward = ExtractAnimSeq(JointRoot,1,5)  

PositionEntity(Camera,1.5,0.875,1.5)
RotateEntity(Camera,0,135,0)

While(KeyDown(1)&lt;&gt;1)
 
 If(KeyDown(30)=1)
  TurnEntity(Camera,0,1.5,0)
 ElseIf(KeyDown(32)=1)
  TurnEntity(Camera,0,-1.5,0)
 EndIf
 If(KeyDown(17)=1)
  MoveEntity(Camera,0,0,0.075)
 ElseIf(KeyDown(31)=1)
  MoveEntity(Camera,0,0,-0.075)
 EndIf

 ;animate the skeleton (the root joint)
 If(KeyDown(57)=0)
  If(Not Animating(JointRoot))
   Animate(JointRoot,3,0.1,AnimationStandIdle)
  EndIf 
 ElseIf(KeyDown(57)=1)
  If(Not Animating(JointRoot))
   Animate(JointRoot,3,0.1,AnimationWalkForward)
  EndIf 
 EndIf

 UpdateWorld()

 If(KeyDown(2)=0)
  Wireframe(False)
 ElseIf(KeyDown(2)=1)
  Wireframe(True)
 EndIf 

 SetBuffer(BackBuffer())
 RenderWorld()

 Flip(1)

Wend

End()
</textarea> <br><br></td></tr></table><br>
<a name="1308425"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> That's not ugly Code, Remi. That's actually beautiful code. =)<br><br>~GF <br><br></td></tr></table><br>
<a name="1324221"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >gpete</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> I found it useful to see coded animation! So the character mesh needs to be separate entities attached to the joints? (Yes or No??). In the samples there was a program by Birdie I think that used a one entity mesh that was attached to the joints- I believe attached to vertexes? I find it hard to wrap my head around that- I guess I consider characters to be like "puppet" models.. To me, the difficulty of animation is the main thing that holds me from creating human character-based programs.  <br>I know a lot of things I post are redundant- but I just retired a year ago and now I have time to code again... (wish I had the sharper mind I had 15 years ago ;-) <br><br>Any comments? <br><br></td></tr></table><br>
<a name="1324227"></a>

<a name="1324228"></a>

<a name="1324229"></a>

<a name="1324230"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>So the character mesh needs to be separate entities attached to the joints?<br> <br></div><br>not necessarily<br><br>you need a skeleton with joints/bones<br><br>then you can either create separate body parts and set each body part as a child of  the appropriate joint/bone (so that it will turn/move with it) or you can create a whole body and then set influences (weights) of some joints/bones over some vertices (then called "skinned vertices") and then the whole body will be deformed depending on how the joints/bones turn/move (when animating)<br><br>you can create a mesh, and then a skeleton with joints/bones, and then influences (weights) of some joints/bones over some vertices by using a modeling/rigging/skinning/animating tool like Fragmotion <br><br></td></tr></table><br>
<a name="1324233"></a>

<a name="1324234"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#23">[#23]</a></td></tr></table></td></tr><tr ><td class="posttext"> here is a simple example on how to animate a rigged skinned mesh (with a b3d rigged skinned animated mesh provided) :<br><a href="http://rd-stuff.fr/rigged-skinned-mesh-and-animation-201611042221.7z" target="_blank">http://rd-stuff.fr/rigged-skinned-mesh-and-animation-201611042221.7z</a> <br><br></td></tr></table><br>
<a name="1324346"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >gpete</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#24">[#24]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for your reply Remi- you certainly write very interesting programming threads.  <br>What I am going to try to do is create a animation list for several basic animations using a simple list of 3d coordinates moves for the joint or body limb/objects. I have looked at Milkshape and and Blender animations where you manually move the joints in a animation sequence- but I do not want to use an external program for animation.<br>I want the functions to be in Blitz 3D code:<br>WALK function: right and left leg anim for one step- trigger is a KeyDown-<br>Raise Arm function and use gun:  and another to swing sword<br>Turn head function:<br>Falldown Function:<br>etc, etc.... <br>If I can do it I will share the code.. (   ;-) ) <br><br></td></tr></table><br>
<a name="1324347"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#25">[#25]</a></td></tr></table></td></tr><tr ><td class="posttext"> Of course you can create an animation in code (see my example in post #19) but it will be less intuitive and less realistic.<br>You certainly will have difficulties to create complex animations...<br><br>(i suggest to try Fragmotion, it is easy enough to use, you have all you need to create meshes, skeletons (with joints/bones), influences (weights) of some joints/bones over some vertices, animations with poses , and the b3d export is clean) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
