<!DOCTYPE html><html lang="en" ><head ><title >Read files in directory?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Read files in directory?</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Read files in directory?</a><br><br>
<a name="1205328"></a>

<a name="1205329"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi all. I was working on my project as usual &amp; everything was going well. Until I needed a function which would grab all the names of files &amp; their full directories into the same function return variable and then returning it via a function.<br><br>I need to be able to grab from folder "X", file(s) "10293.x", "10294.x", etc, and it must grab their FULL directories as well, so "10293" becomes "C:\Users\X\Desktop\10293.x" &amp; so on until it has read through ONLY once, ALL the files in the directory, "meshes".<br><br>Here's the code:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics3D(800, 600, 0, 2)

Global folder$ = "meshes"

	While Not KeyHit(1)
		files$ = Grab_Files$(folder$)
			If files$&lt;&gt;""
				Print files$
			EndIf
		Flip()
	Wend
End

Function Grab_Files$(folder$)

	;Open up the directory, and assign the handle to myDir
	myDir=ReadDir(folder$)

	; Let's loop forever until we run out of files/folders to list!
	Repeat
	
		; Assign the next entry in the folder to file$
		file$=NextFile$(myDir)
		
		; If there isn't another one, let's exit this loop
		If file$="" Then Exit
		
		; Use FileType to determine if it is a folder (value 2) or a file and print  results
		If FileType(folder$+"\"+file$) = 2 Then
			final$ = folder$
		ElseIf FileType(folder$+"\"+file$)=1 Then
			final$ = folder$+"\"+file$
			file_count=file_count+1
		End If
		
		Return final$
		
	Forever
	
End Function</textarea><br><br>If you need me to explain further, just ask me and I will do my best to go a bit deeper into what I need help with.<br><br>Thank you guys! <br><br></td></tr></table><br>
<a name="1205454"></a>

<a name="1205455"></a>

<a name="1205456"></a>

<a name="1205457"></a>

<a name="1205458"></a>

<a name="1205459"></a>

<a name="1205460"></a>

<a name="1205539"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >virtlands</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Thundros, <br><br> Here's a redo, in case you like...<br> Will use a function <b>List_X_Files( x_path$,  ".x" )</b><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">;; __ Here we go ___

 Global  A_max = 300
 Dim A$(A_max)

 Local FC   ;; FC = file count 

 Graphics 800,600,32,2 

 Print 
 Print " 'X' files Lister  :: THE TRUTH IS OUT THERE. "
 Print 

 saveDir = CurrentDir$()     ;; save a copy of your current directory
 x_path$ = "C:\Users\X\Desktop"   ;; Adjust this destination path to your liking.

 FC = List_X_Files( x_path,  ".x" )   ;; ... List those files with that kind of extension ...

;;;; -- This next version of the function is optional, 
;;;; -- will send the data to an array (instead of printing it). 
;;;; -- [ uncomment it to make it active ]
;; FC = List_X_Files_to_array( x_path,  ".x" )  

 Print : Print"  __ File count = "+FC
 Print 

  ;;;;  __ This next loop is optional, if your want it. _
  ;
  ; Print"  __ Files Found : "
  ; For z=1 To FC 
  ;         Print "  "+A(z)
  ; Next 

 ChangeDir savedir            ;; return to the saved directory

 Print
 WaitKey():End 


  ;;  The optional ext$ parameter lets you search for files of any extension,  
  ;;  such as ".txt",  ".x",  or as you wish,   [ defaults to ".x" ]

Function List_X_Files(path$, ext$)

        Local saveDir$, curDir$, file$
        Local ft, L, xCount, Le

        If ext$="" Then ext$ = ".x"
        Le = Len(ext)
        
        ext = Lower$(ext) ;; Just in case

        saveDir = CurrentDir$()   ;; save your previous directory
        path = lastslash(path)     ;; add a "\" to the end of "path"

        If savedir &lt;&gt; path
                   ChangeDir path
        End If 

        myDir = ReadDir(path)

        Repeat 
                      file = Lower$(NextFile$(myDir))
                      L = Len(file)
                      If L=0 Then Exit

                      ft = FileType(file)
                      If ft=1
                                  If Mid$(file, L-Le+1, Le) = ext
                                              Print "  "+path+file
                                              xCount = xCount +1
                                  End If                                  
                      End If   
        Forever

        CloseDir myDir

        If savedir &lt;&gt; path
                   ChangeDir savedir
        End If 

End Function 


Function List_X_Files_to_array(path$, ext$  )
        Local saveDir$, curDir$, file$
        Local ft, L, xCount, Le

        If ext$="" Then ext$ = ".x"
        Le = Len(ext)

        saveDir = CurrentDir$()   ;; save your previous directory
        path = lastslash(path)     ;; add a "\" to the end of "path"

        If savedir &lt;&gt; path
                   ChangeDir path
        End If 

        myDir = ReadDir(path)

        Repeat 
                      file = Lower$(NextFile$(myDir))
                      L = Len(file)
                      If L=0 Then Exit

                      ft = FileType(file)
                      If ft=1
                                  If Mid$(file, L-Le+1, Le) = ext                                          
                                              xCount = xCount +1                                              

                                              If xCount&lt;= A_max
                                                          A(xCount) = path+file
                                              Else 
                                                          Print" The array index is out of bounds "
                                              End If 
                                  End If                                  
                      End If   
        Forever

        CloseDir myDir

        If savedir &lt;&gt; path
                   ChangeDir savedir
        End If 

        Return xCount 

End Function 



Function lastslash$(s$)    ; Gives a last slash to any string that is missing one
       L = Len(s)
       If L
               If Mid$(s,L,1) = "\" 
                       Return s
               Else  
                       Return s+"\"
               End If
       Else 
               Return s
       End If 
End Function
 </textarea> <br><br></td></tr></table><br>
<a name="1205461"></a>

<a name="1205462"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi, @virtlands. Thank you, this is AMAZING! There's only 1 small problem though. I would like the ability to use a Return function and return the array of results kinda like PHP does.<br><br>What I mean is, I would like to return: <br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Global max_arrs = 1

Dim myarr$(max_arrs)
Function myfunc(maxarrs)
    myvar$ = myarr$(max_arrs)
    Return myvar$
End Function</textarea><br><br>Like that.<br><br>If you can do that for me, I would hug you. LOL<br><br>Thanks, @virtlands! <br><br></td></tr></table><br>
<a name="1205468"></a>

<a name="1205469"></a>

<a name="1205535"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >virtlands</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hmmm, are you trying to . . .<br><br> (<b>A:</b>) Return an array of strings ?  (or, return a pointer to an array of strings ?)<br> (<b>B:</b>) Allow the function to return 1 string at a time ? <br><br></td></tr></table><br>
<a name="1205499"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I need to do (A). <br><br></td></tr></table><br>
<a name="1205543"></a>

<a name="1205551"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >virtlands</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm not sure, <br>but I doubt it's possible to return an array of strings from a Blitz3d function.<br>That might be possible to do in 'C' instead.<br><br>Were you considering the possibility of concatenating all found <br>files into 1 huge string ?  (and then returning that 1 string?)<br>[ I haven't considered that possibility yet;  It's too unusual. ]<br><br><br>What follows is another recoding.<br>The purpose here is to return arbitrary collections of string data,<br>by way of <i>indexes</i>.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
 ;; This is an effort in using a "String Stack" to return an
 ;; arbitrary collection of string values...

 ;; coded by VirtLands,  Oct 29,2013

 Global  A_max = 300
 Dim A$(A_max)

 Global ssmax = 32     ;; .. start with about 32 items.
 Global ssp=0          ;; .. an index pointer into sstack.
 Dim  sstack$(ssmax)   ;; .. create a string stack,  [ index 0 shall be deliberately left as empty, "" ]

 ;;; sstack(n)  can be automatically redimensioned later, using  "redim_sstack(n)"

 Type ssr                    ;;  "string stack" range type
          Field i_start      ;;  The purpose of this type is to store indexes 
          Field i_end       ;;   into the sstack(..)
 End Type                   ;;   if  i_end=0, then that means no data was found
 

 Local FC   ;; FC = file count 

 Graphics 800,600,32,2 

 Print 
 Print " 'X' files Lister  :: THE TRUTH WAS FOUND. "
 Print 

 saveDir = CurrentDir$()    
 x_path$ = "C:\users\x\desktop"     

 Local R.ssr =Null   ;; create an "index range" type

 R = List_X_Files_ssr( x_path,  ".x" )  ;; retrieve information in an "index" format

 If R\i_end&lt;&gt;0   ;; THE FOUND FILES ARE AS FOLLOWS:::

            For z= R\i_start To R\i_end
                      Print "   "+sstack(z)
            Next 
 
            Print:Print" The number of files found = "+ ( R\i_end - R\i_start +1 )        
 End If 

 ChangeDir savedir          ;; return to the saved directory
 
 Print: Print" Ok. "
 WaitKey():End 


;; This function returns an SSR type, ("indexes" into the "string stack")
;; These indexes describe an arbitrary collection of string values.
;;
Function List_X_Files_ssr.ssr(path$, ext$)

        Local saveDir$, curDir$, file$
        Local ft, L, xCount, Le
        Local ssr.ssr

        ssr = New ssr 

        ssr\i_start = ssp  +1   ;; point to the next unused string stack index

        If ext$="" Then ext$ = ".x"
        Le = Len(ext)
        
        ext = Lower$(ext) ;; Just in case

        saveDir = CurrentDir$()   ;; save your previous directory
        path = lastslash(path)     ;; add a "\" to the end of "path"

        If savedir &lt;&gt; path
                   ChangeDir path
        End If 

        myDir = ReadDir(path)
        If myDir

        Repeat 
                      file = Lower$(NextFile$(myDir))
                      L = Len(file)
                      If L=0 Then Exit

                      ft = FileType(file)
                      If ft=1
                                  If Mid$(file, L-Le+1, Le) = ext
                                  
                                             pushSS(path+file)     ;; push an item onto the string stack                                           
                                                                              ;; ssp is automatically incremented
                                             xCount = xCount +1
                                  End If                                  
                      End If   
        Forever

        CloseDir myDir
        End If 

        If savedir &lt;&gt; path
                   ChangeDir savedir
        End If 
  
        If xCount
                   ssr\i_end = ssp  ;; store the ending index into "i_end"
        End If 

        ;;; If  ssr\i_end =0,  then that means that NO files were found

        Return ssr
End Function 


Function pushSS$(s$)    ;; pushes an item onto the string stack.
           If ssp = ssmax
                      redim_sstack(ssmax+100)  ; redimension if necessary
           End If 
           ssp = ssp +1
           sstack(ssp) = s
           Return s
End Function 

Function popSS$()   ;; pops an item from the string stack.
           Local s$
           If ssp=0
                      Return s
           End If 
           s = sstack(ssp)
           ssp = ssp -1
           Return s  
End Function 


;; This function redimensions the "string stack" when it runs out of space....
;;        You can redimension UP as well as redimension DOWN,
;;        but most people redimension UP.  ;)  
;;  parameter 'n' is the new amount of items. 
;;
Function redim_sstack(n)
     Local tempfile$ = "sstack.tmp"
     Local z,L
     L = lesser(ssmax,n)

     If ssp&lt;&gt;n  ;;  There is no use in running this code if ssmax =n

          fp = WriteFile(tempfile)
          If fp 
                  For z = 0 To L 
                           WriteString(fp, sstack(z))
                           sstack(z) = ""   ;; free up some string memory
                  Next 
                  CloseFile(fp)

                  Dim sstack(n)  ;; Re-dimension it to the new value "n"
                  ssmax = n   

                  fp = ReadFile(tempfile)
                  For z = 0 To L
                            sstack(z) = ReadString(fp) ;; Read the data back in
                  Next 
                  CloseFile(fp)   
          Else 
                  Print" There was an error creating "+Q(tempfile) + " ... "
                  Print" ___________________________________
                  WaitKey():End 
          End If    
     End If 

     Return n
End Function 


Function Lesser(a,b)     ;;; returns the lesser of a,b
       If a&lt;b 
                 Return a
       Else 
                 Return b   
       End If
End Function 

Function greater(a,b)   ;;; returns the greater of a,b
       If a=lesser(a,b)
                Return b
       Else  
                Return a 
       End If
End Function 

Function we()
         WaitKey():End 
End Function 

;; returns a string in quotes
Function Q$(Q$) 
             Return Chr$(34)+Q$+Chr$(34)
End Function 

Function lastslash$(s$)    ; Gives a last slash to any string that is missing one
       L = Len(s)
       If L
               If Mid$(s,L,1) = "\" 
                       Return s
               Else  
                       Return s+"\"
               End If
       Else 
               Return s
       End If 
End Function </textarea><br><br>Thanks for the virtual hug. <img src="http://www.picgifs.com/smileys/smileys-and-emoticons/holidays/smileys-holidays-597927.png">  <img src="http://www.picgifs.com/smileys/smileys-and-emoticons/holidays/smileys-holidays-307967.png"> <br><br></td></tr></table><br>
<a name="1205550"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> No problem, bro. You earned that hug! :P <br><br></td></tr></table><br>
<a name="1205553"></a>

<a name="1205555"></a>

<a name="1205556"></a>

<a name="1205557"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here ya go, I reformatted the source code so that it's easier to understand (Mainly changed spaces to tabs) :P<br><br>Credit: Virtlands<br><br>==Return array values from functions==<br><br>ReturnArray.bb: <br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">;; This is an effort in using a "String Stack" to return an
;; arbitrary collection of string values...

;; Coded by VirtLands,  Oct 29,2013
;; Edited by Thundros,  Oct 29,2013

Global  A_max = 9999999
Dim A$(A_max)

;; .. Start with about 9999999 items.
Global ssmax = 9999999

;; .. An index pointer into sstack.
Global ssp=0

;; .. Create a string stack,
;;  [ index 0 shall be deliberately left as empty, "" ]
Dim sstack$(ssmax)

;;; Sstack(n)  can be automatically redimensioned later, using  "redim_sstack(n)"
;; "String stack" range type
Type ssr
	;; The purpose of this type is to store indexes
	Field i_start
	;; Into the sstack(..)
	Field i_end
;; If i_end=0, then that means no data was found
End Type

;; FC = file count
Local FC

Graphics 800,600,32,2 

Print 
Print " 'X' files Lister  :: THE TRUTH WAS FOUND. "
Print 

saveDir$ = CurrentDir$()    
x_path$ = saveDir$+"meshes\"

;; Create an "index range" type
Local R.ssr =Null

;; Retrieve information in an "index" format
R = List_X_Files_ssr( x_path,  ".x" )

;; :::THE FOUND FILES ARE AS FOLLOWS:::
If R\i_end&lt;&gt;0

	For z= R\i_start To R\i_end
		Print "   "+sstack(z)
	Next

	Print:Print" The number of files found = "+ ( R\i_end - R\i_start +1 )

End If

;; Return to the saved directory
ChangeDir savedir

Print: Print" Ok. "
WaitKey():End

;; This function returns an SSR type, ("indexes" into the "string stack")
;; These indexes describe an arbitrary collection of string values.
;;
Function List_X_Files_ssr.ssr(path$, ext$)

	Local saveDir$, curDir$, file$
	Local ft, L, xCount, Le
	Local ssr.ssr

	ssr = New ssr

	;; Point to the next unused string stack index
	ssr\i_start = ssp  +1

	If ext$="" Then ext$ = ".x"
		
		Le = Len(ext)

		;; Just in case
		ext = Lower$(ext)

		;; Save your previous directory
		saveDir = CurrentDir$()

		;; Add a "\" to the end of "path"
		path = lastslash(path)

		If savedir &lt;&gt; path
			ChangeDir path
		End If 

		myDir = ReadDir(path)
		If myDir

			Repeat
			
				file = Lower$(NextFile$(myDir))
				L = Len(file)
				If L=0 Then Exit
	
				ft = FileType(file)
				If ft=1
					If Mid$(file, L-Le+1, Le) = ext
						;; Push an item onto the string stack
						pushSS(path+file)
						xCount = xCount +1
					End If
				End If
				
			Forever
	
			CloseDir myDir

		End If 

		If savedir &lt;&gt; path
		
			ChangeDir savedir

		End If

		;; Store the ending index into "i_end"
		If xCount
			ssr\i_end = ssp
		End If

	;;; If  ssr\i_end =0, then
	;;; that means that NO files
	;;; were found
	Return ssr

End Function 

;; Pushes an item onto the string stack.
Function pushSS$(s$)
	If ssp = ssmax
		; Redimension if necessary
		redim_sstack(ssmax+100)
	End If
	ssp = ssp +1
	sstack(ssp) = s
	Return s
End Function 

;; Pops an item from the string stack
Function popSS$()
	Local s$
   If ssp=0
		Return s
	End If
	s = sstack(ssp)
	ssp = ssp -1
	Return s  
End Function 

;; This function redimensions the "string stack" when it runs out of space....
;; You can redimension UP as well as redimension DOWN,
;; but most people redimension UP. ;)
;; parameter 'n' is the new amount of items. 
;;
Function redim_sstack(n)
	
	Local tempfile$ = "sstack.tmp"
	Local z,L
	L = lesser(ssmax,n)

	;; There is no use in running this code if ssmax =n
	If ssp&lt;&gt;n

		fp = WriteFile(tempfile)
		If fp
			For z = 0 To L
				WriteString(fp, sstack(z))
				;; Free up some string memory
				sstack(z) = ""
			Next
			CloseFile(fp)

			;; Re-dimension it to the new value "n"
			Dim sstack(n)
			ssmax = n

			fp = ReadFile(tempfile)
			For z = 0 To L
				;; Read the data back in
				sstack(z) = ReadString(fp)
			Next
			CloseFile(fp)

		Else
		
			Print" There was an error creating "+Q(tempfile) + " ... "
			Print" ___________________________________
			WaitKey():End
			
		End If
		
	End If

	Return n
	
End Function 

;;; Returns the lesser of a, b
Function Lesser(a,b)
	If a&lt;b 
		Return a
	Else
		Return b   
	End If
End Function 

;;; Returns the greater of a, b
Function greater(a,b)
	If a=lesser(a,b)
		Return b
	Else
		Return a 
	End If
End Function 

Function we()
	WaitKey():End 
End Function 

;; Returns a string in quotes
Function Q$(Q$) 
	Return Chr$(34)+Q$+Chr$(34)
End Function 

; Gives a last slash to any string that is missing one
Function lastslash$(s$)
	L = Len(s)
	If L
		If Mid$(s,L,1) = "\"
			Return s
		Else
			Return s+"\"
		End If
	Else
		Return s
	End If
End Function</textarea><br><br>EDIT: <br><br>- Added a recursive folder / file counter.<br>- Kept hard way of getting file count as well as easy way.<br>- See comments in code.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">;; This is an effort in using a "String Stack" to return an
;; arbitrary collection of string values...

;; Coded by VirtLands,  Oct 29,2013
;; Edited by Thundros,  Oct 29,2013

Global DirCount, FileCount

Global  A_max = 9999999
Dim A$(A_max)

;; .. Start with about 9999999 items.
Global ssmax = 9999999

;; .. An index pointer into sstack.
Global ssp=0

;; .. Create a string stack,
;;  [ index 0 shall be deliberately left as empty, "" ]
Dim sstack$(ssmax)

;;; Sstack(n)  can be automatically redimensioned later, using  "redim_sstack(n)"
;; "String stack" range type
Type ssr
	;; The purpose of this type is to store indexes
	Field i_start
	;; Into the sstack(..)
	Field i_end
;; If i_end=0, then that means no data was found
End Type

;; FC = file count
Local FC

Graphics 800,600,32,2

Global fType$ = ".x"

Global saveDir$ = CurrentDir$()    
Global x_path$ = saveDir$+"meshes\"

;; Create an "index range" type
Local R.ssr =Null

;Scan Directory for file(s) / folder(s)
ScanDir(x_path$)

If FileCount    Then TheTruth$ = "THE TRUTH HAS BEEN FOUND "+"["+"OMG! I CAN HAZ THE FILES! :D"+"]"
If FileCount=0  Then TheTruth$ = "THE TRUTH WILL SET YOU FREE"+"["+"Add files of type "+Q(Upper$(ftype$))+" to your directory!"+"]"

Print 
Print " 'X' files Lister "+"::"+" "+TheTruth$+" "+"::"
Print 

;; Retrieve information in an "index" format
R = List_X_Files_ssr(x_path, ftype$)

;; :::THE FOUND FILES ARE AS FOLLOWS:::
If R\i_end&lt;&gt;0

	For z= R\i_start To R\i_end
		Print "   "+sstack(z)
	Next

	; You can use the hard way "(R\i_end - R\i_start +1)"
	; or the easy way: "FileCount"
	Print:Print" The number of files found = "+FileCount

End If

;; Return to the saved directory
ChangeDir savedir

Print: Print" Ok. "
WaitKey():End

;; This function returns an SSR type, ("indexes" into the "string stack")
;; These indexes describe an arbitrary collection of string values.
;;
Function List_X_Files_ssr.ssr(path$, ext$)

	Local saveDir$, curDir$, file$
	Local ft, L, xCount, Le
	Local ssr.ssr

	ssr = New ssr

	;; Point to the next unused string stack index
	ssr\i_start = ssp  +1

	If ext$="" Then ext$ = ".x"
		
		Le = Len(ext)

		;; Just in case
		ext = Lower$(ext)

		;; Save your previous directory
		saveDir = CurrentDir$()

		;; Add a "\" to the end of "path"
		path = lastslash(path)

		If savedir &lt;&gt; path
			ChangeDir path
		End If 

		myDir = ReadDir(path)
		If myDir

			Repeat
			
				file = Lower$(NextFile$(myDir))
				L = Len(file)
				If L=0 Then Exit
	
				ft = FileType(file)
				If ft=1
					If Mid$(file, L-Le+1, Le) = ext
						;; Push an item onto the string stack
						pushSS(path+file)
						xCount = xCount +1
					End If
				End If
				
			Forever
	
			CloseDir myDir

		End If 

		If savedir &lt;&gt; path
		
			ChangeDir savedir

		End If

		;; Store the ending index into "i_end"
		If xCount
			ssr\i_end = ssp
		End If

	;;; If  ssr\i_end =0, then
	;;; that means that NO files
	;;; were found
	Return ssr

End Function 

;; Pushes an item onto the string stack.
Function pushSS$(s$)
	If ssp = ssmax
		; Redimension if necessary
		redim_sstack(ssmax+100)
	End If
	ssp = ssp +1
	sstack(ssp) = s
	Return s
End Function 

;; Pops an item from the string stack
Function popSS$()
	Local s$
   If ssp=0
		Return s
	End If
	s = sstack(ssp)
	ssp = ssp -1
	Return s  
End Function 

;; This function redimensions the "string stack" when it runs out of space....
;; You can redimension UP as well as redimension DOWN,
;; but most people redimension UP. ;)
;; parameter 'n' is the new amount of items. 
;;
Function redim_sstack(n)
	
	Local tempfile$ = "sstack.tmp"
	Local z,L
	L = lesser(ssmax,n)

	;; There is no use in running this code if ssmax =n
	If ssp&lt;&gt;n

		fp = WriteFile(tempfile)
		If fp
			For z = 0 To L
				WriteString(fp, sstack(z))
				;; Free up some string memory
				sstack(z) = ""
			Next
			CloseFile(fp)

			;; Re-dimension it to the new value "n"
			Dim sstack(n)
			ssmax = n

			fp = ReadFile(tempfile)
			For z = 0 To L
				;; Read the data back in
				sstack(z) = ReadString(fp)
			Next
			CloseFile(fp)

		Else
		
			Print" There was an error creating "+Q(tempfile) + " ... "
			Print" ___________________________________
			WaitKey():End
			
		End If
		
	End If

	Return n
	
End Function 

;;; Returns the lesser of a, b
Function Lesser(a,b)
	If a&lt;b 
		Return a
	Else
		Return b   
	End If
End Function 

;;; Returns the greater of a, b
Function greater(a,b)
	If a=lesser(a,b)
		Return b
	Else
		Return a 
	End If
End Function 

Function we()
	WaitKey():End 
End Function 

;; Returns a string in quotes
Function Q$(Q$) 
	Return Chr$(34)+Q$+Chr$(34)
End Function 

; Gives a last slash to any string that is missing one
Function lastslash$(s$)
	L = Len(s)
	If L
		If Mid$(s,L,1) = "\"
			Return s
		Else
			Return s+"\"
		End If
	Else
		Return s
	End If
End Function

Function ScanDir(folder$)

	myDir=ReadDir(folder$) 

	Repeat 
		file$=NextFile$(myDir) 

		If file$="" Then Exit 
	
		If FileType(folder$+"\"+file$) = 2 Then 
			If file$&lt;&gt;"." And file$&lt;&gt;".." Then
				ScanDir(folder$+"\"+file$)
				DirCount=DirCount+1
			EndIf
		Else 
			FileCount=FileCount+1
		End If 
	Forever 

	CloseDir myDir 

End Function</textarea><br><br>Thanks once again, Virtlands! :D<br><br>Mythros <br><br></td></tr></table><br>
<a name="1205558"></a>

<a name="1205559"></a>

<a name="1205560"></a>

<a name="1205561"></a>

<a name="1205563"></a>

<a name="1205564"></a>

<a name="1205681"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >virtlands</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Aha!  Well, we both overlooked some things.  <br><br> I forgot to tell you to delete the following remnant which is not needed <br> from the code [directly above].<br><br><b> Global  A_max = 9999999 </b><br><b> Dim A$(A_max) </b><br>--------------------------------------------------------------<br><br>Furthermore, the following is an essential part of the code,<br><br><b>;; .. Start with about 9999999 items. </b><br><b>Global ssmax = 9999999 </b><br><br>Except for some things, <br><br> (<b>a</b>) Why do you need nearly <u>10 million</u> file positions ?<br> <br> (<b>b</b>) The program already has an automatic re-dimension function<br> that detects when its array is nearly maxed out, to add about 100 more entries.<br> <br> (<b>c</b>) The <b>Dim sstack$(ssmax)</b> command consumes memory immediately,  <br>     at the rate of 4 bytes per string entry, <br>     which means that 10 million string allocations consumes<br>     a total of <b>40 megabytes</b>, even for empty strings.<br><br>     That's why I started off with a low value, such as 32. <br>     32 empty strings = 128 bytes.  <img src="http://pcpuzzle.com/forum/images/smiles/icon_wink.gif"><br><br>-------------------------------------------------------------------------<br><br> Here is another variation for you.<br> IN this case, the function returns 1 superstring.<br><br> <b>S = List_X_Files_s( x_path,  ".x" )  ;;</b><br><br> (String data is separated by a 'separator' character, such as <b>"*"</b> . )<br><br> A special routine deconstructs the string (S$) back into human readable data ::<br><br> <textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
 ;; Several filename entries are search for, and retrieved as 1 long string.
 ;; coded by VirtLands,  Oct 29,2013


 Local fc=0  ;; fc = file count 

 Global Sep$ = "*"  ;; You must choose a filename data separater (delimiter)
                               ;; Choose something that is not normally allowed in filenames,
                               ;; such as an 'asterisk'. 

 Graphics 800,600,32,2 

 Print 
 Print " 'X' files Lister  :: STRING THEORY "
 Print 

 saveDir = CurrentDir$()    

 x_path$ = "C:\users\x\desktop"  ;; Adjust this as you wish

 Local S$ = ""

 S = List_X_Files_s( x_path,  ".x" )  ;; retrieve information in 1 long string

 ChangeDir savedir          ;; return to the saved directory

 ;;;  Decipher that string to show its contents ::: 
 ;;;
 ;;;  To save memory, I decided it's best to store the "homepath" in the first entry only. 
 ;;;  Each further entry consists only of "file" names"
 ;;;  The data can be reconstructed as shown: 

 i = Instr(S,Sep) : j=1
 parpath$ = Mid$(S,j, i-j)       ;; The first entry is the 'parent path'

 Repeat 
              j = i+1
              i = Instr(S,Sep,j)
              If i
                        Print "  "+parpath+Mid$(S,j,i-j)   ;; Now list all the files.
                        fc = fc +1                                    ;;  increments the file count
              Else 
                        Exit
              End If 
 Forever 

 Print:Print"  There were "+fc+" files found. "
 Print 
 Print" Ok. "
 WaitKey():End 


;;  This function returns 1 long string, which is a 
;;  concatenation of string data (separated by delimiters).
;;
Function List_X_Files_s$(path$, ext$)

        Local saveDir$, curDir$, file$
        Local ft, L, xCount, Le

        Local S$   ;;  return the file data in 1 long string
                        ;;  .. each data portion will be separated by a delimiter ...

        If ext$="" Then ext$ = ".x"
        Le = Len(ext)
        
        ext = Lower$(ext) ;; Just in case

        saveDir = CurrentDir$()   ;; save your previous directory
        path = lastslash(path)     ;; add a "\" to the end of "path"

        S = path+Sep   ;; The very first entry in 'S' shall be the 'parent directory'
                                 ;; ( Sep is a separator ).
                                 ;; All following data shall be 'sub' directories only

        If savedir &lt;&gt; path
                   ChangeDir path
        End If 

        myDir = ReadDir(path)
        If myDir

        Repeat 
                      file = Lower$(NextFile$(myDir))
                      L = Len(file)
                      If L=0 Then Exit

                      ft = FileType(file)
                      If ft=1
                                  If Mid$(file, L-Le+1, Le) = ext

                                             S = S +file +Sep        ;; 'S' grows with each file data.
                                             xCount = xCount +1
                                  End If                                  
                      End If   
        Forever

        CloseDir myDir
        End If 

        If savedir &lt;&gt; path
                   ChangeDir savedir
        End If 
  
        If xCount=0
                   S=""  ;; If no files were found, 
                            ;; indicate that by returning an empty string.
        End If 

        Return S
End Function 

Function Lesser(a,b)     ;;; returns the lesser of a,b
       If a&lt;b 
                 Return a
       Else 
                 Return b   
       End If
End Function 

Function we()
         WaitKey():End 
End Function 

;; returns a string in quotes
Function Q$(Q$) 
             Return Chr$(34)+Q$+Chr$(34)
End Function 

Function lastslash$(s$)    ; Gives a last slash to any string that is missing one
       L = Len(s)
       If L
               If Mid$(s,L,1) = "\" 
                       Return s
               Else  
                       Return s+"\"
               End If
       Else 
               Return s
       End If 
End Function </textarea><br><br> This would perhaps solve your wish that a "string" be returned.  <br>--------<br><br>Update:  I found your recursive <b>"ScanDir( )"</b> addition.<br>It's an interesting strategy.<br>Will study it further to see how it performs. <br><br></td></tr></table><br>
<a name="1205565"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks alot, Virtlands! Kudos to you, and another hug! :P<br><br>I have once again made the above code by Virtlands, easier to read.<br><br>ReturnSuperString.bb: <br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">;; Several filename entries are search for, and retrieved as 1 long string.

;; Coded by VirtLands, Oct 29,2013
;; Edited by VirtLands / Thundros, Oct 29,2013

;; fc = file count
Local fc=0

;; You must choose a filename data separator (delimiter)
;; Choose something that is not normally allowed in filenames,
;; such as an 'asterisk'.

Global Sep$ = "*"

Graphics 800,600,32,2

Print
Print " 'X' files Lister  :: STRING THEORY "
Print

;; Set Current Save Directory
Global saveDir$ = CurrentDir$()

;; Adjust this as you wish
Global x_path$ = saveDir$+"meshes\"

Local S$ = ""

;; Retrieve information in 1 long string
S = List_X_Files_s(x_path$, ".x")

;; Return to the saved directory
ChangeDir saveDir$

 ;;; Decipher that string to show its contents :::
 ;;;
 ;;; To save memory, I decided it's best to store the "homepath"
 ;;; in the first entry only.
 ;;; Each further entry consists only of "file" names"

 ;;; The data can be reconstructed as shown:
 i = Instr(S,Sep) : j=1

;; The first entry is the 'parent path'
parpath$ = Mid$(S,j, i-j)

Repeat
	j = i+1
	i = Instr(S,Sep,j)
	If i
		Print "  "+parpath+Mid$(S,j,i-j)   ;; Now list all the files.
		;; Increments the file count
		fc = fc +1
	Else
		Exit
	End If
Forever

Print:Print"  There were "+fc+" files found. "
Print
Print" Ok. "

WaitKey():End

;;  This function returns 1 long string, which is a
;;  concatenation of string data (separated by delimiters).
;;
Function List_X_Files_s$(path$, ext$)

	Local saveDir$, curDir$, file$
	Local ft, L, xCount, Le

	;; Return the file data in 1 long string
	Local S$

	;; Each data portion will be separated by a delimiter...
	If ext$="" Then ext$ = ".x"
	Le = Len(ext)

	;; Just in case
	ext = Lower$(ext)

	;; Save your previous directory
	saveDir = CurrentDir$()

	;; Add a "\" to the end of "path"
	path = lastslash(path)

	;; The very first entry in 'S' shall be the 'parent directory'
	S = path+Sep

	;; Sep is a separator.
    ;; All following data shall be 'sub' directories only
	If savedir &lt;&gt; path
		ChangeDir path
	End If

	myDir = ReadDir(path)
	If myDir
		Repeat
			file = Lower$(NextFile$(myDir))
			L = Len(file)
			If L=0 Then Exit

 			ft = FileType(file)
			If ft=1
				If Mid$(file, L-Le+1, Le) = ext

					;; 'S' grows with each file data.
					S = S +file +Sep
					xCount = xCount +1
				End If
			End If
        Forever
		CloseDir myDir
	End If

	If savedir &lt;&gt; path
		ChangeDir savedir
	End If

	If xCount=0

		;; If no files were found,
		;; indicate that by returning an empty string.
		S=""

	End If

	Return S

End Function

;;; Returns the lesser of a, b
Function Lesser(a,b)
	If a&lt;b
    	Return a
	Else
		Return b
	End If
End Function

Function we()
	WaitKey():End
End Function

;; Returns a string in quotes
Function Q$(Q$)
	Return Chr$(34)+Q$+Chr$(34)
End Function

; Gives a last slash to any string that is missing one
Function lastslash$(s$)
	L = Len(s)
	If L
		If Mid$(s,L,1) = "\"
			Return s
		Else
			Return s+"\"
		End If
	Else
		Return s
	End If
End Function</textarea><br><br>Thanks again!<br><br>Mythros <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
