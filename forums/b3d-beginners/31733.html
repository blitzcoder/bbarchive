<!DOCTYPE html><html lang="en" ><head ><title >Cylinder style 3d list</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Cylinder style 3d list</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Cylinder style 3d list</a><br><br>
<a name="338962"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >REDi</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi all<br><br>Could some kind soul please take a look at this piece of code to see where i'm going wrong, Basically its a menu system with each item displayed on a seperate 3d sprite, all of the sprites rotate around a center pivot to give a cylinder effect.<br>The problem is my CurveAngle() function, I just can't get it to handle the jump from positive to negative angles.<br><pre class=code>
; Use up and down keys to scroll the list

Graphics3D 800,600,32
Const MaxLines = 40
Global SelectedTrackIndex

; create camera
Global Camera = CreateCamera()

; create list wheel
Dim Item(MaxLines)
Dim ItemTex(MaxLines)
Global Wheel = CreatePivot()
PositionEntity Wheel,0,0,4
For n = 1 To MaxLines
	Item(n) = CreateSprite(Wheel)
	ScaleSprite Item(n),2,0.1
	TranslateEntity Item(n),0,0,-1.5,True
	EntityAutoFade Item(n),3.0,3.8	
	EntityBlend Item(n),3
	ItemTex(n) = CreateTexture(512,10)
	TurnEntity Wheel,(360/MaxLines),0,0
Next
UpdateWheelTextures()

Global BackPlate = CreateSprite()
ScaleSprite BackPlate,2,0.1
PositionEntity BackPlate,0,0,3
EntityColor BackPlate,0,0,50


; main loop
While Not KeyDown(1)

	; Scroll up
	If KeyHit(200)
		SelectedTrackIndex = SelectedTrackIndex - 1
		If SelectedTrackIndex &lt; 0 Then SelectedTrackIndex = MaxLines
	EndIf
	
	; Scroll down
	If KeyHit(208)
		SelectedTrackIndex = SelectedTrackIndex + 1
		If SelectedTrackIndex &gt; MaxLines Then SelectedTrackIndex = 0
	EndIf

	UpdateListWheel()

	UpdateWorld()
	RenderWorld()
	Flip

Wend
End



Function UpdateListWheel()

	current# = EntityPitch(Wheel)
	destin#	= (360 / MaxLines) * SelectedTrackIndex

	;dist# = destin# - current#
	;speed# = dist# / 5
	;TurnEntity Wheel,speed#,0,0

	RotateEntity Wheel,CurveAngle(current#,destin#,10),EntityYaw(wheel),EntityRoll(wheel)

End Function

Function UpdateWheelTextures()

	x = TextureWidth(ItemTex(1))/2
	y = TextureHeight(ItemTex(1))/2
	For n=1 To MaxLines
		SetBuffer TextureBuffer(ItemTex(n))
		ClsColor 0,0,0 : Cls
		Color 255,255,255
		Text x,y,"TEST LINE "+n,1,1
		EntityTexture Item(n),ItemTex(n)
	Next
	
End Function

Function CurveAngle#(current#,desired#,smooth#)

	Return current + (desired-current)/smooth

End Function
</pre><br>Thanx for your help! <br><br></td></tr></table><br>
<a name="339046"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's a hastily contrived version which seems to work.<br><br>CurveAngle has been fixed. <br><br>And Roll is used instead of Pitch.<br>Pitch has only a 180 degree range, from -90 to +90. Roll has 360 degree range.<br><br><pre class=code>
; Use up and down keys to scroll the list

Graphics3D 800,600,32
Const MaxLines = 40
Global SelectedTrackIndex

Global current#, destin#  ; not necessary, just for debugging...

; create camera
Global Camera = CreateCamera()

; create list wheel
Dim Item(MaxLines)
Dim ItemTex(MaxLines)
Global Wheel = CreatePivot()
PositionEntity Wheel,0,0,4

TurnEntity wheel, 0, 90, 0 ; point z-axis along original x-axis

For n = 1 To MaxLines
	Item(n) = CreateSprite(Wheel)
	ScaleSprite Item(n),2,0.1
	TranslateEntity Item(n),0,0,-1.5,True
	EntityAutoFade Item(n),3.0,3.8	
	EntityBlend Item(n),3
	ItemTex(n) = CreateTexture(512,10)
	TurnEntity Wheel,0,0,(360/MaxLines)
Next
UpdateWheelTextures()

Global BackPlate = CreateSprite()
ScaleSprite BackPlate,2,0.1
PositionEntity BackPlate,0,0,3
EntityColor BackPlate,0,0,50


; main loop
While Not KeyDown(1)

	; Scroll up
	If KeyHit(200)
		SelectedTrackIndex = SelectedTrackIndex - 1
		If SelectedTrackIndex &lt; 0 Then SelectedTrackIndex = MaxLines
	EndIf
	
	; Scroll down
	If KeyHit(208)
		SelectedTrackIndex = SelectedTrackIndex + 1
		If SelectedTrackIndex &gt; MaxLines Then SelectedTrackIndex = 0
	EndIf

	UpdateListWheel()


	RenderWorld()
	Text 10,10, current
	Text 10,30, destin
	Flip

Wend
End



Function UpdateListWheel()  ; note use of Roll rather than Pitch

	current# = EntityRoll(Wheel)
	destin#	= (360 / MaxLines) * SelectedTrackIndex

; Orient in two steps. This gets rid of confusing interactions 
; among Pitch, Yaw and Roll.

	RotateEntity wheel, 0, 90, 0
	TurnEntity Wheel, 0, 0, CurveAngle(current ,destin ,10)

End Function

Function UpdateWheelTextures()

	x = TextureWidth(ItemTex(1))/2
	y = TextureHeight(ItemTex(1))/2
	For n=1 To MaxLines
		SetBuffer TextureBuffer(ItemTex(n))
		ClsColor 0,0,0 : Cls
		Color 255,255,255
		Text x,y,"TEST LINE "+n,1,1
		EntityTexture Item(n),ItemTex(n)
	Next
	SetBuffer BackBuffer()
	
End Function

Function CurveAngle#(current#,desired#,smooth#)
Local delta#   ;  small version of angle, e.g. +350 becomes -10.

	delta = desired-current
	If Abs(delta) &gt; 180 Then delta = delta - 360 * Sgn(delta)
	Return current + delta/smooth

End Function
</pre><br>This could be neatened up with some effort. But it seems to work. <br><br></td></tr></table><br>
<a name="339060"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rob Farley</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> For some reason the sprite didn't work squidged up like you had it... however...<br><br>Added a bit to rotate the sprite so it's at the right angle on the cylinder... Just replace the initial loopy bit with this.<br><br><pre class=code>For n = 1 To MaxLines

	Item(n) = CreateSprite(Wheel)
	ScaleSprite Item(n),2,2
	SpriteViewMode item(n),2
	RotateEntity item(n),(360/maxlines)*(n-1),270,0
	EntityFX item(n),16
	TranslateEntity Item(n),0,0,-1.5,True
	;EntityAutoFade Item(n),3.0,3.8	
	EntityBlend Item(n),3
	ItemTex(n) = CreateTexture(256,256)
	TurnEntity Wheel,0,0,(360/MaxLines)

Next</pre> <br><br></td></tr></table><br>
<a name="339237"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DJWoodgate</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Another possibility.  Does not use curveangle at all... (to be honest I could not be bothered to try and figure out how to change it all, so consider this the lazy way).<br><br><pre class=code>
Function UpdateListWheel()
	
	destin#	= (360 / MaxLines) * (SelectedTrackIndex-1)
	
	vecy#=-Sin(destin) : vecz#=Cos(destin)
		
	AlignToVector wheel,0,vecy,vecz,3,0.1

End Function

</pre><br><br>A couple of other changes are needed to limit the values correctly...<br><br><pre class=code>
Global SelectedTrackIndex=1
;....
		If SelectedTrackIndex &lt; 1 Then SelectedTrackIndex = MaxLines
;....
		If SelectedTrackIndex &gt; MaxLines Then SelectedTrackIndex = 1
</pre><br><br>Well in fact three changes, but who's counting. ;) <br><br></td></tr></table><br>
<a name="339291"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >REDi</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for all your help guys! :)<br><br>I used Davids code in the end, works a treat! Cheers mate :D <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
