<!DOCTYPE html><html lang="en" ><head ><title >entity order problems</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >entity order problems</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >entity order problems</a><br><br>
<a name="979434"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Aussie</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Gday all.<br>     I am having a few problems with a fire effect disipearing beneath a water plane when it gets to a certain point on the screen. I have tryed useing entity order but it causes a bit of a slowdown. I was wandering if anyone has had this problem or if anyone can help.<br><br>Cheers. :)<br><br>Edit: It only seem to happen when i give the water plane an alpha level less than 1???<br><br>here is a screen shot where the fire renders above the water plane.<br><img src="http://i977.photobucket.com/albums/ae251/Aussie_011/screen%20shots/screen1.jpg"><br><br>&amp; here is a screen shot where the fire dissapears behind the water plane.<br><img src="http://i977.photobucket.com/albums/ae251/Aussie_011/screen%20shots/screen2.jpg"><br><br>here is the code if anyone wants a look<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800,600,0,2

SetBuffer BackBuffer()


Global pl.player
Global bl.ball
Global ex.exball
Global FrameTime
Global Period



Global 	ballimage 	= LoadImage("images\can_ball.jpg")
	  					ScaleImage ballimage,.5,.5 
Global 	expballimage 	= LoadImage("images\can_ball_exp.jpg")
						ScaleImage expballimage,.5,.5 
Global  flameimage 	= LoadImage("images\can_fire.jpg")
						ScaleImage flameimage,.5,.5
Global  flame		= LoadSprite("images\fire1.png",2) ;;;;;Flame sprite loaded;;;;;;;
						
						HideEntity flame
Global  smoke		= LoadSprite("images\flare.png",2)
						EntityOrder smoke,-1
						HideEntity smoke
Global  expsprite 	= LoadSprite("images\explosion.png",2)
						HideEntity expsprite
						
Global plwep_coll 	= 1
Global ter_coll		= 2
Global pla_coll		= 3
Global enm_coll		= 4
Global enwep_coll	= 5

						
Collisions plwep_coll,enm_coll,2,1
Collisions plwep_coll,ter_coll,2,1

Collisions enwep_coll,pla_coll,2,1
Collisions enwep_coll,ter_coll,2,1

Collisions enm_coll,ter_coll,2,1
Collisions enm_coll,pla_coll,2,2

Collisions pla_coll,ter_coll,2,1
Collisions pla_coll,enm_coll,2,2


light =	CreateLight()
		RotateEntity light,90,0,0

	
u#	=	10	
v#	=	10

water = LoadTexture("Textures\water2.jpg")
		ScaleTexture water,u,v

plane =	CreatePlane()
		EntityTexture plane,water,0,0
		EntityAlpha plane,.5


level =	LoadAnimMesh("models\island.x")
		EntityOrder level,0
		PositionEntity level,-250,0,-250
		EntityType level,2,True	

	

	
cube2 = CreateCube()
		PositionEntity cube2,0,-1,150
		ScaleEntity cube2,5,4,10
		EntityColor cube2,255,0,0
		EntityType cube2,4
	
cam_piv 	= 	CreatePivot()
camera	=	CreateCamera(cam_piv)
			RotateEntity camera,90,0,0


SeedRnd MilliSecs()

create_player()	
;;;;;;;;;;Main Loop;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
While Not KeyDown (1)
LimitFrameRate(60)
Cls

	cam_x# = (EntityX(pl\p_piv,True) + EntityX(cube2,True)) /2
	cam_z# = (EntityZ(pl\p_piv,True) + EntityZ(cube2,True)) /2
	cam_y# = EntityDistance(pl\p_piv,cube2)*1.5
	
	update_player()

	If cam_y &lt; 200 cam_y = 200
	If cam_y &gt; 300 cam_y = 300
	
	PositionEntity cam_piv,cam_x,0,cam_z
	PositionEntity camera,0,cam_y,0

ScaleTexture water,50+(Cos(u)*.5),50+(Sin(v)*.5)
	u = (u + Rnd(1,5)) Mod 360.0
	v = (v + Rnd(1,5)) Mod 360.0
	
up# = up-Rnd(.001,.003)
vp# = vp+Rnd(.001,.003)

PositionTexture water,up#,vp#

update_ball()
update_exball()
update_wep_fire()
update_wepsmoke()
update_explosion()

UpdateWorld
RenderWorld
;;;;;;;;;;2D stuff;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
Color 255,255,255
Text 0,0,		"cam_x: "	+cam_x
Text 0,15,		"cam_y: "	+cam_y
Text 0,30,		"cam_z: "	+cam_z
Text 713,90,	"Ammo: "	+pl\p_ammo
Text 713,105,	"Health: "	+pl\p_health
Text 150,15,	"Weapon: " 	+pl\p_ammosel
Color 255,0,0
Rect 713,19,66,66,False
DrawBlock pl\p_wep_sprite,714,20 
Flip
Wend
;;;;;;;;;;;Loop End;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
End
;;;;;;;;;;;;;;;;;;;;;;;;creates player;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


Type player
Field p_piv,p_mesh,p_vel#,p_rot#,p_pitch#,p_ammosel,p_ammo1,p_ammo2,p_ammo3#,p_ammo,p_health
Field p_wep_sprite
End Type


Function create_player()
	pl.player = New player
		pl\p_piv 	= CreatePivot()
							PositionEntity pl\p_piv,0,-.5,0
							EntityType pl\p_piv,3
		pl\p_mesh 		= CreateCube(pl\p_piv)
							ScaleEntity pl\p_mesh,5,4,10
		pl\p_vel			= 0
		pl\p_rot 			= 0
		pl\p_pitch 		= pl\p_vel*-10
		pl\p_ammo1 		= 5000
		pl\p_ammo2		= 5000
		pl\p_ammo3		= 5000
		pl\p_ammosel 		= 1
		pl\p_wep_sprite	= ballimage
		pl\p_health		= 100
		
End Function

Function update_player()
	For pl.player = Each player
		pl\p_vel 		= pl\p_vel - .02
		pl\p_pitch 	= pl\p_vel*-10
						If KeyDown(200) pl\p_vel 			= pl\p_vel + .1
						If pl\p_vel &gt; 1.5 Then pl\p_vel 		= 1.5
						If pl\p_vel &lt; 0 pl\p_vel			= 0
						If KeyDown(205) pl\p_rot 			= pl\p_rot - 2
						If KeyDown(203) pl\p_rot 			= pl\p_rot + 2
		MoveEntity 		pl\p_piv,0,0,pl\p_vel
		RotateEntity 	pl\p_piv,0,pl\p_rot,0
		RotateEntity 	pl\p_mesh,pl\p_pitch,0,0
		
						If pl\p_ammosel &gt; 3 pl\p_ammosel = 1
						If KeyHit(57) pl\p_ammosel = pl\p_ammosel + 1
						
			Select pl\p_ammosel


				Case 1 pl\p_ammo = pl\p_ammo1
						pl\p_wep_sprite = ballimage
						If pl\p_ammo1 &lt;= 0 Then If KeyHit(51) pl\p_ammosel = 1
						If pl\p_ammo1 &lt;= 0 Then If KeyHit(52) pl\p_ammosel = 1
						If pl\p_ammo1 &gt;  0 Then
							If KeyHit(51) pl\p_ammo1 = pl\p_ammo1 -1: create_ball(2.5,90,pl\p_piv,-8,1): create_wepsmoke(pl\p_piv,-7,EntityYaw(pl\p_piv)+90)
							If KeyHit(52) pl\p_ammo1 = pl\p_ammo1 -1: create_ball(2.5,-90,pl\p_piv,8,1): create_wepsmoke(pl\p_piv,7,EntityYaw(pl\p_piv)-90)
						End If
						FlushKeys
						

				
				Case 2 pl\p_ammo = pl\p_ammo2
						pl\p_wep_sprite = expballimage
						If pl\p_ammo2 &lt;= 0 Then If KeyHit(51) pl\p_ammosel = 1
						If pl\p_ammo2 &lt;= 0 Then If KeyHit(52) pl\p_ammosel = 1
						If pl\p_ammo2 &gt;  0 Then
							If KeyHit(51) pl\p_ammo2 = pl\p_ammo2 -1: create_exball(2.5,90,pl\p_piv,-8,1): create_wepsmoke(pl\p_piv,-7,EntityYaw(pl\p_piv)+90)
							If KeyHit(52) pl\p_ammo2 = pl\p_ammo2 -1: create_exball(2.5,-90,pl\p_piv,8,1): create_wepsmoke(pl\p_piv,7,EntityYaw(pl\p_piv)-90)
						End If
						FlushKeys

						
				Case 3 pl\p_ammo = pl\p_ammo3
						pl\p_wep_sprite = flameimage	
						If pl\p_ammo3 	&lt;= 0 Then If KeyDown(51) pl\p_ammosel = 1
						If pl\p_ammo3 	&lt;= 0 Then If KeyDown(52) pl\p_ammosel = 1
						If pl\p_ammo3 	&gt; 0 Then
							If KeyDown(51) pl\p_ammo3 = pl\p_ammo3 -.2: create_wep_fire(pl\p_piv,1,-8,Rnd(85,95),EntityYaw(pl\p_piv)+90)
							If KeyDown(52) pl\p_ammo3 = pl\p_ammo3 -.2: create_wep_fire(pl\p_piv,1,8,Rnd(-85,-95),EntityYaw(pl\p_piv)-90)
						End If
						FlushKeys
					
				
			End Select
			
			
		Return
	Next
End Function
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;creates cannon ball;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Type ball
Field b_mesh,b_damage,b_rot,b_vel#
End Type

Function create_ball(vel#,rot,parent,pos#,coll)
	bl.ball = New ball
		bl\b_mesh 	= CreateSphere(20,parent)
			PositionEntity   bl\b_mesh,pos,2,0
			RotateEntity	 bl\b_mesh,0,rot,0
			EntityColor 	 bl\b_mesh,0,0,0
			EntityParent 	 bl\b_mesh,0
			EntityType		 bl\b_mesh,coll
			bl\b_vel	 	 = vel
			bl\b_damage	 = 5
End Function

Function update_ball()
	For bl.ball = Each ball
		MoveEntity bl\b_mesh,0,0,bl\b_vel
		If EntityCollided (bl\b_mesh,2) Then 
			FreeEntity bl\b_mesh
			Delete bl.ball
		Else If EntityCollided (bl\b_mesh,4) Then 
			FreeEntity bl\b_mesh
			Delete bl.ball
		End If

	Next
End Function
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;creates cannon smoke;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Type wepsmoke
Field sm_sprite,sm_alpha#,sm_scale#,sm_rot#
End Type

Function create_wepsmoke(parent,pos#,rot#)
	sm.wepsmoke = New wepsmoke
		sm\sm_sprite = CopyEntity(smoke,parent)
			PositionEntity	 sm\sm_sprite,pos,0,0
			SpriteViewMode	 sm\sm_sprite,1
			EntityBlend		 sm\sm_sprite,3
			RotateSprite 	 sm\sm_sprite,rot
			
			EntityFX 		 sm\sm_sprite,1
			sm\sm_alpha 	 = 1
			sm\sm_scale 	 = 2
			sm\sm_rot		 = rot
End Function

Function update_wepsmoke()
	For sm.wepsmoke = Each wepsmoke
			sm\sm_alpha 	= sm\sm_alpha - .1
			sm\sm_scale 	= sm\sm_scale + .5
			
			EntityAlpha		sm\sm_sprite,sm\sm_alpha
			ScaleSprite 	sm\sm_sprite,sm\sm_scale,sm\sm_scale
			If sm\sm_alpha &lt; 0 Then
				FreeEntity sm\sm_sprite
				Delete sm.wepsmoke
		End If
	Next
End Function
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;Creates flame thrower;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Type fire
Field f_sprite,f_damage,f_vel#,f_alpha#,f_scale#,f_piv
End Type

Function create_wep_fire(parent,vel#,pos#,rot#,rot2#)
For fl = 0 To 1
	f.fire = New fire
		f\f_sprite = CopyEntity(flame,parent)
			PositionEntity f\f_sprite,pos,2,0
			
			RotateEntity 	 f\f_sprite,0,rot,0
			SpriteViewMode	 f\f_sprite,1
			RotateSprite	 f\f_sprite,rot2
			EntityBlend 	 f\f_sprite,3
			
			EntityParent 	 f\f_sprite,0
			f\f_damage 	 = 1
			f\f_vel 	 = vel
			f\f_scale 	 = 1
			f\f_alpha 	 = Rnd(1,1.5)
	Next
End Function

Function update_wep_fire()
	For f.fire = Each fire
		MoveEntity f\f_sprite,0,0,f\f_vel
			f\f_alpha 		= f\f_alpha - .03
			f\f_scale 		= f\f_scale + .1
			EntityAlpha 	f\f_sprite,f\f_alpha
			ScaleSprite 	f\f_sprite,f\f_scale,f\f_scale
			If f\f_alpha 	&lt; 0 Then
				FreeEntity f\f_sprite
				Delete f.fire
		End If
	Next
End Function
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;creates explosion;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Type explosion
Field ex_sprite,ex_alpha#,ex_scale#,ex_vel#
End Type

Function create_explosion(parent)
For b = 0 To 10
	e.explosion = New explosion
		e\ex_sprite = CopyEntity(expsprite,parent)
			SpriteViewMode e\ex_sprite,1
			EntityBlend e\ex_sprite,3
			RotateSprite e\ex_sprite,Rnd(0,360)
			RotateEntity e\ex_sprite,0,Rnd(0,360),0
			EntityParent e\ex_sprite,0
		e\ex_vel = Rnd(.1,.2)
		e\ex_alpha = 1
		e\ex_scale = .5
	Next
End Function

Function update_explosion()
	For e.explosion = Each explosion
		MoveEntity e\ex_sprite,e\ex_vel,0,0
			EntityAlpha e\ex_sprite,e\ex_alpha
			EntityOrder e\ex_sprite,-1
			ScaleSprite e\ex_sprite,e\ex_scale,e\ex_scale
		e\ex_alpha = e\ex_alpha - .02
		e\ex_scale = e\ex_scale + .15
			If e\ex_alpha &lt; 0 Then
			FreeEntity e\ex_sprite
			Delete e.explosion
		End If
	Next
End Function
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;creates exploding cannon ball;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
Type exball
Field exb_mesh,exb_damage,exb_rot,exb_vel#
End Type

Function create_exball(vel#,rot,parent,pos#,coll)
	ex.exball = New exball
		ex\exb_mesh 	= CreateSphere(20,parent)
			PositionEntity   ex\exb_mesh,pos,2,0
			RotateEntity	 ex\exb_mesh,0,rot,0
			EntityColor 	 ex\exb_mesh,0,0,0
			EntityParent 	 ex\exb_mesh,0
			EntityType		 ex\exb_mesh,coll
			ex\exb_vel	 	 = vel
			ex\exb_damage	 = 10
End Function

Function update_exball()
	For ex.exball = Each exball
		MoveEntity ex\exb_mesh,0,0,ex\exb_vel
		If EntityCollided (ex\exb_mesh,2) Then 
			create_explosion(ex\exb_mesh)
			FreeEntity ex\exb_mesh
			Delete ex.exball
		Else If EntityCollided (ex\exb_mesh,4) Then 
			create_explosion(ex\exb_mesh)
			FreeEntity ex\exb_mesh
			Delete ex.exball
		End If
	Next
End Function
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;limits fps;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Function LimitFrameRate(FPS = 65)
	If FrameTime = 0 Then
		Period = 1000 / FPS
		FrameTime = MilliSecs()
	EndIf

	While (FrameTime + Period) &gt; MilliSecs()
			Delay 1
	Wend
	FrameTime = MilliSecs()
End Function
</textarea> <br><br></td></tr></table><br>
<a name="979436"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think this is impossible to avoid unless you use the slower entityorder command.  It is a problem with alpha sorting.  I think it is possible to solve with messing with the camera range but maybe thats just zorder issues... <br><br></td></tr></table><br>
<a name="979437"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Aussie</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cheers Nate. Ill have a play around with the camera range &amp; see what i can do, otherwize ill just go with entityorder. :) <br><br></td></tr></table><br>
<a name="979461"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Instead of using entityalpha on your water, use entityblend 3 ( additive ).  This works for me and others.  You may need to reduce the brightness of your water texture for this to look the way you originally wanted. <br><br></td></tr></table><br>
<a name="979466"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Aussie</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Stevie. Works perfectly. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
