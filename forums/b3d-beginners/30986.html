<!DOCTYPE html><html lang="en" ><head ><title >Circular movement or something</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Circular movement or something</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Circular movement or something</a><br><br>
<a name="330407"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Glorified_Monkey</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have been fooling around with some code to make a square appear as if it is rotating. My main problem is that I don't know how to make the box "slow down" when your seeing the flat side and speed up when your looking at the edge.<br><br><pre class=code>

Graphics 640,480,16,2
SetBuffer BackBuffer()

Type vertex
     Field x,y,rsp
End Type


Global ver1.vertex=New vertex,ver2.vertex=New vertex,ver3.vertex=New vertex,ver4.vertex=New vertex

While Not KeyHit(1)
Cls

DrawSquare()

Flip

Wend

End

Function DrawSquare(size=100)

If ver1\x=0 Then ver1\x=320-size/2:ver1\y=240-size/2
If ver2\x=0 Then ver2\y=240-size/2+size:ver2\x=320-size/2
If ver3\x=0 Then ver3\x=320-size/2+size:ver3\y=240-size/2
If ver4\x=0 Then ver4\x=320-size/2+size:ver4\y=240-size/2+size

If ver1\x&lt;=320-size/2 Then
     ver1\rsp=1
ElseIf ver1\x&gt;=320+size/2
     ver1\rsp=-1
EndIf
If ver2\x&lt;=320-size/2 Then
     ver2\rsp=1
ElseIf ver2\x&gt;=320+size/2
     ver2\rsp=-1
EndIf
If ver3\x&gt;=320+size/2 Then
     ver3\rsp=-1
ElseIf ver3\x&lt;=320-size/2
     ver3\rsp=1
EndIf
If ver4\x&gt;=320+size/2 Then
     ver4\rsp=-1
ElseIf ver4\x&lt;=320-size/2
     ver4\rsp=1
EndIf


ver1\x=ver1\x+ver1\rsp
ver2\x=ver2\x+ver2\rsp
ver3\x=ver3\x+ver3\rsp
ver4\x=ver4\x+ver4\rsp

Line ver1\x,ver1\y,ver2\x,ver2\y
Line ver2\x,ver2\y,ver4\x,ver4\y
Line ver4\x,ver4\y,ver3\x,ver3\y
Line ver3\x,ver3\y,ver1\x,ver1\y

If ver1\x&lt;320 Then Rect ver1\x,ver1\y,ver3\x-ver1\x,100,1 

End Function
</pre> <br>This code is clearly <b>NOT</b> optimizied.<br><br>the sides of the square appear to 'bounce' when they reach their limits. Can someone give me formula taht will help this or must I write one? <br><br></td></tr></table><br>
<a name="330527"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> In work so haven't run your code but it seems like a strange way to simulate a rotating square!? You've obviously got your reasons :) - maybe if you try using floats for x,y,res in your vertex type this will be smoother.<br><br>If you want to use the correct rotation formulas let me know.<br><br>Stevie <br><br></td></tr></table><br>
<a name="330531"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rob Farley</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>Graphics 640,480
SetBuffer BackBuffer()

r=0
size=50

Repeat
Cls

sc#=Sin(r)*(size/2)

Rect 320-sc,240-(size/2),sc*2,size
Rect 320+sc,240-(size/2),-sc*2,size,False

r=r+1

Flip
Until KeyHit(1)</pre>To spin it faster make r=r+more or r=r+less to make it slower, you'll need to make r a float if you're moving slower. Or just set r to an abosolute value for any particular angle. <br><br></td></tr></table><br>
<a name="330532"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Shambler</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>

Graphics3D 800,600

camera=CreateCamera()
PositionEntity camera,0,0,-2

square=CreateMesh()
squaresurf=CreateSurface(square)

AddVertex squaresurf,-0.5,-0.5,0
AddVertex squaresurf,-0.5,0.5,0
AddVertex squaresurf,0.5,0.5,0
AddVertex squaresurf,0.5,-0.5,0

AddTriangle squaresurf,0,1,2
AddTriangle squaresurf,2,3,0


EntityFX square,16
WireFrame True


While Not KeyHit (1)

TurnEntity square,0,2,0

RenderWorld()
Flip

Wend

</pre><br><br>If you don't want the diagonal line then texture the quad with a square texture or take out the wireframe true. <br><br></td></tr></table><br>
<a name="330533"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rob Farley</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Shambler... I think we're in 2D not 3D. <br><br></td></tr></table><br>
<a name="330534"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Shambler</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> What!? 2D!? what the hell is that? ;)<br><br>Just saw it after I posted -.- <br><br></td></tr></table><br>
<a name="330542"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rob Farley</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Or... if you want to be really posh...<br><br><pre class=code>Graphics 640,480

SetBuffer BackBuffer()

r#=0
size=100

Repeat
Cls
Line 320-(Sin(r)*(size/2)),240-(size/2)-(Cos(r)*(size/8)),320-(Sin(r)*(size/2)),240+(size/2)+(Cos(r)*(size/8))
Line 320-(Sin(r)*(size/2)),240+(size/2)+(Cos(r)*(size/8)),320-(Sin(r+180)*(size/2)),240+(size/2)+(Cos(r+180)*(size/8))
Line 320-(Sin(r+180)*(size/2)),240+(size/2)+(Cos(r+180)*(size/8)),320-(Sin(r+180)*(size/2)),240-(size/2)-(Cos(r+180)*(size/8))
Line 320-(Sin(r+180)*(size/2)),240-(size/2)-(Cos(r+180)*(size/8)),320-(Sin(r)*(size/2)),240-(size/2)-(Cos(r)*(size/8))

r=r+1

Flip

Until KeyHit(1)</pre> <br><br></td></tr></table><br>
<a name="330546"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rob Farley</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> And if you want to be really really flash...<br><br><pre class=code>Graphics 640,480

SetBuffer BackBuffer()

r#=0
size=200

Repeat
Cls

drawbox(320,240,100,r)
drawbox(320,240,100,r+90)
drawbox(320,240,200,r+45)
drawbox(120,240,200,-(r/2))
drawbox(520,240,200,-(r/2))

r=r+1

Flip

Until KeyHit(1)



Function drawbox(x,y,size,angle#)
Line x-(Sin(angle)*(size/2)),y-(size/2)-(Cos(angle)*(size/8)),x-(Sin(angle)*(size/2)),y+(size/2)+(Cos(angle)*(size/8))
Line x-(Sin(angle)*(size/2)),y+(size/2)+(Cos(angle)*(size/8)),x-(Sin(angle+180)*(size/2)),y+(size/2)+(Cos(angle+180)*(size/8))
Line x-(Sin(angle+180)*(size/2)),y+(size/2)+(Cos(angle+180)*(size/8)),x-(Sin(angle+180)*(size/2)),y-(size/2)-(Cos(angle+180)*(size/8))
Line x-(Sin(angle+180)*(size/2)),y-(size/2)-(Cos(angle+180)*(size/8)),x-(Sin(angle)*(size/2)),y-(size/2)-(Cos(angle)*(size/8))
End Function</pre> <br><br></td></tr></table><br>
<a name="330781"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >big10p</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi<br><br>There might be something in this that you can use:<br><br><pre class=code>
	Graphics3D 800,600,16

	SetBuffer BackBuffer()

	SeedRnd MilliSecs()
	
	Type grid_pix
		Field ent
		Field x#,y#,z#
		Field rot#
	End Type
			
	Global frame_count%
	Global fps%
	Global fps_timeout%
	Global frame_time%
	Global slowest_frame%
	Global frame_start%

	Global title_state% = 3
		
	Global cam = CreateCamera()
	PositionEntity cam,0,0,-14
	;TurnEntity cam,10,0,0
	
	Global light = CreateLight()

	Global spiv = CreatePivot()
	Global piv = CreatePivot()
	;RotateEntity piv,0,180,0
	;PositionEntity spiv,0,0,-100
	;EntityParent piv,spiv
	;PositionEntity spiv,0,100,0

	make_titles()	
	
	;RotateEntity piv,-20,0,0

If 1
	sprite = CreateSprite(cam)
	PositionEntity sprite,0,0,1.1
	ScaleSprite sprite,1.5,1
	SpriteViewMode sprite,1
	EntityAlpha sprite,.124
	EntityColor sprite,0,0,0
	EntityBlend sprite,1
	EntityFX sprite,1
	EntityOrder sprite,1
	Dither False
	CameraClsMode cam,0,1
EndIf


	Type part
		Field ent
		Field x#,y#,z#
	End Type

	Const TOTAL_STARS = 400	
	
	make_stars()


	WireFrame 0
	While Not KeyHit(1)
		frame_start = MilliSecs()

		If KeyHit(208) Then title_state = 0
		update_titles()
		update_stars()
		
		UpdateWorld
		If Not KeyDown(14) CopyRect 0,0,800,600,0,0,FrontBuffer(),BackBuffer()
		RenderWorld

		frame_time = MilliSecs() - frame_start	
		show_info()		

		Flip(1)
		
		;Delay 50
		;WaitKey()
	Wend
	
	kill_titles()
	
	End


Global explode_count
Const EXPLODE% = 400
;
; Update title animation
;
Function update_titles()

	Local y#
	
	Select title_state
		Case 0
			title_state = 1
			explode_count = EXPLODE
			
			For this.grid_pix = Each grid_pix
				this\x = Rnd(-1,1)
				this\y = Rnd(-1,1)
				this\z = Rnd(-1,1)
				this\rot = Rnd(-1,1)
			Next		
		Case 1
			For this.grid_pix = Each grid_pix
				TranslateEntity this\ent,this\x,this\y,this\z
				TurnEntity this\ent,this\rot,0,0
				TurnEntity this\ent,0,this\rot,0
				TurnEntity this\ent,0,0,this\rot
			Next
			
			explode_count = explode_count - 1

			If explode_count = 0
				title_state = 2
				explode_count = EXPLODE
			EndIf		
		Case 2
			For this.grid_pix = Each grid_pix
				TranslateEntity this\ent,-this\x,-this\y,-this\z
				TurnEntity this\ent,0,0,-this\rot
				TurnEntity this\ent,0,-this\rot,0
				TurnEntity this\ent,-this\rot,0,0
			Next
				TurnEntity piv,0,-1,0
			
			explode_count = explode_count - 1
			If explode_count = 0 Then title_state = 3				
		Case 3
			y = Abs(EntityYaw(piv))/2.0
			
			TurnEntity piv,0,-(.3 + (Sin(y) * (y/20))),0
			
			If Rand(1,400)=100 Then title_state = 0
	End Select
	
End Function


;
; Create the game title grid of cubes
;
Function make_titles()

	Local title_rows% = 0
	Local title_cols% = 0
	Local grid_row$, datum$
	Local pix.grid_pix
	Local grid_step#
	Local tl_x#, tl_y#
	Local title_width# = 18.0
	Local title_height#
	Local title_ent
	Local pix_size#
	
	title_ent = CreateCube()
	EntityShininess title_ent,1
		
	; Find width and height of title grid from DATA
	Restore TITLE_DATA
	Read grid_row$
	title_cols = Len(grid_row$)
	Repeat
		title_rows = title_rows + 1
		Read grid_row$
	Until grid_row$ = "."
	
	title_height = title_rows * (title_width/title_cols)

	grid_step = title_width/(title_cols-1)
	tl_x = -title_width/2.0
	tl_y = title_height/2.0

	pix_size# = grid_step/2.0 - 0.04
	ScaleEntity piv,pix_size,pix_size,pix_size
	
	Restore TITLE_DATA
	 
	For r = 1 To title_rows
		Read grid_row$

		For c = 1 To title_cols
			datum$ = Mid$(grid_row$,c,1)

			If datum$ &lt;&gt; " "
				pix = New grid_pix
				pix\ent = CopyEntity(title_ent,piv)
				
				PositionEntity pix\ent,tl_x + ((c-1)*grid_step),tl_y - ((r-1)*grid_step),0,True

				Select datum$
					;Case " "
					;	EntityColor pix\ent,0,0,0
					Case "1"
						EntityColor pix\ent,150,50,0
						ScaleEntity pix\ent,1,1,4
					Case "2"
						EntityColor pix\ent,0,150,0
						ScaleEntity pix\ent,1,1,6
					Case "3"
						EntityColor pix\ent,150,150,0
						ScaleEntity pix\ent,1,1,2
					Case "4"
						EntityColor pix\ent,150,0,0
						ScaleEntity pix\ent,1,1,4
					Default
						rgb_step# = 255.0/19.0
						val = Asc(datum$) - Asc("a")
						;DebugLog val
						;DebugLog 255-val*rgb_step#
						EntityColor pix\ent,100,50,0;255,0,255 - val*rgb_step#
				End Select
			EndIf
		Next
	Next
	
	FreeEntity title_ent
	
End Function


;
; Free all mem used by title
;
Function kill_titles()

	For this.grid_pix = Each grid_pix
		FreeEntity this\ent
		Delete this
	Next

End Function


;
; Show debug info
;
Function show_info()
	
	frame_count = frame_count + 1

	If MilliSecs() &gt; fps_timeout Then
		fps_timeout = MilliSecs() + 1000 
		fps = frame_count 
		frame_count = 0 
	EndIf 
	
	If frame_time &gt; slowest_frame Then slowest_frame = frame_time
	
	Color 0,0,100
	Rect 0,0,200,80,1
	Color 255,255,255
	
	Text 10,10," Triangles: " + TrisRendered()
	Text 10,25," Millisecs: " + frame_time
	Text 10,40,"   slowest: " + slowest_frame
	Text 10,55,"       FPS: " + fps

End Function


Function make_stars()

	For n = 1 To TOTAL_STARS
		this.part = New part
		
		this\ent = CreateSprite()
		;ScaleSprite this\ent,5,5
		EntityColor this\ent,255,255,255
		EntityAutoFade this\ent,200,500
	
		init_star(this)
		update_stars()
	Next

End Function


Function kill_stars()

	For this.part = Each part
		FreeEntity this\ent
		Delete this
	Next

End Function


Function update_stars()

	For this.part = Each part

		If Not EntityInView(this\ent,cam)
			init_star(this)
		Else
			TranslateEntity this\ent,this\x,this\y,this\z
		EndIf
	Next

End Function


Function init_star(star.part)

	r#=Rnd(0.0,360.0)
	star\x = Cos(r)/2
	star\y = Sin(r)/2
	star\z = -Rnd(.01,.5)

	PositionEntity star\ent,star\x*50,star\y*50,500

End Function


.TITLE_DATA
Data " ttttttttttttttttttttttttttttttttttttttttt "
Data "t                                         t"
Data "t 111 111 111 111 111     111 1 1   1 111 t"
Data "t 1   1 1 1 1 1   1        1  1 11 11 1   t"
Data "t 111 111 111 1   11  111  1  1 1 1 1 11  t"
Data "t   1 1   1 1 1   1        1  1 1   1 1   t"
Data "t 111 1   1 1 111 111      1  1 1   1 111 t"
Data "t                 2222                    t"
Data "t 1 1  1 1   1  22222222  11  111 111 111 t"
Data "t 1 11 1 1   1 2244224422 1 1 1   1 1 1   t"
Data "t 1 1 11  1 1  2222222222 1 1 11  11  111 t"
Data "t 1 1  1  1 1    22  22   1 1 1   1 1   1 t"
Data "t 1 1  1   1    22 22 22  111 111 1 1 111 t"
Data "t              2        2                 t"
Data " ttttttttttttt      3     tttttttttttttttt "
Data "                   3                       "
Data "                    3                      "
Data "                   3                       "
Data "."
</pre><br><br>...then again, I've been to the pub and am a bit p@~#ed. Something i knocked up aged ago. <br><br></td></tr></table><br>
<a name="330895"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> @big10p  Cool demo mate - you should stick this in the code archives. <br><br></td></tr></table><br>
<a name="330926"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlackJumper</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> .... and I thought Rob was showing off !<br><br>Excellent demo. <br><br></td></tr></table><br>
<a name="331087"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >big10p</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oh, cheers guys! :)<br><br>If you really think it's worth it then I'll try and clean the code up a bit and put it in the archives. Oooh, don't you just hate revisting old code! :/ <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
