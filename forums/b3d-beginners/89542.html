<!DOCTYPE html><html lang="en" ><head ><title >third-person camera?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >third-person camera?</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >third-person camera?</a><br><br>
<a name="1016906"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cubed Inc.</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Does anyone know how to make a third-person camera? I'm trying to make a Lego game similair to Lego Island 2 becuase I think that it might be good to make a simple platform game before I make a more complex one. If someone can help, it would be greatly apprectiated.<br>please reply.later.<br><br>P.S Lego is in copyright of the Lego company, not me. <br><br></td></tr></table><br>
<a name="1016908"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GIB3D</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I made this myself and is one of the many things I made and stored away in a nice folder so I can pull it out later if I need it.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
InitGraphics()
FlushMouse

Global Light = CreateLight(2)
LightRange Light,6
PositionEntity Light,0,8,0


Global Camera_YPivot = CreatePivot()
Global Camera_XPivot = CreatePivot(Camera_YPivot)
Global Camera = CreateCamera(Camera_XPivot) : CameraFogMode Camera,1 : CameraFogRange Camera,100,400
	PositionEntity Camera,0,2,-5
	
Global Player = CreateCone():EntityTexture Player,GTexture(256)
RotateMesh Player,90,0,0
PositionEntity Player,-5,2,0

Global PlayerTurnPivot = CreateSphere(3)

Local Ground = CreatePlane()
EntityTexture Ground,GTexture(510,Rand(1,3))
EntityPickMode Ground,2
EntityType Ground,3

Local Sky = CreateSphere(3,Player)
EntityTexture Sky,GTexture(64,3)
EntityPickMode Sky,2
ScaleMesh Sky,200,200,200
FlipMesh Sky


Global LeftGun = CreateCone() : RotateMesh LeftGun,90,0,0 : ScaleMesh LeftGun,.25,.25,.5
Global RightGun = CopyMesh(LeftGun)
Global GunPointPivot = CreateSphere(3):EntityFX GunPointPivot,5:EntityColor GunPointPivot,255,0,0:EntityAlpha GunPointPivot,.5

Collisions 2,3,2,2

MouseXSpeed()
MouseYSpeed()
FlushMouse
While Not KeyDown(1)
	TranslateEntity Camera,0,(KeyDown(18)-KeyDown(16))*.5,MouseZSpeed() ; Zoom Camera
	
	;Guns
	CameraPick Camera,MouseX(),MouseY()
	PositionEntity GunPointPivot,PickedX(),PickedY(),PickedZ()
	
	TFormPoint -1,0,1,Player,0
	TranslateEntity LeftGun,((TFormedX())-EntityX(LeftGun,1))*.9,(TFormedY()-EntityY(LeftGun,1))*.9,(TFormedZ()-EntityZ(LeftGun,1))*.9
	TFormPoint 1,0,1,Player,0
	TranslateEntity RightGun,((TFormedX())-EntityX(RightGun,1))*.9,(TFormedY()-EntityY(RightGun,1))*.9,(TFormedZ()-EntityZ(RightGun,1))*.9
	
	If MouseDown(2)
		PointEntity LeftGun,GunPointPivot
		PointEntity RightGun,GunPointPivot
	Else
		RotateEntity LeftGun,0,EntityYaw(Player),0
		RotateEntity RightGun,0,EntityYaw(Player),0
	EndIf
	
	If MouseDown(1)
		CreateBullet(EntityX(LeftGun),EntityY(LeftGun),EntityZ(LeftGun),EntityPitch(LeftGun),EntityYaw(LeftGun),Rnd(.5,1),30)
		CreateBullet(EntityX(RightGun),EntityY(RightGun),EntityZ(RightGun),EntityPitch(RightGun),EntityYaw(RightGun),Rnd(.5,1),30)
	EndIf
	;Guns
	
	Local Sprint# = KeyDown(42)*2
	
	If Sprint &lt;&gt; 0
		TFormVector (KeyDown(32)-KeyDown(30))*.1*Sprint,0,(KeyDown(17)-KeyDown(31))*.1*Sprint,Camera_YPivot,0
	Else
		TFormVector (KeyDown(32)-KeyDown(30))*.1,0,(KeyDown(17)-KeyDown(31))*.1,Camera_YPivot,0
	EndIf
	
	TranslateEntity Player,TFormedX(),0,TFormedZ()
	
	If TFormedX() &lt;&gt; 0 Or TFormedZ() &lt;&gt; 0 ; Process turning
		PositionEntity PlayerTurnPivot,EntityX(Camera_YPivot,1)+TFormedX()*30,0,EntityZ(Camera_YPivot,1)+TFormedZ()*30
		TurnEntity Player,0,DeltaYaw(Player,PlayerTurnPivot)*.1,0
	EndIf
	
	FUNC_GTATurnCamera(Player,Camera_XPivot,Camera_YPivot)
	
	UpdateBullet()
	
	UpdateWorld
	RenderWorld
	
	Flip
	
Wend
End

Type Bullet
	Field Model
	Field Life,Speed#
End Type

Function CreateBullet(x#,y#,z#,pitch#,yaw#,speed#,life)
	Local a.Bullet = New Bullet
		a\Model = CreateSphere(4) : RotateMesh a\Model,90,0,0; : ScaleMesh a\Model,.25,.25,.25
		EntityColor a\Model,255,128,0
		EntityAlpha a\Model,Rnd(.1,1)
		EntityType a\Model,2
		
		RotateEntity a\Model,Rand(30,-30),Rand(30,-30),0
		
		Local Size# = Rnd(.5,1)
		ScaleMesh a\Model,Size,Size,Size
		
		PositionEntity a\Model,x,y,z
		RotateEntity a\Model,pitch,yaw,0
		
		a\Life = life
		a\Speed = speed
End Function

Function UpdateBullet()
	Local a.Bullet
	
	For a.Bullet = Each Bullet
		a\Life = a\Life - 1
		MoveEntity a\Model,0,0,a\Speed
		TurnEntity a\Model,0,0,10
		
		If a\Life =&lt; 0 Or CountCollisions(a\Model)
			FreeEntity a\Model
			Delete a
		EndIf
	Next
End Function

Function FUNC_GTATurnCamera(parent,xpivot,ypivot,turn_multiplier#=1)
	TranslateEntity ypivot,(EntityX(parent,1)-EntityX(ypivot,1))*.1,(EntityY(parent,1)-EntityY(ypivot,1))*.1,(EntityZ(parent,1)-EntityZ(ypivot,1))*.1
	
	TurnEntity ypivot,0,-MouseXSpeed()*turn_multiplier,0
	TurnEntity xpivot,MouseYSpeed()*turn_multiplier,0,0
	
	RotateEntity xpivot,EntityPitch(xpivot),0,0
	
	MoveMouse GraphicsWidth()/2,GraphicsHeight()/2
End Function

Function InitGraphics(w = 1024, h = 768,title$="Blitz3D Program",exit_message$="")
	Graphics3D w, h, 32, 2
	SetBuffer BackBuffer()
	SeedRnd MilliSecs()
	
	If exit_message &lt;&gt; ""
		AppTitle title,exit_message
			Else
				AppTitle title
	EndIf
End Function

Function GTexture(size=64,rgb_dominant=2) ; Green Checkered Texture
	Local T = CreateTexture(size,size,1+256)
	Local Red,Green,Blue
	
	Select rgb_dominant
		Case 2 ; Green
			Red = 38
			Green = 148
			Blue = 0
		Case 3 ; Blue
			Red = 0
			Green = 38
			Blue = 148
		Default ; Red
			Red = 148
			Green = 0
			Blue = 38
	End Select
	
	SetBuffer TextureBuffer(T)
		Color Red,Green,Blue
		Rect 0,0,size,size
		
		Color Red*1.4,Green*1.4,Blue*1.4
		Rect size/2,0,size/2,size/2
		Rect 0,size/2,size/2,size/2
	SetBuffer BackBuffer()
	ScaleTexture T,4,4
	PositionTexture T,.25,.25
	
	Return T
End Function

</textarea> <br><br></td></tr></table><br>
<a name="1016918"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'd say the most important thing for you is to play a game and study how THEY did it ('they' being professional game designers + programmers). <br>Blitz3D implementation will definitely <i>not</i> be hard, once you recognize all the features the camera-systems you like have (of course, implementation will only be easy if you know the Blitz3D command set!).<br><br>You can learn a lot from analyzing games (especial some moderately old ones, like N64 games). If you're accepting suggestions, try playing Banjo-Kazooie\Tooie or Castlevania 64. <br><br>They have different 3rd-person camera implementations (in my opinion, the Banjo camera is the best one; not only it follows the player in open environments, but it also has "rails" for when in tight corners or tunnels - something most people around here forget to use).<br>Don't make it just a "stand behind the character" camera. <br><br></td></tr></table><br>
<a name="1016919"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GIB3D</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> The one I posted is a Grand Theft Auto camera style. <br><br></td></tr></table><br>
<a name="1016930"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cubed Inc.</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> GIB3D<br>The code you gave me was great, but it was kinda complex and it fitted more for a shooting game, rather than a platforming/action adventure game. I have studied and watched videos of Lego Island 2 and I noticed, even how simple the camera was, it worked. You couldn't rotate the camera, but the characters movement did all of the rotating for it(for example, when the character walks right and left or at an angle, the camera rotates)if you see any videos of the game on youtube, you'll know what I mean. The code was great, but I just want to use a simple camera before I get into more complex 3rd person game mechanics(such as in gta). Thanks anyway. <br><br></td></tr></table><br>
<a name="1016931"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GIB3D</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Basically what I did for the camera was parent a Camera to a Pitch Pivot then parent that to a Yaw Pivot. How the Yaw pivot turns is your choice. <br><br></td></tr></table><br>
<a name="1016932"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have some code for this. The camera has a little bit of intelligence about it . Fairly simple. I will try and get it when PC is up and running again. <br><br></td></tr></table><br>
<a name="1016941"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Zethrax</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> The Castle demo in the 3d samples for Blitz3D has a good third person camera ( C:\Program Files\Blitz3D\samples\mak\castle ).<br><br>Regarding the 'Lego' bit - make sure that you don't violate any trademarks or other intellectual property rights with your game, or you could find yourself in serious legal trouble. <br><br></td></tr></table><br>
<a name="1016944"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cubed Inc.</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Bill Stanbrook<br>Don't worry, i'm not releasing the game or anything. Just for practice:)<br><br>GIB3D<br>Before I made this post, I tried to parent the camera with a pivot that would be contolled by the character, but it was acting weird and broken.<br><br>Ross C<br>Please explain the camera code when you get the chance. <br><br></td></tr></table><br>
<a name="1016977"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> What i do, is a slightly modifed version of the pivot style camera, parented to the entity. I firstly create a pivot, and parent it to the entity you want to follow. Then, i position it, where it would want my camera to rest behind the entity, when no movement is happening.<br><br>Then, each loop, i do the following:<br><br><pre class=code>
PointEntity camera,pivot
MoveEntity camera,0,0,EntityDistance(camera,pivot)/10 ; A higher dividing number will cause a slower and smoother camera
PointEntity camera,entity
</pre><br><br>camera being your camera<br>pivot being the pivot i described earlier<br>entity being the entity you want the camera to follow.<br><br>I have slightly modified it, for greater control, and so it doesn't get stuck, but try that and see if it does what you need. <br><br></td></tr></table><br>
<a name="1017150"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cubed Inc.</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ross C<br>The trouble that i'm having is that I can't make the camera follow the pivot and I can't make the pivot stick to the player. When I tried your code, the camera didn't follow the player, it just pointed and zoomed. <br><br></td></tr></table><br>
<a name="1017151"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Naughty Alien</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> ..here is one more basic cookie from my 'kitchen'..i hope it helps... :)<br><br><pre class=code>
Graphics3D 1280,1024,32,1
SetBuffer=BackBuffer()
Global FreeLookXS#, FreeLookZS#, FreeLookRotXS#, FreeLookRotYS#,Pitch#

Global campivot=CreateCube() ; create pivot for camera
Global camera=CreateCamera()  ; create camera (!!!)

; ---------------------- LETS GENERATE SOME JUNK AROUND THE WORLD ---------------

Global light=CreateLight()
Global player=CreateCube()    ; create simple player
Global Player_Free_Look=CreatePivot(player)
MoveEntity Player_Free_Look,0,4,0
Global plane=CreatePlane()   ; create simple floor
MoveEntity player,0,1,0
; Create texture of size 256x256
tex=CreateTexture(256,256)

; Set buffer - texture buffer
SetBuffer TextureBuffer(tex)

; Clear texture buffer with background white color
For i=1 To 10
Color Rnd(0,255),Rnd(0,255),Rnd(0,255)
Rect Rnd(0,256),Rnd(0,256),Rnd(0,256),Rnd(0,256)
Next

; Texture cube with texture
EntityTexture plane,tex
EntityTexture player,tex

; Set buffer - backbuffer
SetBuffer BackBuffer()

;camera look variables
; ------------------------------------------------------------------------------------------------

While Not KeyHit(1)
;object movement according to mouse pointer position

	FreeLook(player,Player_Free_Look, .1)
	CamFollow(camera,Player_Free_Look,.18,8,0,0,0)
	

RenderWorld

VWait:Flip False

Wend

End



Function CamFollow(cam,ent,cspeed#,dist#,hite#,xrot#,tilt#)

TFormPoint 0,hite#,-dist#,ent,0

cx#=(TFormedX()-EntityX(cam))*cspeed#
cy#=(TFormedY()-EntityY(cam))*cspeed#
cz#=(TFormedZ()-EntityZ(cam))*cspeed#

TranslateEntity cam,cx,cy,cz

RotateEntity cam,xrot#,EntityYaw(cam),tilt#
PointEntity cam,ent
End Function

Function FreeLook(Object_Horizontal,Object_Vertical, sp# = .1)
If sp# &gt; 0 Then
    Side_Direction=((KeyDown(32) Or KeyDown(205)) - (KeyDown(30) Or KeyDown(203)))
    Front_Direction=((KeyDown(17) Or KeyDown(200)) - (KeyDown(31) Or KeyDown(208)))
	FreeLookXS# = (FreeLookXS# + Side_Direction * (sp#/2)) * .75
	FreeLookZS# = (FreeLookZS# + Front_Direction * sp#) * .75
	MoveEntity Object_Horizontal, FreeLookXS#, 0, FreeLookZS#
EndIf

FreeLookRotXS# = ((MouseXSpeed() - FreeLookRotXS#) * .2 + FreeLookRotXS#) * .45
FreeLookRotYS# = ((MouseYSpeed() - FreeLookRotYS#) * .2 + FreeLookRotYS#) * .45
If EntityPitch(Object_Vertical) + FreeLookRotYS# &lt; -25 Pitch# = -25 ElseIf EntityPitch(Object_Vertical) + FreeLookRotYS# &gt; 45 Pitch# = 45 Else Pitch# = EntityPitch(Object_Vertical) + FreeLookRotYS#
yaw# = -FreeLookRotXS# + EntityYaw(Object_Horizontal)
RotateEntity Object_Horizontal, 0, yaw#, 0
RotateEntity Object_Vertical, Pitch#, 0, 0
MoveMouse GraphicsWidth() / 2, GraphicsHeight() / 2
End Function
</pre> <br><br></td></tr></table><br>
<a name="1017352"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cubed Inc.</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Your example codes are really good and usefull, but I think that I would understand them more if I knew what makes the camera follow the main player in it. Can someone give me a explaination? <br><br></td></tr></table><br>
<a name="1017372"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> All the camera positioning code is in the CamFollow() function.<br><br><pre class=code>TFormPoint 0,hite#,-dist#,ent,0</pre><a href="/b3ddocs/command.php?name=TFormPoint&amp;ref=3d_a-z" target="_blank">This command</a> calculates the XYZ coordinates of the 3D point you specify. <br><br>It's like a 'relative' point, because you specify from which entity you will be <b>moving away from</b>, and also specify the X, Y and Z vectors you are going to be moving away with.<br><br>Translating the TFormPoint usage above would be: I want to move away from entity "ent" (TFormPoint 0,hite#,-dist#,<b>ent</b>,0), by such value in the X axis (TFormPoint <b>0</b>,hite#,-dist#,ent,0), by the value "hite#" in the Y axis (TFormPoint 0,<b>hite#</b>,-dist#,ent,0) and by value "-dist#" on the Z axis (TFormPoint 0,hite#,<b>-dist#</b>,ent,0). Note that the last parameter is 0, and that tells TFormPoint to convert the point's coordinates into global coordinates. This way the point will always be behind the player wherever he is, instead of close to the origin of the 3D world.<br><br>The way it's being used in that code, it calculates the coordinates of a point in 3D space that's just a bit back from the target entity (the effect of the -dist# in the Z axis). Try playing with "hite#" and "-dist#" values. If you put a positive value in hite#, like 5, for instance, the camera would be a bit higher.<br><br>He then slowly moves the camera into this point in space by using this code:<br><pre class=code>
cx#=(TFormedX()-EntityX(cam))*cspeed#
cy#=(TFormedY()-EntityY(cam))*cspeed#
cz#=(TFormedZ()-EntityZ(cam))*cspeed#
</pre><br>Multiplying 'how much the camera has to move to be in that 3D point' by Cspeed#, which is a small floating point value, gives the movement a slow and non-linear nature. The camera moves faster when far from the 3D point's current position, but slower the closer it gets. <br><br>He then moves the camera independently from it's current orientation:<br><pre class=code>TranslateEntity cam,cx,cy,cz</pre><br>(Those cx, cy and cz values will eventually be equal to the 3d point calculated by TFormPoint, if the player stays still long enough)<br><br>Then you keep the camera pointing the target entity (in that case, the player) with PointEntity, so as to keep the player centered on the screen. <br><br></td></tr></table><br>
<a name="1017425"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cubed Inc.</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for the explaination:)I studied the castle demo and put it's code into my game test. When I did, the camera was put a weird angles. Can someone help me out?<br>Here's my code(sorry if it's long)<br>;Lego Island Rescue;<br>Graphics3D 1366,768,32,1<br>SetBuffer BackBuffer ()<br><br>Const GRAVITY#=-1<br><br>Const TYPE_PLAYER=1,TYPE_TARGET=3,TYPE_WATER=3<br>Const TYPE_SCENERY=10,TYPE_TERRAIN=11<br><br>Collisions TYPE_PLAYER,TYPE_TERRAIN,2,3<br>Collisions TYPE_PLAYER,TYPE_SCENERY,2,2<br>Collisions TYPE_TARGET,TYPE_TERRAIN,2,2<br>Collisions TYPE_TARGET,TYPE_SCENERY,2,2<br><br>;Lego Island;<br>island=LoadMesh("media/tower.b3d")<br>islandtex=LoadTexture("media/Untitled2.png",256)<br>PositionEntity island,0,-150,60<br>ScaleEntity island,4,4,4<br>EntityFX island,0<br>EntityType island,TYPE_TERRAIN<br><br>;Lego Island;<br>island2=LoadMesh("media/tower.b3d")<br>islandtex2=LoadTexture("media/Untitled2.png",256)<br>PositionEntity island2,0,-150,5360<br>ScaleEntity island2,4,4,4<br>EntityFX island2,0<br>EntityType island2,TYPE_TERRAIN<br><br>;player;<br>player=CreateCube()<br>PositionEntity player,0,100,0<br>ScaleEntity player,4,4,4<br>EntityType player,TYPE_PLAYER<br><br>;water;<br>water = CreatePlane ()<br>h20 = LoadTexture ("media/water_tex.jpg",256)<br>ScaleTexture h20, 200, 200<br>EntityTexture water, h20<br>EntityAlpha water, 0.4<br>PositionEntity water,0,-140,60<br>EntityFX water,1<br>EntityType water,TYPE_TERRAIN<br><br>;light;<br>light=CreateLight()<br><br>;camera (AKA the player);<br>camera=CreateCamera()<br>EntityRadius camera,30<br>PositionEntity camera,0,100,-50<br>EntityType camera,TYPE_PLAYER<br><br>;target;<br>target=CreatePivot( player )<br><br>;sky/fog/range;<br>CameraFogMode camera,1<br>CameraFogColor camera,200, 220, 255<br>CameraRange camera, 1,5500<br>CameraFogRange camera,1,2500-10<br>CameraClsColor camera,200,220,255<br><br>;move camera;<br>While Not KeyDown(1)<br>		dx#=EntityX( target,True )-EntityX( camera )<br>		dy#=EntityY( target,True )-EntityY( camera )<br>		dz#=EntityZ( target,True )-EntityZ( camera )<br>		TranslateEntity camera,dx*.1,dy*.1,dz*.1<br>	PointEntity camera,player<br>TranslateEntity player,0,GRAVITY,0<br>If KeyDown(200)<br>MoveEntity player,0,0,3<br>EndIf<br>If KeyDown(208)<br>MoveEntity player,0,0,-3<br>EndIf<br>If KeyDown(203)<br>TurnEntity player,0,3,0<br>EndIf<br>If KeyDown(205)<br>TurnEntity player,0,-3,0<br>EndIf<br><br>UpdateWorld<br>RenderWorld<br>Flip<br>Wend<br>End <br><br></td></tr></table><br>
<a name="1017427"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> You created a pivot but didn't distance it from the player properly. Move the "target" with something like this, as soon as you create it:<br><br>MoveEntity Target,0,2,-7 <br><br></td></tr></table><br>
<a name="1017586"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cubed Inc.</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> Kryzon<br>Thanks. My camera is still kinda glitchy and it sometimes gets stuck, but it's a good start. Thanks to everyone btw:) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
