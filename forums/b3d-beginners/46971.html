<!DOCTYPE html><html lang="en" ><head ><title >Character Animation Control</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Character Animation Control</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Character Animation Control</a><br><br>
<a name="522568"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rogue Vector</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm having some frustrating problems with regard to controlling an MD2 character's animation sequences.<br><br>The big problem seems to be with the jump animation.<br><br>Can anyone help?<br><br>My code looks like this:<br><br><pre class=code>

;Called every frame

Function UpdateCharacter(model)
   
   currentanimframe = MD2AnimateTime(model)

   Select(True)
 
      Case (KeyHit(57))   : If (currentanimframe &lt; 67) Or (currentanimframe &gt; 72) AnimateMD2 model, 3, 0.1, 67, 72, 2 ;JUMP ANIMATION (SPACE BAR)

      Case (KeyDown(200)) : If (currentanimframe &lt; 40) Or (currentanimframe &gt; 46) AnimateMD2 model, 1, 0.1, 40, 46, 2 ;RUN ANIMATION (FORWARD ARROW)

      Default   :    If (currentanimframe &gt; 40) AnimateMD2 model, 1, 0.1, 0, 40, 2 ;DEFAULT IDLE ANIMATION

   End Select

End Function


</pre><br><br>Rogue Vector <br><br></td></tr></table><br>
<a name="522609"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >semar</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Rogue,<br>I've not B3D at the moment, so I can't help you with code, but you may check the code located in the B3D samples folder; some of that sample deals with MD2 animation, and I'm sure you'll find it useful.<br><br>If I recall fine, check the Reda directory. There's an example with a MD2 model which can run, fire and stay idle.<br><br>Sergio. <br><br></td></tr></table><br>
<a name="522668"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rogue Vector</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>If I recall fine, check the Reda directory. There's an example with a MD2 model which can run, fire and stay idle.<br> <br></div><br><br>Reda directory?<br><br>My B3D samples directory is 'Blitz3D\Samples\Blitz 3D Samples\'<br><br>And there isn't a directory in there called Reda.<br><br><br>Rogue Vector <br><br></td></tr></table><br>
<a name="522672"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >semar</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Uh, perhaps OpenDNA directory ? Again, under Blitz3D\Samples\Blitz 3D Samples\<br><br>Note that the example uses a .MD2 model, so if you do a search of *.MD2 from the main B3D samples directory, you should find it.<br><br>I recall Reda because it's the name of the author of the example (Reda Borchart I guess)<br><br>Anyway, looking at your code, I see that you use KeyHit for jump, and KeyDown for run animation. The running animation works because using keydown, you constantly animate the model, while using keyhit, you do this only once.<br><br>Bare in mind that the model should constantly be animated, so to speak; is not enough to call once the animation statement, you have to do at each frame - and so you do with the running animation, thanks the use of keydown.<br><br>So, you may better use a flag, and constantly set the animation as you do with the running one:<br><pre class=code>
const A_RUN = 1
const A_JUMP = 2
global anim

;
;character control
anim = 0
if keydown(run_key) then anim = A_RUN
if keydown(jump_key) then anim = A_JUMP

;
;
;in your main loop
select anim
case A_RUN
If (currentanimframe &lt; 40) Or (currentanimframe &gt; 46) AnimateMD2 model, 1, 0.1, 40, 46, 2 ;RUN ANIMATION (FORWARD ARROW)


case A_JUMP
If (currentanimframe &lt; 67) Or (currentanimframe &gt; 72) AnimateMD2 model, 3, 0.1, 67, 72, 2 ;JUMP ANIMATION (SPACE BAR)

end select

</pre><br><br>Does he jump now ? <br><br>:)<br><br>Sergio. <br><br></td></tr></table><br>
<a name="522964"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rogue Vector</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's my full code.<br><br>Maybe someone could explain why it doesn't work.<br><br>What's wrong with my logic here?<br><br>You'll need to substitute an MD2 model file path<br><br><pre class=code>

AppTitle "Character Animation System", "Are you sure you want to quit?"
Graphics3D 1024,768,16,0
SetBuffer BackBuffer()

;constants
Const FPS = 30

;globals
Global index%           = 0
Global ticks#           = 0.0 
Global elapsed#         = 0.0
Global period#          = 1000/FPS
Global time#            = MilliSecs() - period
Global tween#           = 0.0
Global model            = 0
Global texture          = 0
Global camera           = 0
Global light            = 0
Global modelfilepath$   = "system/media/MD2/alita/tris.md2"
Global texturefilepath$ = "system/media/MD2/alita/alita.pcx"
Global currentanimframe = 0
Global jumping          = False
Global crouching        = True


;start program
Initialise()

;main loop
Repeat

	Repeat
	
		elapsed = MilliSecs() - time
	
	Until elapsed

	ticks = elapsed / period
	
	tween# = Float(elapsed Mod period)/ Float(period)
	
	For index = 1 To ticks
		
		time = time + period
		
		If (index = ticks) CaptureWorld()
		
		Update_Character()

		
		UpdateWorld()
				
	Next
		
	RenderWorld(tween)

	Flip

Until KeyHit(1)

;shutdown program
ClearWorld()
End


;functions
Function Initialise()

	camera=CreateCamera() 
	
	PositionEntity camera,0,3,-4 

	light=CreateLight()
	
	PositionEntity light, 2, 6, -5 
	
	model   = LoadMD2(modelfilepath)
	
	texture = LoadTexture(texturefilepath)

	ScaleEntity model, 0.07, 0.07, 0.07
	
	EntityTexture model, texture
	
	PositionEntity model, 0, 1.7, 0

	PointEntity camera, model
	
	PointEntity light, model
	
	EntityParent light, model

	AnimateMD2 model, 1, 0.1, 0, 40 ;DEFAULT IDLE ANIMATIION


End Function


Function Update_Character()

	currentanimframe = MD2AnimTime(model)
			
	Select(True)
				
		Case (KeyHit(57))	: 		jumping  = True 
										
		Case (KeyHit(46))	:		crouching = True 	
									
		Case (KeyDown(200)) :		If (currentanimframe &lt; 40) Or (currentanimframe &gt; 46) AnimateMD2 model, 1,  0.25, 40, 46, 2 ;FORWARD with UP ARROW
				
		Case (KeyDown(208))	:		If (currentanimframe &lt; 40) Or (currentanimframe &gt; 46) AnimateMD2 model, 1, -0.25, 40, 46, 2 ;REVERSE with DOWN ARROW
									
		Default 			:		If (currentanimframe &gt; 40) AnimateMD2 model, 1, 0.05, 0, 40, 2 	  							  ;DEFAULT IDLE ANIMATIION
									

	End Select
		
	;process animations
	If (jumping)
									
		AnimateMD2 model, 3, 0.1, 67, 72, 2 ;ONE-SHOT JUMP ANIMAION
			
	EndIf
	
	If (currentanimframe =&gt; 72) 
	
		jumping = False 
		If (currentanimframe &gt; 40) AnimateMD2 model, 1, 0.05, 0, 40, 2 ;DEFAULT IDLE ANIMATIION
	
	EndIf 

End Function

</pre><br><br>Regards,<br><br>Rogue Vector <br><br></td></tr></table><br>
<a name="522995"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think I know what's up -- 'jumping' is set to True when you hit Space, so this...<br><br><pre class=code>
	If (jumping)
									
		AnimateMD2 model, 3, 0.1, 67, 72, 2 ;ONE-SHOT JUMP ANIMAION
			
	EndIf
</pre><br>... is always called when the code gets here. Because Animate/MD2 restarts the animation, it doesn't get past the first frame. <br><br></td></tr></table><br>
<a name="522996"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> This fixes it (changes = *****)...<br><br><pre class=code>
	Select(True)
				
		Case (KeyHit(57))	: 		AnimateMD2 model, 3, 0.1, 67, 72, 2: jumping  = True ; *****
										
		Case (KeyHit(46))	:		crouching = True 	
									
		Case (KeyDown(200)) :		If (currentanimframe &lt; 40) Or (currentanimframe &gt; 46) AnimateMD2 model, 1,  0.25, 40, 46, 2 ;FORWARD with UP ARROW
				
		Case (KeyDown(208))	:		If (currentanimframe &lt; 40) Or (currentanimframe &gt; 46) AnimateMD2 model, 1, -0.25, 40, 46, 2 ;REVERSE with DOWN ARROW
									
		Default 			:		If (jumping = 0) And (currentanimframe &gt; 40) AnimateMD2 model, 1, 0.05, 0, 40, 2  ; *****	  							  ;DEFAULT IDLE ANIMATIION
									

	End Select
		
	;process animations
;	If (jumping)	 ; *****
					 ; *****
					 ; *****
					 ; *****
;	EndIf			 ; *****
	
	If (currentanimframe =&gt; 72) 

		jumping = False
		If (currentanimframe &gt; 40) AnimateMD2 model, 1, 0.05, 0, 40, 2 ;DEFAULT IDLE ANIMATIION
	
	EndIf 
</pre> <br><br></td></tr></table><br>
<a name="523102"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rogue Vector</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> When I ran this, it certainly fixed the problem with the JUMP animation - thanks by the way :) <br><br>But, now the default IDLE animation doesn't work.<br><br>???<br><br>Rogue Vector <br><br></td></tr></table><br>
<a name="523115"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Seems to be working here, even after doing the jump (and with debug on and "DebugLog currentanimframe" at the start of Update_Character I can see it's going from frame 0 to 40). I'm using the dragon model from "\samples\mak\dragon" BTW.<br><br>Here's the full source as I currently have it -- see if it works for you...<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
AppTitle "Character Animation System", "Are you sure you want to quit?"
Graphics3D 1024,768,16,0
SetBuffer BackBuffer()

;constants
Const FPS = 30

;globals
Global index%           = 0
Global ticks#           = 0.0 
Global elapsed#         = 0.0
Global period#          = 1000/FPS
Global time#            = MilliSecs() - period
Global tween#           = 0.0
Global model            = 0
Global texture          = 0
Global camera           = 0
Global light            = 0
Global modelfilepath$   = "C:\Program Files\Blitz3D\samples\mak\dragon\model\dragon.md2"
Global texturefilepath$ = "C:\Program Files\Blitz3D\samples\mak\dragon\model\dragon.bmp"
Global currentanimframe = 0
Global jumping          = False
Global crouching        = True


;start program
Initialise()

;main loop
Repeat

	Repeat
	
		elapsed = MilliSecs() - time
	
	Until elapsed

	ticks = elapsed / period
	
	tween# = Float(elapsed Mod period)/ Float(period)
	
	For index = 1 To ticks
		
		time = time + period
		
		If (index = ticks) CaptureWorld()
		
		Update_Character()

		
		UpdateWorld()
				
	Next
		
	RenderWorld(tween)

	Flip

Until KeyHit(1)

;shutdown program
ClearWorld()
End


;functions
Function Initialise()

	camera=CreateCamera() 
	
	PositionEntity camera,0,3,-4 

	light=CreateLight()
	
	PositionEntity light, 2, 6, -5 
	
	model   = LoadMD2(modelfilepath)
	
	texture = LoadTexture(texturefilepath)

	ScaleEntity model, 0.07, 0.07, 0.07
	
	EntityTexture model, texture
	
	PositionEntity model, 0, 1.7, 0

	PointEntity camera, model
	
	PointEntity light, model
	
	EntityParent light, model

	AnimateMD2 model, 1, 0.1, 0, 40 ;DEFAULT IDLE ANIMATIION


End Function


Function Update_Character()

	currentanimframe = MD2AnimTime(model)
	DebugLog currentanimframe
	
	Select(True)
				
		Case (KeyHit(57))	: 		AnimateMD2 model, 3, 0.1, 67, 72, 2: jumping  = True ; *****
										
		Case (KeyHit(46))	:		crouching = True 	
									
		Case (KeyDown(200)) :		If (currentanimframe &lt; 40) Or (currentanimframe &gt; 46) AnimateMD2 model, 1,  0.25, 40, 46, 2 ;FORWARD with UP ARROW
				
		Case (KeyDown(208))	:		If (currentanimframe &lt; 40) Or (currentanimframe &gt; 46) AnimateMD2 model, 1, -0.25, 40, 46, 2 ;REVERSE with DOWN ARROW
									
		Default 			:		If (jumping = 0) And (currentanimframe &gt; 40) AnimateMD2 model, 1, 0.05, 0, 40, 2  ; *****	  							  ;DEFAULT IDLE ANIMATIION
									

	End Select
		
	;process animations
;	If (jumping)	 ; *****
					 ; *****
					 ; *****
					 ; *****
;	EndIf			 ; *****
	
	If (currentanimframe =&gt; 72) 

		jumping = False
		If (currentanimframe &gt; 40) AnimateMD2 model, 1, 0.05, 0, 40, 2 ;DEFAULT IDLE ANIMATIION
	
	EndIf 

End Function
</textarea> <br><br></td></tr></table><br>
<a name="524567"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rogue Vector</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks! <br><br>Still having a problem with the Death animation sequence.<br><br>It plays out normally, then tweens back to an earlier frame.<br><br>Any ideas?<br><br>The model file and texture can be downloaded from here: <a href="http://www.octanedigitalstudios.com/downloads/AnimationControl.zip" target="_blank">http://www.octanedigitalstudios.com/downloads/AnimationControl.zip</a><br><br>Here's my latest code:<br><pre class=code>
AppTitle "Character Animation System", "Are you sure you want to quit?"
Graphics3D 800,600,16,0
SetBuffer BackBuffer()

Include "keyconstants.bb"

;constants
Const FPS = 30
Const type_MD2 = 1000
Const type_B3D = 1001
Const type_3DS = 1002


;globals
Global index%         		= 0
Global ticks#         		= 0.0 
Global elapsed#       		= 0.0
Global period#        		= 1000/FPS
Global time#          		= MilliSecs() - period
Global tween#         		= 0.0
Global model           		= 0
Global texture         		= 0
Global camera          		= 0
Global light           		= 0
Global modelfilepath$   	= "tris.md2"
Global texturefilepath$ 	= "alita.pcx"
Global g_currentanimframe 	= 0
Global g_jumping      		= False
Global g_dying				= False
Global g_character_is_dead  = False



;start program
Initialise()

;main loop
Repeat

	Repeat
	
		elapsed = MilliSecs() - time
	
	Until elapsed

	ticks = elapsed / period
	
	tween# = Float(elapsed Mod period)/ Float(period)
	
	For index = 1 To ticks
		
		time = time + period
		
		If (index = ticks) CaptureWorld()
		
		Update_CharacterAnimation(model, type_MD2)
		
		UpdateWorld()
				
	Next
		
	RenderWorld(tween)

	Color 0,0,255: Text 5, 2, "Animation Frame: " + g_currentanimframe
	
	Color 255,255,255
	
	Text 5, 20, "ARROW KEYS = FORWARDS \ BACK" 
	
	Text 5, 35, "SPACE = JUMP"
	
	Text 5, 50, "C KEY + ARROW UP = CROUCH WALK"
	
	Text 5, 65, "D KEY = DEATH"
	
	If (g_character_is_dead=True) 
	
		Color 255,0,0:Text 5, 80, "Character is dead!"

	EndIf

	Flip

Until KeyHit(1)

;shutdown program
ClearWorld()
End


;functions
Function Initialise()

	camera=CreateCamera() 
	
	PositionEntity camera,0,3,-5 

	light=CreateLight()
	
	PositionEntity light, 2, 6, -5 
	
	model   = LoadMD2(modelfilepath)
	
	texture = LoadTexture(texturefilepath)

	ScaleEntity model, 0.07, 0.07, 0.07
	
	EntityTexture model, texture
	
	PositionEntity model, 0, 1.7, 0

	PointEntity camera, model
	
	PointEntity light, model
	
	EntityParent light, model
		
	AnimateMD2 model, 1, 0.1, 0, 40, 4 ;DEFAULT IDLE ANIMATIION

End Function


Function Update_CharacterAnimation(v_model, v_model_type)

	g_currentanimframe = process_anim_frame(v_model, v_model_type);Get the current animation frame 
								
	If 	(g_character_is_dead=False)	
		
		Select(True)
					
			Case (KeyHit(KEY_D))				:		process_dying(v_model, v_model_type)		; DEATH 1 with D
			
			Case (KeyHit(KEY_SPACE))			: 		process_jump(v_model, v_model_type)			; JUMP with SPACE BAR
			
			Case (KeyDown(KEY_C))				:		process_crouching(v_model, v_model_type)  	; CROUCH IDLE and CROUCH WALK with C key and/or ARROW KEYS	
			
			Case (KeyDown(KEY_ARROWPAD_UP)) 	:		process_run(v_model, v_model_type)			; RUN with UP ARROW
					
			Case (KeyDown(KEY_ARROWPAD_DOWN))	:		process_run_backwards(v_model, v_model_type); RUN BACKWARDS with DOWN ARROW
										
			Default 							:		If (g_dying=True)
			
															process_dying(v_model, v_model_type)		; CHARACTER IS DYING
			
														Else
			
															process_idle(v_model, v_model_type)			; DEFAULT IDLE ANIMATIION
			
														EndIf							
		End Select
								
		If (g_currentanimframe = 72) 
		
			g_jumping = False
			If (g_currentanimframe &gt; 40) AnimateMD2 v_model, 1, 0.1, 0, 40, 4 ;DEFAULT IDLE ANIMATIION
			
		EndIf 
		
		If (g_currentanimframe = 184)
		
			g_character_is_dead = True		
		
		EndIf
		
	Else
	
		;Character is dead - perform post mortem processes here

	EndIf

	

End Function


Function process_anim_frame%(v_model, v_model_type)

	If (v_model_type = type_MD2)
	
		Return MD2AnimTime(v_model)
		
	Else
	
		Return AnimTime(v_model)
		
	EndIf

End Function


Function process_idle(v_model, v_model_type)

	If (Not g_jumping) And (Not g_death) And (g_currentanimframe &gt; 40) 
	
		AnimateMD2 v_model, 1, 0.1, 0, 40, 4  ; DEFAULT IDLE ANIMATIION

	EndIf
	
End Function


Function process_jump(v_model, v_model_type)

	If (v_model_type = type_MD2)
	
		AnimateMD2 v_model, 3, 0.1, 67, 72, 4 
		g_jumping  = True	
		
	Else
	
		;animate other model types here

	EndIf
	
End Function


Function process_run(v_model, v_model_type)

	If (KeyDown(KEY_C)=False)

		If (v_model_type = type_MD2)
	
			If (g_currentanimframe &lt; 41) Or (g_currentanimframe &gt; 46) 
				
				AnimateMD2 v_model, 1, 0.25, 41, 46, 4
				g_jumping  = False
				
			EndIf
	
		Else
	
			;animate other model types here
	
		EndIf

	EndIf

End Function


Function process_run_backwards(v_model, v_model_type)

	If (KeyDown(KEY_C)=False)
	
		If (v_model_type = type_MD2)
	
			If (g_currentanimframe &lt; 41) Or (g_currentanimframe &gt; 46) 
				
				AnimateMD2 v_model, 1, -0.25, 41, 46, 4
				g_jumping  = False
				
			EndIf
	
		Else
	
			;animate other model types here
	
		EndIf
		
	EndIf

End Function


Function process_crouching(v_model, v_model_type)
	
	Select(True)
	
		Case (KeyDown(KEY_ARROWPAD_UP)=True) 		:			If (v_model_type = type_MD2);CROUCH WALK FORWARDS - UP ARROW
	
																	If (g_currentanimframe &lt; 155) Or (g_currentanimframe &gt; 160) 
																		
																		AnimateMD2 v_model, 1, 0.1, 155, 160, 4
																		g_jumping  = False
																		
																	EndIf
															
																Else
															
																	;animate other model types here
															
																EndIf
	
	
	
		Case (KeyDown(KEY_ARROWPAD_DOWN)=True)		:			If (v_model_type = type_MD2);CROUCH WALK BACKWARDS - DOWN ARROW
	
																	If (g_currentanimframe &lt; 155) Or (g_currentanimframe &gt; 160) 
																		
																		AnimateMD2 v_model, 1, -0.1, 155, 160, 4
																		g_jumping  = False
																		
																	EndIf
															
																Else
															
																	;animate other model types here
															
																EndIf

	
	
		Default										:			If (v_model_type = type_MD2);CROUCH IDLE
	
																	If (g_currentanimframe &lt; 136) Or (g_currentanimframe &gt; 154) 
																		
																		AnimateMD2 v_model, 1, 0.1, 136, 154, 4
																		g_jumping  = False
																		
																	EndIf
															
																Else
															
																	;animate other model types here
															
																EndIf

		
	End Select
	

End Function


Function process_dying(v_model, v_model_type)

	If (v_model_type = type_MD2)
	
		If (g_currentanimframe &lt; 179) Or (g_currentanimframe &gt; 184)
		
			AnimateMD2 v_model, 3, 0.1, 179, 184, 1
			
		EndIf 
			
		g_jumping  	= False
		g_dying = True
							
	Else
	
		;animate other model types here

	EndIf

End Function

</pre><br><br>Rogue Vector. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
