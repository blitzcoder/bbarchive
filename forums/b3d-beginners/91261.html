<!DOCTYPE html><html lang="en" ><head ><title >Triggering screen changing?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Triggering screen changing?</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Triggering screen changing?</a><br><br>
<a name="1038605"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cubed Inc.</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I know that I just created a post asking how to change screens and levels. <br><br>Thanks from the help of the help of numerous Blitz3d users, I now understand how to manage screens and levels. <br><br>Now all I need to learn is how to change them. I need to know how to trigger each screen to switch to the other.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics 640,480,0,1

Const Title_Screen = 101
Const In_Game = 201
Const In_Game_Paused = 202
Global GameStatus = Title_Screen

While Not KeyDown(1)

Cls

Select GameStatus
    Case Title_Screen
        title()
    Case In_Game
         game()
End Select


Flip False


Wend


End


Function title()

Text 10,10, "Screen1"
While Not KeyDown(1)
Flip
Wend 
End

End Function

Function game()

Text 10,10, "Screen2"
While Not KeyDown(1) 
Flip
Wend 
End

End Function
</textarea><br>Can someone help me achieve this?<br><br>please reply.thanks. <br><br></td></tr></table><br>
<a name="1038606"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rob the Great</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here is a sample based on the code you provided.<br><br><pre class=code>

Graphics 640,480,0,1

SetBuffer BackBuffer()

Const Title_Screen = 101
Const In_Game = 201
Const In_Game_Paused = 202
Global GameStatus = Title_Screen
Global SampleText$  ;This will only be used to display what's happening internally in Blitz

;Main Loop
;--------------------------------

While Not KeyDown(1)

	Cls
	
	GetGameStatus()

	Select GameStatus
		Case Title_Screen
			title()
		Case In_Game
			game()
		Case In_Game_Paused
			gamepause()
	End Select

	Text 10,10,SampleText$

	Flip

Wend

;--------------------------------
;End Main Loop

End

;Functions List
;--------------------------------

Function title()

	SampleText$ = "You are now at the Title Screen!"

End Function

Function game()

	SampleText$ = "You are now Playing the Game!"

End Function

Function gamepause()

	SampleText$ = "You have just Paused the Game!"

End Function

Function GetGameStatus()

	If KeyDown(57) ;If We Push the Spacebar
		GameStatus = Title_Screen ;Go to the Title Menu
	EndIf
	If KeyDown(28) ;If We Push the Enter Key
		GameStatus = In_Game ;Start the Game
	EndIf
	If KeyDown(25) ;If We Push the "P" Key
		GameStatus = In_Game_Paused ;Pause the Game
	EndIf

End Function

;--------------------------------
;End Functions List

</pre> <br><br></td></tr></table><br>
<a name="1038617"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cubed Inc.</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Rob The Great<br>Thanks for the example, but I have another little problem. <br><br>I tried modifying your well done example by making it change from a 2d screen with text to a 3d game with blocks. <br><br>When I changed to the 3d level from the 2d title screen, it worked great, but when I tried to change to the 2d title screen from the 3d level, it doesn't change. <br><br>Could you help me out again? <br><br>Sorry for asking for so much from just one person, but the help would be very...well....helpful.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics 640,480,0,1

SetBuffer BackBuffer()

Const Title_Screen = 101
Const In_Game = 201
Const In_Game_Paused = 202
Global GameStatus = Title_Screen
Global SampleText$  ;This will only be used to display what's happening internally in Blitz

;Main Loop
;--------------------------------

While Not KeyDown(1)

	Cls
	
	GetGameStatus()

	Select GameStatus
		Case Title_Screen
			title()
		Case In_Game
			game()
		Case In_Game_Paused
			gamepause()
	End Select

	Text 10,10,SampleText$

	Flip

Wend

;--------------------------------
;End Main Loop

End

;Functions List
;--------------------------------

Function title()

	SampleText$ = "You are now at the Title Screen!"

End Function

Function game()

Graphics3D 1280,600,32,1

camera = CreateCamera()
PositionEntity camera,0,0,-10

light=CreateLight()

For x = -3 To 3
cube=CreateCube()
EntityColor cube,Rand(0,255),Rand(0,255),Rand(0,255)
PositionEntity cube,x,x,0
ScaleEntity cube,1.5,0.5,0.5
Next

While Not KeyDown(1)

RenderWorld()
If KeyDown(2) Then 
Return 
EndIf 
Flip
Wend

End

End Function

Function gamepause()

	SampleText$ = "You have just Paused the Game!"

End Function

Function GetGameStatus()

	If KeyDown(57) ;If We Push the Spacebar
		GameStatus = Title_Screen ;Go to the Title Menu
	EndIf
	If KeyDown(28) ;If We Push the Enter Key
		GameStatus = In_Game ;Start the Game
	EndIf
	If KeyDown(25) ;If We Push the "P" Key
		GameStatus = In_Game_Paused ;Pause the Game
	EndIf

End Function
</textarea><br><br>please reply.thanks. <br><br></td></tr></table><br>
<a name="1038692"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dawlane</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Check your Graphics 3D resolution as I don't think that I have never seen a screen resolution of 1280x600 (yet) and it would be a good idea to re-write your code using a flag variable so the the graphics mode is set only once for each mode change.<br><br>Edit: had another look at your code the problem your having is this bit<br><pre class=code>While Not KeyDown(1)

RenderWorld()
If KeyDown(2) Then 
Return 
EndIf 
Flip
Wend

End</pre>In the "game()" function try changing it to this<br><pre class=code>While GameStatus = In_Game
	RenderWorld()
	Flip
	GetGameStatus()
	If KeyDown(1) Then End
Wend</pre> and it would be wise to move your Graphics3D/2D commands into the "GetGameStatus()" function so that they are set only once.<br><br>What was happening was that after you was returning from the while loop the GameStatus variable was still set to In_Game and all it was doing was calling the "game()" function over and over. <br><br></td></tr></table><br>
<a name="1038742"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dawlane</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> OK had a play with your code (Note you have to un-pause to goback to the title screen) <br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics 640,480,0,1

SetBuffer BackBuffer()

Const Title_Screen = 101
Const In_Game = 201
Const In_Game_Paused = 202
Global GameStatus = Title_Screen
Global SampleText$  ;This will only be used to display what's happening internally in Blitz
Global game_initailised = False
Global game_paused = False	; Used to flag if the game is paused
Global old_GameState = 0

;Main Loop
;--------------------------------

While Not KeyDown(1)
	
	Cls
	
	GetGameStatus()
	
	Select GameStatus
		Case Title_Screen
			title()
		Case In_Game
			game()
		Case In_Game_Paused
			gamepause()
	End Select
	
	; render only the IN GAME Display and paused game if the title screen or other 2D Dispaly is not showing would be nice if there was a way to detect if Graphics3D was initialised 
	If GameStatus &gt; Title_Screen Then RenderWorld()
	
	Text 10,10,SampleText$
	Flip
	
Wend

;--------------------------------
;End Main Loop

End

;Functions List
;--------------------------------

Function title()
	
	SampleText$ = "You are now at the Title Screen!"
	
End Function

Function game()
	
	;You could initialise your game in another function or in the if keydown statement
	If game_initailised = False
		
		camera = CreateCamera()
		PositionEntity camera,0,0,-10
	
		light=CreateLight()
	
		For x = -3 To 3
			cube=CreateCube()
			EntityColor cube,Rand(0,255),Rand(0,255),Rand(0,255)
			PositionEntity cube,x,x,0
			ScaleEntity cube,1.5,0.5,0.5
		Next
		game_initailised = True
		
	EndIf
	SampleText$ = "You have are now in the Game"
	;Do game logic stuff etc
	
End Function

Function gamepause()
	
	SampleText$ = "You have just Paused the Game!"
	
End Function

Function GetGameStatus()
	
	If KeyDown(57) And GameStatus &lt; In_Game_Paused ;If We Push the Spacebar and we are not paused then do the title
		GameStatus = Title_Screen ;Go to the Title Menu
		Graphics 640,480,0,1		;Set the Graphics mode for the Title screen
		game_initailised = False
		
	EndIf
	
	If KeyDown(28) And GameStatus &lt; In_Game ;If We Push the Enter Key and the game isn't running then start the game
		GameStatus = In_Game ;Start the Game
		Graphics3D 1280,800,32,1	;Set the graphics mode for the 3D Game
		game_initailised = False ; You could call a function to initialise your game here
	EndIf
	
	If KeyDown(25) And GameStatus &gt; Title_Screen ;If We Push the "P" Key and the game is running then pause the game
		game_paused = game_paused Xor 1
		Delay 100
	EndIf
	
	If Not GameStatus = In_Game_Paused Then old_GameState = GameStatus	;copy the current GameStatus only if the game is not paused
	
	;Pause the game if the pause flag is True else just carry on
		If game_paused = True
			GameStatus = In_Game_Paused
			Else GameStatus = old_GameState
		EndIf
	
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1038776"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rob the Great</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I like the adaptation and the direction this is moving.<br><br>I think you're having issues because you're trying to set two different resolutions in your game. While this can work if it's done correctly, I would recommend only setting your Graphics once, and that's when you first start the program.<br><br>Here's a little example of what I'm talking about. This is an example only, and will not execute unless you find an image to load (I haven't tested it). Keep in mind that all 2d commands will work the same way, whether Graphics3D is called or not.<br><br><pre class=code>
Graphcics3D 640,480

SetBuffer BackBuffer()

;this is where you would load the title screen
Global titleimage = LoadImage("YouImageHere.bmp")

While Not KeyDown(1)

;Run your program stuff here. When using Renderworld, I don't even think 
;that you have to use the Cls command, as it's built in.

RenderWorld()

;now, if you were to place all 2D drawing commands here...
DrawImage titleimage,0,0
Text 15,15,"Some Random Text, blah blah..."

;...before using the Flip command
Flip

;Everything that is 2D will draw on the screen, on top of whatever was rendered below it.
;This means that if you don't have any 3D objects or cameras, it will be 
;nothing but a black screen with your 2D images appearing. It also means that you can ;run a camera and a demo world with your image appearing on top of it.

Wend

End
</pre><br><br>If this doesn't quite make sense and you wish to change graphics modes throughout your program, then just remember to use "End Graphics" before calling your next "Graphics" command, otherwise Blitz will fail to set the new graphics. Keep in mind that this will cause the program screen to flicker and jump for a second or so (I think, haven't tried that either).<br><br><pre class=code>
Graphics 640,480

;Run your whole title screen here

EndGraphics

Graphics3D 1440,900 ;Real resolution on my laptop, lol.

;Run your whole game here.
</pre><br>I didn't quite read through dawlane's version, but it sounds like she was on to something. <br><br></td></tr></table><br>
<a name="1038832"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Serpent</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> You can have a second graphics call but after each Graphics call, all graphics items (like loaded images, fonts etc.) are lost from what I've seen.  It's generally easier just to stick with one graphics mode, but if you wan't to try this, make sure everything necessary is loaded after each Graphics call. <br><br></td></tr></table><br>
<a name="1038861"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rob the Great</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I stand corrected. I've never actually tried to call graphics twice in the same program, so I wasn't entirely sure how Blitz would react.<br><br>That brings up another question: What exactly is the purpose of the EndGraphics command? To free up memory, or something? <br><br></td></tr></table><br>
<a name="1038889"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dawlane</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> I stand corrected. I've never actually tried to call graphics twice in the same program, so I wasn't entirely sure how Blitz would react. <br></div><br>That makes two of us.<br><br>From what I can see the EndGraphics command returns Bliz3D back in to it's "TEXT" mode (400x300) and I use the term "TEXT" loosely as you can still use the 2D image commands etc. This is what Blitz3D defaults to if you don't use the and Graphics2D/3D command.<br>Run the code bellow to see what I mean and then uncomment the Graphics and EndGraphics command.<br><br><pre class=code>; End Graphics Example

; Enter graphics mode
;Graphics 800,600,16

; Print hello 10 times on the graphic screen
For t = 1 To 10
	Print "Hello"
Next

Print
Print "Height = " + GraphicsHeight()
Print "Width = " + GraphicsWidth()
Print "Depth = " + GraphicsDepth()

Plot 50,50

; Wait for a left mouse button
While Not MouseHit(1)
Wend 

; End the graphics mode
;EndGraphics 

; Print again, this time to the text screen
For t = 1 To 10
	Print "There"
Next

Print
Print "Height = " + GraphicsHeight()
Print "Width = " + GraphicsWidth()
Print "Depth = " + GraphicsDepth()

WaitKey()</pre> <br><br></td></tr></table><br>
<a name="1039099"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Serpent</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think that along with returning Blitz to its original state, it might do a similar thing to different Graphics calls and clear memory associated with 'graphics objects'.  Don't take my word for it though, I'm not too sure about the internal things that happen here. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
