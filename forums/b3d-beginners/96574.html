<!DOCTYPE html><html lang="en" ><head ><title >Chase Cam</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Chase Cam</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Chase Cam</a><br><br>
<a name="1117997"></a>

<a name="1117998"></a>

<a name="1118000"></a>

<a name="1118002"></a>

<a name="1118003"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Happy Llama</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi i am making a simple came where the camera follows the player. I have added a simple chase cam that follows the player but does not rotate with it. Please help!<br><br><br><br>Graphics3D 1000,700,64,3<br><br>Const gravity=0.3<br><br>sun=CreateLight()<br><br>playercam=CreateCamera()<br>RotateEntity playercam,30,0,0<br><br><br>ground=CreatePlane()<br>PositionEntity ground,0,-10,0<br>groundtex=LoadTexture("terrain-1.jpg")<br>EntityTexture ground, groundtex<br>ScaleTexture groundtex,50,50<br><br>player=CreateCube()<br>EntityColor player,0,255,0<br> <br><br><br>While Not KeyDown (1)<br><br>If KeyDown(200) MoveEntity player,0,0,1 <br>If KeyDown(208) MoveEntity player,0,0,-1<br>If KeyDown(203) TurnEntity player,0,3,0<br>If KeyDown(205) TurnEntity player,0,-3,0<br><br>PositionEntity playercam,EntityX(player),EntityY(player),EntityZ(player)<br>MoveEntity playercam,0,0,-5<br><br><br>UpdateWorld<br>RenderWorld<br><br>Flip<br><br><br>Wend<br><br>End<br><br>Thanks!<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1118015"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rob the Great</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> There are many ways to control a camera, but for what it sounds like you're trying to do, give one of these two examples a try. The first one positions the camera where you want it to be relative to the player, and then sets EntityParent camera,player so that it follows the player's movements:<br><pre class=code>
Graphics3D 640,480,0,2

SetBuffer BackBuffer()

Global player = CreateCube()

Global camera = CreateCamera()
MoveEntity camera,0,0,-5
EntityParent camera,player

Global ground = CreatePlane()
EntityColor ground,0,255,0
MoveEntity ground,0,-1,0

While Not KeyDown(1)

   If KeyDown(200) Then MoveEntity player,0,0,1
   If KeyDown(208) Then MoveEntity player,0,0,-1
   If KeyDown(203) Then TurnEntity player,0,3,0
   If KeyDown(205) Then TurnEntity player,0,-3,0

   RenderWorld
   Flip

Wend

End
</pre><br>The other option is a little more complex. Basically, you create a camera pivot, make the camera pivot the parent entity of the camera, position the camera pivot where the player is at every logic loop, point the camera at the player every loop, rotate the camera pivot (orbiting the camera) if they push the A or D keys, and then move the camera closer or further to the camera pivot if they push the W or S keys and reparent to the pivot. This sounds complex, but it's not too bad. Just call UpdateCamera() every loop and you're set. Keep in mind that this is untested and is only meant for concept purposes. If you do wish to use this, make sure you add in actual scancodes in place of the KeyDown() letters in the UpdateCamera() function.<br><pre class=code>
Graphics3D 640,480,0,2

SetBuffer BackBuffer()

Global player = CreateCube()

Global camera = CreateCamera()
MoveEntity camera,0,0,-5
Global camerapivot = CreatePivot()
EntityParent camera,camerapivot

Global ground = CreatePlane()
EntityColor ground,0,255,0
MoveEntity ground,0,-1,0

While Not KeyDown(1)

   If KeyDown(200) Then MoveEntity player,0,0,1
   If KeyDown(208) Then MoveEntity player,0,0,-1
   If KeyDown(203) Then TurnEntity player,0,3,0
   If KeyDown(205) Then TurnEntity player,0,-3,0

   UpdateCamera()

   RenderWorld
   Flip

Wend

Function UpdateCamera()

   PointEntity camera,player

   PositionEntity camerapivot,EntityX(player),EntityY(player),EntityZ(player)

   If KeyDown(A)
      TurnEntity camerapivot,0,1,0
   EndIf

   If KeyDown(D)
      TurnEntity camerapivot,0,-1,0
   EndIf

   If KeyDown(W)
      If EntityDistance(player,camera) &gt; 3
         EntityParent camera,0
         PointEntity camera,player
         MoveEntity camera,0,0,1
         EntityParent camera,camerapivot
      EndIf
   EndIf

   If KeyDown(S)
      If EntityDistance(player,camera) &lt; 10
         EntityParent camera,0
         PointEntity camera,player
         MoveEntity camera,0,0,-1
         EntityParent camera,camerapivot
      EndIf
   EndIf

End Function

End
</pre> <br><br></td></tr></table><br>
<a name="1118024"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Happy Llama</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> The camera still wont rotate with the cube. Any ideas on how to do that? <br><br></td></tr></table><br>
<a name="1118054"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rob the Great</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I don't think I follow...That first example should do that. I might have done something wrong, it was all written on the fly.<br><br>What do you mean by "rotate with the cube"? Something more like the camera movement in the castle demo? <br><br></td></tr></table><br>
<a name="1118057"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hmm, do you want this effect (based on your code)?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics3D 800,600,32,2

timer=CreateTimer(60)

sun=CreateLight()

player=CreateCube()
EntityColor player,0,255,0
PositionEntity player,0,1.5,0

playercam=CreateCamera(player)
PositionEntity playercam,0,3,-6
RotateEntity playercam,30,0,0

ground=CreateGrid()

While Not KeyDown (1)
	
	If KeyDown(200) Then MoveEntity player,0,0,1
	If KeyDown(208) Then MoveEntity player,0,0,-1
	If KeyDown(203) Then TurnEntity player,0,3,0
	If KeyDown(205) Then TurnEntity player,0,-3,0
	
	UpdateWorld
	RenderWorld
	
	WaitTimer timer
	
	Flip 0
	
Wend

End

; create a nice ground grid
Function CreateGrid()
	
	Local x%,y%
	
	Local plane%=CreatePlane()
	Local tex%=CreateTexture(512,512,8)
	
	SetBuffer TextureBuffer(tex)
	
	; texture background
	Color 0,0,64 : Rect 0,0,512,512,1
	
	; texture grid
	Color 255,255,255
	For x=0 To 511 Step 32
		For y=0 To 511 Step 32
			Rect x,y,32,32,0
		Next
	Next
	
	SetBuffer BackBuffer()
	
	EntityTexture plane,tex
	ScaleTexture tex,128,128
	EntityAlpha plane,0.5
	
	Local mirror=CreateMirror(plane)
	
	
End Function</textarea><br><br>My own Third/Chasecam code goes like that:<br><br><img src="http://www.christianhart.de/bb/thirdcam.jpg"><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">; changeable variables
Const ScreenWidth% = 800
Const ScreenHeight% = 600
Const PlayerMovespeed# = 10.0
Const PlayerSize# = 16.0
Const CameraDistance# = 100.0
Global PlayerTurnspeed# = 25.0
Global CameraFOV# = 90.0
Global Timer% = CreateTimer(60)

; scene objects
Global Player%,PlayerHead%,PlayerCenter%,PlayerPivot%,PlayerCone%,Camera%

; collision types
Const CCAM%=1
Const CWORLD%=2

Graphics3D ScreenWidth,ScreenHeight,32,2

InitScene()

While Not KeyHit(1)
	
	Local wf%,mwheel%
	
	; SPACE = Wireframe
	If KeyHit(57) Then wf=1-wf : WireFrame wf
	
	; Mousewheel: change FOV angle
	mwheel=MouseZSpeed()
	If mwheel&lt;0 Then
		CameraFOV=CameraFOV+5
		If CameraFOV&gt;150 Then CameraFOV=150
		CameraZoom Camera,SetFOV(CameraFOV)
	EndIf
	If mwheel&gt;0 Then
		CameraFOV=CameraFOV-5
		If CameraFOV&lt;30 Then CameraFOV=30
		CameraZoom Camera,SetFOV(CameraFOV)
	EndIf
	
	; RMB = reset FOV to 90°
	If MouseDown(2) Then
		CameraFOV=90
		CameraZoom Camera,SetFOV(CameraFOV)
	EndIf
	
	ThirdCam(PlayerSize*10)
		
	UpdateWorld
		
	RenderWorld
	
	WaitTimer Timer
	
	Flip 0
	
Wend

End

Function InitScene()
	
	; Create Plane and Mirror
	CreateGrid()
	
	; Create Player body
	Player=CreateCube()
	ScaleMesh Player,PlayerSize,PlayerSize,PlayerSize
	PositionEntity Player,0,PlayerSize,0
	EntityFX Player,1
	EntityColor Player,255,0,0
	
	; Create Player "nose"
	PlayerCone=CreateCone(32)
	RotateEntity PlayerCone,90,0,0
	ScaleMesh PlayerCone,PlayerSize,PlayerSize,PlayerSize
	PositionEntity PlayerCone,0,PlayerSize,PlayerSize*2
	EntityParent PlayerCone,Player
	EntityFX PlayerCone,1
	EntityColor PlayerCone,0,255,0
	
	; Create invisible Player Pivots
	PlayerHead=CreatePivot()
	EntityParent PlayerHead,Player,1
	PositionEntity PlayerHead,0,PlayerSize,0
	PlayerCenter=CreatePivot(Player)
	PlayerPivot=CreatePivot()
	EntityParent PlayerPivot,PlayerCenter,1
	PositionEntity PlayerPivot,0,CameraDistance,-CameraDistance
	
	; Create Camera
	Camera=CreateCamera()
	CameraRange Camera,0.1,1000
	CameraFogRange Camera,0.1,1000
	CameraFogMode Camera,True
	CameraClsColor Camera,150,200,255
	CameraFogColor Camera,150,200,255
	EntityType Camera,CCAM
	EntityRadius Camera,PlayerSize
	
	Collisions CCAM,CWORLD,2,3
	
	MoveMouse ScreenWidth/2,ScreenHeight/2
	
	HidePointer
	
End Function

; create a nice ground grid
Function CreateGrid()
	
	Local x%,y%
	
	Local plane%=CreatePlane()
	Local tex%=CreateTexture(512,512,8)
	
	SetBuffer TextureBuffer(tex)
	
	; texture background
	Color 0,0,64 : Rect 0,0,512,512,1
	
	; texture grid
	Color 255,255,255
	For x=0 To 511 Step 64
		For y=0 To 511 Step 64
			Rect x,y,64,64,0
		Next
	Next
	
	SetBuffer BackBuffer()
	
	EntityTexture plane,tex
	ScaleTexture tex,512,512
	EntityType plane,CWORLD
	EntityAlpha plane,0.5
	
	Local mirror=CreateMirror(plane)
	
End Function

; third cam logic
Function ThirdCam(maxdistance#=100)
	
	Local movex#,movez#,px#,py#,pz#
	
	; move and turn player input
	movex#=(KeyDown(203)-KeyDown(205))
	movez#=(KeyDown(200)-KeyDown(208))
	
	; increase turnspeed
	PlayerTurnspeed=PlayerTurnspeed+(movex*2.00)
	
	; limit turnspeed
	PlayerTurnspeed=PlayerTurnspeed*0.6
	If Abs(PlayerTurnspeed)&lt;0.0100 Then PlayerTurnspeed=0
	
	; move player
	MoveEntity Player,0,0,movez*PlayerMovespeed
	
	; move around player
	TurnEntity PlayerCenter,0,-MouseXSpeed()/3.0,0
	MoveMouse ScreenWidth/2,LimitMouseY()
	
	; smart cam
	TurnEntity Player,0,PlayerTurnspeed,0
	px=EntityX(PlayerPivot)
	py=Normalize(((MouseY()-(ScreenHeight/2))*1.0/(ScreenHeight/2)),-1,1,0,maxdistance)
	pz=EntityZ(PlayerPivot)
	PositionEntity PlayerPivot,px,py,pz
	px=EntityX(PlayerPivot,1)
	py=EntityY(PlayerPivot,1)
	pz=EntityZ(PlayerPivot,1)
	PositionEntity Camera,px,py,pz
	PointEntity Camera,PlayerHead
	
End Function

; limits vertical cursor position to 10% / 90%
Function LimitMouseY()
	
	Local my%=MouseY()
	
	If my&lt;(ScreenHeight*0.1) Then my=(ScreenHeight*0.1) Else If my&gt;ScreenHeight*0.9 Then my=ScreenHeight*0.9
	
	Return my
	
End Function

; normalizes a value
Function Normalize#(value#=128.0,value_min#=0.0,value_max#=255.0,norm_min#=0.0,norm_max#=1.0)
	
	Return ((value#-value_min#)/(value_max#-value_min#))*(norm_max#-norm_min#)+norm_min#
	
End Function

; sets camera field of view to a given angle
Function SetFOV#(angle#)
	
	Return 1.0/Tan(angle/2.0)
	
End Function</textarea> <br><br></td></tr></table><br>
<a name="1118235"></a>

<a name="1118236"></a>

<a name="1118237"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Happy Llama</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks I was trying to get an effect on where the camera always looks at the back of the character. Thanks Krischan, that is exactly what is was looking for.<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
