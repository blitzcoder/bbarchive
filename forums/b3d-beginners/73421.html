<!DOCTYPE html><html lang="en" ><head ><title >About surfaces</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >About surfaces</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >About surfaces</a><br><br>
<a name="820300"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >PowerPC603</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi all,<br><br>If you have a mesh with 2 surfaces, where each surface holds the vertices to create 1 cube, is this the same as having 2 seperate cubes?<br><br>In this code, the mesh has 2 surfaces, each holding only 4 vertices and 2 triangles, making up on quad.<br>Is this the same as having 2 seperate meshes, where each mesh has one surface and holds the vertices and triangles for one quad?<br><br><pre class=code>
Graphics3D 800, 600, 0, 2

SetBuffer BackBuffer()

camera = CreateCamera()
PositionEntity camera, 0, 0, -20

mesh = CreateMesh()
surf1 = CreateSurface(mesh)
surf2 = CreateSurface(mesh)

v0 = AddVertex(surf1, -15, 5, 0)
v1 = AddVertex(surf1, -5, 5, 0)
v2 = AddVertex(surf1, -5, -5, 0)
v3 = AddVertex(surf1, -15, -5, 0)
t0 = AddTriangle(surf1, v0, v1, v3)
t1 = AddTriangle(surf1, v1, v2, v3)

v0 = AddVertex(surf2, 5, 5, 0)
v1 = AddVertex(surf2, 15, 5, 0)
v2 = AddVertex(surf2, 15, -5, 0)
v3 = AddVertex(surf2, 5, -5, 0)
t0 = AddTriangle(surf2, v0, v1, v3)
t1 = AddTriangle(surf2, v1, v2, v3)

While Not KeyHit(1)
	RenderWorld
	Flip
Wend
End
</pre><br><br>I don't seem to understand why someone would make 1 big mesh with several different surfaces, while several different meshes with only one surface is the same?<br>On top of that, seperate meshes can be moved individually while one big mesh moves everything.<br><br>Also, when you create a mesh with 2 surfaces, how do you apply the texture to the second surface?<br>Or does texturing apply the texture to all surfaces of the mesh? <br><br></td></tr></table><br>
<a name="820336"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm not 100% sure how blitz treats this. It may render that little bit quickier, but don't hold me to that.<br><br>As for the reasons for doing this: It's so you can get textures to blend with each other. If you create many surfaces, you can have each surface with it's own properties, but still be part of the same mesh.<br><br>You can change individual surfaces with the PaintSurface command.<br><br><a href="http://www.blitzbasic.com/b3ddocs/command.php?name=PaintSurface&amp;ref=3d_a-z" target="_blank">http://www.blitzbasic.com/b3ddocs/command.php?name=PaintSurface&amp;ref=3d_a-z</a><br><br>You will need to create a brush first though.<br><br>TBH, i can't think of too many instances when creating another surface would be more useful, except for certain texturing purposes (using vertexalpha on a surface, to see through to the surface below, thus creating a nice texture blend, useful for say, terrains)<br><br>Just use another entity. It's easier to rotate and move and scale. <br><br></td></tr></table><br>
<a name="820339"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dreamora</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> No it is not the same as 2 meshes.<br><br>If a single part of a mesh is in view, the whole mesh is sent to the GPU.<br>no mather if only surface 1 of 50 surfaces is in view or not. <br><br></td></tr></table><br>
<a name="820343"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Good point. I'm rusty on this stuff now... Feel old. <br><br></td></tr></table><br>
<a name="820355"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >PowerPC603</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah ok, so it's better to use multiple meshes with only 1 surface.<br>I'm thinking about making a game where you can drive your vehicle (car, truck, bus, taxi, ...) through a city.<br>The entire map will be tile-based and needs to be created on-the-fly as you drive through the map.<br>Several tiles around you (withing range of sight) need to be created while playing.<br>I cannot have the map modelled in a 3D-package, as you need to have the ability to build new factories, create extra roads, tunnels and bridges (all in 3D).<br><br>If it was just a game like GTA, then I could divide the entire map into tiles and load them as you go, but now the game needs to check several things.<br>If there is a tunnel-entrance on a certain tile, that tile may not be created as part of the terrain, as it would block the tunnel-entrance for example.<br>When you add a road-tile next to a straight road, that straight road needs to be converted in a T-junction, if you know what I mean.<br><br>So my idea was to have tiles (which are 10x10 Blitz Units) and segments (which are 10x10 tiles).<br>A road-piece is just 1x1 tile, and a building can be 3x3 tiles.<br>A segment is one big mesh of 100x100 Blitz Units and is created based on the data for each tile which is in that segment.<br>I need this, because when too many tiles are created seperately (I had a range of sight of 250 Units around you), then I had 2500 seperate meshes, which slows down severely.<br>Now I only have 25 segments, which speeds up the rendering.<br><br>I create a mesh (segment) and create all vertices and triangles of each tile inside that segment and add it to that segment-mesh (all into one surface).<br><br>I want to do this like that, because I hate loading-screens, so the game must dynamically load everything in range (and unload everything outside this range).<br><br>It's a game like GTA, where you can drive through a city and do missions, but you can also build factories, buy vehicles and set them to work to transport the goods from one factory to another.<br>The game must process every vehicle on the map, even when not in range (like X2: The Threat and X3: Reunion). <br><br></td></tr></table><br>
<a name="820360"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> There's nothing wrong with you doing the tiles within the one surface. Just assign different texture co-ords. For instance split the texture up into different things, such as a road part, grass... etc etc, and assign the tiles to different parts of the same texture.<br><br>There is a balancing act in this though. Try and keep the surface count low-ish. The more surfaces (Meshes basically), the slower things will run. If you create your tiles out of the one surface, it will get rendered quicker.<br><br>I would suggest processing the cars within a certain range. Probably wouldn't be a great idea keeping tracking moving, checking collisions... etc, on everything in the city. The way GTA does it, is it keeps track of cars and people within a certain area. If they go out this area, they disappear.<br><br>Pretty ambitious project :o) <br><br></td></tr></table><br>
<a name="820534"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >PowerPC603</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes, I did a test where each tile is a seperate mesh, so I ended up with several thousand meshes, that the reason why I'm now joining 100 tiles into one bigger mesh, to reduce surface-count.<br><br>I experimented with this dynamic loading and creation of meshes and it works pretty well.<br>I also have a field for each tile which states what surface to use for this tile (grass, concrete, sand, rock, ...).<br>I've created a texture which has all these parts in it (just a square of green pixels for now for grass) and while creating the vertices, I set the UV-coords to take that part of the texture and that works too.<br><br>Now I only need to review my entire structure for holding all data.<br>I had an array Map.Tile(1000,1000), where every index was filled with a Tile type-instance.<br>I've calculated that this array is 4Mb in size (1000x1000x4), as each index is a integer (4 bytes).<br>Then every tile holds 3 fields (and 2 invisible ones: prev and next, created by Blitz I presume, is this correct?).<br>This gives me a type-instance of 20 bytes, so the total memory-usage for the map is about 80Mb.<br>Now I've been thinking of using banks.<br>Each field of a tile only needs values from 0 to 255, so bytes will suffice.<br>So a bank of 1000x1000x3 (map = 1000x1000 and 3 bytes per tile) = 3Mb.<br>This is 26 times smaller than using types and holds the same amount of info.<br>And this way I can load my Terrain.dat file directly into a bank, without the need for extra processing when loading the game.<br><br>The way cars will be processed is not using collisions and stuff like that.<br>Once the car is out of range, the 3D-model disappears and I only process their coords mathematically.<br>So a certain car is on tile 128x145 (world-coord x=1280, z=1450) and is on it's way to tile 128x146 at a speed of 75kph for example.<br>I only keep track of their X and Z coords and each frame I update their coords based on their speed.<br>I also check on which tile they are currently, so they can check if they need to take another direction when they come on a T-junction.<br>When planning a route from one factory to another, the pathfinding will be done only once and will be attached to the car's type-instance.<br>It's just a linked-list of types that define which tiles must be passed to reach their destination.<br>The cars, truck, boats, ... need to transport goods from one location to another.<br>If they would just disappear into thin air, no goods will ever reach their destination, so I have to keep track of everything.<br><br>I realize this is a big project, but I like to play GTA and miss things like building your own factories, which can make additional money. In GTA you can buy (or be rewarded) certain buildings which produce some cash, but it's not the same as an entire trading-empire.<br>And this way I can learn very much about 3D-programming.<br><br>X2 and X3 (games released by Egosoft) do this too.<br>You're flying in one part of space, while the game keeps track of all the ships and factories, even when they are in a different part of space.<br>Outside your sector, there is no collision checking, the game only needs to know about current ship positions,  speed etc.<br><br>You can also see my game as a 3D-version of transport tycoon, if anyone remembers this game.<br>This game also keeps track of all trains, boats, trucks, planes, ... even when not in view (in range).<br>You can also build your own factories there.<br>But my game will add GTA-like missions and a storyline (to which I've not payed attention yet).<br><br>This is some testing code to create a segment and apply the terrain-texture on it:<br><pre class=code>
Const MapX = 500, MapZ = 500 ; Size of the map in tiles
Const HMul = 3 ; Height multiplier for mesh-creation
Const COLTERRAIN = 1
Const COLCAMERA = 2

Graphics3D 800, 600, 0, 2
AmbientLight 255, 255, 255

; Create an array that holds all tiles of the entire map
Dim Map.Tile(MapX, MapZ)
; Also create an array that holds all segments of the entire map (a segment is 10x10 tiles)
Dim LMap.Segment((MapX/10), (MapZ/10))
; Also create an array that holds True if there is a road present on a certain tile
Dim Roads(MapX, MapZ)



; Create a new player
Global Player.TPlayer = New TPlayer
Player\X = 1280
Player\Y = 20
Player\Z = 1280
Player\TileX = Player\X / 10
Player\TileZ = Player\Z / 10
Player\SegX = Player\TileX / 10
Player\SegZ = Player\TileZ / 10
Player\Obj = CreateCamera()

Type Tile
	Field Y ; Holds the height-location of the tile (in steps of "HMul" BlitzUnits)
	Field TerrainType ; Terrain-type model
	Field TerrTex ; Type of terrain (0 = Concrete, 1 = Water, 2 = Grass, 3 = Sand)
End Type

Type Segment
	Field Obj ; A pointer to the 3D-model
	Field TerrainObj ; A pointer to the 3D-model of the terrain
	Field RoadObj ; A pointer to the 3D-model of the roads
End Type

Type TPlayer
	Field TileX ; The tile where the player is located in the X-direction
	Field TileZ ; The tile where the player is located in the Z-direction
	Field SegX ; The segment where the player is located in the X-direction
	Field SegZ ; The segment where the player is located in the Z-direction
	Field X ; The global X-direction of the player in the world
	Field Y ; The global Y-direction of the player in the world
	Field Z ; The global Z-direction of the player in the world
	Field Obj ; Holds a pointer to the player's 3D-object
End Type

; Include all source-code
Include "DynamicLoading.bb"



; Fill the map-array
For z = 0 To MapZ-1
	For x = 0 To MapX-1
		; Create a new tile
		Map(x,z) = New Tile
		; Set the entire terrain to be flat
		Map(x,z)\TerrainType = 1
	Next
Next

For z = 0 To (MapZ-1)/10
	For x = 0 To (MapX-1)/10
		LMap(x,z) = New Segment
	Next
Next


; Create a hill in the middle of flat terrain
Map(24,28)\TerrainType = 7
Map(25,28)\TerrainType = 4
Map(26,28)\TerrainType = 4
Map(27,28)\TerrainType = 4
Map(28,28)\TerrainType = 8

Map(24,27)\TerrainType = 3
Map(25,27)\TerrainType = 7
Map(26,27)\TerrainType = 4
Map(27,27)\TerrainType = 8
Map(28,27)\TerrainType = 5

Map(24,26)\TerrainType = 3
Map(25,26)\TerrainType = 3
Map(26,26)\TerrainType = 1
Map(27,26)\TerrainType = 5
Map(28,26)\TerrainType = 5

Map(24,25)\TerrainType = 3
Map(25,25)\TerrainType = 6
Map(26,25)\TerrainType = 2
Map(27,25)\TerrainType = 9
Map(28,25)\TerrainType = 5

Map(24,24)\TerrainType = 6
Map(25,24)\TerrainType = 2
Map(26,24)\TerrainType = 2
Map(27,24)\TerrainType = 2
Map(28,24)\TerrainType = 9

Map(25, 27)\Y = 1
Map(25, 27)\TerrTex = 3
Map(26, 27)\Y = 1
Map(26, 27)\TerrTex = 3
Map(27, 27)\Y = 1
Map(27, 27)\TerrTex = 3

Map(25, 26)\Y = 1
Map(25, 26)\TerrTex = 3
Map(26, 26)\Y = 2
Map(26, 26)\TerrTex = 2
Map(27, 26)\Y = 1
Map(27, 26)\TerrTex = 3

Map(25, 25)\Y = 1
Map(25, 25)\TerrTex = 3
Map(26, 25)\Y = 1
Map(26, 25)\TerrTex = 3
Map(27, 25)\Y = 1
Map(27, 25)\TerrTex = 3



Texture = LoadTexture("Textures\Terrain.bmp")
SetBuffer BackBuffer()
CreateTerrainSegment(2, 2)
EntityTexture LMap(2,2)\Obj, Texture
EntityType LMap(2,2)\Obj, COLTERRAIN


PositionEntity Player\Obj, 250, 20, 250
EntityType Player\Obj, COLCAMERA
EntityRadius Player\Obj, 3

Collisions COLCAMERA, COLTERRAIN, 2, 3



While Not KeyHit(1)
	MoveEntity Player\Obj, 0, -0.1, 0

	If KeyDown(200) Then MoveEntity Player\Obj, 0, 0, 0.1
	If KeyDown(208) Then MoveEntity Player\Obj, 0, 0, -0.1

	If KeyDown(203) Then TurnEntity Player\Obj, 0, 0.5, 0
	If KeyDown(205) Then TurnEntity Player\Obj, 0, -0.5, 0

	UpdateWorld
	RenderWorld
	VWait:Flip False
Wend

End
</pre><br>File DynamicLoading.bb:<br><pre class=code>
Function CreateTerrainSegment(SegX, SegZ)
	Local mesh = CreateMesh()
	Local surf = CreateSurface(mesh)

	PositionMesh mesh, (SegX*100)+50, 0, (SegZ*100)+50

	TileXMin = (SegX*10) + 1
	TileXMax = (SegX*10) + 10
	TileZMin = (SegZ*10) + 1
	TileZMax = (SegZ*10) + 10

	For TileZ = TileZMin To TileZMax
		For TileX = TileXMin To TileXMax
			CreateTerrainTile(surf, TileX, TileZ)
		Next
	Next

	; Store the reference to the segment-model in the LMap-array
	LMap(SegX, SegZ)\Obj = mesh
End Function

Function CreateTerrainTile(surf, TileX, TileZ)
	; This routine adds the required vertices and triangles of the required tile to the given surface, adding the tile to the segment's mesh
	Select Map(TileX, TileZ)\TerrainType
		Case 1 ; Flat tile
			v0 = AddVertex(surf, (TileX*10)-5, (Map(TileX,TileZ)\Y*HMul), (TileZ*10)+5)
			v1 = AddVertex(surf, (TileX*10)+5, (Map(TileX,TileZ)\Y*HMul), (TileZ*10)+5)
			v2 = AddVertex(surf, (TileX*10)+5, (Map(TileX,TileZ)\Y*HMul), (TileZ*10)-5)
			v3 = AddVertex(surf, (TileX*10)-5, (Map(TileX,TileZ)\Y*HMul), (TileZ*10)-5)
			t0 = AddTriangle(surf, v0, v1, v3)
			t1 = AddTriangle(surf, v1, v2, v3)
		Case 2 ; Goes up to the top
			v0 = AddVertex(surf, (TileX*10)-5, (Map(TileX,TileZ)\Y*HMul)+HMul, (TileZ*10)+5)
			v1 = AddVertex(surf, (TileX*10)+5, (Map(TileX,TileZ)\Y*HMul)+HMul, (TileZ*10)+5)
			v2 = AddVertex(surf, (TileX*10)+5, (Map(TileX,TileZ)\Y*HMul), (TileZ*10)-5)
			v3 = AddVertex(surf, (TileX*10)-5, (Map(TileX,TileZ)\Y*HMul), (TileZ*10)-5)
			t0 = AddTriangle(surf, v0, v1, v3)
			t1 = AddTriangle(surf, v1, v2, v3)
		Case 3 ; Goes up to the right
			v0 = AddVertex(surf, (TileX*10)-5, (Map(TileX,TileZ)\Y*HMul), (TileZ*10)+5)
			v1 = AddVertex(surf, (TileX*10)+5, (Map(TileX,TileZ)\Y*HMul)+HMul, (TileZ*10)+5)
			v2 = AddVertex(surf, (TileX*10)+5, (Map(TileX,TileZ)\Y*HMul)+HMul, (TileZ*10)-5)
			v3 = AddVertex(surf, (TileX*10)-5, (Map(TileX,TileZ)\Y*HMul), (TileZ*10)-5)
			t0 = AddTriangle(surf, v0, v1, v3)
			t1 = AddTriangle(surf, v1, v2, v3)
		Case 4 ; Goes up to the bottom
			v0 = AddVertex(surf, (TileX*10)-5, (Map(TileX,TileZ)\Y*HMul), (TileZ*10)+5)
			v1 = AddVertex(surf, (TileX*10)+5, (Map(TileX,TileZ)\Y*HMul), (TileZ*10)+5)
			v2 = AddVertex(surf, (TileX*10)+5, (Map(TileX,TileZ)\Y*HMul)+HMul, (TileZ*10)-5)
			v3 = AddVertex(surf, (TileX*10)-5, (Map(TileX,TileZ)\Y*HMul)+HMul, (TileZ*10)-5)
			t0 = AddTriangle(surf, v0, v1, v3)
			t1 = AddTriangle(surf, v1, v2, v3)
		Case 5 ; Goes up to the left
			v0 = AddVertex(surf, (TileX*10)-5, (Map(TileX,TileZ)\Y*HMul)+HMul, (TileZ*10)+5)
			v1 = AddVertex(surf, (TileX*10)+5, (Map(TileX,TileZ)\Y*HMul), (TileZ*10)+5)
			v2 = AddVertex(surf, (TileX*10)+5, (Map(TileX,TileZ)\Y*HMul), (TileZ*10)-5)
			v3 = AddVertex(surf, (TileX*10)-5, (Map(TileX,TileZ)\Y*HMul)+HMul, (TileZ*10)-5)
			t0 = AddTriangle(surf, v0, v1, v3)
			t1 = AddTriangle(surf, v1, v2, v3)
		Case 6 ; Corner-up to the top-right
			v0 = AddVertex(surf, (TileX*10)-5, (Map(TileX,TileZ)\Y*HMul), (TileZ*10)+5)
			v1 = AddVertex(surf, (TileX*10)+5, (Map(TileX,TileZ)\Y*HMul)+HMul, (TileZ*10)+5)
			v2 = AddVertex(surf, (TileX*10)+5, (Map(TileX,TileZ)\Y*HMul), (TileZ*10)-5)
			v3 = AddVertex(surf, (TileX*10)-5, (Map(TileX,TileZ)\Y*HMul), (TileZ*10)-5)
			t0 = AddTriangle(surf, v0, v1, v3)
			t1 = AddTriangle(surf, v1, v2, v3)
;			t0 = AddTriangle(surf, v0, v2, v3)
;			t1 = AddTriangle(surf, v0, v1, v2)
		Case 7 ; Corner-up to the right-bottom
			v0 = AddVertex(surf, (TileX*10)-5, (Map(TileX,TileZ)\Y*HMul), (TileZ*10)+5)
			v1 = AddVertex(surf, (TileX*10)+5, (Map(TileX,TileZ)\Y*HMul), (TileZ*10)+5)
			v2 = AddVertex(surf, (TileX*10)+5, (Map(TileX,TileZ)\Y*HMul)+HMul, (TileZ*10)-5)
			v3 = AddVertex(surf, (TileX*10)-5, (Map(TileX,TileZ)\Y*HMul), (TileZ*10)-5)
			t0 = AddTriangle(surf, v0, v1, v3)
			t1 = AddTriangle(surf, v1, v2, v3)
		Case 8 ; Corner-up to the left-bottom
			v0 = AddVertex(surf, (TileX*10)-5, (Map(TileX,TileZ)\Y*HMul), (TileZ*10)+5)
			v1 = AddVertex(surf, (TileX*10)+5, (Map(TileX,TileZ)\Y*HMul), (TileZ*10)+5)
			v2 = AddVertex(surf, (TileX*10)+5, (Map(TileX,TileZ)\Y*HMul), (TileZ*10)-5)
			v3 = AddVertex(surf, (TileX*10)-5, (Map(TileX,TileZ)\Y*HMul)+HMul, (TileZ*10)-5)
			t0 = AddTriangle(surf, v0, v2, v3)
			t1 = AddTriangle(surf, v0, v1, v2)
		Case 9 ; Corner up to the top-left
			v0 = AddVertex(surf, (TileX*10)-5, (Map(TileX,TileZ)\Y*HMul)+HMul, (TileZ*10)+5)
			v1 = AddVertex(surf, (TileX*10)+5, (Map(TileX,TileZ)\Y*HMul), (TileZ*10)+5)
			v2 = AddVertex(surf, (TileX*10)+5, (Map(TileX,TileZ)\Y*HMul), (TileZ*10)-5)
			v3 = AddVertex(surf, (TileX*10)-5, (Map(TileX,TileZ)\Y*HMul), (TileZ*10)-5)
			t0 = AddTriangle(surf, v0, v1, v3)
			t1 = AddTriangle(surf, v1, v2, v3)
	End Select

	Select Map(TileX, TileZ)\TerrTex
		Case 0 ; Concrete
			VertexTexCoords surf, v0, 0.0, 0.0
			VertexTexCoords surf, v1, 0.497, 0.0
			VertexTexCoords surf, v2, 0.497, 0.499
			VertexTexCoords surf, v3, 0.0, 0.499
		Case 1 ; Water
			VertexTexCoords surf, v0, 0.499, 0.0
			VertexTexCoords surf, v1, 1.0, 0.0
			VertexTexCoords surf, v2, 1.0, 0.499
			VertexTexCoords surf, v3, 0.499, 0.499
		Case 2 ; Grass
			VertexTexCoords surf, v0, 0.0, 0.501
			VertexTexCoords surf, v1, 0.497, 0.501
			VertexTexCoords surf, v2, 0.497, 1.0
			VertexTexCoords surf, v3, 0.0, 1.0
		Case 3 ; Sand
			VertexTexCoords surf, v0, 0.499, 0.501
			VertexTexCoords surf, v1, 1.0, 0.501
			VertexTexCoords surf, v2, 1.0, 1.0
			VertexTexCoords surf, v3, 0.499, 1.0
	End Select
End Function
</pre><br><br>And this is the terrain texture I've used:<br><a href="http://users.telenet.be/vge/downloads/Terrain.bmp" target="_blank">http://users.telenet.be/vge/downloads/Terrain.bmp</a> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
