<!DOCTYPE html><html lang="en" ><head ><title >reading pixel color data to an array</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >reading pixel color data to an array</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >reading pixel color data to an array</a><br><br>
<a name="1038137"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AvestheFox</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Alright, so my goal here is to create a tile graphics editor that would save the image data into a basic array, ex:<br><br><pre class=code>
.tilegfx
data 1,1,1,1,1,1,1,1
data 1,0,0,0,0,0,0,1
data 1,0,5,1,1,1,0,1
data 1,0,1,5,1,1,0,1
data 1,0,1,1,5,1,0,1
data 1,0,1,1,1,5,0,1
data 1,0,0,0,0,0,0,1
data 1,1,1,1,1,1,1,1
</pre><br><br>Which then could be read into the program like this:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
restore tilegfx
for y=1 to 8
for x=1 to 8
	read pixel
	if pixel=0 then color   0,  0,  0 : plot x-1,y-1 ; transparent
	if pixel=1 then color   0,  0,  1 : plot x-1,y-1
	if pixel=2 then color  83, 83, 83 : plot x-1,y-1
	if pixel=3 then color 119,119,119 : plot x-1,y-1
	if pixel=4 then color 151,151,151 : plot x-1,y-1
	if pixel=5 then color 186,186,186 : plot x-1,y-1
	if pixel=6 then color 255,255,255 : plot x-1,y-1
next
next
</textarea><br><br>At the moment, I have the basic editor functions down pat (except for loading and saving, which will come later). But the problem I'm having is this:<br><br><img src="http://i514.photobucket.com/albums/t346/PutterWatch/halpme.jpg"><br><br>as the image describes, I'm having a really rough time getting the pixel grid to read the pixel rgb data from the selected tile.<br><br>Here's how I have my program set up:<br><br>globals and types:<br><pre class=code>
Global ms_x#, ms_y#
Global curr_color
Global curr_tile=0

Type pal
	Field x,y
	Field img,col
End Type

Type pix
	Field x,y
	Field px,py
	Field img,col
	Field red,green,blue
End Type

Type tile
	Field x,y
	Field img,frame    
End Type
</pre><br><br>setting up the graphics:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics 640,480,16,2

Global mouse=CreateImage(1,1)

Global tile=CreateImage(16,16,256) ; our tileset that we'll be editing.

Global tile_grid=CreateImage(16,16)
	SetBuffer ImageBuffer(tile_grid) : Color 50,50,50 : Rect 0,0,16,16,0

Global grid=CreateImage(8,8) ; the grid behind the pixels. makes it easier for editing.
	SetBuffer ImageBuffer(grid) : Color 50,50,50 : Rect 0,0,8,8,0

Global curs=CreateImage(8,8) ; the pixel editor cursor
	SetBuffer ImageBuffer(curs) : Color 0,255,255 : Rect 0,0,8,8,0

Global sel=CreateImage(16,16) ; the color selection cursor (and tile selector)
	SetBuffer ImageBuffer(sel) : Color 0,255,255 : Rect 0,0,16,16,0

Global curr=CreateImage(16,16) ; this cursor shows the current selected pal color
	SetBuffer ImageBuffer(curr) : Color 255,0,0 : Rect 0,0,16,16,0

Global pal=CreateImage(16,16,7) ; the color pallete.
	SetBuffer ImageBuffer(pal,0) : Color   0,  0,  0 : Rect 0,0,16,16,1 ; transparent
	SetBuffer ImageBuffer(pal,1) : Color   0,  0,  1 : Rect 0,0,16,16,1
	SetBuffer ImageBuffer(pal,2) : Color  83, 83, 83 : Rect 0,0,16,16,1
	SetBuffer ImageBuffer(pal,3) : Color 119,119,119 : Rect 0,0,16,16,1
	SetBuffer ImageBuffer(pal,4) : Color 151,151,151 : Rect 0,0,16,16,1
	SetBuffer ImageBuffer(pal,5) : Color 186,186,186 : Rect 0,0,16,16,1
	SetBuffer ImageBuffer(pal,6) : Color 255,255,255 : Rect 0,0,16,16,1

Global pix=CreateImage(8,8,7) ; the pixel grid for the tile.
	SetBuffer ImageBuffer(pix,0) : Color   0,  0,  0 : Rect 0,0,8,8,1 ; transparent
	SetBuffer ImageBuffer(pix,1) : Color   0,  0,  1 : Rect 0,0,8,8,1
	SetBuffer ImageBuffer(pix,2) : Color  83, 83, 83 : Rect 0,0,8,8,1
	SetBuffer ImageBuffer(pix,3) : Color 119,119,119 : Rect 0,0,8,8,1
	SetBuffer ImageBuffer(pix,4) : Color 151,151,151 : Rect 0,0,8,8,1
	SetBuffer ImageBuffer(pix,5) : Color 186,186,186 : Rect 0,0,8,8,1
	SetBuffer ImageBuffer(pix,6) : Color 255,255,255 : Rect 0,0,8,8,1

Global current=CreateImage(16,16,7) ; the current color viewer.
	SetBuffer ImageBuffer(current,0) : Color   0,  0,  0 : Rect 0,0,16,16,1 ; transparent
	SetBuffer ImageBuffer(current,1) : Color   0,  0,  1 : Rect 0,0,16,16,1
	SetBuffer ImageBuffer(current,2) : Color  83, 83, 83 : Rect 0,0,16,16,1
	SetBuffer ImageBuffer(current,3) : Color 119,119,119 : Rect 0,0,16,16,1
	SetBuffer ImageBuffer(current,4) : Color 151,151,151 : Rect 0,0,16,16,1
	SetBuffer ImageBuffer(current,5) : Color 186,186,186 : Rect 0,0,16,16,1
	SetBuffer ImageBuffer(current,6) : Color 255,255,255 : Rect 0,0,16,16,1

SetBuffer BackBuffer()

</textarea><br><br>Main Loop:<br><pre class=code>
setup_pallete
setup_pixels
setup_tiles

While Not KeyHit(1)

ms_x=MouseX() : ms_y=MouseY()

Cls

DrawImage mouse,ms_x,ms_y
draw_pallete(152,16)
draw_pixels(16,16)
draw_tiles(175,16)
Color 0,0,175 : Rect 152,152,16,16
DrawImage current,152,152,curr_color
Color 255,255,255 : Rect 152,152,16,16,0
If KeyHit(57) Then write_pixels
Flip
Wend
End

</pre><br><br>Setting up the palette and the grids:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Function setup_pallete()
 	For y=1 To 7
  		pa.pal=New pal
 		pa\x=0 : pa\y=y*16-16
		pa\img=pal
		pa\col=y-1
	Next
End Function

Function setup_pixels()
	For y=1 To 16
	For x=1 To 16
		px.pix=New pix
		px\x=x*8-8 : px\y=y*8-8
		px\img=pix
	Next
	Next
End Function

Function setup_tiles()
	For y=1 To 16
	For x=1 To 16
		t.tile=New tile
		t\x=x*16-16 : t\y=y*16-16
		If count&lt;256 Then count=count+1
		t\img=tile : t\frame=count-1
	Next
	Next
End Function
</textarea><br><br>Drawing the palette and the grids:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Function draw_pallete(x,y)
	Color 0,0,150 : Rect x,y,16,16
	For pa.pal=Each pal
		DrawImage pa\img,pa\x+x,pa\y+y,pa\col
		Color 100,100,100 : Rect pa\x+x,pa\y+y,16,16,0
		If ImagesOverlap(mouse,ms_x,ms_y,pa\img,pa\x+x,pa\y+y) 
			DrawImage sel,pa\x+x,pa\y+y
			If MouseDown(1) Then curr_color=pa\col
		End If 
		If curr_color=pa\col Then DrawImage curr,pa\x+x,pa\y+y
	Next 
End Function

Function draw_pixels(x,y)
	Color 0,0,150 : Rect x,y,128,128
	For px.pix=Each pix
		DrawImage px\img,px\x+x,px\y+y,px\col
		DrawImage grid,px\x+x,px\y+y
		If ImagesOverlap(mouse,ms_x,ms_y,px\img,px\x+x,px\y+y)
			DrawImage curs,px\x+x,px\y+y
			If MouseDown(1) Then px\col=curr_color
			If MouseDown(2) Then curr_color=px\col
		End If 
	Next 
End Function 

Function draw_tiles(x,y)
	For t.tile=Each tile
		DrawImage tile_grid,t\x+x,t\y+y
		DrawImage t\img,t\x+x,t\y+y,t\frame
		If ImagesOverlap(mouse,ms_x,ms_y,t\img,t\x+x,t\y+y) 
			DrawImage sel,t\x+x,t\y+y
			If MouseDown(1) Then curr_tile=t\frame : read_pixels
		End If 
		If curr_tile=t\frame Then DrawImage curr,t\x+x,t\y+y
	Next 
End Function 
</textarea><br><br>And now the functions used to read and write the rgb values of the tile's pixel data:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Function read_pixels()
	SetBuffer ImageBuffer(tile,curr_tile)
		For px.pix=Each pix
			For x=1 To 16
			For y=1 To 16
				px\px=x : px\py=y
				GetColor px\px,px\py
				px\red   = ColorRed()
				px\green = ColorGreen()
				px\blue  = ColorBlue()
				If px\Red=  0 And px\Green=  0 And px\Blue=  0 Then px\col=0
				If px\Red=  0 And px\Green=  0 And px\Blue=  1 Then px\col=1
				If px\Red= 83 And px\Green= 83 And px\Blue= 83 Then px\col=2
				If px\Red=119 And px\Green=119 And px\Blue=119 Then px\col=3
				If px\Red=151 And px\Green=151 And px\Blue=151 Then px\col=4
				If px\Red=186 And px\Green=186 And px\Blue=186 Then px\col=5
				If px\Red=255 And px\Green=255 And px\Blue=255 Then px\col=6 
			Next
			Next
		Next
SetBuffer BackBuffer()
End Function 


Function write_pixels()
	For px.pix=Each pix
		SetBuffer ImageBuffer(tile,curr_tile)
		For y=1 To 16
		For x=1 To 16
			If px\col=0 Then Color   0,  0,  0 : Plot px\x/8,px\y/8 ; transparent
			If px\col=1 Then Color   0,  0,  1 : Plot px\x/8,px\y/8
			If px\col=2 Then Color  83, 83, 83 : Plot px\x/8,px\y/8
			If px\col=3 Then Color 119,119,119 : Plot px\x/8,px\y/8
			If px\col=4 Then Color 151,151,151 : Plot px\x/8,px\y/8
			If px\col=5 Then Color 186,186,186 : Plot px\x/8,px\y/8
			If px\col=6 Then Color 255,255,255 : Plot px\x/8,px\y/8
		Next
		Next
	Next
	SetBuffer BackBuffer()
End Function 

</textarea><br><br>What works and what doesn't seem to work:<br><br>if you study the code carefully you'll note that the grid editor is made up of 8x8 tiles with 7 frames (each frame represents a shade of gray from the palette) when the user presses spacebar, the tile data is transfered to the current tile and plotted as pixels (see function write_pixel() )<br><br>But when the user selects a tile from the tileset grid, the pixel rgb data needs to be sent to the editor grid which accordingly to that rgb data, the "pixel" is set. (see function read_pixel() )<br><br>This does not seem to work at all, and the problem seems to be something with the read_pixel function which is supposed to call this action into play.<br><br>the read pixel function is called like this from the draw_tiles(x,y) function :<br><br><pre class=code>
Function draw_tiles(x,y)
	For t.tile=Each tile
		DrawImage tile_grid,t\x+x,t\y+y
		DrawImage t\img,t\x+x,t\y+y,t\frame
		If ImagesOverlap(mouse,ms_x,ms_y,t\img,t\x+x,t\y+y) 
			DrawImage sel,t\x+x,t\y+y
			If MouseDown(1) Then curr_tile=t\frame : read_pixels
		End If 
		If curr_tile=t\frame Then DrawImage curr,t\x+x,t\y+y
	Next 
End Function 

</pre><br><br>this is the best I can do in describing my problem... if any further information is needed in helping me then feel free to ask.<br><br> Thanks in advance! <br><br></td></tr></table><br>
<a name="1038149"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> You are using 16-bit graphics and looking for RGB values which cannot possibly exist, such as red=119.<br><br>The right way to determine the color values is to set the graphics mode, plot a color with a red value of 119 and then read it back to see what it really is. It will probably be something like 116.<br><br><br>NOTE: I just tried this. R,G,B values of 119 read back as 112, 116, 112.<br><br>I thought 16-bit color was no longer available on my PC, but it is. <br><br></td></tr></table><br>
<a name="1038151"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> And an afterthought, be sure this is done every time the program runs. Don't do a test like I just did and then save the values 112, 116, 112 in the source code. There is no guarantee they will alway be the same on other machines. <br><br></td></tr></table><br>
<a name="1038154"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AvestheFox</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> what's the test code that your using?<br><br>I tried it with this:<br><br><pre class=code>
GetColor(ms_x,ms_y) ; get color at mouse x and y coords
Text 0,0,"r: "+ColorRed()+" g: "+ColorBlue()+" b: "+ColorGreen()
</pre><br><br>and the color values came up correct on both 16 and 32 bit mode <br><br></td></tr></table><br>
<a name="1038155"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> You're in luck, I hadn't even closed the IDE.<br><br><pre class=code>
Graphics 1280, 1024, 16, 0

Color 119,119,119
Plot 0,0
GetColor 0,0
Print ColorRed()
Print ColorGreen()
Print ColorBlue()
WaitKey</pre><br><br>This is 16-bit, full screen. If you can read back 119 then you are not really in 16-bit mode. That would be the case with windowed mode and 32-bit desktop graphics. If that is what you are running then my claim that values like 119 are unavailable does not apply. The problem must then be something else. <br><br></td></tr></table><br>
<a name="1038156"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AvestheFox</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> thanks..<br><br>but strange, its always returning 119,119,119<br><br>again, in both modes<br><br>edit: just saw the explanation below your code... so it must be something else :P <br><br></td></tr></table><br>
<a name="1038158"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AvestheFox</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> okay, tested it in full screen mode. the values came up different.. so that solves that bit <br><br></td></tr></table><br>
<a name="1038162"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AvestheFox</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> unfortunately that still does not seem to solve my problem here.. :(<br><br>I still appreciate your input here, Floyd, I learned something from it :) but I still cant get the program to do what I want... <br><br></td></tr></table><br>
<a name="1038339"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Serpent</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hang on, I can't really understand your problem - well I might.  Can you please confirm this or explain where I'm wrong:<br><br>- Your main editor is tile based.<br>- You have a pixel editor which lets you edit each tile.<br>- The problem is transferring the data between the tile editor and the pixel editor.<br><br>Is this right? <br><br></td></tr></table><br>
<a name="1038388"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AvestheFox</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> correct<br><br>I cant get the pixel editor to change when the user clicks on a different tile <br><br></td></tr></table><br>
<a name="1038395"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Serpent</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Okay, I'll start looking at your code. <br><br></td></tr></table><br>
<a name="1038405"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Serpent</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Found the problem - and another unnecessary bit of code.<br><br>At the start, you initialise your 16x16 set of pixels in setup_pixels().  Then in your two pixel changing functions (read_pixels and write_pixels) you have unnecessary for loops which are causing the problems.<br><br>read_pixels()<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Function read_pixels()
	SetBuffer ImageBuffer(tile,curr_tile)
		For px.pix=Each pix
			For x=1 To 16
			For y=1 To 16
				px\px=x : px\py=y
				GetColor px\px,px\py
				px\red   = ColorRed()
				px\green = ColorGreen()
				px\blue  = ColorBlue()
				If px\Red=  0 And px\Green=  0 And px\Blue=  0 Then px\col=0
				If px\Red=  0 And px\Green=  0 And px\Blue=  1 Then px\col=1
				If px\Red= 83 And px\Green= 83 And px\Blue= 83 Then px\col=2
				If px\Red=119 And px\Green=119 And px\Blue=119 Then px\col=3
				If px\Red=151 And px\Green=151 And px\Blue=151 Then px\col=4
				If px\Red=186 And px\Green=186 And px\Blue=186 Then px\col=5
				If px\Red=255 And px\Green=255 And px\Blue=255 Then px\col=6 
			Next
			Next
		Next
SetBuffer BackBuffer()
End Function </textarea><br><br>See how you have the two for loops here (i.e. x = 1 to 16 and y = 1 to 16)?  This means that for each individual pixel, your program will go through every pixel in the tile.  You don't need these loops because the for-each loop will go through each pixel.  Essentially, because of these loops, each pixel would be set to the value of {16,16} in the tile.<br>In fact, this was another problem.  When indexing pixels like this in Blitz, the first pixel is 0, not 1.  So, the pixels actually range from {0,0} to {15,15}.  When you call GetPixel on {16,16}, which is the last thing that happens before your loops move on, it won't find anything because pixel {16,16} doesn't exist!  That's why all of your pixels were being set to 'Transparent' - the RGB values from the non-existent pixel would come out to 0, 0, and 0.<br><br>This is what was causing your program 'not' to read tiles.  After taking out the two inner loops, px and py will be useless.  There are two ways you can resolve this.  The first is to use a similar method to your 'write_pixels' function:<br><pre class=code>GetColor px\x / 8, px\y / 8</pre><br>Alternatively, in your initialisation loop, you can set the px and py values of each pixel and use these.  This will save on divisions, but this doesn't really matter anyway.<br><br>Working read_pixels() function:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Function read_pixels()
	SetBuffer ImageBuffer(tile,curr_tile)
	For px.pix=Each pix
		GetColor px\x / 8,px\y / 8
		px\red   = ColorRed()
		px\green = ColorGreen()
		px\blue  = ColorBlue()
		If px\Red=  0 And px\Green=  0 And px\Blue=  0 Then px\col=0
		If px\Red=  0 And px\Green=  0 And px\Blue=  1 Then px\col=1
		If px\Red= 83 And px\Green= 83 And px\Blue= 83 Then px\col=2
		If px\Red=119 And px\Green=119 And px\Blue=119 Then px\col=3
		If px\Red=151 And px\Green=151 And px\Blue=151 Then px\col=4
		If px\Red=186 And px\Green=186 And px\Blue=186 Then px\col=5
		If px\Red=255 And px\Green=255 And px\Blue=255 Then px\col=6 
	Next
	SetBuffer BackBuffer()
End Function</textarea><br><br><br>Now, in your 'write_pixels' function:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Function write_pixels()
	For px.pix=Each pix
		SetBuffer ImageBuffer(tile,curr_tile)
		For y=1 To 16
		For x=1 To 16
			If px\col=0 Then Color   0,  0,  0 : Plot px\x/8,px\y/8 ; transparent
			If px\col=1 Then Color   0,  0,  1 : Plot px\x/8,px\y/8
			If px\col=2 Then Color  83, 83, 83 : Plot px\x/8,px\y/8
			If px\col=3 Then Color 119,119,119 : Plot px\x/8,px\y/8
			If px\col=4 Then Color 151,151,151 : Plot px\x/8,px\y/8
			If px\col=5 Then Color 186,186,186 : Plot px\x/8,px\y/8
			If px\col=6 Then Color 255,255,255 : Plot px\x/8,px\y/8
		Next
		Next
	Next
	SetBuffer BackBuffer()
End Function</textarea><br>You also have two unnecessary for loops here.  These don't affect the results though - they just mean each pixel is written 256 times to the same spot...  So I've taken them out as well:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Function write_pixels()
	For px.pix=Each pix
		SetBuffer ImageBuffer(tile,curr_tile)
		If px\col=0 Then Color   0,  0,  0 : Plot px\x/8,px\y/8 ; transparent
		If px\col=1 Then Color   0,  0,  1 : Plot px\x/8,px\y/8
		If px\col=2 Then Color  83, 83, 83 : Plot px\x/8,px\y/8
		If px\col=3 Then Color 119,119,119 : Plot px\x/8,px\y/8
		If px\col=4 Then Color 151,151,151 : Plot px\x/8,px\y/8
		If px\col=5 Then Color 186,186,186 : Plot px\x/8,px\y/8
		If px\col=6 Then Color 255,255,255 : Plot px\x/8,px\y/8
	Next
	SetBuffer BackBuffer()
End Function</textarea><br><br>As far as I can see, everything is working fine now.  Be careful about putting in loops that shouldn't be there.  If you haven't understood my explanation, let me know and I'll try again. <br><br></td></tr></table><br>
<a name="1038411"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AvestheFox</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks a bunch, Serpant! :) I'll be looking over your explanation and changes when I get the time to do so. Your help here is greatly apreciated! :) <br><br></td></tr></table><br>
<a name="1038421"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AvestheFox</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> alright, so now I got the time to look over everything. It makes perfectly good sense about the for loops causing the problems so your explanation was a perfect ace :)<br><br>I guess I have a nasty habit of using these loops when it comes to programming :P<br><br>Again, thanks! :) if I make something with this, I'll credit you for your help ;) <br><br></td></tr></table><br>
<a name="1038464"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Adam Novagen</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> You are using 16-bit graphics and looking for RGB values which cannot possibly exist, such as red=119. <br></div><br>Not strictly true. In Blitz, if you're in 16-bit mode, it'll still return 24-bit colour values. However, it'll simply average them out. So in other words, it'll take the 16-bit colour of the pixel and return the actual 24-bit colour data. <br><br></td></tr></table><br>
<a name="1038501"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> And make sure your in full screen, as i don't believe blitz properly does 16 bit mode, in windowed mode. <br><br></td></tr></table><br>
<a name="1038541"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Adam Novagen</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> And make sure your in full screen, as i don't believe blitz properly does 16 bit mode, in windowed mode. <br></div><br>Yep, quite so. In windowed mode, Blitz is forced to render at whatever the Windows desktop colour depth is, and the colour depth parameter of <b>Graphics()</b> is overridden. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
