<!DOCTYPE html><html lang="en" ><head ><title >Type vs Array vs Bank to store datas and space tak</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Type vs Array vs Bank to store datas and space tak</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Type vs Array vs Bank to store datas and space tak</a><br><br>
<a name="1208966"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello, :)<br><br>I am doing some tests with types and dim arrays and banks to store datas and see how much space it takes in memory.<br><br>I want to store 100x100 heights (from 0 to 255) of 100x100 terrains.<br><br>Firstly, can you please tell me if this code is correct :<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D(640,480,32,2)

n% = 1000000

;Type
Type Ent
 Field Var%
End Type
For i% = 1 To n
 e.Ent = New Ent
Next

;Dim array
EntsMaxCount% = n
Global EntsCount% = 0
Dim EntVar%(EntsMaxCount)
For i% = 1 To n
 EntsCount = EntsCount + 1
 EId% = EntsCount
Next

;Bank
BankEntVarSize% = n*1
BankEntVar = CreateBank(BankEntVarSize)

ClsColor(000,000,000)
Cls()
Print("For "+Str(n)+" instances : ")

;Write in a slot of an instance of the Type
RoutineStart% = MilliSecs()
For e.Ent = Each Ent
 e\Var = Rand(0,255)
Next
RoutineTime% = MilliSecs() - RoutineStart
Print(RoutineTime+" millisecs to write (integers) in a type") 

;Write in a slot of the Dim Array
RoutineStart% = MilliSecs()
For EId% = 1 To EntsCount Step 1
 EntVar(EId) = Rand(0,255)
Next
RoutineTime% = MilliSecs() - RoutineStart
Print(RoutineTime+" millisecs to write (integers) in a dim array") 

;Write in a slot of the Bank
RoutineStart% = MilliSecs()
For o% = 0 To BankEntVarSize-1 Step 1
 PokeByte(BankEntVar,o,Rand(0,255))
Next
RoutineTime% = MilliSecs() - RoutineStart
Print(RoutineTime+" millisecs to write (bytes) in a bank")

;Read in a slot of an instance of the Type
RoutineStart% = MilliSecs()
For e.Ent = Each Ent
 TVar# = e\Var
Next
RoutineTime% = MilliSecs() - RoutineStart
Print(RoutineTime+" millisecs to read (integers) in a type") 

;Read in a slot of the Dim Array
RoutineStart% = MilliSecs()
For EId% = 1 To EntsCount Step 1
 TVar# = EntVar(EId)
Next
RoutineTime% = MilliSecs() - RoutineStart
Print(RoutineTime+" millisecs to read (integers) in a dim array") 

;Read in a slot of the Bank
RoutineStart% = MilliSecs()
For o% = 0 To BankEntVarSize-1 Step 1
 TVar# = PeekByte(BankEntVar,o)
Next
RoutineTime% = MilliSecs() - RoutineStart
Print(RoutineTime+" millisecs to read (bytes) in a bank")
 
FlushKeys()
WaitKey()
</textarea><br><br><br><br>Secondly, can you please tell me if my understanding of the space taken in memory is correct :<br><br>With types, i can't store a byte, so i have to store an integer, so the space taken in memory will be :<br>4x100x100x100x100 = 400 000 000 or 400mb<br><br>With dim arrays, i can't store a byte, so i have to store an integer, so the space taken in memory will be :<br>4x100x100x100x100 = 400 000 000 or 400mb<br><br>With banks, i can store a byte, so the space taken in memory will be :<br>1x100x100x100x100 = 100 000 000 or 100mb<br><br><br><br>Thanks, good day <br><br></td></tr></table><br>
<a name="1208984"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> You will have to be very careful with the memory management if you really intend to use this much in one go!<br><br>Identifying timing in this manner isn't always reliable, since the CPU may be busy with other processes, the memory may be more or less full, and of course, is largely system-dependent.<br><br>Your memory assumptions are correct, although bear in mind that Type objects may contain a little more information, as each object will have its own identity.<br><br>Typically, banks should always be much faster, since they operate directly with the memory heap and do not require excess translation - however, manipulating memory may take a modicum of extra calculations but on most systems nowadays that shouldn't be an issue.<br><br><pre class=code>
;DECLS
Const TERRAINS=10000
Const HEIGHTS=10000

Dim ARRAYS(0,0)

Type TYPES
	Field HEIGHTS
End Type

Global BANKS;=CreateBank(TERRAINS*HEIGHTS)

;EXAMPLE

Print Str(ArrayWriteTime())+" ms to populate integers in Array"
Print Str(ArrayReadTime())+" ms to retrieve integers from Array"
Print Str(TypeWriteTime())+" ms to populate integers in Type"
Print Str(TypeReadTime())+" ms to retrieve integers from Type"
Print Str(BankWriteTime())+" ms to populate bytes in Bank"
Print Str(BankReadTime())+" ms to retrieve bytes from Bank"
WaitKey()
End






;FUNCTIONS

Function ArrayWriteTime%()
	;Reserve Memory
	Dim ARRAYS(TERRAINS,HEIGHTS)
	
	Local Iter1
	Local Iter2
	Local Start=MilliSecs()
	
	For Iter1 = 0 To HEIGHTS-1
		For Iter2 = 0 To TERRAINS-1
			ARRAYS(Iter2,Iter1)=Rand(0,255)
			;DebugLog "Arrays: "+Str((HEIGHTS*TERRAINS-1)-((Iter1*TERRAINS)+Iter2))
		Next
	Next
	
	Local Finish=MilliSecs()-Start
	
	Return Finish
End Function

Function ArrayReadTime%()
	Local Iter1
	Local Iter2
	Local Test
	
	Local Start=MilliSecs()
	
	For Iter1 = 0 To HEIGHTS-1
		For Iter2 = 0 To TERRAINS-1
			Test=ARRAYS(Iter2,Iter1)
			;DebugLog "Arrays: "+Str((HEIGHTS*TERRAINS-1)-((Iter1*TERRAINS)+Iter2))
		Next
	Next
	
	Local Finish=MilliSecs()-Start
	
	;Free Memory
	Dim ARRAYS(0,0)
	
	Return Finish
End Function

Function TypeWriteTime%()
	Local Iter
	Local Max=(TERRAINS*HEIGHTS)-1
	Local T.TYPES
	
	Local Start=MilliSecs()
	
	For Iter = 0 To Max
		;Reserve Memory
		T.TYPES=New TYPES
		T\HEIGHTS=Rand(0,255)
		;DebugLog "Types: "+Str(Max-Iter)
	Next
	
	Local Finish=MilliSecs()-Start
	
	Return Finish
End Function

Function TypeReadTime%()
	Local Max=(TERRAINS*HEIGHTS)-1
	Local T.TYPES
	Local Iter
	Local Test
	
	Local Start=MilliSecs()
	
	For T.TYPES=Each TYPES
		Test=T\HEIGHTS
		Iter=Iter+1
		;DebugLog "Types: "+Str(Max-Iter)
	Next
	
	Local Finish=MilliSecs()-Start
	
	;Free Memory
	Delete Each TYPES
	
	Return Finish
End Function

Function BankWriteTime%()
	;Reserve Memory
	BANKS=CreateBank(TERRAINS*HEIGHTS)
	
	Local Iter
	Local Max=(TERRAINS*HEIGHTS)-1
	
	Local Start=MilliSecs()
	
	For Iter = 0 To Max
		PokeByte BANKS,Iter,Rand(0,255)
		;DebugLog "Banks: "+Str(Max-Iter)
	Next
	
	Local Finish=MilliSecs()-Start
	
	Return Finish
End Function

Function BankReadTime%()
	Local Iter
	Local Max=BankSize(BANKS)-1
	Local Test
	
	Local Start=MilliSecs()
	
	For Iter = 0 To Max
		Test=PeekByte(BANKS,Iter)
		;DebugLog "Banks: "+Str(Max-Iter)
	Next
	
	Local Finish=MilliSecs()-Start
	
	;Free Memory
	FreeBank BANKS
	
	Return Finish
End Function</pre> <br><br></td></tr></table><br>
<a name="1209024"></a>

<a name="1209025"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> _PJ_&gt;&gt;Thanks for your feedback<br><br><div class="quote"> <br>You will have to be very careful with the memory management if you really intend to use this much in one go!<br><br>Identifying timing in this manner isn't always reliable, since the CPU may be busy with other processes, the memory may be more or less full, and of course, is largely system-dependent.<br> <br></div><br>What do you mean by that ? I thought that i was able to create a bank when i wanted if the target computer has enough free memory space.<br><br>For example, if the program requires to have at least a computer with 512mo of free memory space, and asks the user to not run others programs at the same time, i thought that i could use almost all of this free memory space to store the datas for the game.<br>If i can't assume that there is enough free memory space, is there a way to check how much free memory space there is ?<br><br>Thanks, <br><br></td></tr></table><br>
<a name="1209031"></a>

<a name="1209032"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>is there a way to check how much free memory space there is ?<br> <br></div><br>Apparently yes :<br><a href="http://www.blitzbasic.com/Community/posts.php?topic=39105" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=39105</a> (#6)<br><br>Edit : it does not seems to return the correct values, i will have to search more. <br><br></td></tr></table><br>
<a name="1209527"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I wonder if incorrect values are due to having &gt;4Gig of RAM, since Blitz is limited to 32-Bit integers (Signed ones at that, so over 16 777 216 Megabytes might be reported oddly?) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
