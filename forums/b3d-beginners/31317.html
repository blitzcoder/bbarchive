<!DOCTYPE html><html lang="en" ><head ><title >Style Jezzball game</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Style Jezzball game</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Style Jezzball game</a><br><br>
<a name="333966"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Insane Games</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi! I'm trying to make a Jezzball clone but i'm stuck with a problem, i really don't know how to create the rectangles...<br>My code is below:<br><pre class=code>AppTitle "Jezzball Clone v 0.2"
Graphics 800,600,16,2
SetBuffer BackBuffer()

; Constantes para o teclado
Const botaoEsquerdo = 1
Const botaoDireito = 2

; Variáveis de ajuda
Global podeCriarLinhas = True ; só pode criar uma linha por vez !
Const velocidadeLinha = 1

; definindo um tipo para o cursor
Type CURSOR
	Field x ; posição no eixo x do cursor
	Field y ; posição no eixo y do cursor
	Field altura ; altura do cursor
	Field largura ; largura do cursor
End Type

; definindo paredes
Type PAREDE
	Field x ; x da parede
	Field y ; y da parede
	Field altura ; altura da parede
	Field largura ; largura da parede
End Type

; definindo tipo pra linhas
Type LINHA_HORIZONTAL
	Field x ; posição x da linha
	Field xFim ; fim do x da linha
	Field y ; posição y da linha
	Field yFim ; fim do y da linha
End Type

Type LINHA_VERTICAL
	Field x ; posição x da linha
	Field xFim ; fim do x da linha
	Field y ; posição y da linha
	Field yFim ; fim do y da linha
End Type

; Criação do cursor
cursor.CURSOR = New CURSOR
cursor\x = 320
cursor\y = 240
cursor\altura = 4
cursor\largura = 4

; Criação das paredes
paredePrincipal.PAREDE = New PAREDE
paredePrincipal\x = 220
paredePrincipal\y = 20
paredePrincipal\altura = 560
paredePrincipal\largura = 560

; LOOP!
While Not KeyHit(1)
Cls
	
	; Movimentação do cursor
	cursor\x = MouseX()
	cursor\y = MouseY()
	
	; Criando linhas	
	If (MouseHit(botaoEsquerdo) And podeCriarLinhas = True) And ((MouseX() &gt; paredePrincipal\x) And (MouseX() &lt; paredePrincipal\y + paredePrincipal\altura)) And ((MouseY() &gt; paredePrincipal\y) And (MouseY() &lt; paredePrincipal\x + paredePrincipal\largura))
		linhaH.LINHA_HORIZONTAL = New LINHA_HORIZONTAL
		linhaH\x = MouseX()
		linhaH\xFim = MouseX()
		linhaH\y = MouseY()
		linhaH\yFim = MouseY()
	EndIf

	If (MouseHit(botaoDireito) And podeCriarLinhas = True) And ((MouseX() &gt; paredePrincipal\x) And (MouseX() &lt; paredePrincipal\y + paredePrincipal\altura)) And ((MouseY() &gt; paredePrincipal\y) And (MouseY() &lt; paredePrincipal\x + paredePrincipal\largura))
		linhaV.LINHA_VERTICAL = New LINHA_VERTICAL
		linhaV\x = MouseX()
		linhaV\xFim = MouseX()
		linhaV\y = MouseY()
		linhaV\yFim = MouseY()
	EndIf
	
	; Var para criação de linhas
	podeCriarLinhas = True
	
	; Desenhando linha horizontal
	For linhaH = Each LINHA_HORIZONTAL
		If (linhaH\x &gt; paredePrincipal\x)
			linhaH\x = linhaH\x - velocidadeLinha
			podeCriarLinhas = False ; ainda ta desenhando alguma linha
		EndIf
		If (linhaH\xFim &lt; paredePrincipal\y + paredePrincipal\altura)
			linhaH\xFim = linhaH\xFim + velocidadeLinha
			podeCriarLinhas = False ; ainda ta desenhando alguma linha
		EndIf
		Line linhaH\x,linhaH\y,linhaH\xFim,linhaH\yFim
	Next
	
	; Desenhando linha vertical
	For linhaV = Each LINHA_VERTICAL
		If (linhaV\y &gt; paredePrincipal\y)
			linhaV\y = linhaV\y - 1
			podeCriarLinhas = False ; ainda ta desenhando alguma linha
		EndIf
		If (linhaV\yFim &lt; paredePrincipal\x + paredePrincipal\largura)
			linhaV\yFim = linhaV\yFim + 1
			podeCriarLinhas = False ; ainda ta desenhando alguma linha
		EndIf
		Line linhaV\x,linhaV\y,linhaV\xFim,linhaV\yFim
	Next
		
	; Desenhando cursor
	Rect cursor\x,cursor\y,cursor\largura,cursor\altura,1

	; Desenhando parede principal
	Rect paredePrincipal\x,paredePrincipal\y,paredePrincipal\largura,paredePrincipal\altura,0
	
	debug()
	
Flip
Wend

Function debug()
	Text 10,10,"MouseX: "+MouseX()
	Text 10,20,"MouseY: "+MouseY()
	Text 10,30,"Criar linhas? "+podeCriarLinhas
End Function</pre> <br><br></td></tr></table><br>
<a name="334004"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >rdodson41</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> First, not many people on these forums can read Spanish or whatever language you are using, so it might not be easy to decode.<br><br>Second, I'm not sure what jezzball is, but if it is what i think it is, then there is a blitz game sample that comes with the program called "Blitzanoid". check that out, hope it helps! <br><br></td></tr></table><br>
<a name="334078"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Andy_A</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Had to look it up, but Jezzball is like a Microsoft version of Qix.<br><br>The object of the game is to paint/fill 75% of the game screen with out being touched by the Jezzball(MS) or the Spark(Qix).<br><br>The clearest description of the game is here:<br><a href="http://takegame.com/arcade/htm/jezzball.htm" target="_blank">http://takegame.com/arcade/htm/jezzball.htm</a><br><br>@Insane Games<br>The original Jezzball was a 28x20 grid of squares. So make an array:  grid(28,20) which are all zeroes at the beginning of a game.<br><br>Draw a square 28x28 pixels in your favorite color. This ensures that the playfield will fit on a 800x600 res screen. Use the "GrabImage" command to store the square as an image in memory.<br><br>You have to scale the MouseX() and MouseY() coords to work with the grid() and to draw images on the screen.<br><br>When the player left clicks (horizontal lines) divide the MouseX() and MouseY() locations by 28, use only the whole number result. These whole number results tell where to begin drawing your squares. Use these whole numbers as a basis for filling the grid(array) and drawing images on the screen. <br><br>The code below is NOT complete, if I did it all for you what fun would that be? There should be enough code to get you started.<br><br>By the way, I don't speak Portuguese, but it's close enough to Spanish to understand your code.<br><br>Hope this helps,<br><br>Andy<br><br><br><pre class=code>
sizeX% = 28 ;width of playfield
sizeY% = 20 ;height of playfield
square = 28 ;width and height of THE square

minX% =  0
maxX% =  sizeX * square

minY% = 0
maxY% = sizeY * square

speed = 100 ;delay in milliseconds

Dim grid%(sizeX, sizeY)

Graphics 800,600,0,2
SetBuffer BackBuffer()


Cls
;We're drawing an image to copy here
Color 255,0,255
Rect 0,0,28,28,True
Color 255,0,0
Oval 0,0,28,28,True
Color 255,255,255
Line 14,0,14,28
Line 15,0,15,28
Line 13,0,13,27
Line 16,1,16,27
Flip
gfxSqr% = CreateImage(square,square)
GrabImage(gfxSqr,0,0)
MaskImage gfxSqr, 255,0,255

Cls
Flip

;main loop
While KeyHit(1) = 0

	If MouseHit(1)=True Then
		mx% = MouseX() ;store in mx to lock x position
		my% = MouseY() ;store in my to lock y position
		x% = Floor(mx/square)  ; x is x position in grid(array) and screen
		y% = Floor(my/square)  ; y is y position in grid(array) and screen

		;Calculate the longest x-line segment
		deltaX = sizeX - x
		If x &gt; deltaX Then deltaX = x

		;Draw a square at current position
		currentPosX = x * square
		currentPosY = y * square
		DrawImage(gfxSqr, currentPosX, currentPosY)
		;Draw horizontal lines from current position
		xR% = x
		xL% = x
		For i = 1 To deltaX
			xR% = xR+1
			xL% = xL-1
			If xR &lt;= sizeX-1 Then
				goRight = xR * square
					DrawImage(gfxSqr, goRight, currentPosY)
			End If
	
			If xL &gt;= minX Then
				goLeft = xL * square
				DrawImage(gfxSqr, goLeft, currentPosY)
			End If
			Delay speed
			Flip
		Next
	End If

	If MouseHit(2)=True Then

		mx% = MouseX() ;store in mx to lock x position
		my% = MouseY() ;store in my to lock y position
		x% = Floor(mx/square)  ; x will equal 14
		y% = Floor(my/square)  ; y will equal 10

		;Calculate the longest y-line segment
		deltaY = sizeY - y
		If y &gt; deltaY Then deltaY = y

		;Draw a square at current position
		currentPosX = x * square
		currentPosY = y * square
		DrawImage(gfxSqr, currentPosX, currentPosY)

		;Draw vertical lines from current position
		yD = y
		yU = y
		For i% = 1 To deltaY
			yD = yD+1
			yU = yU-1
			If yD &lt;= sizeY Then
				goDown = yD * square
				DrawImage(gfxSqr, currentPosX, goDown)
			End If
	
			If yU &gt;= minY Then
				goUp = yU * square
				DrawImage(gfxSqr, currentPosX, goUp)
			End If
			Delay speed

			Flip
		Next
	End If

Wend

FreeImage(gfxSqr)

End
</pre> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
