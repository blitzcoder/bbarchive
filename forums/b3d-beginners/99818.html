<!DOCTYPE html><html lang="en" ><head ><title >2D collisions (wall)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >2D collisions (wall)</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >2D collisions (wall)</a><br><br>
<a name="1173814"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >psychicbottle</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> So, I'm trying to make a simple wall in blitz3d(2d part) it's just a simple thing I'm doing while on vacation but everytime I try to make "wall-like" collisions my character flips out haha <br><br></td></tr></table><br>
<a name="1173924"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matty</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Logic - <br><br>Calculate new x/y position in world of character when in motion.<br><br>If new x/y position has passed through a wall then check if it is possible to move along only 1 axis instead without passing through a wall.  <br><br>Depending on results above either don't move the character, move along one axis only, or move to new position.<br><br>Simple. <br><br></td></tr></table><br>
<a name="1174003"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cooode, where are you~ <br><br></td></tr></table><br>
<a name="1174178"></a>

<a name="1174180"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >psychicbottle</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> So this is the function I use right now but it is very flawed like I was trying to think of a way to make a good platformer so if you were to like jump on top of blocks then they would act like floors and walls would act like walls ect. right now it's a top down so you move Left Right Up Down and what happens is "LK$" (Last Key) records which key you pressed last then when you run into a wall it pushes you in the opposite direction of that key that was pressed.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Function UpdateFence()

	For F.Fence = Each Fence
		DrawImage F\Image,F\X#,F\Y#
				
		If ImagesCollide(Player_Image,Player_X#,Player_Y#,0,F\Image,F\X#,F\Y#,0)
			If LK$ = "UP" Then Player_Y# = Player_Y# + 6
			If LK$ = "DOWN" Then Player_Y# = Player_Y# - 6
			If LK$ = "RIGHT" Then Player_X# = Player_X# - 6
			If LK$ = "LEFT" Then Player_X# = Player_X# + 6

		EndIf
		
	Next

End Function
</textarea><br><br><font class="tiny">Last edited 2013</font> <br><br></td></tr></table><br>
<a name="1174291"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >psychicbottle</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Kryzon how should I check if that space is occupied? It would need to be fairly fast should I make a transparent image and check with that? or the image to rect collision? <br><br></td></tr></table><br>
<a name="1174401"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi psychic.<br>What happens is this; instead of using the pixel-based approach you should use a math one. It's faster and has generally better results. <br>Plus it allows for extention: you can add more complex types of fences and obstacles later.<br><br>It is not easier to understand, however.<br>What we're going to do is attach "colliders" to every object we want our player to be able to bump against. For your character, imagine a single rectangle that perfectly encloses the sprite image.<br><br>A collider is an abstract rectangle.<br>A rectangle, as you may know, is formed by 4 line segments:<br><br><img src="http://i50.tinypic.com/143ia1k.jpg"><br><br>The way with which you move your character is by applying a vector to him, an oriented velocty:<br><br><img src="http://i48.tinypic.com/mj0lcn.jpg"><br><br>This vector can be seen as being formed by two components: a horizontal (X) and a vertical (Y).<br>Depending on the SIGN of these components (negative or positive) we can see what will be the FRONT FACING sides of that rectangle. The ones that point toward the direction it's moving:<br><br><img src="http://i50.tinypic.com/1gcut.jpg"><br><br>Since that rectangle is moving up (negative Y value in Blitz), we know side A is the one that can potentially hit anything ahead. Since that rectangle is also moving right (positive X value in Blitz), we know side B can also hit something that may be ahead.<br><br>With this in mind we can build a system that, based on the motion of a rectangle, compares the intersection of the front sides (the ones that are first going to hit something) against the OPPOSITE sides of all other rectangles.<br>Imagine this: if a rectangle moves right it's going to hit the LEFT side of something. There's no way its right side will hit another right side as well. It's always the opposite sides of rectangles that collide.<br>If that rectangle moves up, its top side is going to hit the bottom side of anything that comes ahead.<br><br>So I've coded and built a demo to work with this behavior; it seems to be doing ok.<br><br>- Download this image: <a href="http://i50.tinypic.com/2rfstnl.png" target="_blank">2rfstnl.png</a>  (it has this weird name from TinyPic.com)<br>- Save it as "level.png" in a certain folder.<br>- Then copy the following code to your Blitz, save it as a .bb file in the same folder you downloaded that image and run it.<br><br>Code:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;I hope I didn't talk out of my ass in the comments.

Graphics 640,480,0,2

Const PLAYER_X_SPEED# = 4.0
Const PLAYER_JUMP_FORCE# = 10
Const GRAVITY# = 0.5

Global levelImage = LoadAnimImage("level.png",640,480,0,2) ;Background

Global playerImage = CreateImage(40,75)
	SetBuffer ImageBuffer(playerImage)
	Color 20,200,60
	Rect 0,0,40,75
	HandleImage playerImage,19,74

;Generic "rectangle collision" object.
Type TCollider
	;Position of the rectangle on the screen.
	Field x#, y#
	
	;Local offset of the rectangle's handle\pivot (analogous to ImageHandle)
	Field handleX#, handleY# 
	
	;Width and height of the rectangle.
	Field width#, height# 
	
	;2D vector\direction representing the speed moving this rectangle.
	Field xSpeed#, ySpeed# 
	
	;Boolean flag indicating whether this rectangle is CONCAVE or CONVEX. Fences and most obstacles are convex, they block to the 
	;outside.
	;Level boundaries (which enclose the player) are concave, they block to the inside. Defaults to false (CONVEX).
	Field flipped% 
End Type 

;Game objects that have a rectangle collision element.
Type TFence
	;These fields are unused, since the fence images are "baked" into the level image.
	Field x#, y#
	Field image
	
	Field col.TCollider ;Collider for the fence.
End Type 

Type TPlayer
	Field x#, y#
	Field xSpeed#, ySpeed#
	Field jumping%
	Field image
	
	Field col.TCollider ;Collider for the player.
End Type

Type TLevel
	Field boundaries.TCollider ;Collider for the level.
End Type


Global player.TPlayer = New TPlayer
	player\image = playerImage ;No need to create a copy of this image: it's only going to be used a single time.
	player\x = 100
	player\y = 342
	player\jumping = False
	
	;Create a convex collider with the same "handle" position as the player's graphic, for proper alignment.
	player\col = CreateRectangleCollider(19,74,40,75) 
	
Local fence1.TFence = New TFence
	;In case you use separate sprite images for each fence...
	;fence1\image = LoadImage(...)
	;player\x = 100
	;player\y = 300
	
	;Create a convex collider for fence1, and use the optional "X" and "Y" parameters to position it on screen, aligned with its
	;graphic.
	fence1\col = CreateRectangleCollider(0,0,52,53,False,289,288)

Local fence2.TFence = New TFence
	;fence2\image = LoadImage(...)	
	;fence1\x = 400
	;fence1\y = 378
	
	;Collider for fence2.
	fence2\col = CreateRectangleCollider(0,0,52,53,False,549,288)
	
Local fence3.TFence = New TFence
	;fence3\image = LoadImage(...)
	;fence3\x = 200
	;fence3\y = 378
	
	;Collider for fence3.
	fence3\col = CreateRectangleCollider(0,0,51,53,False,589,231)	

Global firstLevel.TLevel = New TLevel
	;Create a concave collider for the level boundaries.
	firstLevel\boundaries = CreateRectangleCollider(0,0,575,343,True,67,0)

Local fpsTimer = CreateTimer(60)
Local toggleColliders = False
SetBuffer BackBuffer()


While Not KeyHit(1)
	WaitTimer(fpsTimer)
	Cls
	
	If (KeyHit(28) + KeyHit(156)) Then toggleColliders = Not toggleColliders
	
	UpdatePlayer()

	DrawLevelBKG()
	DrawPlayer()
	DrawLevelFG()
	If toggleColliders Then DrawColliders()
	
	Color 255,255,255
	Text 10,455,"Press ENTER to toggle the display of colliders."
	
	Flip
Wend


Function CreateRectangleCollider.TCollider(handleX#, handleY#, width#, height#, flipped = False, x# = 0.0, y# = 0.0)
	Local newCol.TCollider = New TCollider
	newCol\handleX = handleX
	newCol\handleY = handleY
	newCol\width = width
	newCol\height = height
	newCol\flipped = flipped
	newCol\x = x
	newCol\y = y
		
	Return newCol
End Function

;Auxiliary function to keep code clean. This function is used to update the position and speed value of a collider with that of its
;owner.
Function UpdateCollider(col.TCollider, x#, y#, xSpeed#, ySpeed#)
	col\x = x : col\y = y
	col\xSpeed = xSpeed : col\ySpeed = ySpeed
End Function 

;The RectanglesCollide function always tests the source rectangle against the destination rectangle, changing the source rectangle's 
;component speeds according to the sides that hit.
Function ReactColliders(sourceCol.TCollider, destCol.TCollider)
	Local horizontalDistance#, verticalDistance#
	
	;Test for the possibility of horizontal collision.
	If ( (sourceCol\y - sourceCol\handleY) &lt; (destCol\y + (destCol\height - destCol\handleY)) And (sourceCol\y + (sourceCol\height - sourceCol\handleY)) &gt; (destCol\y - destCol\handleY) ) Or ( (sourceCol\y - sourceCol\handleY + sourceCol\ySpeed) &lt; (destCol\y + (destCol\height - destCol\handleY)) And (sourceCol\y + (sourceCol\height - sourceCol\handleY) + sourceCol\ySpeed) &gt; (destCol\y - destCol\handleY) ) Then
		
		;Find which sides are the "front sides", according to the horizontal component of speed.
		If sourceCol\xSpeed &gt; 0 Then 
			;Right side.
					
			;Check if the source rectangle's horizontal speed for the current frame is enough to hit the right side of the source 
			;rectangle with the left side of the destination rectangle.
			
			If destCol\flipped Then
				;Concave destination collider. 
				horizontalDistance = (destCol\x + (destCol\width - destCol\handleX)) - (sourceCol\x + (sourceCol\width - sourceCol\handleX))
				If (sourceCol\x + (sourceCol\width - sourceCol\handleX)) &lt;= (destCol\x + (destCol\width - destCol\handleX)) And sourceCol\xSpeed &gt;= horizontalDistance Then sourceCol\xSpeed = horizontalDistance
			Else
				;Convex destination collider.
				horizontalDistance = (destCol\x - destCol\handleX) - (sourceCol\x + (sourceCol\width - sourceCol\handleX)) 
				If (sourceCol\x + (sourceCol\width - sourceCol\handleX)) &lt;= (destCol\x - destCol\handleX) And sourceCol\xSpeed &gt;= horizontalDistance Then sourceCol\xSpeed = horizontalDistance
			EndIf
		
			;If (sourceCol\x + (sourceCol\width - sourceCol\handleX)) &lt;= (destCol\x - destCol\handleX) And sourceCol\xSpeed &gt;= horizontalDistance Then sourceCol\xSpeed = horizontalDistance
			
		ElseIf sourceCol\xSpeed &lt; 0 Then
			;Left side.
			
			;Check if the source rectangle's horizontal speed for the current frame is enough to hit the left side of the source 
			;rectangle with the right side of the destination rectangle.
			
			If destCol\flipped Then 
				;horizontalDistance is negated to enable comparison with a negative speed.
				horizontalDistance = (sourceCol\x - sourceCol\handleX) - (destCol\x - destCol\handleX)
				If (sourceCol\x - sourceCol\handleX) &gt;= (destCol\x - destCol\handleX) And sourceCol\xSpeed &lt;= -horizontalDistance Then sourceCol\xSpeed = -horizontalDistance
			Else
				horizontalDistance = (sourceCol\x - sourceCol\handleX) - (destCol\x + (destCol\width - destCol\handleX)) 
				If (sourceCol\x - sourceCol\handleX) &gt;= (destCol\x + (destCol\width - destCol\handleX)) And sourceCol\xSpeed &lt;= -horizontalDistance Then sourceCol\xSpeed = -horizontalDistance
			EndIf 
			
			
			;If (sourceCol\x - sourceCol\handleX) &gt;= (destCol\x + (destCol\width - destCol\handleX)) And sourceCol\xSpeed &lt;= -horizontalDistance Then sourceCol\xSpeed = -horizontalDistance
			
		EndIf
	EndIf
	
	;Test for the possibility of vertical collision.
	If ( (sourceCol\x - sourceCol\handleX) &lt; (destCol\x + (destCol\width - destCol\handleX)) And (sourceCol\x + (sourceCol\width - sourceCol\handleX)) &gt; (destCol\x - destCol\handleX) ) Or ( (sourceCol\x - sourceCol\handleX + sourceCol\xSpeed) &lt; (destCol\x + (destCol\width - destCol\handleX)) And (sourceCol\x + (sourceCol\width - sourceCol\handleX) + sourceCol\xSpeed) &gt; (destCol\x - destCol\handleX) )  Then
	 
		;Find which sides are the "front sides", according o the vertical component of speed.
		If sourceCol\ySpeed &gt; 0 Then
			;Bottom side.
			
			;Check if the source rectangle's vertical speed for the current frame is enough to hit the bottom side of the source
			;rectangle with the top side of the destination rectangle.
			
			If destCol\flipped Then
				verticalDistance = (destCol\y + (destCol\height - destCol\handleY)) - (sourceCol\y + (sourceCol\height - sourceCol\handleY))
				If (sourceCol\y + (sourceCol\height - sourceCol\handleY)) &lt;= (destCol\y + (destCol\height - destCol\handleY)) And sourceCol\ySpeed &gt;= verticalDistance Then sourceCol\ySpeed = verticalDistance
			Else
				verticalDistance = (destCol\y - destCol\handleY) - (sourceCol\y + (sourceCol\height - sourceCol\handleY))
				If (sourceCol\y + (sourceCol\height - sourceCol\handleY)) &lt;= (destCol\y - destCol\handleY) And sourceCol\ySpeed &gt;= verticalDistance Then sourceCol\ySpeed = verticalDistance
			EndIf
			
		Else If sourceCol\ySpeed &lt; 0 Then
			;Top side.
		
			;Check if the source rectangle's vertical speed for the current frame is enough to hit the top side of the source
			;rectangle with the bottom side of the destination rectangle.
			
			If destCol\flipped Then 
				verticalDistance = (sourceCol\y - sourceCol\handleY) - (destCol\y - destCol\handleY)
				If (sourceCol\y - sourceCol\handleY) &gt;= (destCol\y - destCol\handleY) And sourceCol\ySpeed &lt;= -verticalDistance Then sourceCol\ySpeed = -verticalDistance 
			Else
				verticalDistance = (sourceCol\y - sourceCol\handleY) - (destCol\y + (destCol\height - destCol\handleY))
				If (sourceCol\y - sourceCol\handleY) &gt;= (destCol\y + (destCol\height - destCol\handleY)) And sourceCol\ySpeed &lt;= -verticalDistance Then sourceCol\ySpeed = -verticalDistance 
			EndIf 	
		
		EndIf 
	
	EndIf 
	
End Function

Function DrawLevelBKG()
	DrawImage levelImage,0,0,0
End Function

Function DrawLevelFG()
	DrawImage levelImage,0,0,1
End Function

Function DrawPlayer()
	DrawImage player\image,player\x,player\y
End Function

Function DrawColliders()
	Color 210,210,210
	
	;Fences
	For f.TFence = Each TFence
		DrawCollider(f\col)
	Next
	
	;Player
	DrawCollider(player\col)
	
	;Level
	DrawCollider(firstLevel\boundaries)
End Function

Function DrawCollider(col.TCollider)
	Rect (col\x - col\handleX), (col\y - col\handleY), col\width, col\height, 0
End Function

Function UpdatePlayer()
	
	;Update player's velocities' default behavior:
		
	player\xSpeed = KeyDown(205)*PLAYER_X_SPEED - KeyDown(203)*PLAYER_X_SPEED
	player\ySpeed = player\ySpeed + GRAVITY
	
	If KeyHit(57) And (Not player\jumping) Then 
		player\ySpeed = -PLAYER_JUMP_FORCE
		player\jumping = True ;To prevent consecutive jumps.
	EndIf 
	 
	;Check for collisions (they can potentially change default velocities).
	;---------------------------------------------------------------------
		
		;1) Update the player's dynamic rectangle collider's position and speed:
		UpdateCollider(player\col, player\x, player\y, player\xSpeed, player\ySpeed)
	
		;2) Check against level:
		ReactColliders(player\col, firstLevel\boundaries)
			;We just tested against the ground. See if we landed (collider's ySpeed = 0 and player's original ySpeed non-zero):
			If player\ySpeed &lt;&gt; 0 And player\col\ySpeed = 0 Then player\jumping = False
	
		;3) Check against fences (look how easy it is to compare against several at the same time):
		For f.TFence = Each TFence
			ReactColliders(player\col, f\col)
			
			;Same "ground" test for fences, as they can act as leveled grounds as well.
			If player\ySpeed &lt;&gt; 0 And player\col\ySpeed = 0 Then player\jumping = False
			;All objects that act as jumpable ground need tests like these.
		Next
	
	;---------------------------------------------------------------------	
	;End of collision checking block.
		
	;Apply COLLIDER's final velocities to player. The collider's final velocities take collisions into consideration.
	player\x = player\x + player\col\xSpeed
	player\y = player\y + player\col\ySpeed
End Function


End</textarea><br><br>There's more stuff to this method, we'll talk later. <br><br></td></tr></table><br>
<a name="1174545"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >psychicbottle</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> whoa, thanks so much. Kinda confused but I think I am piecing it together slowly haha <br><br></td></tr></table><br>
<a name="1174643"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi. You're right, it's gonna take some time to digest it.<br><br>Replace that ReactColliders() function with this. I've replaced the calculations with appropriately-named variables. Should make more sense:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">;The ReactColliders function always tests the source rectangle against the destination rectangle, changing the source rectangle's 
;component speeds according to the sides that hit.
Function ReactColliders(sourceCol.TCollider, destCol.TCollider)
	Local horizontalDistance#, verticalDistance#
	
	;Source rectangle's 4 sides:
	Local srcTop# = (sourceCol\y - sourceCol\handleY)
	Local srcRight# = (sourceCol\x + (sourceCol\width - sourceCol\handleX))
	Local srcBottom# = (sourceCol\y + (sourceCol\height - sourceCol\handleY))
	Local srcLeft# = (sourceCol\x - sourceCol\handleX)
	
	;Destination rectangle's 4 sides:
	Local dstTop# = (destCol\y - destCol\handleY)
	Local dstRight# = (destCol\x + (destCol\width - destCol\handleX))
	Local dstBottom# = (destCol\y + (destCol\height - destCol\handleY))
	Local dstLeft# = (destCol\x - destCol\handleX)
	
	;Test for the possibility of horizontal collision.
	If ( (srcTop &lt; dstBottom) And (srcBottom &gt; dstTop) ) Or ( ((srcTop + sourceCol\ySpeed) &lt; dstBottom) And ((srcBottom + sourceCol\ySpeed) &gt; dstTop) ) Then
		
		;Find which sides are the "front sides", according to the horizontal component of speed.
		If sourceCol\xSpeed &gt; 0 Then 
			;Right side.
					
			;Check if the source rectangle's horizontal speed for the current frame is enough to hit the right side of the source 
			;rectangle with the left side of the destination rectangle.
			
			If destCol\flipped Then
				;Concave destination collider. 
				horizontalDistance = dstRight - srcRight
				If srcRight &lt;= dstRight And sourceCol\xSpeed &gt;= horizontalDistance Then sourceCol\xSpeed = horizontalDistance
			Else
				;Convex destination collider.
				horizontalDistance = dstLeft - srcRight 
				If srcRight &lt;= dstLeft And sourceCol\xSpeed &gt;= horizontalDistance Then sourceCol\xSpeed = horizontalDistance
			EndIf
					
		ElseIf sourceCol\xSpeed &lt; 0 Then
			;Left side.
			
			;Check if the source rectangle's horizontal speed for the current frame is enough to hit the left side of the source 
			;rectangle with the right side of the destination rectangle.
			
			If destCol\flipped Then 
				;horizontalDistance is negated to enable comparison with a negative speed.
				
				horizontalDistance = srcLeft - dstLeft ;This yields a positive value, and needs to be negated in the following line.
				If srcLeft &gt;= dstLeft And sourceCol\xSpeed &lt;= -horizontalDistance Then sourceCol\xSpeed = -horizontalDistance
			Else
				horizontalDistance = srcLeft - dstRight 
				If srcLeft &gt;= dstRight And sourceCol\xSpeed &lt;= -horizontalDistance Then sourceCol\xSpeed = -horizontalDistance
			EndIf 
		EndIf
	EndIf
	
	;Test for the possibility of vertical collision.
	If ( (srcLeft &lt; dstRight) And (srcRight &gt; dstLeft) ) Or ( ((srcLeft + sourceCol\xSpeed) &lt; dstRight) And ((srcRight + sourceCol\xSpeed) &gt; dstLeft) )  Then
	 
		;Find which sides are the "front sides", according o the vertical component of speed.
		If sourceCol\ySpeed &gt; 0 Then
			;Bottom side.
			
			;Check if the source rectangle's vertical speed for the current frame is enough to hit the bottom side of the source
			;rectangle with the top side of the destination rectangle.
			
			If destCol\flipped Then
				verticalDistance = dstBottom - srcBottom
				If srcBottom &lt;= dstBottom And sourceCol\ySpeed &gt;= verticalDistance Then sourceCol\ySpeed = verticalDistance
			Else
				verticalDistance = dstTop - srcBottom
				If srcBottom &lt;= dstTop And sourceCol\ySpeed &gt;= verticalDistance Then sourceCol\ySpeed = verticalDistance
			EndIf
			
		Else If sourceCol\ySpeed &lt; 0 Then
			;Top side.
		
			;Check if the source rectangle's vertical speed for the current frame is enough to hit the top side of the source
			;rectangle with the bottom side of the destination rectangle.
			
			If destCol\flipped Then 
				verticalDistance = srcTop - dstTop
				If srcTop &gt;= dstTop And sourceCol\ySpeed &lt;= -verticalDistance Then sourceCol\ySpeed = -verticalDistance 
			Else
				verticalDistance = srcTop - dstBottom
				If srcTop &gt;= dstBottom And sourceCol\ySpeed &lt;= -verticalDistance Then sourceCol\ySpeed = -verticalDistance 
			EndIf 	
		EndIf 
	EndIf 
End Function</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
