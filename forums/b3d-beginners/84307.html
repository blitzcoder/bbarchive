<!DOCTYPE html><html lang="en" ><head ><title >Collision problems</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Collision problems</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Collision problems</a><br><br>
<a name="952564"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >fox95871</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> All I want to do is run and jump in a 3d level, why is that so difficult? I know, I know, it can be done. Well, here's my 10% working code for anyone who wants to try to help:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
.setup
Graphics3D 640,480,32,1
SetBuffer BackBuffer()
SeedRnd MilliSecs()
Const fps=90
blitz=LoadFont("blitz",10,1,0,0)
SetFont blitz
Color 100,100,100
ClearTextureFilters
period=1000/fps
time=MilliSecs()-period
elapsed%=0
ticks%=0
tween#=0
rt%=0

.charactervariables
Global characterposition#=0
Global characterheight#=3
Global characterdistance#=0
Global facing$="down"
Global facing2$=""
Global walking$=""
Global walking2$=""
Global running$=""
Global running2$=""

.collisionvariables
Global charactercollider%=1
Global levelcollider%=2
Global collisionmethod%=2
;1=sphere
;2=mesh
;3=box
Global collisionresponse%=2
;1=stick
;2=slide
;3=slide but not down 

.cameracreation
Global camera=CreateCamera()
CameraZoom camera,1.6
RotateEntity camera,40,0,0
PositionEntity camera,0,20,-20

.lightcreation
Global light=CreateLight(1)
RotateEntity light,50,-70,0

.levelcreation
Global ground=CreateCube()
EntityColor ground,70,60,30
EntityType ground,levelcollider
ScaleMesh ground,20,1,20
Global hill=CreateCube()
ScaleMesh hill,10,10,10
RotateMesh hill,0,0,30
PositionMesh hill,-12,-4,0
AddMesh hill,ground
FreeEntity hill
Global platform=CreateCube()
ScaleMesh platform,4,.5,4
PositionMesh platform,7,4,0
EntityType platform,levelcollider
AddMesh platform,ground
FreeEntity platform

.charactercreation
Global character=CreateCube()
EntityType character,charactercollider
EntityRadius character,.05,2
EntityColor character,20,20,60
ScaleEntity character,1,2,1
arrow=CreateCone()
ScaleMesh arrow,.5,.5,.5
RotateMesh arrow,90,0,0
PositionMesh arrow,0,0,2
AddMesh arrow,character
FreeEntity arrow
Global upmarker=CreateSphere(2)
ScaleEntity upmarker,.1,.1,.1
Global uprightmarker=CreateSphere(2)
ScaleEntity uprightmarker,.1,.1,.1
Global rightmarker=CreateSphere(2)
ScaleEntity rightmarker,.1,.1,.1
Global rightdownmarker=CreateSphere(2)
ScaleEntity rightdownmarker,.1,.1,.1
Global downmarker=CreateSphere(2)
ScaleEntity downmarker,.1,.1,.1
Global downleftmarker=CreateSphere(2)
ScaleEntity downleftmarker,.1,.1,.1
Global leftmarker=CreateSphere(2)
ScaleEntity leftmarker,.1,.1,.1
Global leftupmarker=CreateSphere(2)
ScaleEntity leftupmarker,.1,.1,.1

.collisionsetup
Collisions charactercollider,levelcollider,collisionmethod,collisionresponse

.blitz3d
While Not KeyHit(1)
Cls
Repeat
elapsed=MilliSecs()-time
Until elapsed
ticks=elapsed/period
tween#=Float(elapsed Mod period)/Float(period)
For rt=1 To ticks
time=time+period
If rt=ticks Then CaptureWorld
;Gravity()
FacingAndMovingControl()
FacingEffects()
MovingEffects()
MarkerUpdater()
UpdateWorld
Next
RenderWorld tween

.blitz2d
DebugText()
Flip
Wend
End

.facingandmovingcontrol
Function FacingAndMovingControl()
;nates code
;/walking
If     KeyDown(200):facing="up"    :walking= "up"
ElseIf KeyDown(208):facing="down"  :walking= "down"
Else               :facing=""      :walking= ""     :running=""
EndIf
If KeyDown(45)     :facing2="right":walking2="right"
ElseIf KeyDown(44) :facing2="left" :walking2="left"
Else               :facing2=""     :walking2=""     :running2=""
EndIf
;/running
If     KeyDown(200) And KeyDown(203):facing= "up"   :running= "up"   :walking=""
ElseIf KeyDown(208) And KeyDown(203):facing= "down" :running= "down" :walking=""
Else                                                 running=""
EndIf
If     KeyDown(45)  And KeyDown(203):facing2="right":running2="right":walking2=""
ElseIf KeyDown(44)  And KeyDown(203):facing2="left" :running2="left" :walking2=""
Else                                                 running2=""
EndIf
;/prevariables 
walking=walking$+walking2$
running=running$+running2$
facing=facing$+facing2$
End Function

.facingeffects
Function FacingEffects()
Select facing
Case "up":PointEntity character,upmarker
Case "upright":PointEntity character,uprightmarker
Case "right":PointEntity character,rightmarker
Case "downright":PointEntity character,rightdownmarker
Case "down":PointEntity character,downmarker
Case "downleft":PointEntity character,downleftmarker
Case "left":PointEntity character,leftmarker
Case "upleft":PointEntity character,leftupmarker
End Select
End Function

.movingeffects
Function MovingEffects()

Select walking
Case "up":characterdistance=characterdistance+.1
Case "upright":characterdistance=characterdistance+.1:characterposition=characterposition+.1
Case "right":characterposition=characterposition+.1
Case "downright":characterdistance=characterdistance-.1:characterposition=characterposition+.1
Case "down":characterdistance=characterdistance-.1
Case "downleft":characterdistance=characterdistance-.1:characterposition=characterposition-.1
Case "left":characterposition=characterposition-.1
Case "upleft":characterposition=characterposition-.1:characterdistance=characterdistance+.1
End Select

Select running
Case "up":characterdistance=characterdistance+.2
Case "upright":characterdistance=characterdistance+.2:characterposition=characterposition+.2
Case "right":characterposition=characterposition+.2
Case "downright":characterdistance=characterdistance-.2:characterposition=characterposition+.2
Case "down":characterdistance=characterdistance-.2
Case "downleft":characterdistance=characterdistance-.2:characterposition=characterposition-.2
Case "left":characterposition=characterposition-.2
Case "upleft":characterposition=characterposition-.2:characterdistance=characterdistance+.2
End Select

End Function

.gravity
Function Gravity()
If Not EntityCollided(character,ground) characterheight=characterheight=-.1
End Function

.markerupdater
Function MarkerUpdater()
PositionEntity character,characterposition,characterheight,characterdistance
PositionEntity upmarker,characterposition,characterheight,characterdistance+2
PositionEntity uprightmarker,characterposition+1.5,characterheight,characterdistance+1.5
PositionEntity rightmarker,characterposition+2,characterheight,characterdistance
PositionEntity rightdownmarker,characterposition+1.5,characterheight,characterdistance-1.5
PositionEntity downmarker,characterposition,characterheight,characterdistance-2
PositionEntity downleftmarker,characterposition-1.5,characterheight,characterdistance-1.5
PositionEntity leftmarker,characterposition-2,characterheight,characterdistance
PositionEntity leftupmarker,characterposition-1.5,characterheight,characterdistance+1.5
End Function

.debugtext
Function DebugText()
Text 0,00,"Controls: up down z and x to move, left arrow to run"
Text 0,15,"facing " +facing
Text 0,30,"walking " +walking
Text 0,45,"running " +running
End Function
</textarea><br><br>Currently gravity is off, and jumping is nonexistent. I want to fix the more basic problems first. If anyone's looking for something to do, I'd really appreciate the help. I've been avoiding collisions long enough. <br><br></td></tr></table><br>
<a name="952596"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here is something I tried, hopefully it is helpful:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
.setup
Graphics3D 640,480,0,2
SetBuffer BackBuffer()

SeedRnd MilliSecs()
Const fps=90
blitz=LoadFont("blitz",10,1,0,0)
SetFont blitz
Color 100,100,100
ClearTextureFilters
period=1000/fps
time=MilliSecs()-period
elapsed%=0
ticks%=0
tween#=0
rt%=0

.cameracreation
Global camera=CreateCamera()
CameraZoom camera,1.6
RotateEntity camera,40,0,0
PositionEntity camera,0,20,-20

.lightcreation
Global light=CreateLight(1)
RotateEntity light,50,-70,0

.levelcreation
Global level=CreatePivot()
ground=CreateCube(level)
EntityColor ground,70,60,30
ScaleMesh ground,20,1,20
hill=CreateCube(level)
ScaleMesh hill,10,10,10
RotateMesh hill,0,0,30
PositionMesh hill,-12,-4,0
AddMesh hill,ground
FreeEntity hill
platform=CreateCube(level)
ScaleMesh platform,4,.5,4
PositionMesh platform,7,4,0
AddMesh platform,ground
FreeEntity platform

.charactercreation
Global character=CreateSphere()
EntityColor character,20,20,60
PositionEntity character,0,10,0

.explorerpivotcreation
Global testpiv = CreatePivot()
EntityRadius testpiv,1

.enablecollisions
EntityType testpiv, 1
EntityType level, 2, 1
Collisions 1, 2, 2, 3

.mainloop
While Not KeyHit(1)

	xs# = (KeyDown(205) - KeyDown(203)) * 0.1
	zs# = (KeyDown(200) - KeyDown(208)) * 0.1
;	If TestPosition(character, xs, 0, 0) Then TranslateEntity character, xs, 0, 0
;	If TestPosition(character, 0, 0, zs) Then TranslateEntity character, 0, 0, zs
	Move character, xs, 0, zs
		
	If jump &gt; 0 Then
		If TestPosition(character, 0, +jsp#, 0) Then
			TranslateEntity character, 0, +jsp#, 0
			jsp# = jsp# * 0.9
			jump = jump - 1
		Else
			jump = 0
		End If
	Else
		If TestPosition(character, 0, -gv#, 0) Then
			TranslateEntity character, 0, -gv#, 0
			gv# = gv# * 1.08 + 0.01
		Else
			If KeyDown(57) Then jump = 30: jsp# = 1
			gv# = 0
		End If
	End If
	
	UpdateWorld
	
	PointEntity camera, character
	RenderWorld
	
	Text 0 ,0, "arrow keys to move, space to jump"

	Flip

Wend

End

Function TestPosition(obj, x#, y#, z#)

	PositionEntity testpiv, EntityX(obj, 1), EntityY(obj, 1), EntityZ(obj, 1)
	MoveEntity testpiv, x, y, z
	UpdateWorld
	Return Not(EntityCollided(testpiv, 2))
	
End Function

Function Move(obj, x#, y#, z#)

	PositionEntity testpiv, EntityX(obj, 1), EntityY(obj, 1), EntityZ(obj, 1)
	MoveEntity testpiv, x, y, z
	UpdateWorld
	PositionEntity obj, EntityX(testpiv, 1), EntityY(testpiv, 1), EntityZ(testpiv, 1)

End Function
</textarea> <br><br></td></tr></table><br>
<a name="952738"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >fox95871</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hope so! Thanks, I'll try it tonight. <br><br></td></tr></table><br>
<a name="953008"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >fox95871</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks! I'm really happy with this.<br><br>I want to alter it a bit, but I think I can handle doing it myself. I want to: remove all control after a jump has been started so you can't move in midair, make jump KeyHit instead of KeyDown so you don't accidentally bounce, and I need to freeze the camera again because my game's going to use all 2d overlays for the level graphics. I also need to see if my facing code will work with what you did. Did you remove it because it didn't? <br><br></td></tr></table><br>
<a name="953044"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Wouldn't those extra UpdateWorld calls screw up animations? everytime you call TestPosition, all the animations in your game would be updated. <br>I think you need to send (0) to it just so it doesn't update the animations. <br><br></td></tr></table><br>
<a name="953069"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> That is indeed a good suggestion: to use "UpdateWorld 0" instead. Also, if no keys are pressed, TestPosition (and Move) should not be used. The way it works now it is calling UpdateWorld too often.<br>The reason I removed the facing code is that I figured it would be more easy to use a sphere as a character, since all collisions are sphere-to-something else. So by using a sphere, you can see what the collisions are doing. When the sphere is working, it can be replaced by something else. <br><br></td></tr></table><br>
<a name="953181"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >fox95871</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I noticed one thing that would be a pretty big problem for me. If you go to the top of the hill and then try to walk down it to the right and jump to the ledge from about the middle of the slope, it's pretty much impossible because you're usually in the air. My game's going to have lots of places where you have to jump from slopes to other things. Is there a way this pseudocode could be written in Blitz?<br><br>Character is always attached to the ground unless jump is pressed<br>If jump is pressed, character detaches from the ground with a set y speed<br>Gravity subtracts from the y speed until the character is attached to the ground again <br><br></td></tr></table><br>
<a name="953357"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >fox95871</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I couldn't get my facing code to work with it afterall. I think I'm just going to copy every single example of this kind of code on the site til I find one that works. The tediousness of that seems less likely to make me lose it.<br><br>If anyone knows of any threads with complete examples of running and jumping in a 3d area please point them out to me. Til then I'll just keep searching. <br><br></td></tr></table><br>
<a name="964481"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >fox95871</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> This topic is continued at <a href="http://www.blitzbasic.com/Community/posts.php?topic=84846" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=84846</a> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
