<!DOCTYPE html><html lang="en" ><head ><title >'Memory Access Violation"</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >'Memory Access Violation"</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >'Memory Access Violation"</a><br><br>
<a name="587388"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Q</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I keep getting this error message when running my compiled code saying "Memory Access Violation".  It happens in the first second of the program being run.  Why is this happening?  Here is my code:<br><br><pre class=code>
; map editor

Graphics3D 800, 600, 32, 2

SetBuffer BackBuffer()

AppTitle("Map Editor v1.0")

Global t_menu = LoadImage("graphics\t_menu.bmp")
	MaskImage t_menu, 0, 255, 0
Global b_menu = LoadImage("graphics\b_menu2.bmp")
	MaskImage b_menu, 0, 255, 0
Global file = LoadImage("graphics\file.bmp")
	MaskImage file, 255, 255, 0
Global quit = LoadImage("graphics\quit.bmp")
	MaskImage quit, 255, 255, 0
Global n = LoadImage("graphics\n.bmp")
	MaskImage n, 255,255,255
Global p = LoadImage("graphics\p.bmp")
	MaskImage p, 255, 255, 255
Global tiles = LoadAnimImage("\Tiles\Terrains\terrain.bmp",64,64,0,5)
	MaskImage tiles, 255, 255, 255
Global pointer = LoadImage("graphics\mouse.bmp")

Dim map(32, 24)

Global currenttile#=1
Global mapx# = 32
Global mapy# = 24

Global gridon# = 0

Const erase#=0


While Not KeyHit(1)

	; pointer coordinates

	p_x# = MouseX()
	p_y# = MouseY()
	
	Cls
	
	; Button setup
	If ImagesOverlap(pointer, p_x#, p_y#, n, 101, 582) Then buttonon(n)
	If ImagesOverlap(pointer, p_x#, p_y#, n, 101, 582) And MouseHit(1) Then currenttile=currenttile+1
	If currenttile &gt; 4 Then currenttile = 1	
	If Not ImagesOverlap(pointer, p_x#, p_y#, n, 101, 582) Then buttonoff(n)
	
	If ImagesOverlap(pointer, p_x#, p_y#, p, 20, 582) Then button2on(p)
	If ImagesOverlap(pointer, p_x#, p_y#, p, 20, 582) And MouseHit(1) Then currenttile=currenttile-1
	If currenttile &lt; 0 Then currenttile = 4
	If Not ImagesOverlap(pointer, p_x#, p_y#, p, 20, 582) Then button2off(p)
	
	If ImagesOverlap(pointer, p_x#, p_y#, quit, 73, 0) And MouseDown(1) Then End
	
	update()
	controls()

	Text 0,18,"X: "+MouseX()
	Text 0,28,"Y: "+MouseY()
	Text 0,38,"Press the 'G' key to enable the grid."
	
	Flip

Wend
End

; button mouse-over effects

Function buttonon(n)

	FreeImage n
	n = LoadImage("Graphics\n2.bmp")

End Function

Function buttonoff(n)

	FreeImage n
	n = LoadImage("graphics\n.bmp")

End Function

Function button2on(p)

	FreeImage p
	p = LoadImage("graphics\p2.bmp")
	
End Function

Function button2off(p)

	FreeImage p
	p = LoadImage("graphics\p.bmp")

End Function

; update

Function update()

For x#=0 To mapx#
	For y#=0 To mapy#
		DrawImage tiles, x#*64, y#*64,map(x#, y#)
	Next
Next

	DrawImage t_menu, 0, 0
	DrawImage file, 0, 0
	DrawImage quit, 73, 0
	DrawImage b_menu, 0, 540
	DrawImage n, 101, 582 ; next button
	DrawImage p, 20, 582 ; previous button
	DrawImage tiles, 38, 540, currenttile

End Function

Function controls()

	tile_x# = MouseX()/64
	tile_y# = MouseY()/64

	DrawImage pointer, p_x, p_y ; mouse

	If MouseDown(1) And tile_x# &lt; 32 And tile_y# &lt; 24 Then map(tile_x#, tile_y#)=currenttile#
	If MouseDown(2) And tile_x# &lt; 32 And tile_y# &lt; 24 Then map(tile_x#, tile_y#)=0
	
	If KeyHit(34) Then gridon# = +1
	
	grid()
	
	
End Function

Function grid()

If gridon# = 1
If gridon# &gt; 1 Then gridon = 0

For x# = 0 To mapx#
	For y# = 0 To mapy#
		Rect x#*64, y#*64, 64, 64, 0
	Next
Next

EndIf

End Function
</pre> <br><br></td></tr></table><br>
<a name="587411"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Shifty Geezer</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Have you got Debug enabled? Otherwise the only errors you ever get are MAVs which don't tell yuo squat! <br><br></td></tr></table><br>
<a name="587421"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Q</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes, I have debug enabled.  Hmm..  I don't know what to do.  So depressing.<br><br>I deleted the grid function and everything involving it.  Leaving me with the following code: <br><br><pre class=code>
; map editor

Graphics3D 800, 600, 32, 2

SetBuffer BackBuffer()

AppTitle("Map Editor v1.0")

Global t_menu = LoadImage("Graphics\t_menu.bmp")
	MaskImage t_menu, 0, 255, 0
Global b_menu = LoadImage("Graphics\b_menu.bmp")
	MaskImage b_menu, 0, 255, 0
Global file = LoadImage("Graphics\file.bmp")
	MaskImage file, 255, 255, 0
Global quit = LoadImage("Graphics\quit.bmp")
	MaskImage quit, 255, 255, 0
Global n = LoadImage("Graphics\n.bmp")
	MaskImage n, 255,255,255
Global p = LoadImage("Graphics\p.bmp")
	MaskImage p, 255, 255, 255
Global tiles = LoadAnimImage("\Tiles\Terrains\terrain.bmp",64,64,0,5)
	MaskImage tiles, 255, 255, 255
Global pointer = LoadImage("graphics\mouse.bmp")

Dim map(32, 24)

Global currenttile#=1
Global mapx# = 32
Global mapy# = 24

Const erase#=0


While Not KeyHit(1)

	; pointer coordinates

	p_x# = MouseX()
	p_y# = MouseY()
	
	Cls
	
	; Button setup
	If ImagesOverlap(pointer, p_x#, p_y#, n, 101, 582) Then buttonon(n)
	If ImagesOverlap(pointer, p_x#, p_y#, n, 101, 582) And MouseHit(1) Then currenttile=currenttile+1
	If currenttile &gt; 4 Then currenttile = 1	
	If Not ImagesOverlap(pointer, p_x#, p_y#, n, 101, 582) Then buttonoff(n)
	
	If ImagesOverlap(pointer, p_x#, p_y#, p, 20, 582) Then button2on(p)
	If ImagesOverlap(pointer, p_x#, p_y#, p, 20, 582) And MouseHit(1) Then currenttile=currenttile-1
	If currenttile &lt; 0 Then currenttile = 4
	If Not ImagesOverlap(pointer, p_x#, p_y#, p, 20, 582) Then button2off(p)
	
	If ImagesOverlap(pointer, p_x#, p_y#, quit, 73, 0) And MouseDown(1) Then End
	
	update()
	controls()

	Text 0,18,"X: "+MouseX()
	Text 0,28,"Y: "+MouseY()
	Text 0,38,"Press the 'G' key to enable the grid."
	
	Flip

Wend
End

; button mouse-over effects

Function buttonon(n)

	FreeImage n
	n = LoadImage("Graphics\n2.bmp")

End Function

Function buttonoff(n)

	FreeImage n
	n = LoadImage("graphics\n.bmp")

End Function

Function button2on(p)

	FreeImage p
	p = LoadImage("graphics\p2.bmp")
	
End Function

Function button2off(p)

	FreeImage p
	p = LoadImage("graphics\p.bmp")

End Function

; update

Function update()

For x#=0 To mapx#
	For y#=0 To mapy#
		DrawImage tiles, x#*64, y#*64,map(x#, y#)
	Next
Next

	DrawImage t_menu, 0, 0
	DrawImage file, 0, 0
	DrawImage quit, 73, 0
	DrawImage b_menu, 0, 540
	DrawImage n, 101, 582 ; next button
	DrawImage p, 20, 582 ; previous button
	DrawImage tiles, 38, 540, currenttile

End Function

Function controls()

	tile_x# = MouseX()/64
	tile_y# = MouseY()/64

	DrawImage pointer, p_x, p_y ; mouse

	If MouseDown(1) And tile_x# &lt; 32 And tile_y# &lt; 24 Then map(tile_x#, tile_y#)=currenttile#
	If MouseDown(2) And tile_x# &lt; 32 And tile_y# &lt; 24 Then map(tile_x#, tile_y#)=0
	
	
	
End Function
</pre><br><br>Everything works like it should when I test run it, but once compiled into a .exe I am missing a few images, thus causing other parts of the program to not work.  The images that are not showing up are n.bmp, n2.bmp, p.bmp, and p2.bmp.  This is wierd :S<br><br>Also, approximately 26 seconds the program gets the odd 'MAV' error.. <br><br></td></tr></table><br>
<a name="587439"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >WolRon</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I would guess it's because you are loading/freeing images on the fly so much like so:<pre class=code>Function button2off(p)

	FreeImage p
	p = LoadImage("graphics\p.bmp")

End Function
</pre>I'm sure it would be better to just load all of the images at the beginning of the program and just Do or Don't draw the necessary ones. <br><br></td></tr></table><br>
<a name="587443"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >big10p</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> [edit] Arg! Someone beats me to it again. :P<br><br>These lines:<br><pre class=code>If ImagesOverlap(pointer, p_x#, p_y#, n, 101, 582) Then buttonon(n)

If Not ImagesOverlap(pointer, p_x#, p_y#, n, 101, 582) Then buttonoff(n)</pre><br>cause the button image to be <b>constantly</b> freed and loaded. This is a very bad idea. While not syntactically wrong, it's probably causing chaos because it's being done so frequently.<br><br>The best way is to load all your images outside of the main loop and then just display the correct image depending on whether you're pointing on the button, or not.<br><br>Also, using global variable names of 'p' and 'n' is not such a good idea. They're not descriptive and simple variable names like this are often used for other things, like For/Next loops. <br><br></td></tr></table><br>
<a name="587475"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sir Gak</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Another reason you don't want to load them constantly, is that harddrive access is slower than memory access.  Had this been a situation where you were loading from an old fashioned floppy disk, you would screamed at the agonizingly slow disk access.<br><br>So I agree with the others: load them once at the beginning of your program, and then just draw them as needed. <br><br></td></tr></table><br>
<a name="587480"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Q</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks a ton guys!  That got rid of the error.  I also made the variables more obvious of what they are.  I've learned alot here.  Just one more problem, for some reason when I click the previous.bmp currenttile should subract 1 but it doesn't.  Any clue why it's not?<br><br>Again, here's my code:<br><br><pre class=code>
; map editor

Graphics3D 800, 600, 32, 2

SetBuffer BackBuffer()

AppTitle("Map Editor v1.0")

Global t_menu = LoadImage("Graphics\t_menu.bmp")
	MaskImage t_menu, 0, 255, 0
Global b_menu = LoadImage("graphics\b_menu.bmp")
	MaskImage b_menu, 0, 255, 0
Global file = LoadImage("graphics\file.bmp")
	MaskImage file, 255, 255, 0
Global quit = LoadImage("graphics\quit.bmp")
	MaskImage quit, 255, 255, 0
Global Next1 = LoadImage("graphics\n.bmp")
	MaskImage Next1, 255,255,255
Global next2 = LoadImage("graphics\n2.bmp")
	MaskImage next2, 255,255,255
Global previous = LoadImage("graphics\p.bmp")
	MaskImage previous, 255, 255, 255
Global previous2 = LoadImage("graphics\p2.bmp")
	MaskImage previous2, 255,255,255
Global tiles = LoadAnimImage("Tiles\Terrains\terrain.bmp",64,64,0,5)
	MaskImage tiles, 255, 255, 255
Global pointer = LoadImage("graphics\mouse.bmp")

Dim map(32, 24)

Global currenttile#=1
Global mapx# = 32
Global mapy# = 24

Global gridon# = 0

Const erase#=0


While Not KeyHit(1)

	; pointer coordinates

	pointer_x# = MouseX()
	pointer_y# = MouseY()
	
	Cls
	
	update()
	
	DrawImage t_menu, 0, 0
	DrawImage file, 0, 0
	DrawImage quit, 73, 0
	DrawImage b_menu, 0, 540
	DrawImage tiles, 38, 540, currenttile
	DrawImage pointer, pointer_x, pointer_y
	
	; Button setup
	If ImagesOverlap(pointer, pointer_x, pointer_y, Next1, 102, 582) Then DrawImage next2, 102, 582
	If ImagesOverlap(pointer, pointer_x, pointer_y, Next1, 102, 582) And MouseHit(1) Then currenttile=currenttile+1
	If currenttile &gt; 4 Then currenttile = 0	
	If Not ImagesOverlap(pointer, pointer_x, pointer_y, next1, 102, 582) Then DrawImage Next1, 102, 582
	
	If ImagesOverlap(pointer, pointer_x, pointer_y, previous, 20, 582) Then DrawImage previous2, 20, 582
	If ImagesOverlap(pointer, pointer_x, pointer_y, previous, 20, 582) And MouseHit(1) Then currenttile=currenttile-1
	If currenttile &lt; 0 Then currenttile = 4
	If Not ImagesOverlap(pointer, pointer_x, pointer_y, previous, 20, 582) Then DrawImage previous, 20, 582
	
	If ImagesOverlap(pointer, pointer_x, pointer_y, quit, 73, 0) And MouseDown(1) Then End
	
	controls()

	Text 0,18,"X: "+MouseX()
	Text 0,28,"Y: "+MouseY()
	Text 0,38,"Press the 'G' key to enable the grid."
	
	Flip

Wend
End

; update

Function update()

For x#=0 To mapx#
	For y#=0 To mapy#
		DrawImage tiles, x#*64, y#*64,map(x#, y#)
	Next
Next

End Function

Function controls()

	tile_x# = MouseX()/64
	tile_y# = MouseY()/64

	If MouseDown(1) And tile_x# &lt; 32 And tile_y# &lt; 24 Then map(tile_x#, tile_y#)=currenttile#
	If MouseDown(2) And tile_x# &lt; 32 And tile_y# &lt; 24 Then map(tile_x#, tile_y#)=0
	
	If KeyHit(34) Then gridon# = +1
	
	grid()
	
	
End Function

Function grid()

If gridon# = 1
If gridon# &gt; 1 Then gridon = 0

For x# = 0 To mapx#
	For y# = 0 To mapy#
		Rect x#*64, y#*64, 64, 64, 0
	Next
Next

EndIf

End Function
</pre> <br><br></td></tr></table><br>
<a name="587540"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yeshu777</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> You are using both currenttile(int?) &amp; currenttile#(float?)<br><br>Not sure if Blitz overlooks this. <br><br>Also, as a matter of practive I always tend to use under scores between words (current_tile) - helps when you do stupid things like - currentttile and repeat letters within variable names.<br><br>Rgds, <br><br></td></tr></table><br>
<a name="587626"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Q</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> I changed it to a int but I still get the same problem. <br><br></td></tr></table><br>
<a name="587642"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >WolRon</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> The problem is your use of the command MouseHit().<br><br>Ironically, this is kinda similar to the image loading problem you encountered in the sense that you should only be doing it once per loop.<br><br>You see, there is a buffer that holds the number of times that the mouse was hit since the last time you called the MouseHit() command.  And every time you check that buffer with the MouseHit() command, the buffer is cleared (zeroed).<br><br>In your code you check for a MouseHit() on this line:<pre class=code>If ImagesOverlap(pointer, pointer_x, pointer_y, Next1, 102, 582) And MouseHit(1) Then currenttile=currenttile+1</pre>which is fine BUT then you check for it AGAIN on this line:<pre class=code>If ImagesOverlap(pointer, pointer_x, pointer_y, previous, 20, 582) And MouseHit(1) Then currenttile=currenttile-1</pre>Now, since calling the MouseHit() command the first time cleared the MouseHit buffer, the second call to it will give you an incorrect result of zero hits.  Do you understand this?<br><br>To avoid this from happening you should only make ONE call to MouseHit() per loop of your game and save that value in a variable.  Then you can check the value of that variable as many times as you want in your loop without it 'magically' changing value.<br><br>At my Programming Tutorial, I give a few examples of how to do this in my code examples.  Take a look.  Here is one example:<pre class=code>  wkey = KeyDown(17)				;collect user input
  skey = KeyDown(31)				;It's a good practice to collect these inputs only once
  akey = KeyDown(30)				;per loop.  This will prevent odd behaviors from happening,
  dkey = KeyDown(32)				;for instance if the state of a key changes between multiple
  mouse1 = MouseHit(1)				;checks of that key while still in the same loop.

;...later...
  If mouse1 = True				;check if left mouse button was pressed
    ;blah, blah, blah
</pre><br><br><br>Also, just to be helpful, really long lines of code like this:<pre class=code>If ImagesOverlap(pointer, pointer_x, pointer_y, Next1, 102, 582) Then DrawImage next2, 102, 582
If ImagesOverlap(pointer, pointer_x, pointer_y, Next1, 102, 582) And Mouse1 Then currenttile=currenttile+1
If currenttile &gt; 4 Then currenttile = 0	
If Not ImagesOverlap(pointer, pointer_x, pointer_y, next1, 102, 582) Then DrawImage Next1, 102, 582</pre>where you are using redundant If arguments can be reduced to a lot less code.  This not only helps make it more readable, but actually makes it faster too since you are only performing the redundant check once.<br>Try writing it like this (it works exactly the same way):<pre class=code>If ImagesOverlap(pointer, pointer_x, pointer_y, Next1, 102, 582)
	DrawImage next2, 102, 582
	If Mouse1 Then currenttile = currenttile + 1
	If currenttile &gt; 4 Then currenttile = 0	
Else
	DrawImage Next1, 102, 582
EndIf</pre>You see, we reduced the three ImagesOverlap checks to only one check. <br><br></td></tr></table><br>
<a name="587648"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Q</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks!  Where on your website can I find this tutorial, I can't find it? <br><br></td></tr></table><br>
<a name="587649"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >WolRon</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> ??  The link is in my sig.<br>BTW, I was editing my reply above some just a few minutes ago, so you might want to reread it, in case you didn't get the updated post. <br><br></td></tr></table><br>
<a name="587654"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Q</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm all about speed, I'll make that syntax a habit.  Thanks a ton! =) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
