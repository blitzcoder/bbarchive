<!DOCTYPE html><html lang="en" ><head ><title >Shooting Snowballs</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Shooting Snowballs</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Shooting Snowballs</a><br><br>
<a name="1122719"></a>

<a name="1122720"></a>

<a name="1122721"></a>

<a name="1122722"></a>

<a name="1122723"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Happy Llama</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm making a game where you throw snowballs at the enemy. Its not done yet so some things (like the Types) are not really important right now. Any way I've set up some code to fire a snowball every time the left mouse button is pressed. When I fire about 6-8 snowballs it comes up with the error "Illegal memory address" Can some one tell me what i'm doing wrong?!<br><br>Code:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Graphics3D 800,600,0,1
SetBuffer BackBuffer()

;variables
Const spacebar=57,escape=1,leftkey=208,rightkey=205
gravity=0.3
cam_y#=-3
cam_x#=0 

HidePointer

shoosh=LoadSound("swoosh.wmv")

;Types

Type p_snowball
Field damage=5
Field psys=0
End Type

Type e_snowball
Field damage=5
Field esys=0
End Type

Type player
Field health=100
End Type
 
Type enemy
Field health=100
End Type

AmbientLight 255,255,255


;Light
sun=CreateLight()

;Player
player=CreateCamera()
CameraFogRange player,0.1,400
CameraFogMode player,True
CameraClsColor player,100,130,150
CameraFogColor player,100,130,150


;Terrain
ground=CreatePlane()
PositionEntity ground,0,-5,0
EntityColor ground,200,200,200


;Sky
sky=CreatePlane()
PositionEntity sky,0,100,0
RotateEntity sky,-180,0,0
;Sky texture
skytex=LoadTexture("sky.jpg")
EntityTexture sky,skytex
ScaleTexture skytex,5,5
ScaleTexture skytex,500,500

;Load the enemy

enemy=LoadMesh("enemy.3ds")
PositionEntity enemy,0,-4,30
ScaleEntity enemy,0.5,0.5,0.5


;Make the snow forts:

;Fort 1
fort1=LoadMesh("snowfort.3ds")
PositionEntity fort1,0,-3.5,4
EntityColor fort1,255,255,255
ScaleEntity fort1,1,1,2.5
RotateEntity fort1,0,-90,0


;Fort 2
fort2=CreateCube()
PositionEntity fort2,0,-4,30
EntityColor fort2,255,255,255
ScaleEntity fort2,2,1,1
;Player Snowballs

fullcart=25
Dim bullet(fullcart)
For i=0 To fullcart
bullet(i)=CreateSphere(36)
ScaleEntity bullet(i),0.2,0.2,0.2
HideEntity bullet(i)
Next

;Making it snow!

Dim snowflake(1000)

For p=0 To 1000

snowflake(p)=CreateSphere()

ScaleEntity snowflake(p),0.05,0.05,0.05

PositionEntity snowflake(p),Rnd(-50,50),Rnd(1,100),Rnd(-50,50)

Next


;###############
;#  Main Loop  #
;###############
While Not KeyDown (escape)

;Shooting the player snowballs

If MouseDown(1) Then 
ShowEntity bullet(t)
PositionEntity bullet(t),EntityX(player,1),EntityY(player,1),EntityZ(player,1)
RotateEntity bullet(t),EntityPitch#(player),EntityYaw#(player),EntityRoll#(player)
t=t+1
EndIf

For q=0 To fullcart
MoveEntity bullet(q),0,snowball_y,0.5
Next

;Move the snow downwards
For l=0 To 1000
MoveEntity snowflake(l),0,Rnd(-0.002,-0.1),0
Next

;duck 

If KeyDown(57) Then  
cam_y=-3.7
Else
cam_y=-3
EndIf

PositionEntity player,cam_x,cam_y,3

;straffting  

If KeyDown(30) Then cam_x=cam_x-0.1

If KeyDown(32) Then cam_x=cam_x+0.1


If cam_x &gt; 0.5 Then cam_x=0.5
If cam_x &lt; -0.5 Then cam_x=-0.5 

;point the enemy at the player
PointEntity enemy,player

;FPS Camera Stuff
mxs#=mxs#+MouseXSpeed()
mys#=mys#+MouseYSpeed()

If mxs# &gt; 100 Then mxs# = 100
If mxs# &lt; -100 Then mxs# = -100

If mys# &gt; 40 Then mys# = 40
If mys# &lt; -80 Then mys# = -80

RotateEntity player,mys#,-mxs#,0

;MoveMouse 400,300



RenderWorld

UpdateWorld 

Color 0,0,0

Rect 10,10,100,100,False


Flip




Wend



End

</textarea><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1122730"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matty</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> First - put debug mode on - that should help you with finding where the error is.<br><br>I would guess however it is because you are exceeding the bounds of the array "bullet".<br><br>Notice that when the mouse is pressed you increment the bullet's index "t" but you never do anything to prevent it going outside the maximum size of the array (25 in this case). <br><br></td></tr></table><br>
<a name="1122806"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've updated your code a bit.  I would suggest changing from using an array for bullets to using types.  It's much easier to manage in my opinion.  Read through this as I've commented all the lines I added or changed.  (I also had to comment out the media as I did not have it)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Graphics3D 800,600,0,1
SetBuffer BackBuffer()

;variables
Const spacebar=57,escape=1,leftkey=208,rightkey=205
gravity=0.3
cam_y#=-3
cam_x#=0 



HidePointer

;shoosh=LoadSound("swoosh.wmv")

;Types

Type p_snowball
Field damage=5
Field psys=0
End Type

Type e_snowball
Field damage=5
Field esys=0
End Type

Type player
Field health=100
End Type
 
Type enemy
Field health=100
End Type
;-----------------------------------------------------------------------------------------------------
;-----------------------------------------------------------------------------------------------------
;-------------------------------------------------------------------------------------------
;I added these lines here:  Take a look at how they work.  I find it MUCH easier to use a 
;type for projectiles instead of an array.

Global magazine	= 25	;this can hold the total number of snowballs the player has available (25)

Type snowball
	Field mesh	;This will hold the actual physical mesh of the snowball
	Field id	;This can tell you who threw the snowball, such as 1 for player, 2 for enemy
	Field damage;This can hold the value you want for damage as you had above
End Type
;-----------------------------------------------------------------------------------------------------
;-----------------------------------------------------------------------------------------------------


AmbientLight 255,255,255


;Light
sun=CreateLight()

;Player
player=CreateCamera()
CameraFogRange player,0.1,400
CameraFogMode player,True
CameraClsColor player,100,130,150
CameraFogColor player,100,130,150


;Terrain
ground=CreatePlane()
PositionEntity ground,0,-5,0
EntityColor ground,200,200,200


;Sky
sky=CreatePlane()
PositionEntity sky,0,100,0
RotateEntity sky,-180,0,0
;Sky texture
;skytex=LoadTexture("sky.jpg")
;EntityTexture sky,skytex
;ScaleTexture skytex,5,5
;ScaleTexture skytex,500,500

;Load the enemy

;enemy=LoadMesh("enemy.3ds")
enemy=CreateCylinder()
PositionEntity enemy,0,-4,30
ScaleEntity enemy,0.5,0.5,0.5


;Make the snow forts:

;Fort 1
fort1=CreateCube()
PositionEntity fort1,0,-3.5,4
EntityColor fort1,255,255,255
ScaleEntity fort1,1,1,2.5
RotateEntity fort1,0,-90,0


;Fort 2
fort2=CreateCube()
PositionEntity fort2,0,-4,30
EntityColor fort2,255,255,255
ScaleEntity fort2,2,1,1
;Player Snowballs

;-----------------------------------------------------------------------------------------------------
;-----------------------------------------------------------------------------------------------------
;create just a simple bullet mesh for the snowballs
bullet=CreateSphere(36)
ScaleEntity bullet,0.2,0.2,0.2
HideEntity bullet
;-----------------------------------------------------------------------------------------------------
;-----------------------------------------------------------------------------------------------------

;Making it snow!

Dim snowflake(1000)

For p=0 To 1000

snowflake(p)=CreateSphere()

ScaleEntity snowflake(p),0.05,0.05,0.05

PositionEntity snowflake(p),Rnd(-50,50),Rnd(1,100),Rnd(-50,50)

Next


;###############
;#  Main Loop  #
;###############
While Not KeyDown (escape)


;-----------------------------------------------------------------------------------------------------
;-----------------------------------------------------------------------------------------------------

;Shooting the player snowballs

If MouseHit(1) And magazine &gt; 0	;check if the player has snowballs left (more than 0)
	s.snowball = New snowball 		;create a new snowball
	s\mesh = CopyEntity(bullet)		;we'll copy the bulletmesh you've already loaded
	s\id = 1						;Lets say 1 is from the player, 2 is from the enemy
	s\damage = 5					;Damage will be 5 still
	PositionEntity s\mesh,EntityX(player),EntityY(player),EntityZ(player)			;position it correctly
	RotateEntity s\mesh, EntityPitch(player),EntityYaw(player),EntityRoll(player)	;and orientate it correctly
	magazine = magazine -1			;this will subtract 1 snowball from the player's stash of snowballs
End If 


;here is where we'll move the snowballs
For s.snowball = Each snowball		;for each snowball
	MoveEntity s\mesh, 0,0,1		;move it forward
	If Abs(EntityZ(s\mesh)) &gt; 400 	;if the snowball gets beyond 400 units
		FreeEntity s\mesh			;free the mesh from memory
		Delete s					;and delete the reference to it
	End If
Next
;-----------------------------------------------------------------------------------------------------
;-----------------------------------------------------------------------------------------------------

		

;Move the snow downwards
For l=0 To 1000
MoveEntity snowflake(l),0,Rnd(-0.002,-0.1),0
Next

;duck 

If KeyDown(57) Then  
cam_y=-3.7
Else
cam_y=-3
EndIf

PositionEntity player,cam_x,cam_y,3

;straffting  

If KeyDown(30) Then cam_x=cam_x-0.1

If KeyDown(32) Then cam_x=cam_x+0.1


If cam_x &gt; 0.5 Then cam_x=0.5
If cam_x &lt; -0.5 Then cam_x=-0.5 

;point the enemy at the player
PointEntity enemy,player

;FPS Camera Stuff
mxs#=mxs#+MouseXSpeed()
mys#=mys#+MouseYSpeed()

If mxs# &gt; 100 Then mxs# = 100
If mxs# &lt; -100 Then mxs# = -100

If mys# &gt; 40 Then mys# = 40
If mys# &lt; -80 Then mys# = -80

RotateEntity player,mys#,-mxs#,0

;MoveMouse 400,300



RenderWorld

UpdateWorld 

Color 0,0,0

Rect 10,10,100,100,False

;-----------------------------------------------------------------------------------------------------
;Display how many showballs you have left:
Text 350,0,"Snowballs Left: " + magazine

;here's where we make sure the snowballs are deleted properly
;count them 


count = 0	;create a variable to hold the amount of snowballs counted
For s.snowball = Each snowball	;for each snowball
	count = count + 1			;add 1 to the count!
Next
Text 350,10,"Snowballs tracked: " + count	;then display it here. 
											;This can obviously be deleted later as it's just for debugging purposes
;-----------------------------------------------------------------------------------------------------



Flip




Wend



End
</textarea><br><br>You can of course put each process into it's own function to greatly simplify the code.  I've also added the ability to count each bullet and delete it when it gets too far off the screen. <br><br></td></tr></table><br>
<a name="1123146"></a>

<a name="1123280"></a>

<a name="1123281"></a>

<a name="1123282"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Happy Llama</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Edit: Any suggestions on reloading?<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1123548"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Happy Llama</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'll just make a new post. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
