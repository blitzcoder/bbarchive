<!DOCTYPE html><html lang="en" ><head ><title >Question about Stevie G's code.</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Question about Stevie G's code.</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Question about Stevie G's code.</a><br><br>
<a name="1049475"></a>

<a name="1049476"></a>

<a name="1049477"></a>

<a name="1049478"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cubed Inc.</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> About a week ago, I saw a post of some user asking how to do standard 3rd person controls (mario 64) and the user Stevie G posted this code<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 640,480,16,1

Global LIGHT = CreateLight()
Global PLANE = CreatePlane() 
EntityColor PLANE, 200,100,100
texture = CreateTexture( 32, 32 )
SetBuffer TextureBuffer( texture )
For y = 0 To 1
	For x = 0 To 1
		Color 100, 150+25*( ( x+y) Mod 2 ) , 100
		Rect x*16,y*16,16,16,1
	Next
Next
SetBuffer BackBuffer()
ScaleTexture texture, 50,50
EntityTexture PLANE, texture
FreeTexture texture

Global CUBE = CreateCube() : FitMesh CUBE, -2,0,-2,4,10,4 
Tmp = CreateCube() : FitMesh Tmp, -1,7,2,2,2,2 : AddMesh Tmp, CUBE : FreeEntity Tmp
Global TARGET = CreatePivot()

Global CAMERApivot = CreatePivot()
Global CAMERA = CreateCamera( CAMERApivot )
PositionEntity CAMERA, 0,50,-50
PointEntity CAMERA , CAMERApivot

While Not KeyDown(1)

	PositionEntity CAMERApivot , EntityX( CUBE ) , 0, EntityZ( CUBE )
	TurnEntity CAMERApivot, 0 , KeyDown(31) - KeyDown(30) , 0
	
	;move relative to camera
	Mx# = KeyDown(205) - KeyDown(203)
	Mz# = KeyDown(200) - KeyDown(208 )

	If Mx &lt;&gt; 0 Or Mz &lt;&gt; 0
		;move relative to camera
		TFormVector Mx, 0, Mz , CAMERApivot , 0
		TranslateEntity CUBE, TFormedX(), 0, TFormedZ()
		;turn relative to movement
		PositionEntity TARGET, EntityX( CUBE ) + TFormedX() , 0, EntityZ( CUBE ) + TFormedZ()
		TurnEntity CUBE, 0 , DeltaYaw( CUBE, TARGET ) * .25, 0
	EndIf
	
	RenderWorld()
	Flip

Wend
</textarea><br><br>The only problem with this code was that the players speed is faster when he is pointed at and angled direction that he is when pointed left and right or front and back.<br><br>Another user whose name I cannot remember fixed this problem so that it doesn't do that. <br><br>Now that I am looking for the thread that had this, I can't find it. Maybe it was in the programmers section instead of the beginners area, although I checked.<br><br>If anyone knows about the thread that I am talking about or if stevie g sees this, can you tell me what happened to the thread and where it is?<br><br><font class="tiny"><i>Last edited 2010</i></font><br><br><font class="tiny"><i>Last edited 2010</i></font><br><br><font class="tiny"><i>Last edited 2010</i></font> <br><br></td></tr></table><br>
<a name="1049481"></a>

<a name="1049482"></a>

<a name="1049483"></a>

<a name="1049484"></a>

<a name="1049501"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >puki</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> About a week ago <br></div><br>Are you sure it was him?<br><br><a href="http://www.blitzbasic.com/Community/userposts.php?user=5891" target="_blank">http://www.blitzbasic.com/Community/userposts.php?user=5891</a> <br><br></td></tr></table><br>
<a name="1049532"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cubed Inc.</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> puki <br>Its definintly his code. I'm trying to find the thread that had the problem with stevie g's code fixed. <br><br></td></tr></table><br>
<a name="1049573"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>It's just a case of normalising the Mx + Mz variables before movement.  See below:<br><br><pre class=code>
	If Mx &lt;&gt; 0 Or Mz &lt;&gt; 0
		;normalise direction
		D# = Sqr( Mx * Mx + Mz * Mz )
		Mx = Mx / D 
		Mz = Mz / D
		;move relative to camera
		TFormVector Mx, 0, Mz , CAMERApivot , 0
		TranslateEntity CUBE, TFormedX(), 0, TFormedZ()
		;turn relative to movement
		PositionEntity TARGET, EntityX( CUBE ) + TFormedX() , 0, EntityZ( CUBE ) + TFormedZ()
		TurnEntity CUBE, 0 , DeltaYaw( CUBE, TARGET ) * .25, 0
	EndIf
</pre> <br><br></td></tr></table><br>
<a name="1049702"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cubed Inc.</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Stevie G<br>The code I saw was different and even simpler. The guy did the same thing with your code, except he used a variable named speed. I'm trying to take the fixed version of your code, and add acceleration( like when a car moves ) <br><br></td></tr></table><br>
<a name="1049737"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Why not use the search facility?  I searched for "Mario 64" and this is the first post I found that I had been involved in:<br><br><a href="http://www.blitzbasic.co.nz/Community/posts.php?topic=62205#695441" target="_blank">http://www.blitzbasic.co.nz/Community/posts.php?topic=62205#695441</a><br><br>Here is a car thing I posted a looong time ago - not sure if this is the kind of thing you are after..<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 640,480,16,1

Global plane = CreatePlane():EntityColor plane, 50,50,100:EntityAlpha plane, .75
texture = CreateTexture( 16,16)
SetBuffer TextureBuffer( texture )
Color 0,0,0:Rect 0,0,16,16,1
Color 50,50,200:Rect 0,0,16,16,0
SetBuffer BackBuffer()
ScaleTexture texture,20,20
EntityTexture plane, texture
FreeTexture texture

Global camera = CreateCamera():PositionEntity camera, 0, 50, -100
Global mirror = CreateMirror()

Global light = CreateLight():RotateEntity light , 15,-45,0

Type Car
	Field Model
	Field Wheel[3]
	Field WheelRotation#
	Field Wheelradius#
	Field TopSpeed#
	Field Drag#
	Field Acceleration#
	Field Brakes#
	Field Speed#
	Field Vx#, Vy#, Vz#
End Type

Global MyCar.car = CARcreate( 4, 3, 6, 2, 3 )

While Not KeyDown(1)

	CARupdate( MyCar )
	PointEntity camera, MyCar\model
	CameraZoom camera, EntityDistance( camera, MyCar\model ) / 100
	RenderWorld()
	Flip

Wend

;==========================================
;==========================================
;==========================================

Function CARupdate( c. car )
	
	;user input
	jx# = KeyDown(203) - KeyDown(205 )
	jb#  = KeyDown( 200)
	
	TurnEntity c\Model, 0, jx * 3.0,0
	For w = 0 To 1
		RotateEntity c\wheel[w], 0 , jx*45, 0
	Next
	
	c\Speed = LIMIT ( c\Speed + ( jb * c\Acceleration ) - ( (Not jb ) * c\Brakes ) , 0, c\TopSpeed )
	TFormVector 0,0,(1.0 - c\drag ), c\Model, 0
	c\Vx = ( c\Vx * c\drag ) + ( TFormedX() * c\Speed )
	c\Vz = ( c\Vz * c\drag ) + ( TFormedZ() * c\Speed )
	c\Vy = ( c\Vy * c\drag ) + ( TFormedY() * c\Speed ) 
	speed# = Sqr( c\Vx*c\Vx + c\Vy*c\Vy + c\Vz*c\Vz )

	TranslateEntity c\Model, c\Vx, c\vy , c\Vz
	
	c\WheelRotation = c\WheelRotation + Speed * 90.0 / ( Pi * c\WheelRadius )  
	For w = 0 To 3
		RotateEntity c\wheel[ w] , c\WheelRotation, EntityYaw( c\wheel[w] ) , 0
	Next

End Function

;==========================================
;==========================================
;==========================================

Function CARcreate.car( w#, h#, d# , tw# , tr# )

	c.car = New car
	c\Acceleration = .0075
	c\Brakes = .005 
	c\Drag = .95
	c\Speed = 0.0
	c\TopSpeed = 1.25
	c\Wheelradius = tw
		
	c\Model = CreateCube()
	FitMesh c\Model , -w,-h,-d, w*2,h*2,d*2
	UpdateNormals c\Model
	EntityColor c\Model,255,255,0
	
	Wheel = CreateCylinder( 5 )
	RotateMesh Wheel,0,0,90
	ScaleMesh Wheel , tw,tr,tr
	UpdateNormals Wheel
	EntityColor Wheel, 64,64,64

	For l= 0 To 3
		dx = ( l &gt;0 And l&lt;3 ) - ( l = 0 Or l = 3 )
		dz = ( l &lt; 2 ) - ( l &gt; 1 )
		c\wheel[l] = CopyEntity(wheel , c\Model )
		PositionEntity c\wheel[l], w*dx , -h , d*dz
	Next
	
	PositionEntity c\Model, 0 , tr+h, 0
	
	FreeEntity Wheel
	
	Return c
	
End Function

;==========================================
;==========================================
;==========================================

Function LIMIT#(n#,n1#,n2#)
	
	If n&gt;n2 Then n=n2
	If n&lt;n1 Then n=n1	
	Return n

End Function
</textarea> <br><br></td></tr></table><br>
<a name="1049740"></a>

<a name="1049741"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cubed Inc.</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Stevie G <br><br>Would you have happen to remember any other posts that you have posted in? The version of your code that I am looking for was in a post of someone asking how to create a control system like in mario 64. <br><br>I'd be extemely grateful if you did.<br><br>P.S That car example is fantastic. I really like the zooming and panning that the camera does.<br><br><font class="tiny"><i>Last edited 2010</i></font> <br><br></td></tr></table><br>
<a name="1049742"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Search for "Mario 64" in all forums and hunt through the posts like I did. <br><br></td></tr></table><br>
<a name="1049857"></a>

<a name="1049875"></a>

<a name="1049876"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cubed Inc.</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Stevie G <br>I found it, but could you help me with one more thing.<br>The reason why I wanted to find the code was becuase I wanted to add "acceleration" to it, like when a person runs.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 640,480,16,1

Global LIGHT = CreateLight()
Global PLANE = CreatePlane() 
EntityColor PLANE, 200,100,100
texture = CreateTexture( 32, 32 )
SetBuffer TextureBuffer( texture )
For y = 0 To 1
	For x = 0 To 1
		Color 100, 150+25*( ( x+y) Mod 2 ) , 100
		Rect x*16,y*16,16,16,1
	Next
Next
SetBuffer BackBuffer()
ScaleTexture texture, 50,50
EntityTexture PLANE, texture
FreeTexture texture

Global CUBE = CreateCube() : FitMesh CUBE, -2,0,-2,4,10,4 
Tmp = CreateCube() : FitMesh Tmp, -1,7,2,2,2,2 : AddMesh Tmp, CUBE : FreeEntity Tmp
Global TARGET = CreatePivot()

Global CAMERApivot = CreatePivot()
Global CAMERA = CreateCamera( CAMERApivot )
PositionEntity CAMERA, 0,30,-70

While Not KeyDown(1)

	PositionEntity CAMERApivot , EntityX( CUBE ) , 0, EntityZ( CUBE )
	TurnEntity CAMERApivot, 0 , KeyDown(30) - KeyDown(31) , 0
	TurnEntity CAMERApivot, 0 , KeyDown(203) - KeyDown(205) , 0
	
	;move relative to camera
	Mx# = KeyDown(205) - KeyDown(203)
	Mz# = KeyDown(200) - KeyDown(208)

speed1# = speed1

	If Mx &lt;&gt; 0 Or Mz &lt;&gt; 0
		;move relative to camera
		TFormNormal Mx, 0, Mz , CAMERApivot , 0
		TranslateEntity CUBE, TFormedX() * speed1 , 0, TFormedZ() * speed1
		;turn relative to movement
		PositionEntity TARGET, EntityX( CUBE ) + TFormedX() , 0, EntityZ( CUBE ) + TFormedZ()
		TurnEntity CUBE, 0 , DeltaYaw( CUBE, TARGET ) * .15, 0
	EndIf
	
	If KeyDown(200)
			speed1=speed1+.2
			
			If speed1&gt;1.5 speed1=1.5
			
		Else If KeyDown(208)
		
			speed1=speed1+.2
			
			If speed1&gt;1.5 speed1=1.5
			
		Else If KeyDown(205)
		
			speed1=speed1+.2
			
			If speed1&gt;1.5 speed1=1.5
			
        Else If KeyDown(203)

			speed1=speed1+.2
			
			If speed1&gt;1.5 speed1=1.5
	
			
		Else
			speed1=speed1=2
EndIf


UpdateWorld()
PointEntity CAMERA , CUBE
	RenderWorld()
	Flip

Wend

End

</textarea><br><br><br><br>I was able to add the acceleration when the player moves forward, but when the player stops, the player stops dead on his tracks, were when a somebody whos running stops, they don't just stop like that. <br><br>How could I fix this?<br><br><font class="tiny"><i>Last edited 2010</i></font><br><br><font class="tiny"><i>Last edited 2010</i></font> <br><br></td></tr></table><br>
<a name="1049898"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Like this?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 640,480,16,1

Global LIGHT = CreateLight()
Global PLANE = CreatePlane() 
EntityColor PLANE, 200,100,100
texture = CreateTexture( 32, 32 )
SetBuffer TextureBuffer( texture )
For y = 0 To 1
	For x = 0 To 1
		Color 100, 150+25*( ( x+y) Mod 2 ) , 100
		Rect x*16,y*16,16,16,1
	Next
Next
SetBuffer BackBuffer()
ScaleTexture texture, 50,50
EntityTexture PLANE, texture
FreeTexture texture

Global CUBE = CreateCube() : FitMesh CUBE, -2,0,-2,4,10,4 
Tmp = CreateCube() : FitMesh Tmp, -1,7,2,2,2,2 : AddMesh Tmp, CUBE : FreeEntity Tmp
Global TARGET = CreatePivot()

Global CAMERApivot = CreatePivot()
Global CAMERA = CreateCamera( CAMERApivot )
PositionEntity CAMERA, 0,30,-70


;*******
Global VelX#, VelZ# 
Global Acc# = .1, Drag# = .95	;top speed = Acc / ( 1.0 - Drag )
Global AccX#, AccZ#
Global Speed#

While Not KeyDown(1)

	PositionEntity CAMERApivot , EntityX( CUBE ) , 0, EntityZ( CUBE )
	TurnEntity CAMERApivot, 0 , KeyDown(30) - KeyDown(31) , 0
	TurnEntity CAMERApivot, 0 , KeyDown(203) - KeyDown(205) , 0
	
	;get input
	Mx# = KeyDown(205) - KeyDown(203)
	Mz# = KeyDown(200) - KeyDown(208)
	;get acceleration relative to camera
	If Mx &lt;&gt; 0 Or Mz &lt;&gt; 0
		TFormNormal Mx, 0, Mz , CAMERApivot , 0
		AccX = TFormedX() * Acc
		AccZ = TFormedZ() * Acc
	Else
		AccX = 0
		AccZ = 0
	EndIf
	
	;Calculate velocity
	VelX = VelX * Drag + AccX
	VelZ = VelZ * Drag + AccZ
	TranslateEntity CUBE, VelX , 0, VelZ

	Speed = Sqr( VelX * VelX + VelZ * VelZ )

	;turn relative to movement
	If Speed &gt; 0
		PositionEntity TARGET, EntityX( CUBE ) + VelX , 0, EntityZ( CUBE ) + VelZ
		TurnEntity CUBE, 0 , DeltaYaw( CUBE, TARGET ) * .15, 0
	EndIf

	UpdateWorld()
	PointEntity CAMERA , CUBE
	RenderWorld()
	Flip

Wend

End

</textarea> <br><br></td></tr></table><br>
<a name="1049957"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cubed Inc.</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Stevie G<br>It's almost perfect. The only problem with it is that now you turn too slowly and when you turn left and right, it doesn't turn all the way.<br><br>Would you by any chance be able to fix it? <br><br>Sorry for asking for so much but it so close to perfection that I can't help but to ask. <br><br></td></tr></table><br>
<a name="1050011"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Increase the .15 in this line of code to a number between 0 and 1 which suits.<br><br>TurnEntity CUBE, 0 , DeltaYaw( CUBE, TARGET ) * .15, 0 <br><br></td></tr></table><br>
<a name="1050081"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cubed Inc.</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Stevie G<br>It didn't work.<br>Would it be possible for you to keep the turning from edit#9 and also keep the acceleration and drag from post #10?<br><br>I'd really appreciate it. <br><br></td></tr></table><br>
<a name="1050101"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Robert Cummings</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> At this point you should begin trying to understand why that code works so you can become self-sufficient... <br><br></td></tr></table><br>
<a name="1050141"></a>

<a name="1050143"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> @ TaGames, <br><br>I think this is what you're looking for..<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 640,480,16,1

Global LIGHT = CreateLight()
Global PLANE = CreatePlane() 
EntityColor PLANE, 200,100,100
texture = CreateTexture( 32, 32 )
SetBuffer TextureBuffer( texture )
For y = 0 To 1
	For x = 0 To 1
		Color 100, 150+25*( ( x+y) Mod 2 ) , 100
		Rect x*16,y*16,16,16,1
	Next
Next
SetBuffer BackBuffer()
ScaleTexture texture, 50,50
EntityTexture PLANE, texture
FreeTexture texture

Global CUBE = CreateCube() : FitMesh CUBE, -2,0,-2,4,10,4 
Tmp = CreateCube() : FitMesh Tmp, -1,7,2,2,2,2 : AddMesh Tmp, CUBE : FreeEntity Tmp
Global TARGET = CreatePivot()

Global CAMERApivot = CreatePivot()
Global CAMERA = CreateCamera( CAMERApivot )
PositionEntity CAMERA, 0,30,-70


;*******
Global VelX#, VelZ# 
Global Acc# = .1, Drag# = .95	;top speed = Acc / ( 1.0 - Drag )
Global AccX#, AccZ#
;Global Speed#

While Not KeyDown(1)

	PositionEntity CAMERApivot , EntityX( CUBE ) , 0, EntityZ( CUBE )
	TurnEntity CAMERApivot, 0 , KeyDown(30) - KeyDown(31) , 0
	TurnEntity CAMERApivot, 0 , KeyDown(203) - KeyDown(205) , 0
	
	;get input
	Mx# = KeyDown(205) - KeyDown(203)
	Mz# = KeyDown(200) - KeyDown(208)
	;get acceleration relative to camera
	If Mx &lt;&gt; 0 Or Mz &lt;&gt; 0
		TFormNormal Mx, 0, Mz , CAMERApivot , 0
		AccX = TFormedX() * Acc
		AccZ = TFormedZ() * Acc
	Else
		AccX = 0
		AccZ = 0
	EndIf
	
	;Calculate velocity
	VelX = VelX * Drag + AccX
	VelZ = VelZ * Drag + AccZ
	TranslateEntity CUBE, VelX , 0, VelZ

	;Speed = Sqr( VelX * VelX + VelZ * VelZ )

	;turn relative to movement
	If AccX &lt;&gt; 0 Or AccZ &lt;&gt; 0 ;Speed &gt; 0
		PositionEntity TARGET, EntityX( CUBE ) + AccX , 0, EntityZ( CUBE ) + AccZ
		TurnEntity CUBE, 0 , DeltaYaw( CUBE, TARGET ) * .15, 0
	EndIf

	UpdateWorld()
	PointEntity CAMERA , CUBE
	RenderWorld()
	Flip

Wend

End
</textarea><br><br>I think I've helped you enough for now.  As Robert says, it's time to help yourself and have a play about with the code and try to understand what is going on.  <br><br>I'm not coding your game for you I'm afraid.  <br><br>Cheers<br>Stevie<br><br><font class="tiny"><i>Last edited 2010</i></font> <br><br></td></tr></table><br>
<a name="1050286"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cubed Inc.</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Stevie G <br>I wasn't expecting you to code my game, I just wanted to ask a few questions.<br><br>Thanks for all the help. It was greatly appreciated. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
