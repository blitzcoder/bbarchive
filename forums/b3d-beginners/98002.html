<!DOCTYPE html><html lang="en" ><head ><title >CopyRect and Image Buffers</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >CopyRect and Image Buffers</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >CopyRect and Image Buffers</a><br><br>
<a name="1141926"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm basically just wanting to load a bunch of single, identically-sized images and re-export them as a single inline imagestrip.<br>I figured it would be quick and easy (Har Har Har...) to just use Blitz to do this.<br><br>However, I seem to just get black 'empty' images. So the Images are created and ecxported okay, The sizes seem okay, just the read buffer data doesn't seem to be written.<br>From what I can see, the problem may be with CopyRect command when neither the Source/Destination Buffers are the Front/Back Buffers.<br><br>The Documentation/Examples doesn't seem at all specific on the issue, and any other results from Searches just seem to use the Front/Back buffers themselves.<br><br>Have I missed something (probably painfully blatant) in this code???<br><br><br><pre class=code>
Graphics 1024,768,32,2

Const UI_ICONS=21
Const UI_WIDTH=48
Const UI_HEIGHT=48

Const FX_ICONS=11
Const FX_WIDTH=48
Const FX_HEIGHT=48

Global UIImage=CreateImage(UI_WIDTH*UI_ICONS,UI_HEIGHT)
Global FXImage=CreateImage(FX_WIDTH*FX_ICONS,FX_HEIGHT)

MakeUIImage
MakeFXImage

SaveImage(UIImage,"UIpppp.bmp")
SaveImage(FXImage,"FXpppp.bmp")

FreeImage UIImage
FreeImage FXImage

Function MakeUIImage()
	Local Path$="F:\bb\Blitz\WIP\Imgs\"	
	Local Directory=ReadDir(Path)
	Local File
	
	Local FileName$=NextFile(Directory)
	Local FullPath$=""
	Local Frame
	While (FileName$&lt;&gt;"")
		FullPath=Path+FileName
		If (FileType(FullPath)=1)
			If (Len(FileName)&gt;4)
				If ((Left(FileName,2)="UI") And (Right(FileName,4)=".png"))
					Frame=Int(Mid(FileName,3,Instr(FileName,".")-3))
					File=LoadImage(FullPath)
					SetBuffer ImageBuffer(UIImage)
					CopyRect 0,0,UI_WIDTH,UI_HEIGHT,Frame*UI_WIDTH,0,ImageBuffer(File),ImageBuffer(UIImage)
					FreeImage File
				End If
			End If
		End If
		FileName$=NextFile(Directory)
	Wend
	CloseDir Directory
End Function

Function MakeFXImage()
	Local Path$="F:\bb\Blitz\WIP\Imgs\"
	
	Local Directory=ReadDir(Path)
	Local File
	
	Local FileName$=NextFile(Directory)
	Local FullPath$=""
	Local Frame
	While (FileName$&lt;&gt;"")
		FullPath=Path+FileName
		If (FileType(FullPath)=1)
			If (Len(FileName)&gt;4)
				If ((Left(FileName,2)="FX") And (Right(FileName,4)=".png"))
					Frame=Int(Mid(FileName,3,Instr(FileName,".")-3))
					File=LoadImage(FullPath)
					CopyRect 0,0,FX_WIDTH,FX_HEIGHT,Frame*FX_WIDTH,0,ImageBuffer(File),ImageBuffer(FXImage)
					FreeImage File
				End If
			End If
		End If
		FileName$=NextFile(Directory)
	Wend
	CloseDir Directory
End Function

</pre><br>NOTE:They shouldnt exceed the DX image size limitations. I think the largest strip is just over 1000 pixels <br><br></td></tr></table><br>
<a name="1141933"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Be systematic.<br><br><br>Does CopyRect work? Load ONE image, CopyRect to another image, draw both and look.<br><br>Are the file names correct? Display them and look.<br><br>Are images loaded properly?  Draw them and look.<br><br>Continue until everything works. <br><br></td></tr></table><br>
<a name="1142037"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bobysait</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> for purpose only :<br><br><pre class=code>Graphics 1008,48,0,2

; Create a temporary folder to store temp bmp images
Local rep$="F:\bb\Blitz\WIP\Imgs\temp\"
if filteype (rep)=0 then createdir(rep)

; create some fake icons (48*48 with random color)
Local icon=0
For icon=0 To 20
	Local img=CreateImage(48,48)
	SetBuffer ImageBuffer(img)
		Color Rand(50,255),Rand(50,255),Rand(50,255)
		Rect 0,0,48,48,1
		Color Rand(1,255),Rand(1,255),Rand(1,255)
		Text 24,24,Str(icon),1,1
	SetBuffer BackBuffer()
	; save the icons as bmp
	SaveImage img, rep+"UI"+icon+".bmp"
	; for debug : show the icons
	DrawImage img,icon*48,0
	Flip True
Next

WaitKey
End
</pre><br>this will create some sample-images to test your copyrect thing<br><br>Then you can launch your code with this modified function :<br><pre class=code>
Function MakeUIImage()
	Local Path$="F:\bb\Blitz\WIP\Imgs\temp\"
	Local Directory=ReadDir(Path)
	Local File
	
	Local FileName$=NextFile(Directory)
	Local FullPath$=""
	Local Frame
	While (FileName$&lt;&gt;"")
		FullPath=Path+FileName
		If (FileType(FullPath)=1)
			If (Len(FileName)&gt;4)
				If ((Lower(Left(FileName,2))="ui") And (Lower(Right(FileName,4))=".bmp"))
					Frame=Int(Mid(FileName,3,Instr(FileName,".")-3))
					File=LoadImage(FullPath)
					SetBuffer ImageBuffer(UIImage)
					CopyRect 0,0,UI_WIDTH,UI_HEIGHT,Frame*UI_WIDTH,0,ImageBuffer(File),ImageBuffer(UIImage)
					FreeImage File
				End If
			End If
		End If
		FileName$=NextFile(Directory)
	Wend
	CloseDir Directory
End Function</pre><br><br><br>it works for me. If it doesn't for you, then it's probably something wrong from your instalation or your graphics card that does not support something (?)<br>If it works, then the problem might come from your png images.<br>Have you tried just drawing them in blitz to be sure they are correctly loaded ?<br><br>also, take care of your icons start identifier (I made them start at "0" because you use frame*FX_WIDTH for the copyrect-X-destination) <br><br></td></tr></table><br>
<a name="1143484"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi, sorry I have been away for the last coupla weeks, but I'm back and have done some testing.<br><br>I thought I'd see if it may be perhaps something to do with copying from AnimImage frames or various different methods. The following all worked perfectly.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics 800,600,32,6
SetBuffer BackBuffer()

Global FILEPATH_1$
Global FILEPATH_2$

TestCopyFromImage

TestCopyFromFrame

TestCopyBackFromFrames



Function TestCopyFromImage()
	
	
	Init
	
	
	Local Icon1=LoadImage(FILEPATH_1)
	Local Icon2=LoadImage(FILEPATH_2)
	
	Local CombinedImage=CreateImage(96,48)
	
	TestCopyImage(CombinedImage,0,Icon1)
	TestCopyImage(CombinedImage,48,Icon2)
	
	FreeImage Icon1
	FreeImage Icon2
	
	DrawImage CombinedImage,0,0
	Color 255,255,255
	
	Text 100,0,"Image Combined as long image"
	
	Flip
	
	FreeImage CombinedImage
	
	TestPause
End Function

Function TestCopyFromFrame()
	
	
	Init
	
	
	Local Icon1=LoadImage(FILEPATH_1)
	Local Icon2=LoadImage(FILEPATH_2)
	
	Local CombinedImage=CreateImage(48,48,2)
	
	TestCopyFrame(CombinedImage,Icon1,1)
	TestCopyFrame(CombinedImage,Icon2,2)
	
	FreeImage Icon1
	FreeImage Icon2
	
	DrawImage CombinedImage,0,0,0
	DrawImage CombinedImage,48,0,1
	
	Color 255,255,255
	
	Text 100,0,"Image Combined as individual Frames"
	
	Flip
	
	FreeImage CombinedImage
	
	TestPause
	
End Function

Function TestCopyBackFromFrames()
	
	
	Init
	
	
	Local Icon1=LoadImage(FILEPATH_1)
	Local Icon2=LoadImage(FILEPATH_2)
	
	Local CombinedImage=CreateImage(48,48,2)
	
	TestCopyFrame(CombinedImage,Icon1,1)
	TestCopyFrame(CombinedImage,Icon2,2)
	
	FreeImage Icon1
	FreeImage Icon2
	
	Icon1=CreateImage(48,48)
	Icon2=CreateImage(48,48)
	
	CopyRect 0,0,48,48,0,0,ImageBuffer(CombinedImage,0),ImageBuffer(Icon1)
	CopyRect 0,0,48,48,0,0,ImageBuffer(CombinedImage,1),ImageBuffer(Icon2)
	
	FreeImage CombinedImage
	
	DrawImage Icon1,0,0
	DrawImage Icon2,48,0
	
	Color 255,255,255
	
	Text 100,0,"Images Separated from Combined Frames"
	
	Flip
	
	FreeImage Icon1
	FreeImage Icon2
	
	TestClose
End Function

Function Init()
	
	If (FILEPATH_1="")
	
		FILEPATH_1$=BuildPath(1)
		FILEPATH_2$=BuildPath(2)
	End If
	
	If (FileType(FILEPATH_1))
		DeleteFile FILEPATH_1
		DeleteFile FILEPATH_2
		SaveImages
	End If
	
	Cls
End Function

Function BuildPath$(frame=1)
	Local Dir$=CurrentDir()
	Local FileNamePrefix$="Image"
	Local FileNameSuffix$=".bmp"
	
	Return Dir+FileNamePrefix+Str(frame)+FileNameSuffix
End Function 

Function SaveImages()
	BuildImage(1)
	BuildImage(2)
	
	SetBuffer BackBuffer()
End Function

Function BuildImage(Frame)
	Local File=CreateImage(48,48)
	
	SetBuffer ImageBuffer(File)
	Color(255*(Frame=2),255*(Frame=1),0)
	Oval 24,24,24,24,1
	
	Local Path$=BuildPath(Frame)
	
	SaveBuffer(ImageBuffer(File),Path)
	
	FreeImage File
End Function

Function TestCopyImage(Image,X,Icon)
	CopyRect 0,0,48,48,X,0,ImageBuffer(Icon),ImageBuffer(Image)
End Function

Function TestCopyFrame(Image,Icon,frame)
	CopyRect 0,0,48,48,0,0,ImageBuffer(Icon),ImageBuffer(Image,frame-1)
End Function

Function TestPause()
	WaitKey()
	Cls
	Text 100,0,"Ready to continue..."
	Flip
	WaitKey()
	Cls
	Flip
	Cls
End Function 

Function TestClose()
	WaitKey()
	Cls
	Text 100,0,"Test Complete"
	Flip
	WaitKey()
	Cls
End Function 	</textarea><br><br>So it seems as though CopyRect itself does indeed function adequately. I know that the fuiles all exist and the names are correct. Bad paths would result in null handles.<br>With this in mind, I'll try using bitmaps instead of PNGs first, then, if still no luck, perhaps I may just have to splice the images together 'manually'. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
