<!DOCTYPE html><html lang="en" ><head ><title >BVH to Blitz (MoCap) [RESOLVED]</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >BVH to Blitz (MoCap) [RESOLVED]</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >BVH to Blitz (MoCap) [RESOLVED]</a><br><br>
<a name="1270620"></a>

<a name="1270621"></a>

<a name="1270622"></a>

<a name="1270661"></a>

<a name="1270699"></a>

<a name="1270767"></a>

<a name="1271441"></a>

<a name="1271450"></a>

<a name="1271458"></a>

<a name="1271526"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mikorians</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I wanted to better understand how the BVH matrices are applied to a skeleton.<br>I've had some marginal success reading the angles and just turning bones on my skinned mesh in blitz3d.<br>Yes. I know I can just LOAD it outright.<br><br>The zip package includes:<br>My WORKING Max5 Importer MaxScript<br>My ALMOST-WORKING Blitz3d Playback program.<br>Actual Capture_0000.bvh from kinnect Brekel Windows Application<br>The Just Walkin' .avi file to see the max result.<br>A note I've made about basic matrix rotations and multiplication<br>based on a mathematics website and a smart Japanese fellow's<br>website.<br><br>Link:<br><a href="https://sites.google.com/site/mikorians/BVH%20Playback.zip" target="_blank">https://sites.google.com/site/mikorians/BVH%20Playback.zip</a><br><br>Anyone expert who can enlighten us all about the matrix transformations I'm doing wrong will be appreciated.<br>The WORLD needs this.<br><br>Thanks to all in advance! <br><br></td></tr></table><br>
<a name="1270636"></a>

<a name="1270643"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Not really the answer you are looking for, but know that you can merge a bhv skeleton with animations with a ugh/b3d skeleton if the corresponding joints have the same name (with Fragmotion). It works well, i have done it. You can then export your rigged skinned animated mesh in b3d and then load it in blitz3d with loadanimmesh. <br><br></td></tr></table><br>
<a name="1270658"></a>

<a name="1270659"></a>

<a name="1270710"></a>

<a name="1271446"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mikorians</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you very much, RemiD (You're always so helpful).<br>I DO actually have Fragmotion!  I wasn't aware it could do that.<br>But this program will also address a solution for multiple animations.<br><br>I've noticed the different axis orders.  I tried swapping them around (nothing different happened)<br><br>UPDATE:I can already export skinned meshes with the .bvh import macroscript posted here! <br><br></td></tr></table><br>
<a name="1270666"></a>

<a name="1271447"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mikorians</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> It occurred to me-<br>Has anybody seen the supposed BVH skeleton loader for Blitz?<br>Does anybody have the source or a valid link to it?<br><br>IT WOULD BE A HANDY THING TO HAVE RIGHT NOW! <br><br></td></tr></table><br>
<a name="1270698"></a>

<a name="1270700"></a>

<a name="1270701"></a>

<a name="1270707"></a>

<a name="1270709"></a>

<a name="1270768"></a>

<a name="1271439"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mikorians</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Working BVH Import MaxScript:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
--By Joseph Bockholt Jun 2015
--This maxscript does only rudimentary syntax checking
--So if you have more bones or something is different,
--It may not work...

--Adjust bn2 array way below for your skin rig.

--IMPORTANT:
--You need to make sure your skelly has 0 rotation
--on all of its joints for this to work.
--(It has to be created this way in the first place)

--What I have ALWAYS USED on my skin rigs:
--I have a macro that puts the parent name into each
--joint's 'User Defined Properties' Page so that
--Rapid skeletal re-assembly is possible.
--I remove the skin modifier from my mesh,
--My skeleton is then un-linked, the point objects
--are un-rotated. I then can move any I want prior to
--re-assembly.
--My other macro then re-skins and puts the bones
--back on based on vertex colors and point wireframe colors.
--(I edit and tweak my skins/skeletons a lot)
--Vertex colors and user info were the only non-
--volatile place to store such information for me in Max 5.
--I also have skin CREATION tools that make it all a snap.
--If you're interested in any of my macros,
--let me know.    Joe

--Settings:
filnm="D:\\Documents and Settings\\Joseph D. Bockholt\\Desktop\\Kinnect Captures\\capture_0000.bvh"
global TossSkelly=false --Throw away the BVH skelly when done?
global SkipFrames=10--Minimum:1
global DoSkin=false  --Do we try to apply the bvh skelly to our skin?
global inPlace=false	--No root Walking/movement allowed

--Internal variables
global hier			--Level in hierarchy
global obj=#()		--Hierarchy tracker (not complete list)
global obj1=#()		--Complete list of skelly bones
global BonLst=#()   --Total list of skelly objects with MOTION CHANNELS
global origpos=#()  --The positions from the file
global origmat=#()  --The starting matrices
global r=[0,0,0]    --Position switcher
global frmskp       --Kind of a General Counter
global mat0         --matrix holder
global mat1			--matrix holder
global mat2			--matrix holder
global rotbon=#()	--bone name to be rotated
global posroot=#()  --Root bone positions FROM FILE
global rotpos=#()	--Root bone positions post matrix mult
global rotabx=#()   --Rotation for Bones X
global rotaby=#()   --Rotation for Bones Y
global rotabz=#()   --Rotation for Bones Z
global d
hier=0
chan=0
frmskp=SkipFrames-1
disablesceneredraw()
progressstart ""
--OPEN THE FILE AND COUNT THE CHANNELS
fil=openfile filnm mode:"rt"
a=readline fil
if a=="HIERARCHY" then ( --Valid File?

---------------
--LOAD SKELLY--
---------------
while not eof fil do (
a=readline fil;a=trimright (trimleft a (" \x09")) (" \x09")

--SELECT CASE (HEH, Easier to read)

--CASE 1
--Obtain bones, name and parent them to each other
if (((substring a 1 4) == "ROOT") or ((substring a 1 5) == "JOINT")) or ((substring a 1 8) == "End Site") do (
hier=hier+1
onam=a;nam=(substring a 6 -1)
--i=findstring nam "Chest";if i!=undefined do (nam=(substring nam 1 (i-1))+"Body")
--i=findstring nam "Shoulder";if i!=undefined do (nam=(substring nam 1 (i-1))+"Arm")
--i=findstring nam "Collar";if i!=undefined do (nam=(substring nam 1 (i-1))+"Shoulder")
--i=findstring nam "Elbow";if i!=undefined do (nam=(substring nam 1 (i-1))+"Forearm")
--i=findstring nam "Wrist";if i!=undefined do (nam=(substring nam 1 (i-1))+"Hand")
--i=findstring nam "Hips";if i!=undefined do (nam=(substring nam 1 (i-1))+"Pelvis")
--i=findstring nam "Hip";if i!=undefined do (nam=(substring nam 1 (i-1))+"Thigh")
--i=findstring nam "Knee";if i!=undefined do (nam=(substring nam 1 (i-1))+"Calf")
--i=findstring nam "Ankle";if i!=undefined do (nam=(substring nam 1 (i-1))+"Foot")
If onam=="End Site" do ( --(Means Tip of previous)
prevnam=obj[obj.count].name --Change below to "Hand" and "Foot" if you enable the above
i1=findstring prevnam "Wrist";if i1!=undefined do (prevnam=(substring prevnam 1 (i1-1))+"Fingers")
i2=findstring prevnam "Ankle";if i2!=undefined do (prevnam=(substring prevnam 1 (i2-1))+"Toes")
if i1==undefined and i2==undefined do prevnam=prevnam+"Tip"
Nam=(prevnam)
)
--Bracket
a=readline fil
--Offset
a=readline fil;a=trimright (trimleft a (" \x09")) (" \x09");q=filterstring a " "
r=[q[2] as float,-q[4] as float,q[3] as float]
r1=r
if (substring onam 1 5)=="ROOT " do (r=[0,0,0];append posroot r1)

--Create point
b=point()
b.name=trimright (trimleft nam " ") " "
b.wirecolor=(color 0 255 0)
b.size=5
if hier-1&gt;0 do (r=r+obj[hier-1].pos) --Shouldn't happen on root
b.pos=r
if hier-1&gt;0 do (b.parent=obj[hier-1]) --Shouldn't happen on root
append obj b
append obj1 b
b.showlinks=true
--Check for channels
a=readline fil;a=trimright (trimleft a (" \x09")) (" \x09")
if ((substring a 1 8)=="CHANNELS") do (
chan=chan+((substring a 9 1) as integer)
append BonLst b
append OrigPos r1
)
)

--CASE 2
if a=="MOTION" do exit --Animation Data Next

--CASE 3
--Loose hierarchy tracking (back out a level)
if a=="}" do (if hier&gt;1 do (hier=hier-1;deleteitem obj obj.count))
)
--END CASES AND LOOP


--MOTION was hit, so load up the anim header
a=readline fil;a=trimright (trimleft a (" \x09")) (" \x09")
--# frames
if substring a 1 7 =="Frames:" do (
nf=((substring a 8 -1) as integer)-1
animationrange=(interval 0f (nf as time))
--Speed... Hm.
a=readline fil;a=trimright (trimleft a (" \x09")) (" \x09")
if substring a 1 11 == "Frame Time:" do (
FrameRate=((floor ((1.0 / ((substring a 12 -1) as float)))) as integer)

------------------
--PREPARE SKELLY--
------------------
OrigMat=#()
for t=1 to BonLst.count do (
obj=BonLst[t]
Append OrigMat obj.transform
)
if inPlace==0 then (PostInitRootPos=OrigPos[1]) else (PostInitRootPos=[0,0,0])
--------------------
--ANIMATION LOADER--
--------------------
for t=1 to nf do (
--slidertime=((t-1) as time)
a=readline fil
q=filterstring a " "
for qt=1 to q.count do (q[qt]=(q[qt] as float))
frmskp=frmskp+1
for u=1 to BonLst.count do ( --For each bone
qt=(3+((u-1)*3))+1
bon=BonLst[u]
ZMat=rotateZMatrix q[qt+2]
XMat=rotateXMatrix q[qt+1]
YMat=rotateYMatrix (360.0-q[qt])
Mat1=ZMat*XMat*YMat
if u&gt;1 then (
parbon=bon.parent
Mat0 = matrix3 [1,0,0] [0,1,0] [0,0,1] [0,0,0]
Mat0.row4 = origpos[u]
bon.transform=Mat0*parbon.transform
Mat2=bon.transform
bon.transform=Mat1*Mat2
)
else
(
PosFil=[q[1],-q[3],q[2]]
if inPlace==0 then (PsFl=PosFil+PostInitRootPos) else (PsFl=[0,0,0])
bon.transform=Mat1*OrigMat[1]
bon.position=PsFl*OrigMat[1]--&lt;&lt;PosFil
)
if frmskp&gt;SkipFrames-1 do ( --Only do every 30th frame
progressupdate (t/(nf*.01))
--Add the 3 channel keys for the bone in order
if u==1 do (append posroot PosFil;in coordsys world (append rotpos bon.pos))
append rotbon bon
append rotaby bon.rotation.y_rotation
append rotabx bon.rotation.x_rotation
append rotabz bon.rotation.z_rotation
)
)
if frmskp&gt;SkipFrames-1 do (frmskp=0)
)
)
)
slidertime = 1

close fil

progressend()
enablesceneredraw()
---------------------------------
--KEY BIT NOW... WAIT FOR IT...--
---------------------------------
--Reset the pose of the skelly
for t=1 to BonLst.count do (
obj=BonLst[t]
obj.transform=OrigMat[t]
)

--Bake the ROTATIONS instead of the MATRIX to smoothed keys
with animate on (
frmskp=1;frmskp2=1
for t=1 to nf by SkipFrames do (
for u=1 to BonLst.count do (
at time ((t-1) as time) (
obj=rotbon[frmskp]
obj.rotation.z_rotation=rotabz[frmskp]
obj.rotation.x_rotation=rotabx[frmskp]
obj.rotation.y_rotation=rotaby[frmskp]
if u==1 do (if inPlace==0 do obj.pos=rotpos[frmskp2];frmskp2=frmskp2+1)
frmskp=frmskp+1
)
)
)
)


if DoSkin==True do (
--These rotations can now be applied to
--ANY SIZED SKELLY BY ANGLE ONLY NOW.
--MY SKELETAL DATA BELOW in bn2 - ORDER COUNTS

--Conversions: FROM - Default bones from Brekel .bvh file
--(some are renamed toward the top of macro)
--Conversions: TO - My rig
--Use undefined if you lack that bone.
--I don't bother with Legs, "LeftFingers", "RightFingers", "Head", or "HeadTip" (Neck does Head)
global bn1=obj1
global bn2=#($z_BackL, Undefined, Undefined, Undefined, Undefined, Undefined, Undefined, Undefined, Undefined, $z_BackU, $z_ShouldL1, $z_ArmL, $z_ForearmL, $z_HandL, Undefined, $z_ShouldR1, $z_ArmR, $z_ForearmR, $z_HandR, Undefined, $z_Head, Undefined, Undefined)
--global bn2=#($z_BackL, $z_ThighL, $z_CalfL, $z_FootL, $z_ToeL, $z_ThighR, $z_CalfR, $z_FootR, $z_ToeR, $z_BackU, $z_ShouldL1, $z_ArmL, $z_ForearmL, $z_HandL, Undefined, $z_ShouldR1, $z_ArmR, $z_ForearmR, $z_HandR, Undefined, $z_Head, Undefined, Undefined)
--These 2 bn arrays should be the same number of elements
--In the case of Brekel, the bvh file has 23 total bones

-----------------
--SKIN ROTATION--
-----------------
for t=(animationrange.start as string) as integer to ((animationrange.end as string) as integer)-1 by SkipFrames do (
slidertime = (t as time)
print bn2
--From ROOT to Outermost children (Yep)
for u=1 to BonLst.count do (
Bon=BonLst[u]
i=finditem bn1 Bon
if bn2[i]!=undefined do (
p=bn2[i].pos;bn2[i].pos=[0,0,0]
with animate on (
bn2[i].rotation=at time slidertime (bon.rotation)
)
bn2[i].pos=p
)
)
)

-----------------
--SKIN POSITION--
-----------------
if inPlace==0 do (
frm=1
--This next line is SPECIFIC to my skin + bones rig
D=(in coordsys world ($z_BackL.pos-$z_PelvisPiv.pos)) --Move my pelvis at body height Z (dumb)
with animate on (
for t=(animationrange.start as string) as integer to ((animationrange.end as string) as integer)-1 by SkipFrames do (
slidertime = (t as time)
in coordsys world (
if frm&gt;rotpos.count do (frm=rotpos.count)
$z_PelvisPiv.position=rotpos[frm]-D
frm=frm+1
)
)
)
)
)

if TossSkelly==True do (delete obj1)

)--Hierarchy? Valid File?

else

(close fil)
</textarea> <br><br></td></tr></table><br>
<a name="1270805"></a>

<a name="1270807"></a>

<a name="1271445"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mikorians</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> HERE is a REALLY GOOD Maxscript to start with.<br>(I adapted it to max 5)<br><a href="http://zigncreations.com/shared/max_import_bvh.txt" target="_blank">http://zigncreations.com/shared/max_import_bvh.txt</a><br>It's by Markus Bliem 12/31/2007.<br><br>I would have posted the capture_0000.bvh file, but it was too large.<br>It's in the .zip file I mention throughout this discussion. <br><br></td></tr></table><br>
<a name="1270853"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rick Nasher</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cool. This may come in handy. <br><br></td></tr></table><br>
<a name="1270863"></a>

<a name="1270864"></a>

<a name="1271442"></a>

<a name="1271448"></a>

<a name="1271451"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mikorians</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> (Removed my pointless yacking about stuff that's now fixed) <br><br></td></tr></table><br>
<a name="1270986"></a>

<a name="1271443"></a>

<a name="1271449"></a>

<a name="1271452"></a>

<a name="1271453"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mikorians</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> (Removed more pointless blathering about answers I now have) <br><br></td></tr></table><br>
<a name="1271157"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mikorians</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hm. Not much response. Guess it's the Independece Day stuff.<br>I will be posting the vastly improved Maxscript!<br><br>I now see the unrotated skeleton in Blitz also.<br><br>I got the matrix arithmetic library from the codearc.<br>So now it's just a matter of transposing from max to blitz.<br>Nearly there.  Anyone know more about receiving/setting<br>Xforms for entities properly? I just fiddle till I find the correct permutation... <br><br></td></tr></table><br>
<a name="1271173"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Not much response. Guess it's the Independece Day stuff. <br></div><br>And I guess it's because nobody knows anything.<br><br>If I understand this correctly bvh is motion capture data. Some human being has his movements recorded. The same motions will then be applied a 3d model of a person, or robot or King Kong or something.<br><br>It sounds like you want to use the motion data in Blitz3D. I would think the simplest way to get started is by experimenting with a model loaded into Blitz3D. Initially it would have to be in whatever state the bvh data expects. I suppose this is something like: standing up straight, arms hanging at sides etc. <br><br>The .bvh file tells you how to turn the various parts such as RightWrist, LeftHip...<br><br>In Blitz3D this can be done with TurnEntity, applied three times in the bvh order of Z,X,Y The turns can be global or relative to the parent. I assume bvh is doing relative turns.<br><br>One thing to watch out for is handedness. Blitz3D uses left handed coordinates, like Direct3D, and the bvh system is right handed. Typically that means Y is up, X is right, Z is into the screen for left handed and out of the screen for right handed. That probably means you need to turn the opposite direction for Z.<br><br>If you figure out how all of this works you can then get the matrices directly from Blitz3D with GetMatElement. I suppose you need them to build a .b3d file, or similar. <br><br></td></tr></table><br>
<a name="1271180"></a>

<a name="1271182"></a>

<a name="1271184"></a>

<a name="1271591"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mikorians</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> (He was actually RIGHT about it being super easy!) <br><br></td></tr></table><br>
<a name="1271259"></a>

<a name="1271444"></a>

<a name="1271454"></a>

<a name="1271592"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mikorians</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> <a href="https://sites.google.com/site/mikorians/BVH%20Playback.zip" target="_blank">https://sites.google.com/site/mikorians/BVH%20Playback.zip</a> <br><br></td></tr></table><br>
<a name="1271265"></a>

<a name="1271462"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> It's very hard to understand all of those matrix calculations, and I still don't know why they are needed. <br><br>Here is my view on what is happening. I will accept all the skeleton building and setup code as correct and just try to figure out the movements. <br><br>I have added a function at the beginning of the code, bvhRotateEntity. This mimics the bvh style of z,x,y order.<br><br>Look at the code starting at line 205. Three angles have been used in elaborate matrix calculations which are then used with RotateEntity. I have commented out the RotateEntity and instead use bvhRotateEntity with the three angles. It seems to accomplish the same thing.<br><br>I don't know what, if anything, is wrong with the rest of the code.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Function bvhRotateEntity( ent, x#, y#, z# )
	RotateEntity ent, 0, 0, 0  ; begin in unrotated state
	TurnEntity   ent, 0, 0, z  ; then do z,x,y turns ( bvh order )
	TurnEntity   ent, x, 0, 0  ; this is all relative to the parent, not global
	TurnEntity   ent, 0, y, 0
End Function



;Display and Animate a BVH in Blitz3D
;By Joseph Bockholt 2015
;Matrix Algorithms by JoshK - His posting 21362 circa 2003

Type J
Field Name$
Field Ent
Field Parent
Field OrgPosX#
Field OrgPosY#
Field OrgPosZ#
Field OrgMat        ;Pointers to the xform banks
Field CurMat
Field Cha									;Does it have animation channels?
End Type

;Settings:
Global filnm$="capture_0000.bvh"
Global TossSkelly=False ;Throw away the BVH skelly when done?
Global SkipFrames=10			;Minimum:1
Global DoSkin=False  			;Do we try To apply the bvh skelly To our skin?
Global inPlace=0						;No root Walking/movement allowed

Global MaxSplitCnt=100
Dim Split$(MaxSplitCnt)
Dim Cor#(MaxSplitCnt)
Global SplitCnt

fil=ReadFile(filnm$)
a$=ReadLine$(fil)
If Upper$(a$)&lt;&gt;"HIERARCHY" Then RuntimeError "Not a valid .bvh file!"
CloseFile(fil)

Type CamD
	Field Body
	Field Camera
End Type

Global Cam.CamD=New CamD

Graphics3D 800,600,0,0
Cam\Camera=CreateCamera()
CameraRange Cam\Camera,1,10000;Whatever size your map CELL is, you should scale these to it
CameraFogRange Cam\Camera,9000,10000 ;Fade range
CameraFogMode Cam\Camera,1
CameraFogColor Cam\Camera,62,62,62
CameraZoom Cam\Camera,1.0/Tan(64.0108/2.0) ;Human Vision
Cam\Body=CreatePivot()
EntityParent Cam\Camera,Cam\Body

l=CreateLight():TurnEntity l,45,45,0
;ChangeDir "D:\Documents and Settings\Joseph D. Bockholt\Desktop\Tourist\X-Files"
;a=LoadAnimMesh("_Test Scene.b3d")
PositionEntity Cam\Body,0,50,150

Global nj ;# Joints
Global nf ;# frames
;Count 'em up. YES, I know it seems redundant.
fil=ReadFile(filnm$)
a$=ReadLine$(fil) ;HIERARCHY
While Not Eof(fil)
a$=ReadLine$(fil)
a$=Trim$(a$)
If Left$(a$,5)="ROOT " Or Left$(a$,6)="JOINT " Or Left$(Upper$(a$),8)="END SITE" Then nj=nj+1
If Left$(a$,7)="Frames:" Then nf=Int(Mid$(a$,9)):Exit
Wend
CloseFile(fil):fil=0
nj=nj-1

;Internal variables
Global hier										;Level in hierarchy
Dim Obj(nj+1)        ;Hierarchy pointers
Global frmskp       			;Kind of a General Counter
Dim rotbon$(nf)							;bone name To be rotated
Dim posroot#(nf,2)					;Root bone positions FROM FILE
Dim rotpos#(nf,2)						;Root bone positions post matrix mult
Dim rotabx#(nf)							;Rotation For Bones X
Dim rotaby#(nf)							;Rotation For Bones Y
Dim rotabz#(nf)							;Rotation For Bones Z
Global d
hier=0
chan=0
frmskp=SkipFrames-1
Dim Jnt.J(nj):For t=0 To nj:Jnt.J(t) = New J:Next

;Load the skelly.
fil=ReadFile(filnm$)
a$=ReadLine$(fil) ;HIERARCHY
t=0
While Not Eof(fil)
a$=Trim$(ReadLine$(fil))

;JOINT OBJECT
If Left$(a$,5)="ROOT " Or Left$(a$,6)="JOINT " Or Left$(Upper$(a$),8)="END SITE" Then
obj(hier)=t
If Left$(Upper$(a$),8)="END SITE" Then a$=PrevName$+"Tip"
i=Inst(a$," ")
a$=Mid$(a$,i+1)
Jnt(t)\Name$=a$:PrevName$=Jnt(t)\Name$
a$=ReadLine$(fil)															;{
a$=Trim$(ReadLine$(fil))									;OFFSET
SplitStr(a$," ")
x#=Float(split$(1)):y#=Float(split$(2)):z#=-Float(split$(3))
a$=Upper$(Trim$(ReadLine$(fil)))
If Left$(a$,9)="CHANNELS " Then				;CHANNELS (or not)
	chan=chan+Int(Mid$(a$,10))
	jnt(t)\ent=CreateCube()
	EntityFX Jnt(t)\Ent,9
	jnt(t)\cha=1 ;Has channels
Else
	jnt(t)\ent=CreatePivot()
End If
Jnt(t)\OrgPosX#=x#
Jnt(t)\OrgPosY#=y#
Jnt(t)\OrgPosZ#=z#
If hier&gt;0 Then
	x#=x#+EntityX#(Jnt(obj(hier-1))\Ent,True)
	y#=y#+EntityY#(Jnt(obj(hier-1))\Ent,True)
	z#=z#+EntityZ#(Jnt(obj(hier-1))\Ent,True)
	PositionEntity Jnt(t)\Ent,x#,y#,z#,True
	EntityParent Jnt(t)\Ent,Jnt(obj(hier-1))\Ent,True
	Jnt(t)\Parent=obj(hier-1) ;Need IDX for Matrix!
	Else
	If inPlace=0 Then
		PositionEntity Jnt(t)\Ent,x#,y#,z#,True
		Else
		PositionEntity Jnt(t)\Ent,0,0,0,True
	End If
End If
NameEntity Jnt(t)\Ent,Jnt(t)\Name$
EntityPickMode Jnt(t)\Ent,1
Hier=Hier+1:t=t+1
End If

;BACK OUT 1 BRANCH
If a$="}" Then hier=hier-1

;END OF SKELLY
If Left$(a$,11)="Frame Time:" Then Exit
Wend


;****************
;*PREPARE SKELLY*
;****************
For t=0 To nj ;Record initial transformation matrices
Ent=Jnt(t)\Ent
pitch#=EntityPitch#(Ent,True):Yaw#=EntityYaw#(Ent,True):Roll#=EntityRoll#(Ent,True)
x#=EntityX#(Ent,True):y#=EntityY#(Ent,True):z#=EntityZ#(Ent,True)
Jnt(t)\OrgMat=CreateMatrix()
rotatematrix Jnt(t)\OrgMat,pitch#,yaw#,roll#
If t&gt;0 Then positionmatrix Jnt(t)\OrgMat,x#,y#,z#
Jnt(t)\CurMat=CopyMatrix(Jnt(t)\OrgMat)
Next
If inPlace=0 Then ;Starting root bone position - note: NOT the entity position
	PostInitRootPosX#=Jnt(0)\OrgPosX#
	PostInitRootPosY#=Jnt(0)\OrgPosY#
	PostInitRootPosZ#=Jnt(0)\OrgPosZ#
	Else
	PostInitRootPosX#=0
	PostInitRootPosY#=0
	PostInitRootPosZ#=0
End If
fm=0
fPos=FilePos(fil)
ZMat=CreateMatrix():XMat=CreateMatrix():YMat=CreateMatrix()
Mat0=CreateMatrix():Mat1=CreateMatrix():Mat2=CreateMatrix():Mat3=CreateMatrix()

While Not KeyHit(1)
	fm=fm+1:If fm&gt;nf Then
		SeekFile(fil,fPos):fm=0
	Else ;Do animation
		a$=Trim$(ReadLine$(fil))
		splitstr(a$," ")
		For t=0 To splitcnt:cor#(t)=Float(Split$(t)):Next
		For u=0 To nj
			qt=(u*3)+3
					
			MatrixIdentity(ZMat):MatrixIdentity(YMat):MatrixIdentity(XMat)
			RotateMatrix ZMat,0,0, cor#(qt+2)
			RotateMatrix YMat,0,cor#(qt+1),0
			RotateMatrix XMat,cor#(qt),0,0
			MulMatrices yMat,XMat,Mat2
			MulMatrices Mat2,zMat,Mat1:MatrixIdentity(Mat0):MatrixIdentity(Mat2):MatrixIdentity(Mat3)
			If u&gt;0 Then
				ParBon=Jnt(u)\Parent
				PositionMatrix Mat0,Jnt(u)\OrgPosX#,Jnt(u)\OrgPosY#,Jnt(u)\OrgPosZ#
				;EqualMatrix Mat2,Jnt(Jnt(u)\Parent)\CurMat
				MulMatricesRot Mat0,Mat2,Jnt(u)\CurMat
				MulMatricesRot Mat1,Jnt(u)\CurMat,Mat3
				EqualMatrix Jnt(u)\CurMat,Mat3
				
				;PositionEntity Jnt(u)\Ent,MatrixX(Mat3),MatrixY(Mat3),MatrixZ(Mat3),False
;If u=1 Then DebugLog EntityX#(Jnt(u)\Ent,True)+","+EntityY#(Jnt(u)\Ent,True)+","+EntityZ#(Jnt(u)\Ent,True)


; RotateEntity is the original matrix based code.
;				RotateEntity Jnt(u)\Ent,MatrixPitch(Mat3),MatrixYaw(Mat3),MatrixRoll(Mat3)

; bvhRotateEntity ignores the matrix computations, does bvh turns directly.				
				bvhRotateEntity Jnt(u)\Ent, cor(qt), cor(qt+1), cor(qt+2)

			Else
				PosFilX#=cor#(0)
				PosFilY#=cor#(1)
				PosFilZ#=-cor#(2)
				If inPlace=0 Then
					PsFlX#=PosFilX#+PostInitRootPosX#
					PsFlY#=PosFilY#+PostInitRootPosY#
					PsFlZ#=PosFilZ#+PostInitRootPosZ#
				Else
					PsFlX#=0
					PsFlY#=0
					PsFlZ#=0
				End If
				MulMatrices Mat1,Jnt(0)\OrgMat,Jnt(0)\CurMat
				EqualMatrixRot Mat3,Jnt(0)\CurMat
				EqualMatrixRot Mat2,Mat3
				PositionMatrix Mat3,PsFlX#,PsFlY#,PsFlZ#
				EqualMatrixRot Jnt(0)\CurMat,Mat3
				PositionEntity Jnt(0)\Ent,0,0,0
				RotateEntity Jnt(0)\Ent,MatrixPitch(Mat3),MatrixYaw(Mat3),MatrixRoll(Mat3),True
				PositionEntity Jnt(0)\Ent,MatrixX(Mat3),MatrixY(Mat3),MatrixZ(Mat3),True
;If u=0 Then
;	DebugLog EntityX#(Jnt(0)\Ent,True)+","+EntityY#(Jnt(0)\Ent,True)+","+EntityZ#(Jnt(0)\Ent,True)
;	DebugLog EntityX#(Cam\Camera,True)+","+EntityY#(Cam\Camera,True)+","+EntityZ#(Cam\Camera,True)
;End If
			End If
		Next
	End If
	
	If MouseHit(1)=True Then
		CameraPick(Cam\Camera,MouseX(),MouseY())
		If PickedEntity()&lt;&gt;0 Then DebugLog EntityName$(PickedEntity())
	End If

	;Turn
	mxs=0:mys=0:mdr#=0 ;PU,PD,R,L
	If KeyDown(201)	Or KeyDown(73) Then mxs=-3
	If KeyDown(209) Or KeyDown(81) Then mxs=3
	If Int((EntityPitch(Cam\Camera)+mxs)/90)=(EntityPitch(Cam\Camera)+mxs)/90 Then mxs=mxs*2 ;Avoid camera gimbal problem
	If KeyDown(205) Or KeyDown(77) Then mys=-3
	If KeyDown(203) Or KeyDown(75) Then mys=3
	
	;Rotations
	TurnEntity Cam\Body,0,mys,0
	If EntityYaw(Cam\Body)&lt;0 Then TurnEntity Cam\Body,0,360,0
	If EntityYaw(Cam\Body)&gt;360 Then TurnEntity Cam\Body,0,-360,0
	TurnEntity Cam\Camera,mxs,0,0
	If EntityPitch#(Cam\Camera)&lt;0 Then TurnEntity Cam\Camera,360,0,0
	If EntityPitch#(Cam\Camera)&gt;360 Then TurnEntity Cam\Camera,-360,0,0
	
	;Move
	If KeyDown(200)=True Or KeyDown(72)=True Then
			mdr#=10			;Forward
	End If
	If KeyDown(208)=True Or KeyDown(80)=True Then
			mdr#=-10			;Backward
	End If
	
;	FixRoll()
	
		;Sidestep (for ladders)
	ss#=0
	If KeyDown(51) Then ss#=-5
	If KeyDown(52) Then ss#=+5

	TranslateEntity Cam\Body,-Sin(EntityYaw(Cam\Body))*mdr#,SS#,Cos(EntityYaw(Cam\Body))*mdr#,True

UpdateWorld
RenderWorld
Flip
Wend
If fil&lt;&gt;0 Then CloseFile(fil)

FreeBank ZMat:FreeBank XMat:FreeBank YMat
FreeBank Mat0:FreeBank Mat1:FreeBank Mat2:FreeBank Mat3
For t=0 To nj
FreeBank Jnt(t)\CurMat
FreeBank Jnt(t)\OrgMat
Next
End

Function FixRoll() ;For kewokas dismount of wild carry objects
	tmp=CreatePivot()
	RotateEntity tmp,EntityPitch#(Cam\Camera,True),EntityYaw#(Cam\Body,True),0,True
;	q=ParentEntity(Cam\Body)
	EntityParent Cam\Body,0
	RotateEntity Cam\Body,0,0,0,True
	RotateEntity Cam\Camera,0,0,0,True
	RotateEntity Cam\Camera,EntityPitch#(tmp,True),0,0,True
	RotateEntity Cam\Body,0,EntityYaw#(tmp,True),0,True
	EntityParent Cam\Body,q
	FreeEntity tmp
End Function

Function SplitStr(a$,del$)
Local i,j
SplitCnt=0
For t=0 To MaxSplitCnt:Split$(t)="":Next
i=Inst(a$,del$)
If i=0 Then Split$(0)=a$:Return 0
i=1:SplitCnt=0
a$=del$+a$
While i&lt;&gt;0
j=Inst(a$,del$,i+1)
If j=0 Then j=Len(a$)+1
Split$(SplitCnt)=Mid$(a$,i+1,j-i-1)
If j&lt;Len(a$) Then i=j Else i=0
SplitCnt=SplitCnt+1
Wend
If Right$(a$,1)=del$ Then Split$(SplitCnt)="":SplitCnt=SplitCnt+1
SplitCnt=SplitCnt-1
Return SplitCnt
End Function

Function Inst(a$,b$,offset=1)
;FAULTY
If Len(a$)=0 Or Len(b$)=0 Or offset&lt;1 Or offset&gt;Len(a$) Then Return 0
Return Instr(a$,b$,offset)
End Function


;By JoshK - His posting 21362 circa 2003
;I added MatrixIdentity, MulMatricesRot, EqualMatrix, and EqualMatrixRot
Function CreateMatrix()
matrix=CreateBank(64)
For n=0 To 15
	PokeFloat matrix,n*4,0
	Next
PokeFloat matrix,0,1
PokeFloat matrix,5*4,1
PokeFloat matrix,10*4,1
PokeFloat matrix,11*4,1
setmatrixelement matrix,0,3,1
;setmatrixelement matrix,1,3,1
;setmatrixelement matrix,2,3,1
;setmatrixelement matrix,3,3,1
Return matrix
End Function

Function MatrixIdentity(matrix)
For n=0 To 15
	PokeFloat matrix,n*4,0
Next
PokeFloat matrix,0,1
PokeFloat matrix,5*4,1
PokeFloat matrix,10*4,1
PokeFloat matrix,11*4,1
setmatrixelement matrix,0,3,1
End Function

Function MatrixX#(matrix)
Return GetMatrixElement(matrix,3,0)
End Function

Function MatrixY#(matrix)
Return GetMatrixElement(matrix,3,1)
End Function

Function MatrixZ#(matrix)
Return GetMatrixElement(matrix,3,2)
End Function

Function MatrixPitch#(matrix)
Return -ASin(GetMatrixElement(matrix,2,1))
End Function

Function MatrixYaw#(matrix)
Return -ATan2(GetMatrixElement(matrix,2,0),GetMatrixElement(matrix,2,2)) 
End Function

Function MatrixRoll#(matrix)
Return ATan2(GetMatrixElement(matrix,0,1),GetMatrixElement(matrix,1,1)) 
;-ATan2(m(1,0),m(1,1)) 
End Function

Function PositionMatrix(matrix,x#,y#,z#)
SetMatrixElement matrix,3,0,x
SetMatrixElement matrix,3,1,y
SetMatrixElement matrix,3,2,z
End Function

Function TranslateMatrix(matrix,x#,y#,z#)
SetMatrixElement matrix,3,0,x+matrixx(matrix)
SetMatrixElement matrix,3,1,y+matrixy(matrix)
SetMatrixElement matrix,3,2,z+matrixz(matrix)
End Function

Function MoveMatrix(matrix,x#,y#,z#)
mx#=matrixx(matrix)
my#=matrixy(matrix)
mz#=matrixz(matrix)
temp=copymatrix(matrix)
positionmatrix temp,0,0,0
temp2=creatematrix()
positionmatrix temp2,x,y,z
mulmatrices(temp2,temp,matrix)
setmatrixelement matrix,3,0,getmatrixelement(matrix,3,0)+mx
setmatrixelement matrix,3,1,getmatrixelement(matrix,3,1)+my
setmatrixelement matrix,3,2,getmatrixelement(matrix,3,2)+mz
FreeBank temp
FreeBank temp2
End Function

Function EqualMatrix(matrix1,matrix2)
For n=0 To 63
	PokeByte matrix1,n,PeekByte(matrix2,n)
Next
End Function

Function EqualMatrixRot(matrix1,matrix2)
For r=0 To 2
	For c=0 To 3
		value#=GetMatrixElement(matrix2,r,c)
		SetMatrixElement matrix1,r,c,value
	Next
Next
End Function

Function CopyMatrix(matrix)
m=CreateBank(64)
For n=0 To 63
	PokeByte m,n,PeekByte(matrix,n)
	Next
Return m
End Function

Function GetMatrixElement#(matrix,row,column)
Return PeekFloat(matrix,(row*4+column)*4)
End Function

Function SetMatrixElement#(matrix,row,column,value#)
PokeFloat matrix,(row*4+column)*4,value
End Function

Function MulMatrices(matrix1,matrix2,matrix3)
For r=0 To 3
	For c=0 To 3
		value#=0.0
		For c0=0 To 3
			a#=GetMatrixElement(matrix1,r,c0)
			b#=GetMatrixElement(matrix2,c0,c)
			value=value+a*b
			Next
		SetMatrixElement matrix3,r,c,value
		Next
	Next
End Function

Function MulMatricesRot(matrix1,matrix2,matrix3)
For r=0 To 2
	For c=0 To 3
		value#=0.0
		For c0=0 To 3
			a#=GetMatrixElement(matrix1,r,c0)
			b#=GetMatrixElement(matrix2,c0,c)
			value=value+a*b
			Next
		SetMatrixElement matrix3,r,c,value
		Next
	Next
End Function

Function RotateMatrix(matrix,pitch#,yaw#,roll#)

temp=creatematrix()

xmatrix=creatematrix()
SetMatrixElement xmatrix,0,0,1.0
SetMatrixElement xmatrix,1,1,Cos(-pitch)
SetMatrixElement xmatrix,1,2,-Sin(-pitch)
SetMatrixElement xmatrix,2,1,Sin(-pitch)
SetMatrixElement xmatrix,2,2,Cos(-pitch)

ymatrix=creatematrix()
SetMatrixElement ymatrix,0,0,Cos(yaw)
SetMatrixElement ymatrix,0,2,Sin(yaw)
SetMatrixElement ymatrix,1,1,1.0
SetMatrixElement ymatrix,2,0,-Sin(yaw)
SetMatrixElement ymatrix,2,2,Cos(yaw)

zmatrix=creatematrix()
SetMatrixElement zmatrix,0,0,Cos(-roll)
SetMatrixElement zmatrix,0,1,-Sin(-roll)
SetMatrixElement zmatrix,1,0,Sin(-roll)
SetMatrixElement zmatrix,1,1,Cos(-roll)
SetMatrixElement zmatrix,2,2,1.0

mulmatrices xmatrix,ymatrix,temp
mulmatrices zmatrix,temp,matrix

FreeBank xmatrix
FreeBank ymatrix
FreeBank zmatrix
FreeBank temp

End Function

</textarea> <br><br></td></tr></table><br>
<a name="1271294"></a>

<a name="1271399"></a>

<a name="1271455"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mikorians</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank's Floyd!<br>I will examine the difference, and perhaps the bug will be revealed. Blitz does things a bit differently from Max (or perhaps in a different order). Certainly Blitz is a bit more true to the parent/child relationship than Max is.<br><br>UPDATE: Both the codearc entry and a posted source listing touted as working were mathematically inappropriate.  I now have a CORRECTED set of matrix arithmetic functions.<br><br>UPDATE2: Floyd's probably right about the tranformation matrices not being needed, but I'll include them for everyone anyway. <br><br></td></tr></table><br>
<a name="1271456"></a>

<a name="1271457"></a>

<a name="1271593"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mikorians</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> OK, I've updated ALL my previous postings.<br>It all works now. <br><br></td></tr></table><br>
<a name="1271479"></a>

<a name="1271480"></a>

<a name="1271481"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> I looked at some of my earliest backup CDs and found a note and some code. Maybe you can make something of it.<br><br><pre class=code>From Halo, 12/27/2001

When creating an animation sequence with a hierarchal entity, you must set the animation key for each 
entity in the hierarchy. You may then add the animseq to any entity in the hierarchy, and all children will
be included. This wasn't easy to figure out. Please make it public knowledge.</pre><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">; file loadBVH.bb
; Posted in Code Archives by Halo, 3/15/2002.

; Please mail halo9@... if you find any bugs.
; Standard animation commands may be used on the loaded skeleton.
; This was designed with the Character Studio motion capture samples.


;=====================================================
;LoadBVH( file$, joint, endsites, rootanimmode, debug ) function by halo and TechLord
;file$ - file to load
;joint (optional) - entity to use for limbs.  A pivot is used by default.
;endsites (optional) - FALSE will not load end sites.
;rootanimmode (optional) - FALSE omits X and Z position keys for root node.
;debug (optional) - write file info to debuglog

Function LoadBVH(file$,joint=0,endsites=False,rootanimmode=False,debug=False)

f=ReadFile(file)

If Not f Return 0

figure=CreateMesh()

entitybank=CreateBank()
hierarchybank=CreateBank()
channelbank=CreateBank()

While s$&lt;&gt;"motion"

    s=ReadLine(f)
    s=Lower(Trim(s))
    If s="end site"
        If endsites
            s="joint endsite"
        Else
            limb=0
            ResizeBank hierarchybank,BankSize(hierarchybank)+4
            PokeInt hierarchybank,BankSize(hierarchybank)-4,limb
        EndIf
    EndIf
        
    Select piece(s,1," ")
    
        ;root entity
        Case "root"
            If Not joint limb=CreatePivot() Else limb=CopyEntity(joint)
            NameEntity limb,piece(s,2," ")
            EntityParent limb,figure
            ResizeBank hierarchybank,BankSize(hierarchybank)+4
            PokeInt hierarchybank,BankSize(hierarchybank)-4,limb
            ResizeBank entitybank,BankSize(entitybank)+4
            PokeInt entitybank,BankSize(entitybank)-4,limb
            ResizeBank channelbank,BankSize(channelbank)+1
            root=limb
            If debug DebugLog "Root: "+EntityName(limb)
        
        ;new limb
        Case "joint"
            If Not joint limb=CreatePivot() Else limb=CopyEntity(joint)
            NameEntity limb,Trim(piece(s,2," "))                
            EntityParent limb,PeekInt(hierarchybank,BankSize(hierarchybank)-4)
            ResizeBank hierarchybank,BankSize(hierarchybank)+4
            PokeInt hierarchybank,BankSize(hierarchybank)-4,limb
            ResizeBank entitybank,BankSize(entitybank)+4
            PokeInt entitybank,BankSize(entitybank)-4,limb
            ResizeBank channelbank,BankSize(channelbank)+1
            If debug
                DebugLog ""
                DebugLog "Joint: "+EntityName(limb)
                DebugLog "Parent: "+EntityName(GetParent(limb))
            EndIf

        ;position (relative to parent)    
        Case "offset"
            If limb
                PositionEntity limb,Float(piece(s,2," ")),Float(piece(s,3," ")),Float(piece(s,4," "))
                If debug DebugLog "Offset: "+EntityX(limb)+", "+EntityY(limb)+", "+EntityZ(limb)
            EndIf
                
        ;animation keys
        Case "channels"
            If limb
                channelcount=piece(s,2," ")
                If debug channelstring$=""
                For n=1 To channelcount
                    Select Lower(Trim(piece(s,2+n," ")))
                        Case "xposition" : channels=channels+1 : If debug channelstring=channelstring+" XPosition"
                        Case "yposition" : channels=channels+2 : If debug channelstring=channelstring+" YPosition"                            
                        Case "zposition" : channels=channels+4 : If debug channelstring=channelstring+" ZPosition"
                        Case "xrotation" : channels=channels+8 : If debug channelstring=channelstring+" XRotation"
                        Case "yrotation" : channels=channels+16 : If debug channelstring=channelstring+" YRotation"
                        Case "zrotation" : channels=channels+32    : If debug channelstring=channelstring+" ZRotation"                    
                    End Select        
                Next
                If debug DebugLog "Channels:"+channelstring
                PokeByte channelbank,BankSize(channelbank)-1,channels
                channels=0
            EndIf
                
        ;step bank hierarchy
        Case "}"
            ResizeBank hierarchybank,BankSize(hierarchybank)-4
            
    End Select

Wend

;frames
s=ReadLine(f)
framecount=Int(piece(s,2,"    ")); tab, not space!

;frame time
s=ReadLine(f)
framespeed#=Float(piece(s,2,"    ")); tab, not space!

;more debug info
If debug
    DebugLog ""
    DebugLog "Framecount: "+framecount
    DebugLog "Framespeed: "+framespeed
    DebugLog ""
EndIf
    
;animation frames
keybank=CreateBank()

For frame=1 To framecount

    s=Trim(ReadLine(f))
    ResizeBank keybank,0
    readshift=0

    ;enter channels into bank
    .loop
    p=Instr(s," "); space
    p2=Instr(s,"    "); tab
    If p2&lt;p And p2 p=p2
    If p key$=Left(s,p-1) Else key=s
    s=Trim(Right(s,Len(s)-p))        
    ResizeBank keybank,BankSize(keybank)+4
    PokeFloat keybank,BankSize(keybank)-4,Float(key)
    If p Goto loop

    ;record channel values
    If debug
        s=""
        For n=0 To BankSize(keybank)/4-1
            s=s+PeekFloat(keybank,n*4)+" "
        Next
        DebugLog s
    EndIf

    For n=0 To BankSize(entitybank)/4-1
        limb=PeekInt(entitybank,n*4)
        channels=PeekByte(channelbank,n)
        
        poskey=False
        rotkey=False
        
        xposition#=EntityX(limb)
        yposition#=EntityY(limb)
        zposition#=EntityZ(limb)
        xrotation#=EntityPitch(limb)
        yrotation#=EntityYaw(limb)
        zrotation#=EntityRoll(limb)
        
        If 2^(0) And channels
            xposition=PeekFloat(keybank,(readshift*4))
            readshift=readshift+1
            poskey=True
            If Not rootanimmode
                If limb=root xposition=EntityX(limb)
            EndIf
        EndIf
        
        If 2^(1) And channels
            yposition=PeekFloat(keybank,(readshift*4))
            readshift=readshift+1
            poskey=True
        EndIf
        
        If 2^(2) And channels
            zposition=PeekFloat(keybank,(readshift*4))
            readshift=readshift+1
            poskey=True
            If Not rootanimmode
                If limb=root zposition=EntityZ(limb)
            EndIf
        EndIf    

        If 2^(3) And channels
            zrotation=PeekFloat(keybank,(readshift*4))
            readshift=readshift+1
            rotkey=True
        EndIf

        If 2^(4) And channels
            xrotation=PeekFloat(keybank,(readshift*4))
            readshift=readshift+1
            rotkey=True
        EndIf

        If 2^(5) And channels
            yrotation=PeekFloat(keybank,(readshift*4))
            readshift=readshift+1
            rotkey=True
        EndIf
        
        If poskey PositionEntity limb,xposition,yposition,zposition
        If rotkey
            RotateEntity limb,0,0,0

            TurnEntity limb,0,0,zrotation
            TurnEntity limb,xrotation,0,0
            TurnEntity limb,0,yrotation,0


            EndIf
        
    Next

    ;set keys
    For n=0 To BankSize(entitybank)/4-1
        limb=PeekInt(entitybank,n*4)
        SetAnimKey limb,frame
        If frame=framecount SetAnimKey limb,0
    Next
    
Next

AddAnimSeq figure,framecount

;free memory blocks
FreeBank entitybank
FreeBank hierarchybank
FreeBank channelbank
FreeBank keybank

Return figure

End Function


;==================================
Function piece$(s$,entry,char$=",")
For n=1 To entry-1
    p=Instr(s,char)
    s=Right(s,Len(s)-p)
    Next
p=Instr(s,char)
If p&lt;1
    a$=s
    Else
    a=Left(s,p-1)
    EndIf
Return a
End Function</textarea> <br><br></td></tr></table><br>
<a name="1271511"></a>

<a name="1271525"></a>

<a name="1271527"></a>

<a name="1271528"></a>

<a name="1271573"></a>

<a name="1271575"></a>

<a name="1271576"></a>

<a name="1271594"></a>

<a name="1271595"></a>

<a name="1271596"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mikorians</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks a bunch, Floyd!<br>This IS the answer- it had an infinite loop bug and I had to update the text parser, and the floats from the file were being passed as ints.<br>But it plays the animation correctly!<br>It IS essentially CORRECT.<br>I may bother to post a more flexible, live-action thing much later.<br><br>(Here ya go Rick Nasher!  Heh.)<br>I've updated my zip file posting also.<br><br>NOW you can watch it play AS the animation loads if you set inPlace=1 and CamAng=2    Ha!  SSD drives be praised!<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
; file loadBVH.bb
; Posted in Code Archives by Halo, 3/15/2002.

; Please mail &lt;a href=\"mailto:halo9@planetquake.com\"&gt;halo9@...; if you find any bugs.
; Standard animation commands may be used on the loaded skeleton.
; This was designed with the Character Studio motion capture samples.

;From Halo, 12/27/2001
;When creating an animation sequence with a hierarchal entity, you must set the animation key for each 
;entity in the hierarchy. You may then add the animseq to any entity in the hierarchy, and all children will
;be included. This wasn't easy to figure out. Please make it public knowledge.

Dim Split$(100)
Global SplitCnt
Global inPlace=0
Global InitPosX#
Global InitPosY#
Global InitPosZ#

CamAng=1:If inPlace=1 Then CamAng=2
Graphics3D 800,600,0,0
b=CreateLight(1):PositionEntity b,0,320,0
c=CreateCube():PointEntity b,c

Select CamAng
Case 0
	;Looks like what the Kinnect must've seen...
	a=CreateCamera():PositionEntity a,0,100,0
Case 1
	;Resembles "Just Walkin'.avi"
	a=CreateCamera():PositionEntity a,-60,200,10:TurnEntity a,30,0,0
Case 2
	;For inPlace
	a=CreateCamera():PositionEntity a,0,0,-150
End Select

d=LoadBVH("D:\Documents and Settings\Joseph D. Bockholt\Desktop\Kinnect Captures\Capture_0000.bvh",c,False,True,False)
FreeEntity c
DebugLog "DONE"

Select CamAng
Case 0
	;Looks like what the Kinnect must've seen...
	PositionEntity d,0,0,0:TurnEntity d,0,180,0
Case 1
	;Resembles "Just Walkin'.avi"
	PositionEntity d,50,0,0:TurnEntity d,0,200,0
Case 2
	;For inPlace
	PositionEntity d,0,0,0:TurnEntity d,0,180,0
	c=CreateSphere():ScaleMesh c,5,5,5 ;(at 0,0,0)
End Select

;Mode: Looped
;Speeds below 1.0 didn't work.
;Animation is sequence #1.
;Transition: No good. 0.
Animate d,1,1,0
While Not KeyHit(1)
UpdateWorld
RenderWorld
Flip
Wend
End

;=====================================================
;LoadBVH( file$, joint, endsites, rootanimmode, debug ) function by halo and TechLord
;file$ - file to load
;joint (optional) - entity to use for limbs.  A pivot is used by default.
;endsites (optional) - FALSE will not load end sites.
;rootanimmode (optional) - FALSE omits X and Z position keys for root node.
;debug (optional) - write file info to debuglog

Function LoadBVH(file$,joint=0,endsites=False,rootanimmode=False,debug=False)

f=ReadFile(file)

If Not f Return 0

figure=CreateMesh()

entitybank=CreateBank()
hierarchybank=CreateBank()
channelbank=CreateBank()

While s$&lt;&gt;"motion"

    s=ReadLine(f)
    s=Lower(Trim(s))
			SplitStr(s," ")
    If s="end site"
        If endsites
            s="joint endsite"
        Else
            limb=0
            ResizeBank hierarchybank,BankSize(hierarchybank)+4
            PokeInt hierarchybank,BankSize(hierarchybank)-4,limb
        EndIf
    EndIf

    Select Split$(0); piece(s,1," ") - faulty
    
        ;root entity
        Case "root"
									rootjnt=1
            If Not joint limb=CreatePivot() Else limb=CopyEntity(joint)
            NameEntity limb,Split$(1)
            EntityParent limb,figure
            ResizeBank hierarchybank,BankSize(hierarchybank)+4
            PokeInt hierarchybank,BankSize(hierarchybank)-4,limb
            ResizeBank entitybank,BankSize(entitybank)+4
            PokeInt entitybank,BankSize(entitybank)-4,limb
            ResizeBank channelbank,BankSize(channelbank)+1
            root=limb
            If debug Then DebugLog "Root: "+EntityName$(limb)
        ;new limb
        Case "joint"
									rootjnt=0
            If Not joint limb=CreatePivot() Else limb=CopyEntity(joint)
            NameEntity limb,Split$(1);Trim(piece(s,2," "))                
            EntityParent limb,PeekInt(hierarchybank,BankSize(hierarchybank)-4)
            ResizeBank hierarchybank,BankSize(hierarchybank)+4
            PokeInt hierarchybank,BankSize(hierarchybank)-4,limb
            ResizeBank entitybank,BankSize(entitybank)+4
            PokeInt entitybank,BankSize(entitybank)-4,limb
            ResizeBank channelbank,BankSize(channelbank)+1
            If debug
                DebugLog ""
                DebugLog "Joint: "+EntityName(limb)
                DebugLog "Parent: "+EntityName(GetParent(limb))
            EndIf

        ;position (relative to parent)    
        Case "offset"
									If (rootjnt And inPlace=0) Or rootjnt=0 Then
            If limb
                PositionEntity limb,Float(Split$(1)),Float(Split$(2)),Float(Split$(3))
                If debug DebugLog "Offset: "+EntityX(limb)+", "+EntityY(limb)+", "+EntityZ(limb)
            EndIf
            EndIf
        ;animation keys
        Case "channels"
            If limb
                channelcount=Int(Split$(1)) ;piece(s,2," ")
                If debug channelstring$=""
                For n=1 To channelcount
                    Select Lower(split$(1+n))
                        Case "xposition" : channels=channels+1 : If debug channelstring=channelstring+" XPosition"
                        Case "yposition" : channels=channels+2 : If debug channelstring=channelstring+" YPosition"                            
                        Case "zposition" : channels=channels+4 : If debug channelstring=channelstring+" ZPosition"
                        Case "xrotation" : channels=channels+8 : If debug channelstring=channelstring+" XRotation"
                        Case "yrotation" : channels=channels+16 : If debug channelstring=channelstring+" YRotation"
                        Case "zrotation" : channels=channels+32    : If debug channelstring=channelstring+" ZRotation"                    
                    End Select        
                Next
                If debug DebugLog "Channels:"+channelstring
                PokeByte channelbank,BankSize(channelbank)-1,channels
                channels=0
            EndIf
                
        ;step bank hierarchy
        Case "}"
            ResizeBank hierarchybank,BankSize(hierarchybank)-4
            
    End Select

Wend

;frames
s=ReadLine(f)
framecount=Int(Trim$(Mid$(s,8)))

;frame time
s=ReadLine(f)
framespeed#=Float(Trim$(Mid$(s,12)))

;more debug info
If debug
    DebugLog ""
    DebugLog "Framecount: "+framecount
    DebugLog "Framespeed: "+framespeed
    DebugLog ""
EndIf
    
;animation frames
keybank=CreateBank()
frmskp=1
For frame=1 To framecount; Step 30

    s=Trim(ReadLine(f))
    ResizeBank keybank,0
    readshift=0

			;BUGGY INFINITE LOOP replaced.
			SplitStr(s," ")
			For t=0 To SplitCnt
			key#=Float(Split$(t))
    ResizeBank keybank,BankSize(keybank)+4
    PokeFloat keybank,BankSize(keybank)-4,Float(key#)
			Next
			
    ;record channel values
    If debug
        s=""
        For n=0 To BankSize(keybank)/4-1
            s=s+PeekFloat(keybank,n*4)+" "
        Next
        DebugLog s
    EndIf

    For n=0 To BankSize(entitybank)/4-1
        limb=PeekInt(entitybank,n*4)
        channels=PeekByte(channelbank,n)
        
        poskey=False
        rotkey=False
        
        xposition#=EntityX#(limb)
        yposition#=EntityY#(limb)
        zposition#=EntityZ#(limb)
        xrotation#=EntityPitch#(limb)
        yrotation#=EntityYaw#(limb)
        zrotation#=EntityRoll#(limb)
        
        If 2^(0) And channels
            xposition=PeekFloat(keybank,(readshift*4))
            readshift=readshift+1
            poskey=True
            If Not rootanimmode
                If limb=root xposition=EntityX(limb)
            EndIf
        EndIf
        
        If 2^(1) And channels
            yposition=PeekFloat(keybank,(readshift*4))
            readshift=readshift+1
            poskey=True
        EndIf
        
        If 2^(2) And channels
            zposition=PeekFloat(keybank,(readshift*4))
            readshift=readshift+1
            poskey=True
            If Not rootanimmode
                If limb=root zposition=EntityZ(limb)
            EndIf
        EndIf    

        If 2^(3) And channels
            zrotation=PeekFloat(keybank,(readshift*4))
            readshift=readshift+1
            rotkey=True
        EndIf

        If 2^(4) And channels
            xrotation=PeekFloat(keybank,(readshift*4))
            readshift=readshift+1
            rotkey=True
        EndIf

        If 2^(5) And channels
            yrotation=PeekFloat(keybank,(readshift*4))
            readshift=readshift+1
            rotkey=True
        EndIf
        

						If n=0 And inPlace=0 Then
        If poskey PositionEntity limb,xposition,yposition,zposition
						End If
						
        If rotkey
            RotateEntity limb,0,0,0

            TurnEntity limb,0,0,zrotation#
            TurnEntity limb,xrotation#,0,0
            TurnEntity limb,0,yrotation#,0

            EndIf
        
    Next ;All limbs

			;See as we load
			If inPlace Then UpdateWorld:RenderWorld:Flip
			
    ;set key
    For n=0 To BankSize(entitybank)/4-1
        limb=PeekInt(entitybank,n*4)
        SetAnimKey limb,frmskp
    Next
frmskp=frmskp+1    
Next

;set last key
For n=0 To BankSize(entitybank)/4-1
			limb=PeekInt(entitybank,n*4)
			SetAnimKey limb,0
Next

AddAnimSeq figure,frmskp

;free memory blocks
FreeBank entitybank
FreeBank hierarchybank
FreeBank channelbank
FreeBank keybank

Return figure

End Function

Function SplitStr(a$,del$)
Local i,j
SplitCnt=0
For t=0 To MaxSplitCnt:Split$(t)="":Next
i=Inst(a$,del$)
If i=0 Then Split$(0)=a$:Return 0
i=1:SplitCnt=0
a$=del$+a$
While i&lt;&gt;0
j=Inst(a$,del$,i+1)
If j=0 Then j=Len(a$)+1
Split$(SplitCnt)=Mid$(a$,i+1,j-i-1)
If j&lt;Len(a$) Then i=j Else i=0
SplitCnt=SplitCnt+1
Wend
If Right$(a$,1)=del$ Then Split$(SplitCnt)="":SplitCnt=SplitCnt+1
SplitCnt=SplitCnt-1
Return SplitCnt
End Function

Function Inst(a$,b$,offset=1) ;Boy, don't name me Instr (bad recursion)
;FAULTY
If Len(a$)=0 Or Len(b$)=0 Or offset&lt;1 Or offset&gt;Len(a$) Then Return 0
Return Instr(a$,b$,offset)
End Function
</textarea><br><br>And here's MY version- Thanks to all who contributed to it and its components!<br>I wanted to make an ugly one that is easier to read without all of<br>the elegant bank work that the original program by Halo and TechLord had.<br>(It's in the .zip file:)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;BVH Viewer.bb
;Display and Animate a BVH in Blitz3D
;Mine's less flexible with file formats: It expects a standard
;Brekel Kinnect .bvh file to apply to a skin.
;By Mikorians 2015 - Various Contributors to the ideas here.
;Thanks to Floyd (CaptainPool) !!

;Settings:
Global Dir$="" ;Your folder for meshes and bmps
Global filnm$="capture_0000.bvh" ;The bvh
Global BVH$[22] ;Default BVH joint names
BVH[0]="Hips":BVH[1]="LeftHip":BVH[2]="LeftKnee":BVH[3]="LeftAnkle":BVH[4]="LeftAnkleTip"
BVH[5]="RightHip":BVH[6]="RightKnee":BVH[7]="RightAnkle":BVH[8]="RightAnkleTip"
BVH[9]="Chest":BVH[10]="LeftCollar":BVH[11]="LeftShoulder":BVH[12]="LeftElbow":BVH[13]="LeftWrist":BVH[14]="LeftWristTip"
BVH[15]="RightCollar":BVH[16]="RightShoulder":BVH[17]="RightElbow":BVH[18]="RightWrist":BVH[19]="RightWristTip"
BVH[20]="Neck":BVH[21]="Head":BVH[22]="HeadTip"
Global Jnt$[22] ;Your skelly names (leave blank for unaffected joints)
Global JntEnt[22] ;For storing the X-Ref entity pointers
Jnt[0]="z_Pelvis":Jnt[1]="z_ThighL":Jnt[2]="z_CalfL":Jnt[3]="z_FootL":Jnt[4]="z_ToeL"
Jnt[5]="z_ThighR":Jnt[6]="z_CalfR":Jnt[7]="z_FootR":Jnt[8]="z_ToeR"
Jnt[9]="z_BackU":Jnt[10]="z_ShouldL1":Jnt[11]="z_ArmL":Jnt[12]="z_ForearmL":Jnt[13]="z_HandL":Jnt[14]=""
Jnt[15]="z_ShouldR1":Jnt[16]="z_ArmR":Jnt[17]="z_ForearmR":Jnt[18]="z_HandR":Jnt[19]=""
Jnt[20]="z_Head":Jnt[21]="":Jnt[22]=""

Global DoSkelly=True			;Don't bother creating the bvh skelly
Global DoSkin=True  			;Do we try To apply the bvh skelly To our skin?
Global SkipFrames=10			;Minimum:1
Global inPlace=0						;No root Walking/movement allowed

Global MaxSplitCnt=100
Dim Split$(MaxSplitCnt)
Dim Cor#(MaxSplitCnt)
Global SplitCnt

fil=ReadFile(filnm$)
s$=ReadLine$(fil)
If Upper$(s$)&lt;&gt;"HIERARCHY" Then RuntimeError "Not a valid .bvh file!"
CloseFile(fil)

Type CamD
	Field Body
	Field Camera
End Type
Global Cam.CamD=New CamD

;Set up your scene 1st
Graphics3D 800,600,0,0
Cam\Camera=CreateCamera()
CameraRange Cam\Camera,1,10000;Whatever size your map CELL is, you should scale these to it
CameraFogRange Cam\Camera,9000,10000 ;Fade range
CameraFogMode Cam\Camera,1
CameraFogColor Cam\Camera,62,62,62
CameraZoom Cam\Camera,1.0/Tan(64.0108/2.0) ;Human Vision
Cam\Body=CreatePivot()
EntityParent Cam\Camera,Cam\Body
PositionEntity Cam\Body,0,50,-350

;Load your scene and a relevant actor (a)
l=CreateLight():TurnEntity l,45,45,0
ChangeDir(Dir$+"\Bmps\Actors")
a=LoadAnimMesh(Dir$+"\Actors\Minotaur.b3d") ;GET YER OWN!
ChangeDir(Dir$+"\Bmps")
;b=LoadAnimMesh(Dir$+"\_Test Scene.b3d")
;For coefficient body positioning, we need starting position
StPosX#=EntityX#(FindChild(a,Jnt[0]),True)
StPosY#=EntityY#(FindChild(a,Jnt[0]),True)
StPosZ#=EntityZ#(FindChild(a,Jnt[0]),True)-400

;Pre-parse the skeleton of the .bvh file
Global nj ;# Joints
Global nf ;# frames
fil=ReadFile(filnm$)
s$=ReadLine$(fil) ;HIERARCHY
While Not Eof(fil)
s$=ReadLine$(fil)
s$=Trim$(s$)
If Left$(s$,5)="ROOT " Or Left$(s$,6)="JOINT " Or Left$(Upper$(s$),8)="END SITE" Then nj=nj+1
If Left$(s$,7)="Frames:" Then nf=Int(Mid$(s$,9)):Exit
Wend
CloseFile(fil):fil=0
nj=nj-1
;So we can dim temp variables
Global hier										;Level in hierarchy
Dim Obj(nj+1)        			;Hierarchy construction pointers
Dim Crea(nj+1)								;''
Dim SkelBonNam$(nj+1)
Dim SkelBones(nj+1)				;The animation-relevant bones
Dim SkinBones(nj+1)				;The animation-relevant bones
Global frmskp       			;Kind of a General Counter
hier=0
chan=0
frmskp=SkipFrames-1

;******************
;* Process skelly *
;******************
;Bounding box variables
SMinX#=+99999.0:SMinY#=+99999.0:SMinZ#=+99999.0
SMaxX#=-99999.0:SMaxY#=-99999.0:SMaxZ#=-99999.0
KMinX#=+99999.0:KMinY#=+99999.0:KMinZ#=+99999.0
KMaxX#=-99999.0:KMaxY#=-99999.0:KMaxZ#=-99999.0
If DoSkelly=True Then JntObj=CreateCube() ;Joint knob shape
fil=ReadFile(filnm$)
s$=ReadLine$(fil) 														;HIERARCHY
t=0:chacnt=0
While Not Eof(fil)
s$=Trim$(ReadLine$(fil))
																											;JOINT
If Left$(s$,5)="ROOT " Or Left$(s$,6)="JOINT " Or Left$(Upper$(s$),8)="END SITE" Then
obj(hier)=t
If Left$(Upper$(s$),8)="END SITE" Then s$=PrevName$+"Tip"
i=Inst(s$," ")
s$=Mid$(s$,i+1)
Nam$=s$:PrevName$=Nam$
s$=ReadLine$(fil)															;{
s$=Trim$(ReadLine$(fil))									;OFFSET
SplitStr(s$," ")
x#=x#+Float(split$(1)):y#=y#+Float(split$(2)):z#=z#-Float(split$(3))
s$=Upper$(Trim$(ReadLine$(fil)))
If Left$(s$,9)="CHANNELS " Then				;CHANNELS (or not)
	chan=chan+Int(Mid$(s$,10))
	SkelBonNam$(chacnt)=Upper$(Nam$) ;Need for later xref
	If DoSkelly=True Then
		crea(t)=CopyEntity(JntObj)
		EntityPickMode crea(t),1
		EntityFX crea(t),9 ;Fullbright, non-foggable
		SkelBones(chacnt)=crea(t)			;Objects with channels-entities
	End If
	chacnt=chacnt+1
Else
	If DoSkelly=True Then
		crea(t)=CopyEntity(JntObj)
		EntityPickMode crea(t),1
		EntityFX crea(t),9 ;Fullbright, non-foggable
	End If
End If
If x#&lt;KMinX# Then KMinX#=x#
If y#&lt;KMinY# Then KMinY#=y#
If z#&lt;KMinZ# Then KMinZ#=z#
If x#&gt;KMaxX# Then KMaxX#=x#
If y#&gt;KMaxY# Then KMaxY#=y#
If z#&gt;KMaxZ# Then KMaxZ#=z#
If DoSkelly=True Then
If hier&gt;0 Then
	PositionEntity crea(t),x#,y#,z#,False
	EntityParent crea(t),crea(obj(hier-1)),True
	Else
	If inPlace=0 Then
		PositionEntity crea(t),x#,y#,z#,True
		Else
		PositionEntity crea(t),0,0,0,True
		If t=0 Then x#=0:y#=0:z#=0
	End If
End If
NameEntity crea(t),Nam$
End If
Hier=Hier+1:t=t+1
End If
;Back out 1 hierarchy branch
If s$="}" Then
	hier=hier-1
	If DoSkelly=True Then
	If hier&gt;1 Then
		x#=EntityX#(crea(obj(Hier-1)),True)
		y#=EntityY#(crea(obj(Hier-1)),True)
		z#=EntityZ#(crea(obj(Hier-1)),True)
	Else
		If inPlace=1 Then 
		x#=0:y#=0:z#=0
		Else
		x#=EntityX#(crea(0)):y#=EntityY#(crea(0)):z#=EntityZ#(crea(0))
		End If
	End If
	End If
End If
;End of Skelly
If Left$(s$,11)="Frame Time:" Then Exit
Wend
If DoSkelly=True Then FreeEntity JntObj
chacnt=chacnt-1

;**************************************
;* Obtain your skin's bones to rotate *
;**************************************
For t=0 To chacnt
For u=0 To 22
If SkelBonNam$(t)=Upper$(BVH[u]) Then
	SkinBones(t)=FindChild(a,Jnt[u])
	If SkinBones(t)&lt;&gt;0 Then
		x#=EntityX#(SkinBones(t),True)
		y#=EntityY#(SkinBones(t),True)
		z#=EntityZ#(SkinBones(t),True)
		If x#&lt;SMinX# Then SMinX#=x#
		If y#&lt;SMinY# Then SMinY#=y#
		If z#&lt;SMinZ# Then SMinZ#=z#
		If x#&gt;SMaxX# Then SMaxX#=x#
		If y#&gt;SMaxY# Then SMaxY#=y#
		If z#&gt;SMaxZ# Then SMaxZ#=z#
	End If
End If
Next
Next
;Compute ratio for body root bone motion
SMinX#=SMaxX#-SMinX# ;Distance
SMinY#=SMaxY#-SMinY#
SMinZ#=SMaxZ#-SMinZ#
KMinX#=KMaxX#-KMinX# ;Distance
KMinY#=KMaxY#-KMinY#
KMinZ#=KMaxZ#-KMinZ#
;These are now the coeffients to multiply by.
If KMinX#&lt;&gt;0 Then SMinX#=SMinX#/KMinX# Else SMinX#=0
If KMinY#&lt;&gt;0 Then SMinY#=SMinY#/KMinY# Else SMinY#=0
If KMinZ#&lt;&gt;0 Then SMinZ#=SMinZ#/KMinZ# Else SMinZ#=0

fm=0
fPos=FilePos(fil)
While Not KeyHit(1)
	fm=fm+1:If fm&gt;nf Then
		SeekFile(fil,fPos):fm=0
	Else ;Do animation
		s$=Trim$(ReadLine$(fil))
		splitstr(s$," ")
		For t=0 To splitcnt:cor#(t)=Float(Split$(t)):Next
		xp#=cor#(0)
		yp#=cor#(1)
		zp#=-cor#(2)
		v=0:For u=0 To chacnt ;(Joints with channels)
				qt=(v*3)+3
				zr#=cor#(qt+0)
				xr#=cor#(qt+1)
				yr#=cor#(qt+2)
				If DoSkelly Then
					RotateEntity SkelBones(u),0,0,0,False
					TurnEntity SkelBones(u),0,0,zr#,False
					TurnEntity SkelBones(u),-xr#,0,0,False
					TurnEntity SkelBones(u),0,yr#,0,False
					If u=0 And inPlace=0 Then
						PositionEntity SkelBones(0),xp#,yp#,zp#,True
					End If
				End If
				If DoSkin And SkinBones(u)&lt;&gt;0 Then
					RotateEntity SkinBones(u),0,0,0,False
					TurnEntity SkinBones(u),0,0,zr#,False
					;For my minotaur creature, I use this:
					If Left$(EntityName$(SkinBones(u)),6)="z_Calf" Then
						TurnEntity SkinBones(u),(-xr#)+15,0,0,False
						Else
						TurnEntity SkinBones(u),-xr#,0,0,False
					End If
					TurnEntity SkinBones(u),0,yr#,0,False
					If u=0 And inPlace=0 Then
						;I USE THIS JUST FOR ME - You might use just SkinBones(0)
						;The coefficient mostly corrects for the difference in skeleton sizes
						PositionEntity GetParent(SkinBones(0)),StPosX#+(xp#*SMinX#),StPosY#+(yp#*SMinY#),StPosZ#+(zp#*SMinZ#),True
					End If
				End If
				v=v+1
		Next
	End If
	
	If MouseHit(1)=True Then
		CameraPick(Cam\Camera,MouseX(),MouseY())
		If PickedEntity()&lt;&gt;0 Then DebugLog EntityName$(PickedEntity())
	End If

	;Turn
	mxs=0:mys=0:mdr#=0 ;PU,PD,R,L
	If KeyDown(201)	Or KeyDown(73) Then mxs=-3
	If KeyDown(209) Or KeyDown(81) Then mxs=3
	If Int((EntityPitch(Cam\Camera)+mxs)/90)=(EntityPitch(Cam\Camera)+mxs)/90 Then mxs=mxs*2 ;Avoid camera gimbal problem
	If KeyDown(205) Or KeyDown(77) Then mys=-3
	If KeyDown(203) Or KeyDown(75) Then mys=3
	
	;Rotations
	TurnEntity Cam\Body,0,mys,0
	If EntityYaw(Cam\Body)&lt;0 Then TurnEntity Cam\Body,0,360,0
	If EntityYaw(Cam\Body)&gt;360 Then TurnEntity Cam\Body,0,-360,0
	TurnEntity Cam\Camera,mxs,0,0
	If EntityPitch#(Cam\Camera)&lt;0 Then TurnEntity Cam\Camera,360,0,0
	If EntityPitch#(Cam\Camera)&gt;360 Then TurnEntity Cam\Camera,-360,0,0
	
	;Move
	If KeyDown(200)=True Or KeyDown(72)=True Then
			mdr#=10			;Forward
	End If
	If KeyDown(208)=True Or KeyDown(80)=True Then
			mdr#=-10			;Backward
	End If
	
	FixRoll()
	
		;Sidestep (for ladders)
	ss#=0
	If KeyDown(51) Then ss#=-5
	If KeyDown(52) Then ss#=+5

	TranslateEntity Cam\Body,-Sin(EntityYaw(Cam\Body))*mdr#,SS#,Cos(EntityYaw(Cam\Body))*mdr#,True

UpdateWorld
RenderWorld
Flip
Wend
If fil&lt;&gt;0 Then CloseFile(fil)

End

Function FixRoll() ;For kewokas dismount of wild carry objects
	tmp=CreatePivot()
	RotateEntity tmp,EntityPitch#(Cam\Camera,True),EntityYaw#(Cam\Body,True),0,True
	q=ParentEntity(Cam\Body)
	EntityParent Cam\Body,0
	RotateEntity Cam\Body,0,0,0,True
	RotateEntity Cam\Camera,0,0,0,True
	RotateEntity Cam\Camera,EntityPitch#(tmp,True),0,0,True
	RotateEntity Cam\Body,0,EntityYaw#(tmp,True),0,True
	EntityParent Cam\Body,q
	FreeEntity tmp
End Function

Function SplitStr(a$,del$)
Local i,j
SplitCnt=0
For t=0 To MaxSplitCnt:Split$(t)="":Next
i=Inst(a$,del$)
If i=0 Then Split$(0)=a$:Return 0
i=1:SplitCnt=0
a$=del$+a$
While i&lt;&gt;0
j=Inst(a$,del$,i+1)
If j=0 Then j=Len(a$)+1
Split$(SplitCnt)=Mid$(a$,i+1,j-i-1)
If j&lt;Len(a$) Then i=j Else i=0
SplitCnt=SplitCnt+1
Wend
If Right$(a$,1)=del$ Then Split$(SplitCnt)="":SplitCnt=SplitCnt+1
SplitCnt=SplitCnt-1
Return SplitCnt
End Function

Function Inst(a$,b$,offset=1) ;Boy, don't name me Instr (bad recursion)
;FAULTY
If Len(a$)=0 Or Len(b$)=0 Or offset&lt;1 Or offset&gt;Len(a$) Then Return 0
Return Instr(a$,b$,offset)
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1271869"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rick Nasher</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> Pretty cool Mikorians ! (bit of delayed response)<br>This should pave the way for people who want to capture real live motion to incorporate in a game. <br><br></td></tr></table><br>
<a name="1271873"></a>

<a name="1271874"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mikorians</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> I was glad it turned out to be real simple after all.<br>Thanks to Floyd and all contributors to the archive, posts, and any codearc submissions, who's code mine is a derivative/compilation of!<br>I just wanted to ENSURE blitz users would be able to easily use this type of file and reap the rewards of motion capture.<br>(Max 5 users too! THAT was tricky!)<br><br>If you see your work in my code- just post here "I helped" !!! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
