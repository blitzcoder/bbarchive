<!DOCTYPE html><html lang="en" ><head ><title >Pressing Buttons to Animate</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Pressing Buttons to Animate</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Pressing Buttons to Animate</a><br><br>
<a name="1338565"></a>

<a name="1338566"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >En929</td><td align="right"><font class=tiny>(Posted April)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I was having trouble with my animation. I wanted to make it so that when the left or right key is pressed, the walking animation starts (as in a normal game) and when it is not pressed, the animation stops. How do I do that with my code below? Thanks!<br><br><br><pre class=code>



Graphics3D 800,600 

SetBuffer BackBuffer() 

camera=CreateCamera() 
CameraViewport camera,0,0,800,600 

light=CreateLight() 

man= LoadAnimMesh ("Marty.b3d" ) 
PositionEntity man,0,-330,600 
ScaleEntity man, 3, 3, 3
RotateEntity man,0,0,0 




While Not KeyHit(1) 



	If KeyDown(203) Then 
	
		MoveEntity man, -3,0,0 

		 Animate man,1,0.5,0
	

		
	
	End If 
	
	If KeyDown(205) Then 
	
		MoveEntity man, 3,0,0 
	
		Animate man,1,0.5,0

		
	
	End If 
	


	
	
UpdateWorld 
RenderWorld 

Text 320,500,"An Animated MD2 Demo" 

Flip 

Wend 
End 
</pre> <br><br></td></tr></table><br>
<a name="1338570"></a>

<a name="1338571"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>(Posted April)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> untested but I think this logic should work:<br><br><pre class=code>
	If KeyDown(203) Then 
		MoveEntity man, -3,0,0 
		 Animate man,1,0.5,0
	ElseIf KeyDown(205) Then 	
		MoveEntity man, 3,0,0 
		Animate man,1,0.5,0		
	Else
		Animate man,0
	End If 
</pre> <br><br></td></tr></table><br>
<a name="1338586"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >En929</td><td align="right"><font class=tiny>(Posted April)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Skidracer, I copied and pasted the code that you sent, but it's not working correctly. <br><br></td></tr></table><br>
<a name="1338631"></a>

<a name="1338632"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted April)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> here is an extract from a code i worked on a few months ago (all worked well) :<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;before the mainloop :

;the humanoid mesh (rigged skinned animated) and the animations
Global XLDHumanoid = LoadAnimMesh("temps\LDhumanoid50w175h50d 201702031242.b3d")
ScaleEntity(XLDHumanoid,0.01,0.01,0.01)
HideEntity(XLDHumanoid)

Global XStandIdleAnimation = ExtractAnimSeq(XLDHumanoid,1,1)
Global XStandStealthAnimation = ExtractAnimSeq(XLDHumanoid,1,1)
Global XStandWalkAnimation = ExtractAnimSeq(XLDHumanoid,3,43)
Global XStandRunAnimation = ExtractAnimSeq(XLDHumanoid,45,75)
Global XStandProtectAnimation = ExtractAnimSeq(XLDHumanoid,1,1)
Global XStandAttackWithWeaponRAnimation = ExtractAnimSeq(XLDHumanoid,77,87)
Global XStandAttackWithWeaponLAnimation = ExtractAnimSeq(XLDHumanoid,101,111)
Global XStandAttackWithProjectileRAnimation = ExtractAnimSeq(XLDHumanoid,134,146)
Global XStandAttackWithProjectileLAnimation = ExtractAnimSeq(XLDHumanoid,157,169)
Global XStandAttackWithGrenadeRAnimation = ExtractAnimSeq(XLDHumanoid,180,192)
Global XStandAttackWithGrenadeLAnimation = ExtractAnimSeq(XLDHumanoid,203,215)

;Characters (arrays for the list of characters and their properties/states)
Global CharactersCount%
Dim Character_Name$(100)
;Dim Character_Gender%(100)
;Dim Character_BodyShape%(100)
;Dim Character_FaceShape%(100)
;Dim Character_SkinColor%(100)
;Dim Character_EyesColor%(100)
;Dim Character_HairShape%(100)
;Dim Character_HairColor%(100)
;Dim Character_Model$(100)
Dim Character_Root(100)
Dim Character_Eyes(100)
Dim Character_Eardrums(100)
Dim Character_Skin(100)
Dim Character_On%(100) ;onair or onwater or onearth
Dim Character_PostureMode%(100) ;stand or crouch
Dim Character_MovementMode%(100) ;stealth or walk or run
Dim Character_AttackMode%(100) ;withweapon or withprojectile or withgrenade
Dim Character_Action%(100)
Dim Character_MotionVariation%(100) ;1 or 2 (different animations for the same action)
Dim Character_AnimationState%(100) ;true or false
Dim Character_Mesh(100)
Dim Character_ArmorH%(100) ;item handle
Dim Character_ShieldH%(100) ;item handle
Dim Character_WeaponH%(100) ;item handle
Dim Character_ProjectilesCount%(100) ;projectiles count
Dim Character_GrenadesCount%(100) ;projectiles count
Dim Character_MoneyCount%(100) ;money count
Dim Character_ItemsCount%(100) ;items count
Dim Character_ItemH%(100,10) ;item handle
Dim Character_BodyState#(100)
Dim Character_MentalState#(100)
Dim Character_Energy#(100)
Dim Character_ArmorProtection#(100)
Dim Character_ShieldProtection#(100)
Dim Character_WeaponDamage#(100)
Dim Character_ProjectileDamage#(100)
Dim Character_GrenadeDamage#(100)
Dim Character_Alignment%(100) ;neutral, ally, ennemy

;create some characters 
;player
;bots (neutrals, allies, ennemies)
SetBuffer(ImageBuffer(TurningMovings_Image))
I% = 0
For PX% = 0 To 100-1 Step 1
 For PY% = 0 To 100-1 Step 1
  GX% = PX
  GZ% = 100-1-PY
  OldI% = CharactersCount
  ReadPix(PX,PY)
  PR% = PixRed : PG% = PixGreen : PB% = PixBlue
  If( PR = 000 And PG = 000 And PB = 175 )
   ;create player
   CharactersCount = CharactersCount + 1 : I% = CharactersCount
   Character_Root(I) = CreatePivot()
   Character_Eyes(I) = CreatePivot()
   MoveEntity(Character_Eyes(I),0,1.65,0)
   EntityParent(Character_Eyes(I),Character_Root(I),True)
   Character_Mesh(I) = CopyEntity(XLDHumanoid)
   EntityColor(Character_Mesh(I),000,000,175)
   EntityParent(Character_Mesh(I),Character_Root(I),True)
   ;Character_On(I) = COnAir
   ;Character_PostureMode(I) = CStand
   Character_MovementMode(I) = CWalk
   Character_AttackMode(I) = CWithWeapon
   Character_MotionVariation(I) = Rand(1,2)
   Player_I = I
  Else If( PR = 000 And PG = 175 And PB = 175 )
   ;create a bot
   CharactersCount = CharactersCount + 1 : I% = CharactersCount
   Character_Root(I) = CreatePivot()
   Character_Eyes(I) = CreatePivot()
   MoveEntity(Character_Eyes(I),0,1.65,0)
   EntityParent(Character_Eyes(I),Character_Root(I),True)
   Character_Mesh(I) = CopyEntity(XLDHumanoid)
   EntityColor(Character_Mesh(I),000,175,175)
   EntityParent(Character_Mesh(I),Character_Root(I),True)
  EndIf
  If( I &gt; OldI )
   HeightY# = TerrainY(Terrain_ROAM,GX+0.5,0,GZ+0.5)
   PositionEntity(Character_Root(I),GX+0.5,HeightY,GZ+0.5,True)
   RotateEntity(Character_Root(I),0,Rand(-180,180),0,True)
   NameEntity(Character_Mesh(I),"Character"+"-"+Str(I))
  EndIf
 Next
Next
DebugLog("CharactersCount = "+CharactersCount)



;during the mainloop :

 I% = Player_I
 
 ;camera view
 PositionEntityLikeOtherEntity(View_Root,Character_Root(I))

 If( KeyDown(200)=1 )
  View_Pitch = View_Pitch + 3.0
  If( View_Pitch &gt; 90.0 )
   View_Pitch = 90.0
  EndIf
 Else If( KeyDown(208)=1 )
  View_Pitch = View_Pitch - 3.0
  If( View_Pitch &lt; 45.0 )
   View_Pitch = 45.0
  EndIf
 EndIf

 If( KeyDown(203)=1 )
  View_Yaw = View_Yaw - 3.0
  If( View_Yaw &lt; -180.0 )
   View_Yaw = 180.0
  EndIf
 Else If( KeyDown(205)=1 )
  View_Yaw = View_Yaw + 3.0
  If( View_Yaw &gt; 180.0 )
   View_Yaw = -180.0
  EndIf
 EndIf
 
 RotateEntity(View_Root,0,View_Yaw,0,False)

 ;On state
 ;Character_On(I) = COnEarh

 ;character orientation depending on cursor position
 EntityPickMode(Plane,2)
 PEntity = 0
 CameraPick(Camera,MX,MY)
 PEntity = PickedEntity()
 If( PEntity &lt;&gt; 0 )
  PositionEntity(Marker,PickedX(),PickedY(),PickedZ(),True)
  PointEntity(Character_Root(I),Marker,0)
  RotateEntity(Character_Root(I),0,EntityYaw(Character_Root(I),True),0,True)
 EndIf
 EntityPickMode(Plane,0)

 ;Posture mode
 ;Character_PostureMode(I) = CStand

 ;Movement mode depending on if shiftL held/notheld 
  ;stealth
  ;walk
  ;run
 If( KeyHit(29)=1 )
  If( Character_MovementMode(I) = CStealth )
   ;
  Else If( Character_MovementMode(I) = CWalk )
   Character_MovementMode(I) = CStealth
  Else If( Character_MovementMode(I) = CRun )
   Character_MovementMode(I) = CWalk
  EndIf
 Else If( KeyHit(42)=1 )
  If( Character_MovementMode(I) = CStealth )
   Character_MovementMode(I) = CWalk
  Else If( Character_MovementMode(I) = CWalk )
   Character_MovementMode(I) = CRun
  Else If( Character_MovementMode(I) = CRun )
   ;
  EndIf
 EndIf

 ;Attack mode
 If( MouseHit(3)=1 )
  If( Character_AttackMode(I) = CWithWeapon )
   Character_AttackMode(I) = CWithProjectile
  Else If( Character_AttackMode(I) = CWithProjectile )
   Character_AttackMode(I) = CWithGrenade
  Else If( Character_AttackMode(I) = CWithGrenade )
   Character_AttackMode(I) = CWithWeapon
  EndIf
 EndIf

 ;protect or attackwithweapon or attackwithprojectile or attackwithgrenade or interact or useitem or move depending on mouseL mouseR mouseM hit/held/nothit/notheld

 If( Character_AnimationState(I) = True )
  
  If( AnimSeq(Character_Mesh(I)) = XStandProtectAnimation )
   If( AnimTime(Character_Mesh(I)) = 1-1 )
    Character_AnimationState(I) = False
   EndIf 
  Else If( AnimSeq(Character_Mesh(I)) = XStandAttackWithWeaponRAnimation )
   If( AnimTime(Character_Mesh(I)) = 87-77 )
    Character_AnimationState(I) = False
   EndIf 
  Else If( AnimSeq(Character_Mesh(I)) = XStandAttackWithWeaponLAnimation )
   If( AnimTime(Character_Mesh(I)) = 111-101 )
    Character_AnimationState(I) = False
   EndIf
  Else If( AnimSeq(Character_Mesh(I)) = XStandAttackWithProjectileRAnimation )
   If( AnimTime(Character_Mesh(I)) = 146-134 )
    Character_AnimationState(I) = False
   EndIf
  Else If( AnimSeq(Character_Mesh(I)) = XStandAttackWithProjectileLAnimation )
   If( AnimTime(Character_Mesh(I)) = 169-157 )
    Character_AnimationState(I) = False
   EndIf
  Else If( AnimSeq(Character_Mesh(I)) = XStandAttackWithGrenadeRAnimation )
   If( AnimTime(Character_Mesh(I)) = 192-180 )
    Character_AnimationState(I) = False
   EndIf
  Else If( AnimSeq(Character_Mesh(I)) = XStandAttackWithGrenadeLAnimation )
   If( AnimTime(Character_Mesh(I)) = 215-203 )
    Character_AnimationState(I) = False
   EndIf
  EndIf

 Else If( Character_AnimationState(I) = False )

  Character_Action(I) = CStandIdle
 
  If( Character_MovementMode(I) = CStealth )
   Speed# = 0.025
   If( MouseDown(2)=1 )
    Character_Action(I) = CStandStealth
   EndIf
  Else If( Character_MovementMode(I) = CWalk )
   Speed# = 0.05
   If( MouseDown(2)=1 )
    Character_Action(I) = CStandWalk
   EndIf
  Else If( Character_MovementMode(I) = CRun ) 
   Speed# = 0.15
   If( MouseDown(2)=1 )
    Character_Action(I) = CStandRun
   EndIf
  EndIf

  If( KeyHit(56)=1 ) ;protect
   Character_Action(I) = CStandProtect
  Else If( MouseHit(1)=1 ) ;attackwithweapon or attackwithprojectile or attackwithgrenade
   If( Character_AttackMode(I) = CWithWeapon )
    Character_Action(I) = CStandAttackWithWeapon
    If( Character_MotionVariation(I) = 1 )
     Character_MotionVariation(I) = 2
    Else If( Character_MotionVariation(I) = 2 )
     Character_MotionVariation(I) = 1
    EndIf
   Else If( Character_AttackMode(I) = CWithProjectile )
    Character_Action(I) = CStandAttackWithProjectile
    If( Character_MotionVariation(I) = 1 )
     Character_MotionVariation(I) = 2
    Else If( Character_MotionVariation(I) = 2 )
     Character_MotionVariation(I) = 1
    EndIf
   Else If( Character_AttackMode(I) = CWithGrenade )
    Character_Action(I) = CStandAttackWithGrenade
    If( Character_MotionVariation(I) = 1 )
     Character_MotionVariation(I) = 2
    Else If( Character_MotionVariation(I) = 2 )
     Character_MotionVariation(I) = 1
    EndIf
   EndIf
  Else If( KeyHit(56)=1 ) ;interact
   Character_Action(I) = CInteract
  EndIf

  If( Character_Action(I) = CStandIdle )
   If( AnimSeq(Character_Mesh(I)) &lt;&gt; XStandIdleAnimation ) Or ( Animating(Character_Mesh(I)) = False )
    Animate(Character_Mesh(I),3,1.0,XStandIdleAnimation)
   EndIf
  Else If( Character_Action(I) = CStandStealth )
   MoveEntity(Character_Root(I),0,0,Speed)
   If( AnimSeq(Character_Mesh(I)) &lt;&gt; XStandStealthAnimation ) Or ( Animating(Character_Mesh(I)) = False )
    Animate(Character_Mesh(I),3,1.0,XStandStealthAnimation)
   EndIf
  Else If( Character_Action(I) = CStandWalk )
   MoveEntity(Character_Root(I),0,0,Speed)
   If( AnimSeq(Character_Mesh(I)) &lt;&gt; XStandWalkAnimation ) Or ( Animating(Character_Mesh(I)) = False )
    Animate(Character_Mesh(I),3,1.0,XStandWalkAnimation)
   EndIf
  Else If( Character_Action(I) = CStandRun )
   MoveEntity(Character_Root(I),0,0,Speed)
   If( AnimSeq(Character_Mesh(I)) &lt;&gt; XStandRunAnimation ) Or ( Animating(Character_Mesh(I)) = False )
    Animate(Character_Mesh(I),3,1.0,XStandRunAnimation)
   EndIf
  Else If( Character_Action(I) = CStandProtect )
   If( AnimSeq(Character_Mesh(I)) &lt;&gt; XStandProtectAnimation ) Or ( Animating(Character_Mesh(I)) = False )
    Animate(Character_Mesh(I),3,1.0,XStandProtectAnimation)
    Character_AnimationState(I) = True
   EndIf
  Else If( Character_Action(I) = CStandAttackWithWeapon )
   If( Character_MotionVariation(I) = 1 )
    If( AnimSeq(Character_Mesh(I)) &lt;&gt; XStandAttackWithWeaponRAnimation ) Or ( Animating(Character_Mesh(I)) = False )
     Animate(Character_Mesh(I),3,1.0,XStandAttackWithWeaponRAnimation)
     Character_AnimationState(I) = True
    EndIf
   Else If( Character_MotionVariation(I) = 2 )
    If( AnimSeq(Character_Mesh(I)) &lt;&gt; XStandAttackWithWeaponLAnimation ) Or ( Animating(Character_Mesh(I)) = False )
     Animate(Character_Mesh(I),3,1.0,XStandAttackWithWeaponLAnimation)
     Character_AnimationState(I) = True
    EndIf
   EndIf
  Else If( Character_Action(I) = CStandAttackWithProjectile )
   If( Character_MotionVariation(I) = 1 )
    If( AnimSeq(Character_Mesh(I)) &lt;&gt; XStandAttackWithProjectileRAnimation ) Or ( Animating(Character_Mesh(I)) = False )
     Animate(Character_Mesh(I),3,1.0,XStandAttackWithProjectileRAnimation)
     Character_AnimationState(I) = True
    EndIf
   Else If( Character_MotionVariation(I) = 2 )
    If( AnimSeq(Character_Mesh(I)) &lt;&gt; XStandAttackWithProjectileLAnimation ) Or ( Animating(Character_Mesh(I)) = False )
     Animate(Character_Mesh(I),3,1.0,XStandAttackWithProjectileLAnimation)
     Character_AnimationState(I) = True
    EndIf
   EndIf
  Else If( Character_Action(I) = CStandAttackWithGrenade )
   If( Character_MotionVariation(I) = 1 )
    If( AnimSeq(Character_Mesh(I)) &lt;&gt; XStandAttackWithGrenadeRAnimation ) Or ( Animating(Character_Mesh(I)) = False )
     Animate(Character_Mesh(I),3,1.0,XStandAttackWithGrenadeRAnimation)
     Character_AnimationState(I) = True
    EndIf
   Else If( Character_MotionVariation(I) = 2 )
    If( AnimSeq(Character_Mesh(I)) &lt;&gt; XStandAttackWithGrenadeLAnimation ) Or ( Animating(Character_Mesh(I)) = False )
     Animate(Character_Mesh(I),3,1.0,XStandAttackWithGrenadeLAnimation)
     Character_AnimationState(I) = True
    EndIf
   EndIf
  Else If( Character_Action(I) = CStandInteract )

  EndIf

 EndIf

 PositionRotateEntityLikeOtherEntity(Camera,View_Root)
 TurnEntity(Camera,View_Pitch,0,0)
 MoveEntity(Camera,0,0,-10.0)
</textarea> <br><br></td></tr></table><br>
<a name="1338654"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Flanker</td><td align="right"><font class=tiny>(Posted April)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> The code posted by skidracer should work for B3D model.<br><br>At the bottom of your code you wrote "An Animated MD2 Demo", if your model is an MD2 you'll have to use the command AnimateMD2, wich is a little different. <br><br></td></tr></table><br>
<a name="1339870"></a>

<a name="1339871"></a>

<a name="1339872"></a>

<a name="1339873"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >En929</td><td align="right"><font class=tiny>(Posted April)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, I got my animation ALMOST to where I want it. Thanks for the info Skyricer, I got it to work. And Flanker, lol, the "An Animated MD2 Demo" was something that I forgot to take out. I copied and pasted some of this code. Remi, thanks for the help. I'm kind of a newbie to programming in Blitz 3D, thus I was trying to learn more about what you wrote in maybe small steps. But, what do you think about my code below?<br><br>I got my walking animation to work (after a lot of wrestling with the codes) and the walking animation works with all the buttons that I wanted. That is, when I press the arrow keys, my character walks.<br><br>But, now I'm wondering why isn't my punch animation playing through? When I hit the "A" key, it only plays the first animation frame, but stops there. Thus, what should I add to my code to get it to play the full animation when the A key is pressed. Here's the code that I came up with.<br><br>By the way, I put both my walk animation and punch animation all on the same strip.<br><br>Thanks<br><br><br><pre class=code>

Graphics3D 800,600 

SetBuffer BackBuffer() 

camera=CreateCamera() 
CameraViewport camera,0,0,800,600 

light=CreateLight() 



man = LoadAnimMesh ("Henry2.b3d") 
ScaleEntity man, 3, 3, 3
RotateEntity man,0,0,0 
PositionEntity  man, frames -0,-330,600 

walking = ExtractAnimSeq(man, 3,30)


;This is the punch animation that I added.
;The beginning of this clip starts at frame 49
;on the filmstrip.
punching = ExtractAnimSeq(man, 49,99)




While Not KeyHit(1) 


If KeyDown(200) Or KeyDown(208) Or KeyDown(203) Or KeyDown(205) Then

	



Else 
	 
		Animate man, 1,0.5, walking 


EndIf 




If KeyDown(30) Then

;this is the part that I needed help with specifically. The punch animation
;only plays the first frame when I press the A key. But, I want to play the full animation here.

Animate man, 1, 1, punching






Else 




EndIf 






;----------------------Everything below this line is fine for now. Thanks!------------------------------------------------------


If KeyDown(200) MoveEntity man,  0,3,0 
If KeyDown(205) MoveEntity man,  3,0,0 
If KeyDown(208) MoveEntity man,  0,-3,0 
If KeyDown(203) MoveEntity man, -3,0,0 


		
UpdateWorld 
RenderWorld 



Flip 

Wend 
End 

</pre> <br><br></td></tr></table><br>
<a name="1339909"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted April)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> An punching animation should start on Keydown() like the walking animation. But then it should run independent from the Key status. So try this:<br><br>PunchIsRunning knows three status: 0="No Punch", 1="Start Punch" and 2="Punch already running"<br> <br><pre class=code>
Global PunchIsRunning=0
Const ONE_SHOT=3 
....
If PunchIsRunning=1 ;animation starts
     Animate man, ONE_SHOT, 1, punching
     PunchIsRunning=2
ElseIf PunchIsRunning=2 ; animation already running
     If AnimationTime=99
          PunchIsRunning=0 ;reset,when ready     
     Endif
ElseIf KeyDown(30) Then
     PunchIsRunning=1
EndIf 
</pre> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
