<!DOCTYPE html><html lang="en" ><head ><title >Car movement in 3d</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Car movement in 3d</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Car movement in 3d</a><br><br>
<a name="292017"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Insane Games</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi to all! I'm trying to make a car movement where the wheels make is what make the path... i wan't to do something like this: <a href="http://venisproductions.com/games/drivers_ed/drivers_ed.html" target="_blank">http://venisproductions.com/games/drivers_ed/drivers_ed.html</a><br>Look my code... and please help me tks!!!<br><br><pre class=code>
Graphics3D 640,480,16,2
SetBuffer BackBuffer()

camera = CreateCamera()
PositionEntity camera,0,5,-10
RotateEntity camera,30,0,0

pivo = CreatePivot()

roda1 = CreateSphere(10,pivo)
roda2 = CreateSphere(10,pivo)
ScaleEntity roda1,.5,1,1
ScaleEntity roda2,.5,1,1
PositionEntity roda1,-2,0,0
PositionEntity roda2,2,0,0

While Not KeyHit(1)
	
If KeyDown(203) And virando# &lt; 30
virando# = virando# + 1
EndIf
	
If KeyDown(205) And virando# &gt; -30
virando# = virando# - 1
EndIf

If KeyDown(200) And andando# &lt; 3
andando# = andando# + 0.03
EndIf
	
If KeyDown(208) And andando# &gt; -2
andando# = andando# - 0.03
EndIf
	
RotateEntity roda1,0,virando#,0
RotateEntity roda2,0,virando#,0
	
TurnEntity pivo,0,virando#,0
MoveEntity pivo,0,0,andando#
	
UpdateWorld
RenderWorld
Text 10,10,"Virando: "+virando#
Text 10,20,"Andando: "+andando#
Flip

Wend
</pre> <br><br></td></tr></table><br>
<a name="292089"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> eh.... hey. Is there nayway you can re-pharse that question. I don't understand. :) <br><br></td></tr></table><br>
<a name="292118"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Insane Games</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sorry eheheh, i want to make a car movement like the shockwave... that's all!<br><br>Tks <br><br></td></tr></table><br>
<a name="292409"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jeppe Nielsen</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Not ultra realistic, but still.. :)<br><pre class=code>
Const gravity#=-0.01 ;gravity constant
Graphics3D 640,480,16,2

Const car_col=1
Const world_col=2
Collisions car_col,world_col,2,2

light=CreateLight(1)
RotateEntity light,30,20,0

tex=CreateTexture(64,64,1+8)
SetBuffer TextureBuffer(tex)
Color 255,0,0
Rect 0,0,64,64,1
Color 255,255,255
Rect 0,0,32,32,1
Rect 32,32,32,32,1
SetBuffer BackBuffer()

plane=CreatePlane()
EntityTexture plane,tex
ScaleTexture tex,10,10
EntityAlpha plane,0.5
CreateMirror()

EntityType plane,world_col


For n=1 To 10

If Rnd(10)&lt;5

sphere=CreateSphere(16)

Else

sphere=CreateCube()

EndIf

EntityType sphere,world_col
PositionEntity sphere,Rnd(-40,40),Rnd(2),Rnd(-40,40)
EntityColor sphere,Rnd(255),Rnd(255),Rnd(255)

Next


camera=CreateCamera()
CameraClsColor camera,0,0,255


car.car=carnew(0,5,0)

Repeat
TFormPoint 0,5,-8,car\e,0

dx#=(TFormedX()-EntityX(camera))*.01
dy#=(TFormedY()-EntityY(camera))*.01
dz#=(TFormedZ()-EntityZ(camera))*.01

TranslateEntity camera,dx,dy,dz

PointEntity camera,car\e

carcontrol()
carupdate()

RenderWorld()

Flip


Until KeyDown(1)
End


Type car

Field e ;entity

Field x#,y#,z# ; position in 3d-space

Field vx#,vy#,vz# ; velocity

Field ax#,ay#,az# ; acceleration

Field wheel[3]

Field wheelangle#

End Type


Function carnew.car(x#,y#,z#)
	
	c.car=New car
	
	c\x#=x#
	c\y#=y#
	c\z#=z#
	
	c\e=CreateCube()
	ScaleMesh c\e,2,0.5,3
	
	For n=0 To 3
		c\wheel[n]=CreateSphere(8,c\e)
		ScaleMesh c\wheel[n],0.2,1,1
	Next
	PositionEntity c\wheel[0],-2.2,-0.5,2.8
	PositionEntity c\wheel[1],2.2,-0.5,2.8
	PositionEntity c\wheel[2],-2.2,-0.5,-2.8
	PositionEntity c\wheel[3],2.2,-0.5,-2.8
	
	
	EntityType c\e,car_col
	EntityRadius c\e,1.5
	
	PositionEntity c\e,c\x,c\y,c\z
	
	Return c
	
End Function

Function carupdate()
	
	For c.car=Each car
	
		c\vy#=c\vy#+gravity#
		
		c\vx#=c\vx#+c\ax#
		c\vy#=c\vy#+c\ay#
		c\vz#=c\vz#+c\az#
		
		c\x#=EntityX(c\e)
		c\y#=EntityY(c\e)
		c\z#=EntityZ(c\e)
		
		TranslateEntity c\e,c\vx,c\vy,c\vz
	
	Next
	
	UpdateWorld()
	
	For c.car=Each car
		
		;correct velocity if collided
		c\vx=(EntityX(c\e)-c\x)
		c\vy=(EntityY(c\e)-c\y)
		c\vz=(EntityZ(c\e)-c\z)
		
		
		;slow down due to friction
		If EntityCollided(c\e,world_col)
			
			c\vx#=c\vx*0.98
			c\vy#=c\vy*0.98
			c\vz#=c\vz*0.98
		
		EndIf
		
		c\ax#=0
		c\ay#=0
		c\az#=0
		
	Next
	
End Function


Function carcontrol()
	
	For c.car=Each car
		
		If KeyDown(200)
					
			TFormVector 0,0,0.02,c\wheel[0],0
			
			c\ax#=TFormedX()
			c\ay#=TFormedY()
			c\az#=TFormedZ()
			
			TurnEntity c\e,0,c\wheelangle#/10,0
			
		EndIf
		
		If KeyDown(208)
			
			c\vx=c\vx*0.99
			c\vy=c\vy*0.99
			c\vz=c\vz*0.99
			
		EndIf
		
		If KeyDown(57)
			
			TFormVector 0,0.05,0,c\e,0
			
			c\ax#=c\ax+TFormedX()
			c\ay#=c\ay+TFormedY()
			c\az#=c\az+TFormedZ()
			
									
		EndIf
		
		If KeyDown(203)
			
			c\wheelangle#=c\wheelangle#+2
			If c\wheelangle#&gt;50
				c\wheelangle#=50
			EndIf
			
			RotateEntity c\wheel[0],0,c\wheelangle#,0
			RotateEntity c\wheel[1],0,c\wheelangle#,0
					
		EndIf
		
		If KeyDown(205)
			
			c\wheelangle#=c\wheelangle#-2
			If c\wheelangle#&lt;-50
				c\wheelangle#=-50
			EndIf
			
			RotateEntity c\wheel[0],0,c\wheelangle#,0
			RotateEntity c\wheel[1],0,c\wheelangle#,0

			
		EndIf
		
		
	Next
	
	
	
End Function
</pre> <br><br></td></tr></table><br>
<a name="293919"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Insane Games</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Tks Jeppe! I like!! Nice! <br><br></td></tr></table><br>
<a name="294037"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >eBusiness</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>
Graphics 800,600
speed#=0
posx#=400
posy#=320
rot#=0
wheel#=0
While KeyDown(1)=0
	Delay 40
	If KeyDown(200) Then speed=speed+.4
	If KeyDown(208) Then speed=speed-.2
	speed=speed*.95
	speed2#=Abs(speed)
	If speed2&lt;.01 Then speed2=.01
	If KeyDown(203) Then
		wheel=wheel+.2
	Else If KeyDown(205) Then
		wheel=wheel-.2
	Else If wheel&lt;.2 And wheel&gt;-.2 Then
		wheel=0
	Else If wheel&gt;0 Then
		wheel=wheel-.2
	Else If wheel&lt;0 Then
		wheel=wheel+.2
	End If
	If wheel&gt;1.8 Then wheel=1.8
	If wheel&lt;-1.8 Then wheel=-1.8
	If wheel&gt;3/speed2 Then wheel=3/speed2
	If wheel&lt;-3/speed2 Then wheel=-3/speed2
	rot=rot+wheel*speed
	posx=posx+Sin(rot)*speed
	posy=posy+Cos(rot)*speed
	Cls
	WritePixel posx,posy,16777215
	WritePixel posx+Sin(rot)*20,posy+Cos(rot)*20,16777215
	Text 0,0,speed
Wend
</pre><br><br>This should be a pretty realistic car. Funny game you found there. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
