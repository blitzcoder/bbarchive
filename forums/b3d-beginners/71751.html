<!DOCTYPE html><html lang="en" ><head ><title >Drawing Cards?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Drawing Cards?</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Drawing Cards?</a><br><br>
<a name="801798"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Fuller</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> i'm making a simple card game and am having trouble "drawing" or picking cards from a deck.<br><br>here's my attempt:<br><pre class=code>
For x=1 To drawnum 
temp(x)=Rand(1,drawnum)
Next 

For c.card=Each card
	If c\num=temp(x) And c\where$="" Then 
		c\where$="hand"
		c\x=500+nx     ;offset hand value
	EndIf 
Next 
</pre><br><br>this doesn't seem to work as it will draw multiple card of the same kind.<br>Please Help! <br><br></td></tr></table><br>
<a name="801807"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Fuller</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>
For x=1 To drawnum 
temp(x)=Rand(1,drawnum)
For xx=1 To drawnum
If temp(x)=temp(xx) And x&lt;&gt;xx Then drawnum=drawnum+1
Next 
Next 

For x=1 To drawnum
For c.card=Each card
	If c\num=temp(x) And c\where$="" Then 
		c\where$="hand"
		c\x=500+nx     ;offset hand value
	EndIf 
Next 
Next 
</pre><br><br>ok, this is selecting the first two of three cards i told it to draw but not the third one. Whats wrong? <br><br></td></tr></table><br>
<a name="801868"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Fuller</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> hmm.. i kinda rooted the problem down to this:<br>i need a function that return a different number between a [to] and [from] variable that is much like the rand() function.<br><br>Seems simple but i cant get it. <br><br></td></tr></table><br>
<a name="801928"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yahfree</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> hmm, i'll try to whip up a small example of drawing random numbers <br><br></td></tr></table><br>
<a name="801933"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yahfree</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> heres a small example:<br><br><pre class=code>
Type Card
	Field name_number%
	Field name$
	Field number%
End Type

Global maxnumber

Graphics 500,300
SeedRnd MilliSecs()

Print "Press R to redraw!"
maxnumber%=Input("Whats the max amount of cards to be drawn?")
Print ""

ReDraw()

i=0

While Not KeyHit(1)

If KeyHit(19)
	ReDraw()
End If

Wend
End
	
	
	
Function ReDraw()

Delete Each Card

For i=0 To maxnumber-1

	DrawnCard.Card = New Card
	DrawnCard\name_number = Rand(1,4)
	
	Select DrawnCard\name_number
	
		Case 1: DrawnCard\name = "Spades"
		Case 2: DrawnCard\name = "Hearts"
		Case 3: DrawnCard\name = "Dimonds"
		Case 4: DrawnCard\name = "Clubs"
	
	End Select
	DrawnCard\number = Rand(1,10)
	
Next

Print ""
i=1
	For Hand.Card = Each Card
		Print "Card "+i+": " + Hand\number + " Of " + Hand\name
		i=i+1
	Next

End Function
</pre><br><br><br>hope it helps <br><br></td></tr></table><br>
<a name="801936"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Fuller</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> thats helpful but i'm getting duplicates:<br><div class="quote"> <br>3 of clubs<br>...<br>3 of clubs <br> <br></div><br><br>I've got an idea for a card game that's like Fluxx (if you ever heard of it) and uses a special deck of cards which has no dups.<br><br>thanks for the help <br><br></td></tr></table><br>
<a name="801941"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yahfree</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> yeah, it doesnt count dupes, you'd have to set up a system:<br><br>1) create a card<br>2) check that card agenst all of the cards<br>3) if it finds one card, its fine, because that 1 would be the card just drawn.<br>4) if it find more then 1, delete the card, and redraw it.<br><br>heres an example:<br><br><pre class=code>
Type Card
	Field name_number%
	Field name$
	Field number%
End Type

Global maxnumber

Graphics 500,300
SeedRnd MilliSecs()

Print "Press R to redraw!"
maxnumber%=Input("Whats the max amount of cards to be drawn?")
Print ""

ReDraw()

i=0

While Not KeyHit(1)

If KeyHit(19)
	ReDraw()
End If

Wend
End
	
	
	
Function ReDraw()

Delete Each Card


For i=0 To maxnumber-1
.redo

;sets matchs up to count matchs
matchs=0

    
	DrawnCard.Card = New Card
	DrawnCard\name_number = Rand(1,4)
	
	Select DrawnCard\name_number
	
		Case 1: DrawnCard\name = "Spades"
		Case 2: DrawnCard\name = "Hearts"
		Case 3: DrawnCard\name = "Dimonds"
		Case 4: DrawnCard\name = "Clubs"
	
	End Select
	DrawnCard\number = Rand(1,10)
	
	
	For check.Card = Each Card
	
		If DrawnCard\name$ = check\name$ And DrawnCard\number = check\number matchs=matchs+1
			
	Next
	
	If matchs &gt; 1 
		Delete DrawnCard
		Goto redo
	End If
Next

Print "" 
	i=1
	For Hand.Card = Each Card
		Print "Card "+i+": " + Hand\number + " Of " + Hand\name
		i=i+1
	Next

End Function
</pre> <br><br></td></tr></table><br>
<a name="802066"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Fuller</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> oh, yeah! I forgot you could locally put a goto in a function! That was what i was trying to work around! Thanks! <br><br></td></tr></table><br>
<a name="802080"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> I actually prefer to make an array of cards and scramble it.  Then just draw off the top like you would any deck.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics 800,600,32,2

Type TCard
	Field Value
	Field Suit$
End Type

Dim Deck.TCard(52) ;The deck
Global TopCard ;the top card (next to be drawn) of the deck
Dim Player.TCard(4,5) ;4 players recieve 5 cards each

LoadDeck() ;fills the deck with the cards
ShuffleDeck() ;Now we shuffle the deck

For c = 1 To 5
	For p = 1 To 4
		Player(p,c) = DrawCard() ;deal the cards to each player
	Next
Next

ClsColor 0,198,0
While Not KeyHit(1)
	Cls
	For p = 1 To 4
		Select p
			Case 1: x = 200: y = 10
			Case 2: x = 10: y = 250
			Case 3: x = 500:y = 250
			Case 4:x = 200: y = 400
		End Select
		
		For c = 1 To 5
			If Player(p,c)\Suit = "Hearts" Or Player(p,c)\Suit = "Diamonds"
				Color 255,0,0
			Else
				Color 0,0,0
			End If
			Select Player(p,c)\Value
				Case 1: Text x,y,"Ace of "+Player(p,c)\suit
				Case 11: Text x,y,"Jack of "+Player(p,c)\suit
				Case 12: Text x,y,"Queen of "+Player(p,c)\Suit
				Case 13: Text x,y,"King of "+Player(p,c)\suit
				Default: Text x,y,Player(p,c)\Value+" of "+Player(p,c)\Suit
			End Select
			y = y + 20
		Next
	Next
	Flip
Wend

End

Function LoadDeck()
	TopCard = 0
	For t = 0 To 51
		Deck(t) = New TCard
		SuitNum = Floor(t/13.0)
		Deck(t)\Value = (t - SuitNum*13)+1
		Select SuitNum
			Case 0: Deck(t)\Suit = "Hearts"
			Case 1: Deck(t)\Suit = "Spades"
			Case 2: Deck(t)\Suit = "Diamonds"
			Case 3: Deck(t)\Suit = "Clubs"
		End Select
	Next
End Function

Function ShuffleDeck()
	For t = 0 To 51
		Card = Rand(0,51)
		temp.TCard = Deck(t)
		Deck(t) = Deck(Card)
		Deck(Card) = temp
	Next
End Function

Function DrawCard.TCard()
	If TopCard &gt; 51 Then Return Null ;no cards left to draw
	TopCard = TopCard + 1
	Return Deck(TopCard - 1)
End Function</textarea><br><br>Edit: And no Goto in sight :) <br><br></td></tr></table><br>
<a name="802082"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rob Farley</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Why not just create your pack of cards and have a flag if it's been drawn or not...<br><br>So<br><br>Type TCard<br>Field Value<br>Field Suit$<br>Field HeldBy$<br>End Type<br><br>Then when picking a new card just check if it's been drawn already, if it has just move onto the next one until you get a card that isn't drawn. This way you have your deck of cards and you know which player is holding it, or if it's on the table, in the dealers hands, up someone's sleeve etc.<br><br>Or go down the array route, I'm pretty sure I wrote a card game a while back and used arrays. <br><br></td></tr></table><br>
<a name="802102"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Fuller</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> thats kinda what i've been doing but i need to draw new cards every turn out of the deck and be able to draw cards out of the discard pile randomly for this card game.<br><br>anyways this is kinda working:<br><pre class=code>
Function DrawHand(drawnum)

Local x=1

.Draw
drawrand=Rand(1,total)
Print drawrand
For c.card=Each card
If c\num=drawrand And c\where$="" Then 
	c\where$="hand"
	c\x=x*100
	c\y=GraphicsHeight()-100
	DebugLog  "Card "+x+" Found"
	If x&lt;drawnum Then x=x+1:Else If x=drawnum Then Return  
Else 
Goto Draw
EndIf
Next 


End Function 
</pre><br><br>...but it results in finding one card and then gets stuck in an endless loop <br><br></td></tr></table><br>
<a name="802114"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yahfree</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Would it be ok if you posted all of your code? or at least more, its hard for me to make out the problem with the code listed in your latest post. <br><br></td></tr></table><br>
<a name="802132"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rob Farley</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> OK... A bit of work and I get something like this:<br><br>This creates a deck of cards, each card can be given to a 'pile'. A pile could be the main deck, a player, the table, whatever you want, you can have any number of piles, it doesn't matter.<br><br>You can actually create any number of decks of cards, however, there's no id for which deck you're working with so that might be something worth adding if you need to distinguish between multiple decks.<br><br>Each pile can be drawn from, by default it draws from pile 0, simply pass which pile you want to draw from. ie DrawNextCard(5) will take the next card off 5.<br><br>You can pass a card to any pile by using the function GiveCardTo(card, pile)<br><br>Example code of how to use it after all the functions.<br><br>Not sure if this is 100% as not really tested it. So it probably needs tweaking.<br><br>It needs a couple more functions<br>MovePileTo(PileID) - Moves all cards from one pile to another<br>ResetPile(PileID) - Resets the next card (used after a shuffle etc)<br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics 640,480,32,2


Dim Suit$(3)
Suit(0) = "Hearts"
Suit(1) = "Diamonds"
Suit(2) = "Clubs"
Suit(3) = "Spades"

Type Card
	Field Value
	Field Suit
	Field Pile
	Field Order
End Type

Type Piles
	Field ID
	Field NextCard
End Type

Function CreatePile(ID)
	p.piles = New piles
	p\id = id
	p\nextcard = 1
End Function

Function CreateDeck(PileID=0)
	If Not PileExist(PileID) Then CreatePile(PileID)
	Order = 0
	For s = 0 To 3
	For v = 1 To 13
		c.Card = New Card
		c\value = v
		c\suit = s
		c\Pile = PileID
		c\Order = Order
		Order = Order + 1
	Next
	Next
End Function

Function PileCount(Pile=0)
	; Checks if a pile is empty
	count = 0
	For c.card = Each card
		If c\Pile = Pile Then Count = Count + 1
	Next
	Return Count
End Function

Function PileExist(PileID)
	For p.piles = Each piles
		If p\ID = PileID Then Return 1
	Next
	Return 0
End Function

Function DrawRandomCard.Card(Pile=0)
	Print "Get Random Card from " + Pile

	If PileCount(Pile) = 0 Then Return Null
	
	SeedRnd MilliSecs()
	i = Rand(1,pilecount(pile))
	
	For c.card = Each card
		If c\Pile = Pile And c\order = i Return c.card
	Next	
		
End Function

Function DrawNextCard.Card(cc.card,PileID=0)
	Print "Get Next Card from " + pileid

	pc = PileCount(PileID) 
	If pc = 0 Then Return Null
	

	NextCard = 0
	For p.piles = Each piles
		If p\ID = PileID Then
			NextCard = p\nextcard
			p\nextcard = p\nextcard + 1
			If p\nextcard &gt; pc Then p\nextcard = 1
		EndIf
	Next
	
	For c.card = Each card
		If c\pile = pileid And c\order = nextcard Then Return c.card
	Next
	
	Return Null
	
End Function

Function ConvertValue$(Value)
	If Value = 1 Then Return "Ace"
	If Value &gt; 1 And Value &lt; 11 Then Return Value
	If Value = 11 Then Return "Jack"
	If Value = 12 Then Return "Queen"
	If value = 13 Then Return "King"
End Function

Function DisplayCard(c.card)
	Print ConvertValue(c\value) + " " + Suit(c\suit) + ", In pile " + c\Pile + " order " + c\order
	Print	
End Function

Function GiveCardTo(c.card,PileID)
	Print "Give card to " + pileid
	If Not PileExist(PileID) Then CreatePile(PileID)

	c\Pile = PileID
	c\Order = PileCount(PileID)
End Function

Function ShufflePile(PileID)
	pc = PileCount(PileID)
	For c.card = Each card
		If c\pile = pileid Then
			r = Rand(1,pc)
			For cc.card = Each card
				If cc\pile = pileid And cc\order = r Then
					cc\order = c\order
					c\order = r
					Exit
				EndIf
			Next
		EndIf
	Next
End Function



; The actual stuff
createdeck(0)
shufflePile(0)

current.card = New card
current = drawrandomcard.card()
DisplayCard(current)

GiveCardTo(current,1)
DisplayCard(current)

current = drawrandomcard.card()
DisplayCard(current)

current = drawrandomcard.card()
DisplayCard(current)

GiveCardTo(Current,2)
DisplayCard(current)

current = DrawNextcard(Current.card)
DisplayCard(current)

GiveCardTo(Current,2)
DisplayCard(current)

current =DrawNextcard(Current)
DisplayCard(current)

GiveCardTo(Current,2)
DisplayCard(current)

WaitKey</textarea> <br><br></td></tr></table><br>
<a name="802191"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rob Farley</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Don't you just hate demonstrating you have too much free time on your hands?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics 1024,768,32,2

Global fntSymbol = LoadFont ("symbol.ttf",20)
Global fntSymbolBig = LoadFont ("symbol.ttf",40)
Global fntArialBig = LoadFont ("Arial.ttf",70)
Global fntArial = LoadFont ("Arial.ttf",15)

Function DrawCard(x,y,Value,Suit)

SuitSymbol$ = Mid("зийк",Suit+1,1)

Color 255,255,255
Rect x+1,y+1,98,148,True
Color 0,0,0
Rect x+2,y,96,1,False
Rect x,y+2,1,146,False
Rect x+99,y+2,1,146,False
Rect x+2,y+149,96,1,False
Plot x+1,y+1
Plot x+98,y+1
Plot x+1,y+148
Plot x+98,y+148

If suit=1 Or suit = 2 Then Color 200,0,0

SetFont fntArial

Text x+8,y+10,Convert(Value),True,True
Text x+92,y+140,Convert(Value),True,True

SetFont fntSymbol
Text x+8,y+25,SuitSymbol,True,True
Text x+92,y+125,SuitSymbol,True,True

If Value &gt; 10 Then
	SetFont fntArialBig
	Text x+50,y+75,Convert(Value),True,True
Else
	SetFont fntSymbolBig
	If Value = 1 Or Value = 3 Or Value = 5 Or Value = 9 Then
		Text x+50,y+75,SuitSymbol,1,1
	EndIf
	
	If Value = 2 Or Value = 3 Then
		Text x+50,y+25,SuitSymbol,1,1
		Text x+50,y+125,SuitSymbol,1,1
	EndIf
		
	If Value &gt; 3 Then
		Text x+30,y+25,SuitSymbol,1,1
		Text x+70,y+25,SuitSymbol,1,1
		Text x+30,y+125,SuitSymbol,1,1
		Text x+70,y+125,SuitSymbol,1,1
	EndIf
	
	If Value &gt; 5 And Value &lt; 9 Then
		Text x+30,y+75,SuitSymbol,1,1
		Text x+70,y+75,SuitSymbol,1,1
	EndIf
	
	If Value = 7 Or Value = 8 Then
		Text x+50,y+50,SuitSymbol,1,1
	EndIf

	If Value = 8 Then
		Text x+50,y+100,SuitSymbol,1,1
	EndIf
	
	If Value = 9 Or Value = 10 Then
		Text x+30,y+57,SuitSymbol,1,1
		Text x+70,y+57,SuitSymbol,1,1
		Text x+30,y+93,SuitSymbol,1,1
		Text x+70,y+93,SuitSymbol,1,1
	EndIf
	
	If Value = 10 Then
		Text x+50,y+40,SuitSymbol,1,1
		Text x+50,y+110,SuitSymbol,1,1
	EndIf
EndIf


End Function

Function Convert$(Value)
	If Value = 1 Then Return "A"
	If Value = 11 Then Return "J"
	If Value = 12 Then Return "Q"
	If Value = 13 Then Return "K"
	Return Value
End Function

; demo!


ClsColor 100,150,100
Cls

For n=1 To 100
	drawcard(Rand(0,924),Rand(0,618),Rand(1,13),Rand(0,3))
Next

WaitKey</textarea> <br><br></td></tr></table><br>
<a name="802200"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yahfree</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> thats neat, i'm a bit on the bored side aswell, i'll try to make some nifty images in photoshop :D <br><br></td></tr></table><br>
<a name="802202"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Fuller</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> here's my code:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;fluxx card game framework
Graphics 800,600,32,2
SetBuffer BackBuffer()
SeedRnd MilliSecs()

Type card
Field num
Field x,y
Field name$,info$,kind$
Field playfunction$
Field where$
End Type 

Global hand$=""
Global keepers$=""
Global rules$=""
Global notinplay$=""
Global total

Dim temp(100)

MakeCards()
DrawHand(3)

While Not KeyDown(1)


Draw()

Wend 
End 

Function MakeCards()

Restore cards

Read total 

For num=1 To total 

Read name$,kind$

c.card=New card
c\num=num
c\name$=name$
c\kind$=kind$
c\info$=CardInfo(c\kind$)
c\playfunction$="keeper"
c\where$=""

Next 

End Function

Function DrawHand(drawnum)

Local x=1

.Draw
drawrand=Rand(1,total)
Print drawrand
For c.card=Each card
If c\num=drawrand And c\where$="" Then 
	c\where$="hand"
	c\x=x*100
	c\y=GraphicsHeight()-100
	DebugLog  "Card "+x+" Found"
	If x&lt;drawnum Then x=x+1:Else If x=drawnum Then Return  
Else 
Goto Draw
EndIf
Next 


End Function 

Function CardInfo(typeofcard$)

Select typeofcard$

Case "Keeper"

Return "To Play Place Face Up in Front of You"

End Select 


End Function 

Function Draw()

Cls 

For c.card=Each card 
Rect c\x,c\y,50,70,1
Color 255,0,0
Text c\x+1,c\y+10,c\name$
Text c\x+1,c\y+1,c\kind$
Color 255,255,255
Next 

Flip 

End Function 

.cards
Data 4   ;total cards
Data "chocolate","Keeper"
Data "sun","Keeper"
Data "rocket","Keeper" 
Data "war","Keeper"
</textarea><br><br>those examples look useful, i'll go and see if i can get this baby running. <br><br></td></tr></table><br>
<a name="802210"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rob Farley</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> With my code if you wanted to deal 5 cards to 3 players for example:<br><br><pre class=code>Current.Card = New Card
CreateDeck(0)
shufflePile(0)

For Deal = 1 To 5
For Player = 1 To 3
	current = DrawNextcard(Current.Card,0)
	GiveCardTo(Current,Player)
Next
Next</pre> <br><br></td></tr></table><br>
<a name="802215"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yahfree</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> <img src="http://img164.imageshack.us/img164/5687/untitled3hl1.png"><br><br>heres a 1 of hearts i made in photoshop, took 10 minutes :D<br><br>Edit, i think the texture is a bit to visible.. :\ <br><br></td></tr></table><br>
<a name="802220"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LineOf7s</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice card...<br><br>...but when was the last time you saw a standard deck of cards with a "1" in it? <br><br></td></tr></table><br>
<a name="802222"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yahfree</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> As far as card images go i think this is a good find<br>(5 minutes of google/photoshop):<br><img src="http://img511.imageshack.us/img511/2434/classicplayingcardsub8.png"><br><br>load as an animated image strip and use like so:<br><br><pre class=code>
AutoMidHandle True

image = LoadAnimImage("Media/Cards/classic-playing-cards.png",73,98,0,52)
MaskImage image,255,0,255
cur_image=0
ClsColor 0,100,0
Repeat

Cls

DrawImage image,GraphicsHeight()/2,GraphicsWidth()/2,cur_image
cur_image=cur_image+1

Delay 1000
Until cur_image &gt;51


WaitKey
</pre> <br><br></td></tr></table><br>
<a name="802229"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rob Farley</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> I thought I'd have a go at making them look a little better...<br><br><img src="http://homepage.ntlworld.com/rob.farley/screens/cards.png"> <br><br></td></tr></table><br>
<a name="802234"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yahfree</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> holy crap, that looks nice.<br><br>Edit:<br><br>:D<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics 800,600,16,2
ClsColor 0,100,0
Cls

Type Card
	Field position$
	Field Card_number
End Type

SeedRnd MilliSecs()


	Global image = LoadAnimImage("Media/Cards/classic-playing-cards.png",73,98,0,52)
		MaskImage image,255,0,255

	


While Not KeyHit(1)
Cls

Text 0,0,"R to redraw"

If KeyHit(19) 
Redeal()
Hand=True
End If

If Hand=True
	i=0
	For all.Card = Each Card
		DrawImage image,150+i*80,500,all\Card_number
		i=i+1	
	Next
End If

Delay 5
Flip
Wend
End


;-------------------------------
;       Redeal()
;-------------------------------

Function Redeal()

cur_image=0
Repeat
	DrawImage image,350,250,cur_image : cur_image=cur_image+1
	Delay 50
Until cur_image&gt;51

Delete Each Card
For i=1 To 5
	.ReDraw
	Draw.Card = New Card
		Draw\position$ = "Hand"
		Draw\Card_number = Rand(0,52)
	
	Matchs = 0
	For Check.Card = Each Card
		If Draw\Card_number = Check\Card_number
			Matchs=Matchs+1
		End If
	Next

	If Matchs &gt; 1
		Delete Draw
		Goto ReDraw
	End If
	

Next




End Function
</textarea> <br><br></td></tr></table><br>
<a name="802342"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Fuller</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#23">[#23]</a></td></tr></table></td></tr><tr ><td class="posttext"> actually yahfree, i like the first one with the texture that shows. It reminds me of the plastic-coated playing card decks. <br><br></td></tr></table><br>
<a name="802395"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yahfree</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#24">[#24]</a></td></tr></table></td></tr><tr ><td class="posttext"> if you like the texture fill i used maybe you'll like this:<br><br><img src="http://img119.imageshack.us/img119/8097/classicplayingcardscopyyd6.png"> <br><br></td></tr></table><br>
<a name="802462"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Fuller</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#25">[#25]</a></td></tr></table></td></tr><tr ><td class="posttext"> hmm... the texture actually is a little too much isn't it?<br><br>Doesn't really matter to me as i can't use them :) <br><br></td></tr></table><br>
<a name="805500"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Blitzplotter</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#26">[#26]</a></td></tr></table></td></tr><tr ><td class="posttext"> not so sure fuller, I quite like Yahfree's textures.  Go s back to the old addage 'beauty is in the eye of the beholder chap'.  I don't have B3d on this computer, but I'm firing up my main beast (B3D capable) to check out these posts.  It's great to have a reason to play with protean again.....<br><br>regards,  BP. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
