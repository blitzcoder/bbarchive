<!DOCTYPE html><html lang="en" ><head ><title >Ghosts after hide entity?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Ghosts after hide entity?</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Ghosts after hide entity?</a><br><br>
<a name="1178498"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >gerald</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> When using a proximity code to have the player take damage I find that even after the entity is hit and hidden its ghost can trigger more damage.<br><br>Missles destroyed at 300 elevation will still set off a positiony#&lt; 25 loop at the end of the sequence also after they are entity hidden.<br><br>This is on one pass through a big loop.  I have to translateentity,0,5000,0 to get the destroyed entity to stop setting off the damage  at positiony#&lt;25.  <br><br>The command reference implies that an "hideentity" command means an eneity cannot be involved in collisions.  <br><br>What is the deal with these ghosts setting off collisions?<br>Gerald <br><br></td></tr></table><br>
<a name="1178509"></a>

<a name="1178511"></a>

<a name="1178512"></a>

<a name="1178513"></a>

<a name="1178514"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> A hidden entity is not renderable, nor will it be processed with blitz collisions.<br><br>It can still be moved, rotated, scaled among being subject to several other changes.<br>If you're using the position of an entity as the condition for something to happen ("proximity code"), hiding it will not invalidate that condition.<br><br>You need something like a flag variable: a variable that stores either True or False. Once the entity triggers the condition, change the flag variable so the entity gets discarded from further tests.<br>Each missile entity should have its own flag variable, so you need to use Types for this. <br><br></td></tr></table><br>
<a name="1178565"></a>

<a name="1178566"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>A hidden entity is not renderable, nor will it be processed with blitz collisions.<br> <br></div><br>That's what the doc explains, but see this example (i have already posted it before but without any answers)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D(800,600,32,2) 
SetBuffer(BackBuffer()) 
HidePointer()  
SeedRnd(MilliSecs())

Camera = CreateCamera()
CameraRange(Camera,0.01,1000)

WallCollider = CreateCube()
ScaleMesh(WallCollider,0.1/2,3.0/2,10.0/2)
PositionMesh(WallCollider,-0.1/2,3.0/2,10.0/2)
EntityColor(WallCollider,255,255,255)

Const IsStatic% = 1
Const IsMoving% = 2
Const IsCollided% = 3
Global BulletState% = 0

BulletOrigine = CreatePivot()
PositionEntity(BulletOrigine,0,0,0)
BulletMesh = CreateCube()
ScaleMesh(BulletMesh,0.01/2,0.001/2,0.01/2)
PositionEntity(BulletMesh,EntityX(BulletOrigine,True),EntityY(BulletOrigine,True),EntityZ(BulletOrigine,True))
RotateEntity(BulletMesh,EntityPitch(BulletOrigine,True),EntityYaw(BulletOrigine,True),EntityRoll(BulletOrigine,True))
EntityParent(BulletMesh,BulletOrigine,True)
EntityColor(BulletMesh,100,100,100)
BulletCollider = CreatePivot()
PositionEntity(BulletCollider,EntityX(BulletOrigine,True),EntityY(BulletOrigine,True),EntityZ(BulletOrigine,True))
RotateEntity(BulletCollider,EntityPitch(BulletOrigine,True),EntityYaw(BulletOrigine,True),EntityRoll(BulletOrigine,True))
EntityParent(BulletCollider,BulletOrigine,True)
Debug_BulletCollider = CreateSphere(16)
ScaleMesh(Debug_BulletCollider,0.01/2,0.01/2,0.01/2)
PositionEntity(Debug_BulletCollider,EntityX(BulletCollider,True),EntityY(BulletCollider,True),EntityZ(BulletCollider,True))
RotateEntity(Debug_BulletCollider,EntityPitch(BulletCollider,True),EntityYaw(BulletCollider,True),EntityRoll(BulletCollider,True))
EntityParent(Debug_BulletCollider,BulletCollider,True)
EntityColor(Debug_BulletCollider,000,000,255)

BarrelMesh = CreateCube()
ScaleMesh(BarrelMesh,0.011/2,0.011/2,0.5/2)
PositionMesh(BarrelMesh,0,0,0.5/2)
PositionEntity(BarrelMesh,1,1.5,5)
;PositionEntity(BarrelMesh,Rnd(1,20),1.5,5)
TurnEntity(BarrelMesh,0,90,0)
EntityColor(BarrelMesh,100,100,100)

OLight = CreateLight(2)
LightRange(OLight,3.0)
LightColor(OLight,125,125,125)
PositionEntity(OLight,5,1.5,5)
AmbientLight(063,063,063)

PositionEntity(Camera,0.001,2.5,5)
TurnEntity(Camera,90,0,0)

Const CollWalls% = 1
Const CollBullets% = 100

Repeat

 Cls()

 ;Colliders and Collisions groups update (useful in order to desactivate/activate only some colliders and some collisions groups to avoid useless calculations)
 ClearCollisions()
 EntityType(WallCollider,0)
 EntityType(BulletCollider,0)
 EntityType(WallCollider,CollWalls%)
 If(BulletState% = IsMoving%)
  EntityRadius(BulletCollider,0.01/2)
  EntityType(BulletCollider,CollBullets%)
 EndIf
 If(BulletState% = IsMoving%)
  Collisions(CollBullets%,CollWalls%,2,1)
 EndIf

 If(KeyHit(57)&gt;0)
  PositionEntity(BulletOrigine,EntityX(BarrelMesh,True),EntityY(BarrelMesh,True),EntityZ(BarrelMesh,True))
  RotateEntity(BulletOrigine,EntityPitch(BarrelMesh,True),EntityYaw(BarrelMesh,True),EntityRoll(BarrelMesh,True))
  BulletState% = IsMoving%
  EntityRadius(BulletCollider,0.01/2)
  EntityType(BulletCollider,CollBullets%)
 EndIf
 
 ;Bullets update1
 If(BulletState% = IsStatic%)
  ;
 ElseIf(BulletState% = IsMoving%)
  MoveEntity(BulletOrigine,0,0,0.01)
 ElseIf(BulletState% = IsCollided%)
  ResetMode% = 3 ;Set the ResetMode here
  ;Resets the collider by using EntityType() + PositionEntity() + RotateEntity()
  If(ResetMode% = 1)
   EntityType(BulletCollider,0)
   PositionEntity(BulletCollider,EntityX(BulletOrigine,True),EntityY(BulletOrigine,True),EntityZ(BulletOrigine,True))
   RotateEntity(BulletCollider,EntityPitch(BulletOrigine,True),EntityYaw(BulletOrigine,True),EntityRoll(BulletOrigine,True))
   EntityColor(Debug_BulletCollider,000,000,255)
  ;Resets the collider by using PositionEntity() + RotateEntity() + ResetEntity()
  ElseIf(ResetMode% = 2)
   PositionEntity(BulletCollider,EntityX(BulletOrigine,True),EntityY(BulletOrigine,True),EntityZ(BulletOrigine,True))
   RotateEntity(BulletCollider,EntityPitch(BulletOrigine,True),EntityYaw(BulletOrigine,True),EntityRoll(BulletOrigine,True))
   ResetEntity(BulletCollider)
   EntityColor(Debug_BulletCollider,000,000,255)
  ;Resets the collider by using HideEntity() + PositionEntity() + RotateEntity() + ShowEntity()
  ElseIf(ResetMode% = 3)
   HideEntity(BulletCollider)
   PositionEntity(BulletCollider,EntityX(BulletOrigine,True),EntityY(BulletOrigine,True),EntityZ(BulletOrigine,True))
   RotateEntity(BulletCollider,EntityPitch(BulletOrigine,True),EntityYaw(BulletOrigine,True),EntityRoll(BulletOrigine,True))
   ShowEntity(BulletCollider)
   EntityColor(Debug_BulletCollider,000,000,255)
  ;Resets the collider by deleting the old collider and creating a new collider at the correct position, orientation
  ElseIf(ResetMode% = 4)
   FreeEntity(Debug_BulletCollider)
   FreeEntity(BulletCollider)
   BulletCollider = CreatePivot()
   PositionEntity(BulletCollider,EntityX(BulletOrigine,True),EntityY(BulletOrigine,True),EntityZ(BulletOrigine,True))
   RotateEntity(BulletCollider,EntityPitch(BulletOrigine,True),EntityYaw(BulletOrigine,True),EntityRoll(BulletOrigine,True))
   EntityParent(BulletCollider,BulletOrigine,True)
   Debug_BulletCollider = CreateSphere(16)
   ScaleMesh(Debug_BulletCollider,0.01/2,0.01/2,0.01/2)
   PositionEntity(Debug_BulletCollider,EntityX(BulletCollider,True),EntityY(BulletCollider,True),EntityZ(BulletCollider,True))
   RotateEntity(Debug_BulletCollider,EntityPitch(BulletCollider,True),EntityYaw(BulletCollider,True),EntityRoll(BulletCollider,True))
   EntityParent(Debug_BulletCollider,BulletCollider,True)
   EntityColor(Debug_BulletCollider,000,000,255)
  EndIf
  BulletState% = IsMoving%
 EndIf

 ;Collisions detection and response update
 UpdateWorld(0)

 ;Bullets update2
 If(BulletState% = IsStatic%)
  ;
 ElseIf(BulletState% = IsMoving%)
  CCount% = CountCollisions(BulletCollider)
  If(CCount% &gt; 0)
   BulletState% = IsCollided%
   OCollider = CollisionEntity(BulletCollider,1)
   CPX# = CollisionX(BulletCollider,1)
   CPY# = CollisionY(BulletCollider,1)
   CPZ# = CollisionZ(BulletCollider,1)
   EntityColor(Debug_BulletCollider,255,000,000)
   EntityColor(OCollider,125,000,000)
   Debug_CollisionPoint = CreateCube()
   ScaleMesh(Debug_CollisionPoint,0.01/2,0.01/2,0.01/2)
   PositionEntity(Debug_CollisionPoint,CPX#,CPY#,CPZ#)
   EntityColor(Debug_CollisionPoint,255,125,000)
   BulletColliderOldX# = EntityX(BulletCollider,True)
   BulletColliderOldY# = EntityY(BulletCollider,True)
   BulletColliderOldZ# = EntityZ(BulletCollider,True)
  EndIf
 ElseIf(BulletState% = IsCollided%)
  ;
 EndIf

 If(KeyDown(2)&gt;0)
  Wireframe(True)
 Else
  Wireframe(False)
 EndIf
 
 ;Render3D update
 RenderWorld()

 ;Render2D update
 Text(0,0,"Hit the space key to shoot the bullet.")
 Text(0,20,"BulletOrigineX# = "+EntityX(BulletOrigine,True))
 Text(0,40,"BulletOrigineY# = "+EntityY(BulletOrigine,True))
 Text(0,60,"BulletOrigineZ# = "+EntityZ(BulletOrigine,True))
 Text(0,80,"BulletColliderOldX# = "+BulletColliderOldX#)
 Text(0,100,"BulletColliderOldY# = "+BulletColliderOldY#)
 Text(0,120,"BulletColliderOldZ# = "+BulletColliderOldZ#)
 Text(0,140,"BulletColliderX# = "+EntityX(BulletCollider,True))
 Text(0,160,"BulletColliderY# = "+EntityY(BulletCollider,True))
 Text(0,180,"BulletColliderZ# = "+EntityZ(BulletCollider,True))

 Flip(True)

Until(KeyDown(1)&gt;0)

End
</textarea><br><br>This example shows that to desactivate a collider in order to be able to reposition it, only one method works reliably, it is to delete the collider which has been collided and to create another collider. <br>With the others ways (EntityType 0, or ResetEntity, or HideEntity then ShowEntity) the collider is still active, you can see the differences between the X,Y coordinates of the BulletOrigine and  the X,Y coordinates of the BulletCollider.<br>Or maybe i am not using the commands in the right way. If so please explain.<br> <br>Thanks, <br><br></td></tr></table><br>
<a name="1178593"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> The 'collisions' command should be used outside your main loop and you should not be using clearcollisions each loop.<br><br>If you hide and reset the entity after it has collided with something then no further collisions will be recorded.<br><br>Stevie <br><br></td></tr></table><br>
<a name="1178598"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> There's some undocumented behavior with Hide\ShowEntity and collisions.<br><br>I gave the low-down here:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">;Disabling collisions with HideEntity().

;Move with KEYS. 
;Hide with SPACE.

Graphics3D 800,600,0,2
SetBuffer BackBuffer()

camera=CreateCamera()
light=CreateLight()

sphere=CreateSphere( 32 )
PositionEntity sphere,-2,0,5

cone=CreateCone( 32 )
EntityType cone,type_cone
PositionEntity cone,2,0,5

; Set collision type values
type_sphere=1
type_cone=2

; Set sphere and cone entity types
EntityType sphere,type_sphere
EntityType cone,type_cone

; Enable collisions between type_sphere and type_cone, with ellipsoid-&gt;polygon method and slide response
Collisions type_sphere,type_cone,2,2

sphereIsHidden = 0

While Not KeyDown( 1 )

x#=0
y#=0
z#=0

If KeyDown( 203 )=True Then x#=-0.1
If KeyDown( 205 )=True Then x#=0.1
If KeyDown( 208 )=True Then y#=-0.1
If KeyDown( 200 )=True Then y#=0.1

MoveEntity sphere,x#,y#,z#

;Make the cone red when being collided with the sphere.
If EntityCollided(sphere,type_cone) Then
	EntityColor cone,255,0,0
Else
	EntityColor cone,255,255,255
EndIf

If KeyDown(57) Then 
	;Freaky behavior: if you hide the sphere while it's colliding with the cone, the collision state remains active even if 
	;you move the sphere away from the cone.
	HideEntity sphere
	sphereIsHidden = 1
	
	;To fix this, everytime you hide a collideable entity you must also call...
	ResetEntity sphere ;(Try commenting this line and hiding the sphere while colliding it against the cone.)
Else
	
	;Important: everytime you call ShowEntity, the entity is RESET (meaning, it will not react with any other collision type)
	;for that frame. If you want to see this in action, leave a "ShowEntity sphere" open in this loop for execution
	;in every frame; the sphere will pass through the cone.
	
	;This is why I use a state variable, so we don't keep calling ShowEntity every frame and resetting the sphere's collisions.
	If sphereIsHidden Then
		sphereIsHidden = 0
		ShowEntity sphere
	EndIf
EndIf 

; Perform collision checking
UpdateWorld

RenderWorld

Text 0,0,"Use the KEYBOARD KEYS to move the sphere."
Text 0,20,"Hold SPACE to hide the sphere."
Text 0,60,"The cone turns red when colliding with the sphere."

Flip

Wend

End</textarea> <br><br></td></tr></table><br>
<a name="1178609"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >gerald</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks,<br><br>I never thought of a locking variable true/false.  I spent 4 hours trying stuff like entityviewable and entity in view to find out how to get rid of the ghosts.  I will try switching from translate entity crash 0,5000,0 to a variable that changes after the entity is hit/hidden.<br><br>Gerald <br><br></td></tr></table><br>
<a name="1178621"></a>

<a name="1178622"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>The 'collisions' command should be used outside your main loop and you should not be using clearcollisions each loop.<br> <br></div><br>Stevie G&gt;&gt;I have tried to remove them from the loop and it does not change anything. But thanks anyway.<br><br>Kryzon&gt;&gt;Thanks for the example. I see what you are doing, the collider must be either shown or hidden before updateworld.<br><br>My example was more about how to reposition a collider after a collision has occured (collisions without response) <br><br></td></tr></table><br>
<a name="1178787"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >gerald</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> hide entity = true<br>view entity = true<br><br>OMG, I see dead entities!<br><br>Gerald <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
