<!DOCTYPE html><html lang="en" ><head ><title >click on object</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >click on object</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >click on object</a><br><br>
<a name="1017192"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dicon</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am sure there is a way to click on a mesh, but even looking through the forums, I havn't spotted one. <br><br>Thanks<br>Dicon <br><br></td></tr></table><br>
<a name="1017196"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Try CameraPick(Camera,MouseX(),MouseY()) <br><br></td></tr></table><br>
<a name="1017232"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> The entity must be pickable as well.  Look into EntityPickMode() as well as CameraPick(). <br><br></td></tr></table><br>
<a name="1017255"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dicon</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks all, will try that tomorrow.<br> Dicon <br><br></td></tr></table><br>
<a name="1017287"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dicon</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well, it does work and the result is ( for the mesh) the number 25707784 which I cannot see how it can identify the mesh itself.<br> I could set up an array to read it out, which would be clumbsy but the name of the mesh would be better.<br><br>Dicon<br><br>Making my own world game creator <br><br></td></tr></table><br>
<a name="1017295"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Do you know of the handle and object commands?<br><br>Well, these commands allow you to jump straight to a type object, if you have the handle number. What you basically do is place this handle in the entityname() of the entity.<br><br>The only other way you can do this, is to store the array number of the entity, in the entityname(). That way, you can get the pickedentity then:<br><br>array(EntityName(PickedEntity()) is the entity your after. I use the type method, as i can store other information in the type fields, but it's really whatever is easiest for you! <br><br></td></tr></table><br>
<a name="1017307"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> That number is the entity. I suppose it represents the memory address where the entity information is stored.<br><br>try:<br>cube = createcube()<br>print cube<br><br>You can use this number for instance like this:<br>HideEntity PickedEntity()<br>(PickedEntity returns which mesh was picked) <br><br></td></tr></table><br>
<a name="1017315"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think he's wanting to jump straight to the entity, and the only real way to do that at speed is to store the information in the entityname(), as it's the only associated data with the entity that will hold this information, with actually affecting the entity in any way. <br><br></td></tr></table><br>
<a name="1017489"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dicon</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is where I am so far ...............................<br>( snipped for brevity )<br><br>item$="med_support_beam.b3d"<br>My_Object=LoadMesh(item$)<br>NameEntity My_Object,"beam"<br>EntityPickMode My_Object,3 <br>PositionEntity My_object, 30,ground_level#+h#,30<br>PointEntity camera,my_object<br>If MouseDown(1) Then EntityName$=CameraPick ( camera,MouseX(),MouseY() )<br>Text 0,80," Object Picked is "+ EntityName$<br><br>which returns 25707784<br>I tried If MouseDown (1)Then Entitynames$=EntityPick ( camera,10 )<br>which doesn't work at all.<br><br>I am making a "Game Maker" that needs for  meshes to be loaded, then selected, then moved or scaled then saved as a "Scene"<br> Any suggestions?<br><br>Dicon <br><br></td></tr></table><br>
<a name="1017493"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> You want to store all your meshes in an array to start (if your using arrays), then, go through each array slot and give each entity an entityname, using it's array slot.<br><br><pre class=code>
for loop = 0 to 20 ; 20 entities in the array
  nameentity entity_array(loop),loop
next

... later on in code, when your doing your pick....

CameraPick camera,mousex(),mousey()
array_slot = entityname(pickedentity())

entitycolor array(array_slot), 0,0,0

</pre><br><br>that will give you the array slot of the entity picked, and color it black. <br><br></td></tr></table><br>
<a name="1017506"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Have a study on this: I wrote this for Rez a few weeks ago.  It does show you how to name an entity, and retrieve and display that name when clicked on.  This is a fully functioning "shape editor" :)  It allows the user to select, move, rotate each shape.  This could easily be modified to use modeled meshes instead of primitive shapes.<br><br>Pay particular attention to where the shapes are created, right before the main loop.  That is where they are named and numbered.  The function get_input() performs the camerapick and stores the name, coordinates, and rotation information for the function hud() to display.  Important global variables are the global "entity" and "name$."  The global "entity" actually holds Blitz's reference to that particular entity, which is that number you've seen before. (478328018) or similar.  When you want to alter or move an entity later in the program, we check that variable "entity" (which is again the stored number) against all entities to find out which one is "selected".<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;THIS program shows how to select an entity
;ALSO unselect it by clicking on the selected entity,
;AND also to unselect the entity if no entites are clicked on
;SELF adjusting and aligning camera to the picked object
;MOVEABLE objects
;TWO types of movement
;OBJECTS will not go below Y value of 1
;RESETS the camera to original position when picking an already picked entity


AppTitle "Item selection DEMO by Drak."
SeedRnd MilliSecs()
Graphics3D 800,600,0,2
SetBuffer BackBuffer()

;

Const type_shape 	= 1
Const type_land		= 2


Collisions type_shape, type_land, 2, 3
Collisions type_shape, type_shape, 1, 3		


Global land=CreatePlane()
EntityColor land, 100,100,100
EntityType land,type_land

EntityColor land,32,32,32

Global camera = CreateCamera()		;create a camera
EntityRadius camera,2
EntityType camera,typecam
PositionEntity camera, 0,25,-75 	;move the camera up and backwards
TurnEntity camera,15,0,0
sun = CreateLight()					;create some light

;----------------------------------------------------------
;GLOBALS
;----------------------------------------------------------
Global entity	;this global will hold the picked entity
Global name$	;this will hold the name of the picked entity
Global pos_x#	;this is used for the HUD function 
Global pos_y#	;as is this
Global pos_z#	;and this
Global rotation	;and also this
Global camera_speed#	;this is used to move the camera
Global move_camera		;this is a toggle switch to move the camera or not
Global movement_type = 1	;this is a switch for movement type, unit, or free, leave at 1 to start
Global gravity = 1

;create a pivot for the camera movement
Global camera_target = CreatePivot()

;create a cursor to identify the picked shape
Global cursor = CreateSphere()
EntityAlpha cursor, .3
ScaleEntity cursor,2,2,2
HideEntity cursor

;This will be a VERY simple demo on how to select an object
;First, we need to create a type to store all our selectable objects in:

Type shape			;This sets up a type referred to as "shape"
	Field mesh		;mesh is the trait that actually stores the visible part of the shape
	Field name$		;we're going to name our shapes with this field
End Type

;Ok, now, we need to populate our area with the 100 shapes you want

For number = 1 To 100			;this will create 100 shapes 
	s.shape = New shape			;create a reference to a new shape type
		this_shape = Rand(1,4)	;pick a random number between 1 and 4 and store it in variable "this_shape"
		
		Select this_shape
			Case 1
				s\mesh = CreateCube()				;create a cube and store it in the mesh field
				s\name$ = "Cube " + number			;this sets the field to cube + the current number of 1-100.  This is not the actual entity name, it is just a field that HOLDS the entity name
				EntityRadius s\mesh,1
				EntityType s\mesh,type_shape
			Case 2
				s\mesh = CreateSphere()				
				s\name$ = "Sphere " + number			
				EntityRadius s\mesh,1
				EntityType s\mesh,type_shape
			Case 3
				s\mesh = CreateCylinder()				
				s\name$ = "Cylinder " + number			
				EntityRadius s\mesh,1
				EntityType s\mesh,type_shape
			Case 4
				s\mesh = CreateCone()				
				s\name$ = "Cone " + number			
				EntityRadius s\mesh,1
				EntityType s\mesh,type_shape
		End Select
		
		NameEntity(s\mesh, "" + s\name$)					;this sets the actual name of the entity to s\name$
		EntityColor s\mesh,Rand(255),Rand(255),Rand(255)	;give it a random color
		PositionEntity s\mesh,Rand(-50,50),3,Rand(-50,50)	;position them randomly
		EntityPickMode s\mesh,2,1							;and then make it pickable of course
Next
		
;Thats it! The shapes are created!  
;Here's our program loop:

While Not KeyHit(1)

	get_input()		;grabs input from input devices
	update_cursor()	;this function updates the cursor
	move_camera()	;and this function moves the camera
	gravity()
	
	RenderWorld
	UpdateWorld

	hud()			;heads up display (displays texts in this program)

	Flip

Wend 

;------------------------------------
;FUNCTION GET_INPUT()   THIS FUNCTION GRABS ALL INPUT AND DECIDES WHAT SHOULD BE DONE WITH THAT INPUT
;------------------------------------
Function get_input()
	
	
	If KeyHit(34)
		Select gravity
			Case 1
				gravity = 0
			Case 0
				gravity = 1
		End Select
	End If
	
	
	;OK THIS IS THE COMMAND THAT ALLOWS THE CAMERA TO FOLLOW THE MOUSE
	
	If MouseDown(2)
	TurnEntity camera, 0, -MouseXSpeed()/5.0, 0					;rotate player Pivot according to mouse X movement
	TurnEntity camera, MouseYSpeed()/5.0, 0, 0					;rotate camera up/down according to mouse Y 
	MoveMouse GraphicsWidth()/2, GraphicsHeight()/2
		If EntityPitch(camera) &lt; -55								;don't allow camera to look above 55 degrees
			 RotateEntity camera, -55, EntityYaw(camera), EntityRoll(camera)
		EndIf 
 	 	
		If EntityPitch(camera) &gt; 30									;don't allow camera to look below 30 degrees
			 RotateEntity camera, 30, EntityYaw(camera), EntityRoll(camera)
		EndIf
	End If
	
	roll# = EntityRoll(camera)
		If roll &lt;&gt; 0
			TurnEntity camera, 0,0, - roll
		End If  

	
	mx = MouseX()
	my = MouseY()
		
	If mx &lt; 10 Then TranslateEntity camera, -.2,0,0
	If mx &gt; 790 Then TranslateEntity camera, .2,0,0
	If my &lt; 10 Then TranslateEntity camera, 0,0,.2
	If my &gt; 590 Then TranslateEntity camera, 0,0,-.2
	
	
	
	;THIS IS THE PICK COMMAND, THIS IS HOW AN ENTITY IS PICKED OR UNPICKED
	If MouseHit(1) = 1 
		Local i_was_picked = entity	;store the old picked entity in variable "i_was_picked"
		entity = CameraPick(camera,MouseX(),MouseY())	;shoot a camera pick out from the camera to the mouse
			
	
			If entity = 0 				;if nothing is picked (clicked on a blank area)				
				HideEntity cursor		;hide the cursor
				name$ = "None"			;and the global name$ will be nothing
				movement_type = 1
					
			ElseIf entity &lt;&gt; 0				;but if an entity IS picked, (not = to 0)
				If entity = i_was_picked 	;if this new entity is the OLD entity...
					PositionEntity camera,0,25,-75	;THIS SETS THE CAMERA TO ITS ORIGINAL POSITION
					RotateEntity camera,15,0,0		;AND THIS SETS IT TO ITS ORIGINAL ORIENTATION
					HideEntity cursor		;do the same.. unselect it
					name$ = "None"			;and set name$ to none again
					entity = 0
					move_camera = 0	
				
				Else If entity &lt;&gt; i_was_picked
					ShowEntity cursor				;show the cursor
						Local px# = EntityX(entity)	;grab the entities x coordinate
						Local py# = EntityY(entity)	;and also it's y coordinate
						Local pz# = EntityZ(entity)	;and also it's z coordinate
						PositionEntity cursor, px,py,pz,1	;and finally place it at those coordinates
						name$ = EntityName(entity)	;store the entities given name in the global name$ variable
						pos_x = EntityX(entity)		;store values for the HUD diplay
						pos_y = EntityY(entity)		;here too
						pos_z = EntityZ(entity)		;and here
						rotation = EntityYaw(entity);and finally the rotation as well
				
						PositionEntity camera_target,px,py+10,pz-10	;position the camera's target up and in front of the entity
						move_camera = 1								;flip the switch to true
				
				
				
				End If 
			End If 
	End If
	
	;--------------------------------------------------------------------------
	;THIS PART WILL TAKE INPUT FROM THE KEYBOARD AND EDIT THE APPROPRIATE SHAPE
	;--------------------------------------------------------------------------
	If movement_type = 1
	If KeyHit(17) Or KeyHit(200)	;W KEY
		For s.shape = Each shape		;go through all the shapes
			If entity = s\mesh			;if this is the selected entity
			TranslateEntity s\mesh, 0,1,0	;move it up 1, we use translateentity instead of moveentity because we always want it to move globally instead of the objects orientation
			pos_y = EntityY(s\mesh)			;dont forget to store the new y coordinate in global pos_y
			End If
		Next
	End If
	;----------------------------------------------------
	If KeyHit(31) Or KeyHit(208)   ;S KEY
		For s.shape = Each shape
			If entity = s\mesh
				current_y# = EntityY(s\mesh)	;gather the Y coordinate
				If current_y# &gt;= 2				;if it's greater than 1
					TranslateEntity s\mesh, 0,-1,0		;move it down 1
					pos_y = EntityY(s\mesh)				;and gather the new Y coordinate
				End If 
				
			
			End If
		Next
	End If
	;----------------------------------------------------

	If KeyHit(30) Or KeyHit(203)   ;A KEY
		For s.shape = Each shape
			If entity = s\mesh
			TranslateEntity s\mesh, -1,0,0	;move it left 1
			pos_x = EntityX(s\mesh)			;same for the X coordinate as above
			End If
		Next
	End If
	;----------------------------------------------------

	If KeyHit(32) Or KeyHit(205)   ;D KEY
		For s.shape = Each shape
			If entity = s\mesh
			TranslateEntity s\mesh, 1,0,0	;move it right 1
			pos_x = EntityX(s\mesh)
			End If
		Next
	End If
	;----------------------------------------------------

	If KeyDown(19) = True And KeyDown(42) = False	;if R is down and L shift is not held down
		For s.shape = Each shape
			If entity= s\mesh
			TurnEntity s\mesh,0,1,0					;rotate it 1
			rotation = EntityYaw(s\mesh)
			End If
		Next
	End If
	;----------------------------------------------------

	If KeyDown(19) = True And KeyDown(42) = True	;if R is down and L shift IS held down
		For s.shape = Each shape
			If entity = s\mesh
			TurnEntity s\mesh,0,-1,0				;rotate it -1
			rotation = EntityYaw(s\mesh)
			End If
		Next
	End If
	;----------------------------------------------------

	If KeyHit(23)	;I key
		For s.shape = Each shape
			If entity = s\mesh
			TranslateEntity s\mesh, 0,0,1	;move it up 1
			pos_z = EntityZ(s\mesh)
			End If
		Next
	End If
	;----------------------------------------------------

	If KeyHit(37)	;K key
		For s.shape = Each shape
			If entity = s\mesh
			TranslateEntity s\mesh, 0,0,-1	;move it down 1
			pos_z = EntityZ(s\mesh)
			End If
		Next
	End If
	;----------------------------------------------------
	
	If KeyHit(46)	;C key	
	
	
		
		HideEntity cursor		;hide the cursor
		name$ = "None"			;and the global name$ will be nothing
     	movement_type = 1
		PositionEntity camera,0,25,-75 
		RotateEntity camera,15,0,0 
		move_camera = 0
			
	End If 
	
	If KeyHit(50) Then movement_type = 2	;M key
	
	End If 	
	
	;-----------------------------------------------------
	;THIS IS THE SAME AS ABOVE, BUT WILL CONTINUE TO MOVE
	;-----------------------------------------------------
	If movement_type = 2
	If KeyDown(17) Or KeyDown(200)	;W KEY
		For s.shape = Each shape		;go through all the shapes
			If entity = s\mesh			;if this is the selected entity
			TranslateEntity s\mesh, 0,.1,0	;move it up 1, we use translateentity instead of moveentity because we always want it to move globally instead of the objects orientation
			pos_y = EntityY(s\mesh)			;dont forget to store the new y coordinate in global pos_y
			End If
		Next
	End If
	;----------------------------------------------------
	If KeyDown(31) Or KeyDown(208)   ;S KEY
		For s.shape = Each shape
			If entity = s\mesh
				current_y# = EntityY(s\mesh)	;gather the Y coordinate
				If current_y# &gt;= 1.1				;if it's greater than 1
					TranslateEntity s\mesh, 0,-.1,0		;move it down 1
					pos_y = EntityY(s\mesh)				;and gather the new Y coordinate
				End If 
				
			
			End If
		Next
	End If
	;----------------------------------------------------

	If KeyDown(30) Or KeyDown(203)   ;A KEY
		For s.shape = Each shape
			If entity = s\mesh
			TranslateEntity s\mesh, -.1,0,0	;move it left 1
			pos_x = EntityX(s\mesh)			;same for the X coordinate as above
			End If
		Next
	End If
	;----------------------------------------------------

	If KeyDown(32) Or KeyDown(205)   ;D KEY
		For s.shape = Each shape
			If entity = s\mesh
			TranslateEntity s\mesh, .1,0,0	;move it right 1
			pos_x = EntityX(s\mesh)
			End If
		Next
	End If
	;----------------------------------------------------

	If KeyDown(19) = True And KeyDown(42) = False	;if R is down and L shift is not held down
		For s.shape = Each shape
			If entity= s\mesh
			TurnEntity s\mesh,0,1,0					;rotate it 1
			rotation = EntityYaw(s\mesh)
			End If
		Next
	End If
	;----------------------------------------------------

	If KeyDown(19) = True And KeyDown(42) = True	;if R is down and L shift IS held down
		For s.shape = Each shape
			If entity = s\mesh
			TurnEntity s\mesh,0,-1,0				;rotate it -1
			rotation = EntityYaw(s\mesh)
			End If
		Next
	End If
	;----------------------------------------------------

	If KeyDown(23)	;I key
		For s.shape = Each shape
			If entity = s\mesh
			TranslateEntity s\mesh, 0,0,.1	;move it up 1
			pos_z = EntityZ(s\mesh)
			End If
		Next
	End If
	;----------------------------------------------------

	If KeyDown(37)	;K key
		For s.shape = Each shape
			If entity = s\mesh
			TranslateEntity s\mesh, 0,0,-.1	;move it down 1
			pos_z = EntityZ(s\mesh)
			End If
		Next
	End If
	;----------------------------------------------------
	
	If KeyHit(46)	;C key
		PositionEntity camera,0,25,-75
		RotateEntity camera,15,0,0
		move_camera = 0
	End If 
	
	If KeyHit(50) Then movement_type = 1
	End If

	
End Function 

;------------------------------------
;FUNCTION HEADS UP DISPLAY (HUD())
;------------------------------------
Function hud()
Text 0,0,"Picked Entity: " + name$	;now dislay the global variable name$.
Text 0,10,"Pos X: " + pos_x
Text 0,20,"Pos Y: " + pos_y
Text 0,30,"Pos Z: " + pos_z
Text 0,40,"Rotation: " + rotation
Select movement_type
	Case 1
		Text 0,50,"Unit by Unit movement. 1 space per keyhit."
	Case 2
		Text 0,50,"Continuous movement. Hold key down to move."
End Select

Select gravity
	Case 1
		Text 0,60,"Gravity on."
	Case 0
		Text 0,60,"Gravity off."
End Select

Text 280,0,"Press C to reset the camera."
Text 270,10,"Press M to change movement type."
Text 272,20,"Press G to turn gravity on\off."
Text 0,570,"W/S to move up/down (y axis), A/D to move left\right (x axis), I/K to move out\in (z axis)"
Text 0,580,"R key will rotate clockwise, hold LSHIFT + R to rotate counter-clockwise."
End Function 		

;------------------------------------
;FUNCTION UPDATE CURSOR		THIS KEEPS THE CURSOR AT THE POSITION OF THE PICKED ENTITY
;------------------------------------
Function update_cursor()
If entity &lt;&gt; 0						;if an entity is selected
	Local px# = EntityX(entity)		;grab it's coordinates		
	Local py# = EntityY(entity)		;here..
	Local pz# = EntityZ(entity)		;and here...
		PositionEntity cursor,px#,py#,pz#	;now position the cursor at those coordinates
End If
End Function 
		
	
Function move_camera()
 
	If move_camera = 1	;if we should continue to move the camera....
		distance_to_target# = EntityDistance(camera,camera_target)
			
			PointEntity camera,camera_target 					;point the camera at the camera target, you won't actually see this step, its only to simplify movement of the camera
			If distance_to_target &gt;= 20							;check the distance
				camera_speed = camera_speed + .01				;increase if needed
				If camera_speed &gt; .5 Then camera_speed = .5		;set a maximum
			Else If distance_to_target &lt; 20						;same as above but will slow camera down
				camera_speed = camera_speed - .01
				If camera_speed &lt; .05 Then camera_speed = .05	;heres the minimum camera speed
			End If
			
			MoveEntity camera,0,0,camera_speed					;now we move the camera towards the pivot
			
			If entity &lt;&gt; 0					;double check that an entity has been picked, may get MAV without this line
				PointEntity camera,entity	;now point it back at the picked entity
			End If 
			
			If distance_to_target# &lt; 5	;if the camera is close to the camera target
				camera_speed = 0		;set camera speed to 0
				move_camera = 0			;stop moving the camera
			End If
	End If
 
End Function


;-----------------------------------
;function gravity()
;-----------------------------------
Function gravity()
If gravity = 1
For s.shape = Each shape
	TranslateEntity s\mesh,0,-.02,0
	If entity &lt;&gt; 0
		If entity = s\mesh
		pos_y# = EntityY(s\mesh)
		End If
	End If 
	
Next
End If 
End Function 
</textarea> <br><br></td></tr></table><br>
<a name="1017512"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >PowerPC603</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Use this:<br><pre class=code>
	; Check if mouse is being clicked
	If MouseDown(1) Then
		; Pick the entity where the mousepointer is (PickedEnt now contains the handle to the picked entity)
		PickedEnt = CameraPick(camera, MouseX(), MouseY())
		; Check if a valid entity has been picked
		If PickedEnt Then
			; If the entity was valid (the user clicked on a pickable entity), grab the name of the entity
			EntName$ = EntityName$(PickedEnt)
		Else
			; In case no valid entity was picked (open space or non-pickable entity), set EntName to nothing ("")
			EntName$ = ""
		EndIf
	EndIf
	; Print the entity name
	Text 0,80," Object Picked is "+ EntName$

</pre> <br><br></td></tr></table><br>
<a name="1017519"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Can i ask how you are storing your entities? An array, or a Type list? <br><br></td></tr></table><br>
<a name="1017596"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dicon</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Drak! That does look good. All I have to do now is to unpick the formula from your program and incorperate it into mine. <br>Failing this I will have to hire a paid programmer to do the work for me as I am not going to use the program to write a game but to show off my grapics designs for game makers.<br>I am NOT a programmer ( Gawd know I have tried ) but I have the designs and game plans and the graphics. But. Not the skill to programe.<br>I have tried quite a few "Game Designers " ( no skill coding skill needed ) and there were bugs and flaws in them all.<br>So... I will spend a few more days on this then get a pro to program for me. ( There must be a few out there that need the dosh.<br><br> Dicon<br>square eyed and flat bum. <br><br></td></tr></table><br>
<a name="1017603"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dicon</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Drak...It works fine. I was so near, but not close enough.<br> Tomorrow I will set  up the arrays to hold the mesh type,name, x,y,z and rotation direction.<br>Nice to see the name I have given them show up. <br>I use Blender for the meshes and Ultimate unwrap for UV work, DPaint for graphics.<br> I tried 3D World Studio and Silent Walk but they were  too buggy, a shame really cause' 3D World Studio did export all the meshes and textures to B3D. So, that's why I am doing it for myself.  <br>With help from allthe folks here.<br><br>Thanks.<br><br> Dicon <br><br></td></tr></table><br>
<a name="1017653"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dicon</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> PowerPC603, I chose your system as the simplest. Like I said, I am a bad coder.<br><br>Further up my program, the position,rotation and all the other variables  are put into an array, along with the name$ with name_cntr being the variable pointer to each mesh array group.<br>I have the name. now all I need is the way to load a new mesh and/or access a pre loaded one. I can save and load arrays and most other simple things but i seem to have reached an impasse.<br>Thanks<br><br><br>;...........................................................Load Mesh .......................<br>Function load_mesh()<br>item$="meshes\med_support_beam.b3d"; ;name of mesh chosen from menu<br><br>My_Object=LoadMesh(item$)<br>NameEntity My_Object,"med_support_beam"+Str$(name_cntr) ;name of mesh chosen from menu<br>m_name$(name_cntr)="med_support_beam"; name of beam<br>ScaleEntity my_object,x_scale#,y_scale#,z_scale#<br>h#=MeshHeight(my_object)/2<br>EntityPickMode my_object,3 <br>FlipMesh my_object<br>PositionEntity my_object,5,5,5 ; starting position<br>RotateEntity  my_object ,0,0,180 ; make it face the Right way<br>name_cntr=name_cntr+1 ; adds the number to the end of the name i.e. beam2<br>End Function<br>;.........................................................<br><br>If any one can help it would be appreciated.<br><br>Dicon <br><br></td></tr></table><br>
<a name="1017697"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dicon</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> The above has been tweaked and works ok. Thanks all.<br><br>I guess that all I need to get further on with it is a way to change  "my_object" which is the mesh "beam1" to become "beam2" which is another mesh, so's I can add more meshes to the terrain.<br>Thanks<br>  Dicon <br><br></td></tr></table><br>
<a name="1018053"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dicon</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> Having arrived at an impasse' I need to find one thing.<br>That clicking on a mesh from a selection of meshes, ( probably the pick command ) I can then move, rotate and change the "picked" entity. <br> I have tried everything, de-constructing code from the forums and spent hours re-arranging what I have.<br>Please help.<br><br>Dicon<br>I would go gray if I wasn't already! <br><br></td></tr></table><br>
<a name="1018193"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >PowerPC603</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've constructed a complete example that:<br>- shows the name of the picked entity<br>- shows the entity-handle of the picked entity<br>- searches through types to find the entityhandle and show a value stored in this instance (so you can see that the correct type instance was found)<br>- it also marks the recently picked entity red, while it unmarks all other entities white (using entitycolor), so you visually see that the correct entity was picked and can be used to modify it<br><br><pre class=code>
Graphics3D 800, 600, 0, 2
SetBuffer BackBuffer()

SeedRnd MilliSecs()


; Setup type structure
Type TObj
	Field Entity ; Holds the handle to an entity
	Field Value ; Holds a random value
End Type

; Create 20 instances of the type "TObj" and create a cube for each instance
For i = 1 To 20
	; Create a new TObj instance
	temp.TObj = New TObj
	; Create a new cube entity and store the handle in the type instance
	temp\Entity = CreateCube()
	; Position the cube at some random coordinates
	PositionEntity temp\Entity, Rnd#(-100.0, 100.0), Rnd#(-100.0, 100.0), Rnd#(-100.0, 100.0)
	; Scale the cubes
	ScaleEntity temp\Entity, 5.0, 5.0, 5.0
	; Make the cube polygon-pickable
	EntityPickMode temp\Entity, 2
	; Set the entityname
	NameEntity temp\Entity, "Cube " + Str$(i)
	; Finally add a number to the instance
	temp\Value = i * 1000
Next

Global camera = CreateCamera()
PositionEntity camera, 0, 0, -200



While Not KeyHit(1)
	Local PickedEnt, EntName$, EntityInstance.TObj

	; Check if mouse is being clicked
	If MouseHit(1) Then
		; Pick the entity where the mousepointer is (PickedEnt now contains the handle to the picked entity)
		PickedEnt = CameraPick(camera, MouseX(), MouseY())
		; Check if a valid entity has been picked
		If PickedEnt Then
			; If the entity was valid (the user clicked on a pickable entity), grab the name of the entity
			EntName$ = EntityName$(PickedEnt)
			; Find the type-instance where the entity belongs to
			EntityInstance.TObj = FindObject(PickedEnt)
			; Mark the picked entity
			MarkObject(PickedEnt)
		Else
			; In case no valid entity was picked (open space or non-pickable entity), set EntName to nothing ("")
			EntName$ = ""
			; Also set the reference to a TObj instance to null (no instance)
			EntityInstance.TObj = Null
			; Un-mark all entities
			MarkObject(0)
		EndIf
	EndIf

	RenderWorld

	; Print the entity name
	Text 10, 10, "Object-name: " + EntName$
	Text 10, 22, "Object-handle: " + PickedEnt
	If EntityInstance &lt;&gt; Null Then
		Text 10, 34, "Value of the object instance: " + EntityInstance\Value
	EndIf

	Flip
Wend

End



; This function finds the TObj type-instance where the stored entity-handle is the same as the Entity-handle provided as a parameter
Function FindObject.TObj(Entity)
	; Search through all TObj instances
	For temp.TObj = Each TObj
		; When the given Entity-handle matches the entity-handle that was stored in the TObj instance, return the TObj instance
		If temp\entity = Entity Then Return temp
	Next
End Function

Function MarkObject(Entity)
	; Search through all TObj instances
	For temp.TObj = Each TObj
		; When the given Entity-handle matches the entity-handle that was stored in the TObj instance
		If temp\entity = Entity Then
			; Color the picked entity red
			EntityColor temp\Entity, 255, 0, 0
		Else
			; Mark all other entities white (in case another entity was picked before, it would stay red if it isn't un-marked)
			EntityColor temp\Entity, 255, 255, 255
		EndIf
	Next
End Function
</pre> <br><br></td></tr></table><br>
<a name="1018201"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> Once you have the entity handle then you can keep a record of that as a Global variable, something like 'CurrentMesh'<br>This way, you can then deal with the rotating and all by referring to the CurrentMesh. <br><br></td></tr></table><br>
<a name="1018229"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dicon</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks to you all for your help. I will now play with the code over the weekend. Thanks for the program PowerPC603.<br> Dicon<br>If I ( no.. when) I do get it finished I will put up some screen shots!<br>Great <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
