<!DOCTYPE html><html lang="en" ><head ><title >Movement around a sphere.</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Movement around a sphere.</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Movement around a sphere.</a><br><br>
<a name="1158682"></a>

<a name="1158765"></a>

<a name="1158766"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Aussie</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey there all, been a while since I have posted here.<br>I have a sphere as a planet &amp; have a player that orbits the planet. <br>I have used a pivot &amp; parented the player to the pivot that is used to move the player around the planet. <br>I am trying to setup a system for an enemy to chase the player &amp; am having problems regarding the best way to go about the enemy chasing system &amp; aligning to the player correctly.<br><br>If someone could offer some advice or an example it would be greatly appreciated.<br><br>Here is what I have. Use the arrow keys to move the player. <br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 1024,768,32,1

SetBuffer BackBuffer()

P_VEL#  = 0
P_YAW# = 0
	
light = CreateLight()
	RotateEntity light,180,0,0

camera = CreateCamera()
	CameraRange camera,.1,5000
	

masterpiv = CreatePivot()

sphere = CreateSphere(18)
	EntityColor sphere,0,150,255
	ScaleEntity sphere,550,550,550


shippiv = CreatePivot()
	PositionEntity shippiv,0,0,0
	player = CreateCone()
		EntityParent player,shippiv
		;PositionEntity player,0,0,-590
		PositionEntity player,0,590,0
		RotateMesh player,90,0,0
		ScaleEntity player,5,5,5
		EntityColor player,0,255,0
		RotateEntity shippiv,-90,0,0
		
		
enemypiv = CreatePivot()
	PositionEntity enemypiv,0,0,0
	enemy = CreateCone()
		EntityParent enemy,enemypiv
		PositionEntity enemy,0,590,0
		RotateMesh enemy,90,0,0
		ScaleEntity enemy,5,5,5
		EntityColor enemy,255,0,0
		RotateEntity enemypiv,10,200,0

		
		
campiv = CreatePivot()
	EntityParent campiv,shippiv
	PositionEntity campiv,0,1000,0
	pivx# = EntityX(campiv,True)
	pivy# = EntityY(campiv,True)
	pivz# = EntityZ(campiv,True)
	PositionEntity camera,pivx#,pivy#,pivz#


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
While Not KeyDown (1)


If KeyDown (203)
	P_YAW# = P_YAW# + .25
	ElseIf KeyDown (205)
	P_YAW# = P_YAW# - .25
End If

If P_YAW# &gt; 4
	P_YAW# = 4
ElseIf P_YAW# &lt; -4
	P_YAW# = -4
End If



				
If KeyDown (200)
	P_VEL# = P_VEL# + .01
	create_flame(player,Rnd(-.5,.5),0,0,Rnd(10,20))
End If




If P_VEL# &gt; 1 P_VEL# = 1


If P_VEL# &gt; 0
P_VEL# = P_VEL# - .005
If P_VEL# &lt; 0 P_VEL# = 0
End If


If P_YAW# &gt; 0
   P_YAW# = P_YAW# - .05
  If P_YAW# &lt; 0 P_YAW# = 0             
ElseIf P_YAW# &lt; 0
   P_YAW# = P_YAW# + .05
  If P_YAW# &gt; 0 P_YAW# = 0
ElseIf P_YAW# = 0
   P_YAW# = 0
End If




;TurnEntity shippiv,PV#,0,P_YAW#

TurnEntity shippiv,P_VEL#,P_YAW#,0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;HELP;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

If EntityDistance(enemy,player) &lt; 150 
	PointEntity enemy,player
	;ex# = EntityX( enemy ) - EntityX( player )
	;ey# = EntityY( enemy ) - EntityY( player )
	;ez# = EntityZ( enemy ) - EntityZ( player )
	;AlignToVector enemypiv,ex,ey,ez,2,.5
	
	;RotateEntity enemypiv,0,ey#,0
	ey# = EntityYaw(enemy)
	
End If

TurnEntity enemypiv,.5,ey#,0 

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;






pivx# = EntityX(campiv,True)
pivy# = EntityY(campiv,True)
pivz# = EntityZ(campiv,True)
PositionEntity camera,pivx#,pivy#,pivz#



vx# = EntityX( shippiv ) - EntityX( camera )
vy# = EntityY( shippiv ) - EntityY( camera )
vz# = EntityZ( shippiv ) - EntityZ( camera )

AlignToVector camera,vx,vy,vz,3,1


update_flame()

UpdateWorld


RenderWorld



Flip
LimitFrameRate(38)
Wend
End

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Function LimitFrameRate(FPS = 65)
	If FrameTime = 0 Then
		Period = 1000 / FPS
		FrameTime = MilliSecs()
	EndIf

	While (FrameTime + Period) &gt; MilliSecs()
			Delay 1
	Wend
	FrameTime = MilliSecs()
End Function 


Type flame
	Field life%
	Field scale#
	Field x#,y#,z#
	Field obj
	Field alpha#
	Field R#,G#,B#
End Type

Function create_flame(entity,x#,y#,z#,life#)
	f.flame = New flame
		f\obj = CreateSphere(10,entity)
		f\R# = 0
		f\G# = 155
		f\B# = 255
		EntityColor f\obj,f\R,f\G,f\B
		EntityFX f\obj,1
		EntityBlend f\obj,3
		f\scale# = Rand(1,4)
		f\alpha# = 1
		f\life = life#
	PositionEntity f\obj,x,y,z
	EntityParent f\obj,0
End Function

Function update_flame()
	For f.flame = Each flame
		f\G = f\G - Rand(0,10)
		EntityColor f\obj,f\R,f\G,f\B
		f\alpha = f\alpha - .1
		ScaleEntity f\obj,f\scale,f\scale,f\scale
		EntityAlpha f\obj,f\alpha
		f\life = f\life - 1
		If f\life &lt; 0 Then
			FreeEntity f\obj
			Delete f
		End If
	Next
End Function
</textarea><br><br><font class="tiny">Last edited 2012</font><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1158688"></a>

<a name="1158689"></a>

<a name="1158690"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>I am trying to setup a system for an enemy to chase the player &amp; am having problems regarding the best way to go about the enemy chasing system &amp; aligning to the player correctly.<br> <br></div><br>If you want the enemy to move around the planet, as the player does, and chase the player at this height, here what i would do :<br><br>1)Turn the enemy towards the player<br>2)Move the enemy forward (+Z)<br>3)Make your planet pickable<br>4)LinePick from above the enemy to below the enemy (from above because the enemy ship may be inside the planet at first)<br>5)Use the picked point coordinates to position the enemy on the planet at the desired height (if you want to add the ability to go up or down.)<br>6)Align the enemy to the ground of the planet with AlignToVector<br><br>I suggest you also use the same method for the movement of the player ship, so that you will be able to adjust the height you want.<br><br>This is probably not the best technique, your planet being a sphere it may be easier to do it with maths. But i can't help with this.<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1158693"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Aussie</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks RemiD,<br><br>I probably should have said the reason i have been using the pivots &amp; positioning the enemy &amp; player offset from the pivots is because i'm not to keen on going down the path of using linepicks.<br><br>This is just a test to get a system of the enemy being able to chase the player correctly &amp; eventually i will be having a fair few enemies/objects around the planet &amp; if i use a heap of linepicks it will probably cause to much of a slowdown.<br><br>I had thought maybe there was a way to do it with maths but i am not that great at using maths for this kind of a system. I am tipping it would use Sin, Cos, Tan but i could never really get my head around it.<br><br>Thanks for the help anyway.<br> <br>Actually, <br>dose anyone know how much of a slowdown linepicks cause? I remember reading that to many will cause a slowdown &amp; eventually there could be up to 100 or so objects around the planet. <br><br></td></tr></table><br>
<a name="1158702"></a>

<a name="1158703"></a>

<a name="1158704"></a>

<a name="1158705"></a>

<a name="1158713"></a>

<a name="1158714"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Linepick and Collisions are calculated very fast if used properly.<br><br>The best way i have found to make it fast :<br>At the begining of each loop, delete all colliders and disable all pickmode.<br><br>Before you do a check, set only the collider or the pickmode of the colliders you need.<br><br>If you use meshes for colliders and pickable entities, make sure these meshes have the fewest tris possible, just to approximate the shape of the collider / pickable entity.<br><br>This way you check if a collision or a pick happens on only a few colliders / pickable entities and they are low tris so it is very fast !<br><br>You can use linepick for many things and it won't slow down your game if you follow the advices mentioned above.<br><br><br><br>Of course for a planet being made of a perfect sphere, i think will be better with maths...<br>Edit : corrections<br>Maybe you can add vectors :<br>The vector of movement of the ship (+Z locally)<br>+<br>The vector of the difference of the distance from the ship to the center of the sphere minus the radius of the sphere, this will give you a length that is the distance from the ship to the ground of the planet after the movement of the ship.<br><br>And with this you will be able to position the ship.<br><br>Try it it should work.<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1158711"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Zethrax</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Not sure what you're after exactly, but here's some smart missile code I wrote at one point that sound like what you're requesting.<br><br><a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=2258" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=2258</a> <br><br></td></tr></table><br>
<a name="1158763"></a>

<a name="1158764"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Aussie</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks guys<br><br>@ RemiD<br>I never thought of doing that with linepicks &amp; collisions i will have to try it out.<br> <br>With the vector<br><div class="quote"> The vector of movement of the ship (+Z locally) <br></div><br>Are you talking velocity or Z coordinates?<br>Like i was saying, i'm not that great with maths when it comes to dealing with circles or spheres. :)<br><br>@ Zethrax<br>What you have there is the kind of thing i am trying to achieve, the difference is that the missile would be orbiting a planet &amp; chasing other objects in the same orbit. Hope that explains what i am trying to achieve.<br><br>I will have a play around &amp; see what i can come up with.<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1158779"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Aussie</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok i am struggling with this.<br>I decided to try &amp; use the linepick method instead of using pivots &amp; i can't get the player to align with the sphere using the aligntovector. Here is what i have, if some one could let me know what i am doing wrong it would be greatly appreciated.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 1024,768,32,1

SetBuffer BackBuffer()

P_VEL#  = 0
P_YAW# = 0
	
light = CreateLight()
	RotateEntity light,180,0,0

camera = CreateCamera()
	CameraRange camera,.1,5000
	PositionEntity camera,0,1000,0
	RotateEntity camera,090,0,0
	

masterpiv = CreatePivot()

sphere = CreateSphere(18)
	EntityColor sphere,0,150,255
	ScaleEntity sphere,550,550,550
	UpdateNormals sphere
	EntityPickMode sphere,2



player = CreateCone()
	
	PositionEntity player,0,590,0
	RotateMesh player,90,0,0
	ScaleEntity player,5,5,5
	EntityColor player,0,255,0

		
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
While Not KeyDown (1)

TFormVector 0,-50,0,player,0

LinePick (EntityX(player,True),EntityY(player,True),EntityZ(player,True),TFormedX(),TFormedY(),TFormedZ())
AlignToVector player,PickedNX#,PickedNY#,PickedNZ#,2,1

If KeyDown (203)
	P_YAW# = P_YAW# + .25
	ElseIf KeyDown (205)
	P_YAW# = P_YAW# - .25
End If

If P_YAW# &gt; 4
	P_YAW# = 4
ElseIf P_YAW# &lt; -4
	P_YAW# = -4
End If



				
If KeyDown (200)
	P_VEL# = P_VEL# + .5
	create_flame(player,Rnd(-.5,.5),0,0,Rnd(10,20))
End If




If P_VEL# &gt; 5 P_VEL# = 5


If P_VEL# &gt; 0
P_VEL# = P_VEL# - .1
If P_VEL# &lt; 0 P_VEL# = 0
End If


If P_YAW# &gt; 0
   P_YAW# = P_YAW# - .05
  If P_YAW# &lt; 0 P_YAW# = 0             
ElseIf P_YAW# &lt; 0
   P_YAW# = P_YAW# + .05
  If P_YAW# &gt; 0 P_YAW# = 0
ElseIf P_YAW# = 0
   P_YAW# = 0
End If


MoveEntity player,0,0,P_VEL#
TurnEntity player,0,P_YAW#,0


update_flame()

UpdateWorld


RenderWorld



Flip
LimitFrameRate(38)
Wend
End

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Function LimitFrameRate(FPS = 65)
	If FrameTime = 0 Then
		Period = 1000 / FPS
		FrameTime = MilliSecs()
	EndIf

	While (FrameTime + Period) &gt; MilliSecs()
			Delay 1
	Wend
	FrameTime = MilliSecs()
End Function 


Type flame
	Field life%
	Field scale#
	Field x#,y#,z#
	Field obj
	Field alpha#
	Field R#,G#,B#
End Type

Function create_flame(entity,x#,y#,z#,life#)
	f.flame = New flame
		f\obj = CreateSphere(10,entity)
		f\R# = 0
		f\G# = 155
		f\B# = 255
		EntityColor f\obj,f\R,f\G,f\B
		EntityFX f\obj,1
		EntityBlend f\obj,3
		f\scale# = Rand(1,4)
		f\alpha# = 1
		f\life = life#
	PositionEntity f\obj,x,y,z
	EntityParent f\obj,0
End Function

Function update_flame()
	For f.flame = Each flame
		f\G = f\G - Rand(0,10)
		EntityColor f\obj,f\R,f\G,f\B
		f\alpha = f\alpha - .1
		ScaleEntity f\obj,f\scale,f\scale,f\scale
		EntityAlpha f\obj,f\alpha
		f\life = f\life - 1
		If f\life &lt; 0 Then
			FreeEntity f\obj
			Delete f
		End If
	Next
End Function
</textarea><br><br>I have also been trying to figure out how to use the vectors for doing this kind of a control system  with no luck. I did do some looking around for a spherical coordinate system &amp; information on how they work &amp; it is way over my head.<br>If i can get the linepick &amp; aligntovector to work i think i should be able to get the enemy to chase the player properly. <br><br></td></tr></table><br>
<a name="1158783"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> The PickedNX, NY and NZ commands are functions which return values so should always be used with open/close brackets,i.e:<br><br>[bbcode]<br>AlignToVector player,PickedNX(),PickedNY(),PickedNZ(),2,1<br>[/bbcode]<br><br>This seems to be your only issue.<br><br>Stevie <br><br></td></tr></table><br>
<a name="1158794"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Aussie</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks heaps Stevie G<br><br>It has been driving me nuts not being able to figure out what was going wrong. I looked at other examples &amp; didn't even notice the ().<br>Works great now. <br><br></td></tr></table><br>
<a name="1158800"></a>

<a name="1158801"></a>

<a name="1158808"></a>

<a name="1158815"></a>

<a name="1158816"></a>

<a name="1158819"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Aussie&gt;&gt;This works well and Linepick seems to be fast enough for now.<br>Here are some suggestions :<br>-&gt;First turn your ship and move it, and then do the linepick calculation in order to correct the position and orientation.<br>-&gt;You should use a collider with less tris for the sphere, or if it is faster, use a combination of EntityPickMode sphere with EntityRadius()<br><br>Edit: i have added the EntityPickMode(1) with EntityRadius(), it is really fast !<br><br>Here is your code with some suggestions :<br>Also i have added the routine with vectors in words, maybe somebody can help you find the math formula, i can't help sorry.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 1024,768,32,2
HidePointer()
SetBuffer BackBuffer()

P_VEL#  = 0
P_YAW# = 0
	
light = CreateLight()
	RotateEntity light,180,0,0

camera = CreateCamera()
	CameraRange camera,.1,5000
	PositionEntity camera,0,1000,0
	RotateEntity camera,090,0,0
	

masterpiv = CreatePivot()

sphere = CreateSphere(16)
	EntityColor sphere,0,150,255
	ScaleEntity sphere,550,550,550
	UpdateNormals sphere
;SphereCollider = CreateSphere(8)
;ScaleEntity SphereCollider,550,550,550
;EntityAlpha(SphereCollider,0)
SphereCollider = CreatePivot()
EntityRadius(SphereCollider,550)

player = CreateCone()
	
	PositionEntity player,0,590,0
	RotateMesh player,90,0,0
	ScaleEntity player,5,5,5
	EntityColor player,0,255,0

Const Pick% = 1
Const Vectors% = 2
RoutineMode% = Pick
		
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
While Not KeyDown (1)

If KeyDown (203)
	P_YAW# = P_YAW# + .25
	ElseIf KeyDown (205)
	P_YAW# = P_YAW# - .25
End If

If P_YAW# &gt; 4
	P_YAW# = 4
ElseIf P_YAW# &lt; -4
	P_YAW# = -4
End If
				
If KeyDown (200)
	P_VEL# = P_VEL# + .5
	create_flame(player,Rnd(-.5,.5),0,0,Rnd(10,20))
End If

If P_VEL# &gt; 5 P_VEL# = 5

If P_VEL# &gt; 0
P_VEL# = P_VEL# - .1
If P_VEL# &lt; 0 P_VEL# = 0
End If

If P_YAW# &gt; 0
   P_YAW# = P_YAW# - .05
  If P_YAW# &lt; 0 P_YAW# = 0             
ElseIf P_YAW# &lt; 0
   P_YAW# = P_YAW# + .05
  If P_YAW# &gt; 0 P_YAW# = 0
ElseIf P_YAW# = 0
   P_YAW# = 0
End If

MoveEntity player,0,0,P_VEL#
TurnEntity player,0,P_YAW#,0

If(RoutineMode = Pick)
 LinePickStart# = MilliSecs()
 ;You want to remove the For Next loop, it is just to see if it is faster with EntityPickMode(1) or EntityPickMode(2), EntityPickMode(1) is faster.
 For i% = 1 To 1000
  ;EntityPickMode(SphereCollider,2)
  EntityPickMode(SphereCollider,1)
  TFormVector 0,-50,0,player,0
  LinePick (EntityX(player,True),EntityY(player,True),EntityZ(player,True),TFormedX(),TFormedY(),TFormedZ())
  EntityPickMode(SphereCollider,0)
 Next
 LinePickTime# = (Float(MilliSecs()) - LinePickStart)/i
 AlignToVector player,PickedNX(),PickedNY(),PickedNZ(),2,1
ElseIf(RoutineMode = Vectors)
 ;Calculates the distance from the ship to the center of the sphere
 ;VLengthSC# =
 ;Calculates the distance from the ground to the center of the sphere (its radius...)
 ;VLengthGC# =
 ;Calculates the distance from the ship to the ground
 ;VLengthSG# = VLengthSC - VLengthGC
 ;Moves the ship  at the level of the ground + add some height so that the ship is flying above the ground 
 MoveEntity(Player,0,-VLengthSG#+1,0)
 ;Aligns the ship to the ground of the sphere at this specific location
 ;Here you want to calculate an angle with several vectors (but i don't know the formula)
 ;The vector that starts from the center of the sphere and ends at the ship
 ;The vector of movement of the ship
 ;A vector that is perpendicular to the first vector and goes in the same "direction" (except on one axis) than the second vector.
 ;The angle between the third vector and the second vector should be the angle that the ship needs to turn in order to be aligned to the ground
EndIf

update_flame()

RenderWorld()

Text(0,0,"LinePickTime = "+LinePickTime+" ms")

Flip()

LimitFrameRate(38)

Wend

End

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Function LimitFrameRate(FPS = 65)
	If FrameTime = 0 Then
		Period = 1000 / FPS
		FrameTime = MilliSecs()
	EndIf

	While (FrameTime + Period) &gt; MilliSecs()
			Delay 1
	Wend
	FrameTime = MilliSecs()
End Function 

Type flame
	Field life%
	Field scale#
	Field x#,y#,z#
	Field obj
	Field alpha#
	Field R#,G#,B#
End Type

Function create_flame(entity,x#,y#,z#,life#)
	f.flame = New flame
		f\obj = CreateSphere(10,entity)
		f\R# = 0
		f\G# = 155
		f\B# = 255
		EntityColor f\obj,f\R,f\G,f\B
		EntityFX f\obj,1
		EntityBlend f\obj,3
		f\scale# = Rand(1,4)
		f\alpha# = 1
		f\life = life#
	PositionEntity f\obj,x,y,z
	EntityParent f\obj,0
End Function

Function update_flame()
	For f.flame = Each flame
		f\G = f\G - Rand(0,10)
		EntityColor f\obj,f\R,f\G,f\B
		f\alpha = f\alpha - .1
		ScaleEntity f\obj,f\scale,f\scale,f\scale
		EntityAlpha f\obj,f\alpha
		f\life = f\life - 1
		If f\life &lt; 0 Then
			FreeEntity f\obj
			Delete f
		End If
	Next
End Function
</textarea><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1158836"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Aussie</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for the help RemiD.<br>Very much appreciated. I would be interested in trying to use vectors but way over my head with the formulas. <br><br></td></tr></table><br>
<a name="1158879"></a>

<a name="1158881"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> The best way i have found to make it fast :<br>At the begining of each loop, delete all colliders and disable all pickmode.<br><br>Before you do a check, set only the collider or the pickmode of the colliders you need. <br></div><br>Cool idea; I hadn't thought of that before.<br>Changing pickmodes is merely setting values in memory internally, so it should be much faster than having every redundant but potentially pickable entity be tested.<br><br>Anyway, I played with the code a bit, changed [a lot of] things... and filled the vector math routine. Took a while. <br>Seems to work ok; it's definitely much faster. Just copy and paste:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">; Vector-math adaptation by Rafael "Kryzon".

Graphics3D 1024,768,0,2
HidePointer()
SetBuffer BackBuffer()

Const BENCHMARK_PASSES% = 1000 ;Try 10k to see a bigger difference. Vector-math is much faster.

Const MOVE_INCREMENT# = 0.25
Const MOVE_DECAY# = 0.1
Const TURN_INCREMENT# = 0.1

Const PICK_ROUTINE% = 1
Const VECTOR_ROUTINE% = 2

Const PLAYER_CLASS% = 1
Const ENEMY_CLASS% = 2
Const NEUTRAL_CLASS% = 3

Global RoutineMode% = PICK_ROUTINE

Global FPSTimer% = CreateTimer(60)
Global performanceTime#


AmbientLight 130,70,40
light = CreateLight()
	RotateEntity light,150,-45,0
	  
Global camera = CreateCamera()
	CameraRange camera,1,2000
	PositionEntity camera,0,1000,0
	RotateEntity camera,90,0,0
	
Global planet = CreateSphere(64)
	EntityColor planet,25,150,250
	;Radius of planet is '550'. Distance from ship to planet is 20, so a 'real' radius of 570.
	ScaleEntity planet,550,550,550 
	;If using the Sphere pickmode the 'collider pivot' is no longer necessary. Linepick will still use the same fast spherical 
	;representation of the entity even if it's a complex mesh.
	EntityRadius(planet,550)
	EntityPickMode(planet,1)

Global player.starShip = CreateShip(PLAYER_CLASS)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
While Not KeyDown (1)
	WaitTimer(FPSTimer)

	UpdateWorld()
	UpdateScene()

	RenderWorld()
	Render2D()

	Flip()
Wend

End
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


Type flame
	Field life%
	Field scale#
	Field x#,y#,z#
	Field obj
	Field alpha#
	Field R#,G#,B#
End Type


Type starShip
	Field mesh%
	Field flamePoint%
	Field flameLight%
	Field vector.movementVector
	
	Field class% 
End Type


Type movementVector ;A vector in the X ~ Z plane.
	Field x#
	Field y#
End Type


Function CreateShip.starShip(class%)
	s.starShip = New starShip
	s\vector = New movementVector
	s\class = class
	s\flamePoint = CreatePivot()

	Select class
		Case PLAYER_CLASS
			s\mesh = CreateCone()
			PositionEntity s\mesh,0,570,0
			RotateMesh s\mesh,90,0,0
			ScaleEntity s\mesh,8,8,12
			EntityColor s\mesh,0,255,0
			EntityParent s\flamePoint,s\mesh,False
			MoveEntity s\flamePoint,0,0,-1
			
			s\flameLight = CreateLight(2)
			EntityParent s\flameLight,s\flamePoint,False
			MoveEntity s\flameLight,0,0,-1
			LightRange s\flameLight,0.1
		
		Case ENEMY_CLASS
			;Different visuals, settings etc.
		
		Case NEUTRAL_CLASS
			;Different visuals, settings etc.
	End Select
	
	Return s
End Function


Function CreateFlame(entity,x#,y#,z#,life#)	
	f.flame = New flame
		f\obj = CreateSphere(10,entity)
		f\R# = 0
		f\G# = 155
		f\B# = 255
		EntityColor f\obj,f\R,f\G,f\B
		EntityFX f\obj,1
		EntityBlend f\obj,3
		f\scale# = Rand(1,4)
		f\alpha# = 1
		f\life = life#
	PositionEntity f\obj,x,y,z
	EntityParent f\obj,0
End Function


Function UpdateFlame()
	For f.flame = Each flame
		f\G = f\G - Rand(0,10)
		EntityColor f\obj,f\R,f\G,f\B
		f\alpha = f\alpha - .075
		ScaleEntity f\obj,f\scale,f\scale,f\scale
		EntityAlpha f\obj,f\alpha
		f\life = f\life - 1
		If f\life &lt; 0 Then
			FreeEntity f\obj
			Delete f
		End If
	Next
End Function


Function UpdateScene()
	UpdateShips()
   ;UpdateProps()
   ;[...]
   
	UpdateFlame()
End Function


Function UpdateShips()
	;Update each class of ship based on specific logic.
	For s.starShip = Each starShip
		Select s\class
			Case PLAYER_CLASS
				UpdatePlayer(s)
			Case ENEMY_CLASS
				;UpdateEnemy(s)
			Case NEUTRAL_CLASS
				;UpdateNeutral(s)
		End Select
		
		MoveShip(s)
	Next
End Function


Function UpdatePlayer(p.starShip)	
	Local kLEFT = KeyDown(203)
	Local kRIGHT = KeyDown(205)
	Local kUP = KeyDown(200)
	Local kDOWN = KeyDown(208)
	If KeyHit(57) Then RoutineMode = 3 - RoutineMode ;Toggles between '1' and '2'.
	
	;Left, Right.
	p\vector\x = p\vector\x - (TURN_INCREMENT * kLEFT) + (TURN_INCREMENT * kRIGHT)
	If (kLEFT + kRIGHT &lt;&gt; 1) Then p\vector\x = 0 ;If pressing both keys or none at all, do not turn the ship.
	
	If p\vector\x &gt; 1 Then p\vector\x = 1
	If p\vector\x &lt; -1 Then p\vector\x = -1
		
	;Up, Down.
	p\vector\y = p\vector\y + (MOVE_INCREMENT * kUP) - (MOVE_INCREMENT * kDOWN) 
	
	p\vector\y = p\vector\y - MOVE_DECAY
	If p\vector\y &gt; 5 Then p\vector\y = 5
	If p\vector\y &lt; 0 Then p\vector\y = 0
	
	;Special FX.
	If kUP Then CreateFlame(p\flamePoint,Rnd(-.75,.75),0,0,Rnd(75,200))
	LightRange(p\flameLight, (30*(p\vector\y / 5)^2 + 0.1) )
End Function


Function MoveShip(s.starShip)
	performanceTime = MilliSecs()

	Select RoutineMode			
		Case PICK_ROUTINE
			For benchmark = 1 To BENCHMARK_PASSES
				TFormVector 0,-100,0,s\mesh,0
				LinePick (EntityX(s\mesh,True),EntityY(s\mesh,True),EntityZ(s\mesh,True),TFormedX(),TFormedY(),TFormedZ())
			Next 
				
			PositionEntity s\mesh,PickedX(),PickedY(),PickedZ()
			AlignToVector s\mesh,PickedNX(),PickedNY(),PickedNZ(),2			  
			
			MoveEntity s\mesh,0,20,s\vector\y
			
			If s\vector\y &gt; 0 Then ;If the ship is moving at all then allow turning.
				TFormVector s\vector\x * (s\vector\y / 5),0,s\vector\y,s\mesh,0 ;Get the motion vector in world coordinates. Note the use of the X ~ Z plane.
				AlignToVector s\mesh,TFormedX(),TFormedY(),TFormedZ(),3
			EndIf 			
			
			performanceTime = (MilliSecs() - performanceTime)
		
		Case VECTOR_ROUTINE
			For benchmark = 1 To BENCHMARK_PASSES
				TFormPoint s\vector\x,0,s\vector\y,s\mesh,0
				;Distance from the target direction to the center of the planet:
				Local pointLength# = Sqr(TFormedX()*TFormedX() + TFormedY()*TFormedY() + TFormedZ()*TFormedZ())
				pointLength = 570.0 / pointLength
			
				Local vx#,vy#,vz# 
				vx = TFormedX()*pointLength
				vy = TFormedY()*pointLength
				vz = TFormedZ()*pointLength
			Next
			
			MoveEntity s\mesh,0,0,s\vector\y
				
			If s\vector\y &gt; 0 Then
				AlignToVector s\mesh,(vx - EntityX(s\mesh,True)),(vy - EntityY(s\mesh,True)),(vz - EntityZ(s\mesh,True)),3,(s\vector\y / 5)			
			EndIf 
									
			performanceTime = (MilliSecs() - performanceTime)			
	End Select
End Function


Function Render2D()
	Local tempX#,tempY#
	
	;Neat bit, showing indicator for the occluded player ship so you have some sense where you're at.
	If EntityDistance(camera, player\mesh) &gt; 1000 Then
		CameraProject camera,EntityX(player\mesh,True),EntityY(player\mesh,True),EntityZ(player\mesh,True)
		Rect ProjectedX()-2,ProjectedY()-2,4,4,0
		tempX = ProjectedX() : tempY = ProjectedY()
		TFormPoint player\vector\x,0,player\vector\y,player\mesh,0
		CameraProject camera,TFormedX(),TFormedY(),TFormedZ()
		Line tempX,tempY,ProjectedX(),ProjectedY()
	EndIf 

	;Performance information.
	Select RoutineMode
		Case PICK_ROUTINE
			Text(0,8," LinePick performance: "+performanceTime+" ms")
		Case VECTOR_ROUTINE
			Text(0,8," Vector-math performance: "+performanceTime+" ms")
	End Select
	
	Local msg$ = "Press SPACE to change movement logic. "
	Text GraphicsWidth()-StringWidth(msg),8,msg
End Function</textarea><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1158907"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Aussie</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cool.<br>Thanks heaps Kryzon.<br>Love the effect of the light on the surface of the planet. <br><br></td></tr></table><br>
<a name="1159024"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> • Added a single enemy following the player.<br><br>When you want to add more features to your program you notice how important modularity and structure are. Generic setup functions, generic types\classes, sensible frameworking.<br>It made it all that much easier knowing where to add the new code, or change some parts (like the behind-the-planet 2D indicators).<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
; Vector-math adaptation 2.0 - Includes simple AI.

Graphics3D 1024,768,0,2
HidePointer()
SetBuffer BackBuffer()

Const BENCHMARK_PASSES% = 1 ;Try 10k to see a bigger difference. Vector-math is much faster.

Const MOVE_INCREMENT# = 0.25
Const MOVE_DECAY# = 0.1
Const TURN_INCREMENT# = 0.1

Const PICK_ROUTINE% = 1
Const VECTOR_ROUTINE% = 2

Const PLAYER_CLASS% = 1
Const ENEMY_CLASS% = 2
Const NEUTRAL_CLASS% = 3

Global RoutineMode% = PICK_ROUTINE

Global FPSTimer% = CreateTimer(60)
Global performanceTime#


AmbientLight 130,70,40
light = CreateLight()
	RotateEntity light,150,-45,0
	  
Global camera = CreateCamera()
	CameraRange camera,1,2000
	PositionEntity camera,0,1000,0
	RotateEntity camera,90,0,0
	
Global planet = CreateSphere(64)
	EntityColor planet,25,150,250
	ScaleEntity planet,550,550,550 ;Radius of planet is '550'. Distance from ship to planet is 20, so a 'real' radius of 570.
	;If using the Sphere pickmode the 'collider pivot' is no longer necessary. Linepick will still use the same fast spherical 
	;representation of the entity even if it's a complex mesh.
	EntityRadius(planet,550)
	EntityPickMode(planet,1)

Global player.starShip = CreateShip(PLAYER_CLASS)
Global enemy1.starShip = CreateShip(ENEMY_CLASS)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
While Not KeyDown (1)
	WaitTimer(FPSTimer)

	UpdateWorld()
	UpdateScene()

	RenderWorld()
	Render2D()

	Flip()
Wend

End
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


Type flame
	Field life%
	Field scale#
	Field x#,y#,z#
	Field obj
	Field alpha#
	Field R#,G#,B#
End Type


Type starShip
	Field mesh%
	Field flamePoint%
	Field flameLight%
	Field vector.movementVector
	
	Field class% 
End Type


Type movementVector ;A vector on the X ~ Z plane.
	Field x#
	Field y#
End Type


Function CreateShip.starShip(class%)
	;Generic setup for all ships.
	s.starShip = New starShip
	s\vector = New movementVector
	s\class = class
	s\flamePoint = CreatePivot()
	
	;Class-specific setup.
	Select class
		Case PLAYER_CLASS
			s\mesh = CreateCone()
			PositionEntity s\mesh,0,570,0
			RotateMesh s\mesh,90,0,0
			ScaleEntity s\mesh,8,8,12
			EntityColor s\mesh,0,255,0
			EntityParent s\flamePoint,s\mesh,False
			MoveEntity s\flamePoint,0,0,-1
			
			s\flameLight = CreateLight(2)
			EntityParent s\flameLight,s\flamePoint,False
			MoveEntity s\flameLight,0,0,-1
			LightRange s\flameLight,0.1
		
		Case ENEMY_CLASS
			;Different visuals, settings etc.
			s\mesh = CreateCone()
			PositionEntity s\mesh,0,-570,0
			
			TurnEntity s\mesh,0,180,180 ;Necessary to setup the enemy behind the planet, and coming from behind.
			
			ScaleEntity s\mesh,8,8,12
			RotateMesh s\mesh,90,0,0
			EntityColor s\mesh,255,100,0
			EntityParent s\flamePoint,s\mesh,False
			MoveEntity s\flamePoint,0,0,-1
			
			s\flameLight = CreateLight(2)
			LightColor s\flameLight,255,200,-100
			EntityParent s\flameLight,s\flamePoint,False
			MoveEntity s\flameLight,0,0,-1
			LightRange s\flameLight,0.1
					
		Case NEUTRAL_CLASS
			;Different visuals, settings etc.
			
	End Select
	
	Return s
End Function


Function CreateFlame(entity,x#,y#,z#,life#,class%=PLAYER_CLASS)	
	f.flame = New flame
		f\obj = CreateSphere(10,entity)
		
		Select class
			Case PLAYER_CLASS
				f\R# = 0
				f\G# = 155
				f\B# = 255
			Case ENEMY_CLASS
				f\R# = 255
				f\G# = 180
				f\B# = 70
			Case NEUTRAL_CLASS
		End Select
		 
		EntityColor f\obj,f\R,f\G,f\B
		EntityFX f\obj,1
		EntityBlend f\obj,3
		f\scale# = Rand(1,4)
		f\alpha# = 1
		f\life = life#
	PositionEntity f\obj,x,y,z
	EntityParent f\obj,0
End Function


Function UpdateFlame()
	For f.flame = Each flame
		f\G = f\G - Rand(0,10)
		EntityColor f\obj,f\R,f\G,f\B
		f\alpha = f\alpha - .075
		ScaleEntity f\obj,f\scale,f\scale,f\scale
		EntityAlpha f\obj,f\alpha
		f\life = f\life - 1
		If f\life &lt; 0 Then
			FreeEntity f\obj
			Delete f
		End If
	Next
End Function


Function UpdateScene()
	UpdateShips()
   ;UpdateProps()
   ;[...]
   
	UpdateFlame()
End Function


Function UpdateShips()
	;Update each class of ship based on specific logic.
	For s.starShip = Each starShip
		Select s\class
			Case PLAYER_CLASS
				UpdatePlayer(s)
			Case ENEMY_CLASS
				UpdateEnemies(s)
			Case NEUTRAL_CLASS
				;UpdateNeutral(s)
		End Select
		
		MoveShip(s)
	Next
End Function


Function UpdatePlayer(p.starShip)	
	Local kLEFT = KeyDown(203)
	Local kRIGHT = KeyDown(205)
	Local kUP = KeyDown(200)
	Local kDOWN = KeyDown(208)
	If KeyHit(57) Then RoutineMode = 3 - RoutineMode ;Toggles between '1' and '2'.
	
	;Left, Right.
	p\vector\x = p\vector\x - (TURN_INCREMENT * kLEFT) + (TURN_INCREMENT * kRIGHT)
	If (kLEFT + kRIGHT &lt;&gt; 1) Then p\vector\x = 0 ;If pressing both keys or none at all, do not turn the ship.
	
	If p\vector\x &gt; 1 Then p\vector\x = 1
	If p\vector\x &lt; -1 Then p\vector\x = -1
		
	;Up, Down.
	p\vector\y = p\vector\y + (MOVE_INCREMENT * kUP) - (MOVE_INCREMENT * kDOWN) 
	
	p\vector\y = p\vector\y - MOVE_DECAY
	If p\vector\y &gt; 5 Then p\vector\y = 5
	If p\vector\y &lt; 0 Then p\vector\y = 0
	
	;Special FX.
	If kUP Then CreateFlame(p\flamePoint,Rnd(-.75,.75),0,0,Rnd(75,200),PLAYER_CLASS)
	LightRange(p\flameLight, (30*(p\vector\y / 5)^2 + 0.1) )
End Function


Function UpdateEnemies(e.starShip)
	;Floyd's TFormPoint suggestion!
	
	;Transform player's world position to enemy's local space, aligned with the enemy's local axes.
	TFormPoint EntityX(player\mesh,True),EntityY(player\mesh,True),EntityZ(player\mesh,True),0,e\mesh
	
	;Steer the enemy based on the player's X position in the enemy's space.
	If Abs(TFormedX()) &gt; 10.0 Then 
		e\vector\x = e\vector\x + Sgn(TFormedX())*TURN_INCREMENT
	Else 
		e\vector\x = TFormedX()/10.0 ;Smooth-out the steering the more the enemy faces the player.
	EndIf 
	
	If e\vector\x &gt; 1 Then e\vector\x = 1
	If e\vector\x &lt; -1 Then e\vector\x = -1
	
	;Move the enemy towards the player, but just so much as to reach out behind him (around 109 units distant).
	Local tempDistance# = EntityDistance(e\mesh, player\mesh)
	e\vector\y = e\vector\y + ((tempDistance &gt; 125)*MOVE_INCREMENT)
	
	e\vector\y = e\vector\y - MOVE_DECAY
	If e\vector\y &gt; 4 Then e\vector\y = 4 ;Note the enemy's speed is 4, instead of player's 5.
	If e\vector\y &lt; 0 Then e\vector\y = 0
	
	;Special FX.
	If tempDistance &gt; 125 Then CreateFlame(e\flamePoint,Rnd(-.75,.75),0,0,Rnd(75,200),ENEMY_CLASS)
	LightRange(e\flameLight, (30*(e\vector\y / 4)^2 + 0.1) )
End Function


Function MoveShip(s.starShip)
	performanceTime = MilliSecs()

	Select RoutineMode			
		Case PICK_ROUTINE
			For benchmark = 1 To BENCHMARK_PASSES
				TFormVector 0,-100,0,s\mesh,0
				LinePick (EntityX(s\mesh,True),EntityY(s\mesh,True),EntityZ(s\mesh,True),TFormedX(),TFormedY(),TFormedZ())
			Next 
				
			PositionEntity s\mesh,PickedX(),PickedY(),PickedZ()
			AlignToVector s\mesh,PickedNX(),PickedNY(),PickedNZ(),2			  
			
			MoveEntity s\mesh,0,20,s\vector\y
			
			If s\vector\y &gt; 0 Then ;If the ship is moving at all then allow turning.
				TFormVector s\vector\x * (s\vector\y / 5),0,s\vector\y,s\mesh,0 ;Get the motion vector in world coordinates. Note the use of the X ~ Z plane.
				AlignToVector s\mesh,TFormedX(),TFormedY(),TFormedZ(),3
			EndIf 			
			
			performanceTime = (MilliSecs() - performanceTime)
		
		Case VECTOR_ROUTINE
			For benchmark = 1 To BENCHMARK_PASSES
				TFormPoint s\vector\x,0,s\vector\y,s\mesh,0
				;Distance from the target direction to the center of the planet:
				Local pointLength# = Sqr(TFormedX()*TFormedX() + TFormedY()*TFormedY() + TFormedZ()*TFormedZ())
				pointLength = 570.0 / pointLength
			
				Local vx#,vy#,vz# 
				vx = TFormedX()*pointLength
				vy = TFormedY()*pointLength
				vz = TFormedZ()*pointLength
			Next
			
			MoveEntity s\mesh,0,0,s\vector\y
				
			If s\vector\y &gt; 0 Then
				AlignToVector s\mesh,(vx - EntityX(s\mesh,True)),(vy - EntityY(s\mesh,True)),(vz - EntityZ(s\mesh,True)),3,(s\vector\y / 5)			
			EndIf 
									
			performanceTime = (MilliSecs() - performanceTime)			
	End Select
End Function


Function Render2D()
	
	
	;Neat bit, showing indicator for the occluded player ship so you have some sense where you're at.
	Local tempX#,tempY#
	
	For s.starShip = Each starShip
		If EntityDistance(camera, s\mesh) &gt; 1000 Then
			CameraProject camera,EntityX(s\mesh,True),EntityY(s\mesh,True),EntityZ(s\mesh,True)
			Rect ProjectedX()-2,ProjectedY()-2,4,4,0
			tempX = ProjectedX() : tempY = ProjectedY()
			TFormPoint s\vector\x,0,s\vector\y,s\mesh,0
			CameraProject camera,TFormedX(),TFormedY(),TFormedZ()
			Line tempX,tempY,ProjectedX(),ProjectedY()
		EndIf
	Next 

	;Performance information.
	Select RoutineMode
		Case PICK_ROUTINE
			Text(0,8," LinePick performance: "+performanceTime+" ms")
		Case VECTOR_ROUTINE
			Text(0,8," Vector-math performance: "+performanceTime+" ms")
	End Select
	
	Local msg$ = "Press SPACE to change movement logic. "
	Text GraphicsWidth()-StringWidth(msg),8,msg
End Function</textarea> <br><br></td></tr></table><br>
<a name="1159063"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Aussie</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks again Kryzon.<br><br>I'm not at my computer at the moment so i am unable to see what you have done &amp; probably won’t get much of a chance until the weekend.<br><div class="quote"> <br>When you want to add more features to your program you notice how important modularity and structure are. Generic setup functions, generic types\classes, sensible frameworking. <br> <br></div><br>I agree completely. Whenever i get the chance to have a go at coding &amp; play with ideas it always starts off a bit mix &amp; match until i get things starting to work. Once i get things tested &amp; working i then go through &amp; start organising things into functions &amp; types. One thing i do need to do is start commenting my code more as i go, i have some bits &amp; pieces of code from  when i first started coding &amp; if i haven’t been able to code for a while it takes a fair bit time to catch up on what i was doing.<br><br>Thanks again. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
