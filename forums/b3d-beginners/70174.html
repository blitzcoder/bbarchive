<!DOCTYPE html><html lang="en" ><head ><title >ShapeEntity System</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >ShapeEntity System</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >ShapeEntity System</a><br><br>
<a name="785034"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >kochOn</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi, there!<br><br>I am currently working on a ShapeEntity System that will simplify (hopefully) the creation of vector games.<br><br>Here are the engine files (kline.bb, kShape.bb, glow.png, no example):<br><a href="http://www.kochonet.com/kSES/kSES.zip" target="_blank">http://www.kochonet.com/kSES/kSES.zip</a><br><br>This is an alpha version with lot of missing functions, surely a bunch of bugs and no help files. The best way to learn it is to download separate examples and post your questions here.<br><br>latest example showing you how it's easy to make a simple asteroids clone:<br><img src="http://www.kochonet.com/kSES/vectoroids_thumb.png"><br><br>The source:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Include "kShape.bb"

; FrameTiming Globals
;=========================================================================
Global fps
Global fps_count
Global fps_mil

; Initialisation
;=========================================================================
Graphics3D 640, 480, 32, 2
SetBuffer BackBuffer()

Global cam = CreateCamera()
Global dl = kDL_Create(cam, True, True, "glow.png")
kDL_Area(dl, 660, 500)

SeedRnd MilliSecs()

; Shapes Creation
;=========================================================================
Global shp_ship.kShape = kCreateShape() 
kBeginShape(shp_ship)
kShapeAdd(0, -10)
kShapeAdd(10, 10)
kShapeAdd(0, 0)
kShapeAdd(-10, 10)
kEndShape(True)

Global shp_grid.kShape = kCreateGridShape(0, 0, 640, 480, 10, 10)
Global shp_bullet.kShape = kCreatePixShape(0, 0)

Global shp_ast_big.kShape = kCreateCircleShape(0, 0, 40, 7)
Global shp_ast_medium.kShape = kCreateCircleShape(0, 0, 30, 6)
Global shp_ast_small.kShape = kCreateCircleShape(0, 0, 20, 5)

Global shp_planet.kShape = kCreateCircleShape(0, 0, 300, 32)

; ShapeEntities Creation and Settings
;=========================================================================
Global timer% ; timer = kSE_Create(null, dl) -&gt; kSE_LifeTime(timer, 150) -&gt; kSE_Exists(timer)
			  ; This way you can easily create and control timers.

Global ship = kSE_Create(shp_ship, dl)
kSE_WrapMode(ship, 3)
kSE_Thickness(ship, 1.8)
kSE_Position(ship, 320, 240)
kSE_SetFriction(ship, 0.995)
kSE_Alpha(ship, 0)

Global grid = kSE_Create(shp_grid, dl)
kSE_Color(grid, 0, 0, 255)
kSE_Thickness(grid, 0.35)

Global planet = kSE_Create(shp_planet, dl)
kSE_Position(planet, 0, 0)
kSE_Color(planet, 255, 128, 255)
kSE_Thickness(planet, 0.5)
kSE_RotSpeed(planet, 0.01)

; Tag Lists
;=========================================================================

Global taglist1 = kSE_CreateTagList()
Global taglist2 = kSE_CreateTagList()

; Main Loop
;=========================================================================
While Not KeyDown(1)
	kDL_Clear(dl)
	
	If KeyDown(203) Then
		kSE_Rotate(ship, kSE_GetAngle(ship) - 2.5)
	EndIf
	If KeyDown(205) Then
		kSE_Rotate(ship, kSE_GetAngle(ship) + 2.5)
	EndIf
	If KeyDown(200) Then
		kSE_PushByAngle(ship, kSE_GetAngle(ship), 0.03)
	EndIf
	If KeyHit(57) Then
		If kSE_Exists(ship) Then
			tmp = kSE_Create(shp_bullet, dl)
			kSE_MoveTo(tmp, ship)
			kSE_Color(tmp, 255, 0, 0)
			kSE_LifeTime(tmp, 50)
			kSE_Dir(tmp, kSE_GetAngle(ship))
			kSE_Speed(tmp, 8)
			kSE_MoveFront(tmp, 10)
			kSE_Thickness(tmp, 3)
			kSE_WrapMode(tmp, 3)
			kSE_SetTag(tmp, "BULLET")
		Else
			If kSE_Exists(timer) = False Then
				ship = kSE_Create(shp_ship, dl)
				kSE_WrapMode(ship, 3)
				kSE_Thickness(ship, 1.8)
				kSE_Position(ship, 320, 240)
				kSE_SetFriction(ship, 0.995)
				kSE_Alpha(ship, 0)
			EndIf
		EndIf
	EndIf
	
	kSE_FillTagList(taglist1, "ASTEROID")
	If kSE_TagListSize(taglist1) &lt;&gt; 0 Then
		kSE_FillTagList(taglist2, "BULLET")
	
		For n = 1 To kSE_TagListSize(taglist2)
			bullet_tmp = kSE_TagItem(taglist2, n)
			For o = 1 To kSE_TagListSize(taglist1)
				ast_tmp = kSE_TagItem(taglist1, o)
				If kSE_PointInShape(ast_tmp, kSE_GetPosX(bullet_tmp), kSE_GetPosY(bullet_tmp)) = True Then 	
					kSE_Free(bullet_tmp)
					Select kGetParam(kSE_GetTag$(ast_tmp))
						Case "BIG"
							kSE_Thickness(ast_tmp, 10)
							kSE_SubDivide(ast_tmp)
							kSE_TrailMode(ast_tmp, True)
							For i= 1 To 3
								tmp = kSE_Create(shp_ast_medium, dl)
								kSE_MoveTo(tmp, ast_tmp)
								kSE_Alpha(tmp, kSE_GetAlpha(ast_tmp))
								kSE_Dir(tmp, Rnd(360))
								kSE_Speed(tmp, Rnd(0.75, 1.25))
								kSE_RotSpeed(tmp, Rnd(0.75, 1.25))
								kSE_WrapMode(tmp, 3)
								kSE_Thickness(tmp, 3)
								kSE_Color(tmp, 255, 128, 0)
								kSE_SetTag(tmp, "ASTEROID(MED)")
							Next
							kSE_Explode(ast_tmp, 50, 20)	
						Case "MED"	
							kSE_Thickness(ast_tmp, 10)
							kSE_SubDivide(ast_tmp)
							kSE_TrailMode(ast_tmp, True)
							For i= 1 To 3
								tmp = kSE_Create(shp_ast_small, dl)
								kSE_MoveTo(tmp, ast_tmp)
								kSE_Alpha(tmp, kSE_GetAlpha(ast_tmp))
								kSE_Dir(tmp, Rnd(360))
								kSE_Speed(tmp, Rnd(1, 1.5))
								kSE_RotSpeed(tmp, Rnd(1, 1.5))
								kSE_WrapMode(tmp, 3)
								kSE_Thickness(tmp, 2.8)
								kSE_Color(tmp, 255, 0, 0)
								kSE_SetTag(tmp, "ASTEROID(SMALL)")
							Next
							kSE_Explode(ast_tmp, 50, 15)	
						Case "SMALL"
							kSE_Thickness(ast_tmp, 10)
							kSE_SubDivide(ast_tmp)
							kSE_TrailMode(ast_tmp, True)
							kSE_Explode(ast_tmp, 100, 5)
					End Select		
					Exit
				EndIf
			Next
		Next
	Else
		For n = 1 To 3
			tmp = kSE_Create(shp_ast_big, dl)
			kSE_Position(tmp, Rnd(640), Rnd(480))
			kSE_Alpha(tmp, 0)
			kSE_Dir(tmp, Rnd(360))
			kSE_Speed(tmp, Rnd(0.5, 1))
			kSE_RotSpeed(tmp, Rnd(0.5, 1))
			kSE_WrapMode(tmp, 3)
			kSE_Thickness(tmp, 3.2)
			kSE_Color(tmp, 255, 255, 0)
			kSE_SetTag(tmp, "ASTEROID(BIG)")
		Next
	EndIf
	
	kSE_FillTagList(taglist1, "ASTEROID")
	
	alpha# = kSE_GetAlpha(ship)
	If alpha# &lt; 1 Then
		alpha# = alpha# + 0.01
		kSE_Alpha(ship, alpha#)
	EndIf
	
	For n = 1 To kSE_TagListSize(taglist1)
		ast_tmp = kSE_TagItem(taglist1, n)
		alpha# = kSE_GetAlpha(ast_tmp)
		If alpha# &lt; 1 Then
			alpha# = alpha# + 0.01
			kSE_Alpha(ast_tmp, alpha#)
		Else
			If kSE_GetAlpha(ship) &gt;= 1 Then
				col = False
				If kSE_PointInShape(ast_tmp, kSE_GetPosX(ship), kSE_GetPosY(ship)) = True Then
					col = True
				ElseIf kSE_Intersects(ship, ast_tmp) = True Then
					col = True
				EndIf
				If col = True Then
					kSE_Thickness(ship, 5)
					kSE_SubDivide(ship)
					kSE_Explode(ship, 200, 1)
					timer = kSE_Create(Null, dl)
					kSE_LifeTime(timer, 150)
				EndIf
			EndIf
		EndIf
	Next
	
	kSE_Update()
	lr = kSE_Render()
	RenderWorld
	
	; Info Output
	;---------------------------------------------------------------------
	FpsUpdate()
	Text 0, 0, "FPS: " + fps
	Text 0, 15, "Lines Rendered: " + lr
	Flip
Wend

; End of Program
;=========================================================================
End

; FrameTiming Function
;=========================================================================
Function FpsUpdate()
	If MilliSecs() &gt; fps_mil + 1000 Then
		fps_mil = MilliSecs()
		fps = fps_count
		fps_count = 0
	Else
		fps_count = fps_count + 1
	EndIf
End Function
</textarea><br><br>Link to the demo - exe/main source (library not yet included):<br><a href="http://www.kochonet.com/kSES/vectoroids_example.zip" target="_blank">http://www.kochonet.com/kSES/vectoroids_example.zip</a><br><br>Previous examples: <br>- <a href="http://www.kochonet.com/kSES/trails_example.zip" target="_blank">http://www.kochonet.com/kSES/trails_example.zip</a><br>- <a href="http://www.kochonet.com/kSES/usershape_example.zip" target="_blank">http://www.kochonet.com/kSES/usershape_example.zip</a><br>- <a href="http://www.kochonet.com/kSES/basic_example.zip" target="_blank">http://www.kochonet.com/kSES/basic_example.zip</a><br><br>Working on more examples... <br><br></td></tr></table><br>
<a name="785045"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >IPete2</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> wow that is really lovely!<br><br>IPete2. <br><br></td></tr></table><br>
<a name="785051"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >puki</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> This looks very exciting.<br><br>However, I am missing 'kShape.bb' - it's not in the download. <br><br></td></tr></table><br>
<a name="785058"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >kochOn</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks guys!<br><br>kShape.bb and kLine.bb which is part of the other are not included for the moment as I am still working on them and they are missing a lot of functions.<br><br>I do not plan to make money with my system but I need encouragements and good feedbacks... <br><br></td></tr></table><br>
<a name="785059"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >puki</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Looks great to me.<br><br>Finish it and hand it over <br><br></td></tr></table><br>
<a name="785363"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mortiis</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Lots of eyecandy :) good work! <br><br></td></tr></table><br>
<a name="787182"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >kochOn</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks mates!<br><br>What do you think about the vectoroids example?<br>This is not yet a complete game, but it can show you some good stuffs from my ShapeEntity System.<br><br>Take a look at the code to understand how it works! <br><br></td></tr></table><br>
<a name="787296"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> looks cool, but in the asteroids sample I could barely see the shots... <br><br></td></tr></table><br>
<a name="787322"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >kochOn</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is due to the color I've used for the bullets!<br>I have uploaded the example with white shots, so think they will be visible now ;) <br><br></td></tr></table><br>
<a name="787326"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >kochOn</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi all!<br><br>I've updated the first post with a link to the engine.<br>This is an alpha version without any help files.<br><br>You need to have kLine.bb, kShape.bb and glow.png in the same folder as your main source.<br><br>A basic program that do nothing else than showing a centered ShapeEntity:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Include "kShape.bb"

; Main Init
Graphics3D 640, 480, 32, 2
SetBuffer BackBuffer()
cam = CreateCamera()

;-&gt; kDL_Create(camera to assign, blended: true/false, smoothed: true/false, glowing texture)
dl = kDL_Create(cam, True, True, "glow.png") ; Create a DrawingLayer

;-&gt; kCreateCircleShape(x#, y#, radius#, number of segments, "POINT" or "SEGMENT" based)
shape_circle.kShape = kCreateCircleShape(0, 0, 50, 10, "POINT") 

;-&gt; Create a ShapeEntity(based on an existing Shape) on the specified DrawingLayer
circle = kSE_Create(shape_circle, dl)

kSE_Position(circle, 320, 240); Position the ShapeEntity in the center of the screen

While Not KeyDown(1)
	kDL_Clear(dl); Clear the DrawingLayer
	kSE_Render(); Render all ShapeEntities
	
	RenderWorld
	Flip
Wend
</textarea><br><br>Please try and ask questions, cause I need to know what I could add to make a better system.<br><br>I haven't so much time atm to make a simple help file so if someone can help! NicolasATEK@... <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
