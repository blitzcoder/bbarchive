<!DOCTYPE html><html lang="en" ><head ><title >Space Harrier - Input freaking out?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Space Harrier - Input freaking out?</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Space Harrier - Input freaking out?</a><br><br>
<a name="1187089"></a>

<a name="1187090"></a>

<a name="1187091"></a>

<a name="1187092"></a>

<a name="1187093"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Imperium</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Working on a little project to replicate the classic Space Harrier. I was moving along fine until I noticed the controls are randomly freezing. I'm not sure why this is happening, could someone help me add some tweening to separate the input updates from the actual fps? Take a look what I have so far. If you try out the code for yourself you will see that it works fine then will lock up or the player will drop bellow the maximum movement range specified.<br><br><br>Main.bb<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Include "globals.bb"

HidePointer	
Graphics3D 1024, 768, 0, 1			
SetBuffer BackBuffer()				

light=CreateLight()
RotateEntity light,90,0,0

camera=CreateCamera()
CameraViewport camera,0,100,1024,500 ;357
CameraRange camera,1,800
PositionEntity camera,0,1,0

; SET THE FONT
font=LoadFont( "Terminal",14 ) ;Lucida Console
SetFont font
;---------------------------------------------------------------
; SETUP THE TEXTURES
;logo = LoadTexture("TBS_hud_concept.png",1)

planetsurface_tex=LoadTexture( "art/grass2.png",9)   ;previously blue_surface2.png
ScaleTexture planetsurface_tex,3,3 ;70,70 default   previously 20,20
oldsky_tex=LoadTexture( "art/purple.png",9)
harrierguy=LoadTexture( "art/guy.png",9)
;---------------------------------------------------------------
; LIMIT THE FPS (3)
Const Lmt_FPS=60				 ; Target frames per second
Const Lmt_Frametime=1000/Lmt_FPS ; Local originally
Local Lmt_Frame_begin
;---------------------------------------------------------------
;FlushMouse
;Title = LoadImage("title.png")
;If Title = 0 Then RuntimeError "Asset is missing!"
;DrawImage Title,0,60 ; Draw the image!
;Flip ; flip the image into view and clear the back buffer
;Delay 2000
;FreeImage Title   ;free image from memory
;Flip
;Gosub Start
;---------------------------------------------------------------
; OBJECTS

Local skycube = CreateCube(camera)
MoveEntity skycube,0,5,50
ScaleEntity skycube, 40,10,15
EntityTexture skycube,oldsky_tex,0,0

ground = CreatePlane(2)
PositionEntity ground,0,0,0
EntityTexture ground,planetsurface_tex,0,0


test = CreateCube()
TranslateEntity test,0,0,200,True

gfxPlayer = LoadSprite ( "art/guy.png")
MoveEntity gfxPlayer,0,1.5,10
;---------------------------------------------------------------


While Not KeyHit(1)
	
	Lmt_Frame_begin=MilliSecs() ; LIMIT THE FPS (*)
	;-----------------------------------------------------------
	
	; CONTROLS
	If KeyDown( 203 )=True And Cango_right = 0 Then X = X + 1 TranslateEntity gfxPlayer,-.1,0,0	;left
	If KeyDown( 205 )=True And Cango_right = 0 Then X = X - 1 TranslateEntity gfxPlayer,.1,0,0	;right
	If KeyDown( 208 )=True And Cango_down = 0 Then Y = Y - 1 TranslateEntity gfxPlayer,0,-.1,0	;down
	If KeyDown( 200 )=True And Cango_up = 0 Then Y = Y + 1 TranslateEntity gfxPlayer,0,.1,0	;up
	;If KeyDown( 57 ) = ;Spacebar to fire weapon
	
	;	=              Equals
	;	&lt;              is less than
	;	&gt;              is greater than
	;	&lt;=             is less than Or equal To
	;	&gt;=             is greater than Or equal To
	;	&lt;&gt;             is unequal To
	
	
	If X = Max_Left
		Cango_left = Cango_left + 1		;When its 1 you can't move
		X = 85
		
	ElseIf X = Max_Right
			Cango_right= Cango_right + 1	;When its 1 you can't move
			X = -85
		
		ElseIf Y = Max_up
			Cango_up= Cango_up + 1			;When its 1 you can't move
			Y = 35
		
		Else If Y = Max_down
			Cango_down= Cango_down + 1  	;When its 1 you can't move
			Y = -10
		
		
	Else
		Cango_left = 0
		Cango_right = 0
		Cango_up = 0
		Cango_down = 0
		
	End If
	;---------------------------------------------------------------
	
	;Move World Forward
	TranslateEntity camera,0,0,.20
	TranslateEntity gfxPlayer,0,0,.20	
	
	;-----------------------------------------------------------
	UpdateWorld 1					;figures out collisions
	RenderWorld	0					;draws the 3d scene -previously 1
	;-----------------------------------------------------------
	; FPS CONTROL AND DISPLAY FRAMES CODE - LEAVE THIS ALONE
	; FPS LIMITER (*)
	If (MilliSecs()-Lmt_Frame_begin)&lt;Lmt_Frametime
		
		Delay Lmt_Frametime-(MilliSecs()-Lmt_Frame_begin)
		
	EndIf
	
	;DISPLAY FPS COUNTER
	If MilliSecs()-settime&gt;1000
		GetFPS=setfps : setfps=0 : settime=MilliSecs()
	Else
		setfps=setfps+1
	EndIf
	
	Text 10,100,"FPS = "+GetFPS
	Text 10,110, "X: " +X
	Text 10,120, "Y: " +Y
	Text 10, 130, "Player shot" +Player_shot
	;-----------------------------------------------------------
	Flip
Wend

End

; FUNCTIONS GO HERE

;~IDEal Editor Parameters:
;~C#Blitz3D</textarea><br><br>Globals.bb<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">; Globals

Global CurrentLocation = 0
Global PlayerLife = 0
Const Max_Playerlife= 100

Const Max_Right=-85
Const Max_Left=85
Const Max_Up=35
Const Max_Down=-10

Global Y = 0
Global X = 0

Global Cango_left = 0
Global Cango_right = 0
Global Cango_up = 0
Global Cango_down = 0

Global Player_shot = 0

;Global Lazer = LoadSound("Lasergun.wav")       ;Lazer Sound
Global PlayerLives=3

Global ShotTime=0      ;Shot Timer
Global MaxTime=10      ;Max Shot Timer 

Const PLAYERSHOT_TYPE =5

;~IDEal Editor Parameters:
;~C#Blitz3D</textarea><br><br><img src="http://s10.postimg.org/sn59h3co5/screen1.jpg"><br><img src="http://s23.postimg.org/hb928pgrb/grass2.png"><br><img src="http://s22.postimg.org/r5se9kkr1/guy.jpg"><br><img src="http://s9.postimg.org/8nrqbv82z/purple.png"> <br><br></td></tr></table><br>
<a name="1187100"></a>

<a name="1187112"></a>

<a name="1187113"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> First thing: your "FPS limiter" code (that bit where it says "leave this alone") is broken and doesn't do what you think. It's also the root of the problem: on my machine at least, removing that gets rid of all pauses. Manual calls to Delay do not play nicely with Flip unless you turn off Flip's own vsync, and will introduce random jitters as the two pause mechanisms conflict with each other (you can of course make the boolean condition to flip, or the existence of VWait, something adjustable as an option for those machines where this isn't true).<br><br><br>Here's an example of separating the render and update loops to run on different timers. It's a loose translation of the code from the famous Gaffer article <a href="http://gafferongames.com/game-physics/fix-your-timestep/" target="_blank">Fix Your Timestep!</a>. This code is copied directly from a real released project (interesting bits removed and replaced with ;!COMMENTS, but the timer code is unmodified from something that works):<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Local renderTime = Floor(1000.0 / Settings\DrawRate), updateTime# = 1000.0 / Settings\UpdateRate
Local cTime = MilliSecs(), accum#
Local ShowDebugInfo = False, FPScount, FPStime, FPSframes		;Debug info

Global RenderTween# = 1.0



While Not KeyDown(1)		;Outermost main loop - rendering and all secondary functions
	accum = accum + (MilliSecs() - cTime)		;Accumulator for update loop
	cTime = MilliSecs()		;"Official" start of loop
	
	;Update loop (for render tweening) - all movement, input and update happens within this one
	While accum &gt;= updateTime
		;! CAPTURE STUFF
		ShowEntity WHATEVER		;! all "scene roots" need to be Shown here to be Captured
		CaptureWorld
		HideEntity WHATEVER
		
		;! UPDATE STUFF (e.g. input)
		UpdateTouchMouse	;Detect mouse input events
		GUI_Update		;Main GUI update function
		
		;! VARIOUS OTHER UPDATE FUNCTIONS
		
		accum = accum - updateTime
	Wend
	RenderTween = accum / updateTime
	
	;! SETUP DRAW STUFF
	
	;! DRAW STUFF
	;Render world camera view
	RenderWorld RenderTween
	
	;! DRAW OTHER STUFF (e.g. 2D)
	
	If Settings\LimiterOn Then Delay (renderTime - (MilliSecs() - cTime)) - 1		;Free spare CPU time
	If Settings\VSync Then VWait
	Flip False
Wend
</textarea><br><br>The organisation is mainly to separate the two cycles (for instance, it works very happily drawing at 60FPS while updating at 100FPS), but it also provides a tween value for rendering that can be passed to RenderWorld (and any custom draw functions that use tweening). Notice also that to actually apply tweening to 3D entities, you need to Capture them with CaptureWorld in the update loop (which in turn requires them to not be hidden); tweening is between their captured position and their current position, not anything to do with where they were at the last RenderWorld call.<br><br>There is no requirement to actually tween your renders, but because the update section works by either skipping iterations or adding extra iterations within the "owning" render loop, not tweening may cause the movement to appear jerky (the movement will not actually <i>be</i> jerky internally - it justs risks <i>looking</i> jerky due to mismatches between loops). At reasonably high framerates (60+), this is not usually a problem in practice; nor is it a problem if the updates and renders use the same timer (so you can set updates to use the same FPS as the most common draw rate, e.g. 60, and then skip the tweening on the majority of setups!). Tweening is also more annoying than it sounds if you have e.g. moving 2D elements, because you have to do it manually.<br><br>To apply this to your code, just dump everything up to the UpdateWorld command in the update loop at ;!UPDATE STUFF, and everything below that (minus the delay code) into the render loop at ;!DRAW STUFF. <br><br></td></tr></table><br>
<a name="1187119"></a>

<a name="1187864"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Imperium</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I would have never suspected my FPS limiter. Considering it has worked fine up until now but I'm glad you discovered my mistake. Here is my attempt at applying the posted code. I'm having some trouble following it. The showentity "WHATEVER" has me most confused.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Include "globals.bb"

HidePointer	
Graphics3D 1024, 768, 0, 1			
SetBuffer BackBuffer()	

Local renderTime = Floor(1000.0 / Settings\DrawRate), updateTime# = 1000.0 / Settings\UpdateRate
Local cTime = MilliSecs(), accum#
Local ShowDebugInfo = False, FPScount, FPStime, FPSframes		;Debug info

light=CreateLight()
RotateEntity light,90,0,0

camera=CreateCamera()
CameraViewport camera,0,100,1024,500 ;357
CameraRange camera,1,800
PositionEntity camera,0,1,0

; SET THE FONT
font=LoadFont( "Terminal",14 ) ;Lucida Console
SetFont font
;---------------------------------------------------------------
; SETUP THE TEXTURES
planetsurface_tex=LoadTexture( "art/grass2.png",9)   ;previously blue_surface2.png
ScaleTexture planetsurface_tex,3,3 ;70,70 default   previously 20,20
oldsky_tex=LoadTexture( "art/purple.png",9)
harrierguy=LoadTexture( "art/guy.png",9)
; OBJECTS

Local skycube = CreateCube(camera)
MoveEntity skycube,0,5,50
ScaleEntity skycube, 40,10,15
EntityTexture skycube,oldsky_tex,0,0

ground = CreatePlane(2)
PositionEntity ground,0,0,0
EntityTexture ground,planetsurface_tex,0,0

test = CreateCube()
TranslateEntity test,0,0,200,True

gfxPlayer = LoadSprite ( "art/guy.png")
MoveEntity gfxPlayer,0,1.5,10
;---------------------------------------------------------------

Global RenderTween# = 1.0

While Not KeyDown(1)		;Outermost main loop - rendering and all secondary functions
	accum = accum + (MilliSecs() - cTime)		;Accumulator for update loop
	cTime = MilliSecs()		;"Official" start of loop
	
	;Update loop (for render tweening) - all movement, input and update happens within this one
	While accum &gt;= updateTime
		;! CAPTURE STUFF
		ShowEntity WHATEVER		;! all "scene roots" need to be Shown here to be Captured
		CaptureWorld
		HideEntity WHATEVER
		
		;! UPDATE STUFF (e.g. input)
		UpdateTouchMouse	;Detect mouse input events
		GUI_Update		;Main GUI update function
		
			; CONTROLS
		If KeyDown( 203 )=True And Cango_right = 0 Then X = X + 1 TranslateEntity gfxPlayer,-.1,0,0	;left
		If KeyDown( 205 )=True And Cango_right = 0 Then X = X - 1 TranslateEntity gfxPlayer,.1,0,0	;right
		If KeyDown( 208 )=True And Cango_down = 0 Then Y = Y - 1 TranslateEntity gfxPlayer,0,-.1,0	;down
		If KeyDown( 200 )=True And Cango_up = 0 Then Y = Y + 1 TranslateEntity gfxPlayer,0,.1,0	;up
		;If KeyDown( 57 ) = ;Spacebar to fire weapon
		
		
		If X = Max_Left
			Cango_left = Cango_left + 1		;When its 1 you can't move
			X = 85
			
		ElseIf X = Max_Right
			Cango_right= Cango_right + 1	;When its 1 you can't move
			X = -85
			
		ElseIf Y = Max_up
			Cango_up= Cango_up + 1			;When its 1 you can't move
			Y = 35
			
		Else If Y = Max_down
			Cango_down= Cango_down + 1  	;When its 1 you can't move
			Y = -10
			
			
		Else
			Cango_left = 0
			Cango_right = 0
			Cango_up = 0
			Cango_down = 0
			
		End If
		;---------------------------------------------------------------
		
		;Move World Forward
		TranslateEntity camera,0,0,.20
		TranslateEntity gfxPlayer,0,0,.20
		
		;---------------------------------------------------------------
		;DISPLAY FPS COUNTER
		;If MilliSecs()-settime&gt;1000
		;GetFPS=setfps : setfps=0 : settime=MilliSecs()
		;Else
		;	setfps=setfps+1
		;EndIf
		
		;Text 10,100,"FPS = "+GetFPS
		;Text 10,110, "X: " +X
		;Text 10,120, "Y: " +Y
		;Text 10, 130, "Player shot" +Player_shot
		;---------------------------------------------------------------
		
		;! VARIOUS OTHER UPDATE FUNCTIONS
		
		accum = accum - updateTime
	Wend
	RenderTween = accum / updateTime
	
	;! SETUP DRAW STUFF
	
	;! DRAW STUFF
	;Render world camera view
	RenderWorld RenderTween
	
	;! DRAW OTHER STUFF (e.g. 2D)
	
	If Settings\LimiterOn Then Delay (renderTime - (MilliSecs() - cTime)) - 1		;Free spare CPU time
	If Settings\VSync Then VWait
	Flip False
Wend
End


;~IDEal Editor Parameters:
;~C#Blitz3D</textarea><br><br>Do you accept donations Yasha? Most often you're the first person to rescue me from my code issues. I really appreciate the help. :) <br><br></td></tr></table><br>
<a name="1187120"></a>

<a name="1187121"></a>

<a name="1187122"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> OK, working down from the top...<br><br>-- the strange expressions 'Settings\DrawRate' and similar are assuming that you have a custom type object containing program settings. You can replace these with constants or any other numerical expression; '60' and '50' might work well for the draw rate and update rate, for example.<br><br>-- the line 'Local ShowDebugInfo = ...' is irrelevant; I overlooked it when deleting stuff. You have your own FPS measurement code that does the same thing.<br><br>-- CaptureWorld, like Render- and UpdateWorld, only deals in entities that are <i>not hidden</i>. This means that if an entity is un-hidden during the ensuing update loop, it may have the wrong old location still Captured for it when the loop ends; therefore, all entities that might be relevant should be Shown before CaptureWorld, and then set back to hidden again if necessary. In practice this may not actually be very relevant for you (the program this was taken from did a lot of showing and hiding based on events in the update loop, so it needed this step).<br><br>-- the 'WHATEVER' in capitals was just meant to represent that you should apply this to entities that need it as relevant. Probably best to just delete these two lines for now.<br><br>-- you ...don't need my update functions ('UpdateTouchMouse'?) in your program - they just show where to put your control-update functions.<br><br>-- if you use animation or collision, you need to use UpdateWorld (conversely, programs that don't use animation or collision - the things that need to be updated in the background of a frame - don't need UpdateWorld). If you use it, it should be the last thing before the end of the update loop (or at least, alongside any other related functions; it should be the last thing to operate on 3D entities). UpdateWorld doesn't need a parameter, as the parameter is for delta-timing, which is roughly the opposite of render tweening (fixed-speed render loop and a variable-speed update loop; we're doing it the other way round).<br><br>-- the FPS-counter and text display count as "draw stuff" (technically the counter part is updating, but it's doing it based on the draw loop), so they should go in the 'DRAW STUFF' area.<br><br>-- as with the update rates, the flags for vsync and delay were represented as part of a settings object. Different setups seem to respond differently to vsync so it's always good to make it optional.<br><br>-- I can't remember offhand why I was subtracting 1 from the Delay value. Much smoother without that (if you don't delay accurately, you end up needing VWait or Flip True, and then they tend to conflict and stutter).<br><br>That gives this:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Include "globals.bb"

HidePointer	
Graphics3D 1024, 768, 0, 1
SetBuffer BackBuffer()	

Local renderTime = Floor(1000.0 / 60), updateTime# = 1000.0 / 50
Local cTime = MilliSecs(), accum#


light=CreateLight()
RotateEntity light,90,0,0

camera=CreateCamera()
CameraViewport camera,0,100,1024,500 ;357
CameraRange camera,1,800
PositionEntity camera,0,1,0

; SET THE FONT
font=LoadFont( "Terminal",14 ) ;Lucida Console
SetFont font
;---------------------------------------------------------------
; SETUP THE TEXTURES
planetsurface_tex=LoadTexture( "art/grass2.png",9)   ;previously blue_surface2.png
ScaleTexture planetsurface_tex,3,3 ;70,70 default   previously 20,20
oldsky_tex=LoadTexture( "art/purple.png",9)
harrierguy=LoadTexture( "art/guy.png",9)
; OBJECTS

Local skycube = CreateCube(camera)
MoveEntity skycube,0,5,50
ScaleEntity skycube, 40,10,15
EntityTexture skycube,oldsky_tex,0,0

ground = CreatePlane(2)
PositionEntity ground,0,0,0
EntityTexture ground,planetsurface_tex,0,0

test = CreateCube()
TranslateEntity test,0,0,200,True

gfxPlayer = LoadSprite ( "art/guy.png")
MoveEntity gfxPlayer,0,1.5,10
;---------------------------------------------------------------

Global RenderTween# = 1.0
Local DoVsync = False, LimiterOn = True

While Not KeyDown(1)		;Outermost main loop - rendering and all secondary functions
	accum = accum + (MilliSecs() - cTime)		;Accumulator for update loop
	cTime = MilliSecs()		;"Official" start of loop
	
	;Update loop (for render tweening) - all movement, input and update happens within this one
	While accum &gt;= updateTime
		;! CAPTURE STUFF
		CaptureWorld
		
		;! UPDATE STUFF (e.g. input)
		
		; CONTROLS
		If KeyDown( 203 )=True And Cango_right = 0 Then X = X + 1 TranslateEntity gfxPlayer,-.1,0,0	;left
		If KeyDown( 205 )=True And Cango_right = 0 Then X = X - 1 TranslateEntity gfxPlayer,.1,0,0	;right
		If KeyDown( 208 )=True And Cango_down = 0 Then Y = Y - 1 TranslateEntity gfxPlayer,0,-.1,0	;down
		If KeyDown( 200 )=True And Cango_up = 0 Then Y = Y + 1 TranslateEntity gfxPlayer,0,.1,0	;up
		;If KeyDown( 57 ) = ;Spacebar to fire weapon
		
		
		If X = Max_Left
			Cango_left = Cango_left + 1		;When its 1 you can't move
			X = 85
			
		ElseIf X = Max_Right
			Cango_right= Cango_right + 1	;When its 1 you can't move
			X = -85
			
		ElseIf Y = Max_Up
			Cango_up= Cango_up + 1			;When its 1 you can't move
			Y = 35
			
		Else If Y = Max_Down
			Cango_down= Cango_down + 1  	;When its 1 you can't move
			Y = -10
			
			
		Else
			Cango_left = 0
			Cango_right = 0
			Cango_up = 0
			Cango_down = 0
			
		End If
		;---------------------------------------------------------------
		
		;Move World Forward
		TranslateEntity camera,0,0,.20
		TranslateEntity gfxPlayer,0,0,.20
		
		;! VARIOUS OTHER UPDATE FUNCTIONS
		UpdateWorld		;!If necessary
		
		accum = accum - updateTime
	Wend
	RenderTween = accum / updateTime
	
	;! SETUP DRAW STUFF
	
	;! DRAW STUFF
	;Render world camera view
	RenderWorld RenderTween
	
	;! DRAW OTHER STUFF (e.g. 2D)
	
	;---------------------------------------------------------------
	;DISPLAY FPS COUNTER
	If MilliSecs()-settime&gt;1000
		GetFPS=setfps : setfps=0 : settime=MilliSecs()
	Else
		setfps=setfps+1
	EndIf
	
	Text 10,100,"FPS = "+GetFPS
	Text 10,110, "X: " +X
	Text 10,120, "Y: " +Y
	Text 10, 130, "Player shot" +Player_shot
	;---------------------------------------------------------------
	
	
	If LimiterOn Then Delay (renderTime - (MilliSecs() - cTime)); - 1		;Free spare CPU time
	If DoVsync Then VWait
	Flip False
Wend
End
</textarea><br><br><br>Also... 'Yue'? (and.... no thanks. Besides, very little of this is my ideas.) <br><br></td></tr></table><br>
<a name="1187199"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yue</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> oO?? Yue i confused. xD <br><br></td></tr></table><br>
<a name="1187865"></a>

<a name="1187866"></a>

<a name="1187868"></a>

<a name="1187869"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Imperium</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well that was embarrassing. Next time I should pay closer attention. Either way your help is greatly appreciated. :) <br><br></td></tr></table><br>
<a name="1188647"></a>

<a name="1188649"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Imperium</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I hate to bother with this again but the input keys are still freezing. If I hit up,right, and down in quick session or hold them down the main character get's stuck.<br><br>edit: found my goof in the keyboard inputs. I had a keyboard left set for keyboard right and a - integer where a positive should have been. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
