<!DOCTYPE html><html lang="en" ><head ><title >Help mark/correct/optimize/simplify my code</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Help mark/correct/optimize/simplify my code</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=5" >Blitz3D Beginners Area</a>/<a href="#bottom" >Help mark/correct/optimize/simplify my code</a><br><br>
<a name="315483"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RXArt</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> hi again everyone. I hope I've not become too annoying. Thanks to some of your help, I've completed my first game in Blitz :: Pong!! I've tried to code in the most straight forward and simple structure (KISS). But I believe my code can be further optimized and simplify. I hope that you can please help "mark" my code, and perhaps give me some tips on improving it. <br><br>I know I should have used more functions, but it was pretty impossible to do so, because somehow, I can't get my type variables to work in a function. Anyway, here's the code:<br><br><pre class=code>
AppTitle "-= My Pong =-"
Global screenx	= 640
Global screeny	= 480
Global upkey 	= 200
Global leftkey 	= 203
Global rightkey   = 205
Global downkey 	= 208
Global batspd	= 6

Global MyScore	= 0
Global OpScore    = 0

SeedRnd MilliSecs()

Graphics screenx,screeny,16,2

;Entity type
Type entityinfo
	Field x
	Field y
	Field width
	Field height
	Field speedx#
	Field speedy#
	Field directionx
	Field directiony
End Type

;Create Mybat instance
Mybat.entityinfo 	= New entityinfo
Mybat\x 			= 320
Mybat\y 			= screeny - 30
Mybat\width			= 80
Mybat\height		= 15

;Create Opbat instance
Opbat.entityinfo 	      = New entityinfo
Opbat\x 			= 320
Opbat\y 			= 30
Opbat\width			= 80
Opbat\height		= 15

;Create Ball instance
Ball.entityinfo 	            = New entityinfo
Ball\x				= 320
Ball\y				= 240
Ball\width			      = 15
Ball\height		      	= 15
Ball\speedx#	      	= Rnd(10)
Ball\speedy#		      = 6 ;Rnd(10)
Ball\directionx		      = 1
Ball\directiony		      = 1

;Create bat and ball image
gfxBat 	= CreateImage(Mybat\width,Mybat\height)
gfxBall	= CreateImage(ball\width,ball\height)

;Draw bat and ball image
SetBuffer ImageBuffer(gfxBat)
	Rect 0,0,mybat\width,mybat\height,1

SetBuffer ImageBuffer(gfxBall)
	Oval 0,0,ball\width,ball\height,1

gfxOpBat = CopyImage(gfxBat)

;Center the images
MidHandle gfxBat
MidHandle gfxOpBat
MidHandle gfxBall

;Switch back to back buffer
SetBuffer BackBuffer()

;Game loops starts here
;================================
While Not KeyHit(1)
;================================

	;Clear screen
	Cls

	;Show the score
	Text 2,screeny-15, "Your Score       : " + MyScore
	Text 2,0,          "Computer's Score : " + OpScore
	AppTitle "-= My Pong =-     You : " + MyScore + "         Comp : " + OpScore

	;Draw my bat onto screen
	DrawImage gfxbat,mybat\x,mybat\y
	DrawImage gfxOpBat,Opbat\x,Opbat\y	

	;Calculate ball location.
	;Keep ball in screen
	If ball\x &lt;(ball\width)/2+5 Or ball\x &gt; screenx-(ball\width)/2-5 Then 
		ball\speedx# = ball\speedx# * - ball\directionx
	EndIf
	If ball\y &lt;(ball\height)/2 Then
		MyScore = MyScore + 1		;I Scored!!
		ball\y = 240				;Reset ball to center-y
		ball\speedx# = Rnd(2)		;ball move straighter
		Delay 500
	ElseIf ball\y &gt; screeny-(ball\height)/2 Then 
		OpScore = OpScore + 1		;Opponent Scored!!
		ball\y = 240				;Reset ball to center-y
		ball\speedx# = Rnd(2)		;ball move straighter
		Delay 500	
	EndIf
	
	ball\x = (ball\x + ball\speedx#)
	ball\y = (ball\y + ball\speedy#)
	
	;Draw Ball to screen
	DrawImage gfxball,ball\x,ball\y 

	;Collision with my or opponent's bat
	If ImagesCollide (gfxBat,Mybat\x,Mybat\y,0,gfxBall,Ball\x,Ball\y,0) Then
		ball\speedx# = Rnd(10)
		ball\speedy# = ball\speedy# * - ball\directiony
		ball\speedx# = ball\speedx# * - ball\directionx
	EndIf
	
	If ImagesCollide (gfxOpBat,Opbat\x,Opbat\y,0,gfxBall,Ball\x,Ball\y,0) Then
		ball\speedx# = Rnd(10)
		ball\speedy# = ball\speedy# * - ball\directiony
		ball\speedx# = ball\speedx# * - ball\directionx
	EndIf

	
	;Simple Opponent AI Movement
	If ball\x &lt; OpBat\x Then 
		OpBat\x = MoveBatLeft(OpBat\x, OpBat\width)
	EndIf
	If ball\x &gt; OpBat\x Then 
		OpBat\x = MoveBatRight(OpBat\x, OpBat\width)
	EndIf
	
	;My Bat Movement
	If leftkey()=1 Then 
		Mybat\x = movebatleft(Mybat\x, Mybat\width)
	EndIf
	
	If rightkey()=1 Then 
		Mybat\x = movebatright(Mybat\x, Mybat\width)
	EndIf
	Flip	
Wend

;Free images
FreeImage gfxBat
FreeImage gfxOpBat
FreeImage gfxBall
Cls
WaitKey()
End

;Function to move the bat left
Function MoveBatLeft(batx, batwidth)
	Local old_x = batx
	If batx &gt; (batwidth/2) Then	
		batx = batx - batspd
		Return batx
	Else
		Return old_x
	EndIf
End Function

;Function to move the bat right
Function MoveBatRight(batx, batwidth)
	Local old_x = batx
	If batx &lt; screenx-(batwidth/2) Then
		batx = batx + batspd
		Return batx
	Else
		Return old_x
	EndIf
End Function

;Key functions
;Leftkey
Function LeftKey()
	If KeyDown(leftkey) = 1 Then 
		Return 1
	Else
		Return 0
	EndIf
End Function

;rightkey
Function RightKey()
	If KeyDown(rightkey) = 1 Then 
		Return 1
	Else
		Return 0
	EndIf
End Function

;Upkey
Function UpKey()
	If KeyDown(upkey) = 1 Then 
		Return 1
	Else
		Return 0
	EndIf
End Function

;Downkey
Function DownKey()
	If KeyDown(Downkey) = 1 Then 
		Return 1
	Else
		Return 0
	EndIf
End Function
</pre><br><br>Cheers, and thanks again! <br><br></td></tr></table><br>
<a name="315488"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Curtastic</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> take apptitle out of the loop <br><br></td></tr></table><br>
<a name="315497"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey man. The reason you types aren't working in function ( it got me too ;) ) is because they aren't declared as global. The actual pointer needs to be declared global. So:<br><br><pre class=code>
Mybat.entityinfo 	= New entityinfo
</pre><br><br>Turns into:<br><br><pre class=code>
Global Mybat.entityinfo 	= New entityinfo
</pre><br><br>Try that :) <br><br></td></tr></table><br>
<a name="315521"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RXArt</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Geez, thanks Ross! I've actually resorted to using gosub and labels inorder to make my codes simpler to read. But I'll definitely give your suggestion a go. :)<br><br>And Coorae, I guess I'll leave the apptitle in for a while longer (It kinda reminds me of Game Maker), but will definitely take it out if there are enough ppl to whack me straight in the head for leaving it in. :)<br><br>cheers!! I'm currently trying to work on a snake game. Man, there's so much for me to catch up!! <br><br></td></tr></table><br>
<a name="315603"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >WolRon</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Remove these lines:<pre class=code>
Cls
WaitKey()</pre>They are pointless.<br><br>Remove your leftkey(), rightkey(), etc. functions.  They serve no real purpose. Just change:<pre class=code>	If leftkey()=1 Then </pre>to<pre class=code>	If KeyDown(leftkey) Then 
</pre> <br><br></td></tr></table><br>
<a name="315622"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RXArt</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for the info Wolron. Here's what I came up with:<br><br><pre class=code>
AppTitle "-= My Pong =-"

Const Lft = 1
Const Rgt = 2

Global screenx	= 640
Global screeny	= 480
Global upkey 	= 200
Global leftkey 	= 203
Global rightkey = 205
Global downkey 	= 208
Global batspd	= 6

Global MyScore	= 0
Global OpScore  = 0

SeedRnd MilliSecs()

Graphics screenx,screeny,16,2

;Entity type
Type entityinfo
	Field x
	Field y
	Field width
	Field height
	Field speedx#
	Field speedy#
	Field directionx
	Field directiony
End Type

;Create Mybat instance
Mybat.entityinfo 	= New entityinfo
Mybat\x 			= 320
Mybat\y 			= screeny - 30
Mybat\width			= 80
Mybat\height		= 15

;Create Opbat instance
Opbat.entityinfo 	= New entityinfo
Opbat\x 			= 320
Opbat\y 			= 30
Opbat\width			= 80
Opbat\height		= 15

;Create Ball instance
Ball.entityinfo 	= New entityinfo
Ball\x				= 320
Ball\y				= 240
Ball\width			= 15
Ball\height			= 15
Ball\speedx#		= Rnd(3)
Ball\speedy#		= 6 ;Rnd(10)
Ball\directionx		= 1
Ball\directiony		= 1

;Create bat and ball image
gfxBat 	= CreateImage(Mybat\width,Mybat\height)
gfxBall	= CreateImage(ball\width,ball\height)

;Draw bat and ball image
SetBuffer ImageBuffer(gfxBat)
	Rect 0,0,mybat\width,mybat\height,1

SetBuffer ImageBuffer(gfxBall)
	Oval 0,0,ball\width,ball\height,1

gfxOpBat = CopyImage(gfxBat)

;Center the images
MidHandle gfxBat
MidHandle gfxOpBat
MidHandle gfxBall

;Switch back to back buffer
SetBuffer BackBuffer()

;Game loops starts here
;================================
While Not KeyHit(1)
;================================

	;Clear screen
	Cls
	Gosub ShowScore
	Gosub UpdateSprite
	Gosub Ball
	Gosub BallCollision
	Gosub MoveBat
	Gosub AIBat
	Flip	
Wend

;Free images
FreeImage gfxBat
FreeImage gfxOpBat
FreeImage gfxBall
End

;Ball Collision and reflection
.BallCollision
If ImagesCollide (gfxBat,Mybat\x,Mybat\y,0,gfxBall,Ball\x,Ball\y,0) Then
		ball\speedx# = Rnd(10)
		ball\speedy# = ball\speedy# * - ball\directiony
		ball\speedx# = ball\speedx# * - ball\directionx
	EndIf
	
	If ImagesCollide (gfxOpBat,Opbat\x,Opbat\y,0,gfxBall,Ball\x,Ball\y,0) Then
		ball\speedx# = Rnd(10)
		ball\speedy# = ball\speedy# * - ball\directiony
		ball\speedx# = ball\speedx# * - ball\directionx
	EndIf
Return

;Ball Movement and Scoring
.Ball
If ball\x &lt;(ball\width)/2+5 Or ball\x &gt; screenx-(ball\width)/2-5 Then 
		ball\speedx# = ball\speedx# * - ball\directionx
	EndIf
	If ball\y &lt;(ball\height)/2 Then
		MyScore = MyScore + 1		;I Scored!!
		ball\y = 240				;Reset ball to center-y
		ball\speedx# = Rnd(2)		;ball move straighter
		Delay 500
	ElseIf ball\y &gt; screeny-(ball\height)/2 Then 
		OpScore = OpScore + 1		;Opponent Scored!!
		ball\y = 240				;Reset ball to center-y
		ball\speedx# = Rnd(2)		;ball move straighter
		Delay 500	
	EndIf
	
	ball\x = (ball\x + ball\speedx#)
	ball\y = (ball\y + ball\speedy#)
Return

;Draw images to screen
.UpdateSprite
	DrawImage gfxbat,mybat\x,mybat\y
	DrawImage gfxOpBat,Opbat\x,Opbat\y	
	DrawImage gfxball,ball\x,ball\y 
Return

;Simple Opponent AI Movement
.AIBat
	If ball\x &lt; OpBat\x Then 
		OpBat\x = MoveBat(lft,OpBat\x, OpBat\width)
	EndIf
	If ball\x &gt; OpBat\x Then 
		OpBat\x = MoveBat(rgt,OpBat\x, OpBat\width)
	EndIf

Return

;My Bat Movement
.MoveBat
	If KeyDown(leftkey) Then 
		Mybat\x = movebat(lft,Mybat\x, Mybat\width)
	EndIf
	
	If KeyDown(rightkey) Then 
		Mybat\x = movebat(rgt,Mybat\x, Mybat\width)
	EndIf
Return

;Show the score
.ShowScore
	Text 2,screeny-15, "Your Score       : " + MyScore
	Text 2,0,          "Computer's Score : " + OpScore
	AppTitle "-= My Pong =-     You  :  " + MyScore + "         Comp  :  " + OpScore
Return

;Function to move the bat left
Function MoveBat(direction, batx, batwidth)
	Local old_x = batx
	If direction = Lft
		If batx &gt; (batwidth/2) Then	
			batx = batx - batspd
			Return batx
		Else
			Return old_x
		EndIf
	ElseIf direction = Rgt
		If batx &lt; screenx-(batwidth/2) Then
			batx = batx + batspd
			Return batx
		Else
			Return old_x
		EndIf
	Else
		Return old_x
	EndIf
End Function

</pre><br><br>I've combined the movebatleft and movebatright functions into one, and took away those leftkey and rightkey thingy. <br><br>;) <br><br></td></tr></table><br>
<a name="315892"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >darklordz</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> There is also a bug that gets the ball stuck on the right side of teh screengoing up an ddown.. I guess it's the math. Very good pong AI tough :P<br><br>*BrAG* &gt; <br><img src="http://www.balpoint.com/files/games/blpnt.c/Classic%20Pong/Scr_1s.gif"><br><img src="http://www.balpoint.com/files/games/blpnt.c/Classic%20Pong/Scr_2s.gif"><br><br><a href="http://www.balpoint.com/files/games/blpnt.c/Classic%20Pong/Install.Classic.Pong.v1.0.exe" target="_blank">download</a><br><br>My first 2D Project (Skinnable/Multiplayer &gt; Pong) It turned out ehrmm not to well but some seem to like it. I totaly think since ur a beginner u did GREAT!! <br><br></td></tr></table><br>
<a name="315971"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RXArt</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> darklordz: thanks for the comments!! Pong seems like an easy one, since it involves inverting the x and y speed of the ball. I'd have love to make the ai guess the location where the ball will hit, and I'm working on that right now. But my next big thingy is the snake clone, that I'm having some trouble with. I'm not using fancy 2d art at the moment, because I'm still trying to get familiar with the blitz language. cheers <br><br></td></tr></table><br>
<a name="316018"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bot Builder</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> the globals at the top(well, down to batspd) should be constants. So it would be:<br>[code]<br>Const screenx	= 640<br>Const screeny	= 480<br>Const upkey 	= 200<br>Const leftkey 	= 203<br>Const rightkey = 205<br>Const downkey 	= 208<br>Const batspd	= 6<br>[/code <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
