<!DOCTYPE html><html lang="en" ><head ><title >JV-ODE with Blitz3DSDK example</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >JV-ODE with Blitz3DSDK example</h1><a href="forums.php" >Archives Forums</a>/<a href="topics.php?forum=120" >Blitz3D SDK Programming</a>/<a href="#bottom" >JV-ODE with Blitz3DSDK example</a><br><br>
<a name="785170"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Avon</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've converted the "JV-ODE - Cubes Demo" BlitzMax JV-ODE demo to run with the Blitz3DSDK.  The original code is by VIP3R, who I hope doesn't mind me posting this here.  It's just to show people that it's pretty straightforward to get the SDK working with his wonderful ODE wrapper.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' #############################################################################
' #                            JV-ODE - Cubes Demo                            #
' #                        Code by Jim Williams (VIP3R)                       #
' #                    Devious Codeworks - Copyright © 2007                   #
' #############################################################################

SuperStrict

Framework BRL.GLMax2D ' only needed for "AppTerminate()"
Import BRL.Random
Import DevCode.JVODE
Import blitz3d.blitz3dsdk

AppTitle:String="JV-ODE - Cubes Demo"

bbBeginBlitz3D()
bbGraphics3D 800,600,0,2

Type ODEGeom
	Field body:Int
	Field geom:Int
	Field mesh:Int
	Field rgb:Int[]
	Field scale:Float[]
End Type

Global ODEGeomList:TList=CreateList()

' #############################################################################

' ### Setup ODE

Global World:Int=dWorldCreate()
Global Space:Int=dHashSpaceCreate(0)
Global ContactGroup:Int=dJointGroupCreate(0)

dWorldSetAutoDisableFlag(World,1)
dWorldSetGravity(World,0,-0.98,0)
dContactSetMode(dContactBounce)
dContactSetBounce(0.1)
dContactSetMu(48)

' ### Create light

Global Light:Int = bbCreateLight()

bbRotateEntity Light,45,-90,0
bbLightColor Light,255,255,255
bbAmbientLight 130,130,130

' ### Create camera

Global Camera:Int = bbCreateCamera()
bbCameraClsColor Camera,0,0,0
bbCameraRange Camera,1,1000
bbPositionEntity Camera,0,20,-50
bbRotateEntity Camera,30,0,0

' ### Create plane

dCreatePlane(Space,0,1,0,0)

Local plane:Int = bbCreatePlane()

bbEntityAlpha Plane,0.8

Local PlaneTexture:Int = bbCreateTexture(128,128,9)

bbClsColor 0,200,80
bbCls

bbColor 255,255,255

bbRect 0,0,64,64,1
bbRect 64,64,64,64,1

bbCopyRect 0,0,128,128,0,0,bbBackBuffer(),bbTextureBuffer(PlaneTexture)
bbScaleTexture PlaneTexture,20,20
bbEntityTexture Plane,PlaneTexture,0,0

Local Mirror:Int = bbCreateMirror()

' #############################################################################

Local PTime:Int
Local Timer:Int
Local PhysicsTime:Float

While Not AppTerminate() And Not bbKeyDown(1)

	If MilliSecs()-Timer&gt;700
		AddObject()
		Timer=MilliSecs()
	End If

	UpdateGeoms()

	PTime=MilliSecs()

	dSpaceCollide(Space,World,ContactGroup)
	dWorldQuickStep(World,0.1)
	dJointGroupEmpty(ContactGroup)

	PhysicsTime=MilliSecs()-PTime

	bbRenderWorld

	bbText 0,0,"JV-ODE Version "+dGetVersion()
	bbText 0,15,"Physics Time:"+PhysicsTime

	bbFlip 

Wend

dJointGroupDestroy(ContactGroup)
dSpaceDestroy(Space)
dWorldDestroy(World)
dCloseODE()

End

' #############################################################################

Function AddObject()

Local xp:Float
Local zp:Float
Local ode:ODEGeom

xp=Rand(-10,10)
zp=Rand(-10,10)

ode:ODEGeom=New ODEGeom
ode.body=dBodyCreate(World)
dBodySetAxisAngle(ode.body,0,0,0,0)
dBodySetPosition(ode.body,xp,30,zp)
dBodySetAutoDisableFlag(ode.body,1)
ode.geom=dCreateBox(Space,5,5,5)
dGeomSetBody(ode.geom,ode.body)
ode.mesh=bbCreateCube()
bbScaleMesh ode.mesh,2.5,2.5,2.5
bbEntityColor ode.mesh,Rand(0,255),Rand(0,255),Rand(0,255)
ListAddLast(ODEGeomList,ode:ODEGeom)

End Function

' #############################################################################

Function UpdateGeoms()

Local ode:ODEGeom

' ### Update Geoms
For ode:ODEGeom=EachIn ODEGeomList

	If dBodyIsEnabled(ode.body)
		bbPositionEntity ode.mesh,dGeomGetPositionX(ode.geom),dGeomGetPositionY(ode.geom),dGeomGetPositionZ(ode.geom)
		bbRotateEntity ode.mesh,dGeomGetPitch#(ode.geom),dGeomGetYaw#(ode.geom),dGeomGetRoll#(ode.geom)
	EndIf

Next

End Function

' #############################################################################
</textarea> <br><br></td></tr></table><br>
<a name="785615"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >VIP3R</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I don't mind at all :)<br><br>Good work Avon. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
