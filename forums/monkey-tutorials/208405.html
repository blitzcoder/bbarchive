<!DOCTYPE html><html lang="en" ><head ><title >Diddy Tutorials - Simple Shoot 'em Up</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Diddy Tutorials - Simple Shoot 'em Up</h1><a href="forums.php" >Monkey Archive Forums</a>/<a href="topics.php?forum=513" >Monkey Tutorials</a>/<a href="#bottom" >Diddy Tutorials - Simple Shoot 'em Up</a><br><br>
<a name="2085544"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Following on from the Screen Based Framework tutorial, let’s create a game with Diddy :)<br><br>Diddy has a Sprite class which has basic sprite based functionality to make coding games faster.<br><br>To load resources such as images and sounds, Diddy has a resource manager so you can store your images/sounds and retrieve them quickly and easily.<br><br>Let’s start by creating the folder structure for our new game:<br><br><pre class=code>DiddyShootEmUp\
	main.data\
		graphics\
		sounds\</pre><br><br>When using the resource managers Diddy expects graphics to be in a graphics folder and sounds in a sounds folder.<br><br>Create the main file in DiddyShootEmUp called “main.monkey”, this file is the entry point for your game:<br><br><b>main.monkey</b><br><pre class=code>Strict

Import diddy

Function Main:Int()
	MyGame.GetInstance()
	Return True
End

Class MyGame Extends DiddyApp
	Global instance:MyGame = Null

	Method Create:Void()
		' set the virtual resolution
		SetScreenSize(800, 450, True)
	End
	
	Function GetInstance:MyGame()
		If instance = Null
			instance = New MyGame()
		End
		Return instance
	End
End </pre><br><br>I’m using the Singleton Pattern again for the MyGame class, in the future you could declare a font or something in here and refer to it via the GetInstance function.<br><br>The SetScreenSize takes in the virtual resolution you want for your game, width and height. Set the Boolean to be True if you want Diddy to look after the aspect ratio for you.<br><br>Let’s load some resources:<br><br><pre class=code>Strict

Import diddy

Function Main:Int()
	MyGame.GetInstance()
	Return True
End

Class MyGame Extends DiddyApp
	Global instance:MyGame = Null

	Method Create:Void()
		' set the virtual resolution
		SetScreenSize(800, 450, True) ' 16:9 ratio
		' load the resources
		LoadImages()
		LoadSounds()
	End
	
	Function GetInstance:MyGame()
		If instance = Null
			instance = New MyGame()
		End
		Return instance
	End
	
	Method LoadImages:Void()
		' load in an animation 64x64 with 7 frames
		images.LoadAnim("ship.png", 64, 64, 7)
		' load in a background image
		images.Load("background.png", "", false)
	End
	
	Method LoadSounds:Void()
		' load the sounds
		sounds.Load("boom3.ogg")
		sounds.Load("lazer.ogg")
	End
	
End</pre><br><br>The “images” and “sounds” are resource managers within Diddy. When loading images Diddy will auto midhandle them, we don’t want the background to be midhandled so we set the Boolean to false. The resource managers are a hashmap with the name of the file as the key, but you can override the key if you want.<br><br>Copy the following files into the graphics folder:<br><br><img src="https://dl.dropboxusercontent.com/u/35103024/tutorials/background.png"><br><br><img src="https://dl.dropboxusercontent.com/u/35103024/tutorials/ship.png"><br><br>Copy the following files into the sounds folder:<br><br>	<a href="https://dl.dropboxusercontent.com/u/35103024/tutorials/boom3.ogg" target="_blank">https://dl.dropboxusercontent.com/u/35103024/tutorials/boom3.ogg</a><br><br><a href="https://dl.dropboxusercontent.com/u/35103024/tutorials/lazer.ogg" target="_blank">https://dl.dropboxusercontent.com/u/35103024/tutorials/lazer.ogg</a><br><br>Time for some screens, create a new file called “titlescreen.monkey” and one called “gamescreen.monkey” – remember case is important in MonkeyX!<br><br><b>main.monkey</b><br><pre class=code> Strict

Import diddy
Import titlescreen

Function Main:Int()
	MyGame.GetInstance()
	Return True
End

Class MyGame Extends DiddyApp
	Global instance:MyGame = Null

	Method Create:Void()
		' set the virtual resolution
		SetScreenSize(800, 450, True) ' 16:9 ratio
		' load the resources
		LoadImages()
		LoadSounds()
		Start(TitleScreen.GetInstance())
	End
	
	Function GetInstance:MyGame()
		If instance = Null
			instance = New MyGame()
		End
		Return instance
	End
	
	Method LoadImages:Void()
		' load in an animation 64x64 with 7 frames
		images.LoadAnim("ship.png", 64, 64, 7)
		' load in a background image
		images.Load("background.png", "", false)
	End
	
	Method LoadSounds:Void()
		' load the sounds
		sounds.Load("boom3.ogg")
		sounds.Load("lazer.ogg")
	End
	
End</pre><br><br><b>titlescreen.monkey</b><br><br><pre class=code>Strict
Import main
Import gamescreen

Class TitleScreen Extends Screen
	Global instance:TitleScreen = Null
	
	Function GetInstance:TitleScreen()
		If instance = Null
			instance = New TitleScreen()
		End
		Return instance
	End
	
	Method New()
		name = "TitleScreen"
	End
	
	Method Start:Void()
		
	End
	
	Method Update:Void()
		If KeyHit(KEY_SPACE)
			FadeToScreen(GameScreen.GetInstance())
		End
	End
	
	Method Render:Void()
		Cls
		DrawText("Diddy Shoot'em Up!", SCREEN_WIDTH2, SCREEN_HEIGHT2, 0.5, 0.5)
		DrawText("Press Space to Play!", SCREEN_WIDTH2, SCREEN_HEIGHT2 + 50, 0.5, 0.5)
	End
		
End</pre><br><br>Notice the SCREEN_WIDTH2 and SCREEN_HEIGHT2 variables, Diddy calculates half the screen dimensions and stores them in these variables for fast access.<br><br><b>gamescreen.monkey</b><br><br><pre class=code> Strict

Import main
Import titlescreen

Class GameScreen Extends Screen
	Global instance:GameScreen = Null
	Field backgroundImage:GameImage
	
	Function GetInstance:GameScreen()
		If instance = Null
			instance = New GameScreen()
		End
		Return instance
	End

	Method New()
		name = "GameScreen"
	End
	
	Method Start:Void()
		' set the background image
		backgroundImage = diddyGame.images.Find("background")
	End
	
	Method Update:Void()
		If KeyHit(KEY_ESCAPE)
			FadeToScreen(TitleScreen.GetInstance())
		End
	End
	
	Method Render:Void()
		Cls
		backgroundImage.Draw(0, 0)
	End	
End</pre><br><br>Run the game, you will have a title screen and a game screen.<br><br>Now for sprites :)<br><br>Create a new file called “gameobjects.monkey”, you can of course create multiple files with only one class in each, but for ease of reading I’m going to add them to this one file:<br><br><b>gameobjects.monkey</b><br><br><pre class=code>Strict

Import diddy

Class Player Extends Sprite
	Field score:Int = 0
	
	Method New(img:GameImage, x:Float, y:Float)
		Self.image = img
		Self.x = x
		Self.y = y
		Self.alpha = 1
		Self.SetHitBox(-img.image.HandleX(), -img.image.HandleY(), img.w, img.h)
		Self.visible = True
		Self.speedX = 8
	End
	
	Method Update:Void()
		' controls
		If KeyDown(KEY_LEFT)
			Self.x -= Self.speedX * dt.delta
		End
		If KeyDown(KEY_RIGHT)
			Self.x += Self.speedX * dt.delta
		End
		
		' boundary check
		If Self.x &lt; Self.image.w2 Then Self.x = Self.image.w2
		If Self.x &gt; SCREEN_WIDTH - Self.image.w2 Then Self.x = SCREEN_WIDTH - Self.image.w2
	End
End </pre><br><br>The “dt” variable is a global within Diddy so that you can access the built in Delta Timing class, this is to ensure that your movement is the same on all devices.<br><br>Better update gamescreen.monkey:<br><br><b>gamescreen.monkey</b><br><br><pre class=code>Strict

Import main
Import titlescreen
Import gameobjects

Class GameScreen Extends Screen
	Global instance:GameScreen = Null
	Field backgroundImage:GameImage
	Field player:Player
	
	Function GetInstance:GameScreen()
		If instance = Null
			instance = New GameScreen()
		End
		Return instance
	End

	Method New()
		name = "GameScreen"
	End
	
	Method Start:Void()
		' set the background image
		backgroundImage = diddyGame.images.Find("background")
		player = New Player(diddyGame.images.Find("ship"), SCREEN_WIDTH2, SCREEN_HEIGHT - 50)
		player.frame = 3
	End
	
	Method Update:Void()
		player.Update()
		
		If KeyHit(KEY_ESCAPE)
			FadeToScreen(TitleScreen.GetInstance())
		End
	End
	
	Method Render:Void()
		Cls
		backgroundImage.Draw(0, 0)
		player.Draw()
	End	
End</pre><br><br>Now run the code, you should see your player’s ship on the screen and be able to move it using the cursor keys.<br><br>For bullets, alter gameobjects.monkey:<br><br><b>gameobjects.monkey</b><br><pre class=code> Strict

Import diddy

Class Player Extends Sprite
	Field score:Int = 0
	Field lazerSnd:GameSound
	
	Method New(img:GameImage, x:Float, y:Float)
		Self.image = img
		Self.x = x
		Self.y = y
		Self.alpha = 1
		Self.SetHitBox(-img.image.HandleX(), -img.image.HandleY(), img.w, img.h)
		Self.visible = True
		Self.speedX = 8
		Self.lazerSnd = diddyGame.sounds.Find("lazer")
	End
	
	Method Update:Void()
		' controls
		If KeyDown(KEY_LEFT)
			Self.x -= Self.speedX * dt.delta
		End
		If KeyDown(KEY_RIGHT)
			Self.x += Self.speedX * dt.delta
		End
		If KeyHit(KEY_SPACE)
			Self.lazerSnd.Play()
			New Bullet(x, y)
		End
		
		' boundary check
		If Self.x &lt; Self.image.w2 Then Self.x = Self.image.w2
		If Self.x &gt; SCREEN_WIDTH - Self.image.w2 Then Self.x = SCREEN_WIDTH - Self.image.w2
	End
End

Class Bullet Extends Sprite
	Global list:List&lt;Bullet&gt; = New List&lt;Bullet&gt;
	
	Field w:Int
	Field h:Int
	
	Method New(x:Float, y:Float)
		Self.x = x
		Self.y = y
		Self.w = 10
		Self.h = 10
		Self.alpha = 1
		Self.SetHitBox(-w, -h, w, h)
		Self.speedY = 10
		list.AddLast(Self)
	End
	
	Method Draw:Void()
		DrawOval(x - w / 2, y - h / 2, w, h)
	End
	
	Method Update:Void()
		y -= speedY * dt.delta
		If y &lt; - h
			Kill()
		End
	End
	
	Method Kill:Void()
		list.Remove(Self)
	End
	
	Function DrawAll:Void()
		For Local b:Bullet = EachIn list
			b.Draw()
		End
	End

	Function UpdateAll:Void()
		For Local b:Bullet = EachIn list
			b.Update()
		End
	End
		
End</pre><br><br>And alter gamescreen.monkey:<br><br><pre class=code> Strict

Import main
Import titlescreen
Import gameobjects

Class GameScreen Extends Screen
	Global instance:GameScreen = Null
	Field backgroundImage:GameImage
	Field player:Player
	
	Function GetInstance:GameScreen()
		If instance = Null
			instance = New GameScreen()
		End
		Return instance
	End

	Method New()
		name = "GameScreen"
	End
	
	Method Start:Void()
		' set the background image
		backgroundImage = diddyGame.images.Find("background")
		player = New Player(diddyGame.images.Find("ship"), SCREEN_WIDTH2, SCREEN_HEIGHT - 50)
		player.frame = 3
	End
	
	Method Update:Void()
		player.Update()
		Bullet.UpdateAll()
		
		If KeyHit(KEY_ESCAPE)
			FadeToScreen(TitleScreen.GetInstance())
		End
	End
	
	Method Render:Void()
		Cls
		backgroundImage.Draw(0, 0)
		Bullet.DrawAll()
		player.Draw()
	End	
End</pre><br><br>That’ll do for now, you have a ship which can move and a bullet which you can fire. <br><br></td></tr></table><br>
<a name="2085543"></a>

<a name="2085659"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Snader</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you for these diddy tutorials. I hope this will get you a lot of new users for the Diddy framework. <br><br></td></tr></table><br>
<a name="2089400"></a>

<a name="2089402"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >consty</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice tutorials <br><br></td></tr></table><br>
<a name="2089812"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >okee</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi, just trying out this demo but get an error in main.monkey <br>at approx line 32<br><br>---&gt; Unable to find overload for LoadAnim(String,Int,Int,Int)<br><br>images.LoadAnim("ship.png", 64, 64, 7) <br><br></td></tr></table><br>
<a name="2089851"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Asmodean</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Unable to find overload for LoadAnim(String,Int,Int,Int) <br></div><br><br>Works fine for me.<br>Is it possible that you have a typo, a point instead of a comma? Something like images.LoadAnim("ship.png", 64, 64.7) <br><br></td></tr></table><br>
<a name="2090894"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Paul - Taiphoz</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> mate you should think about doing that again as a video tut on youtube. <br><br></td></tr></table><br>
<a name="2093962"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >turizoft</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi, I'm new to Monkey and therefore to Diddy. I have a couple of questions!<br><br>1. Do I need to use the Pro version of Monkey to run Diddy? <br>2. Does it replace Mojo or does it run alongside it? <br>3. What targets can my code run in when using Diddy?<br><br>Thanks a lot! <br><br></td></tr></table><br>
<a name="2093963"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Paul - Taiphoz</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> No<br>No - Yes<br>All<br><br>There may be some compatibility issues if your using an older version of the Free monkey, just update both to the latest and you should be good to go. not a lot of time to do a proper reply, if you need more clarification just shout. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
