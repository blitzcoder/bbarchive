<!DOCTYPE html><html lang="en" ><head ><title >Diddy Tutorials - Screen Based Framework</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Diddy Tutorials - Screen Based Framework</h1><a href="forums.php" >Monkey Archive Forums</a>/<a href="topics.php?forum=513" >Monkey Tutorials</a>/<a href="#bottom" >Diddy Tutorials - Screen Based Framework</a><br><br>
<a name="2085533"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> First thing to do is to grab the latest version of Diddy either using Mercurial (Hg) or the Zip from Google Code:<br><a href="https://code.google.com/p/diddy/" target="_blank">https://code.google.com/p/diddy/</a><br><a href="http://diddy.googlecode.com/archive/default.zip" target="_blank">http://diddy.googlecode.com/archive/default.zip</a><br><br>Once extracted or downloaded you should have a folder structure like this: <br><pre class=code>diddy\
  data\
  examples\
  src\
  utils\</pre><br>  <br>Go into the src folder and copy the diddy folder into your MonkeyX/modules folder or add diddy/src to the Monkey MODPATH.<br><pre class=code>c:\MonkeyX\modules\</pre><br><br>Now Diddy is ready to be used :)<br><br>Diddy's framework already extends Mojo's App so all you have to do is extend DiddyApp:<br><pre class=code>Strict 'enable strict mode - recommended

Import diddy 'imports the main diddy file

'MonkeyX entry point
Function Main:Int()
	New MyGame() ' instantiate MyGame
	Return True
End

' MyGame class
Class MyGame Extends DiddyApp
	' DiddyApp will call Create on instantiation
	Method Create:Void()
	End
End
</pre><br><br>So now let's create a couple of screens like a title screen and a main game screen. There are a few ways to do this, mainly either using Globals or a Singleton pattern.<br>Using Globals:<br><pre class=code>Strict

Import diddy

' declare the screens
Global titleScreen:TitleScreen
Global gameScreen:GameScreen

Function Main:Int()
	New MyGame()
	Return True
End

Class MyGame Extends DiddyApp
	Method Create:Void()
		'instantiate the screens
		titleScreen = New TitleScreen
		gameScreen = New GameScreen
		'start the titlescreen
		Start(titleScreen)
	End
End

' all screens must extend Screen
Class TitleScreen Extends Screen
	Method New()
		name = "Title"
	End
	
	' use Start to set varibles etc
	Method Start:Void()
	End

	' the Render method draws onto the screen
	Method Render:Void()
		Cls
		DrawText "TITLE SCREEN!", SCREEN_WIDTH2, SCREEN_HEIGHT2, 0.5, 0.5
		DrawText "Space to Play!", SCREEN_WIDTH2, SCREEN_HEIGHT2 + 20, 0.5, 0.5
		DrawText "Escape to Quit!", SCREEN_WIDTH2, SCREEN_HEIGHT2 + 40, 0.5, 0.5
	End

	' the Update method wraps the OnUpdate of Mojos App
	Method Update:Void()
		If KeyHit(KEY_ESCAPE)
			'diddyGame.exitScreen is automatically created for you
			FadeToScreen(diddyGame.exitScreen)
		End
		If KeyHit(KEY_SPACE)
			' Fade to the game screen
			FadeToScreen(gameScreen)
		End
	End
End

Class GameScreen Extends Screen
	Method New()
		name = "GameScreen"
	End
	
	Method Start:Void()
	End

	Method Render:Void()
		Cls
		DrawText "GAME SCREEN!", SCREEN_WIDTH2, SCREEN_HEIGHT2, 0.5, 0.5
		DrawText "Escape to go back to the Title!", SCREEN_WIDTH2, SCREEN_HEIGHT2 + 40, 0.5, 0.5
	End

	Method Update:Void()
		If KeyHit(KEY_ESCAPE)
			FadeToScreen(titleScreen)
		End
	End
End</pre><br><br>Using a Singleton like pattern:<br><pre class=code>Strict

Import diddy

Function Main:Int()
	New MyGame()
	Return True
End

Class MyGame Extends DiddyApp
	Method Create:Void()
		Start(TitleScreen.GetInstance())
	End
End

Class TitleScreen Extends Screen
	Global instance:TitleScreen = Null
	
	Method New()
		name = "Title"
	End
	
	Function GetInstance:TitleScreen()
		If instance = Null
			instance = New TitleScreen()
		End
		Return instance
	End
	
	Method Start:Void()
	End

	Method Render:Void()
		Cls
		DrawText "TITLE SCREEN!", SCREEN_WIDTH2, SCREEN_HEIGHT2, 0.5, 0.5
		DrawText "Space to Play!", SCREEN_WIDTH2, SCREEN_HEIGHT2 + 20, 0.5, 0.5
		DrawText "Escape to Quit!", SCREEN_WIDTH2, SCREEN_HEIGHT2 + 40, 0.5, 0.5
	End

	Method Update:Void()
		If KeyHit(KEY_ESCAPE)
			FadeToScreen(diddyGame.exitScreen)
		End
		If KeyHit(KEY_SPACE)
			FadeToScreen(GameScreen.GetInstance())
		End
	End
End

Class GameScreen Extends Screen
	Global instance:GameScreen = Null
	
	Method New()
		name = "GameScreen"
	End
	
	Function GetInstance:GameScreen()
		If instance = Null
			instance = New GameScreen()
		End
		Return instance
	End
	
	Method Start:Void()
	End

	Method Render:Void()
		Cls
		DrawText "GAME SCREEN!", SCREEN_WIDTH2, SCREEN_HEIGHT2, 0.5, 0.5
		DrawText "Escape to go back to the Title!", SCREEN_WIDTH2, SCREEN_HEIGHT2 + 40, 0.5, 0.5
	End

	Method Update:Void()
		If KeyHit(KEY_ESCAPE)
			FadeToScreen(TitleScreen.GetInstance())
		End
	End
End</pre><br><br>The Singleton way is a more correct Object Oriented way.<br><br>Done! We have now created two screens which we can go back forward between :) <br><br></td></tr></table><br>
<a name="2085504"></a>

<a name="2085378"></a>

<a name="2085377"></a>

<a name="2085376"></a>

<a name="2085375"></a>

<a name="2085374"></a>

<a name="2085373"></a>

<a name="2085474"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sammy</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> A clear and useful example, thank you! :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
