<!DOCTYPE html><html lang="en" ><head ><title >A Tlist extension</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >A Tlist extension</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >A Tlist extension</a><br><br>
<a name="947870"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am trying to split a list from given object to one end or the other. going through the tlist and link is giving me a headache( not too bright) and the fact that I have little experience with link lists make it harder to understand. so far I have this code:<br><pre class=code>

SuperStrict
Type TsuperList Extends TList

	Method ExtractToEnd:TsuperList(O:Object)
		Local newList:TsuperList = New TsuperList 
		Local link:TLink = findlink(o)
		newList._head._succ = link
		newList._head._pred = _head._pred
		
		_head._pred = link._pred
		_head._pred._succ = _head
		Return newList
		
	End Method
	
	Method ExtractToStart(O:Object)
	
	End Method
	
	Method AppendToStart(list:TsuperList)
	
	End Method
	
	Method AppendToEnd(list:TsuperList)
	
	End Method
End Type


Local mylist:TSuperList = New TSuperList

mylist.addlast("me")
myList.addlast("you")
Local name:String = "her"
mylist.addlast(name)
mylist.addlast("she")
mylist.addlast("him")
mylist.addlast("i")
Local newList:TsuperList =  mylist.ExtractToend(name)
Print "new list four:"
For name:String = EachIn newlist
	Print name
	Delay(10)
Next
Print
Print "______________"

Print "new list first :" + String(newlist.first())
Print "new list last  :" + String(newList.last())
Print "new list count :" + newlist.count()
Print "______________"
Print
Print "remaining from mylist first :" + String(mylist.first())
Print "remaining from mylist last  :" + String(mylist.last())
Print "remaining from mylist count :" + mylist.count()
Print "______________"
Print "left on original list "
For name:String = EachIn mylist
	Print name
	Delay(10)
Next
</pre><br><br>but it breaks when it has to display the newList.count(). if I remove this line everything works fine. <br>obviously I am doing something wrong but I can't figure what. help please.<br><br>[edit]<br>the delay is used to prevent the program from locking in case it creates a cyclic redundancy. <br><br></td></tr></table><br>
<a name="947873"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >N</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> What exactly are you doing? <br><br></td></tr></table><br>
<a name="947881"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I did a clone of spider solitaire, but I bult my own link list from scratch. it works fine as it is. Now I am trying to use bmax version but there is that one thing, it is not working yet and I can't figure out the problem:<br><br>I save the individual card stacks in lists. as you must know in the game you can grab parts of the card stacks and move/add them to other stacks. bmax lists doesn't have that and therefore I am trying to implement it. <br><br>I just want to know what I am doing wrong in the ExtractToEnd method.<br><br>and yes, I know I can grab one at a time and move them to a new list but I kind of figure it is kind of slow that way therefore I am trying to do it more efficiently. <br><br></td></tr></table><br>
<a name="947884"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Perturbatio</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> You don't appear to be changing the _succ of the original link's _pred <br><br></td></tr></table><br>
<a name="947892"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>You don't appear to be changing the _succ of the original link's _pred<br> <br></div><br>I am tryed to follow you but failed. <br>the original list is working correct(I think). if you comment the newList.count() line out everything is displayed correctly. the problem is with the newList. <br><br></td></tr></table><br>
<a name="947896"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >N</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Assuming you're splitting the list on O, this is probably closer to what you're wanting.<br><pre class=code>SuperStrict
Type TsuperList Extends TList

	Method ExtractToEnd:TsuperList(O:Object)
		If _head._succ = _head Then
			Return New TsuperList
		EndIf
		
		Local fl:TLink = FindLink(O)
		
		If fl = Null Then
			Throw "Link not found in list"
		EndIf
		
		Local newList:TsuperList = New TsuperList 
		
		If _head._succ._succ = _head Then
			Local t:TLink = newList._head
			newList._head = _head
			_head = t
			Return newList
		EndIf
		
		newList._head._succ = fl
		
		newList._head._pred = _head._pred
		_head._pred._succ = newList._head
		
		_head._pred = fl._pred
		_head._pred._succ = _head
		
		fl._pred = newList._head
		
		Return newList
		
	End Method
	
	Method ExtractToStart(O:Object)
	
	End Method
	
	Method AppendToStart(list:TsuperList)
	
	End Method
	
	Method AppendToEnd(list:TsuperList)
	
	End Method
End Type


Local mylist:TSuperList = New TSuperList

mylist.addlast("me")
myList.addlast("you")
Local name:String = "her"
mylist.addlast(name)
mylist.addlast("she")
mylist.addlast("him")
mylist.addlast("i")
Local newList:TsuperList =  mylist.ExtractToend(name)
Print "new list last four:"
For name:String = EachIn newlist
	Print name
	Delay(10)
Next
Print
Print "______________"

Print "new list first :" + String(newlist.first())
Print "new list last  :" + String(newList.last())
Print "new list count :" + newlist.count()
Print "______________"
Print
Print "remaining from mylist first :" + String(mylist.first())
Print "remaining from mylist last  :" + String(mylist.last())
Print "remaining from mylist count :" + mylist.count()
Print "______________"
Print "left on original list "
For name:String = EachIn mylist
	Print name
	Delay(10)
Next
</pre> <br><br></td></tr></table><br>
<a name="947898"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> great thanks! <br>now lets see if I can figure out how yo did it so I can figure out the rest. I will probably be back with more problems. <br><br></td></tr></table><br>
<a name="947904"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Nilium<br>you know it crashes when grabbing all of the objects from the original list and trying to display the original mylist.count()<br><br>Funny, it fails in 1.30 but works in 1.32.<br><br>if you want to try it:  <br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
Type TsuperList Extends TList

	Method ExtractToEnd:TsuperList(O:Object)
		If _head._succ = _head Then
			Return New TsuperList
		EndIf

		Local fl:TLink = FindLink(O)

		If fl = Null Then
			Return Null
		EndIf
		
		Local newList:TsuperList = New TsuperList 
		
		If _head._succ._succ = _head Then
			Local t:TLink = newList._head
			newList._head = _head
			_head = t
			Return newList
		EndIf
		
		newList._head._succ = fl
		
		newList._head._pred = _head._pred
		_head._pred._succ = newList._head
		
		_head._pred = fl._pred
		_head._pred._succ = _head
		
		fl._pred = newList._head
		
		Return newList
		
	End Method
	
	Method ExtractToStart(O:Object)
	End Method
	
	Method AppendToStart(list:TsuperList)
	
	End Method
	
	Method AppendToEnd(list:TsuperList)
	
	End Method
End Type


Local mylist:TSuperList = New TSuperList

Local name:String = "her"
mylist.addlast(name)
mylist.addlast("me")
myList.addlast("you")
mylist.addlast("she")
mylist.addlast("him")
mylist.addlast("i")
Local newList:TsuperList =  mylist.ExtractToend(name)
Print "new list last four:"
For name:String = EachIn newlist
	Print name
	Delay(10)
Next
Print
Print "______________"

Print "new list first :" + String(newlist.first())
Print "new list last  :" + String(newList.last())
Print "new list count :" + newlist.count()
Print "______________"
Print
Print "remaining from mylist first :" + String(mylist.first())
Print "remaining from mylist last  :" + String(mylist.last())
Print "remaining from mylist count :" + mylist.count()
Print "______________"
Print "left on original list "
For name:String = EachIn mylist
	Print name
	Delay(10)
Next
</textarea> <br><br></td></tr></table><br>
<a name="947911"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >N</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> I checked the structure in the debugger and I'm not seeing anything unusual about it, so I don't know what the issue is. <br><br></td></tr></table><br>
<a name="947930"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nilium sorry, I was in a hurry when I posted earlier and therefor I jumped into conclusion. It is working as it should. It bugs out when I try to get the first item simply because there are no items in the list. It seems to be normal behavior for previous versions to 1.32 or at least for 1.30. I didn't want to switch to 1.32 until it was more stable or at least bugs report slowed down so for now I am just going to work around it. <br>Thank you again. <br><br></td></tr></table><br>
<a name="948546"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> yesterday I stayed working on this all afternoon until I figured it out completely (I know lame) and finally completed it. Here it is working, as it should, I think:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

SuperStrict

Function printlist(list:TsuperList)
	For Local name:String = EachIn list
		Print name
		Delay(5) ' not needed for testing purpose to prevent cyclic redundancy.
	Next
End Function

Type TsuperList Extends TList

	Method ExtractToEnd:TsuperList(O:Object)
		If _head._succ = _head Then 
			Return New TsuperList 'If empty
		EndIf

		Local fl:TLink = FindLink(O)

		If fl = Null Then
			Return New TsuperList ' if O not found
		EndIf
		
		Local newList:TsuperList = New TsuperList ' place to save extracted records 
		
		If _head._succ._succ = _head Then 'if O is first link
			Local t:TLink = newList._head
			newList._head = _head
			_head = t
			Return newList 
		EndIf
		
		newList._head._succ = fl
		
		newList._head._pred = _head._pred
		_head._pred._succ = newList._head
		
		_head._pred = fl._pred
		_head._pred._succ = _head
		
		fl._pred = newList._head
		
		Return newList
		
	End Method
	
	Method ExtractToStart:TSuperList(O:Object)

		If _head._pred = _head Then
			Return New TsuperList
		EndIf
		
		Local fl:TLink = FindLink(O)
		
		If fl = Null Then
			Return Null
		EndIf
		
		Local newList:TsuperList = New TsuperList
				
		If _head._pred._pred = _head Then
			Local t:TLink = newList._head
			newList._head = _head
			_head = t
			Return newList
		EndIf
		
		newList._head._pred = fl
		
		newList._head._succ = _head._succ
		_head._succ._pred = newList._head
		
		_head._succ = fl._succ
		_head._succ._pred = _head
		
		fl._succ = newList._head
		
		Return newList
	
	End Method
	
	Method MergeToStart(list:TsuperList Var)
		If list = Null Return
		If list._head._succ = list._head Then 
			Return 
		EndIf

		If _head._succ = _head
			_head._succ = list._head._succ
			_head._pred = list._head._pred
			_head._succ._pred = _head
			_head._pred._succ = _head
			list = New TsuperList
			Return
		EndIf		
		_head._succ._pred = list._head._pred
		list._head._pred._succ = _head._succ
		_head._succ = list._head._succ
		list._head._succ._pred = _head 
		list = New TsuperList
	End Method
	
	Method MergeToEnd(list:TsuperList Var)
		If list = Null Return
		If list._head._pred = list._head Then 
			Return
		EndIf

		If _head._pred = _head 
			_head._succ = list._head._succ
			_head._pred = list._head._pred
			_head._succ._pred = _head
			_head._pred._succ = _head
			list = New TsuperList
			Return
		EndIf		
	
		_head._pred._succ = list._head._succ
		list._head._succ._pred = _head._pred
		_head._pred = list._head._pred
		list._head._pred._succ = _head
		list = New TsuperList
		
	End Method
End Type


Local mylist:TSuperList = New TSuperList
mylist.addlast("one")
myList.addlast("two")
Local name:String = "three"
mylist.addlast(name)
mylist.addlast("four")
mylist.addlast("five")
mylist.addlast("six")
Print "original list:"
printlist(mylist)
Print "items :"+mylist.count()
Print "______________"

Local newList:TsuperList =  mylist.ExtractToend(name)
Print "new list, extracted from end of original list:"
Printlist(newlist)
Print "items :"+newlist.count()
Print "______________"
Print "left on original list :"
Printlist(mylist)
Print "items :"+mylist.count()
Print "______________"
mylist.Mergetostart(newlist)
Print "new list Mergeed to start of original list :"
printlist(mylist)
Print "items :"+mylist.count()
Print "First item :"+String(mylist.first())
Print "Last item  :"+String(mylist.last())
Print "______________"

</textarea><br><br>if anybody is interested, use at your own risk.<br>note: the merge list is cleared to prevent corruption of the "merged" list.<br>thanks again for your help Nilium. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
