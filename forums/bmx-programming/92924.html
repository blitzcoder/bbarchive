<!DOCTYPE html><html lang="en" ><head ><title >Stacktrace from DebugLog?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Stacktrace from DebugLog?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Stacktrace from DebugLog?</a><br><br>
<a name="1060817"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gabriel</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm doing some debugging in BlitzMax and since using Unity, one big feature I miss is having a complete stacktrace on every debuglog. I can drop a debuglog into my code, run it and I can immediately trace where that line got called from, which saves having to constantly backtrack with more and more debuglogs. Is there a way to get this behaviour in BlitzMax? (Without stopping the program. I want to be able to trace lots of logs when I'm combing through them, after the program has finished.) <br><br></td></tr></table><br>
<a name="1060838"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> you dont mean like a debugstop?<br><br>thats about the only way you can pause the game and look where a function has been called from <br><br></td></tr></table><br>
<a name="1060844"></a>

<a name="1060845"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ziggy</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> BLIde does provide this information while debugging (also the free version). It's placed below the debug tree in the debug panel. Just in case it is useful to you. this call stack information is presented in inverse call order. (the first is the current location in code, then the function call, then etc...)<br><br><font class="tiny">Last edited 2010</font> <br><br></td></tr></table><br>
<a name="1060846"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tommo</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> <textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict
import brl.appstub

NoDebug

Private
	'Copied from brl.appstub
	?Win32
	Extern "Win32"
	Const SW_SHOW=5
	Const SW_RESTORE=9
	Function IsIconic( hwnd )
	Function GetForegroundWindow()
	Function SetForegroundWindow( hwnd )
	Function ShowWindow( hwnd,cmdShow )
	Function GetCurrentThreadId()
	End Extern
	?
	
	?MacOS
	Extern
	Function CGDisplayIsCaptured( displayId )
	End Extern
	?
	
	Extern
	Global bbStringClass:Byte
	Global bbArrayClass:Byte
	Global bbNullObject:Byte
	Global bbEmptyArray:Byte
	Global bbEmptyString:Byte
	Global brl_blitz_NullFunctionError:Byte Ptr
	Function bbIsMainThread()="bbIsMainThread"
	Function bbGCValidate:Int( mem:Int ) = "bbGCValidate"
	End Extern
	
	Function ToHex$( val )
		Local buf:Short[8]
		For Local k=7 To 0 Step -1
			Local n=(val&amp;15)+Asc("0")
			If n&gt;Asc("9") n=n+(Asc("A")-Asc("9")-1)
			buf[k]=n
			val:Shr 4
		Next
		Return String.FromShorts( buf,8 ).ToLower()
	End Function
	
	Function IsAlpha( ch )
		Return (ch&gt;=Asc("a") And ch&lt;=Asc("z")) Or (ch&gt;=Asc("A") And ch&lt;=Asc("Z"))
	End Function
	
	Function IsNumeric( ch )
		Return ch&gt;=Asc("0") And ch&lt;=Asc("9")
	End Function
	
	Function IsAlphaNumeric( ch )
		Return IsAlpha(ch) Or IsNumeric(ch)
	End Function
	
	Function IsUnderscore( ch )
		Return ch=Asc("_")
	End Function
	
	Function Ident$( tag$ Var )
		If Not tag Return ""
		If Not IsAlpha( tag[0] ) And Not IsUnderscore( tag[0] ) Return ""
		Local i=1
		While i&lt;tag.length And (IsAlphaNumeric(tag[i]) Or IsUnderscore(tag[i]))
			i:+1
		Wend
		Local id$=tag[..i]
		tag=tag[i..]
		Return id
	End Function
	
	Function TypeName$( tag$ Var )
	
		Local t$=tag[..1]
		tag=tag[1..]
	
		Select t
		Case "b"
			Return "Byte"
		Case "s"
			Return "Short"
		Case "i"
			Return "Int"
		Case "l"
			Return "Long"
		Case "f"
			Return "Float"
		Case "d"
			Return "Double"
		Case "$"
			Return "String"
		Case "z"
			Return "CString"
		Case "w"
			Return "WString"
		Case ":","?"
			Local id$=Ident( tag )
			While tag And tag[0]=Asc(".")
				tag=tag[1..]
				id=Ident( tag )
			Wend
			If Not id DebugError "Invalid object typetag"
			Return id
		Case "*"
			Return TypeName( tag )+" Ptr"
		Case "["
			While tag[..1]=","
				tag=tag[1..]
				t:+","
			Wend
			If tag[..1]&lt;&gt;"]" DebugError "Invalid array typetag"
			tag=tag[1..]
			Return TypeName( tag )+t+"]"
		Case "("
			If tag[..1]&lt;&gt;")"
				t:+TypeName( tag )
				While tag[..1]=","
					tag=tag[1..]
					t:+","+TypeName( tag )
				Wend
				If tag[..1]&lt;&gt;")" DebugError "Invalid function typetag"
			EndIf
			tag=tag[1..]
			Return TypeName( tag )+t+")"
		End Select
	
		DebugError "Invalid debug typetag:"+t
	
	End Function
	
	'int offsets into 12 byte DebugStm struct
	Const DEBUGSTM_FILE=0
	Const DEBUGSTM_LINE=1
	Const DEBUGSTM_CHAR=2
	
	'int offsets into 16 byte DebugDecl struct
	Const DEBUGDECL_KIND=0
	Const DEBUGDECL_NAME=1
	Const DEBUGDECL_TYPE=2
	Const DEBUGDECL_ADDR=3
	
	'DEBUGDECL_KIND values
	Const DEBUGDECLKIND_END=0
	Const DEBUGDECLKIND_CONST=1
	Const DEBUGDECLKIND_LOCAL=2
	Const DEBUGDECLKIND_FIELD=3
	Const DEBUGDECLKIND_GLOBAL=4
	Const DEBUGDECLKIND_VARPARAM=5
	Const DEBUGDECLKIND_TYPEMETHOD=6
	Const DEBUGDECLKIND_TYPEFUNCTION=7
	
	'int offsets into 12+n_decls*4 byte DebugScope struct
	Const DEBUGSCOPE_KIND=0
	Const DEBUGSCOPE_NAME=1
	Const DEBUGSCOPE_DECLS=2
	
	'DEBUGSCOPE_KIND values
	Const DEBUGSCOPEKIND_FUNCTION=1
	Const DEBUGSCOPEKIND_TYPE=2
	Const DEBUGSCOPEKIND_LOCAL=3
	
	Function DebugError( t$ )
		WriteStderr "Debugger Error:"+t+"~n"
		End
	End Function
	
	Function DebugStmFile$( stm:Int Ptr )
		Return String.FromCString( Byte Ptr stm[DEBUGSTM_FILE] )
	End Function
	
	Function DebugStmLine( stm:Int Ptr )
		Return stm[DEBUGSTM_LINE]
	End Function
	
	Function DebugStmChar( stm:Int Ptr )
		Return stm[DEBUGSTM_CHAR]
	End Function
	
	Function DebugDeclKind$( decl:Int Ptr )
		Select decl[DEBUGDECL_KIND]
		Case DEBUGDECLKIND_CONST Return "Const"
		Case DEBUGDECLKIND_LOCAL Return "Local"
		Case DEBUGDECLKIND_FIELD Return "Field"
		Case DEBUGDECLKIND_GLOBAL Return "Global"
		Case DEBUGDECLKIND_VARPARAM Return "Local"
		End Select
		DebugError "Invalid decl kind"
	End Function
	
	Function DebugDeclName$( decl:Int Ptr )
		Return String.FromCString( Byte Ptr decl[DEBUGDECL_NAME] )
	End Function
	
	Function DebugDeclType$( decl:Int Ptr )
		Local t$=String.FromCString( Byte Ptr decl[DEBUGDECL_TYPE] )
		Local ty$=TypeName( t )
		Return ty
	End Function
	
	Function DebugDeclSize( decl:Int Ptr )
	
		Local tag=(Byte Ptr Ptr(decl+DEBUGDECL_TYPE))[0][0]
	
		Select tag
		Case Asc("b") Return 1
		Case Asc("s") Return 2
		Case Asc("l") Return 8
		Case Asc("d") Return 8
		End Select
		
		Return 4
	
	End Function
	
	Function DebugEscapeString$( s$ )
		s=s.Replace( "~~","~~~~")
		s=s.Replace( "~0","~~0" )
		s=s.Replace( "~t","~~t" )
		s=s.Replace( "~n","~~n" )
		s=s.Replace( "~r","~~r" )
		s=s.Replace( "~q","~~q" )
		Return "~q"+s+"~q"
	End Function
	
	Function DebugDeclValue$( decl:Int Ptr,inst:Byte Ptr )
		If decl[DEBUGDECL_KIND]=DEBUGDECLKIND_CONST
			Local p:Byte Ptr=Byte Ptr decl[DEBUGDECL_ADDR]
			Return DebugEscapeString(String.FromShorts( Short Ptr(p+12),(Int Ptr (p+8))[0] ))
		EndIf
	
		Local p:Byte Ptr
		Select decl[DEBUGDECL_KIND]
		Case DEBUGDECLKIND_GLOBAL
			p=Byte Ptr decl[DEBUGDECL_ADDR]
		Case DEBUGDECLKIND_LOCAL,DEBUGDECLKIND_FIELD
			p=Byte Ptr (inst+decl[DEBUGDECL_ADDR])
		Case DEBUGDECLKIND_VARPARAM
			p=Byte Ptr (inst+decl[DEBUGDECL_ADDR])
			p=Byte Ptr ( (Int Ptr p)[0] )
		Default
			DebugError "Invalid decl kind"
		End Select
		
		Local tag=(Byte Ptr Ptr(decl+DEBUGDECL_TYPE))[0][0]
		
		Select tag
		Case Asc("b")
			Return String.FromInt( (Byte Ptr p)[0] )
		Case Asc("s")
			Return String.FromInt( (Short Ptr p)[0] )
		Case Asc("i")
			Return String.FromInt( (Int Ptr p)[0] )
		Case Asc("l")
			Return String.FromLong( (Long Ptr p)[0] )
		Case Asc("f")
			Return String.FromFloat( (Float Ptr p)[0] )
		Case Asc("d")
			Return String.FromDouble( (Double Ptr p)[0] )
		Case Asc("$")
			p=(Byte Ptr Ptr p)[0]
			Local sz=Int Ptr(p+8)[0]
			Local s$=String.FromShorts( Short Ptr(p+12),sz )
			Return DebugEscapeString( s )
		Case Asc("z")
			p=(Byte Ptr Ptr p)[0]
			If Not p Return "Null"
			Local s$=String.FromCString( p )
			Return DebugEscapeString( s )
		Case Asc("w")
			p=(Byte Ptr Ptr p)[0]
			If Not p Return "Null"
			Local s$=String.FromWString( Short Ptr p )
			Return DebugEscapeString( s )
		Case Asc("*"),Asc("?")
			Return "$"+ToHex( (Int Ptr p)[0] )
		Case Asc("(")
			p=(Byte Ptr Ptr p)[0]
			If p=brl_blitz_NullFunctionError Return "Null"
		Case Asc(":")
			p=(Byte Ptr Ptr p)[0]
			If p=Varptr bbNullObject Return "Null"
			If p=Varptr bbEmptyArray Return "Null[]"
			If p=Varptr bbEmptyString Return "Null$"
		Case Asc("[")
			p=(Byte Ptr Ptr p)[0]
			If Not p Return "Null"
			If Not (Int Ptr (p+20))[0] Return "Null"
		Default
			DebugError "Invalid decl typetag:"+Chr(tag)
		End Select
		
		Return "$"+ToHex( Int p )
	
	End Function
	
	Function DebugScopeKind$( scope:Int Ptr )
		Select scope[DEBUGSCOPE_KIND]
		Case DEBUGSCOPEKIND_FUNCTION Return "Function"
		Case DEBUGSCOPEKIND_TYPE Return "Type"
		Case DEBUGSCOPEKIND_LOCAL Return "Local"
		End Select
		DebugError "Invalid scope kind"
	End Function
	
	Function DebugScopeName$( scope:Int Ptr )
		Return String.FromCString( Byte Ptr scope[DEBUGSCOPE_NAME] )
	End Function
	
	Function DebugScopeDecls:Int Ptr[]( scope:Int Ptr )
		Local n,p:Int Ptr=scope+DEBUGSCOPE_DECLS
		While p[n]&lt;&gt;DEBUGDECLKIND_END
			n:+1
		Wend
		Local decls:Int Ptr[n]
		For Local i=0 Until n
			decls[i]=p+i*4
		Next
		Return decls
	End Function
	
	Function DebugObjectScope:Int Ptr( inst:Byte Ptr )
		Local clas:Int Ptr Ptr=(Int Ptr Ptr Ptr inst)[0]
		Return clas[2]
	End Function

		
'hook starts here
	Extern
		Global bbOnDebugStop()
		Global bbOnDebugLog( message$ )
		Global bbOnDebugEnterStm( stm:Int Ptr )
		Global bbOnDebugEnterScope( scope:Int Ptr,inst:Byte Ptr )
		Global bbOnDebugLeaveScope()
		Global bbOnDebugPushExState()
		Global bbOnDebugPopExState()
		Global bbOnDebugUnhandledEx(ex:Object)
	End Extern
	
	Global current_bbOnDebugStop() = bbOnDebugStop
	Global current_bbOnDebugLog(message$) = bbOnDebugLog
	Global current_bbOnDebugEnterStm(stm:Int Ptr) = bbOnDebugEnterStm
	Global current_bbOnDebugEnterScope(scope:Int Ptr, inst:Byte Ptr) = bbOnDebugEnterScope
	Global current_bbOnDebugLeaveScope() = bbOnDebugLeaveScope
	Global current_bbOnDebugPushExState()
	Global current_bbOnDebugPopExState()
	Global current_bbOnDebugUnhandledEx(ex:Object)
		
public

function hook_OnDebugEnterScope(scope:Int Ptr, inst:Byte Ptr)
	return current_bbOnDebugEnterScope(scope, inst)
end function

global lastStm:Int ptr = null

function hook_OnDebugEnterStm(stm:Int Ptr)
	lastStm = stm
	return current_bbOnDebugEnterStm(stm)
end function

function getLastStmString$()
	local stm:Int ptr = lastStm
	if stm = null return null
	return DebugStmFile(stm) + "&lt;" + DebugStmLine(stm) + "," + DebugStmChar(stm) + "&gt;"
end function

function hook_OnDebugLog(msg$)
	writeStdOut "DebugLog@" + getLastStmString() + ":" + msg + "~n"
end function

'bbOnDebugEnterScope = hook_OnDebugEnterScope
bbOnDebugEnterStm = hook_OnDebugEnterStm
bbOnDebugLog = hook_OnDebugLog
</textarea><br>Some dirty hack.<br>It inserts location of DebugLog into the output log.<br>Hope it helps. <br><br></td></tr></table><br>
<a name="1061069"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gabriel</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Slenkar: Not exactly like DebugStop, no. I want to be able to do it without pausing. I want to be able to write to the log ten times, quit the game, and then go back and read the ten different stack traces.<br><br>@Ziggy: I haven't updated Blide in quite a long time, though I did purchase a license. You say that it's possible "while debugging", but is it available after debugging, with multiple stacks as I just described in my response to Slenkar?<br><br>@Tommo: That's a very creative hack. Thanks tons for that! <br><br></td></tr></table><br>
<a name="1061561"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ziggy</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> but is it available after debugging, with multiple stacks as I just described in my response to Slenkar? <br></div><br>I don't reallt know what you mean. BLIde provides the call stack information while you're debugging. When the debug session ends (becouse of a bug, as instance) This information is still available on the debug window. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
