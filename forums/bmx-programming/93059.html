<!DOCTYPE html><html lang="en" ><head ><title >Best Way to do Z-Ordering?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Best Way to do Z-Ordering?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Best Way to do Z-Ordering?</a><br><br>
<a name="1062453"></a>

<a name="1062455"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi All,<br><br>Ive currently got the following for Z-Ordering:<br><br>Sprite.bmx<br><pre class=code>
Type TSprite
	Field x#, y#

	Method Compare:Int(other:Object)
	 	Local s:TSprite = TSprite(other)
	 	If y = s.y Return Super.Compare(other)
	 	Return y - s.y
	End Method
End Type
</pre><br><br>Monster.bmx<br><pre class=code>
Type TMonster Extends TSprite
	Global list:TList

	Function CreateMonster:TMonster(gi:TGameImage, x:Float, y:Float)
		Local m:TMonster = New TMonster
		If list = Null list = CreateList()
		m.x = x
		m.y = y
		list.AddLast m
		GameScreen.level.objectList.AddLast m
		Return m
	End Function
End Type
</pre><br><br>Hero.bmx<br><pre class=code>
Type THero Extends TSprite
	Global list:TList

	Function CreateHero:THero(gi:TGameImage, x:Float, y:Float)
		Local h:THero = New THero
		If list = Null list = CreateList()
		h.x = x
		h.y = y
		list.AddLast h
		GameScreen.level.objectList.AddLast h
		Return h
	End Function
End Type
</pre><br><br>Gamescreen.bmx<br><pre class=code>
Type TGamescreen Extends TScreen
	Field level:TLevel

	Method Update()
		level.update()
	End Method

	Method Draw()
		level.draw()
	End Method

End Type
</pre><br><br>Level.bmx<br><pre class=code>
Type TLevel
	Field objectList:TList

	Function Create:TLevel(levelNumber:Int)
		Local level:TLevel = New TLevel
		
		level.objectList = CreateList()
		Return level
	End Function

	Method update()
		THero.UpdateAll()
		TMonster.UpdateAll()
		SortList objectList
	End Method

	Method draw()
		For Local s:TSprite = EachIn objectList
			s.draw()
		Next
	End Method
</pre><br><br>You see I have currently got 3 lists:<br>* Tlevel.objectList<br>* TMonster.list<br>* THero.list<br><br>Runnable Code:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Graphics 800, 600

Global objectList:TList = CreateList()

For Local i:Int = 1 To 20
	TMonster.CreateMonster(Rand(0, 800), Rand(0, 600))
	THero.CreatHero(Rand(0, 800), Rand(0, 600))
Next

Repeat
	TMonster.UpdateAll()
	SortList objectList
	
	Cls
	For Local s:TSprite = EachIn objectList
		s.draw()
	Next
	Flip
Until AppTerminate()

Type TSprite
	Field x:Float, y:Float
	Field dx:Float, dy:Float
	
	Method Compare:Int(other:Object)
	 	Local s:TSprite = TSprite(other)
	 	If y = s.y Return Super.Compare(other)
	 	Return y - s.y
	End Method
	
	Method Draw()
		
	End Method
End Type


Type TMonster Extends TSprite
	Global list:TList

	Function CreateMonster:TMonster(x:Float, y:Float)
		Local m:TMonster = New TMonster
		If list = Null list = CreateList()
		m.x = x
		m.y = y
		m.dx = Rnd(-1, 1)
		m.dy = Rnd(-1, 1)
		list.AddLast m
		objectList.AddLast m
		Return m
	End Function

	Function UpdateAll()
		If Not list Return
		For Local m:TMonster = EachIn list
			m.Update()
		Next
	End Function
	
	Method Update()
		x:+dx
		y:+dy
		If x &lt; 0 dx = -dx
		If x &gt; 800 dx = -dx
		If y &lt; 0 dy = -dy
		If y &gt; 600 dy = -dy
	End Method
	
	Method Draw()
		SetColor 255, 0, 0
		DrawRect x - 25, y - 25, 50, 50
		SetColor 0, 0, 0
		Plot x, y
		SetColor 255, 255, 255
	End Method	
End Type

Type THero Extends TSprite
	Global list:TList

	Function CreatHero:THero(x:Float, y:Float)
		Local m:THero = New THero
		If list = Null list = CreateList()
		m.x = x
		m.y = y
		m.dx = Rnd(-1, 1)
		m.dy = Rnd(-1, 1)
		list.AddLast m
		objectList.AddLast m
		Return m
	End Function

	Function UpdateAll()
		If Not list Return
		For Local m:THero = EachIn list
			m.Update()
		Next
	End Function
	
	Method Update()
		x:+dx
		y:+dy
	End Method
	
	Method Draw()
		SetColor 0, 255, 0
		DrawRect x - 25, y - 25, 50, 50
		SetColor 0, 0, 0
		Plot x, y
		SetColor 255, 255, 255
	End Method	
End Type
</textarea><br><br>When creating a hero or monster I add them to the Tlevel.objectlist and their own list. I update them via their own lists, but only draw them via the TLevel.objectList. The z-ordering is done via the sortList and compare methods in Max.<br><br>I was wondering if this is the best way of doing this or should I be doing this another way?<br><br>Thanks!<br><br><font class="tiny">Last edited 2010</font> <br><br></td></tr></table><br>
<a name="1062457"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Muttley</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Avoid overriding Compare for sorting, it also affects removing objects from lists (there's a big discussion about this floating around), and you may end up removing the wrong object. <br><br></td></tr></table><br>
<a name="1062576"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have seen that, but doing this line seems to fix that issue:<br><br><pre class=code>
 	If y = s.y Return Super.Compare(other) 
</pre><br><br>If I shouldnt use Compare, then what should I use? <br><br></td></tr></table><br>
<a name="1062581"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Use a custom compare function, which you send as a parameter to Sort. That way, you can even have multiple compare functions! <br><br></td></tr></table><br>
<a name="1062594"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Muttley, I guess you are talking about this thread :)<br><br><a href="http://www.blitzbasic.co.nz/Community/posts.php?topic=87418" target="_blank">http://www.blitzbasic.co.nz/Community/posts.php?topic=87418</a><br><br>@Czar<br><br>Something like this?<br><br><pre class=code>
SortList list, True, ZOrderSort

Function ZOrderSort:Int( o1:Object, o2:Object )
	Return TSprite(o1).y-TSprite(o2).y
End Function
</pre> <br><br></td></tr></table><br>
<a name="1062600"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes, like that! :D<br><br> If you are ok to guarantee that only TSprites are in the list, you can use it like that. A safer, more generic solution is like this:<br><br><pre class=code>Function ZOrderSort:Int(o1:Object, o2:Object)
	Local s1:TSprite = TSprite(o1)
	Local s2:TSprite = TSprite(o2)
	If s1 And s2
		Return s2.y - s1.y
	Else
		Return o1.Compare(o2)
	End if
End Function</pre><br><br>The else statement is a fail-safe comparison. If an "o" does not cast to a TSprite, but you try to get the y anyway, it will crash. <br><br></td></tr></table><br>
<a name="1062603"></a>

<a name="1062604"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I don't know if you know this but you know you can do all using a single list. if you don't know, it's quite a bit of less code to type and less to keep track of:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Graphics 800, 600

Global objectList:TList = CreateList()

For Local i:Int = 1 To 20
	TMonster.CreateMonster(Rand(0, 800), Rand(0, 600))
	THero.CreatHero(Rand(0, 800), Rand(0, 600))
Next
Local ms:Int
Repeat
	TSprite.UpdateAll()
	SortList objectList
	
	Cls
	Tsprite.drawAll()
	SetColor 255,255,255
	DrawText ms,20,20
	Flip
Until AppTerminate()

Type TSprite
	Field x:Float, y:Float
	Field dx:Float, dy:Float
	
	Method draw() Abstract
	Method update() Abstract
		
	Method Compare:Int(other:Object)
	 	Local s:TSprite = TSprite(other)
	 	If y = s.y Return Super.Compare(other)
		
	 	Return y - s.y
	End Method
	
	Function UpdateAll()
		For Local m:Tsprite = EachIn objectlist
			m.Update()
		Next
	End Function
	
	Function drawAll()
		For Local m:Tsprite = EachIn objectList
			m.draw()
		Next
	End function
	
End Type


Type TMonster Extends TSprite

	Function CreateMonster:TMonster(x:Float, y:Float)
		Local m:TMonster = New TMonster
		m.x = x
		m.y = y
		m.dx = Rnd(-1, 1)
		m.dy = Rnd(-1, 1)
		objectList.AddLast m
		
		Return m
	End Function

	Method Update()
		x:+dx
		y:+dy
		If x &lt; 0 dx = -dx
		If x &gt; 800 dx = -dx
		If y &lt; 0 dy = -dy
		If y &gt; 600 dy = -dy
	End Method
	
	Method Draw()
		SetColor 255, 0, 0
		DrawRect x - 25, y - 25, 50, 50
		SetColor 0, 0, 0
		Plot x, y
		SetColor 255, 255, 255
	End Method	
End Type

Type THero Extends TSprite

	Function CreatHero:THero(x:Float, y:Float)
		Local m:THero = New THero
		m.x = x
		m.y = y
		m.dx = Rnd(-1, 1)
		m.dy = Rnd(-1, 1)
		objectList.AddLast m
		Return m
	End Function

	Method Update()
		x:+dx
		y:+dy
	End Method
	
	Method Draw()
		SetColor 0, 255, 0
		DrawRect x - 25, y - 25, 50, 50
		SetColor 0, 0, 0
		Plot x, y
		SetColor 255, 255, 255
	End Method	
	
End Type

</textarea><br><br><font class="tiny">Last edited 2010</font> <br><br></td></tr></table><br>
<a name="1062605"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Czar :)<br><br>@Jesse, yeah I know I can do that, but what about if I have multiple objects in the list (eg, monsters, heroes, projectiles, pickups etc) and I want to do a collision check on monsters with heroes, isnt it faster just to loop on the monsters list instead of the entire objectlist? <br><br></td></tr></table><br>
<a name="1062606"></a>

<a name="1062607"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Unless your game has thousands of objects that need to be checked several times a second, the speed cost of redundant checks on the whole list of objects isn't worth worrying about. If it is, you can optimize later. That said, it's not too complicated to maintain an "all objects" list, and seperate lists for each kind of object. You just need to remember to add and remove objects from all the right lists and the right time.<br><br><font class="tiny">Last edited 2010</font> <br><br></td></tr></table><br>
<a name="1062620"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>isnt it faster just to loop on the monsters list instead of the entire objectlist?<br> <br></div><br>I tried the code I posted with 2000(1000 each) objects and it processed all the 2000 object in under 2 millisecs. all on a 2ghz core duo macbook. <br><br></td></tr></table><br>
<a name="1062643"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matty</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> There is a very quick way to sort a list, simply by counting before displaying, no need to rearrange order within a list, I've used this a lot in B3D/blitzplus and other languages, not sure if bmax would do it but I don't see why not.  It does assume however that your 'y' value is an integer or at least can be expressed as an integer with no decimal component...<br><br>Simply have an array (or a bank) with indices going from minimum value to maximum value for the y coordinate.<br><br>Loop through your objects/sprites whatever, then using the y value of the object as the index into your array store in a bank created at that array index the list of objects/sprites handles to be displayed at that y value.<br><br>Then in your display routine iterate through the array from smallest to largest displaying sprites where there is a bank handle reference containing a bank with the sprites in them.  <br><br>Not sure how bmax handles cleaning up of the banks/arrays but I'm sure it can't be that hard. <br><br></td></tr></table><br>
<a name="1062651"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> I guess you dont even have to go that far Matty:<br><br><pre class=code>
Const ScreenWidth% = 800
Const ScreenHeight% = 600

Repeat

  Cls
  For Local i% = 0 to ScreenHeight
    For Local s:Tsprite = Eachin ObjectList
      if (int)s.y = i Then
        s.draw()
      end if;
    Next
  Next
  Flip

Until Appterminate()

</pre><br><br>LOL :D <br><br></td></tr></table><br>
<a name="1062665"></a>

<a name="1062666"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TaskMaster</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Wow, that will go through your object list 600 times each loop.  That is completely unnecessary.<br><br><font class="tiny">Last edited 2010</font> <br><br></td></tr></table><br>
<a name="1062669"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_JIM</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> I would sort them once per frame making sure to only sort sprites that are in the view. Worst case scenario, you could only sort them if necessary (say if one sprite moved and is touching anothers bounding box). You could even limit it to the moving sprites and "neighbours".<br><br>But optimize only when you need to. A qsort on all onscreen sprites should be fast enough for ones needs, unless you got thousands of sprites. Even then, probably the rendering would be your true bottleneck. <br><br></td></tr></table><br>
<a name="1062696"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Htbaa</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Don't forget your sort function should either return -1, 0 or 1. Not just True or False. <br><br></td></tr></table><br>
<a name="1062721"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> That is completely unnecessary. <br></div><br><br>@TaskMaster - I was joking!! :P<br><br>@Htbaa - I currently using the ZOrderSort which Czar provided:<br><br><pre class=code>
SortList list, False, ZOrderSor

Function ZOrderSort:Int(o1:Object, o2:Object)
	Local s1:TSprite = TSprite(o1)
	Local s2:TSprite = TSprite(o2)
	If s1 And s2
		Return s2.y - s1.y
	Else
		Return o1.Compare(o2)
	End if
End Function
</pre><br><br>This returns s2.y - s1.y (eg 500-600), how does this return true or false? <br><br></td></tr></table><br>
<a name="1062759"></a>

<a name="1062761"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matty</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Therevills - I'm not sure any of you understood my algorithm. Here it is in pseudocode - trust me it is very fast when there are lots of objects...<br><pre class=code>
; Screen Height 600 pixels.
Dim ZBuffer(600)

Loop Through Objects Once
	Array_Index = Object.Y
	If ZBuffer(Array_Index) = 0
		Create a Bank At ZBuffer(Array_Index)
		Insert Handle To Object In Bank
	Else
		Resize Bank By An Additional 'x' bytes
		Append Handle To Object In Bank
	Endif
End Loop

; Draw Routine
Loop Through Array 1 To 600
	If ZBuffer(Array_Index) &lt;&gt; 0
		Iterate Through Handles In Bank At This Index and Draw Each One
		
		Free The Bank At Array_Index 		
	Endif
End Loop

; This is actually a very quick sort method
</pre><br><br><font class="tiny">Last edited 2010</font> <br><br></td></tr></table><br>
<a name="1062799"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> If your game has a world larger than the screen, then you can subtract the current viewing coordinates from the objects' y coordinate.<br><br>I would use an array of lists rather than banks, personally. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
