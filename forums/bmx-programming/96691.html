<!DOCTYPE html><html lang="en" ><head ><title >Image Padder</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Image Padder</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Image Padder</a><br><br>
<a name="1119568"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I could'nt find a program which would open up a sprite sheet, pad the frames and then resave the image - so I coded one:<br><br><pre class=code>
' Image Padder

SuperStrict

' the number of pixels you want around your image (ie the border)
Const PADDING:Int = 4

' the width and height of the each frame
Const WIDTH:Int = 110
Const HEIGHT:Int = 160

' number of frames in the file
Const FRAMES:Int = 4


Local filter$="Image Files:png; All Files:*"
Local testfile$ = RequestFile("Select File...", filter, False, CurrentDir());

If testfile = "" Then 
	Notify "No source Image selected... ", True
	RuntimeError "No source Image selected... "
Else
	Print "Loading.... "+testfile
	Local image:TImage = LoadAnimImage(testfile, WIDTH, HEIGHT, 0, FRAMES)
	Local piximage:TPixmap = LoadPixmap(testfile)
	If image = Null
		Print "Error loading image..."+testfile
	Else
		Print "Processing..."
		Local array%[WIDTH, HEIGHT]
		Local currentFrame% = 0
		Local tmp:TPixmap=CreatePixmap:TPixmap(WIDTH*FRAMES + (PADDING*2*FRAMES), HEIGHT + (PADDING*2), PixmapFormat(piximage))
		
		While currentFrame &lt; FRAMES
			Print "Processing Frame = "+currentFrame
			
			Local tmpImage:TPixmap = LockImage(image, currentFrame)

			' store the pixels into an array					
			For Local x% = 0 To WIDTH - 1
				For Local y% = 0 To HEIGHT -1	
					Local argb:Int=ReadPixel(tmpImage, x, y)
					array[x, y] = argb
				Next
			Next
			
			' write the pixel
			Local argb%
			Local startX% = currentFrame * (WIDTH + (PADDING*2))
			Local startY% = 0
			For Local x% = startX To WIDTH + (PADDING * 2) + startX - 1
				For Local y% = startY To HEIGHT + (PADDING * 2) + startY - 1
					argb = $000000
					If x &gt;= startX + PADDING And x &lt; startX + WIDTH + PADDING And y &gt;= startY + PADDING And y &lt; startY + HEIGHT + PADDING
						argb = array[x - startX - PADDING, y - startY - PADDING]
					EndIf
					WritePixel(tmp, x, y, argb)
				Next
			Next
		
			currentFrame = currentFrame + 1
		Wend
		
		' save the image
		Local file:String = testfile + "_new"
		Print "Saving.... " + file
		If file &gt; "" SavePixmapPNG(tmp, file, 9)
	EndIf
EndIf

Print ""
Print "Finished..."
End
</pre><br><br>Of course this could be a lot better with a GUI, but meh ;) <br><br></td></tr></table><br>
<a name="1119599"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oh go on.....make the GUI :D <br><br></td></tr></table><br>
<a name="1119618"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jsp</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am working between the days, but I can do a little one tomorrow after work. <br><br></td></tr></table><br>
<a name="1119771"></a>

<a name="1119776"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jsp</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, here is a first version:<br><br>There are two different ways to work. Either load an image and do your settings and hit create, or drop a file onto the window and get a new image create without any extra clicks. Of course you can set the padding before.<br>The window is resizable and will show the loaded image and if you manipulate any value it will show the output, but only as pixmap in a panel. I thought that should be enough for such a thing.<br>The values you enter will be checked if they do fit and if not the textfield will turn red to indicate the problematic value.<br>The Statusbar shows some feedback.<br>I have only tried with little strips where the on the fly update of the image is not a problem, if it may is for large images I could add a switch for it.<br>If someone has an icon image I could add it, but I am very bad doing icons...<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'Source Code created on 28 Dec 2011 20:01:31 with Logic Gui Version 5.5 Build 470
'jsp
SuperStrict

Import MaxGui.Drivers

' the number of pixels you want around your image (ie the border)
Global Padding:Int
' the width and height of pixmap
Global Width:Int
Global Height:Int
' number of frames in the file
Global Frames:Int
'file to be loaded
Global File:String
'pixmap of strip
Global Pix:TPixmap





Global	Window1:TGadget
Global	grpOutput:TGadget
Global	txtHeight:TGadget
Global	txtFrames:TGadget
Global	txtPadding:TGadget
Global	txtWidth:TGadget
Global	BorderPanel1:TGadget
Global	btnCreate:TGadget
Global	lblPixData:TGadget

Window1:TGadget = CreateWindow:TGadget("Image Padder",333,167,452,353,Null,WINDOW_TITLEBAR|WINDOW_RESIZABLE |WINDOW_STATUS |WINDOW_CLIENTCOORDS |WINDOW_ACCEPTFILES )
	SetMinWindowSize( Window1:TGadget, 452 , 353 )
	grpOutput:TGadget = CreatePanel:TGadget(284,16,157,256,Window1:TGadget,PANEL_GROUP,"Output Params")
		SetGadgetLayout( grpOutput:TGadget,EDGE_CENTERED,EDGE_ALIGNED,EDGE_ALIGNED,EDGE_CENTERED )
		Local lblPadding:TGadget = CreateLabel:TGadget("Padding:",4,209,70,23,grpOutput:TGadget,LABEL_RIGHT)
		Local lblFrames:TGadget = CreateLabel:TGadget("Frames:",4,141,70,23,grpOutput:TGadget,LABEL_RIGHT)
		Local lblHeight:TGadget = CreateLabel:TGadget("Height:",4,73,70,23,grpOutput:TGadget,LABEL_RIGHT)
		Local lblWidth:TGadget = CreateLabel:TGadget("Width:",4,5,70,23,grpOutput:TGadget,LABEL_RIGHT)
		txtHeight:TGadget = CreateTextField:TGadget(89,70,50,23,grpOutput:TGadget,Null)
			SetGadgetText( txtHeight:TGadget,"")
			DisableGadget( txtHeight:TGadget )
			SetGadgetFilter( txtHeight:TGadget, txt_Filter )
		txtFrames:TGadget = CreateTextField:TGadget(89,138,50,23,grpOutput:TGadget,Null)
			SetGadgetText( txtFrames:TGadget,"")
			DisableGadget( txtFrames:TGadget )
			SetGadgetFilter( txtFrames:TGadget, txt_Filter )
		txtPadding:TGadget = CreateTextField:TGadget(89,206,50,23,grpOutput:TGadget,Null)
			SetGadgetText( txtPadding:TGadget,"0")
			SetGadgetFilter( txtPadding:TGadget, txt_Filter )
		txtWidth:TGadget = CreateTextField:TGadget(89,2,50,23,grpOutput:TGadget,Null)
			SetGadgetText( txtWidth:TGadget,"")
			DisableGadget( txtWidth:TGadget )
			SetGadgetFilter( txtWidth:TGadget, txt_Filter )
	BorderPanel1:TGadget = CreatePanel:TGadget(13,16,256,256,Window1:TGadget,PANEL_BORDER,"BorderPanel1")
		SetGadgetLayout( BorderPanel1:TGadget,EDGE_ALIGNED,EDGE_ALIGNED,EDGE_ALIGNED,EDGE_ALIGNED )
	Local SP6:TGadget = CreateLabel:TGadget("SP6",13,304,428,4,Window1:TGadget,LABEL_SEPARATOR)
		SetGadgetLayout( SP6:TGadget,EDGE_ALIGNED,EDGE_ALIGNED,EDGE_CENTERED,EDGE_ALIGNED )
	btnCreate:TGadget = CreateButton:TGadget("Create",352,320,75,23,Window1:TGadget,BUTTON_PUSH)
		DisableGadget( btnCreate:TGadget )
		SetGadgetLayout( btnCreate:TGadget,EDGE_CENTERED,EDGE_ALIGNED,EDGE_CENTERED,EDGE_ALIGNED )
	Local btnLoad:TGadget = CreateButton:TGadget("Load",184,320,75,23,Window1:TGadget,BUTTON_PUSH)
		SetGadgetLayout( btnLoad:TGadget,EDGE_CENTERED,EDGE_CENTERED,EDGE_CENTERED,EDGE_ALIGNED )
	Local btnQuit:TGadget = CreateButton:TGadget("Quit",22,320,75,23,Window1:TGadget,BUTTON_CANCEL)
		SetGadgetLayout( btnQuit:TGadget,EDGE_ALIGNED,EDGE_CENTERED,EDGE_CENTERED,EDGE_ALIGNED )
	lblPixData:TGadget = CreateLabel:TGadget("Pixmap Data",13,277,256,25,Window1:TGadget,LABEL_CENTER)
		SetGadgetLayout( lblPixData:TGadget,EDGE_ALIGNED,EDGE_ALIGNED,EDGE_CENTERED,EDGE_ALIGNED )

ActivateGadget(btnLoad)


Repeat
	WaitEvent()
	Select EventID()
		Case EVENT_WINDOWCLOSE
			Select EventSource()
				Case Window1	Window1_WC( Window1:TGadget )
			End Select

		Case EVENT_WINDOWACCEPT
			Select EventSource()
				Case Window1	Window1_WA( Window1:TGadget , String(EventExtra())  )
			End Select

		Case EVENT_GADGETACTION
			Select EventSource()
				Case txtHeight	txtHeight_GA( txtHeight:TGadget )
				Case txtFrames	txtFrames_GA( txtFrames:TGadget )
				Case txtPadding	txtPadding_GA( txtPadding:TGadget )
				Case txtWidth	txtWidth_GA( txtWidth:TGadget )
				Case btnCreate	btnCreate_GA( btnCreate:TGadget )
				Case btnLoad	btnLoad_GA( btnLoad:TGadget )
				Case btnQuit	btnQuit_GA( btnQuit:TGadget )
			End Select

	End Select
Forever

Function Window1_WC( Window:TGadget )
	DebugLog "Window Window1 wants to be closed"
	End
End Function

Function Window1_WA( Window:TGadget , Drop:String )
	DebugLog "Window Window1 got "+Drop+" dropped onto!"
	If Drop File=Drop$
	SetParams(File)
	Local FinalPixmap:TPixmap = CreatePadding(File:String,Int(GadgetText(txtWidth)),Int(GadgetText(txtHeight)),Int(GadgetText(txtFrames)),Int(GadgetText(txtPadding)))
	If FinalPixmap
		SetPanelPixmap(BorderPanel1,FinalPixmap,PANELPIXMAP_CENTER)
		Local Filename:String = StripExt( File )+"_new.PNG"
		If Filename SavePixmapPNG(FinalPixmap,Filename,9)
		SetStatusText(Window1, "Created File: "+StripDir(Filename))
	
	End If	
End Function

Function txtHeight_GA( TextField:TGadget )
	DebugLog "TextField txtHeight was modified"
	DebugLog "Text = "+ TextFieldText$( TextField:TGadget )
	DisplayPixmap()
End Function

Function txtFrames_GA( TextField:TGadget )
	DebugLog "TextField txtFrames was modified"
	DebugLog "Text = "+ TextFieldText$( TextField:TGadget )
	DisplayPixmap()
End Function

Function txtPadding_GA( TextField:TGadget )
	DebugLog "TextField txtPadding was modified"
	DebugLog "Text = "+ TextFieldText$( TextField:TGadget )
	DisplayPixmap()
End Function

Function txtWidth_GA( TextField:TGadget )
	DebugLog "TextField txtWidth was modified"
	DebugLog "Text = "+ TextFieldText$( TextField:TGadget )
	DisplayPixmap()
End Function

Function btnCreate_GA( Button:TGadget )
	DebugLog "Button btnCreate was pressed"
	If CheckParams() Return
	Local FinalPixmap:TPixmap = CreatePadding(File:String,Int(GadgetText(txtWidth)),Int(GadgetText(txtHeight)),Int(GadgetText(txtFrames)),Int(GadgetText(txtPadding)))
	If FinalPixmap
		SetPanelPixmap(BorderPanel1,FinalPixmap,PANELPIXMAP_CENTER)
		Local Filename:String = RequestFile("Save Pixmap PNG","PNG",True)
		If Filename SavePixmapPNG(FinalPixmap,Filename,9)
	End If
End Function

Function btnLoad_GA( Button:TGadget )
	DebugLog "Button btnLoad was pressed"
	Local filter$="Image Files:png; All Files:*"
	File$ =RequestFile("Select File...", filter, False, CurrentDir());
	SetParams(File)
End Function

Function btnQuit_GA( Button:TGadget )
	DebugLog "Button btnQuit was pressed"
	End
End Function


Function CreatePadding:TPixmap(testFile:String, Width:Int, Height:Int,Frames:Int, Padding:Int)
	If Not testfile Notify "No source Image selected... ", True ; Return Null
	
	SetPointer(POINTER_WAIT)
	SetStatusText(Window1, "Loading.... "+testfile)

	Local image:TImage = LoadAnimImage(Pix, Width, Height, 0, FRAMES)
	
	If image = Null
		SetStatusText(Window1, "Error loading image..."+testfile)
	Else
		SetStatusText(Window1, "Processing...")
		Local array%[Width, Height]
		Local currentFrame% = 0
		Local tmp:TPixmap=CreatePixmap:TPixmap(Width*(FRAMES) + (PADDING*2*FRAMES), Height + (PADDING*2), PixmapFormat(pix))
		
		While currentFrame &lt; FRAMES
			SetStatusText(Window1, "Processing Frame = "+currentFrame)
			
			Local tmpImage:TPixmap = LockImage(image, currentFrame)

			' store the pixels into an array					
			For Local X% = 0 To Width - 1
				For Local Y% = 0 To Height -1	
					Local argb:Int=ReadPixel(tmpImage, X, Y)
					array[X, Y] = argb
				Next
			Next
			
			' write the pixel
			Local argb%
			Local startX% = currentFrame * (Width + (PADDING*2))
			Local startY% = 0
			For Local X% = startX To Width + (PADDING * 2) + startX - 1
				For Local Y% = startY To Height + (PADDING * 2) + startY - 1
					argb = $000000
					If X &gt;= startX + PADDING And X &lt; startX + Width + PADDING And Y &gt;= startY + PADDING And Y &lt; startY + Height + PADDING
						argb = array[X - startX - PADDING, Y - startY - PADDING]
					EndIf
					WritePixel(tmp, X, Y, argb)
				Next
			Next
		
			currentFrame = currentFrame + 1
		Wend
		SetPointer(POINTER_DEFAULT)
		
		SetStatusText(Window1, "Processing Done! "+currentFrame+" Frames created")
		
		Return tmp
	EndIf
End Function

Function SetParams(File:String)
	If File
		Pix:TPixmap =LoadPixmap(File)
		If Pix
			Width=Pix.Width
			Height=Pix.Height
			Frames=Pix.Width/Pix.Height
			SetGadgetText(txtWidth,Height)
			SetGadgetText(txtHeight,Height)
			SetGadgetText(txtFrames,Frames)
			EnableGadget(btnCreate)
			EnableGadget(txtWidth)
			EnableGadget(txtHeight)
			EnableGadget(txtFrames)
			DisplayPixmap()
			SetGadgetText(lblPixData,"File: "+StripAll(File)+"    Size: "+Width+"x"+Height+"    Frames: "+Frames)
			SetStatusText(Window1, "Pixmap loaded...")

		End If
	End If
End Function

Function txt_Filter:Int( event:TEvent,context:Object )
	DebugLog "TextField txtWidth filtering text input: "+Chr(Event.data)+"="+Event.Data+","+Event.Mods
	Select Event.ID
	    Case EVENT_KEYDOWN
	        If Event.Data = KEY_BACKSPACE Then Return True
	        If Event.Data = KEY_DELETE Then Return True
	    Case EVENT_KEYCHAR
	        If Event.Data =&gt; KEY_0 And Event.Data &lt;= KEY_9 Then Return True
	        If Event.Data = KEY_BACKSPACE Then Return True
	End Select
	Return False
End Function

Function DisplayPixmap()
	If CheckParams() Return
	Local FinalPixmap:TPixmap = CreatePadding(File:String,Int(GadgetText(txtWidth)),Int(GadgetText(txtHeight)),Int(GadgetText(txtFrames)),Int(GadgetText(txtPadding)))
	If FinalPixmap
		SetPanelPixmap(BorderPanel1,FinalPixmap,PANELPIXMAP_CENTER)
	End If
End Function

Function CheckParams:Int()
	Local FinalWidth:Int,FinalHeight:Int,FinalFrames:Int,FinalPadding:Int,ReturnFlag:Int
	FinalWidth=Int(GadgetText(txtWidth))
	FinalFrames=Int(GadgetText(txtFrames))
	If FinalWidth &lt; 1 Or (FinalWidth *FinalFrames+ (PADDING*2*FinalFrames)) &gt; Width*FinalFrames+ (PADDING*2*FinalFrames) Then
		SetGadgetColor( txtWidth:TGadget,200,150,150,True )
		SetGadgetColor( txtFrames:TGadget,200,150,150,True )
		ReturnFlag=True
	Else
		SetGadgetColor( txtWidth:TGadget,255,255,255,True )
		SetGadgetColor( txtFrames:TGadget,255,255,255,True )	
	End If
	FinalHeight=Int(GadgetText(txtHeight))
	If FinalHeight &lt; 1 Or (FinalHeight + (PADDING*2)) &gt; (Height + (PADDING*2))
		SetGadgetColor( txtHeight:TGadget,200,150,150,True )
		ReturnFlag=True
	Else
		SetGadgetColor( txtHeight:TGadget,255,255,255,True )
	End If
	FinalFrames=Int(GadgetText(txtFrames))
	If FinalWidth &gt; 1 And FinalHeight &gt; 0
		If FinalFrames &lt; 1 Or FinalFrames &gt; (((Height/FinalHeight)*Width)/FinalWidth)
			SetGadgetColor( txtFrames:TGadget,200,150,150,True )
			ReturnFlag=True
		Else
			SetGadgetColor( txtFrames:TGadget,255,255,255,True )
		End If
	End If
	FinalPadding=Int(GadgetText(txtPadding))
	If FinalPadding &lt; 0 Or FinalPadding &gt; 1000 Then
		SetGadgetColor( txtPadding:TGadget,200,150,150,True )
		ReturnFlag=True
	Else
		SetGadgetColor( txtPadding:TGadget,255,255,255,True )
	End If
	If ReturnFlag Then DisableGadget(btnCreate) Else EnableGadget(btnCreate)
	Return ReturnFlag
End Function
</textarea><br><br>Little Update: A wait cursor is now shown during processing and the padding value is checked as well.<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1119792"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Very nice jsp! Good job! <br><br></td></tr></table><br>
<a name="1119794"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> nice one jsp :)<br><br>cheers! <br><br></td></tr></table><br>
<a name="1119898"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jsp</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks guys!<br>Let me know if you need any changes. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
