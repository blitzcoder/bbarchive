<!DOCTYPE html><html lang="en" ><head ><title >Download file with callback</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Download file with callback</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Download file with callback</a><br><br>
<a name="723797"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> The command CopyFile() can download a file from the internet and copy it to the user's hard drive.  I want to write a new copyfile function with a callback, so that the download progress can be displayed:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Import leadwerks.lwmath

Function CopyFile2:Int(src$,dst$="",callback:Byte Ptr=Null)
	src$=Replace(src,"\","/")
	src=Replace(src,"://","::")
	If dst="" dst=AppDir$+"/"+StripDir(src)
	
	If callback=Null
		Return CopyFile(src,dst)
	Else
	
		Local DownloadProgressCallback(p#)
		DownloadProgressCallback=callback
	
		out:TStream=WriteFile(dst)
		If Not out Return
		in:TStream=ReadFile(src)
		If Not in
			out.close()
			Return
		EndIf
		
		size=in.size()
		While Not Eof(in)
			WriteByte out,ReadByte(in)

			newprogress#=Float(in.pos())/Float(in.size())
			'If Round(newprogress*100.0)&gt;Round(progress#*100.0)
			'	progress=newprogress
			DownloadProgressCallback newprogress
			'EndIf
		Wend
		
		in.close()
		out.close()
		Return True
		
	EndIf
EndFunction

win:TGadget=CreateWindow("",200,200,400,300,Null,WINDOW_TITLEBAR)
Global progbar:TGadget=CreateProgBar(10,10,win.clientwidth()-20,28,win)
CopyFile2("http://www.leadwerks.com/index.php","",DLCallBack)

Function DLCallBack(progress:Float)
	UpdateProgBar progbar,progress
	If PeekEvent() WaitEvent()
EndFunction</textarea><br><br>The problem is that a URL opened with ReadStream() has an unknown size.  If I pre-cache the file with ReadFile(), the entire file is loaded, so I know the size, but I can't display the progress until after the actual file data is already downloaded.<br><br>How can I find out the size of a file on the internet, or a stream opened with ReadStream()? <br><br></td></tr></table><br>
<a name="723800"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kev</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> a post by Yan sometime back, this will return the files size without downloading.<br><br><pre class=code>
Print HTTPFileSize("http://YOUR URL TO FILE") + " Bytes"

End


Function HTTPFileSize(url$)
	url$ = url$.Replace("http://", "")
	Local slashPos = url$.Find("/"), host$, file$
	
	If slashPos &lt;&gt; -1
		host$ = url$[..slashPos]
		file$ = url$[slashPos..]
	Else
		Return -1
	EndIf
	
	Local stream:TStream=OpenStream("tcp::" + host$, 80)
	If Not stream Then Return -1	
	
	stream.WriteLine "HEAD " + file$ + " HTTP/1.0"
	stream.WriteLine "Host: " + host$
	stream.WriteLine ""
	
	While Not Eof(stream)
		Local in$ = stream.ReadLine()

		If in$.Find("Content-Length:") &lt;&gt; -1
			stream.Close()
			Return Int(in$[in$.Find(":") + 1..].Trim())
		EndIf	
	Wend
	
	stream.Close()
	Return -1		
End Function
</pre> <br><br></td></tr></table><br>
<a name="723813"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Okay, this works.  Now I just need to figure out how to stop the transfer if the connection is broken:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Global win:TGadget=CreateWindow("",Desktop().ClientWidth()/2-200,Desktop().ClientHeight()/2-50,400,100,Null,WINDOW_TITLEBAR)
CreateLabel("Downloading...",10,10,100,16,win)
Global progbar:TGadget=CreateProgBar(10,10+16,win.clientwidth()-20,28,win)
file$="http://blitzbasic.com/file/get.php?file=/Products/demos/Blitz3DDemo183.exe"
If CopyFile2(file$,"",DLCallBack)
	Notify "Download successful."
	OpenURL AppDir$+"\"+StripDir(file)
Else
	Notify "Download failed."
EndIf

Function CopyFile2:Int(src$,dst$="",callback:Byte Ptr=Null)
	url$=src
	src$=Replace(src,"\","/")
	src=Replace(src,"://","::")
	If dst="" dst=AppDir$+"/"+StripDir(src)
	
	If callback=Null
	
		'Just use the regular CopyFile()
		Return CopyFile(src,dst)

	Else
	
		'Set up the callback function
		Local DownloadFileUpdateCallback(p#)
		DownloadFileUpdateCallback=callback

		'Get the file size
		size=FileSize2(url)
		If size=-1 Return
	
		'Open streams
		out:TStream=WriteFile(dst)
		If Not out Return
		in:TStream=ReadStream(src)
		If Not in
			out.close()
			Return
		EndIf
		
		'Copy file and display progress		
		While Not Eof(in)
			pos=pos+1
			WriteByte out,ReadByte(in)
			newprogress#=Float(pos)/Float(size)
			If Round(newprogress*100.0)&gt;Round(progress#*100.0)
				progress=newprogress
				DownloadFileUpdateCallback progress
			EndIf
		Wend
		
		'Close the streams
		in.close()
		out.close()
		
		'Verify file size
		If FileSize(dst)&lt;&gt;size
			DeleteFile dst
			Return
		EndIf
		
		Return True
		
	EndIf
EndFunction

Function Round(val#)
	Local dec#
	dec#=val-Floor(val)
	If dec&lt;0.5 Return Floor(val) Else Return Ceil(val)
EndFunction

Function DLCallBack(progress:Float)
	UpdateProgBar progbar,progress
	If PeekEvent() WaitEvent()
EndFunction

Function FileSize2:Int(url$)
	If Instr(url,"http://")
		url$ = url$.Replace("http://", "")
		Local slashPos = url$.Find("/"), host$, file$
		
		If slashPos &lt;&gt; -1
			host$ = url$[..slashPos]
			file$ = url$[slashPos..]
		Else
			Return -1
		EndIf
		
		Local stream:TStream=OpenStream("tcp::" + host$, 80)
		If Not stream Then Return -1	
		
		stream.WriteLine "HEAD " + file$ + " HTTP/1.0"
		stream.WriteLine "Host: " + host$
		stream.WriteLine ""
		
		While Not Eof(stream)
			Local in$ = stream.ReadLine()
	
			If in$.Find("Content-Length:") &lt;&gt; -1
				stream.Close()
				Return Int(in$[in$.Find(":") + 1..].Trim())
			EndIf	
		Wend
		
		stream.Close()
		Return -1
	Else
		Return FileSize(url)
	EndIf
End Function</textarea> <br><br></td></tr></table><br>
<a name="723819"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Now this is cool.  I fixed it so if you the connection is list, the function just returns false.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Global win:TGadget=CreateWindow("",Desktop().ClientWidth()/2-200,Desktop().ClientHeight()/2-50,400,100,Null,WINDOW_TITLEBAR)
CreateLabel("Downloading...",10,10,100,16,win)
Global progbar:TGadget=CreateProgBar(10,10+16,win.clientwidth()-20,28,win)

file$="http://blitzbasic.com/file/get.php?file=/Products/demos/Blitz3DDemo183.exe"

If CopyFile2(file$,"",DisplayDownloadProgress)
	Notify "Download successful."
	OpenURL AppDir$+"\"+StripDir(file)
Else
	Notify "Download failed."
EndIf


Function CopyFile2:Int(src$,dst$="",callback:Byte Ptr=Null,bufsize=128)
	url$=src
	src$=Replace(src,"\","/")
	src=Replace(src,"://","::")
	If dst="" dst=AppDir$+"/"+StripDir(src)
	
	If callback=Null
	
		'Just use the regular CopyFile()
		Return CopyFile(src,dst)

	Else
	
		'Set up the callback function
		Local DownloadFileUpdateCallback(p#)
		DownloadFileUpdateCallback=callback

		'Get the file size
		size=FileSize2(url)
		If size=-1 Return
	
		'Open streams
		out:TStream=WriteFile(dst)
		If Not out Return
		in:TStream=ReadStream(src)
		If Not in
			out.close()
			Return
		EndIf
		
		'Copy file and display progress
		Try
			Local buf:Byte[bufSize]
			While Not in.Eof()
				out.WriteBytes buf,in.Read(buf,bufSize)
				pos=pos+bufsize
				progress#=Float(pos)/Float(size)
				DownloadFileUpdateCallback progress
			Wend
			Catch ex:TStreamWriteException
		EndTry

		'Close the streams
		in.close()
		out.close()
		
		'Verify file size
		If FileSize(dst)&lt;&gt;size
			DeleteFile dst
			Return
		EndIf
		
		Return True
		
	EndIf
EndFunction

Function Round(val#)
	Local dec#
	dec#=val-Floor(val)
	If dec&lt;0.5 Return Floor(val) Else Return Ceil(val)
EndFunction

Function DisplayDownloadProgress(progress:Float)
	UpdateProgBar progbar,progress
	If PeekEvent() WaitEvent()
EndFunction

Function FileSize2:Int(url$)
	If Instr(url,"http://")
		url$ = url$.Replace("http://", "")
		Local slashPos = url$.Find("/"), host$, file$
		
		If slashPos &lt;&gt; -1
			host$ = url$[..slashPos]
			file$ = url$[slashPos..]
		Else
			Return -1
		EndIf
		
		Local stream:TStream=OpenStream("tcp::" + host$, 80)
		If Not stream Then Return -1	
		
		stream.WriteLine "HEAD " + file$ + " HTTP/1.0"
		stream.WriteLine "Host: " + host$
		stream.WriteLine ""
		
		While Not Eof(stream)
			Local in$ = stream.ReadLine()
	
			If in$.Find("Content-Length:") &lt;&gt; -1
				stream.Close()
				Return Int(in$[in$.Find(":") + 1..].Trim())
			EndIf	
		Wend
		
		stream.Close()
		Return -1
	Else
		Return FileSize(url)
	EndIf
End Function</textarea><br><br>And this will automatically detect and download app updates:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Framework BRL.System
Import Pub.Win32
Import BRL.HTTPStream
Import BRL.Retro
Import BRL.Win32MaxGUI

Extern "win32"
	Function FreeLibrary:Int(hlib)="FreeLibrary@4"
EndExtern

Global SERVERNAME$="www.frauleinspace.com"
Global PRODUCTNAME$="LW3D"
Global PROGBAR:TGADGET
Global BUTTON_CANCEL:TGadget

Global VERSION_MAJOR=0
Global VERSION_MINOR=0
Global VERSION_BUGFIX=0

Notify webupdate()

Function SetUpdateServer(hservername:Byte Ptr)
	servername$=string.fromcstring(hservername)
	MemFree hservername
EndFunction

Function SetUpdateProduct(hproductname:Byte Ptr)
	productname$=string.fromcstring(hproductname)
	MemFree hproductname
EndFunction

Function SetUpdateVersion(major,minor,bugfix)
	VERSION_MAJOR=major
	VERSION_MINOR=minor
	VERSION_BUGFIX=bugfix
EndFunction

Function WebUpdate:Int()

	If Not DeleteFile(CurrentDir$()+"\update.exe") Return
	result=1
	
	Local InternetAttemptConnect:Int(dwReserved:Int)
	hlib=loadlibrarya("wininet.dll")
	If hlib
		InternetAttemptConnect=GetProcAddress(hlib,"InternetAttemptConnect")
		InternetAttemptConnect(0)
		freelibrary hlib
	EndIf
	
	If filesize2("http://"+SERVERNAME+"/data/"+PRODUCTNAME+"/data.ini")=-1 Return
	stream:TStream=ReadFile("http::"+SERVERNAME+"/data/"+PRODUCTNAME+"/data.ini")
	If Not stream Return
	While Not stream.eof()
		s$=stream.readline()
		name$=Lower(Trim(piece(s,1,"=")))
		value$=piece(s,2,"=")
		value=Replace(value,Chr(34),"")
		value=value.trim()
		Select name
			Case "latestversion"
				major=Int(piece(value,1,"."))
				minor=Int(piece(value,2,"."))
				bugfix=Int(piece(value,3,"."))
				If major&gt;VERSION_MAJOR
					update=True
				ElseIf major=VERSION_MAJOR
					If minor&gt;VERSION_MINOR
						update=True
					ElseIf minor=VERSION_MINOR
						If bugfix&gt;VERSION_BUGFIX
							update=True
						Else
							update=False
						EndIf
					Else
						update=False
					EndIf
				Else
					update=False
				EndIf
			Case "latestversionurl"
				updateurl$=value
		EndSelect
	Wend

	If update
		result=result+2
		
		win:TGadget=CreateWindow("",Desktop().ClientWidth()/2-160,Desktop().ClientHeight()/2-50,320,86,Null,WINDOW_HIDDEN)
		hwnd=QueryGadget(win,QUERY_HWND_CLIENT)
		style=getwindowlonga(hwnd,GWL_STYLE)
		setwindowlonga hwnd,GWL_STYLE,style+WS_DLGFRAME
		SetWindowPos hwnd, -1, 0, 0, 0, 0, 3
		SetGadgetShape(win,GadgetX(win),GadgetY(win),GadgetWidth(win)+1,GadgetHeight(win)+1)
		SetGadgetShape(win,GadgetX(win),GadgetY(win),GadgetWidth(win)-1,GadgetHeight(win)-1)
		CreateLabel("Updating...",10,10,100,16,win)
		progbar=CreateProgBar(10,10+16,win.clientwidth()-20,20,win)
		BUTTON_CANCEL=CreateButton("Cancel",win.clientwidth()/2-30,win.clientheight()-26,60,24,win)
		ShowGadget win
		
		If CopyFile2(updateurl$,CurrentDir$()+"\update.exe",DisplayDownloadProgress,64)
			result=result+4
		Else
			
			DeleteFile(CurrentDir$()+"\update.exe")
		EndIf
		
		HideGadget win
		FreeGadget win
		
	EndIf

	Return result
EndFunction

Function DisplayDownloadProgress:Int(progress:Float)
	UpdateProgBar progbar,progress
	If PeekEvent()
		Select WaitEvent()
			Case EVENT_GADGETACTION
				Select EventSource()
					Case BUTTON_CANCEL
						If Confirm("Cancel update?") Return True
				EndSelect
		EndSelect
	EndIf
EndFunction

Function CopyFile2:Int(src$,dst$="",callback:Byte Ptr=Null,bufsize=4096)
	url$=src
	src$=Replace(src,"\","/")
	src=Replace(src,"://","::")
	If dst="" dst=AppDir$+"/"+StripDir(src)
	
	If callback=Null
	
		'Just use the regular CopyFile()
		Return CopyFile(src,dst)

	Else

		'Set up the callback function
		Local DownloadFileUpdateCallback:Int(p#)
		DownloadFileUpdateCallback=callback

		'Get the file size
		size=FileSize2(url)
		If size=-1 Return
	
		'Open streams
		out:TStream=WriteFile(dst)
		If Not out Return
		in:TStream=ReadStream(src)
		If Not in
			out.close()
			Return
		EndIf
		
		'Copy file and display progress
		Try
			Local buf:Byte[bufSize]
			While Not in.Eof()
				out.WriteBytes buf,in.Read(buf,bufSize)
				pos=pos+bufsize
				progress#=Float(pos)/Float(size)
				If DownloadFileUpdateCallback(progress)
					in.close()
					out.close()
					Return False
				EndIf
			Wend
			Catch ex:TStreamWriteException
		EndTry

		'Close the streams
		in.close()
		out.close()
		
		'Verify file size
		If FileSize(dst)&lt;&gt;size
			DeleteFile dst
			Return
		EndIf
		
		Return True
		
	EndIf
EndFunction

Function Piece$(s$,entry,char$=" ")
	Local n
	Local p
	Local a$
	While Instr(s,char+char)
		s=Replace(s,char+char,char)
	Wend
	For n=1 To entry-1
		p=Instr(s,char)
		s=Right(s,Len(s)-p)
		If Not p
			If entry=n Exit Else Return
		EndIf
	Next
	p=Instr(s,char)
	If p&lt;1
		a$=s
	Else
		a=Left(s,p-1)
	EndIf
	Return a
End Function

Function FileSize2:Int(url$)
	If Instr(url,"http://")
		url$ = url$.Replace("http://", "")
		Local slashPos = url$.Find("/"), host$, file$
		
		If slashPos &lt;&gt; -1
			host$ = url$[..slashPos]
			file$ = url$[slashPos..]
		Else
			Return -1
		EndIf
		
		Local stream:TStream=OpenStream("tcp::" + host$, 80)
		If Not stream Then Return -1	
		
		stream.WriteLine "HEAD " + file$ + " HTTP/1.0"
		stream.WriteLine "Host: " + host$
		stream.WriteLine ""
		
		While Not Eof(stream)
			Local in$ = stream.ReadLine()
	
			If in$.Find("Content-Length:") &lt;&gt; -1
				stream.Close()
				Return Int(in$[in$.Find(":") + 1..].Trim())
			EndIf	
		Wend
		
		stream.Close()
		Return -1
	Else
		Return FileSize(url)
	EndIf
End Function</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
