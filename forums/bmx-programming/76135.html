<!DOCTYPE html><html lang="en" ><head ><title >Dealing out cards</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Dealing out cards</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Dealing out cards</a><br><br>
<a name="851094"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >z80jim</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have been using blitzmax for years, though I haven't taken great steps to learn it, I'm only now getting into GUI, and one particular problem has been bothering me. I can't seem to get the darn thing to deal out a deck of cards!<br><br>here's my most successful attempt, though it still won't work properly and I'm stumped. Can anyone help?<br><br><br><br>SeedRnd MilliSecs()<br><br>Global dealnum:Int<br>Global dealsuit:Int<br>Global dealtnum:Int[13]<br>Global dealtsuit:Int[4]<br>Global finalnum:String[13]<br>Global finalsuit:String[4]<br>Global cardnumber:Int[13]<br>Global cardsuit:Int[4]<br>Global deal:Int<br>Global dealdeal:Int<br>Global dealer:Int<br>Graphics 640,480'not neccessary for now, only feedback is through print commands.<br><br>While Not KeyHit(key_q)'mean loop, doesn't do anything yet<br>	deala<br>	'display<br>	End<br>Wend<br><br>Function printatize()<br>		If dealtsuit[c]=0<br>			finalsuit[c]="Hearts"<br>		EndIf<br>		If dealtsuit[c]=1<br>			finalsuit[c]="Diamonds"<br>		EndIf<br>		If dealtsuit[c]=2<br>			finalsuit[c]="Clubs"<br>		EndIf<br>		If dealtsuit[c]=3<br>			finalsuit[c]="Spades"<br>		EndIf<br>		If dealtsuit[c]=3<br>			finalsuit[c]="Spades"<br>		EndIf<br>		<br>		<br>		'... and numbers<br>		If dealtnum[i]=0<br>			finalnum[i]="Two"<br>		EndIf<br>		If dealtnum[i]=1<br>			finalnum[i]="Three"<br>		EndIf<br>		If dealtnum[i]=2<br>			finalnum[i]="Four"<br>		EndIf<br>		If dealtnum[i]=3<br>			finalnum[i]="Five"<br>		EndIf<br>		If dealtnum[i]=4<br>			finalnum[i]="Six"<br>		EndIf<br>		If dealtnum[i]=5<br>			finalnum[i]="Seven"<br>		EndIf<br>		If dealtnum[i]=6<br>			finalnum[i]="Eight"<br>		EndIf<br>		If dealtnum[i]=7<br>			finalnum[i]="Nine"<br>		EndIf<br>		If dealtnum[i]=8<br>			finalnum[i]="Ten"<br>		EndIf<br>		If dealtnum[i]=9<br>			finalnum[i]="Jack"<br>		EndIf<br>		If dealtnum[i]=10<br>			finalnum[i]="Queen"<br>		EndIf<br>		If dealtnum[i]=11<br>			finalnum[i]="King"<br>		EndIf<br>		If dealtnum[i]=12<br>			finalnum[i]="Ace"<br>		EndIf<br>End Function<br><br>Function deala()<br>	For dealnum=0 To 12<br>		For dealsuit=0 To 3<br>			Cls<br>			deal=Rnd(0,12)<br>			dealtnum[deal]=dealnum<br>			deal=Rnd(0,3)<br>			dealtsuit[deal]=dealsuit<br>			DrawText dealnum,1,1<br>			printatize<br>			Print finalnum[i]+" of "+finalsuit[c]<br>			Flip<br>		Next<br>	Next<br>End Function<br><br>Function display()<br>	For i=0 To 12<br>		For c=0 To 3<br>			printatize<br>			Print finalnum[i]+" of "+finalsuit[c]<br>		Next<br>	Next<br>EndFunction <br><br></td></tr></table><br>
<a name="851178"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TwoCorin517</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> This doesn't quite work, but I hope it inspires you.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
SeedRnd MilliSecs()

Type TCard
	Field Face:String
	Field Suit:String
End Type

Type TDeck
	Field Cards:TCard[] 
	
	Method Create(NumCards:Int = 52) 
		Local Loop_Var:Int
		
		Cards:TCard = New TCard[NumCards] 
		For Loop_Var = 0 To Cards.Length - 1
			Cards[Loop_Var] = New TCard
		Next
	End Method
	
	Method Shuffle() 
		Local TempArray:TCard[Cards.Length] 
		Local Active:Int[Cards.Length] 
		Local Return_Array:TCard[Cards.Length] 
		Local Loop_Var:Int
		Local New_Field:Int
		Local TimesAccessed:Int
		
		TempArray = Cards
		
		For Loop_Var = 0 To Cards.Length - 1
			Repeat
				New_Field = Rnd(0, Cards.Length - 1) 
				If Active[New_Field] = False
					Return_Array[Loop_Var] = TempArray[New_Field] 
					Active[New_Field] = True
					Exit
				Else
					If TimesAccessed &gt; 52
						Exit
					End If
				EndIf
				
				If Loop_Var = 51
					Return_Array[Loop_Var] = TempArray[New_Field] 
					Active[New_Field] = True
					Exit
				EndIf
			Forever
		Next
		Cards = Return_Array
	End Method
	
	Method Set() 
		Local Loop_Var1:Int
		Local Loop_Var2:Int
		Local Loop_Var3:Int
		
		For Loop_Var1 = 0 To 12
			For Loop_Var2 = 0 To 3
				If Loop_Var1 = 0
					Cards[Loop_Var3].Face = "Ace"
				ElseIf Loop_Var1 = 1
					Cards[Loop_Var3].Face = "2"
				ElseIf Loop_Var1 = 2
					Cards[Loop_Var3].Face = "3"
				ElseIf Loop_Var1 = 3
					Cards[Loop_Var3].Face = "4"
				ElseIf Loop_Var1 = 4
					Cards[Loop_Var3].Face = "5"
				ElseIf Loop_Var1 = 5
					Cards[Loop_Var3].Face = "6"
				ElseIf Loop_Var1 = 6
					Cards[Loop_Var3].Face = "7"
				ElseIf Loop_Var1 = 7
					Cards[Loop_Var3].Face = "8"
				ElseIf Loop_Var1 = 8
					Cards[Loop_Var3].Face = "9"
				ElseIf Loop_Var1 = 9
					Cards[Loop_Var3].Face = "10"
				ElseIf Loop_Var1 = 10
					Cards[Loop_Var3].Face = "Jack"
				ElseIf Loop_Var1 = 11
					Cards[Loop_Var3].Face = "Queen"
				ElseIf Loop_Var1 = 12
					Cards[Loop_Var3].Face = "King"
				EndIf
		
				If Loop_Var2 = 0
					Cards[Loop_Var3].Suit = "Spades"
				ElseIf Loop_Var2 = 1
					Cards[Loop_Var3].Suit = "Clubs"
				ElseIf Loop_Var2 = 2
					Cards[Loop_Var3].Suit = "Hearts"
				ElseIf Loop_var2 = 3
					Cards[Loop_Var3].Suit = "Diamonds"
				EndIf
			
				Loop_Var3:+1
			Next
		Next
	End Method
End Type

Global MyDeck:TDeck = New TDeck
MyDeck.Create()
MyDeck.Set()
MyDeck.Shuffle() 
Local Loop_Var:Int

For Loop_Var = 0 To MyDeck.Cards.Length - 1
	Print MyDeck.Cards[Loop_Var].Face + " of " + MyDeck.Cards[Loop_Var].Suit
Next
</textarea> <br><br></td></tr></table><br>
<a name="851189"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >z80jim</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> might I ask why it doesn't work though? It seemed to run just fine, it fixed the problems I was having lol.<br>You've used a lot of stuff that I'm only slightly familiar with... can you please please please add notes so I know what's doing what? Thanks a million for replying! I'm specifically only slightly knowledgeable on the topic of types... I used to kinda know how to do a simple one, but I forgot lol. <br><br></td></tr></table><br>
<a name="851193"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Perturbatio</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's mine, I would've finished it about two hours ago if I wasn't watching TV at the same time :)<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

SeedRnd( MilliSecs() )'seed the random number generator to help prevent pattern repeating

Global cardNames:String[] = ["Ace", "Two", "Three", "Four", "Five", "Six", "Seven", "Eight", "Nine", "Ten", "Jack", "Queen", "King"]
Global suits:String[] = ["Hearts", "Clubs", "Diamonds", "Spades"]
Global deck:TDeck = TDeck.Create()

Const SUIT_HEARTS:Int = 0
Const SUIT_CLUBS:Int = 1
Const SUIT_DIAMONDS:Int = 2
Const SUIT_SPADES:Int = 3

TPlayer.Create("Perturbatio")
TPlayer.Create("Barturpetio")
TPlayer.Create("Trupertabio")
TPlayer.Create("Bob")

deck.reset()

deck.Shuffle()

deck.Deal(7)

For Local p:TPlayer = EachIn TPlayer.list
	p.ShowHand()
Next



Type TPlayer
	Global list:TList
	Field Name:String
	Field Hand:TCard[]
	
	
	Method addCardToHand( card:TCard )
		Hand = Hand[..Hand.length+1]
		Hand[Hand.length-1] = card
	End Method
	
	
	Method ShowHand()
		Local out:String = ""
		Local total:Int
		For Local card:TCard = EachIn Hand
			out:+ "Card: "+card.Name+", Value: "+Min(10, card.Value)+"~r~n"
			
			total:+Min(10, card.Value)
		Next
		Print out+"~r~nName:"+Name+"~r~nTotal: "+total+"~r~n~r~n"
	End Method
	
	
	Function Create:TPlayer(Name:String)
		If list = Null Then list = New TList
		Local temp:TPlayer = New TPlayer
			temp.Name = Name
			list.addLast(temp)
		Return temp
	End Function
	
End Type



Type TCard
	Field Name:String
	Field Suit:Int
	Field Value:Int
	
	Function Create:TCard(value:Int, suit:Int)
		
		Local temp:TCard = New TCard
			temp.Name = cardNames[value-1]+" of "+suits[suit]
			temp.Suit = suit
			temp.Value = value
		Return temp
		
	End Function
End Type


Type TDeck
	Field cards:TCard[52]
	
	
	Method Shuffle(numShuffles:Int = 20)
		Local Cut:TCard[]
		Local out:String
		
		For Local i:Int = 1 To numShuffles
		
			Local Split1:Int = Rand(20, 30) 'remove approximately half the pack from the bottom
				'Print Split1
				Cut = cards[Split1..]
				cards= cards[..Split1]
				
			Local Split2:Int = Rand(0, Len(Cut)/2) 'Drop part of that from the top onto the top of the main pack
				cards= Cut[..Split2]+cards'ConcatIntArray(Cut[..Split2], deck)
				Cut = Cut[Split2..]
				
			Local Split3:Int = Rand(0, Len(Cut)/2) 'drop the next part on top of that
				cards= Cut[..Split3]+cards'ConcatIntArray(Cut[..Split3], deck)
				Cut = Cut[Split3..]
			'drop the rest
			cards:+ Cut 'ConcatIntArray(Cut, deck)
			
		Next
		
		'Print out
		
	End Method
	
	
	Method pop:TCard()
		
		Local card:TCard
			card = cards[cards.length - 1] 'get the top card
			cards = cards[..cards.length - 1] 'wipe it from the cards array
		Return card
		
	End Method
	
	
	Method Deal(numCards:Int)
		If ( TPlayer.list.count() * numCards ) &lt;= 52 Then
		
			For Local cardCount:Int = 1 To numCards
			
				For Local player:TPlayer = EachIn TPlayer.list
					
					player.addCardToHand(deck.pop())
					
				Next 'player
				
			Next 'cardCount
			
		Else
			RuntimeError "Not enough cards for all the players"
		EndIf
	End Method
	

	Method Reset()
		cards:TCard = New TCard[52]
		Local suit:Int = -1
		Local count:Int = 0
		For Local suit:Int = 0 To 3
			For Local value:Int = 1 To 13
				cards[ count ] = TCard.Create( value, suit )
				count:+1
			Next 'value
		Next 'suit
		
	End Method
	
	
	Function Create:TDeck()
		Local temp:TDeck = New TDeck
			temp.Reset()
		Return temp
	End Function
	
End Type
</textarea> <br><br></td></tr></table><br>
<a name="851208"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TwoCorin517</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sorry for the slow reply earlier, I was 4 hrs. late, but as my excuse, I live in the real world, and I had to write the code from scratch.<br><br>It works for the most part, but I thought I saw a repeat once.    But I haven't seen it for a bit, so it might be ok.<br><br>First if you plan on doing much of anything with BlitzMax I'd suggest you get familiar with types.  I'll see what I can do about adding comments.<br><br>Types are basically a way to organize information.  Theoretically you can write programs without them, but it makes your job ALOT easier.  Like arrays they organize information under a single name.  Int and Stream are types, they're just preset and you don't have to declare them the same way in arrays.<br><br>Methods are like fields, except they're functions within the type.<br><br>When I created the TCard Type, every instance of the TCard set will have a String Variable named Suit, and a String Variable named Face.<br><br>Think of each type as a Templete, within each Templete it has preset Variables, so every Instance of the type/templete will have the variables declared in the type.  You've used types before whether you know it or not.  <br><br>I didn't comment on the setting Ace-King or Spades - Diamonds as i figured you'd figure it out.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
'Put into SuperStrict mode.
SeedRnd MilliSecs() 'Seed the random generator by milliseconds.

Type TCard 'Make a Card Type
	Field Face:String 'Have a Face field within the Card Type
	Field Suit:String 'Have a Suit field within the Card Type
End Type 'Close the Card Type

Type TDeck 'Make a Deck Type
	Field Cards:TCard[] 'Make a Cards field that's an Array, that contains Null elements as I want to set how many elements later.
	
	Method Create(NumCards:Int = 52) 'Create a Create method with a parameter of NumCards to set the number of Elements in Cards.
		Local Loop_Var:Int 'Create a Loop Variable that I'm going to use and re-use later.
		
		Cards:TCard = New TCard[NumCards] 'Redeclare the Array so I can have a custom number of cards for Cards with Jokers.
		For Loop_Var = 0 To Cards.Length - 1 'Go in a loop from 0 to the Length of the Cards Array - 1
			Cards[Loop_Var] = New TCard 'Declare the Type
		Next
	End Method 'End the method
	
	Method Shuffle() 'Create a Shuffle method that will mix up the cards.
		Local TempArray:TCard[Cards.Length] 'A Temporary array that we will test against, it will be equal to the Cards Array.
		Local Active:Int[Cards.Length] 'An array that we use to ensure that no cards are repeated.
		Local Return_Array:TCard[Cards.Length] 'This will be the new Cards Array.
		Local Loop_Var:Int 'Loop Variable
		Local New_Field:Int 'A random number used for the mixing
		Local TimesAccessed:Int 'Helps us determine what field to acess of the Arrays.
		
		TempArray = Cards 'Set the TempArray and all its fields to that of the Card Array.
		
		For Loop_Var = 0 To Cards.Length - 1 'Enter a for loop
			Repeat 'A repeat forever loop
				New_Field = Rnd(0, Cards.Length - 1) 'Random number 
				If Active[New_Field] = False 'If we haven't used this field before
					Return_Array[Loop_Var] = TempArray[New_Field] 'Set the new Cards field uip
					Active[New_Field] = True 'Make sure we don't use it again
					Exit 'Exit the Repeat...Forver loop
				Else 'Otherwise.
					If TimesAccessed &gt; 52 'If we've gone through it too many times.
						Exit 'Exit repeat forever loop
					End If
				EndIf
				
				If Loop_Var = 51 'If this is the last appropriate card
					Return_Array[Loop_Var] = TempArray[New_Field] 'Set it up
					Active[New_Field] = True
					Exit 'Exit out of the repeat forever loop
				EndIf
			Forever
		Next
		Cards = Return_Array 'Set the Cards array to the new correct array.
	End Method
	
	Method Set() 
		Local Loop_Var1:Int 'Loop Variable 1
		Local Loop_Var2:Int 'Loop Variable 2
		Local Loop_Var3:Int 'Loop Variable 3
		
		For Loop_Var1 = 0 To 12 'Cycle through 13 times (once for each card)
			For Loop_Var2 = 0 To 3 'Cycle through 4 times (once for each suit)
				If Loop_Var1 = 0 'If its ace
					Cards[Loop_Var3].Face = "Ace"
				ElseIf Loop_Var1 = 1
					Cards[Loop_Var3].Face = "2"
				ElseIf Loop_Var1 = 2
					Cards[Loop_Var3].Face = "3"
				ElseIf Loop_Var1 = 3
					Cards[Loop_Var3].Face = "4"
				ElseIf Loop_Var1 = 4
					Cards[Loop_Var3].Face = "5"
				ElseIf Loop_Var1 = 5
					Cards[Loop_Var3].Face = "6"
				ElseIf Loop_Var1 = 6
					Cards[Loop_Var3].Face = "7"
				ElseIf Loop_Var1 = 7
					Cards[Loop_Var3].Face = "8"
				ElseIf Loop_Var1 = 8
					Cards[Loop_Var3].Face = "9"
				ElseIf Loop_Var1 = 9
					Cards[Loop_Var3].Face = "10"
				ElseIf Loop_Var1 = 10
					Cards[Loop_Var3].Face = "Jack"
				ElseIf Loop_Var1 = 11
					Cards[Loop_Var3].Face = "Queen"
				ElseIf Loop_Var1 = 12
					Cards[Loop_Var3].Face = "King"
				EndIf
		
				If Loop_Var2 = 0
					Cards[Loop_Var3].Suit = "Spades"
				ElseIf Loop_Var2 = 1
					Cards[Loop_Var3].Suit = "Clubs"
				ElseIf Loop_Var2 = 2
					Cards[Loop_Var3].Suit = "Hearts"
				ElseIf Loop_var2 = 3
					Cards[Loop_Var3].Suit = "Diamonds"
				EndIf
			
				Loop_Var3:+1 'Increment the 3rd Loop Variable by 1 AKA add to it by 1
			Next
		Next
	End Method
End Type

Global MyDeck:TDeck = New TDeck 'Make a new deck
MyDeck.Create() 'Use the create Method of the deck
MyDeck.Set() 'Use the Set Method of the deck
MyDeck.Shuffle() 'Use the Shuffle Method of the deck
Local Loop_Var:Int 'A LoopVariable

For Loop_Var = 0 To MyDeck.Cards.Length - 1 'Cycle through the cards.
	Print MyDeck.Cards[Loop_Var].Face + " of " + MyDeck.Cards[Loop_Var].Suit 'Print it out.
Next
</textarea><br><br>This way in order to make a traditional Card Deck, all you have to do is:<br><pre class=code>
NewDeck:TDeck
NewDeck.Create()
NewDeck.Set()
</pre><br>And its all ready to be used, then to shuffle it, you do:<br><pre class=code>
NewDeck.Shuffle()
</pre><br><br>The reason I make you Set and Shuffle is you may not want that, with a little more work, and merging with Perturbatio's this could work for most decks.  Like you could have another method that would be, "SetPokerDeck()"  or something like that I think. <br><br></td></tr></table><br>
<a name="851219"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >z80jim</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> wow thanks both of you :) I wasn't expecting people to write code from scratch thanks a million!<br>I'm still reading through your comments Corin, but it seems to be very well documented now I'll fiddle around with it to see what I can mess with now :D <br><br></td></tr></table><br>
<a name="851290"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TwoCorin517</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Your welcome, Good, these are important concepts, some I overuse, but important none the less. :D<br><br>It was a good chance to flex my programming muscles anyway. :D <br><br></td></tr></table><br>
<a name="851297"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >z80jim</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> you're gonna get sick of me lol I'm sure I'll have more questions for you before we're through with this :P <br><br></td></tr></table><br>
<a name="851320"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TwoCorin517</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> I doubt it, however I suggest checking out talk.christiandevs.com That's where I hang out alot, is not just Christians, but there's a great community if I do say so myself, and a couple BM users. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
