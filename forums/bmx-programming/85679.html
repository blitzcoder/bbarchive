<!DOCTYPE html><html lang="en" ><head ><title >BmFont Problems</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >BmFont Problems</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >BmFont Problems</a><br><br>
<a name="969785"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CyBeRGoth</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi<br><br>I just found out about this font module from this post<br><a href="/posts.php?topic=79976#902310" target="_blank">BMFont</a><br><br>But whenever I try the included example I get stream errors, I have been away from max for a bit, perhaps there has been some changes to streams that prevent this mod from working correctly?<br><br>Any help getting it working would be appreciated. <br><br></td></tr></table><br>
<a name="969787"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes, the stream handling has changed.<br><br>A lot of code used a return of zero from a read request as an indicator of eof, but now an empty read throws an exception.<br><br>I can't remember where I modified the source, but it was just in one call (I added an eof check before the read that caused the exception).  So it's a pretty simple fix if you locate the offending code with a debug build. <br><br></td></tr></table><br>
<a name="969788"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> OK, I dug out the original version..<br><br>Original, line 277, reads;<br><pre class=code>
BlockType = ReadByte (file)
</pre><br><br>change it to..<br><pre class=code>
If ( Not Eof( file ) )
  BlockType = ReadByte (file)
Else
  BlockType = 0
End If
</pre><br><br>and that's it :) <br><br></td></tr></table><br>
<a name="969835"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CyBeRGoth</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah<br><br>Thanks a lot matibee that fixed it up nicely :)<br><br>Time to have a play around with the mod now! <br><br></td></tr></table><br>
<a name="1012270"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Imphenzia</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've downloaded the version of BMFont for BlitzMax here <a href="http://www.wieringsoftware.nl/blitz/" target="_blank">http://www.wieringsoftware.nl/blitz/</a> and made the change matibee suggested.<br><br>But I think the code for DrawText in the module needs to be updated with DX9 - does anyone know what needs to be done for that? The graphicsdrivers and singlesurfaces confuse me :)<br><br>And not only that, if I do run it with OpenGL or DX7 all the characters are corrupted and unreadable. Does this work for anyone else? <br><br></td></tr></table><br>
<a name="1012274"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I fixed it again recently for DX9...  <a href="http://www.matibee.co.uk/wpsite/?page_id=6/general-2/bitmap-font-mod-revisited/" target="_blank">http://www.matibee.co.uk/wpsite/?page_id=6/general-2/bitmap-font-mod-revisited/</a><br><br>It no longer supports italics in any graphics driver as it just uses DrawSubImageRect now (a function that didn't exist when it was originally written).<br><br>:) <br><br></td></tr></table><br>
<a name="1012341"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Imphenzia</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Great stuff, thanks :)<br><br>Also I gather it no longer supports textured fonts? (not a problem for me).<br><br>The shadows look funny (inverted) now for both DX9 and OpenGL?<br><br><img src="http://www.nickyf.com/tmp/shadowissue.jpg"> <br><br></td></tr></table><br>
<a name="1012348"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ha!  I sent this fix back to Mike with the disclaimer attached that it's not been fully tested because I simply don't use all of the features.  Stoopid me didn't think of actually running the very sample he provides! :P<br><br>I'll have a look what happened to the texturing and shadows.<br><br>:) <br><br></td></tr></table><br>
<a name="1012353"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well that was simple enough.  Please download again :) <br><br></td></tr></table><br>
<a name="1012428"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Imphenzia</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you very very much Matibee :)<br><br>What's the score with using single surface rather than drawimage - is it faster? and if so - much? or are there other benefits? <br><br></td></tr></table><br>
<a name="1012465"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Having the fonts packed onto individual textures saves video memory for one thing; every loaded image is rounded up to be power-of-two wide and high.  So loading an individual letter that's 9 x 10 pixels (360 bytes) would actually create a texture of 16 x 16 pixels (1024 bytes).  With multiple fonts with lots of characters each that would soon add up to a massive waste.<br><br>It is generally quicker to draw multiple images all off the same texture because it avoids texture changes on the video card, but the bmfont wasn't necessarily built the way it is because of that because it supports multiple textures per font.  Assuming all your fonts are a single texture it'll be a bit quicker than swapping textures every letter.<br><br>Before you try compiling sprite sheets of 8096x8096 with every single bitmap you game needs be warned; there is a overhead with accessing large textures and hardware will impose texture size limits.<br><br>I'm not a graphics guru, my knowledge on the above stems from years of following good advice and best practices and ultimately, writing some very performant code ;)<br><br>The ultimate way to draw a font would be to use one texture and then compile a string into its own vertex buffer that only ever changes when the string does and it can all be drawn in one call.  Moving on from that all the string vertex buffers could be merged together to allow drawing reams of text in one call; or even merge all the 2d buffers into one big buffer and only stop drawing to change renderstates..... and on it goes but there's always limitations and trade offs.  Right now, Blitz uses one hardware draw call for every software call which isn't ideal but on modern hardware it gets by.  Nilium (sp?) was working on a buffered video driver to overcome this. <br><br></td></tr></table><br>
<a name="1044626"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >wmaass</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Anyone out there have this working with the latest editor (1.12)? I am using matibee's version of the bmfont mod and can't get it to read the binary fonts generated from the new editor. <br><br></td></tr></table><br>
<a name="1116892"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skn3</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sorry to dig up an old thread, but does anyone have matibee's modified version of this module? <br><br></td></tr></table><br>
<a name="1116914"></a>

<a name="1116915"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> It's all here..<br><br><a href="http://www.matibee.co.uk/_x/downloads" target="_blank">http://www.matibee.co.uk/_x/downloads</a><br><br>My site is / was a wordpress site that was getting spammed to bloody hell and I don't have the patience to keep maintaining it.<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1116927"></a>

<a name="1116928"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skn3</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks matt!<br><br><br>Gonna post the code here:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' (C) Copyright 2008, Mike Wiering, Wiering Software
' 
' Permission is hereby granted, free of charge, to any person obtaining a copy
' of this software and associated documentation files (the "Software"), to deal
' in the Software without restriction, including without limitation the rights
' to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
' copies of the Software, and to permit persons to whom the Software is
' furnished to do so, subject to the following conditions:
' 
' The above copyright notice and this permission notice shall be included in
' all copies or substantial portions of the Software.
' 
' THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
' IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
' FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
' AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
' LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
' OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
' THE SOFTWARE.

SuperStrict


Rem 
	========================
	Important - please read!
	========================
	
	* This code was originally written by Mike Wiering, and remains Copright (C) 2008 Wiering Software *
	
	This version has been modified by matibee to be compatible with the DX9 Graphics driver.
	
	To use it as a module again, uncomment the Module info below, and place it in Wiering.Mod\BMFont.Mod 
	folder.

End Rem 


Rem
bbdoc: bmfont
End Rem

Rem =============================  Remove this rem to compile as a module again !!
Module Wiering.BMFont

ModuleInfo "Name: BMFont"
ModuleInfo "Description: Library for AngelCode Bitmap Font Generator (www.angelcode.com/products/bmfont)"
ModuleInfo "Version: unoffical 2.0 by matibee"
ModuleInfo "License: MIT"
ModuleInfo "Author: Mike Wiering"
ModuleInfo "Copyright: (C) 2008, Mike Wiering, Wiering Software"

ModuleInfo "History: 2.0"
ModuleInfo "History:	    Updated to work with the D3D9 video driver.  Changed drawing to use DrawSubImageRect.  Slant no longer available!  "
ModuleInfo "History: 1.2"
ModuleInfo "History:       Updated to work with version 3 of the binary file format  "
ModuleInfo "History: 1.1"
ModuleInfo "History:       Improved state handling/restore, including AutoImageFlags, AutoMidHandle  "
ModuleInfo "History: 1.0"
ModuleInfo "History: Initial Release."
End Rem '=============================  Remove this End Rem To compile as a Module again !!

Import BRL.D3D9Max2D
Import BRL.D3D7Max2D
Import BRL.GLMax2D
Import BRL.Stream
Import BRL.Max2D
'Import BRL.PNGLoader
'Import BRL.BMPLoader
'Import BRL.TGALoader
'Import BRL.JPGLoader


Const ALIGN_LEFT: Int    = -1
Const ALIGN_CENTER: Int  =  0
Const ALIGN_RIGHT: Int   =  1

Rem
bbdoc: Type TBMFont
about: Bitmap Font Object
End Rem
Type TBMFont

	Field BMFImage: TImage[]
	Field CustomImage: TImage[]
	Field ShadowImage: TImage[]
	Field DrawingShadow: Int
	Field Image: TImage[]
	Field ImageW: Int[]
	Field ImageH: Int[]
	
	Field BMFVersion: Int

	Rem
	bbdoc: Sets whether the shadow should be drawn (use CreateShadow to create the shadow first).
	about: 
	End Rem
	Field Shadow: Int
	
	Rem
	bbdoc: Horizontal shadow offset.
	about: Relative to the text.
	End Rem
	Field ShadowX: Int
	
	Rem
	bbdoc: Vertical shadow offset.
	about: Relative to the text.
	End Rem
	Field ShadowY: Int
	
	Rem
	bbdoc: Horizontal spacing in pixels.
	about: This can be used to add extra spacing between characters.
	End Rem
	Field HSpacing: Float
	
	Rem
	bbdoc: Vertical spacing in pixels.
	about: This can be used to add extra spacing between lines.
	End Rem
	Field VSpacing: Float
	
	Rem
	bbdoc: Sets whether kerning should be used or not (on by default if FNT file has kerning).
	about: Kerning adjusts the distance between certain pairs of characters.
	End Rem
	Field Kerning: Int
	
	Rem
	bbdoc: Sets the horizontal text alignment.
	about: This can be ALIGN_LEFT (default), ALIGN_CENTER or ALIGN_RIGHT.
	End Rem
	Field Align: Int
	
	Field FontSize: Short
	Field Bold: Byte
	Field Italic: Byte
	Field Unicode: Byte
	Field Smooth: Byte
	Field CharSet: Byte
	Field StretchH: Short
	Field AA: Byte
	Field PaddingUp: Byte
	Field PaddingRight: Byte
	Field SpacingHoriz: Byte
	Field SpacingVert: Byte
	Field Outline: Byte
	
	Field LineHeight: Int
	Field Base: Int
	Field ScaleW: Int
	Field ScaleH: Int
	Field Pages: Int
	Field Packed: Byte
	Field Encoded: Byte
	Field AlphaChannel: Byte
	Field RedChannel: Byte
	Field GreenChannel: Byte
	Field BlueChannel: Byte
	
	Field IdStr: String
	Field Id: Int[]
	Field X: Short[]
	Field Y: Short[]
	Field Width: Short[]
	Field Height: Short[]
	Field XOffset: Short[]
	Field YOffset: Short[]
	Field XAdvance: Short[]
	Field Page: Byte[]
	
	Field ASCII: Int[]
	
	Field KerningChars: String[]
	Field KerningValue: String[] 
	
	Field XScale: Float = 1
	Field YScale: Float = 1
	
	Field LastText: String = ""
	Field LastWidth: Float = 0
	Field LastXScale: Float = 1
	Field LastYScale: Float = 1
	Field LastLine: Float = -1
	
	Field dx: Float = 0
	Field dy: Float = 0
	
	Rem
	bbdoc: String of characters that generate a callback.
	about: This can be used for control characters that change the color or offset (dx, dy).
	End Rem
	Field CallBackChars: String
	
	Rem
	bbdoc: String of characters that should not be drawn.
	about: This can be used for control characters.
	End Rem
	Field SkipChars: String
	
	
	Rem
	bbdoc: Callback function that is called before drawing a character that occurs in the string CallBackChars.
	about: Parameters:
	&lt;ul&gt;
	&lt;li&gt;&lt;b&gt;Char&lt;/b&gt;: The character that is about to be drawn&lt;/li&gt;
	&lt;li&gt;&lt;b&gt;X&lt;/b&gt;: The X position&lt;/li&gt;
	&lt;li&gt;&lt;b&gt;Y&lt;/b&gt;: The Y position&lt;/li&gt;
	&lt;/ul&gt;
	End Rem
	Method OnCallBack (Char: Int, X: Float, Y: Float)
	  ' overload this
	End Method
	
	
	Rem
	bbdoc: Creates a new TBMFont object from a binary .fnt file.
	about: Parameters:
	&lt;ul&gt;
	&lt;li&gt;&lt;b&gt;Prefix&lt;/b&gt;: This can be either "incbin::" or a path like "fonts\"&lt;/li&gt;
	&lt;li&gt;&lt;b&gt;FNTFile&lt;/b&gt;: This must be a binary FNT file (version 2 or 3)&lt;/li&gt;
	&lt;/ul&gt;
	End Rem
	Function Create: TBMFont (Prefix: String, FNTFile: String)
	  Local this: TBMFont = New TBMFont
	  this.Init (Prefix, FNTFile)
	  Return this
	End Function
	
	
	Rem
	bbdoc: Initializes a new TBMFont object from a binary .fnt file.
	about: Parameters:
	&lt;ul&gt;
	&lt;li&gt;&lt;b&gt;Prefix&lt;/b&gt;: This can be either &lt;b&gt;incbin::&lt;/b&gt; or a path like &lt;b&gt;CurrentDir() + "/"&lt;/b&gt;&lt;/li&gt;
	&lt;li&gt;&lt;b&gt;FNTFile&lt;/b&gt;: This must be a binary FNT file (version 2)&lt;/li&gt;
	&lt;/ul&gt;
	End Rem
	Method Init (Prefix: String, FNTFile: String)
		Local file: TStream = ReadStream ("littleendian::" + Prefix + FNTFile)
		BMFVersion = 0
		If ReadString (file, 3) &lt;&gt; "BMF" Then
?debug
			DebugLog Prefix + FNTFile + " is not a binary .FNT file"
?
		Else
			BMFVersion = ReadByte (file)
		End If

		If (BMFVersion = 2) Or (BMFVersion = 3) Then
			Local BlockType: Int = ReadByte (file)
			While BlockType &lt;&gt; 0
				Local BlockLen: Int = ReadInt (file)
				If (BMFVersion = 2) Then BlockLen :- 4

				Select BlockType
          			Case 1  ' info not used here
            			LoadInfo (file, BlockLen)
          			Case 2  ' common
            			LineHeight = ReadShort (file)
            			Base = ReadShort (file)
            			ScaleW = ReadShort (file)
            			ScaleH = ReadShort (file)
            			Pages = ReadShort (file)
            			Local b: Byte = ReadByte (file)
            			Packed = (b &amp; 1) &lt;&gt; 0
            			If (BMFVersion = 2) Then
              				Encoded = (b &amp; 2) &lt;&gt; 0
            			Else
              				AlphaChannel = ReadByte (file)
  			  			RedChannel = ReadByte (file)
			  			GreenChannel = ReadByte (file)
			  			BlueChannel = ReadByte (file)
            			End If
          			Case 3  ' pages
            			LoadPages (Prefix, ReadString (file, BlockLen))
          			Case 4  ' characters
            			LoadCharacters (file, BlockLen)
          			Case 5  ' kerning
            			LoadKerning (file, BlockLen)
          			Default
					?Debug
            			DebugLog "Unknown block: " + BlockType + ", length: " + BlockLen
					?
            			ReadString (file, BlockLen)  ' skip unknown block

        			End Select

        			If ( Not Eof( file ) )
          				BlockType = ReadByte (file)
	  			Else
	    				BlockType = 0
	  			End If
      		Wend
    		End If

		CloseStream file
		HSpacing = 0
		VSpacing = 0
		Align = ALIGN_LEFT
		Shadow = False
		DrawingShadow = False
	End Method


	Method LoadInfo (file: TStream, BlockLen: Int)
		FontSize = ReadShort (file)    
		Local b: Byte = ReadByte (file)
		Bold = (b &amp; $80) &lt;&gt; 0
		Italic = (b &amp; $40) &lt;&gt; 0
		Unicode = (b &amp; $20) &lt;&gt; 0
		Smooth = (b &amp; $10) &lt;&gt; 0
		CharSet = ReadByte (file)
		StretchH = ReadShort (file)
		AA = ReadByte (file)
		PaddingUp = ReadByte (file)
		PaddingRight = ReadByte (file)
		SpacingHoriz = ReadByte (file)
		SpacingVert = ReadByte (file)
		Outline = ReadByte (file)
		ReadString (file, BlockLen - 12)  ' skip name
	End Method


	Function Pow2Size: Int (n: Int)
		Local ry: Int = 1
		While ry &lt; n
			ry = ry Shl 1
		Wend
		Return ry
	End Function


	Method LoadPages (Prefix: String, ImageFile: String)
		Local i: Int
		BMFImage = BMFImage[..Pages]
		For i = 0 To Pages - 1
			BMFImage[i] = LoadImage (Prefix + ImageFile.Split (Chr (0))[i], MASKEDIMAGE | FILTEREDIMAGE)
			SetImageHandle (BMFImage[i], 0, 0)
		Next
		If Packed Then
			DecodeChannels ()
		End If
		ImageW = ImageW[..Pages]
		ImageH = ImageH[..Pages]
		
		For i = 0 To Pages - 1
			ImageW[i] = Pow2Size (BMFImage[i].Width)
			ImageH[i] = Pow2Size (BMFImage[i].Height)
		Next 			
		
		Image = BMFImage
		CustomImage = BMFImage
		ShadowImage = BMFImage
		EnableCustomTexture (False)
	End Method

	Method LoadCharacters (file: TStream, N: Int)
		Local i: Int
		If (BMFVersion = 2)
		  	N :/ 18
		Else
		  	N :/ 20
		End If
		ASCII = ASCII[..256]
		IdStr = ""
		Id = Id[..N]
		X = X[..N]
		Y = Y[..N]
		Width = Width[..N]
		Height = Height[..N]
		XOffset = XOffset[..N]
		YOffset = YOffset[..N]
		XAdvance = XAdvance[..N]
		Page = Page[..N]
		For i = 0 To N - 1
		  	If (BMFVersion = 2) Then
		   		Id[i] = ReadShort (file)
		  	Else
		    		Id[i] = ReadInt (file)
		  	End If
		  	If Id[i] &lt; 256 Then
		    		ASCII[Id[i]] = i
		  	End If
		  	IdStr :+ Chr (Id[i])
		  	X[i] = ReadShort (file)
		  	Y[i] = ReadShort (file)
		  	Width[i] = ReadShort (file)
		  	Height[i] = ReadShort (file)
		  	XOffset[i] = ReadShort (file)
		  	YOffset[i] = ReadShort (file)
		  	XAdvance[i] = ReadShort (file)
		  	Page[i] = ReadByte (file)
		  	Local Channel: Int = ReadByte (file)
		  	If Packed Then
		    		Local ch: Int = 0
		    		Select Channel
		      	Case 2 ch = 1
		      	Case 4 ch = 2
		      	Case 8 ch = 3
		    		End Select
				Page[i] = 4 * Page[i] + ch
			End If
		Next
		Kerning = False
		KerningChars = KerningChars[..N]
		KerningValue = KerningValue[..N]
	End Method

	Function SignedShort: Int (X: Short)
  		Return (X Shl 16) Sar 16
	End Function

	Function SignedByte: Int (X: Byte)
  		Return (X Shl 24) Sar 24
	End Function


	Method FindChar: Int (Char: Short)
  		If Char &lt;= 255 Then
    			Return ASCII[Char]
  		Else
    			Return IdStr.Find (Chr (Char))
  		End If
	End Method


  	Method LoadKerning (file: TStream, N: Int)
    		If (BMFVersion = 2)
      		N :/ 6
    		Else
      		N :/ 10
    		End If
    		For Local i: Int = 0 To N - 1
      		Local First: Int
      		Local Second: Int
      		If (BMFVersion = 2) Then
        			First = ReadShort (file)
        			Second = ReadShort (file)
      		Else
        			First = ReadInt (file)
        			Second = ReadInt (file)
      		End If
      		Local Amount: Int = ReadShort (file)
      		Local j: Int = IdStr.Find (Chr (First))
      		If j &gt;= 0 Then
        			KerningChars[j] :+ Chr (Second)
        			KerningValue[j] :+ Chr (Amount &amp; $FF)
        			Kerning = True
      		End If
    		Next
  	End Method


  Rem
  bbdoc: Returns the height of a string in pixels using the current scale.
  about: Parameters:
  &lt;ul&gt;
  &lt;li&gt;&lt;b&gt;Text&lt;/b&gt;: The string (can have multiple lines)&lt;/li&gt;
  &lt;/ul&gt;
  End Rem
  Method TextHeight: Float (Text: String)
    Local H: Int = 1
    GetScale (XScale, YScale)
    For Local i: Int = 0 To Text.Length - 1
      If Text[i] = Asc ("~n") Then
        H :+ 1
      End If
    Next
    Return H * YScale * (FontSize + SpacingVert + VSpacing)
  End Method


  Rem
  bbdoc: Returns the width of a string in pixels using the current scale.
  about: Parameters:
  &lt;ul&gt;
  &lt;li&gt;&lt;b&gt;Text&lt;/b&gt;: The string (can have multiple lines)&lt;/li&gt;
  &lt;li&gt;&lt;b&gt;Line=-1&lt;/b&gt;: If supplied, TextWidth only counts this line&lt;/li&gt;
  &lt;/ul&gt;
  End Rem
  Method TextWidth: Float (Text: String, Line: Int = -1)
    Local M: Float = 0
    Local L: Int = 0

    Local Char: Int
    Local Index: Int
    Local LastIndex: Int = -1
    Local fdx: Float = 0

    GetScale (XScale, YScale)
    If (Text = LastText) And (XScale = LastXScale) And (YScale = LastYScale) And (Line = LastLine) Then
      Return LastWidth 
    End If
    LastText = Text
    LastXScale = XScale
    LastYScale = YScale
    LastLine = Line
    
    For Local i: Int = 0 To Text.Length - 1
      Char = Text[i]
      If Char = Asc ("~n") Then
        M = Max (M, fdx)
        fdx = 0
        L :+ 1
        LastIndex = -1
      Else
        If (Line = -1) Or (L = Line) Then 

          Index = FindChar (Char)

          If (Index &gt;= 0) And (SkipChars.Find (Chr (Char)) = -1) Then

            If Kerning Then
              If LastIndex &lt;&gt; -1 Then
                Local k: Int = KerningChars[LastIndex].Find (Chr (Char))
                If k &gt;= 0 Then
                  fdx :+ SignedByte (KerningValue[LastIndex][k] &amp; $FF)
                End If
              End If
            End If
            fdx :+ SignedShort (XAdvance[Index])
            If i &lt; Text.Length - 1 Then 
              fdx :+ SpacingHoriz + HSpacing
            End If
            LastIndex = Index

          End If
        End If
      End If
    Next
    M = XScale * Max (M, fdx)
    LastWidth = M
    Return M
  End Method


  Rem
  bbdoc: Draws the text using the bitmap font characters using the current values for Align, Shadow, ShadowX, ShadowY, HSpacing, VSpacing and Kerning. 
  about: Parameters:
  &lt;ul&gt;
  &lt;li&gt;&lt;b&gt;Text&lt;/b&gt;: The string (can have multiple lines)&lt;/li&gt;
  &lt;li&gt;&lt;b&gt;XPos&lt;/b&gt;: Tye X position (center position for ALIGN_CENTER)&lt;/li&gt;
  &lt;li&gt;&lt;b&gt;YPos&lt;/b&gt;: The Y position&lt;/li&gt;
  &lt;/ul&gt;
  End Rem
  Method DrawText (Text: String, XPos: Float, YPos: Float)

    If Shadow And (Not DrawingShadow) Then
      DrawingShadow = True
      Local tmpImage: TImage[] = Image
      Image = ShadowImage
      Local Blend: Int = GetBlend ()
      Local Alpha: Float = GetAlpha ()
      SetBlend SHADEBLEND
      Local ColorR: Int, ColorG: Int, ColorB: Int
      GetColor (ColorR, ColorG, ColorB)
      DrawText (Text, XPos + ShadowX, YPos + ShadowY)  ' draw shadow
      SetColor (ColorR, ColorG, ColorB)
      SetBlend Blend
      SetAlpha Alpha
      Image = tmpImage
      DrawingShadow = False
    End If

    Local Char: Int
    Local LastChar: Int = -1
    Local Index: Int
    Local LastIndex: Int = -1
    Local fdx: Float = 0
    Local fdy: Float = 0

    Local Line: Int = 0
    Local M: Float = 0
    Local W: Float = TextWidth (Text, Line) 
    'GetScale (XScale, YScale)  ' already done by TextWidth

    dx = 0  ' can be used in callback functions
    dy = 0

    If Align = ALIGN_RIGHT Then fdx :- W / XScale
    If Align = ALIGN_CENTER Then fdx :- (W / XScale) / 2
    For Local i: Int = 0 To Text.Length - 1
      Char = Text[i]

      Local CB: Int = CallBackChars.Find (Chr (Char)) &gt; -1
      If CB Then
        OnCallBack (Char, XPos + XScale * fdx, YPos + YScale * fdy)
      End If

      If SkipChars.Find (Chr (Char)) = -1 Then

        If Char = Asc ("~n") Then
          M = Max (M, fdx)
          fdy :+ FontSize + SpacingVert + VSpacing
          Line :+ 1
          LastChar = -1
          LastIndex = -1
          W = TextWidth (Text, Line)
          fdx = 0
          If Align = ALIGN_RIGHT Then fdx :- W / XScale
          If Align = ALIGN_CENTER Then fdx :- (W / XScale) / 2
        Else
          Index = FindChar (Char)
          If Index &gt;= 0 Then

            If Kerning Then
              If LastIndex &lt;&gt; -1 Then
                Local k: Int = KerningChars[LastIndex].Find (Chr (Char))
                If k &gt;= 0 Then
                  fdx :+ SignedByte (KerningValue[LastIndex][k] &amp; $FF)
                End If
              End If
            End If

            Local tw: Float = ImageW[Page[Index]]
            Local th: Float = ImageH[Page[Index]]
            Local xp: Float = X[Index]
            Local yp: Float = Y[Index]
            Local w: Float = Width[Index]
            Local h: Float = Height[Index]

            If DrawingShadow Then
              xp :- (SpacingHoriz / 2)
              yp :- (PaddingUp / 2)
              w :+ (SpacingHoriz + PaddingRight) / 2
              h :+ (PaddingUp + SpacingVert) / 2
              SetColor ($FF, $FF, $FF)
            End If

		Local x:Float = XPos + XScale * (fdx + SignedShort (XOffset[Index]) + dx)
		Local y:Float = YPos + YScale * (fdy + SignedShort (YOffset[Index]) + dy)
		
		DrawSubImageRect( Image[Page[Index]], x, y, w, h, xp, yp, w, h )
		
            fdx :+ SpacingHoriz + SignedShort (XAdvance[Index]) + HSpacing
            LastChar = Char
            LastIndex = Index
          End If
        End If

      End If

    Next
  End Method

  Function ClipByte: Int (X: Int)
    If X &lt; 0 Return 0 Else If X &gt; 255 Return 255 Else Return X
  End Function


  Rem
  bbdoc: Adds a texture to the font.
  about: Parameters:
  &lt;ul&gt;
  &lt;li&gt;&lt;b&gt;Tex&lt;/b&gt;: The texture image or NULL to reset&lt;/li&gt;
  &lt;/ul&gt;
  End Rem
  Method ApplyTexture (Tex: TImage)
    If Not Tex Then
      Image = BMFImage  ' reset
      CustomImage = BMFImage
      EnableCustomTexture (False)
    Else
      Local tw: Int = Tex.Width
      Local th: Int = Tex.Height
      Local pmTex: TPixmap = LockImage (Tex)
      Local N: Int = BMFImage.length
      CustomImage = CustomImage[..N]

      For Local img: Int = 0 To N - 1
        Local w: Int = BMFImage[img].Width
        Local h: Int = BMFImage[img].Height

        Local pmSrc: TPixmap = LockImage (BMFImage[img])
        Local pmDst: TPixmap = CreatePixmap (w, h, PF_BGRA8888)
        ClearPixels (pmDst, 0)

        For Local j: Int = 0 To Id.length - 1
          If Page[j] = img Then

            Local xx: Int = (tw / 2) - (XAdvance[j]) / 2 + SignedShort (XOffset[j])
            Local yy: Int = (th / 2) - (FontSize / 2) + SignedShort (YOffset[j])
            For Local jj: Int = 0 To Height[j] - 1
              For Local ii: Int = 0 To Width[j] - 1
                Local xp: Int = xx + ii
                Local yp: Int = yy + jj
                If xp &lt; 0 Then xp = 0 Else If xp &gt; tw - 1 Then xp = tw - 1
                If yp &lt; 0 Then yp = 0 Else If yp &gt; th - 1 Then yp = th - 1
                Local rgba1: Int = ReadPixel (pmSrc, ii + X[j], jj + Y[j])
                Local rgba2: Int = ReadPixel (pmTex, xp, yp)

                Local r1: Int = (rgba1 Shr 16) &amp; $FF
                Local r2: Int = (rgba2 Shr 16) &amp; $FF
                Local g1: Int = (rgba1 Shr 8) &amp; $FF
                Local g2: Int = (rgba2 Shr 8) &amp; $FF
                Local b1: Int = rgba1 &amp; $FF
                Local b2: Int = rgba2 &amp; $FF

                Local rgba: Int = (rgba1 &amp; $FF000000) + ..
                    (ClipByte ((r1 * r2) / $C0) Shl 16) + ..
                    (ClipByte ((g1 * g2) / $C0) Shl 8) + ..
                     ClipByte ((b1 * b2) / $C0)

                WritePixel (pmDst, ii + X[j], jj + Y[j], rgba)
              Next
            Next

          End If
        Next
        CustomImage[img] = LoadImage (pmDst, MASKEDIMAGE | FILTEREDIMAGE)
        SetImageHandle (CustomImage[img], 0, 0)
        UnlockImage (BMFImage[img])

      Next
      UnlockImage (Tex)
      
      Image = CustomImage
      EnableCustomTexture (True)
    End If
  End Method


  Rem
  bbdoc: Switches custom textures on or off.
  about: Parameters:
  &lt;ul&gt;
  &lt;li&gt;&lt;b&gt;Value&lt;/b&gt;: True for custom texture, False for no texture.&lt;/li&gt;
  &lt;/ul&gt;
  End Rem
  Method EnableCustomTexture (Value: Int)
    If Value Then
      Image = CustomImage
    Else
      Image = BMFImage
    End If
  End Method


  Method DecodeChannels ()
    Local ChannelImage: TImage[]
    ChannelImage = ChannelImage[..Pages * 4]
    For Local i: Int = 0 To Pages - 1
      Local w: Int = BMFImage[i].width
      Local h: Int = BMFImage[i].height
      Local pmSrc: TPixmap = LockImage (BMFImage[i])
      For Local j: Int = 0 To 3
        Local pmDst: TPixmap = CreatePixmap (w, h, PF_A8)
        For Local y: Int = 0 To h - 1
          For Local x: Int = 0 To w - 1
            Local argb: Int = ReadPixel (pmSrc, x, y)
            argb = ((argb Shl (8 * (3 - j))) &amp; $FF000000) | $FFFFFF
            WritePixel pmDst, x, y, argb
          Next
        Next
        ChannelImage[i * 4 + j] = LoadImage (pmDst, MASKEDIMAGE | FILTEREDIMAGE)
        SetImageHandle (ChannelImage[i * 4 + j], 0, 0)
      Next
      UnlockImage (BMFImage[i])
    Next
    Pages = 4 * Pages
    BMFImage = ChannelImage
  End Method


  Rem
  bbdoc: Creates a soft shadow of the bitmap font (position can be set with ShadowX and ShadowY). WARNING: CreateShadow uses the backbuffer!
  about: Parameters:
  &lt;ul&gt;
  &lt;li&gt;&lt;b&gt;ShadowBlur = 3&lt;/b&gt;: Blur size in pixels (1..9), be sure to add padding space on all sides!&lt;/li&gt;
  &lt;li&gt;&lt;b&gt;Amount = 255&lt;/b&gt;: Shadow darkness 1..255 or 0 to reset&lt;/li&gt;
  &lt;/ul&gt;
  End Rem
  Method CreateShadow (ShadowBlur: Float = 3, Amount: Int = 255)
    If Amount = 0 Then
      ShadowImage = Image
    Else
      Local ColorR: Int, ColorG: Int, ColorB: Int
      Local ClsColorR: Int, ClsColorG: Int, ClsColorB: Int
      Local Alpha: Float = GetAlpha ()
      Local Blend: Int = GetBlend ()
      Local Rotation: Int = GetRotation ()

      GetColor (ColorR, ColorG, ColorB)
      GetClsColor (ClsColorR, ClsColorG, ClsColorB)
      GetScale (XScale, YScale)
      SetRotation 0

      Local DW: Int = 256
      Local DH: Int = 256
      Local BlurX: Int = ShadowBlur
      Local BlurY: Int = ShadowBlur

      Local N: Int = BMFImage.length
      ShadowImage = ShadowImage[..N]

      For Local img: Int = 0 To N - 1
        Local w: Int = BMFImage[img].Width
        Local h: Int = BMFImage[img].Height

        SetClsColor 0, 0, 0
        SetScale (1.0, 1.0)
        SetColor Amount, Amount, Amount
        SetBlend AlphaBlend

        Local pmDst: TPixmap = CreatePixmap (w, h, PF_BGRA8888)
        ClearPixels (pmDst, 0)

        For Local by: Int = 0 To (h + DH - 1) / DH - 1
          For Local bx: Int = 0 To (w + DW - 1) / DW - 1

            Cls
            For Local j: Int = 0 To BlurY - 1
              For Local i: Int = 0 To BlurX - 1
                Local A1: Float = Sin (45 + 90 * Float (i) / BlurX)
                Local A2: Float = Sin (45 + 90 * Float (j) / BlurY)
                If A1 &lt; 0 Then A1 = 0
                If A2 &lt; 0 Then A2 = 0
                SetAlpha ((A1 + A2) / (BlurX * BlurY))
                DrawImage (BMFImage[img], -bx * DW + i - BlurX / 2, -by * DH + j - BlurY / 2)
              Next
            Next

            Local pm: TPixmap = GrabPixmap (0, 0, DW, DH)
            Local TmpDH: Int = DH
            If by * DH + DH - 1 &gt;= h Then TmpDH = h - by * DH
            Local TmpDW: Int = DW
            If bx * DW + DW - 1 &gt;= w Then TmpDW = w - bx * DW
            For Local jj: Int = 0 To TmpDH - 1
              For Local ii: Int = 0 To TmpDW - 1
                Local rgba: Int = ReadPixel (pm, ii, jj)
                rgba = $FFFFFFFF - (rgba &amp; $FFFFFF)
                WritePixel (pmDst, bx * DW + ii, by * DH + jj, rgba)
              Next
            Next

          Next
        Next
        ShadowImage[img] = LoadImage (pmDst, MASKEDIMAGE | FILTEREDIMAGE)
        SetImageHandle (ShadowImage[img], 0, 0)
        Shadow = True
      Next

      SetScale (XScale, YScale)
      SetAlpha (Alpha)
      SetBlend (Blend)
      SetRotation (Rotation)
      SetClsColor (ClsColorR, ClsColorG, ClsColorB)
      SetColor (ColorR, ColorG, ColorB)
    End If
  End Method

End Type
</textarea><br>For years in the future when people come a looking!<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
