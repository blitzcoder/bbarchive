<!DOCTYPE html><html lang="en" ><head ><title >Protected frontend GUI</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Protected frontend GUI</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Protected frontend GUI</a><br><br>
<a name="735218"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >EOF</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> (EDIT - Maybe this should be in GUI programming??)<br><br><br>I am starting a project whereby I want to create a protected frontend GUI which restricts a user from tampering with the Windows installation.<br><br>For me, its far too easy for a casual user to accidently screw around with the setup. Renaming, deleting, moving icons, deleting stuff from folders, messing up the display, installing spyware freebie stuff, etc..<br><br>I really want to crack down hard and set major limitations in place.<br><br>My idea is to create a protected frontend display much like you see in a Kiosk. Something which still lets the user click icons, open utilities, and the likes but prevents them from tampering with the O/S itself. Basically, a sandboxed system.<br><br>So, I need to:<br><br>1) Stop the user Alt-Tabbing into the O/S<br>2) Kill all system keys such as Ctrl+Alt,Del, the Windows key, menu key, sticky keys.<br>3) Kill all popup alerts ("Updates ready", "Virus database out of date")<br>4) Allow only selected drives/folders to be viewable from applications like Notepad, MSPaint<br>5) Hide the windows taskbar (if 'Always on top' is selected)<br><br><br>Just to give you a taster here is a 'very basic' frontend GUI<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Framework BRL.Win32MaxGUI

AppTitle="ProtectedGUI"

Global dt:TGadget=Desktop()
Global dtW%=dt.Width
Global dtH%=dt.Height

Global taskbarH%=52
Global panW%=140

Global time$=CurrentTime$()
Global mb%

Global win:TGadget=CreateWindow("Test",0,-18,dtW,dtH+18,Desktop(),EVENT_WINDOWACTIVATE)
Global displaypanel:TGadget=CreatePanel(0,0,win.Width,win.Height-taskbarH,win,PANEL_ACTIVE)

Global taskbarpanel:TGadget=CreatePanel(0,displaypanel.Height,win.Width,taskbarH-18,win,PANEL_ACTIVE)
Global btn_Menu:TGadget=CreateButton("Menu",4,4,56,taskbarpanel.Height-8,taskbarpanel)
Global taskbarinfo:TGadget=CreatePanel(win.Width-panW-4,4,panW,taskbarpanel.Height-8,taskbarpanel,PANEL_BORDER)
Global lbl_Time:TGadget=CreateLabel("",taskbarinfo.Width-38,4,38,taskbarinfo.Height-8,taskbarinfo)

SetPanelColor displaypanel,$22,$44,$aa
SetGadgetText lbl_Time,time[0..time.Length-3]


Global popupmenu:TGadget=CreateMenu(AppTitle,000,Null)
CreateMenu("Preferences",101,popupmenu)
CreateMenu("",000,popupmenu)
CreateMenu("Switch User",102,popupmenu)
CreateMenu("Exit",103,popupmenu)

Global debugwindow:TGadget=CreateListBox(10,10,400,142,displaypanel)

Repeat
	Local s$
	'Debug CurrentEvent.ToString()
	Select WaitEvent()
		Case EVENT_MOUSEMOVE
		Case EVENT_MOUSEDOWN
		mb=EventData()
		If EventSource()=displaypanel s$="Display"
		If EventSource()=taskbarpanel s$="TaskBar"
		Debug s$+" &gt; Mouse Button  = "+String(mb)
		Case EVENT_GADGETACTION
			Select EventSource()
				Case btn_Menu
				MoveMouse taskbarpanel.XPos+4 , taskbarpanel.YPos
				PopupWindowMenu win,popupmenu
			End Select
		Case EVENT_MENUACTION
		Debug "Menu &gt; "+EventData()
		If EventData()=103 Exit
	End Select
Forever
End


Function Debug(txt$)
	If CountGadgetItems(debugwindow)&gt;8 ClearGadgetItems debugwindow
	If txt$&lt;&gt;""
		AddGadgetItem debugwindow,txt$
		SelectGadgetItem debugwindow,CountGadgetItems(debugwindow)-1
	EndIf
End Function
</textarea><br><br>My idea is to add a "Admin" mode which allows the complete control of what is accessible to the user, as well as a quit option to return back to Windows.<br><br>Task too big? <br><br></td></tr></table><br>
<a name="735230"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >popcade</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Didn't do any hook programming on Windows yet... but I heard that's a lot of work.. :| <br><br></td></tr></table><br>
<a name="735252"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Azathoth</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> So is this like a desktop environment? <br><br></td></tr></table><br>
<a name="735420"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Fabian.</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> You could use multiple desktops. Windows creates the most gui stuff on the Default desktop. So if you create another desktop and run your application on this desktop you could have a much more secure system than normal windows. Using a seperate desktop can solve all points in the list you made except number 4.<br><br>I wrote some code showing how this could work, if the app is started once the user can't switch back to the Default desktop manually. You need to click the application's button to close it and return to windows - however, you can easily change the application's source code to make the button invisible or disabled and then the user can't get back to the OS at all. I just tried out myself - even Ctrl-Alt-Del doesn't help. So here's the code:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict
Rem
bbdoc:WinDesktop
EndRem
'Module fmc.WinDesktop

'ModuleInfo "Version: 0.06"
'ModuleInfo "Modserver: Fabian"
?Win32

Import brl.linkedlist
Import brl.win32maxgui

Const LabelText$ = "Be sure that the user has no possibility to switch back the desktop manually; "+..
"only by clicking the button below you can access to the old windows system desktop, however, "+..
"you can easily edit this code and write it without the button."

If Len AppArgs = 2
  Global Window:TGadget = CreateWindow ( "" , 50 , 50 , 400 , 424 , Desktop ( ) , 0 )
  Global Label:TGadget = CreateLabel ( LabelText , 0 , 0 , 400 , 400 , Window )
  Global Button:TGadget = CreateButton ( "Switch back, please!" , 0 , 400 , 400 , 24 , Window )
  AddHook EmitEventHook , Func
  Global Ending
  While Not Ending
    WaitEvent
  Wend

  Function Func:Object ( id , data:Object , context:Object )
    Local Event:TEvent = TEvent ( data )
    If Event.source = Button And Event.id = EVENT_GADGETACTION
      SwitchUserDesktop AppArgs [ 1 ]
      Ending = True
    EndIf
    Return data
  EndFunction
Else
  Local Desk$ = "OurNewDesktop"
  CreateProcessOnDesktop "~q" + AppFile + "~q Default" , Desk
  While Not SwitchUserDesktop ( Desk )
  Wend
EndIf

Rem
bbdoc:Create a process on a specified desktop
returns:#brl.blitz.True if successfully, else #brl.blitz.False
EndRem
Function CreateProcessOnDesktop ( CmdLine$ , Desk$ )
  Local Success
  Local StartupInfo:TStartupInfo = New TStartupInfo
  Local ProcessInformation:TProcessInformation = New TProcessInformation
  StartupInfo.Size = SizeOf TStartupInfo
  StartupInfo.Desk = Desk.ToWString ( )
  Local Cmd:Short Ptr = CmdLine.ToWString ( )
  If CreateProcessW ( Null , Cmd , Null , Null , False , 0 , Null , Null , StartupInfo , ProcessInformation )
    CloseHandle ProcessInformation.Process
    CloseHandle ProcessInformation.Thread
    Success = True
  EndIf
  MemFree StartupInfo.Desk
  MemFree Cmd
  Return Success
EndFunction

Rem
bbdoc:Change the user's desktop
returns:#brl.blitz.True if successfully, else #brl.blitz.False
EndRem
Function SwitchUserDesktop ( Name$ )
  Local Success
  Local Desk:Short Ptr = Name.ToWString ( )
  Local Handle = OpenDesktopW ( Desk , 0 , False , $100 )
  If Handle
    If SwitchDesktop ( Handle )
      Success = True
    EndIf
    CloseDesktop Handle
  EndIf
  MemFree Desk
  Return Success
EndFunction

Rem
bbdoc:Get the desktop names
returns:A string array containing the names of the desktops
EndRem
Function GetDesktops$ [] ( )
  Local List:TList = CreateList ( )
  EnumDesktopsW GetProcessWindowStation ( ) , EnumProc , ( Int Ptr Varptr List ) [ 0 ]
  Local ObjectArray:Object [] = ListToArray ( List )
  Local StringArray$ [ Len ObjectArray ]
  For Local I = 0 Until Len StringArray
    StringArray [ I ] = String ObjectArray [ I ]
  Next
  Return StringArray

  Function EnumProc ( Desk:Short Ptr , Param:TList )
    ListAddLast Param , String.FromWString ( Desk )
    Return True
  EndFunction
EndFunction

Private

Type TProcessInformation
  Field Process
  Field Thread
  Field ProcessId
  Field ThreadId
EndType

Type TStartupInfo
  Field Size
  Field Reserved0:Short Ptr
  Field Desk:Short Ptr
  Field Title:Short Ptr
  Field X
  Field Y
  Field XSize
  Field YSize
  Field XCountChars
  Field YCountChars
  Field FillAttribute
  Field Flags
  Field ShowWindow:Short
  Field Reserved1:Short
  Field Reserved2:Byte Ptr
  Field StdInput
  Field StdOutput
  Field StdError
EndType

Extern "Win32"
  Function EnumDesktopsW ( WinSta , Func:Byte Ptr , Param )
  Function GetProcessWindowStation ( )
  Function CreateProcessW ( App:Short Ptr , Cmd:Short Ptr , PA:Byte Ptr , TA:Byte Ptr , IH , CF , Env:Byte Ptr , CD:Short Ptr , SI:Byte Ptr , P:Byte Ptr )
  Function CloseHandle ( Obj )
  Function OpenDesktopW ( Name:Short Ptr , F , I , DA )
  Function CloseDesktop ( Desk )
  Function SwitchDesktop ( Desk )
EndExtern
?</textarea><br>The functions are extracted from my module, feel free to use them in your applications if you like.<br>I used code like this to create a <a href="http://webspace100.we.funpic.de/root/programs/LockSystem.exe" target="_blank">winlock tool</a> (source <a href="http://webspace100.we.funpic.de/root/programs/LockSystem.bmx" target="_blank">here</a>), which can be used to protect the system if you are temorary away and leave the system alone. The application asks you for a password and as soon as you entered it the OS gets locked and you can't do anything if you don't enter the password again.<br><br>I'm quite sure that the user can't do anything on the secondary desktop; except the actions you explicitly allowed. <br><br></td></tr></table><br>
<a name="735457"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >EOF</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Great work Franz.<br>Thats the sort of thing I'm after.<br>I'll have a play. Many thanks! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
