<!DOCTYPE html><html lang="en" ><head ><title >Only now do you understand the true power</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Only now do you understand the true power</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Only now do you understand the true power</a><br><br>
<a name="959818"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> My brief excursion into wxWidgets left me with one idea I picked up from looking at the program structure.  Instead of using global variables for all my controls, I am separating the application into types with fields for the different gadgets.  Instead of having a bunch of global variables like ScriptEditorWindow I can just use the app.scripteditor.window field.  Using the context argument with event hooks, all event handling for the relevant controls can be done easily in one place.  I thought this might help some other people, since I have been using MaxGUI for a long time, but never thought to be this organized about it:<br><pre class=code>SuperStrict

Import maxgui.maxgui
Import maxgui.win32maxguiex
Import maxgui.proxygadgets
Import "LoadMenu.bmx"
Import "../Common/Framewerk/Framewerk.bmx"

Type TScriptEditor

	Field window:TGadget
	Field textarea:TGadget

	Method Delete()
		RemoveHook EmitEventHook,EventHook,Self
	EndMethod
	
	Method Init(group:TGadget)

		window=CreateWindow("Script",0,0,600,400,group,WINDOW_TITLEBAR|WINDOW_STATUS|WINDOW_RESIZABLE|WINDOW_MENU)
		textarea=CreateTextArea(0,0,window.ClientWidth(),window.ClientHeight(),window)
		SetGadgetLayout textarea,1,1,1,1
		
		LoadMenu "script.mnu",WindowMenu(window)
		UpdateWindowMenu(window)
		
		AddHook EmitEventHook,EventHook,Self
		
	EndMethod
	
	Method Open()
		ShowGadget window
		ActivateGadget window		
	EndMethod
	
	Method Close()
		ActivateGadget window.parent
		HideGadget window	
	EndMethod
	
	Function EventHook:Object(id:Int,data:Object,context:Object)
		Local event:TEvent=TEvent(data)
		Local scripteditor:TScriptEditor=TScriptEditor(context)
		If event
			Select event.id
				
			Case EVENT_WINDOWCLOSE
				If event.source=scripteditor.window
					scripteditor.close()
					Return Null
				EndIf
		
			Case EVENT_MENUACTION
				Select String(TGadget(event.source).extra)
					
				Case "ScriptEditor"
					scripteditor.open()
					Return Null
					
				Case "CloseProgram"
					End
				
				Case "RunScriptFile"
					Local file:String=RequestFile("Open File","Lua Script (*.lua):lua;Text Files (*.txt):txt;All Files:*")
					If file
						TScript.errorstring=""
						If LoadScript(file)
							
						Else
							If TScript.errorstring
								Notify TScript.errorstring,1
							Else
								Notify "Failed to load script ~q"+file+"~q.",1										
							EndIf
						EndIf
					EndIf
					Return Null
					
				Case "OpenScript"
					Local file:String=RequestFile("Open File","Lua Script (*.lua):lua;Text Files (*.txt):txt;All Files:*")
					If file
						Local source$=LoadString(file)
						If source
							SetGadgetText scripteditor.textarea,source
							ShowGadget scripteditor.window
							If WindowMinimized(scripteditor.window) RestoreWindow scripteditor.window
							ActivateGadget scripteditor.window
						EndIf
					EndIf
					Return Null
					
				Case "CloseScriptEditor"
					scripteditor.close()
					Return Null
				
				EndSelect
				
			EndSelect
		EndIf
		Return data
	EndFunction
	
EndType</pre> <br><br></td></tr></table><br>
<a name="959834"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TaskMaster</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> For a minute there, I thought Xylvan was back... <br><br></td></tr></table><br>
<a name="959836"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> In fact, you can create a class like this:<br><pre class=code>Type TEventHandler
	
	Method New()
		AddHook EmitEventHook,EventHook,Self
	EndMethod
	
	Method Delete()
		RemoveHook EmitEventHook,EventHook,Self
	EndMethod
	
	Method ProcessEvent:TEvent(event:TEvent) Abstract
	
	Function EventHook:Object(id:Int,data:Object,context:Object)
		Local event:TEvent=TEvent(data)
		If event Return TEventHandler(context).ProcessEvent(event) Else Return data
	EndFunction
	
EndType</pre><br>And your app components are done like this:<br><pre class=code>SuperStrict

Import maxgui.maxgui
Import maxgui.win32maxguiex
Import maxgui.proxygadgets
Import "../LoadMenu.bmx"
Import "../../../Common/Framewerk/Framewerk.bmx"
Import "../EventHandler.bmx"

Type TScriptEditor Extends TEventHandler

	Field menufile:String="script.mnu"
	Field window:TGadget
	Field textarea:TGadget

	Method Init(group:TGadget)

		window=CreateWindow("Script",0,0,600,400,group,WINDOW_TITLEBAR|WINDOW_STATUS|WINDOW_RESIZABLE|WINDOW_MENU|WINDOW_HIDDEN)
		textarea=CreateTextArea(0,0,window.ClientWidth(),window.ClientHeight(),window)
		SetGadgetLayout textarea,1,1,1,1
		
		If LoadMenu(menufile,WindowMenu(window))
			UpdateWindowMenu(window)
		Else
			Notify "Failed to load menu file ~q"+menufile+"~q.",1
		EndIf
		
	EndMethod
	
	Method Open()
		ShowGadget window
		ActivateGadget window		
	EndMethod
	
	Method Close()
		ActivateGadget window.parent
		HideGadget window	
	EndMethod

	Method ProcessEvent:TEvent(event:TEvent)
		Select event.id
		
		Case EVENT_WINDOWCLOSE
			If event.source=window
				close()
				Return Null
			EndIf
	
		EndSelect
		Return event
	EndMethod

EndType</pre> <br><br></td></tr></table><br>
<a name="959974"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jake L.</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> ...but MaxGui is missing some decent snowflakes and bloom effects... <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
