<!DOCTYPE html><html lang="en" ><head ><title >How should I go about doing this?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >How should I go about doing this?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >How should I go about doing this?</a><br><br>
<a name="973634"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Arabia</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Not sure if this should be in the beginners section or not.<br><br>I'm stuffing about writing a sports type game.<br><br>I've got the code right to make my player run from point1 (x1,y1) to point2 (x2, y2)<br><br>I've got the code right to make the ball go from point1 (x1,y1) to point2 (x2, y2) &lt;--- yeah, same sort of code, but with simple bounce, ball slow down physics.<br><br>The code for each is in two functions:<br>BallTo (x1,y1,x2,y2,speed,angle)<br>PlayerTo (x1,y1,x2,y2)<br><br>Now this all works fine, but now I need to have both running simultaneously.  i.e. as the ball heads in one direction, I need the player to be chasing after it.<br><br>Do I need to re-work everything into one single function?<br><br>Any suggestions?<br><br>Hope this isn't too stupid a question, still learning very very slow :) <br><br></td></tr></table><br>
<a name="973635"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sokurah</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Wouldn't you basically do this;<br><br>BallTo (BallX1, BallY1, BallX2, BallY2, speed, angle)<br>PlayerTo (PlayerX1, PlayerY1, BallX1, BallY1)<br><br>Assuming of course that the BallX1 &amp; BallY1 vars are updated as the ball moves.<br><br>Although if I was doing it my code would probably look more like;<br><br>BallTo (angle, speed)<br>PlayerTo (BallX, BallY) <br><br></td></tr></table><br>
<a name="973637"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Digital Anime</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> You could do the following to calculate where the ball should be.<br><br>if you would take the players values and do x2-x1 and y2-y1 you'll end up with the distance on the x and y axis your player is gonna travel. And with those 2 values you can start calculate the offset of the ball.<br><br>Sokurah's idea is good as well, but it also depends a lot on how the player is controlled if you can use that.<br>Is the player controlled directional with just keyboard/joystick input or do you use the mouse to click somewhere on the screen to get the players x2 and y2 values? <br><br></td></tr></table><br>
<a name="973640"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Arabia</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> The players or the ball are not controlled as such - it's just a random thing for the ball, and the player then chases after it. <br><br>I suppose it's a bit hard to explain without me writing a novel on the game I am trying to re-write (improve on) - it's an old CPC464 cricket game called Test Match Cricket (written by CRL) &lt;-- I use APE as an emulator and you can download the ROM image if anyone is interested in retro cricket games.<br><br>Okay - short explanation, hope this helps (but you will need to know cricket, sry don't know what country you are all from):<br><br>In the original game - the player would hit the ball, and it would go to it's final destination.  The batters would then decide if they want to run or not.  <br><br>If they decide to run, then they start running and the nearest fielder runs to where the ball is.<br><br>Now, I've got the functions written for both ball moving and player running, I just need both functions to run at the same time.<br><br>What I want is for the fielder (player) to start running after the ball while it is on it's way to the destination. I know how to do this, calculate the final location of the ball and make the player run to this position.<br><br>I hope that makes sense.<br><br>Basically, I've got two images moving (the ball and the player - will be four or more images later on) - do I need to include these all in one function - or should I be using Types (in which case I need to muck around more learning about them before I get too far down this path)<br><br>In all honestly, I probably should start with something a little more simple as a programming task, but oh well :) <br><br></td></tr></table><br>
<a name="973643"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Arabia</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm beginning to think that to do this properly, the maths could get rather complex.<br><br>To have a player (running one direction at a fixed speed X) to cut off a ball going in anther direction (at a variable speed and sometimes in the air - i.e. no point cutting off the ball if it's 10m above their head) might be a little involved.<br><br>Hmmmm, I'll keep thinking about it.<br><br>If I can get it working (very basic version), I'll post my code, surely it will be helpful to someone :) <br><br></td></tr></table><br>
<a name="973648"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Muttley</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sounds like you need to add some pursuit behaviour to the player object.<br><br><a href="http://www.red3d.com/cwr/steer/PursueEvade.html" target="_blank">http://www.red3d.com/cwr/steer/PursueEvade.html</a> <br><br></td></tr></table><br>
<a name="973650"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Arabia</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> @muttley<br><br>Had a quick look at that....<br><br>Sounds like I need to go back to school to redo advanced mathematics to even being to understand that LOL<br><br>I'm far too old to be learning such things :) <br><br></td></tr></table><br>
<a name="973654"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >FlameDuck</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Warning! Unoptimized code follows: <a href="http://code.google.com/p/ascr/source/browse/trunk/csr/arise.mod/bline.mod/bline.bmx" target="_blank">http://code.google.com/p/ascr/source/browse/trunk/csr/arise.mod/bline.mod/bline.bmx</a><br><br>You need to do something like this. It's basically a Bresenham line algorithm with a built-in iterator, so you can go through each point one at a time. This doesn't address your specific problem, but illustrates the technique, and you should be able to take it from there. <br><br></td></tr></table><br>
<a name="973745"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Fry Crayola</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Regarding how you move the players and the ball simultaneously, you'll want to look into things like delta timing or fixed rate logic - there's plenty of information on both and more on the forums. The basic principle though is that you have a logic loop which will update the positions of the ball and player based on the amount of time that has passed since the last time you updated the display. <br><br><br>On the issue of the routines though:<br><br>You don't want your fielder to move to the ball's final location. What you want is for the fielder to move to the nearest point at which he can intercept the ball. Otherwise your guys aren't going to catch anyone out unless they get pretty darn lucky.<br><br>I had a similar problem some years back (I'm making a football game), so hopefully this will be of use to you.<br><br>In order to calculate the point of interception, I initially figured that I could use some simple enough ballistic mechanics to determine where the ball and player would meet. Unfortunately, it turned out to be a lot more complex than I had envisaged and I never did work it out.<br><br>Instead, what I did was I'd have the game work out where the ball was going to go ahead of time. I calculated its future position every 1/8 of a second, for a period of about ten seconds - more than enough time. I stored each of these locations in a TList together with the time it takes for the ball to get there. You can calculate its position using even smaller time steps for greater accuracy at the expense of speed, but it'll still be pretty fast. There's an added provision for height - I didn't store any position and time for when the ball was too high for a player to realistically reach. This will speed things up for the next step.<br><br>Now, each player would run at a set speed. So for each player in a given team, I'd run through the list of ball positions to find the earliest possible time, if any, that the player would reach the position before the ball. The time taken is equal to the distance to travel, divided by the velocity of the player. If this time is less than that stored for the ball to get there, then the player would arrive first. Once found, I stored that location for the player, as he'd need it later.<br><br>Once done for all players, I selected the player who would intercept it in the least amount of time, and told him to run towards the position where he'd intercept it. <br><br></td></tr></table><br>
<a name="973758"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_Skully</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Your best bet is to give all moving objects vector based movement...<br><br>Basically, you take the angle between where the player is and where the player needs to be to intercept the ball based on its maximum speed and the current speed/direction of the ball.<br><br>Its one of those "where does a train A moving at x speed and a train B moving at y speed meet" type math questions you got in school, except that now you have directional factors in play (vector math)<br><br>After that you will need to add tactics in the form of AI but thats an entirely different animal. <br><br></td></tr></table><br>
<a name="973776"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Arabia</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Fry - Cheers, I'll look into Delta timing a bit now.<br><br>Thanks everyone, I think I've got a fair idea of how to do the maths (in my head anyway) now and the things that I need to take into account.  <br><br>I'll see if I can work on it more today.<br><br>If I can work something out, I'll try and upload an executable and possibly some code.  What is a good free site for hosting small files &amp; pics? <br><br></td></tr></table><br>
<a name="973890"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Arabia</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've been messing about with this all day, and here's what I've come up with - I think it's interesting, maybe some others will to.<br><br>I'm still very new to BM, so I'm not using any OOP or even anything very advanced, so yes, this could be rewritten a lot better I'm assuming.  I do think I've got the logic right, and maybe that will be more help to others than my lack of more advanced coding techniques :)<br><br>Controls are shown at the bottom of the window.  Variables shown at the top.<br><br>Basically, run the program.  You have a ball (red) on the left, and player (blue) on the right.  <br><br>Click where you want the ball to travel to.<br><br>Now press "P" to generate the path that the player will need to run to to get the ball (if they can)<br><br>Press "R" to run the objects (the ball &amp; player) at their respective speeds to see that they do infact meet at the same time.<br><br>You can modify the ball speed and player speed (roughly between 1 &amp; 5) to see the difference it makes.<br><br>Got some more stuff I want to work on with this yet such as:<br> - Ball Height (player can't intercept the ball if it is in the air<br> - Have the ball slow down gradually as it runs on the ground.<br><br>Let me know if you are interested, and I'll post again when this is done<br><br>Comments/Suggestions if you find bugs/logic errors.<br><br>Enjoy :) <br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'
' Ball Physics
'

' A demonstration of a player running after a ball
' The speed of the ball and the player can be changed after 
' each simulation to show which way the player should run to 
' cut off the ball at the optimal time.
'
 
SuperStrict

Global ballX:Double = 200
Global ballY:Double = 300			' starting x &amp; y position for the ball
Global ballSpeed:Double = 3			' the speed the ball will initally move at
Global groundFriction:Double = 0.05	' friction (applied against the ball speed to slow it down)
Global playerX:Double = 600
Global playerY:Double = 300			' stating x &amp; y position for the player
Global playerSpeed:Double = 1.5		' the speed the player will initally move at
Global ballToX:Double = 0
Global ballToY:Double = 0			' holds the final destination of the ball (if not touched by a player)
Global showConsoleInfo:Int = 1      ' 1 if data is to be output to the console, 0 if not.
Global runToText:String = ""		' Holds information about where the player should run to for displaying 
Global runToX:Double
Global runToY:Double				' Holds the position that the player is going to run to to intercept the ball

'------------------------------------------------------------------------------------------------------------
' Function: ShowControls()
' Returns: Nothing
' Purpose: Show the controls available to change variables during run time at the bottom of the screen.
Function ShowControls()
	SetColor (196, 196, 196)
	DrawRect (1, 560, 800, 600)
	SetColor (32, 32, 32)
	DrawText ("C:Console Output On/Off | P:Calculate Paths | Q:Ball Speed Up .1 | A:Ball Speed Down .1", 5, 565)
	DrawText ("W:Player Speed Up .1 | S:Player Speed Down .1 | R:Run Objects", 5, 580)
End Function

'------------------------------------------------------------------------------------------------------------
' Function: StatusInfo(text:String, text2: String)
' Returns: Nothing
' Purpose: Clears a section at the top of the screen by drawing a black rectangle and then display the 
'          information passeed in as "text" at the top of the screen in white, and the info in "text2"
'		   on the next line.
'
Function StatusInfo(text:String, text2:String, text3:String)
	SetColor (196, 196, 196)
	DrawRect(1, 1, 800, 36)
	SetColor (32, 32, 32)
	DrawText (text, 3, 1)
	DrawText (text2, 3, 12)
	DrawText (text3, 3, 23)
	Flip
End Function

'------------------------------------------------------------------------------------------------------------
' Function: ShowVariables
' Returns: Nothing
' Purpose: Puts all of the variables (i.e. Ball &amp; Players position, speed etc. into a series of string 
'          variables, and then passes them to "StatusInfo" to display them on the screen
Function ShowVariables()
	Local text:String
	Local text2:String
	
	' Force ball, player &amp; ballTo values back to Int's for display purposes
	Local bX:Int = ballX
	Local bY:Int = ballY
	Local pX:Int = playerX
	Local pY:Int = playerY
	Local btX:Int = ballToX
	Local btY:Int = ballToY
	
	text = "Ball X,Y,Speed:" + bX + "," + bY + ", " + Left(ballSpeed, 4) + " "
	text = text + "Player X,Y,Speed:" + pX + "," + pY + "," + Left(playerSpeed, 4) + " "
	text2 = "Ball Travelling to X,Y:" + btX + "," + btY
	StatusInfo(text, text2, runToText)
End Function

'------------------------------------------------------------------------------------------------------------
' Function: DrawBoth()
' Returns: Nothing
' Purpose: Draws a circle of different colours to represent the ball &amp; player
'
Function DrawBoth()
	SetColor (255,0,0)
	DrawOval(ballX , ballY , 5 , 5)
	SetColor (0,255,255)
	DrawOval(playerX , playerY , 5 , 5) 
	Flip	
End Function

'------------------------------------------------------------------------------------------------------------
' Function: LineLength(x1:Int, y1:Int, x2:Int, y2:Int) 
' Returns: double
' Purpose: Calculates the distance between two points on a line (x1,y1) and (x2, y2)
'
Function LineLength:double (x1:Int, y1:Int, x2:Int, y2:Int)
	Local dist:double
	
	dist = Sqr((x1 - x2) ^ 2 + (y1 - y2) ^ 2)
	Repeat
	Until Not MouseDown(1)
	Return dist
End Function

'------------------------------------------------------------------------------------------------------------
' Function: ShowBallPath (x1:Int, y1:Int)
' Returns: Nothing
' Purpose: This function draws a line to indicate the path of the ball from it's initial location
'          which is held in the global variables ballX &amp; ballY.  
'
Function ShowBallPath (x1:Int, y1:Int)
	
	Cls
	DrawBoth	
	SetColor (128,128,128)
	DrawLine (ballX , ballY , x1 , y1) 
	Flip
	
	ballToX = x1
	ballToY = y1
	Flip
End Function

'------------------------------------------------------------------------------------------------------------
' Function: MoveObjects
' Returns: Nothing
' Purpose: This function simple animates the ball and player moving along their path at their respective 
'          speeds.  If the player can not get to the ball before it reaches it's destination (i.e. it is 
'          going to fast, then the line for player (blue) will not meet with the ball (red)
Function MoveObjects()
	Local ballStepX:double
	Local ballStepY:double
	Local playerStepX:double
	Local playerStepY:double
	Local ballStepsRequired:double
	Local playerStepsRequired:double
	Local dist:double
	Local counter:Int
	Local maxSteps:Int
	
	dist = LineLength(ballX, ballY, runToX, runToY)
	ballStepsRequired = dist / ballSpeed
	dist = LineLength(playerX, playerY, runToX, runToY)
	playerStepsRequired = dist / playerSpeed
	
	maxSteps = Min(ballStepsRequired, playerStepsRequired)
	
	ballStepX = (runToX - ballX) / ballStepsRequired
	ballStepY = (runToY - ballY) / ballStepsrequired
	playerStepX = (runToX - playerX) / playerStepsRequired
	playerStepY = (runToY - playerY) / playerStepsRequired

	If showConsoleInfo = 1 Then
		Print "MOVE OBJ: Ball Step X &amp; Y:" + ballStepX + "," + ballStepY
		Print "MOVE OBJ: Player Step X &amp; Y:" + playerStepX + "," + playerStepY
	EndIf
		
	For counter = 0 To maxSteps
		ballX = ballX + ballStepX
		ballY = ballY + ballStepY
		playerX = playerX + playerStepX
		playerY = playerY + playerStepY
		DrawBoth
	Next
	ballX = 200
	ballY = 300
	playerX = 600
	playerY = 300
End Function
'------------------------------------------------------------------------------------------------------------
' Function: CaculatePaths
' Returns: Nothing
' Purpose: This function calulates at what speed the player object should run so that it meets with the 
'          ball object at the first possible time.
'
'          NOTE: This section is a little complex. I hope I've commented/explained it enough - My brain hurts :(
'		   NOTE 2: I'm getting old, so if it's not complex to you, it's because your brain works better than mine :P
'
Function CalculatePaths()
	Local dist:double
	Local counter:Int
	Local xPos:Int[1000]
	Local yPos:Int[1000]
	Local xStep:double
	Local yStep:double
	Local ballStepsRequired:double
	
	If showConsoleInfo = 1 Then Print "Start: " + ballX + "," + ballY + " Finish: " + ballToX + "," + ballToY
	
	
	' Step 1: Determine the total distance the balls is to travel.  Divide this by the ball's speed to get 
	'         the number of steps in a for..next loop that will be needed to animate the ball.
	'         i.e. if dist = 300 pixels, and ball speed = 3, then we need 100 steps in our loop.
	dist = LineLength (ballX, ballY, ballToX, ballToY)
	ballStepsRequired = dist / ballSpeed
	
	If showConsoleInfo = 1 Then Print "Length: " + dist + " Speeed: " + ballSpeed + " Ball Steps Required: " + ballStepsRequired
	
	' Step 2: Now we have to work out x &amp; y step sizes to take on each loop of our for..next loop.
	' If steps = 100 (from 1. above) and the ball is going from 200,300 to 400, 400 then we get
	' xStep = (400-200) / 100 = 2 Pixels per step
	' yStep = (400-300) / 100 = 1 Pixel per step
	xStep = (ballToX - ballX) / ballStepsRequired
	yStep = (ballToY - ballY) / ballStepsRequired
	
	' Step 3: What we are doing here, is stepping through the complete loop, and storing in arrays (xPos - xPosition
	' at point 0,1,2 etc and yPost - yPosition at point 0,1,2 etc).  So going by the above figures: 
	' steps = 100 xStep = 2 yStep = 1
	' xPos[0] will be 200 + 0 * 2 = 200 	yPos[0] will be 300 + 0 * 1 = 300
	' xPos[1] will be 200 + 1 * 2 = 202		yPos[1] will be 300 + 1 * 1 = 301 
	' etc.  
	For counter = 0 To ballStepsRequired
		xPos[counter] = ballX + counter * xStep
		ypos[counter] = ballY + counter * yStep
	Next
	
	' This section just prints to the console (if console info is on - i.e. showConsoleInfo = 1) the array's that
	' we created in Step 3.
	If showConsoleInfo = 1 Then
		Print "XStep: " + xStep + " YStep: " + yStep
		For counter = 0 To ballStepsRequired Step 5
			Print counter + ":" + xPos[counter] + "," + yPos[counter] + " " + ..
				  (counter + 1) + ":" + xPos[counter + 1] + "," + yPos[counter + 1] + " " + ..
				  (counter + 2) + ":" + xPos[counter + 2] + "," + yPos[counter + 2] + " " + ..
				  (counter + 3) + ":" + xPos[counter + 3] + "," + yPos[counter + 3] + " " + ..
				  (counter + 4) + ":" + xPos[counter + 4] + "," + yPos[counter + 4]
		Next
	EndIf
	
	' Okay, that is it for the ball section.  We have stored in an array, exactly where the ball will be at any given time
	' from the time it starts moving until the time it gets to its destination.
	
	
	' The following section goes back through the arrays we just created.  It compares where the the ball is at any given
	' time, and then determines if the player can run to this spot by the time the ball would get there.
	
	' Couple of variables, I'll leave them declared here - as they only relate to code under.
	' 
	Local lowVal:double = 1000
	Local lowValAt:Int
	
	' Step 4: Now we are going to step back through the array.
	' First we work out the distance between the ball and the player at each given time of the balls journey
	' then some Console output if you want to see figures
	' Now there are three figures we rely on:
	' counter - this is which step we are in of the ball movement, i.e. the ball takes "counter" steps to get here
	' dist - players distance to the ball at this time
	' (dist / playerSpeed) - this is how many steps it would take the player to get to the same point as the ball
	' We now step through and find the closest possible match between Ball Steps (counter) and the number of steps the
	' player would need to get here.
	'
	' I suggest you run the code with Console Output on, and have a look at the results:
	'
	' When a "Step Difference:" value is lowest, it means that the player can run directly to this point and get there
	' at approximately the same time as the ball.
	'
	' We then store in lowVal the lowest value we found.  lowValAt stores the array reference to where we found it.
	For counter = 0 To ballStepsRequired
		dist = LineLength (xPos[counter], yPos[counter], playerX, playerY)
		If showConsoleInfo = 1 Then
			Print counter + " [" + xpos[counter] + "," + ypos[counter] + "]- Distance player to ball:" + dist + ..
			" | Player steps to get to ball:" + dist / playerSpeed + ..
			" Step Difference: " + Abs(counter - (dist / playerSpeed))
		EndIf
		If Abs(counter - (dist / playerSpeed)) &lt; lowVal Then
			lowVal = Abs(counter - (dist / playerSpeed))
			lowValAt = counter
		End If
	Next
		
	If showConsoleInfo = 1 Then
		Print
		Print "Low Value is at " + lowValAt + " and is " + lowVal
		If lowVal &lt; 1 Then Print "Player Should run to " + xPos[lowValAt] + "," + yPos[lowValAt] + " to intercept the ball at the first possible time"
	EndIf

	' Final step
	' If the lowvalue that we found is less than 1 then this is where the player should run to to meet the ball.
	' If the lowvalue is greater than 1 (you can change this to see what difference it makes - e.g. you may want it greater
	' if allowing for a player diving etc to get a ball just out of reach) then it means that the player could never get within
	' 1 unit of the ball at any stage of it's path, and therefore, the player should just run to where the ball is going to come to 
	' a rest.
	
	If lowVal &lt; 1 Then
		runToText = "Player Should run to " + xPos[lowValAt] + "," + yPos[lowValAt] + " to intercept the ball at the first possible time"
		runToX = xPos[lowValAt]
		runToY = yPos[lowValAt]
	Else
		runToX = ballToX
		runToY = ballToY
		If showConsoleInfo = 1 Then Print "Player can not intercept ball while in motion and must run to where it will finally stop"
		runToText = "Player can not intercept ball while in motion and must run to where it will finally stop"
	EndIf
	
	SetColor (255, 128, 128)
	DrawLine (playerX, playerY, runToX, runToY)
	
	Flip
	Repeat
	Until Not KeyDown (KEY_B)
End Function

'
' Main Program Init &amp; Loop
'

Graphics 800, 600
Cls
DrawBoth
Repeat
	ShowVariables
	ShowControls

	If MouseDown(1) 
		ShowBallPath(MouseX() , MouseY())
	EndIf

	If KeyDown(KEY_R) Then
		MoveObjects
		Repeat
		Until Not KeyDown(KEY_R)
	End If

	If KeyDown(KEY_P)
		If ballToX &lt;&gt; 0 Or ballToY &lt;&gt; 0 Then
			CalculatePaths
		EndIf
		Repeat
		Until Not KeyDown(KEY_P)
	End If

	If KeyDown(KEY_Q) Then
		If ballSpeed &lt; 5.0 Then
			ballSpeed = ballSpeed + 0.1
		EndIf
		Repeat
		Until Not KeyDown(KEY_Q)
	EndIf

	If KeyDown(KEY_A) Then
		If ballSpeed &gt; 1.0 Then
			ballSpeed = ballSpeed - 0.1
		EndIf
		Repeat
		Until Not KeyDown(KEY_A)
	EndIf

	If KeyDown(KEY_W) Then
		If playerSpeed &lt; 5.0 Then
			playerSpeed = playerSpeed + 0.1
		End If
		Repeat
		Until Not KeyDown(KEY_W)
	EndIf

	If KeyDown(KEY_S) Then
		If playerSpeed &gt; 1.0 Then
			playerSpeed = playerSpeed - 0.1
		End If
		Repeat
		Until Not KeyDown(KEY_S)
	End If

	If KeyDown(KEY_C)
		If showConsoleInfo = 1 Then
			showConsoleInfo = 0
			StatusInfo ("Console Info Off", "", "")
		Else
			showConsoleInfo = 1
			StatusInfo ("Console Info On", "", "")
		End If
		Repeat
		Until Not KeyDown(KEY_C)
	End If
Until KeyDown(Key_ESCAPE)
</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
