<!DOCTYPE html><html lang="en" ><head ><title >I'm Drawing A Blank Here</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >I'm Drawing A Blank Here</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >I'm Drawing A Blank Here</a><br><br>
<a name="1289588"></a>

<a name="1289589"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm pretty sure a lot of you are more familiar with drawing on canvas() than I am.<br><br>Could you please show me how to correct this so both windows appear at the same time ? One is smaller than the other so it should be possible.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' Problems with Double Canvas

Strict
Import MaxGui.Drivers
SetGraphicsDriver(GLMax2DDriver())
Global deskrect[4]
GetDesktopArea(deskrect) '&lt;- Get Desktop size

Local zvideo:TGadget ' main video screen
Local zvidborder:tgadget ' draw simple border on full-size window (always correct size)
Local zvidmain:tgadget ' restricted 960x704 pixel window
SetGraphicsDriver(GLMax2DDriver()) ' no problems
zvideo=createwindow("",deskrect[0],deskrect[1],deskrect[2],deskrect[3],,0)
Global mx=clientwidth(zvideo),my=clientheight(zvideo),mk
Local i,j

zvidborder=createcanvas(0,0,mx,my,zvideo,0) ' initialize border window
SetGraphics canvasgraphics(zvidborder) ' activate it
SetBlend alphablend ' set so black is transparent
For i=deskrect[1] To deskrect[1]+deskrect[3]-1
  For j=deskrect[0] To deskrect[0]+deskrect[2]-1
    If i&lt;4 Or j&lt;4 Or i&gt;deskrect[3]-5 Or j&gt;deskrect[2]-5
      SetColor 0,0,Rand(128,255) ' simple draw blue border
      Plot j,i
    EndIf
  Next
Next
Flip

zvidmain=createcanvas(mx/2-960/2,my/2-704/2,960,704,zvideo,0) ' now create smaller screen inside the full-size one
SetGraphics canvasgraphics(zvidmain)
activategadget zvidmain ' activate it
SetBlend alphablend
DrawOval 50,50,100,100 ' and - doesn't work, oval does not appear

Cls
Repeat
  Flip ' pollevent necessary since we are using canvas screens
Until PollEvent()&gt;0 And CurrentEvent.id=event_keydown

Function getdesktoparea(lprect:Int Ptr) ' get desktop area
  systemparametersinfoa(spi_getworkarea,0,lprect,0)
End Function

Extern "Win32" ' library for desktop area
  Function SystemParametersInfoA:Int(action:Int, param:Int, param2:Byte Ptr, winini:Int)
EndExtern
</textarea> <br><br></td></tr></table><br>
<a name="1289596"></a>

<a name="1289597"></a>

<a name="1289598"></a>

<a name="1289599"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dabhand</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well, for one, the canvas with the oval on is under the main canvas [z-order is wrong], and two, the oval isnt being drawn because your drawing it after SetBlend, then clearing the screen, and flipping a blank backbuffer [forever]!<br><br><pre class=code>
'zvidborder=CreateCanvas(0,0,mx,my,zvideo,0) ' initialize border window
'SetGraphics CanvasGraphics(zvidborder) ' activate it
'SetBlend alphablend ' set so black is transparent
'For i=deskrect[1] To deskrect[1]+deskrect[3]-1
 ' For j=deskrect[0] To deskrect[0]+deskrect[2]-1
  '  If i&lt;4 Or j&lt;4 Or i&gt;deskrect[3]-5 Or j&gt;deskrect[2]-5
   '   SetColor 0,0,Rand(128,255) ' simple draw blue border
    '  Plot j,i
    'EndIf
  'Next
'Next
'Flip

zvidmain=CreateCanvas(mx/2-960/2,my/2-704/2,960,704,zvideo,0) ' now create smaller screen inside the full-size one
SetGraphics CanvasGraphics(zvidmain)
ActivateGadget zvidmain ' activate it
SetBlend alphablend

Repeat
	Cls
	DrawOval 50,50,100,100 
    Flip ' pollevent necessary since we are using canvas screens
Until PollEvent()&gt;0 And CurrentEvent.id=event_keydown

</pre><br><br>Dabz <br><br></td></tr></table><br>
<a name="1289600"></a>

<a name="1289601"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Endive</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> I'm Drawing A Blank Here <br></div><br>I thought "Oh god, it's going to be a question about Cls()."<br><br>I never understood maxgui, it seems really counterintuitive to me.  I probably just have to work with it more... <br><br></td></tr></table><br>
<a name="1289637"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Endive.<br><br>One of the main reasons I'm using it is it has the ability of creating windows without frames. If there's an easier way to accomplish it without using MaxGUI, I certainly would like to see that.<br><br>Ultimately what I wanted to do here was create a screen of 960x704 inside a solid black screen that was the full dimensions of whatever monitor the user has.<br><br>Why ? Then you wouldn't have the frame or tasks appear around the edges, so if you had a 1024x768 screen, that area would appear solid black and inside centered is a smaller 960x704 screen that is active.<br><br>Plotting to pixel position 0,0 would actually plot only to the 960x704 screen and disregard the outer 1024x768 entirely.<br><br>. . .<br><br>As for Cls(), outside of SetClsColor(), that's not too hard to figure out. :) <br><br></td></tr></table><br>
<a name="1289646"></a>

<a name="1289653"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Alright gang, here is some interesting code:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
video:tgraphics=CreateGraphics(640,480,0,0,0)
SetGraphics video
SetColor 0,0,255
For i=0 To 479 Step 2
  For j=0 To 639 Step 2
    Plot j,i
  Next
Next
img:TImage=LoadImage(video)
DrawImage img,0,0
Flip
WaitKey
End
</textarea><br><br>Is there a way to easily make this work without using external libraries ? And BONUS if you can remove the border additionally. <br><br></td></tr></table><br>
<a name="1289651"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> if you can remove the border <br></div><br>hiya,<br><br>Do you mean to remove the standard OS frame that surrounds a regular window? if so<br>is <a href="/posts.php?topic=102797" target="_blank">this</a> any help? <br><br></td></tr></table><br>
<a name="1289654"></a>

<a name="1289655"></a>

<a name="1289656"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Clicked THIS and got an error from inside BlitzBasic.com.<br><br><b>ERROR: Internal error</b> <br><br></td></tr></table><br>
<a name="1289657"></a>

<a name="1289659"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> no worries, it links to...<br><br><a href="http://www.blitzbasic.com/Community/posts.php?topic=102797" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=102797</a> <br><br></td></tr></table><br>
<a name="1289660"></a>

<a name="1289661"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Still getting error. Was it deleted ? Trying a different browser. ... Nope. Even Google Chrome gives an error. <br><br></td></tr></table><br>
<a name="1289662"></a>

<a name="1289663"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Are you logged into BlitzMax.com? see if it works if you log into BlitzBasic.com?<br>If that doesn't work then I'll copy paste it here <br><br></td></tr></table><br>
<a name="1289665"></a>

<a name="1289667"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Strange, I'm definitely logged in, Dave. (Checked and rechecked).<br><br>[X] BlitzBasic Forums<br>[X] Blitzbasic.com<br>[X] Blitzmax.com <br><br></td></tr></table><br>
<a name="1289668"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> For me it works - just replace blitzbasic.com with blitzmax.com<br><br>When linking with [.a]text[./a] you just use this code:<br><br>[.a /Community/posts.php?topic=102797]Relative Link[./a]<br>-&gt; <a href="/posts.php?topic=102797" target="_blank">Relative Link</a><br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="1289670"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Derron,<br>I didn't know that you could do relative linking, thats good to know. <br><br></td></tr></table><br>
<a name="1289671"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Ron and Dave, nope. These both still gives me an error.<br><br><a href="http://www.blitzbasic.com/Community/posts.php?topic=102797" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=102797</a><br><a href="http://www.blitzmax.com/Community/posts.php?topic=102797" target="_blank">http://www.blitzmax.com/Community/posts.php?topic=102797</a> <br><br></td></tr></table><br>
<a name="1289672"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> very strange, oh well not to worry, a copy/paste...<br><div class="quote"> <br>In brl.mod/graphics.mod/graphics.bmx, insert the following at line 48:<br><pre class=code>
Const GRAPHICS_BORDERLESS = $40
</pre><br>In brl.mod/dxgraphics.mod/d3d9graphics.bmx, change this section at line 148:<br><pre class=code>
		If depth
			wstyle=WS_VISIBLE|WS_POPUP
		Else
			wstyle=WS_VISIBLE|WS_CAPTION|WS_SYSMENU|WS_MINIMIZEBOX
		EndIf
</pre><br>To:<br><pre class=code>
		If depth
			wstyle=WS_VISIBLE|WS_POPUP
		Else
			If flags &amp; GRAPHICS_BORDERLESS Then
				wstyle = WS_VISIBLE | WS_POPUP
			Else
				wstyle = WS_VISIBLE | WS_CAPTION | WS_SYSMENU | WS_MINIMIZEBOX
			End If
		EndIf
</pre><br>In brl.mod/dxgraphics.mod/d3d7graphics.bmx, change this section at line 290:<br><pre class=code>
Local style=WS_VISIBLE|WS_CAPTION|WS_SYSMENU|WS_MINIMIZEBOX
</pre><br>To:<br><pre class=code>
Local style=WS_VISIBLE|WS_CAPTION|WS_SYSMENU|WS_MINIMIZEBOX
			If flags &amp; GRAPHICS_BORDERLESS Then style = WS_VISIBLE | WS_POPUP
</pre><br>And in brl.mod/glgraphics.mod/glgraphics.win32.c make the following two changes, first at line 8:<br><pre class=code>
enum{
	_BACKBUFFER=	0x2,
	_ALPHABUFFER=	0x4,
	_DEPTHBUFFER=	0x8,
	_STENCILBUFFER=	0x10,
	_ACCUMBUFFER=	0x20,
};
</pre><br>Becomes:<br><pre class=code>
enum{
	_BACKBUFFER=	0x2,
	_ALPHABUFFER=	0x4,
	_DEPTHBUFFER=	0x8,
	_STENCILBUFFER=	0x10,
	_ACCUMBUFFER=	0x20,
	_BORDERLESS=	0x40,
};
</pre><br>And at line 322:<br><pre class=code>
hwnd_style=WS_CAPTION|WS_SYSMENU|WS_MINIMIZEBOX;
</pre><br>Becomes:<br><pre class=code>
		hwnd_style=WS_CAPTION|WS_SYSMENU|WS_MINIMIZEBOX;
		if (flags &amp;&amp; _BORDERLESS) {
			hwnd_style = WS_POPUP;
		}
</pre><br> <br></div><br><br>Are you familiar with the 'building modules' process? After making the changes above hit CTRL+D, or Program menu-&gt;Build modules. To build a module that imports a .c/.cpp/.asm file you'll need to install a gcc compiler. The .c file is an internal part of the opengl implementation used for the GLGraphicsDriver which GLMax2DDriver uses.<br><br>If you just want to check out the borderless example code then you can make changes to just the .bmx files and the D3D7Max2DDriver/D3D9Max2DDriver will show you the effect without the need for a gcc compiler. <br><br></td></tr></table><br>
<a name="1289674"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> You then use GRAPHICS_BORDERLESS as the 'flags' parameter for the Graphics command...<br><br>Graphics 800,600,,,GRAPHICS_BORDERLESS <br><br></td></tr></table><br>
<a name="1289675"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> BACKING UP MOD ... (First !)<br><br>Making changes ...<br><br>BlitzMax\mod\brl.mod\graphics.mod\graphics.bmx<br><br>Line #48 is within the REM / ENDREM, placing the new code at line 51, outside the REM<br><br>Other changes to MOD libs seem fine. Okay, as for compilation, BUILD MODULES is ghosted out, cannot be selected.<br><br>Did you post the borderless example code yet, Dave ? <br><br></td></tr></table><br>
<a name="1289677"></a>

<a name="1289678"></a>

<a name="1289679"></a>

<a name="1289680"></a>

<a name="1289682"></a>

<a name="1289683"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ahh,<br>Well, it can be done. You see, we have the complete source code for BlitzMax ( the mod folder ). The only thing we don't have installed is the compiler itself ( which has been released as open source now ). So you can rewrite the whole of BlitzMax if you so desired but to do that you'll need extra tools and it's not supported by BRL. As 'Max isn't exactly new most people have learned how to change the modules to suit, but to do that you'll need more than the standard installation.<br><br>So as to stay with your main objective it may be better to leave the module tweaks for anoother time? However if you do find time to learn how to build modules then it becomes very easy to add in features to BlitzMax that didn't exist and you'll see just how powerful a language it actually is.<br><br>Until then.... sorry to have butted in, and... as you were ;-) <br><br></td></tr></table><br>
<a name="1289684"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well, I tried Graphics 640,480,,,GRAPHICS_BORDERLESS and I get the error message:<br><br><b>Compile Error - Identifier 'graphics_borderless' not found</b><br><br>Let's take this a step at a time. I need to compile, "glgraphics.bmx" yes ?<br><br>Here is the code for it:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Strict

Rem
bbdoc: Graphics/Graphics
End Rem
Module BRL.Graphics

Rem

Establish sane Flip behaviour!

Windowed mode + non-0 hertz = use softsync

Windowed mode + non-0 hertz = use softsync

End Rem

ModuleInfo "Version: 1.08"
ModuleInfo "Author: Mark Sibly"
ModuleInfo "License: Blitz Shared Source Code"
ModuleInfo "Copyright: Blitz Research Ltd"
ModuleInfo "Modserver: BRL"

ModuleInfo "History: 1.08 Release"
ModuleInfo "History: Mouse repositioned only in fullscreen mode"
ModuleInfo "History: 1.07 Release"
ModuleInfo "History: Flip mode for attached graphics changed to 0"
ModuleInfo "History: 1.06 Release"
ModuleInfo "History: Fixed softsync period init bug"
ModuleInfo "History: 1.05 Release"
ModuleInfo "History: Fixed softsync routine to prevent overflow"
ModuleInfo "History: 1.04 Release"
ModuleInfo "History: Graphics exceptions now caught"
ModuleInfo "History: 1.03 Release"
ModuleInfo "History: Added DefaultGraphicsFlags() Function"
ModuleInfo "History: 1.02 Release"
ModuleInfo "History: Restored FlipHook"
ModuleInfo "History: 1.01 Release"
ModuleInfo "History: Added default flags to SetGraphicsDriver"

Import BRL.System
Import BRL.PolledInput

Rem
bbdoc: Flip Hook id
about:
Use this id with #AddHook to register a function that
is called every #Flip.
End Rem
Const GRAPHICS_BORDERLESS = $40
Global FlipHook=AllocHookId()

Const GRAPHICS_BACKBUFFER=	$2
Const GRAPHICS_ALPHABUFFER=	$4
Const GRAPHICS_DEPTHBUFFER=	$8
Const GRAPHICS_STENCILBUFFER=	$10
Const GRAPHICS_ACCUMBUFFER=	$20

'Const GRAPHICS_SWAPINTERVAL0=	$40
'Const GRAPHICS_SWAPINTERVAL1=	$80

'Const GRAPHICS_SWAPINTERVALMASK=GRAPHICS_SWAPINTERVAL0|GRAPHICS_SWAPINTERVAL1

Type TGraphics

	Method _pad()
	End Method

	Method Driver:TGraphicsDriver() Abstract

	Method GetSettings( width Var,height Var,depth Var,hertz Var,flags Var ) Abstract

	Method Close() Abstract
	
End Type

Type TGraphicsMode

	Field width,height,depth,hertz
	
	Method ToString$()
		Return width+","+height+","+depth+" "+hertz+"Hz"
	End Method

End Type

Type TGraphicsDriver

	Method GraphicsModes:TGraphicsMode[]() Abstract
	
	Method AttachGraphics:TGraphics( widget,flags ) Abstract
	
	Method CreateGraphics:TGraphics( width,height,depth,hertz,flags ) Abstract
	
	Method SetGraphics( g:TGraphics ) Abstract
	
	Method Flip( sync ) Abstract
	
End Type

Private

Global _defaultFlags
Global _driver:TGraphicsDriver
Global _graphicsModes:TGraphicsMode[]
Global _graphics:TGraphics,_gWidth,_gHeight,_gDepth,_gHertz,_gFlags

Global _exGraphics:TGraphics

'Only valid if _exGraphics=_graphics
Global _softSync,_hardSync,_syncRate,_syncPeriod,_syncFrac,_syncAccum,_syncTime

Public

Global GraphicsSeq=1

Function BumpGraphicsSeq()
	GraphicsSeq:+1
	If Not GraphicsSeq GraphicsSeq=1
End Function

Rem
bbdoc: Set current graphics driver
about:
The current graphics driver determines what kind of graphics are created when you use
the #CreateGraphics or #Graphics functions, as well as the graphics modes available.

The #GLGraphicsDriver, #GLMax2DDriver, #D3D7Max2DDriver and #D3D9Max2DDriver functions can all be
used to obtain a graphics driver.

The @defaultFlags parameter allows you to specify graphics flags that will be applied to any
graphics created with #CreateGraphics or #Graphics.
End Rem
Function SetGraphicsDriver( driver:TGraphicsDriver,defaultFlags=GRAPHICS_BACKBUFFER )
	BumpGraphicsSeq
	If driver&lt;&gt;_driver
		If _driver And _graphics _driver.SetGraphics Null
		_graphicsModes=Null
		_driver=driver
	EndIf
	_defaultFlags=defaultFlags
	_graphics=Null
	_gWidth=0
	_gHeight=0
	_gDepth=0
	_gHertz=0
	_gFlags=0
End Function

Rem
bbdoc: Get current graphics driver
about:
Returns the current graphics driver as selected by #SetGraphicsDriver
End Rem
Function GetGraphicsDriver:TGraphicsDriver()
	Return _driver
End Function

Rem
bbdoc: Get current default graphics flags
End Rem
Function DefaultGraphicsFlags()
	Return _defaultFlags
End Function

Rem
bbdoc: Get graphics modes available under the current graphics driver
returns: An array of TGraphicsMode objects
about:
A TGraphicsMode object contains the following fields: @width, @height, @depth and @hertz
End Rem
Function GraphicsModes:TGraphicsMode[]()
	If Not _graphicsModes _graphicsModes=_driver.GraphicsModes()
	Return _graphicsModes[..]
End Function

Rem
bbdoc: Get number of graphics modes available under the current graphics driver
returns: Number of available Graphics modes
about:
Use #GetGraphicsMode To obtain information about an individual Graphics mode
End Rem
Function CountGraphicsModes()
	Return GraphicsModes().length
End Function

Rem
bbdoc: Get information about a graphics mode
about:
#GetGraphicsMode returns information about a specific graphics mode. @mode should be
in the range 0 (inclusive) to the value returned by #CountGraphicsModes (exclusive).
End Rem
Function GetGraphicsMode( index,width Var,height Var,depth Var,hertz Var )
	Local mode:TGraphicsMode=GraphicsModes()[index]
	width=mode.width
	height=mode.height
	depth=mode.depth
	hertz=mode.hertz
End Function

Rem
bbdoc: Determine if a graphics mode exists
returns: True if a matching graphics mode is found
about:
A value of 0 for any of @width, @height, @depth or @hertz will cause that
parameter to be ignored.
End Rem
Function GraphicsModeExists( width,height,depth=0,hertz=0 )
	For Local mode:TGraphicsMode=EachIn GraphicsModes()
		If width And width&lt;&gt;mode.width Continue
		If height And height&lt;&gt;mode.height Continue
		If depth And depth&lt;&gt;mode.depth Continue
		If hertz And hertz&lt;&gt;mode.hertz Continue
		Return True
	Next
	Return False
End Function

Rem
bbdoc: Create a graphics object
returns: A graphics object
about:
#CreateGraphics creates a graphics object. To use this object for rendering, you will
first have to select it using #SetGraphics.

The kind of graphics object returned depends upon the current graphics driver as set by
#SetGraphicsDriver.
End Rem
Function CreateGraphics:TGraphics( width,height,depth,hertz,flags )
	flags:|_defaultFlags
	Local g:TGraphics
	Try
		g=_driver.CreateGraphics( width,height,depth,hertz,flags )
	Catch ex:Object
?Debug
		WriteStdout "CreateGraphics failed:"+ex.ToString()
?
	End Try
	Return g
End Function

Function AttachGraphics:TGraphics( widget,flags )
	flags:|_defaultFlags
	Local g:TGraphics
	Try
		g=_driver.AttachGraphics( widget,flags )
	Catch ex:Object
?Debug
		WriteStdout "AttachGraphics failed:"+ex.ToString()
?
	End Try
	Return g
End Function

Rem
bbdoc: Close a graphics object
about:
Once closed, a graphics object can no longer be used.
End Rem
Function CloseGraphics( g:TGraphics )
	If g=_exGraphics _exGraphics=Null
	If g=_graphics SetGraphics Null
	g.Close
End Function

Rem
bbdoc: Set current graphics object
about:
#SetGraphics will also change the current graphics driver if @g uses a different driver
than the current driver.
End Rem
Function SetGraphics( g:TGraphics )
	If Not g
		If _driver And _graphics _driver.SetGraphics Null
		_graphics=Null
		_gWidth=0
		_gHeight=0
		_gDepth=0
		_gHertz=0
		_gFlags=0
		Return
	EndIf
	Local d:TGraphicsDriver=g.Driver()
	If d&lt;&gt;_driver
		If _driver And _graphics _driver.SetGraphics Null
		_graphicsModes=Null
		_driver=d
	EndIf
	g.GetSettings _gWidth,_gHeight,_gDepth,_gHertz,_gFlags
	d.SetGraphics g
	_graphics=g
End Function

Rem
bbdoc: Get width of current graphics object
returns: The width, in pixels, of the current graphics object
about:
The current graphics object can be changed using #SetGraphics.
End Rem
Function GraphicsWidth()
	Return _gWidth
End Function

Rem
bbdoc: Get height of current graphics object
returns: The height, in pixels, of the current graphics object
about:
The current graphics object can be changed using #SetGraphics.
End Rem
Function GraphicsHeight()
	Return _gHeight
End Function

Rem
bbdoc: Get depth of current graphics object
returns: The depth, in bits, of the current graphics object
about:
The current graphics object can be changed using #SetGraphics.
End Rem
Function GraphicsDepth()
	Return _gDepth
End Function

Rem
bbdoc: Get refresh rate of current graphics object
returns: The refresh rate, in frames per second, of the current graphics object
about:
The current graphics object can be changed using #SetGraphics.
End Rem
Function GraphicsHertz()
	Return _gHertz
End Function

Rem
bbdoc: Get flags of current graphics object
returns: The flags of the current graphics object
about:
The current graphics object can be changed using #SetGraphics.
End Rem
Function GraphicsFlags()
	Return _gFlags
End Function

Rem
bbdoc: Flip current graphics object
about:
#Flip swap the front and back buffers of the current graphics objects.

If @sync is 0, then the flip occurs as soon as possible. If @sync is 1, then the flip occurs
on the next vertical blank.

If @sync is -1 and the current graphics object was created with the #Graphics command,
then flips will occur at the graphics object's refresh rate, unelss the graphics object was 
created with a refresh rate of 0 in which case flip occurs immediately.

If @sync is -1 and the current graphics object was NOT created with the #Graphics command,
then the flip will occur on the next vertical blank.
End Rem
Function Flip( sync=-1 )
	RunHooks FlipHook,Null

	If sync&lt;&gt;-1
		_driver.Flip sync
		Return
	EndIf
	
	If _graphics&lt;&gt;_exGraphics
		_driver.Flip True
		Return
	EndIf

	If _softSync
		_syncTime:+_syncPeriod
		_syncAccum:+_syncFrac
		If _syncAccum&gt;=_syncRate
			_syncAccum:-_syncRate
			_syncTime:+1
		EndIf
		Local dt=_syncTime-MilliSecs()
		If dt&gt;0
			Delay dt
		Else
			_syncTime:-dt
		EndIf
		_driver.Flip False
	Else If _hardSync
		_driver.Flip True
	Else
		_driver.Flip False
	EndIf

End Function

Rem
Function Flip( sync=-1 )
	RunHooks FlipHook,Null
	If sync&lt;&gt;-1
		_driver.Flip sync
		Return
	EndIf
	If _graphics&lt;&gt;_exGraphics Or Not _softSync
		Local sync=False
		If _gDepth sync=True
		_driver.Flip sync
		Return
	EndIf
	_syncTime:+_syncPeriod
	_syncAccum:+_syncFrac
	If _syncAccum&gt;=_syncRate
		_syncAccum:-_syncRate
		_syncTime:+1
	EndIf
	Local dt=_syncTime-MilliSecs()
	If dt&gt;0
		Delay dt
	Else
		_syncTime:-dt
	EndIf
	_driver.Flip False
End Function
End Rem

Rem
bbdoc: Begin graphics
returns: A graphics object
about:
#Graphics is a convenience function that simplifies the process of creating a graphics
object.

Once #Graphics has executed, you can begin rendering immediately without any need for 
#SetGraphics.

#Graphics also enables #{polled input} mode, providing a simple way to monitor the keyboard
and mouse.
End Rem
Function Graphics:TGraphics( width,height,depth=0,hertz=60,flags=0 )
	EndGraphics
	flags:|_defaultFlags
	
	Local g:TGraphics=CreateGraphics( width,height,depth,hertz,flags )
	If Not g Return
	
	BumpGraphicsSeq
	
	SetGraphics g

	If depth
		_softSync=False
		_hardSync=(hertz&lt;&gt;0)
		MoveMouse width/2,height/2
	Else
		_hardSync=False
		_softSync=(hertz&lt;&gt;0)
	EndIf
	
	If _softSync
		_syncRate=hertz
		If _syncRate _syncPeriod=1000/_syncRate Else _syncPeriod=0
		_syncFrac=1000-_syncPeriod*_syncRate
		_syncAccum=0
		_syncTime=MilliSecs()
	EndIf

	EnablePolledInput
	
	_exGraphics=g
	
	Global _onEnd
	If Not _onEnd
		_onEnd=True
		OnEnd EndGraphics
	EndIf

	Return g
End Function

Rem
Function Graphics:TGraphics( width,height,depth=0,hertz=60,flags=0 )

	EndGraphics
	flags:|_defaultFlags

	Local g:TGraphics=CreateGraphics( width,height,depth,hertz,flags )
	If Not g Return
	
	GraphicsSeq:+1
	If Not GraphicsSeq GraphicsSeq=1
	
	SetGraphics g
	
	_softSync=True
	If Not hertz
		_softSync=False
	Else If depth
		Local e=MilliSecs()+1000,n
		While e-MilliSecs()&gt;0
			Local t=MilliSecs()
			_driver.Flip True
			t=(MilliSecs()-t)-1000/hertz
			If t&gt;=-1 And t&lt;=1
				n:+1
				If n&lt;3 Continue
				_softSync=False
				Exit
			EndIf
			n=0
		Wend
	EndIf
	_syncRate=hertz
	If _syncRate _syncPeriod=1000/_syncRate Else _syncPeriod=0
	_syncFrac=1000-_syncPeriod*_syncRate
	_syncAccum=0
	_syncTime=MilliSecs()

	_exGraphics=g

	If depth
		MoveMouse width/2,height/2
	EndIf
	
	EnablePolledInput
	
	Global _onEnd
	If Not _onEnd
		_onEnd=True
		OnEnd EndGraphics
	EndIf

	Return g

End Function
End Rem

Rem
bbdoc: End graphics
about:
#EndGraphics closes the graphics object returned by #Graphics.
End Rem
Function EndGraphics()
	If Not _exGraphics Return

	GraphicsSeq:+1
	If Not GraphicsSeq GraphicsSeq=1

	DisablePolledInput

	CloseGraphics _exGraphics
End Function
</textarea><br><br>You can see I've made the change to add the constant value. Now, if I try to compile this, however, I get the following:<br><br><b>Compile Error - Modules does not match commandline module</b> <br><br></td></tr></table><br>
<a name="1289687"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> Compiling a file from modules will not work. You won't be able to build modules until you install a C++ toolchain for BlitzMax to use. When BlitzMax is configured correctly the Build Modules menu option will be enabled. <br><br></td></tr></table><br>
<a name="1289688"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oops, I mistakenly did say that you can build them if you do not need to rebuild a .c/.cpp/.asm file - thats my bad and I apologise for the wrong information! <br><br></td></tr></table><br>
<a name="1289715"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> Recompiling BlitzMAX's primary and main libraries. Sounds so -wrong- ... It's just me. I'll stick with MaxGui for borderless. And, I found a way to get around what I needed, which was to change where coordinates 0,0 appear.<pre class=code>SetOrigin (deskrect[0]+deskrect[2])/2-mx/2,(deskrect[1]+deskrect[3])/2-my/2</pre>Where "mx" and "my" are temporary variables for determining the actual size across and down of the play area I want.<br><br>Now, I =DO= know about SetVirtualResolution, I really don't want to go that route as I'm using Truetype Fonts and they ALWAYS blur when you use SetVirtualResolution for something smaller than your normal screen.<br><br>For those of you not familiar, SetOrigin will let you choose where coordinates 0,0 appear. So if you opened a screen that was 1024x768 and said:<pre class=code>SetOrigin 32,32</pre><br><br>Then drawing from 0,0 would actually start at 32,32. Pretty useful that.<br><br>As for the misinformation regarding compilation, No problem, Dave. A lot of these things really are over my head. I'm going to get back to coding - and essentially out of everyone's hair until I run into another snag. Might be a-while.<br><br>Thanks for all the info thus far ! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
