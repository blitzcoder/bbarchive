<!DOCTYPE html><html lang="en" ><head ><title >Need help with IShellLink</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Need help with IShellLink</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Need help with IShellLink</a><br><br>
<a name="1011677"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >beanage</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey,<br><br>I want to create  a desktop shortcut. Therefore, I use bmx EXTERN to get the COM IShellLink class. I call CoCreateInstance() to retreive a new IShellLink object. Tests revealed CoInitialize() has already been executed by BlitzMax. I use SHGetSpecialFolder() to get the desktop directory. I call methods of the windows IShellLink class to set appropriate parameters. I utilize the IPersistFile Class to save the object.<br><br>Now, everything works fine, until at some point a EXCEPTION_ACCESS_VIOLATION occurs, mostly at pPersistFile.Save(). In addition, I am not sure whether i wrapped the methods correctly (do i need to pass this* to the method as well? are they correctly exposed in the extern block?)... I am also not sure whether I should use wide strings with "$w" or with ":Short Ptr" or not using wide strings at all... much room for error. [Edit: I think the key problem really is how I expose the COM methods, but I really dont know how to fix it :{ ]<br><br>I'd be glad if someone could take an analyzing look at that code:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

?Win32
Import	"-lkernel32"
Extern	"Win32"
	'Shortcut Creation
	Function SHGetSpecialFolderPathA( hwnd_:Int, path_:Byte Ptr, csidl_:Int, create_:Int )
	Function CoCreateInstance:Int( rclsid:Byte Ptr, pUnkOuter:Byte Ptr, dwClsContext:Int, riid:Byte Ptr, ppv:Byte Ptr )
	Function CoInitialize:Int( pvReserved:Byte Ptr )
	
	Type IUnknown
		Method Drop:Long( hunknown:IUnknown )"Win32"="Release@0"
	End Type
	
	Type IShellLinkW Extends IUnknown
		Method SetPath:Long( pszFile:Short Ptr ) 'Sets the path And file name of a Shell link Object.
		Method GetPath:Long( pszPath:Byte Ptr, cchMaxPath:Int, pfd:Byte Ptr, fFlags:Int ) 'Gets the path And file name of a Shell link Object.
		Method SetArguments:Long( hshlnk:IShellLinkW, pszArgs:Short Ptr ) 'Sets the command-line arguments For a Shell link Object.
		Method SetDescription:Long( hshlnk:IShellLinkW, pszDescription:Short Ptr ) 'Sets the description For a Shell link Object. The description can be any application-defined String.
		Method SetIconLocation:Long( hshlnk:IShellLinkW, pszIconPath:Short Ptr, iIcon:Int ) 'Sets the location (path And index) of the icon For a Shell link Object.
		Method SetShowCmd:Long( hshlnk:IShellLinkW, iShowCmd:Int ) 'Sets the show command For a Shell link Object. The show command sets the initial show state of the window.
		Method SetWorkingDirectory:Long( hshlnk:IShellLinkW, pszDir:Short Ptr ) 'Sets the name of the working directory For a Shell link Object.
		Method QueryInterface:Long( hshlnk:IShellLinkW, riid:Byte Ptr, ppvObject:Byte Ptr )
	End Type
	
	Type IPersistFile Extends IUnknown
		Method Save:Long( hunknown:IPersistFile, pszFileName:Short Ptr, fRemember:Int )	' Saves a copy of the Object To the specified file.
	End Type
	
End Extern
?

Const CLSCTX_INPROC_SERVER	:Long = 1
Const CSIDL_DESKTOP		:Int	= $0
Const CSIDL_STARTMENU		:Int	= $B
Const MAX_PATH_LEN		:Int = 260

Type TComID
	Field _data	:TBank = CreateBank( 16 )
	
	Function Create:TComID( _0_:Int, _1_:Short, _2_:Short, _3_:Byte[] )
		Local ret_:TcomID = New TComID
		Local mem_:Byte Ptr = MemAlloc( 8 )
		
		PokeInt ret_._data, 0, _0_
		PokeShort ret_._data, 4, _1_
		PokeShort ret_._data, 6, _2_
		PokeByte ret_._data, 8, _3_[0]
		PokeByte ret_._data, 9, _3_[1]
		PokeByte ret_._data, 10, _3_[2]
		PokeByte ret_._data, 11, _3_[3]
		PokeByte ret_._data, 12, _3_[4]
		PokeByte ret_._data, 13, _3_[5]
		PokeByte ret_._data, 14, _3_[6]
		PokeByte ret_._data, 15, _3_[7]
		
		Return ret_
	End Function
	
	Method GetPtr:Byte Ptr()
		Return BankBuf( _data )
	End Method
	
End Type

Global CLSID_ShellLink	:TComID = TComID.Create( Int $00021401, Short $0000, Short $0000, [ Byte $C0, Byte $00, Byte $00, Byte $00, Byte $00, Byte $00, Byte $00, Byte $46 ] )
Global IID_IShellLinkW	:TComID = TComID.Create( Int $000214EE, Short $0000, Short $0000, [ Byte $C0, Byte $00, Byte $00, Byte $00, Byte $00, Byte $00, Byte $00, Byte $46 ] )
Global IID_IPersistFile	:TComID = TComID.Create( Int $0000010B, Short $0000, Short $0000, [ Byte $00, Byte $00, Byte $00, Byte $00, Byte $00, Byte $00, Byte $00, Byte $46 ] )

Function _createDesktopShortcut:Int( hwnd_:Int, pszTargetfile:String, pszTargetargs:String, pszLinkfile:String, pszIconfile:String, iIconindex:Int, iShowmode:Int= False )
?Win32
'------------------------------------------------------------------------------------
	pszLinkfile = _getSpecialFolder( CSIDL_DESKTOP, hwnd_ )+"/"+pszLinkfile+".lnk"
	
	Local hRes				:Long			' Returned COM result code 
	Local pShellLink			:IShellLinkW	' IShellLink Object pointer 
	Local pPersistFile		:IPersistFile	' IPersistFile Object pointer 
	Local buf_				:Short Ptr
	
	Select CoCreateInstance(..		' create the pShellLink object
		CLSID_ShellLink.GetPtr(),..	' pre-defined CLSID of the IShellLink Object
		Null,..				' pointer To parent interface If part of aggregate 
		CLSCTX_INPROC_SERVER,..	' caller And called code are in same process 
		IID_IShellLinkW.GetPtr(),..	' pre-defined interface of the IShellLink Object 
		Varptr pShellLink )		' Returns a pointer To the IShellLink Object 
		
		Case $80040154
			Print "REGDB_E_CLASSNOTREG"
		
		Case $80040110
			Print "CLASS_E_NOAGGREGATION"
		
		Case $80004002
			Print "E_NOINTERFACE"
		
		Case $80004003
			Print "E_POINTER"
		
	End Select
	
	DebugStop
	
	buf_ = pszTargetfile.ToWString()
	hRes = pShellLink.SetPath( buf_ )
	MemFree buf_
	
	Local sbuf_	:TBank = CreateBank( 256 )
	hRes = pShellLink.GetPath( BankBuf( sbuf_ ), 256, Null, True )
	Local path_	:String = String.FromWString( Short Ptr BankBuf( sbuf_ ) )
	
	buf_ = pszTargetargs.ToWString()
	hRes = pShellLink.SetArguments( pShellLink, buf_ )
	MemFree buf_
	
	hRes = pShellLink.SetShowCmd( pShellLink, iShowmode )
	
	buf_ = eng_root.ToWString()
	hRes = pShellLink.SetWorkingDirectory( pShellLink, buf_ )
	MemFree buf_

'		buf_ = pszIconfile.ToWString()
'		hRes = pShellLink.SetIconLocation( pShellLink, Replace( , "/", "\" ).ToCString(), iIconindex )
'		memFree buf_
	
	hRes = pShellLink.QueryInterface( pShellLink, IID_IPersistFile.GetPtr(), Varptr pPersistFile ) 'returns a pointer To the IPersistFile Object 
	
	buf_ = pszLinkfile.ToWString()
	hRes = pPersistFile.Save( pPersistFile, buf_, True )
	MemFree buf_
	
	hRes = pPersistFile.Drop( pPersistFile )
	hRes = pShellLink.Drop( pShellLink )
	
	Return hRes
	
End Function
	
Function _getSpecialFolder:String( csidl_:Int, hwnd_:Int )

	Local path_	:Byte Ptr	= MemAlloc( MAX_PATH_LEN+ 2048 )
	
	SHGetSpecialFolderPathA hwnd_, path_, csidl_, True
	Return Replace( String.FromCString( path_ ), "\", "/" )
?
'------------------------------------------------------------------------------------
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1011690"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> No need to declare <b>IUnknown</b>, its already in <b>PUB.Win32</b> together with some other COM related stuff.<br><br>All methods return <b>Int</b> not <b>Long</b>.<br>Never pass or declare <b>Self</b>.<br><b>All</b> methods must be declared <b>in order</b>, you cant pick and choose which ones you want.<br><br>And for Strings its usually ok to use <b>$w</b> for in-args, and <b>Short Ptr</b> for in/out-args.<br><br>Take a look at <b>PUB.Win32\com.bmx</b> and <b>PUB.DirectX\*</b> for examples.<br><br>I tried to get your example to work though..<br>But it complains about missing CoCreateInstance even though i know its there :( weird.<br><br>edit: hmm.. seems PUB.Win32 doesnt import Ole32 ??? it worked after i imported it myself. (but your example still crashes) <br><br></td></tr></table><br>
<a name="1011692"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> COM stuff is fun in BlitzMax :-p <br><br></td></tr></table><br>
<a name="1011701"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> COM stuff is fun in BlitzMax :-p <br></div>Hehe, so true :P<br><br>You had the wrong IIDs for both IShellLinkW and IPersistFile so no wonder i was getting errors ;)<br><br>Heres a modified (and working) version of your example.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Import PUB.Win32

Import "-lole32"

Extern	"Win32"
	'Shortcut Creation
	Function SHGetSpecialFolderPathA( hwnd_:Int, path_:Byte Ptr, csidl_:Int, create_:Int )
	Function CoCreateInstance:Int( rclsid:Byte Ptr, pUnkOuter:Byte Ptr, dwClsContext:Int, riid:Byte Ptr,ppv:IUnknown Var)
	Function CoInitialize:Int( pvReserved:Byte Ptr )
	Function CLSIDFromString:Int( s$w, clsid:Byte Ptr)
		
	Type IShellLinkW Extends IUnknown
		Method GetPath:Int( pszPath:Short Ptr, cchMaxPath:Int, pfd:Byte Ptr, fFlags:Int)
		Method GetIDList:Int( idlist:Byte Ptr)
		Method SetIDList:Int( idlist:Byte Ptr)
		Method GetDescription:Int( dst:Short Ptr, sz:Int)
		Method SetDescription:Int( pszDescription$w)
		Method GetWorkingDirectory:Int( dst:Short Ptr, sz:Int)
		Method SetWorkingDirectory:Int( pszDir$w)
		Method GetArguments:Int( dst:Short Ptr, sz:Int)
		Method SetArguments:Int( pszArgs$w)
		Method GetHotkey:Int( key:Short Var)
		Method SetHotkey:Int( key:Short)
		Method GetShowCmd:Int( cmd:Int Var)
		Method SetShowCmd:Int( iShowCmd:Int)
		Method GetIconLocation:Int( pszIconPath:Short Ptr, sz:Int, icon:Int Var)
		Method SetIconLocation:Int( pszIconPath$w, iIcon:Int)
		Method SetRelativePath:Int( s$w, dunno:Int)
		Method Resolve:Int( hwnd:Int, dunno:Int)
		Method SetPath:Int( pszFile$w)
	EndType

	Type IPersistFile Extends IUnknown
		Method GetClassID:Int( clsid:Byte Ptr)
		Method IsDirty:Int()
		Method Load:Int( str$w, dunno:Int) ' LPCOLESTR
		Method Save:Int( str$w, dunno:Int) ' LPCOLESTR
		Method SaveCompleted:Int( str$w) ' LPCOLESTR
		Method GetCurFile:Int( dst:Short Ptr Var) ' LPCOLESTR*
	End Type
	
End Extern
?

Const CSIDL_DESKTOP		:Int	= $0
Const CSIDL_STARTMENU		:Int	= $B
Const MAX_PATH_LEN		:Int = 260

Const ShellLink_UUID:String = "{00021401-0000-0000-C000-000000000046}"
Const IShellLinkW_UUID:String = "{000214F9-0000-0000-C000-000000000046}"
Const IPersistFile_UUID:String = "{0000010B-0000-0000-C000-000000000046}"

Global CLSID_ShellLink:GUID = New GUID;  CLSIDFromString( ShellLink_UUID, CLSID_ShellLink)
Global IID_IShellLinkW:GUID = New GUID;  IIDFromString( IShellLinkW_UUID, IID_IShellLinkW)
Global IID_IPersistFile:GUID = New GUID;  IIDFromString( IPersistFile_UUID, IID_IPersistFile)

CoInitialize(Null)


Function _createDesktopShortcut:Int( hwnd_:Int, pszTargetfile:String, pszTargetargs:String, pszLinkfile:String, pszIconfile:String, iIconindex:Int, iShowmode:Int= False )
	pszLinkfile = _getSpecialFolder( CSIDL_DESKTOP, hwnd_ )+"/"+pszLinkfile+".lnk"
	
	Local pShellLink			:IShellLinkW	' IShellLink Object pointer 
	Local pPersistFile		:IPersistFile	' IPersistFile Object pointer 
	
	CoCreateInstance( CLSID_ShellLink, Null, CLSCTX_INPROC_SERVER, IID_IShellLinkW, pShellLink)	
	Assert pShellLink
	
	pShellLink.SetPath( pszTargetfile)
	pShellLink.SetArguments( pszTargetargs)
	pShellLink.SetShowCmd( iShowmode)
	pShellLink.SetIconLocation( pszIconfile, iIconindex)
	
	pShellLink.QueryInterface( IID_IPersistFile, Varptr pPersistFile ) 'returns a pointer To the IPersistFile Object 
	Assert pPersistFile
	
	pPersistFile.Save( pszLinkfile, True )

	' not sure if release has to be manually called
	pPersistFile.Release_()
	pShellLink.Release_()
	
	Return True
End Function
	
Function _getSpecialFolder:String( csidl_:Int, hwnd_:Int )

	Local path_	:Byte Ptr	= MemAlloc( MAX_PATH_LEN+ 2048 )
	
	SHGetSpecialFolderPathA hwnd_, path_, csidl_, True
	Return Replace( String.FromCString( path_ ), "\", "/" )
End Function


_createDesktopShortcut:Int( 0, "c:\", "", "Root Drive", "", 0)
</textarea><br><br>edit: Oh. and btw, if you ever need to figure out the layout of an interface and you have MinGW. check out <b>shlobj.h</b> and <b>objidl.h</b>. <br><br></td></tr></table><br>
<a name="1011705"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >beanage</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> @grable: <b>IT WORKS!!!!! IT WORKS!!! I.T. D.O.E.S. W.O.R.K.!!!!! :}}}</b><br><br>I can barely express how you helped me there buddy, but this is awesome :D And I learned something. Thanks for pointing me to Pub.Win32. Thanks for digging into my buggy code, thanks for pointing me to all the incorrectness i did importing the methods. Thanks for finding the errors in my IIDs - and after all; NOBODY WILL EVER HAVE THIS PROBLEM AGAIN! Another precious code snippet recorded in these wonderful forums, thanks to you!<br><br>[Hehe, and after all I'm proud, that at least a few lines of mine reappeared in your fix :P]<br><br>@Brucey: yea its always a bit of evil wakeup for me as a blitzer, when I step out of my candy MaxIDE paradise back into the big, dark and cold C reality of those windows APIs :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
