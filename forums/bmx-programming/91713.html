<!DOCTYPE html><html lang="en" ><head ><title >2d rectangle collision response</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >2d rectangle collision response</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >2d rectangle collision response</a><br><br>
<a name="1043802"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm working on a collision system, which tries to avoid that two rectangles overlap.<br>Both rectangles can be rotated, and they are not square, although I'm thinking of using formats in the order of 32: 32x32, 64x32, 64x64 etc.<br><br>The method I'm using now is: if a rectangle moves, the movement is done one pixel at a time in a loop. Before each step, I look ahead if an overlap would occur. If so, the loop is aborted.<br><br>Is there a faster way of doing that? Is there a method where the endpoint of the route could be calculated on beforehand?<br><br>Here is a quick example I knocked up to demonstrate this:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Graphics 800, 600, 0, 60, 2

'DIM for storing polys (2 cubes)
Global a_x#[4]
Global a_y#[4]
Global b_x#[4]
Global b_y#[4]

'generate cube 2 (static cube)
For Local i% = 0 To 3
	b_x#[i] = Cos(i * 90 - 45) * 50 + 400
	b_y#[i] = Sin(i * 90 - 45) * 50 + 300
Next

'used to translate (move) cube 1
Local a_mx# = 100
Local a_my# = 100

'used to rotate cube 1
Local oldang# = -1
Local ang# = 0

Repeat

	'arrows = turn cube
	If KeyDown(37) ang = ang - 1
	If KeyDown(39) ang = ang + 1
	'recreate cube poly after rotation
	If ang &lt;&gt; oldang Then
		'regenerate dynamic cube
		For Local i% = 0 To 3
		
			a_x#[i] = Cos(i * 90 + ang) * 50
			a_y#[i] = Sin(i * 90 + ang) * 50
			
		Next
		oldang = ang
	End If


	'click = mouse cube
	If MouseHit(1) Then
	
		'find delta x,y for movement
		Local dx# = MouseX() - a_mx
		Local dy# = MouseY() - a_my
		
		'determine biggest distance (horz or vert)
		Local dist# = aMax(dx, dy)
		Local sg% = Sgn(dist)
		
		'calculate delta pro step
		dx = dx / dist
		dy = dy / dist
		
		'take 1 step at the time
		For Local i% = 0 To Abs(dist)
		
			'use delta
			Local newx# = a_mx + dx*sg
			Local newy# = a_my + dy*sg
			
			'if new position of cube 1 overlaps cube 2, exit this loop
			If overlap(newx, newy) Then Exit
			
			'else move cube to new position and proceed to next step
			a_mx = newx
			a_my = newy
			
		Next		
		
	End If

	'draw interface			
	Cls
	
	'draw suggested path
	DrawLine a_mx, a_my, MouseX(), MouseY()

	'draw cubes	
	For Local i% = 0 To 3
	
		Local j% = (i + 1) Mod 4
	
		DrawLine a_x[i] + a_mx, a_y[i] + a_my, a_x[j] + a_mx, a_y[j] + a_my
		DrawLine b_x[i], b_y[i], b_x[j], b_y[j]
		
	Next

	'show text	
	DrawText "Use arrows to turn cube", 0, 0
	DrawText "Use mouse button to move cube", 0, 20
		
	Flip
	
Until KeyHit(27)

End


'---------------------------------------------------------------------------------------------
'									RayIntersect
'---------------------------------------------------------------------------------------------
'Returns true if ray (cx,cy)-(dx,dy) intersects poly (b_x[], b_y[]) - modified code from Indiepath
Function RayIntersect%(cx#,cy#,dx#,dy#)

	Local j% = 3
	For Local i% = 0 To 3
	
		Local Ax# = b_x[j]
		Local Bx# = b_x[i]
		Local Ay# = b_y[j]
		Local By# = b_y[i]
						
		Local u_b#  = (dy - cy) * (bx - ax) - (dx - cx) * (by - ay)

   				If ( u_b &lt;&gt; 0 ) 
			
					Local ua_t# = (dx - cx) * (ay - cy) - (dy - cy) * (ax - cx)
   					Local ub_t# = (bx - ax) * (ay - cy) - (by - ay) * (ax - cx)
					Local ua# = ua_t / u_b
       				Local ub# = ub_t / u_b

      				If ( 0 &lt;= ua And ua &lt;= 1 And 0 &lt;= ub And ub &lt;= 1 ) 
						Return True		            
       				EndIf 
       			EndIf
		
		j = i
	Next
	Return False
	
End Function

'---------------------------------------------------------------------------------------------
'									Overlap
'---------------------------------------------------------------------------------------------
'function checks if the two polys (a_x[], a_y[]) and (b_x[], b_y[]) overlap
Function Overlap%(posx#,posy#)

	Local tx1#, ty1#, tx2#, ty2#, tx3#, ty3#, tx4#, ty4#
	Local dSqr#, d1#, d2#, dx#, dy#
	Local lx1#, ly1#, lx2#, ly2#
	Local i%,j%
				
	j% = 3
	For Local i% = 0 To 3
		Local Ax# = a_x[j] + posx
		Local Bx# = a_x[i] + posx
		Local Ay# = a_y[j] + posy
		Local By# = a_y[i] + posy
		If RayIntersect(ax,ay,bx,by) Return True
		j = i
	Next
	Return False
			
End Function

'---------------------------------------------------------------------------------------------
'									Abs Max
'---------------------------------------------------------------------------------------------
Function aMax#(a#, b#)

	If Abs(a) &gt; Abs(b) Return a Else Return b

End Function
</textarea><br>Click with the mouse to move the square. It should not be able to go through the other square. <br><br></td></tr></table><br>
<a name="1043803"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> the collision part is as effective as it is going to get. although I would not check for pixel collision every pixel movement. the best method is to use bounding box collision first sense that would be magnitude faster and only do single pixel movement collision if the two shapes collided in the bounding box test. <br>side note:<br>have you tried to run your code in debug mode? it doesn't run. A BlitzMax array of three elements would be n[3] and is base 0. That is, it would be indexed between 0 and 2, not 0 and 3. You just need to change your array dimensions to n[4] for four elements. <br><br></td></tr></table><br>
<a name="1043805"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for pointing that out. I didn't run it in debug mode. I corrected the posted code.<br>I'm thinking of using a boolean search instead of a linear search. However that wouldn't prevent shape 1 from jumping over shape 2.<br>Also, I was thinking about how each side of shape 1 is translated. Those sides and their translated counterparts form a polygon that could be checked for overlapping shape 2. I believe that would be called 'sweeping'?<br>It would be nice if I could somehow trace back how far shape 1 can move based on the collision points it finds in such a swept collision.<br>For now I'm planning on combining both the boolean search and the sweeping method, and a bounding box check as you suggested. <br><br></td></tr></table><br>
<a name="1043811"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I believe warpy posted a line intersection formula in the code archives which you might find useful for determining exactly where the collision happened or if it happened. <br><br></td></tr></table><br>
<a name="1043825"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TaskMaster</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> For the first check to see if it should be more detailed you could also just take half of a diagonal through the rectangle and use it in a distance check.<br><br>In other words:<br><br>Half of the length from the top left to the bottom right is basically the radius of a circle that the rectangle will never leave, no matter how it is rotated. <br><br></td></tr></table><br>
<a name="1043828"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I don't think that the radius check should be the first check because depending on the speed it has the possibility to fail. there is the possibility to fail even with the bounding box check if the speed is   bigger than the size of the object but the radius check will fail for objects that are moving at smaller speed then the size of the object. <br><br></td></tr></table><br>
<a name="1043844"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I tried combining the boolean search with the sweeping technique.<br>However, when I run some tests with it, it seems to be quite a bit slower than my first attempt :(<br>Well, anyway, here is the (slower) code:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Graphics 800, 600, 0, 60, 2

'DIM for storing polys (2 cubes)
Local a_x#[4]
Local a_y#[4]
Local b_x#[6]
Local b_y#[6]
Local a_mx#
Local a_my#
Local b_mx#
Local b_my#

'generate cube 2 (static cube)
For Local i% = 0 To 5
	b_x#[i] = Cos(i * 60 - 45) * 50
	b_y#[i] = Sin(i * 60 - 45) * 50
Next

'used to translate (move) cube 1
a_mx# = 100
a_my# = 100

b_mx# = 400
b_my# = 300

'used to rotate cube 1
Local oldang# = -1
Local ang# = 0

Repeat

	'arrows = turn cube
	If KeyDown(37) ang = ang - 1
	If KeyDown(39) ang = ang + 1
	'recreate cube poly after rotation
	If ang &lt;&gt; oldang Then
		'regenerate dynamic cube
		For Local i% = 0 To 3
		
			a_x#[i] = Cos(i * 90 + ang) * 50
			a_y#[i] = Sin(i * 90 + ang) * 50
			
		Next
		oldang = ang
	End If


	'click = mouse cube
	If MouseHit(1) Then
	
		'find delta x,y for movement
		Local dx# = MouseX() - a_mx
		Local dy# = MouseY() - a_my
		
		If (dx &lt;&gt; 0) Or (dy &lt;&gt; 0) Then
		
			'determine biggest distance (horz or vert)
			Local dist# = aMax(dx, dy)
			Local sg% = Sgn(dist)
			
			'calculate delta pro step
			dx = dx / dist
			dy = dy / dist
			
			'set min/max boundries for translation distance
			Local mind# = 0
			Local maxd# = dist#

			'this will become the new location of the poly			
			Local newx# = a_mx
			Local newy# = a_my
									
			Repeat

				'test if the poly can move as far as possible																		
				If Not(WillCollide(a_mx, a_my, a_x, a_y, maxd * dx, maxd * dy, b_mx, b_my, b_x, b_y)) Then
					newx = a_mx + maxd * dx
					newy = a_my + maxd * dy
					Exit
				End If

				'if the boundries are at the same point, then exit
				If Abs(mind - maxd) &lt; 1 Then
					newx = a_mx + mind * dx
					newy = a_my + mind * dy				
					Exit
				End If

				'find a point in between the two boundries												
				Local av# = (mind + maxd) / 2.0
				
				'if this point can be reached
				If Not(WillCollide(a_mx, a_my, a_x, a_y, av * dx, av * dy, b_mx, b_my, b_x, b_y)) Then
					'set min boundry - the poly can at least travel to here
					mind = av
				Else
					'set max boundry - the poly can not travel past here
					maxd = av
				End If

				'in case it hangs - esc = stop				
				If KeyHit(27) Then End
				
			Forever
	
			a_mx = newx
			a_my = newy
			
		End If
					
	End If

	'draw interface			
	Cls
	
	'draw suggested path
	DrawLine a_mx, a_my, MouseX(), MouseY()

	'draw cubes	
	Local j% = a_x.length - 1
	For Local i% = 0 To a_x.length - 1
		DrawLine a_x[i] + a_mx, a_y[i] + a_my, a_x[j] + a_mx, a_y[j] + a_my		
		j = i
	Next
	
	j% = b_x.length - 1
	For Local i% = 0 To b_x.length - 1				
		DrawLine b_x[i] + b_mx, b_y[i] + b_my, b_x[j] + b_mx, b_y[j] + b_my
		j = i
	Next	

	'show text	
	DrawText "Use arrows to turn cube", 0, 0
	DrawText "Use mouse button to move cube", 0, 20
		
	Flip
	
Until KeyHit(27)

End


'---------------------------------------------------------------------------------------------
'									RayIntersect
'---------------------------------------------------------------------------------------------
'Returns true if ray (cx,cy)-(dx,dy) intersects poly (b_x[], b_y[]) - modified code from Indiepath
Function RayIntersect%(cx#,cy#,dx#,dy#, b_mx#, b_my#, b_x#[], b_y#[])

	Local j% = b_x.length - 1
	For Local i% = 0 To b_x.length - 1
	
		Local Ax# = b_x[j] + b_mx
		Local Bx# = b_x[i] + b_mx
		Local Ay# = b_y[j] + b_my
		Local By# = b_y[i] + b_my
						
		Local u_b#  = (dy - cy) * (bx - ax) - (dx - cx) * (by - ay)

   				If ( u_b &lt;&gt; 0 ) 
			
					Local ua_t# = (dx - cx) * (ay - cy) - (dy - cy) * (ax - cx)
   					Local ub_t# = (bx - ax) * (ay - cy) - (by - ay) * (ax - cx)
					Local ua# = ua_t / u_b
       				Local ub# = ub_t / u_b

      				If ( 0 &lt;= ua And ua &lt;= 1 And 0 &lt;= ub And ub &lt;= 1 ) 
						Return True		            
       				EndIf 
       			EndIf
		
		j = i
	Next
	Return False
	
End Function

'---------------------------------------------------------------------------------------------
'									Overlap
'---------------------------------------------------------------------------------------------
'function checks if the two polys (a_x[], a_y[]) and (b_x[], b_y[]) overlap
Function Overlap%(a_mx#, a_my#, a_x#[], a_y#[], b_mx#, b_my#, b_x#[], b_y#[])

	Local tx1#, ty1#, tx2#, ty2#, tx3#, ty3#, tx4#, ty4#
	Local dSqr#, d1#, d2#, dx#, dy#
	Local lx1#, ly1#, lx2#, ly2#
	Local i%,j%
				
	j% = a_x.length - 1
	For Local i% = 0 To a_x.length - 1
		Local Ax# = a_x[j] + a_mx
		Local Bx# = a_x[i] + a_my
		Local Ay# = a_y[j] + a_mx
		Local By# = a_y[i] + a_my
		If RayIntersect(ax, ay, bx, by, b_mx, b_my, b_x, b_y) Return True
		j = i
	Next
	Return False
			
End Function

'---------------------------------------------------------------------------------------------
'									Abs Max
'---------------------------------------------------------------------------------------------
Function aMax#(a#, b#)

	If Abs(a) &gt; Abs(b) Return a Else Return b

End Function


'---------------------------------------------------------------------------------------------
'									WillCollide
'---------------------------------------------------------------------------------------------
'tests possible movement of polygon a_x[], a_y[] from a_mx, a_my with delta dx, dy
'returns true if this polygon would intersect polygon b_x, b_y at b_mx, b_my during it's transpose
Function WillCollide%(a_mx#, a_my#, a_x#[], a_y#[], dx#, dy#, b_mx#, b_my#, b_x#[], b_y#[])

	Local test% = False
	
	'draw cubes	
	For Local i% = 0 To 3
	
		Local j% = (i + 1) Mod 4
		
		Local x#[4]
		Local y#[4]

		x[0] = a_x[i] + a_mx + dx	
		y[0] = a_y[i] + a_my + dy
		x[1] = a_x[j] + a_mx + dx
		y[1] = a_y[j] + a_my + dy
		x[2] = a_x[j] + a_mx
		y[2] = a_y[j] + a_my
		x[3] = a_x[i] + a_mx
		y[3] = a_y[i] + a_my
		
		DrawLine x[0],y[0],x[1],y[1]
		DrawLine x[1],y[1],x[2],y[2]
		DrawLine x[2],y[2],x[3],y[3]
		DrawLine x[3],y[3],x[0],y[0]
		
		test = Overlap%(0, 0, x, y, b_mx, b_my, b_x, b_y)
		If test Then Exit
		
	Next
		
	Return test
	
End Function
</textarea><br>Thank you for your advice, I will look into it further. <br><br></td></tr></table><br>
<a name="1043887"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> a while a go I converted some code to BlitzMax from Blitzbasic that I have found some good use for. It is a polygon vector code. it might help you solve some problems you are having:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' nPoly - Polygonal Collision Library
' By Jocelyn 'GoSsE' Perreault, &lt;a href="http://www.nuloen.com/" target="_blank"&gt;http://www.nuloen.com/&lt;/a&gt;
' Last Updated: July 8 2004
' Version: 1.0.0

' This source code is copyrighted (c) To Jocelyn Perreault.
' It is FreeWare. You have no limitiations in regard To 
' applications/games that you Release/create with it, either
' they be freeware Or commercial. However, you cannot Release
' this source code under another name Or Release a similar
' library with it. It cannot be redistributed. The code may be 
' modified To be tweaked but you cannot distribute it. If you
' made a modification that you think the community would
' benifit, please let me know.
' If you make tons of money with a commercial project,
' a free copy wouldn't hurt ')
' I would love To have an e-mail And a place in the credits
' of your projects, but it is up To you.
' Thanks,
'                 Jocelyn "GoSsE Korupted" Perreault
'                  &lt;a href=\"mailto:gosse@nuloen.com\"&gt;gosse@...;
'                  Nuclear Loaded Entertainment
'                  &lt;a href="http://www.nuloen.com/" target="_blank"&gt;http://www.nuloen.com/&lt;/a&gt;

' ======================================================================
' Globals And Constants
' ======================================================================
Const nP_MAX = 32			' Maximum number of vertices For a poly

' ======================================================================
' Different objects
' ======================================================================

' Polygon
Type nP_Poly
	Field iX[nP_MAX]		' Vertices X position
	Field iY[nP_MAX]		' Vertices Y position
	Field fX#[nP_MAX]		' Scaled And rotated X position
	Field fY#[nP_MAX]		' Scaled And rotated Y position
	Field iVertexCount		' Number of vertices
	Field fScaleX#			' X-Scale
	Field fScaleY#			' Y-Scale
	Field fAngle#			' Angle rotation
	Field iMinX				' Bounding box
	Field iMinY				' Bounding box
	Field iMaxX				' Bounding box
	Field iMaxY				' Bounding box
End Type

' ======================================================================
' Functions
' ======================================================================

' Creates a New empty polygon
Function nP_CreatePoly:nP_Poly()
	Local nPoly:nP_Poly = New nP_Poly
	nPoly.fScaleX = 1
	nPoly.fScaleY = 1
	Return nPoly
End Function

' ----------------------------------------------------------------------

' Adds a vertex To a polygon
Function nP_AddVertex(poly:nP_Poly, x, y, bUpdate = True)
	Local nPoly:nP_Poly = poly
	If nPoly.iVertexCount &gt; nP_MAX Then
		RuntimeError "Cannot add one more vertex to polygon!"
	Else
		nPoly.iX[nPoly.iVertexCount] = x
		nPoly.iY[nPoly.iVertexCount] = y
		nPoly.iVertexCount = nPoly.iVertexCount + 1
		If bUpdate Then
			nP_UpdatePoly(poly)
		End If
	End If
End Function

' ----------------------------------------------------------------------

' Checks If 2 polygons overlap each other
Function nP_PolyOverlap(poly1:nP_Poly, x1, y1, poly2:nP_Poly, x2, y2, bCheckBounding = True)
	' Polygons
	Local nPoly1:nP_Poly = poly1
	Local nPoly2:nP_Poly = poly2
	' Temp positions
	Local tx1#, ty1#, tx2#, ty2#, tx3#, ty3#, tx4#, ty4#
	' Delta calculations
	Local dSqr#, d1#, d2#, dx#, dy#
	' Last vertex
	Local lx1#, ly1#, lx2#, ly2#
	' Counters
	Local i, j
	' Checks bounding box first (If specified)
	If bCheckBounding Then
		tx1 = X1 + nPoly1.iMinX
		ty1 = Y1 + nPoly1.iMinY
		tx2 = X1 + nPoly1.iMaxX
		ty2 = Y1 + nPoly1.iMaxY
		tx3 = X2 + nPoly2.iMinX
		ty3 = Y2 + nPoly2.iMinY
		tx4 = X2 + nPoly2.iMaxX
		ty4 = Y2 + nPoly2.iMaxX
		If Not (tx3 &gt; tx1 And ty3 &gt; ty1 And tx3 &lt; tx2 And ty3 &lt; ty2) Then
			If Not (tx4 &gt; tx1 And ty4 &gt; ty1 And tx4 &lt; tx2 And ty4 &lt; ty2) Then
				If Not (tx3 &gt; tx1 And ty4 &gt; ty1 And tx3 &lt; tx2 And ty4 &lt; ty2) Then
					If Not (tx4 &gt; tx1 And ty3 &gt; ty1 And tx4 &lt; tx2 And ty3 &lt; ty2) Then
						Return False
					End If
				End If
			End If
		End If
	End If
	' Store the last vertex
	lx1 = nPoly1.fX#[0] + X1
	ly1 = nPoly1.fY#[0] + Y1
	lx2 = nPoly2.fX#[0] + X2
	ly2 = nPoly2.fY#[0] + Y2
	' Cycle through all other vertices
	For i = 1 To nPoly1.iVertexCount - 1
		For j = 1 To nPoly2.iVertexCount - 1
			' Store vertices
			tx1 = nPoly1.fX#[i] + X1
			ty1 = nPoly1.fY#[i] + Y1
			tx3 = nPoly2.fX#[j] + X2
			ty3 = nPoly2.fY#[j] + Y2
			tx2 = lX1
			ty2 = lY1
			tx4 = lX2
			ty4 = lY2
			' Calculate deltas
			dSqr = (ty4-ty3)*(tx2-tx1)-(tx4-tx3)*(ty2-ty1)
			d1 = (ty1-ty3)
			d2 = (tx1-tx3)
			dX = ((tx2-tx1)*d1-(ty2-ty1)*d2)
			dY = ((tx4-tx3)*d1-(ty4-ty3)*d2)
			' Parallel
			If dSqr = 0 Then
				' Coincident
				If dX = 0 And dY = 0 Then
					Return True
				End If
			Else
				' Otherwise
				d1 = dX / dSqr
				d2 = dY / dSqr
				If (d1 &gt;= 0 And d1 &lt;= 1) And (d2 &gt;= 0 And d2 &lt;= 1) Then
					Return True
				End If
			End If
			' Store last vertex
			lX2 = tx3
			lY2 = ty3
		Next
		' Store last vertex
		lX1 = tx1
		lY1 = ty1
	Next
	Return False
End Function

' ----------------------------------------------------------------------

Function nP_PolyOverlapsCircle(poly:nP_Poly, X, Y, iCircleX, iCircleY, iCircleRadius, bCheckBounding = True)
	Local nPoly:nP_Poly = poly
	' Temp positions
	Local tx1#, ty1#, tx2#, ty2#, tx3#, ty3#, tx4#, ty4#
	Local CX1#, CY1#, CX2#, CY2#, pX1#, pY1#, pX2#, pY2#, pXT#, pYT#
	' Delta calculations
	Local d1#, d2#, dX#, dY#, dSqr#
	' Checks bounding box first (If specified)
	If bCheckBounding Then
		tx1 = X + nPoly.iMinX
		ty1 = Y + nPoly.iMinY
		tx2 = X + nPoly.iMaxX
		ty2 = Y + nPoly.iMaxY
		tx3 = iCircleX - iCircleRadius
		ty3 = iCircleY - iCircleRadius
		tx4 = iCircleX + iCircleRadius
		ty4 = iCircleY + iCircleRadius
		If Not (tx3 &gt; tx1 And ty3 &gt; ty1 And tx3 &lt; tx2 And ty3 &lt; ty2) Then
			If Not (tx4 &gt; tx1 And ty4 &gt; ty1 And tx4 &lt; tx2 And ty4 &lt; ty2) Then
				If Not (tx3 &gt; tx1 And ty4 &gt; ty1 And tx3 &lt; tx2 And ty4 &lt; ty2) Then
					If Not (tx4 &gt; tx1 And ty3 &gt; ty1 And tx4 &lt; tx2 And ty3 &lt; ty2) Then
						Return False
					End If
				End If
			End If
		End If
	End If
	' Store the last vertex &amp; circle
	lx = nPoly.fX#[0] + X
	ly = nPoly.fY#[0] + Y
	CX2# = iCircleX
	CY2# = iCircleY
	' Cycle through all other vertices
	For i = 1 To nPoly.iVertexCount - 1	
		CX1# = nPoly.fX#[i] + X
		CY1# = nPoly.fY#[i] + Y
		pX1 = lX - CX1
		pY1 = lY - CY1
		pX2 = CX2 - CX1
		pY2 = CY2 - CY1
		d1 = pX1 * pX2 + pY1 * pY2
		If d1 &lt;= 0 Then
			dX# = CX2 - CX1
			dY# = CY2 - CY1
			dSqr# = dX * dX + dY * dY
		Else
			d2 = pX1 * pX1 + pY1 * pY1
			If (d2 &lt;= d1) Then
				dX# = CX2 - lX
				dY# = CY2 - lY
				dSqr# = dX * dX + dY * dY
			Else
				d1 = d1 / d2
				pXT# = CX1 + d1 * pX1
				pYT# = CY1 + d1 * pY1
				dX# = CX2 - pXT
				dY# = CY2 - PYT
				dSqr# = dX * dX + dY * dY
			End If
		End If
		If dSqr# &lt; iCircleRadius * iCircleRadius Then
			Return True
		End If
		lx = CX1
		ly = CY1
	Next
	Return False	
End Function

' ----------------------------------------------------------------------

' Returns True If both circles overlap
Function nP_CirclesOverlap(x1, y1, rad1, x2, y2, rad2)
	Local dx# = x2 - x1
	Local dy# = y2 - y1
	Local rsqr = rad1 + rad2
	rsqr = rsqr*rsqr
	Return (dx*dx+dy*dy &lt; rsqr)
End Function

' ----------------------------------------------------------------------

' Scales the polygon by the specified scale
Function nP_ScalePoly(poly:nP_Poly, sx#, sy#, bUpdate = True)
	Local nPoly:nP_Poly = poly
	nPoly.fScaleX# = nPoly.fScaleX# * sx#
	nPoly.fScaleY# = nPoly.fScaleY# * sy#
	If bUpdate Then
		nP_UpdatePoly(poly)
	End If
End Function

' ----------------------------------------------------------------------

' Resizes the polygon To the specieid scale
Function nP_ResizePoly(poly:nP_Poly, sx#, sy#, bUpdate = True)
	Local nPoly:nP_Poly = poly
	nPoly.fScaleX# = sx#
	nPoly.fScaleY# = sy#
	If bUpdate Then
		nP_UpdatePoly(poly)
	End If
End Function

' ----------------------------------------------------------------------

' Rotates the polygon by the specified rotation angle
Function nP_RotatePoly(poly:nP_Poly, fRot#, bUpdate = True)
	Local nPoly:nP_Poly = poly
	nPoly.fAngle# = nPoly.fAngle# + fRot#
	If bUpdate Then
		nP_UpdatePoly(poly)
	End If
End Function

' ----------------------------------------------------------------------

' Turns the polygon To the specified angle
Function nP_FreeRotatePoly(poly:nP_Poly, fAngle#, bUpdate = True)
	Local nPoly:nP_Poly = poly
	nPoly.fAngle# = fAngle#
	If bUpdate Then
		nP_UpdatePoly(poly)
	End If
End Function

' ----------------------------------------------------------------------

' Updates the polygon according To scale And rotation
Function nP_UpdatePoly(poly:nP_poly)
	Local nPoly:nP_Poly = poly
	Local fX#, fY#, f1#, f2#
	Local i
	' Set up big extremums
	nPoly.iMinX = 9999999
	nPoly.iMinY = 9999999
	nPoly.iMaxX = -9999999
	nPoly.iMaxY = -9999999
	' Find a valid collision setting
	For i = 0 To nPoly.iVertexCount - 1
		' Standard data
		fX = nPoly.iX[i]
		fY = nPoly.iY[i]
		' Scale
		fX = fX * nPoly.fScaleX
		fY = fY * nPoly.fScaleY
		' Rotation
		If nPoly.fAngle# &lt;&gt; 0 Then
			f1 = Cos(nPoly.fAngle#) * fX - Sin(nPoly.fAngle#) * fY
			f2 = Sin(nPoly.fAngle#) * fX + Cos(nPoly.fAngle#) * fY
			fX = f1
			fY = f2
		End If
		' Final Values
		nPoly.fX#[i] = fX
		nPoly.fY#[i] = fY
		' Check For bounding box
		If fX &lt; nPoly.iMinX Then
			nPoly.iMinX = fX
		End If
		If fY &lt; nPoly.iMinY Then
			nPoly.iMinY = fY
		End If
		If fX &gt; nPoly.iMaxX Then
			nPoly.iMaxX = fX
		End If
		If fY &gt; nPoly.iMaxY Then
			nPoly.iMaxY = fY
		End If
	Next
End Function

' ----------------------------------------------------------------------

' Draws the polygon on screen at the specified location
Function nP_DrawPoly(poly:nP_Poly, x, y, r=255, g=255, b=255, bDrawBoundingBox=True, br=255, bg=255, bb=255)
	Local nPoly:nP_Poly = poly
	Local i, lx, ly
	' Draw bounding box (If specified)
	If bDrawBoundingBox Then
		SetColor br, bg, bb
		DrawRect nPoly.iMinX + x, nPoly.iMinY + y, nPoly.iMaxX - nPoly.iMinX + 1, nPoly.iMaxY - nPoly.iMinY + 1', False
	End If
	' Draw all the lines
	SetColor r, g, b
	lx = nPoly.fX[0] + x
	ly = nPoly.fY[0] + y
	For i = 1 To nPoly.iVertexCount - 1
		DrawLine lx, ly, nPoly.fX[i] + x, nPoly.fY[i] + y
		lx = nPoly.fX[i] + x
		ly = nPoly.fY[i] + y
	Next
End Function

' ----------------------------------------------------------------------

' Copies a polygon And Return the newly created poly
Function nP_CopyPoly:nP_Poly(poly:nP_Poly)
	Local nPoly:nP_Poly = poly
	Local newPoly:nP_Poly = New nP_Poly
	Local i
	' Copy point data
	For i = 0 To nPoly.iVertexCount - 1
		newPoly.iX[i] = nPoly.iX[i]
		newPoly.iY[i] = nPoly.iY[i]
		newPoly.fX#[i] = nPoly.fX#[i]
		newPoly.fY#[i] = nPoly.fY#[i]
	Next
	' Copy all data
	newPoly.iVertexCount = nPoly.iVertexCount
	newPoly.fScaleX# = nPoly.fScaleX#
	newPoly.fScaleY# = nPoly.fScaleY#
	newPoly.fAngle# = nPoly.fAngle#
	newPoly.iMinX = nPoly.iMinX
	newPoly.iMinY = nPoly.iMinY
	newPoly.iMaxX = nPoly.iMaxX
	newPoly.iMaxY = nPoly.iMaxY
	Return newPoly
End Function

' ----------------------------------------------------------------------

'demo follows

'-----------------------------------------------------------------------

SetGraphicsDriver GLMax2DDriver()
Graphics 640, 480, 0, 2
glEnable GL_LINE_SMOOTH

SeedRnd MilliSecs()
Global tmrFPS = CreateTimer(60)
AppTitle ="nPoly Demo"

gear1 = nP_CreatePoly()
nP_AddVertex(gear1, 0, -50, False)
nP_AddVertex(gear1, 10, -10, False)
nP_AddVertex(gear1, 50, 0, False)
nP_AddVertex(gear1, 10, 10, False)
nP_AddVertex(gear1, 0, 50, False)
nP_AddVertex(gear1, -10, 10, False)
nP_AddVertex(gear1, -50, 0, False)
nP_AddVertex(gear1, -10, -10, False)
nP_AddVertex(gear1, 0, -50)
gear2 = nP_CopyPoly(gear1)
nP_ScalePoly(gear2, 0.7, 0.7)

lever = nP_CreatePoly()
nP_AddVertex(lever, -100, 5, False)
nP_AddVertex(lever, 0, 5, False)
nP_AddVertex(lever, 0, -5, False)
nP_AddVertex(lever, -100, -5, False)
nP_AddVertex(lever, -100, 5)

Const MAXC = 4
Local cylinder:nP_Poly[MAXC]
Local cy:Int[MAXC]
cylinder[0] = nP_CreatePoly()
nP_AddVertex(cylinder(0), -9, 20, False)
nP_AddVertex(cylinder(0), 9, 20, False)
nP_AddVertex(cylinder(0), 9, -20, False)
nP_AddVertex(cylinder(0), -9, -20, False)
nP_AddVertex(cylinder(0), -9, 20)
cy(0) = 100
For i = 0 To MAXC-1
	cylinder(i) = nP_CopyPoly(cylinder[0])
	cy(i) = cy(0)
Next

box = nP_CreatePoly()
nP_AddVertex(box, 0, 0, False)
nP_AddVertex(box, 200, 0, False)
nP_AddVertex(box, 170, 70, False)
nP_AddVertex(box, 200, 200, False)
nP_AddVertex(box, 30, 200, False)
nP_AddVertex(box, 65, 130, False)
nP_AddVertex(box, 0, 180, False)
nP_AddVertex(box, 0, 0)
rad = 15

maxb = 100
maxr = 15

minx = 20
miny = 270
maxx = 620
maxy = 460
Local balllist:TList
Type ball
	Field poly
	Field rad
	Field x#
	Field y#
	Field velx#
	Field vely#
End Type

For i = 1 To maxb
	balls:ball = New ball
	balls.x = Rnd(minx + maxr, maxx - maxr)
	balls.y = Rnd(miny + maxr, maxy - maxr)
	balls.rad = Rnd(4, maxr)
	If Not balllist  Then balllist = CreateList()
	balllist.addlast(balls)
	k = False
	For b:ball = EachIn balllist
		If b &lt;&gt; balls Then
			If nP_CirclesOverlap(balls.x, balls.y, balls.rad, b.x, b.y, b.rad) Then
				ListRemove(balllist,balls)
				k = True
				Exit
			End If
		End If
	Next
	If Not k Then
		balls.velx = Rnd(0, 3) - 1.5
		balls.vely = Rnd(0, 3) - 1.5
		If Rand(1, 3) &gt; 1 And balls.rad &gt; 4 Then
			balls.poly = nP_CreatePoly()
			brad# = balls.rad
			srad# = Rand(2, balls.rad - 2)
			pikes = Rand(3, 7)
			deg = 0
			degstep = 180 / pikes
			For j = 1 To pikes
				nP_AddVertex(balls.poly, brad*Cos(deg), brad*Sin(deg), False)
				deg = deg + degstep
				nP_AddVertex(balls.poly, srad*Cos(deg), srad*Sin(deg), False)
				deg = deg + degstep
			Next
			nP_AddVertex(balls.poly, brad*Cos(deg), brad*Sin(deg), False)
		End If
	End If
Next

While Not KeyDown(KEY_escape)

	starttmr = MilliSecs()
	WaitTimer tmrFPS
	endtmr = MilliSecs()
	timFPS = timFPS + endtmr - starttmr

	If MilliSecs() &gt; timFPS Then
		timFPS = MilliSecs() + 1000
		lastFPS = FPS
		FPS = 0
	Else
		FPS = FPS + 1
	End If
	
	framestart = MilliSecs()
	colchecks = 0
	colchecks2 = 0
	colchecks3 = 0
	
	Cls
	
	nP_RotatePoly(gear1, 1)
	colchecks = colchecks + 1
	' Gears
	While nP_PolyOverlap(gear1, 150, 150, gear2, 212, 150, False)
		nP_RotatePoly(gear2, -1)
		colchecks = colchecks + 1
	Wend
	colchecks = colchecks + 1
	' Gear And lever
	If Not nP_PolyOverlap(gear2, 212, 150, lever, 320, 130, False)
		nP_RotatePoly(lever, -1)
	End If
	colchecks = colchecks + 1
	While nP_PolyOverlap(gear2, 212, 150, lever, 320, 130, False)
		nP_RotatePoly(lever, 1)
		colchecks = colchecks + 1
	Wend
	
	' Cylinders with lever
	For i = 0 To MAXC-1
		colchecks = colchecks + 1
		If Not nP_PolyOverlap(cylinder(i), 230 + 22 * i, cy(i), lever, 320, 130, False)
			cy(i) = cy(i) + 3
		End If	
		colchecks = colchecks + 1
		While nP_PolyOverlap(cylinder(i), 230 + 22 * i, cy(i), lever, 320, 130, False)
			cy(i) = cy(i) - 1
			colchecks = colchecks + 1
		Wend
	Next

	' Resize box And mouse ball
	If KeyDown(200) Then
		nP_ScalePoly(box, 1.01, 1.01)
	End If
	If KeyDown(208) Then
		nP_ScalePoly(box, 0.99, 0.99)
	End If
	If KeyDown(203) Then
		nP_RotatePoly(box, 3)
	End If
	If KeyDown(205) Then
		nP_RotatePoly(box, -3)
	End If
	rad = 15 + MouseZ() * 3
	mx = MouseX()
	my = MouseY()

	nP_DrawPoly(gear1, 150, 150, 255, 0, 0, False)
	nP_DrawPoly(gear2, 212, 150, 255, 0, 0, False)
	nP_DrawPoly(lever, 320, 130, 255, 0, 0, False)
	For i = 0 To MAXC-1
		nP_DrawPoly(cylinder(i), 230 + 22 * i, cy(i), 255, 0, 0, False)
	Next
	DrawText "All polygonal collisions",110, 200
	DrawText "done real-time",110,215
	nP_DrawPoly(box, 400, 50, 255, 255, 0, False)
	DrawText "Hover mouse about this box",400,5
	DrawText "Arrow keys, mouse wheel",400,20
	colchecks2 = colchecks2 + 1
	If nP_PolyOverlapsCircle(box, 400, 50, mx, my, rad, False)
		SetColor 0, 0, 255
	Else
		SetColor 255, 255, 255
	End If
	DrawOval mx - rad, my - rad, rad*2, rad*2
	SetColor 0, 255, 0
	'DrawRect minx, miny, maxx - minx, maxy - miny', False
	For balls:ball = EachIn balllist
		balls.x = balls.x + balls.velx
		balls.y = balls.y + balls.vely
		If balls.x + balls.rad &gt;= maxx Or balls.x - balls.rad &lt;= minx Then
			balls.velx = -balls.velx
		End If
		If balls.y + balls.rad &gt;= maxy Or balls.y - balls.rad &lt;= miny Then
			balls.vely = -balls.vely
		End If
		If balls.poly = 0 Then
			DrawOval balls.x - balls.rad, balls.y - balls.rad, balls.rad*2, balls.rad*2
		Else
			nP_RotatePoly(balls.poly, balls.velx - balls.vely)
			nP_DrawPoly(balls.poly, balls.x, balls.y, 0, 255, 0, False)
		End If
		For b:ball = EachIn balllist
			If b &lt;&gt; balls Then
				If b.poly = 0 And balls.poly = 0 Then
					colchecks3 = colchecks3 + 1
					col = nP_CirclesOverlap(balls.x, balls.y, balls.rad, b.x, b.y, b.rad)
				ElseIf b.poly = 0 Then
					colchecks2 = colchecks2 + 1
					col = nP_PolyOverlapsCircle(balls.poly, balls.x, balls.y, b.x, b.y, b.rad)
				ElseIf balls.poly = 0 Then
					colchecks2 = colchecks2 + 1
					col = nP_PolyOverlapsCircle(b.poly, b.x, b.y, balls.x, balls.y, balls.rad)
				Else
					colchecks = colchecks + 1
					col = nP_PolyOverlap(balls.poly, balls.x, balls.y, b.poly, b.x, b.y)
				End If
				If col Then
					x# = balls.x - b.x
					y# = balls.y - b.y
					balls.velx = Abs(balls.velx) * Sgn(x#)
					balls.vely = Abs(balls.vely) * Sgn(y#)
					b.velx = Abs(b.velx) * -Sgn(x#)
					b.vely = Abs(b.vely) * -Sgn(y#)
					tx# = balls.velx * 0.7 + Abs(b.velx) * Sgn(x#) * 0.3
					ty# = balls.vely * 0.7 + Abs(b.vely) * Sgn(y#) * 0.3
					b.velx = b.velx * 0.7 + Abs(balls.velx) * -Sgn(x#) * 0.3
					b.vely = b.vely * 0.7 + Abs(balls.vely) * -Sgn(y#) * 0.3
					balls.velx = tx#
					balls.vely = ty#
					Exit
				End If
			End If
		Next
	Next

	SetColor 255, 255, 255
	DrawText "FPS: " + lastfps,0,0
	DrawText "Collision checks:",0,15
	DrawText "Poly vs poly:     " + colchecks,0,30
	DrawText "Poly vs circle:   " + colchecks2,0,45
	DrawText "Circle vs circle: " + colchecks3,0,60
	DrawText "Total:            " + (colchecks + colchecks2 + colchecks3),0,75
	
	Flip False

Wend

End
</textarea> <br><br></td></tr></table><br>
<a name="1043892"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TaskMaster</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Obviously, if your objects are moving fast enough to pass through each other, then any single check is not the solution.<br><br>How fast are your objects moving?  The issue of objects travelling fast enough to pass all the way through each other is a pain to deal with.<br><br>What I usually do is set my update routine (logic, not drawing) to run fast enough that that no two objects can pass through each other.  If this isn't possible, then I would say you still don't need to check every pixel, unless you have objects that are only 1 pixel in size.  What is the smallest object and how far can they move in one update?  Divide that size down to get the minimum collision check distance.  If it is just "per pixel" then obviously you will have to check every pixel or do some sort ray or line intersection technique. <br><br></td></tr></table><br>
<a name="1044000"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm designing some sort of a general-purpose 2d engine. So I can't predict how fast objects will move. That is the reason why I chose this approach.<br>Indeed, there should be much to gain trying to improve the search algorithm by taking bigger steps based on the object's radius.<br>But I was thinking: since both objects are squares, and I know their size, isn't there a clever line/ray intersection technique that I could use?<br>@Jesse, thanks for the npoly translation <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
