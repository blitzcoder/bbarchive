<!DOCTYPE html><html lang="en" ><head ><title >Help needed with a precedence climbing parser</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Help needed with a precedence climbing parser</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Help needed with a precedence climbing parser</a><br><br>
<a name="1007864"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AnthonyB</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello everyone,<br><br>I am working on a precedence climbing recursive descent parser for expressions, based on this: <a href="http://www.cs.vu.nl/~eliens/documents/libero/lrintr6.htm" target="_blank">http://www.cs.vu.nl/~eliens/documents/libero/lrintr6.htm</a><br><br>I mainly do it since I'm learning how the algorithm works, so that I can put it to use in a scripting language. But for some reason the precedence doesn't quite work. Could someone take a look at the code and help me?<br><br>Here's the code:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
superstrict

framework BRL.Blitz

main()

'// -----------------------------------------------------------------------------------------------------------
'// Main function.
'// -----------------------------------------------------------------------------------------------------------
function main()

	local parse:Parser = new Parser
	
	parse.tokens[0] = Token.create("9", Token.TYPE_VALUE)
	parse.tokens[1] = Token.create("*", Token.TYPE_BINARY_OPERATOR, 1, "left")
	parse.tokens[2] = Token.create("10", Token.TYPE_VALUE)
	parse.tokens[3] = Token.create("+", Token.TYPE_BINARY_OPERATOR, 2, "left")
	parse.tokens[4] = Token.create("6", Token.TYPE_VALUE)
	parse.tokens[5] = Token.create("+", Token.TYPE_BINARY_OPERATOR, 2, "left")
	parse.tokens[6] = Token.create("3", Token.TYPE_VALUE)
	parse.tokens[7] = Token.create(";")
	
	writestdout("Parsing and building AST for expression: ")
	for local t:Token = eachin parse.tokens; writestdout(t.lexeme); next
	writestdout("~n")
	
	local expressionTree:Tree = parse.Eparser()

end function

'// -----------------------------------------------------------------------------------------------------------
'// Token class.
'// -----------------------------------------------------------------------------------------------------------
type Token

	function create:Token(lexeme:string, tokentype:short = 0, precedence:short = 0, associativity:string = "left")
	
		local t:Token = new Token
		t.lexeme = lexeme
		t.tokentype = tokentype
		
		return t
	
	end function
	
	method isBinary:byte(); return tokentype = TYPE_BINARY_OPERATOR; end method
	method isUnary:byte(); return tokentype = TYPE_UNARY_OPERATOR; end method
	method isValue:byte(); return tokentype = TYPE_VALUE; end method
	
	field lexeme:string
	field tokentype:short
	field precedence:short
	field associativity:string
	
	global TYPE_VALUE:short = 1
	global TYPE_UNARY_OPERATOR:short = 2
	global TYPE_BINARY_OPERATOR:short = 3

end type

'// -----------------------------------------------------------------------------------------------------------
'// Tree class.
'// -----------------------------------------------------------------------------------------------------------
type Tree

	field tok:Token

end type

'// -----------------------------------------------------------------------------------------------------------
'// Leaf class.
'// -----------------------------------------------------------------------------------------------------------
type Leaf extends Tree

	function mkLeaf:Leaf(tok:Token)
	
		local n:Leaf = new Leaf
		n.tok = tok
		writestdout("Created leaf node from token: '" + tok.lexeme + "'.~n")
		return n
	
	end function

end type

'// -----------------------------------------------------------------------------------------------------------
'// Unary operator tree class.
'// -----------------------------------------------------------------------------------------------------------
type Unary extends Tree

	function mkNode:Unary(tok:Token, operand:Tree)
	
		local n:Unary = new Unary
		n.tok = tok
		n.operand = operand
		writestdout("Created unary operator node from token: '" + tok.lexeme + ..
			"' with operand: '" + operand.tok.lexeme + "'.~n")
		return n
	
	end function
	
	field operand:Tree

end type

'// -----------------------------------------------------------------------------------------------------------
'// Binary operator tree class.
'// -----------------------------------------------------------------------------------------------------------
type Binary extends Tree

	function mkNode:Binary(tok:Token, leftOperand:Tree, rightOperand:Tree)
	
		local n:Binary = new Binary
		n.tok = tok
		n.leftOperand = leftOperand
		n.rightOperand = rightOperand
		writestdout("Created binary operator node from token: '" + tok.lexeme + ..
			"' with left operand: '" + leftOperand.tok.lexeme + ..
			"' and right operand '" + rightOperand.tok.lexeme + "'.~n")
		return n
	
	end function
	
	field leftOperand:Tree
	field rightOperand:Tree

end type

'// -----------------------------------------------------------------------------------------------------------
'// The parser.
'// -----------------------------------------------------------------------------------------------------------
type Parser

	method Eparser:Tree()
	
		local t:Tree = Expr(0)
		expect(";")
		return t
	
	end method
	
	method Expr:Tree(p:int)
	
		local t:Tree = Primary()
		while (nextToken().isBinary() and nextToken().precedence &gt;= p)
		
			local op:Token = nextToken()
			consume()
			local q:short
			select (op.associativity)
			
				case "right" q = 1+op.precedence
				case "left" q = op.precedence
			
			end select
			local t1:Tree = Expr(q)
			t = Binary.mkNode(op, t, t1)
		
		end while
		return t
	
	end method
	
	method Primary:Tree()
	
		if (nextToken().isUnary()) then
		
			local op:Token = nextToken()
			consume()
			local q:short = op.precedence
			local t:Tree = Expr(q)
			return Unary.mkNode(op, t)
		
		else if (nextToken().lexeme = "(") then
		
			consume()
			local t:Tree = Expr(0)
			expect(")")
			return t
		
		else if (nextToken().isValue()) then
		
			local t:Tree = Leaf.mkLeaf(nextToken())
			consume()
			return t
		
		else
		
			writestdout("'" + nextToken().lexeme + "': ")
			error()
		
		end if
	
	end method
	
	method consume()
	
		if (nextToken() &lt;&gt; endToken) ..
			pos :+ 1
	
	end method
	
	method expect(tok:string)
	
		if (nextToken().lexeme = tok) ..
			consume() ..
		else ..
			error()
	
	end method
	
	method error()
	
		writestdout("Syntax error!~n")
		end
	
	end method
	
	method nextToken:Token()
	
		if (not tokens[pos+1]) ..
			return endToken
		
		return tokens[pos+1]
	
	end method
	
	global endToken:Token = Token.create(";")
	
	field pos:int = -1
	field tokens:Token[8]

end type
</textarea><br><br>This is a direct port of the psuedo-code from the essay I linked to. This is not intended to be the actual parser for my scripting language, but rather a test version, to learn how it works, and to have as a template when I implement it into my real parser. I wrote it really really fast, just to try it out, but now I have been sitting with it for hours and hours on end and I cannot for the life of me figure out what's wrong.<br><br>Any help would be appreciated! :)<br><br>Regards,<br>Anthony <br><br></td></tr></table><br>
<a name="1008048"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AnthonyB</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Noone? :) I thought there would at least be SOMEONE who had any idea of how these things worked. <br><br></td></tr></table><br>
<a name="1008049"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have no idea what a "precedence climbing recursive descent parser for expressions" is, unfortunately, but we've been studying Reverse Polish Notation, which is a way of representing mathematical expressions in a way that is easy for a computer to store and process. You could adapt it, possibly, to other kinds of expression?<br><br>Here's the Wiki article: <a href="http://en.wikipedia.org/wiki/Reverse_polish_notation" target="_blank">http://en.wikipedia.org/wiki/Reverse_polish_notation</a><br>Here's how to convert: <a href="http://en.wikipedia.org/wiki/Shunting-yard_algorithm" target="_blank">http://en.wikipedia.org/wiki/Shunting-yard_algorithm</a><br><br>"Interpreters of Reverse Polish notation are often stack-based; that is, operands are pushed onto a stack, and when an operation is performed, its operands are popped from a stack and its result pushed back on. Stacks, and therefore RPN, have the advantage of being easy to implement and very fast."<br><br>If you think this can suit your need, I can explain further. :) <br><br></td></tr></table><br>
<a name="1008059"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AnthonyB</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for the reply. Precedence climbing is explained in the article I linked to.<br><br>I have glanced at Reverse Polish Notation (RPN) before, but precedence climbing is supposed to be even faster, so I never payed much attention to it. And the fact that GCC uses Precedence Climbing for arithmetic expressions just made it even more appealing. But I guess I have to use RPN either way, since neither I, or anyone else seems to be able to grasp what's wrong with the code I posted.<br><br>Could you ellaborate further on RPN? I don't need this for evaluating expressions per se, but rather to build an Abstract Syntax Tree from it. This is one reason why I haven't been using RPN. It seems time consuming to first convert an expression to RPN, and THEN to an abstract syntax tree. Or is there a fast way to accomplish this?<br><br>Thanks,<br>Anthony <br><br></td></tr></table><br>
<a name="1008062"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warpy</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm getting round to looking at it, Anthony, give me some time! <br>Well, I've done a <a href="/codearcs.php?code=2450" target="_blank">shunting yard algorithm</a> before, I'll see how similar this is. <br><br></td></tr></table><br>
<a name="1008067"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warpy</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> ok, so you never set the precedence of any tokens, that's probably your first error.<br><br>EDIT: that fixed, you just had the precedences the wrong way round. Changing the multiplication to 2 and addition to 1 worked. <br><br></td></tr></table><br>
<a name="1008074"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AnthonyB</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Warpy, I tried that, but it still doesn't work. I get this output:<br><br>Created leaf node from token: '9'.<br>Created leaf node from token: '10'.<br>Created leaf node from token: '6'.<br>Created leaf node from token: '3'.<br>Created binary operator node from token: '+' with left operand: '6' and right operand '3'.<br>Created binary operator node from token: '*' with left operand: '10' and right operand '+'.<br>Created binary operator node from token: '+' with left operand: '9' and right operand '*'.<br><br>But I should get this output (except maybe the order they are created in might be different when it works, but they should be assigned like this):<br><br>Created leaf node from token: '9'.<br>Created leaf node from token: '10'.<br>Created leaf node from token: '6'.<br>Created leaf node from token: '3'.<br>Created binary operator node from token: '+' with left operand: '*' and right operand '3'.<br>Created binary operator node from token: '*' with left operand: '10' and right operand '6'.<br>Created binary operator node from token: '+' with left operand: '9' and right operand '*'. <br><br></td></tr></table><br>
<a name="1008096"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warpy</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> That's odd. Here's my code:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Framework BRL.Blitz

main()

'// -----------------------------------------------------------------------------------------------------------
'// Main function.
'// -----------------------------------------------------------------------------------------------------------
Function main()

	Local parse:Parser = New Parser
	
	parse.tokens[0] = Token.Create("9", Token.TYPE_VALUE)
	parse.tokens[1] = Token.Create("*", Token.TYPE_BINARY_OPERATOR, 2, "left")
	parse.tokens[2] = Token.Create("10", Token.TYPE_VALUE)
	parse.tokens[3] = Token.Create("+", Token.TYPE_BINARY_OPERATOR, 1, "left")
	parse.tokens[4] = Token.Create("6", Token.TYPE_VALUE)
	parse.tokens[5] = Token.Create("+", Token.TYPE_BINARY_OPERATOR, 1, "left")
	parse.tokens[6] = Token.Create("3", Token.TYPE_VALUE)
	parse.tokens[7] = Token.Create(";")
	
	WriteStdout("Parsing and building AST for expression: ")
	For Local t:Token = EachIn parse.tokens; WriteStdout(t.lexeme); Next
	WriteStdout("~n")
	
	Local expressionTree:Tree = parse.Eparser()
	DebugStop

End Function

'// -----------------------------------------------------------------------------------------------------------
'// Token class.
'// -----------------------------------------------------------------------------------------------------------
Type Token

	Function Create:Token(lexeme:String, tokentype:Short = 0, precedence:Short = 0, associativity:String = "left")
	
		Local t:Token = New Token
		t.lexeme = lexeme
		t.tokentype = tokentype
		t.precedence=precedence
		t.associativity=associativity
		
		Return t
	
	End Function
	
	Method isBinary:Byte(); Return tokentype = TYPE_BINARY_OPERATOR; End Method
	Method isUnary:Byte(); Return tokentype = TYPE_UNARY_OPERATOR; End Method
	Method isValue:Byte(); Return tokentype = TYPE_VALUE; End Method
	
	Field lexeme:String
	Field tokentype:Short
	Field precedence:Short
	Field associativity:String
	
	Global TYPE_VALUE:Short = 1
	Global TYPE_UNARY_OPERATOR:Short = 2
	Global TYPE_BINARY_OPERATOR:Short = 3

End Type

'// -----------------------------------------------------------------------------------------------------------
'// Tree class.
'// -----------------------------------------------------------------------------------------------------------
Type Tree

	Field tok:Token

End Type

'// -----------------------------------------------------------------------------------------------------------
'// Leaf class.
'// -----------------------------------------------------------------------------------------------------------
Type Leaf Extends Tree

	Function mkLeaf:Leaf(tok:Token)
	
		Local n:Leaf = New Leaf
		n.tok = tok
		WriteStdout("Created leaf node from token: '" + tok.lexeme + "'.~n")
		Return n
	
	End Function

End Type

'// -----------------------------------------------------------------------------------------------------------
'// Unary operator tree class.
'// -----------------------------------------------------------------------------------------------------------
Type Unary Extends Tree

	Function mkNode:Unary(tok:Token, operand:Tree)
	
		Local n:Unary = New Unary
		n.tok = tok
		n.operand = operand
		WriteStdout("Created unary operator node from token: '" + tok.lexeme + ..
			"' with operand: '" + operand.tok.lexeme + "'.~n")
		Return n
	
	End Function
	
	Field operand:Tree

End Type

'// -----------------------------------------------------------------------------------------------------------
'// Binary operator tree class.
'// -----------------------------------------------------------------------------------------------------------
Type Binary Extends Tree

	Function mkNode:Binary(tok:Token, leftOperand:Tree, rightOperand:Tree)
	
		Local n:Binary = New Binary
		n.tok = tok
		n.leftOperand = leftOperand
		n.rightOperand = rightOperand
		WriteStdout("Created binary operator node from token: '" + tok.lexeme + ..
			"' with left operand: '" + leftOperand.tok.lexeme + ..
			"' and right operand '" + rightOperand.tok.lexeme + "'.~n")
		Return n
	
	End Function
	
	Field leftOperand:Tree
	Field rightOperand:Tree

End Type

'// -----------------------------------------------------------------------------------------------------------
'// The parser.
'// -----------------------------------------------------------------------------------------------------------
Type Parser

	Method Eparser:Tree()
		'DebugStop
		Local t:Tree = Expr(0)
		expect(";")
		Return t
	
	End Method
	
	Method Expr:Tree(p:Int)
	
		Local t:Tree = Primary()
		While (nextToken().isBinary() And nextToken().precedence &gt;= p)
		
			Local op:Token = nextToken()
			consume()
			Local q:Short
			Select (op.associativity)
			
				Case "right" q = 1+op.precedence
				Case "left" q = op.precedence
			
			End Select
			Local t1:Tree = Expr(q)
			t = Binary.mkNode(op, t, t1)
		
		End While
		Return t
	
	End Method
	
	Method Primary:Tree()
	
		If (nextToken().isUnary()) Then
		
			Local op:Token = nextToken()
			consume()
			Local q:Short = op.precedence
			Local t:Tree = Expr(q)
			Return Unary.mkNode(op, t)
		
		Else If (nextToken().lexeme = "(") Then
		
			consume()
			Local t:Tree = Expr(0)
			expect(")")
			Return t
		
		Else If (nextToken().isValue()) Then
		
			Local t:Tree = Leaf.mkLeaf(nextToken())
			consume()
			Return t
		
		Else
		
			WriteStdout("'" + nextToken().lexeme + "': ")
			error()
		
		End If
	
	End Method
	
	Method consume()
	
		If (nextToken() &lt;&gt; endToken) ..
			pos :+ 1
	
	End Method
	
	Method expect(tok:String)
	
		If (nextToken().lexeme = tok) ..
			consume() ..
		Else ..
			error()
	
	End Method
	
	Method error()
	
		WriteStdout("Syntax error!~n")
		End
	
	End Method
	
	Method nextToken:Token()
	
		If (Not tokens[pos+1]) ..
			Return endToken
		
		Return tokens[pos+1]
	
	End Method
	
	Global endToken:Token = Token.Create(";")
	
	Field pos:Int = -1
	Field tokens:Token[8]

End Type

</textarea><br>All I changed was the precedences and Token.Create. Did you set the associativity field as well? <br><br></td></tr></table><br>
<a name="1008104"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AnthonyB</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ahhhhhhhhhhhhhhhhhhhhhhhh... Why the hell didn't I see that? :D Thanks!<br><br>EDIT: Buuuuuuuuuuuuut... there's still one problem. This solves the precedence issue, but now the operators of the same precedence don't work as expected.<br><br>This is the output I get now, when I changed the code:<br><br>Created leaf node from token: '9'.<br>Created leaf node from token: '10'.<br>Created leaf node from token: '6'.<br>Created binary operator node from token: '*' with left operand: '10' and right operand '6'.<br>Created leaf node from token: '3'.<br>Created binary operator node from token: '+' with left operand: '*' and right operand '3'.<br>Created binary operator node from token: '+' with left operand: '9' and right operand '+'.<br><br>But the last '+' operator should have * as it's right operand, not +. <br><br></td></tr></table><br>
<a name="1008121"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AnthonyB</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Although, when looking closer at it, maybe it does work? It yields the same result, when evaluating the tree, as the output I expected it to have. <br><br></td></tr></table><br>
<a name="1008136"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warpy</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> you might like this for printing out your parsed tree:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Type tree
	Field name$
	Method show$[]() Abstract
End Type


Function leaf:tleaf(name$)
	l:tleaf=New tleaf
	l.name=name
	Return l
End Function

Type tleaf Extends tree
	Method show$[]()
		Return [name]
	End Method
End Type

Function binary:tbinary(name$,l:tree,r:tree)
	b:tbinary=New tbinary
	b.name=name
	b.l=l
	b.r=r
	Return b
End Function
Type tbinary Extends tree
	Field name$
	Field l:tree,r:tree
	Method show$[]()
		Local s1$[],s2$[]
		s1=l.show()
		s2=r.show()
		
		Function width(s$[])
			w=0
			For i=0 To Len(s)-1
				w=Max(w,Len(s[i]))
			Next
			Return w
		End Function
		
		w1=width(s1)
		w2=width(s2)
		
		Function mulstring$(s$,n)
			o$=""
			For i=1 To n
				o:+s
			Next
			Return o
		End Function
		
		mw=w1+Len(name)+2
		h1=Len(s1)
		h2=Len(s2)
		Print name
		Print w1+","+h1
		Print w2+","+h2
		Local lines$[Max(h1,h2)+1]
		lines[0]=mulstring(" ",w1+1)+name
		d=h1-h2
		If h1&lt;h2
			For i=1 To h2-h1
				lines[i]=mulstring(" ",mw)+s2[i-1]
			Next
			For i=0 To h1-1
				lines[h2-h1+i+1]=s1[i]+mulstring(" ",mw-Len(s1[i]))+s2[i+h2-h1]
			Next
		Else
			For i=1 To h1-h2
				lines[i]=s1[i-1]
			Next
			For i=0 To h2-1
				lines[h1-h2+i+1]=s1[i+h1-h2]+mulstring(" ",mw-Len(s1[i+h1-h2]))+s2[i]
			Next
			
		EndIf
		Return lines
	End Method
End Type

't:tree=binary("*",leaf("a"),leaf("b"))
t:tree=binary("?",binary("*",leaf("z"),binary("+",leaf("1"),leaf("a"))),leaf("q"))
For line$=EachIn t.show()
	Print line
Next
</textarea> <br><br></td></tr></table><br>
<a name="1008311"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AnthonyB</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Warpy! That saved me a bunch of time :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
