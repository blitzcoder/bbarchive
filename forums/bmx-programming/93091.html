<!DOCTYPE html><html lang="en" ><head ><title >Dynamic Glossy Buttons</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Dynamic Glossy Buttons</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Dynamic Glossy Buttons</a><br><br>
<a name="1062941"></a>

<a name="1062942"></a>

<a name="1062943"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >siread</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've been working on my own gui system lately (sure there are loads out there and Fry's is excellent but I want to have keyboard/joypad control) and I came up with a method for creating nice buttons without needing to load in any images. Check it out...<br><br><img src="http://download.newstargames.com/misc/DynamicGlossyButtons.png"><br><br>The buttons have no function in this code, it's just an example of how to draw them. I'm sure there is a nicer method for setting the alphagrid over the corners of the pixmap, so any help with that would be much appreciated.<br><br>Oh, and make sure you load in a nice font. :)<br><br><pre class=code>
SuperStrict
Graphics(600,400)
SetBlend(ALPHABLEND)
SetClsColor(192,192,192)
' Local fnt:TImageFont = LoadImageFont("trebucbd.ttf", 16, SMOOTHFONT)
' SetImageFont(fnt)

Local btn1:TButton = TButton.CreateButton("btn_1", "X", 550, 10, 40, 40, "FF0000")
Local btn2:TButton = TButton.CreateButton("btn_2", "Dynamic Glossy Buttons", 100, 10, 400, 60, "FFFFFF")
Local btn3:TButton = TButton.CreateButton("btn_3", "Nice Colour", 200, 100, 200, 100, "00FF00")
Local btn4:TButton = TButton.CreateButton("btn_4", "Smooth Corners", 100, 220, 400, 50, "FFFF00")
Local btn5:TButton = TButton.CreateButton("btn_5", "No pngs needed!", 150, 290, 300, 20, "AA00FF")
Local btn6:TButton = TButton.CreateButton("btn_6", "Enjoy!", 250, 320, 100, 30, "0000FF")

Repeat
	Cls
	TButton.DrawAll()
	Flip
Until KeyHit(KEY_ESCAPE) Or AppTerminate()


Type TButton
	Global list:TList
	Global alphagrid:Int[][] = [[0,0,0,0,128,192],[0,0,64,192,255,255],[0,64,255,255,255,255],[0,192,255,255,255,255],[128,255,255,255,255,255],[192,255,255,255,255,255]]		
	
	Field name:String
	Field txt:String
	Field x:Int
	Field y:Int
	Field w:Int
	Field h:Int
	Field hexcol:String
	Field image:TImage
	
	Method New()
		If Not list Then list = CreateList()
		list.AddLast(Self)
	End Method
	
	Function CreateButton:TButton(name:String, txt:String, bx:Int, by:Int, bw:Int, bh:Int, hexcol:String)
		Local b:TButton = New TButton
		b.name = name
		b.txt = txt
		b.x = bx
		b.y = by
		b.w = bw
		b.h = bh
		b.hexcol = hexcol
		b.CreateButtonImage()
		Return b
	End Function
	
	Method CreateButtonImage()
		If w &lt; 12 Or h &lt; 12 Then Return
			
		Local pix:TPixmap = CreatePixmap(w, h, PF_RGBA8888) 
		pix.ClearPixels(0)
		
		' First fill the pixmap with greyscale colour
		For Local x:Int = 0 To w-1
			For Local y:Int = h-1 To 0 Step -1
				Local col:Float = 255.0-(96.0/h)*y				' Base the gradient on the y co-ordinate
				If y &gt;= h/2 Then col = 245.0-(96.0/h)*y			' Change it slightly half-way up
				pix.WritePixel(x,y,GetRGBA(col,col,col,255))
			Next
		Next
	
		' Now use the alphagrid to create the top left corner
		For Local x:Int = 0 To 5
			For Local y:Int = 0 To 5
				' Get existing pixel colour
				Local p:Int = pix.ReadPixel(x,y)
				Local r:Int = (p Shr 16) &amp; $FF
				Local g:Int = (p Shr 8) &amp; $FF
				Local b:Int = (p Shr 0) &amp; $FF
				
				' Replace with alpha
				Local a:Int = alphagrid[x][y]
				pix.WritePixel(x,y,GetRGBA(r,g,b,a))
			Next
		Next
		
		' Top right corner
		Local bx:Int = w-1
		For Local x:Int = 0 To 5
			For Local y:Int = 0 To 5
				Local p:Int = pix.ReadPixel(bx,y)
				Local r:Int = (p Shr 16) &amp; $FF
				Local g:Int = (p Shr 8) &amp; $FF
				Local b:Int = (p Shr 0) &amp; $FF
				Local a:Int = alphagrid[x][y]
				pix.WritePixel(bx,y,GetRGBA(r,g,b,a))
			Next
			
			bx:-1
		Next
				
		' Bottom left corner
		For Local x:Int = 0 To 5
			Local by:Int = h-1
			For Local y:Int = 0 To 5
				Local p:Int = pix.ReadPixel(x,by)
				Local r:Int = (p Shr 16) &amp; $FF
				Local g:Int = (p Shr 8) &amp; $FF
				Local b:Int = (p Shr 0) &amp; $FF
				Local a:Int = alphagrid[x][y]
				pix.WritePixel(x,by,GetRGBA(r,g,b,a))
				by:-1
			Next
		Next
		
		' Bottom right corner
		bx = w-1
		For Local x:Int = 0 To 5
			Local by:Int = h-1
			For Local y:Int = 0 To 5

				Local p:Int = pix.ReadPixel(bx,by)
				Local r:Int = (p Shr 16) &amp; $FF
				Local g:Int = (p Shr 8) &amp; $FF
				Local b:Int = (p Shr 0) &amp; $FF
				Local a:Int = alphagrid[x][y]
				pix.WritePixel(bx,by,GetRGBA(r,g,b,a))
				by:-1
			Next
			
			bx:-1
		Next
		
		image = LoadImage(pix)
	End Method
	
	Function DrawAll()
		For Local b:TButton = EachIn list
			b.Draw()
		Next
	End Function
	
	Method Draw()
		If image
			' Button shadow
			SetColor(0,0,0)
			SetAlpha(0.5)
			DrawImage(image, x+1, y+1)
			
			' Button
			SetHexColour(hexcol)
			SetAlpha(1)
			DrawImage(image, x, y)
			
			' Text position
			Local tx:Int = x+(w/2)
			tx:-TextWidth(txt)/2
			
			Local ty:Int = y+(h/2)
			ty:-TextHeight(txt)/2

			' Shadow
			SetColor(0,0,0)
			SetAlpha(0.25)
			DrawText(txt, tx+1, ty)

			' Text
			SetAlpha(1)
			DrawText(txt, tx, ty-1)
		End If
	End Method
End Type

Function GetRGBA:Int(r:Int, g:Int, b:Int, a:Int = 0)
     Return a Shl 24 | r Shl 16 | g Shl 8 | b Shl 0
End Function

Function SetHexColour(col:String)
	Local r:Int, g:Int, b:Int
	r = Int("$"+col[0..2])
	g = Int("$"+col[2..4])
	b = Int("$"+col[4..6])
	SetColor(r, g, b)
End Function
</pre><br><br><font class="tiny">Last edited 2010</font><br><br><font class="tiny">Last edited 2010</font> <br><br></td></tr></table><br>
<a name="1062972"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >wmaass</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Always looking for new GUI goodness! I have been using FryGui for a lot of stuff lately. I was turned onto it by what you were able to do with it. Thanks for sharing this stuff. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
