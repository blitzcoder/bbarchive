<!DOCTYPE html><html lang="en" ><head ><title >SetVirtualResolution With Aspect Correction?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >SetVirtualResolution With Aspect Correction?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >SetVirtualResolution With Aspect Correction?</a><br><br>
<a name="1023405"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CyBeRGoth</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi<br><br>It has been a while since I checked but has anyone come up with a soloution for using SetVirtualResolution with aspect correction.<br><br>For example creating your game to work widescreen at say 1280 x 800, then using SetVirtualResolution to allow people with non-widescreen monitors to play the game at 800 x 600 with black borders at the top/bottom of their screen? <br><br></td></tr></table><br>
<a name="1023406"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ima747</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I don't know much about virtual resolution so apologies if this is off track. I think you could use SetViewPort to create the resolution frame... would probably have to have an offset as well to have it centered, but SetOrigin() should cover that...<br><br>Perhaps there's some catch with virtual resolution I'm unaware of. <br><br></td></tr></table><br>
<a name="1024090"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MGE</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I thought for sure this would have been added in the latest fix. Maybe the next one if we all beg Mark? Please? <br><br></td></tr></table><br>
<a name="1024154"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Me too! <br><br></td></tr></table><br>
<a name="1024969"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Russell</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I just use SetOrigin() and use the x and y offsets as the parameters. For example, if your game is developed in 1280 x 720 (a nice 16:9 ratio), but the user has a 4:3 display (likely!), then you can set the actual display to, say, 1280 x 960 and SetOrigin would be SetOrigin(0,120). {120 is half of the difference between 960 and 720}.<br><br>SetVirtualResolution() is awesome, except that depending on how far the display has to be stretched (the backbuffer is scaled to the actual display when you flip(), I'm guessing) the resulting graphics can be a little fuzzy. (You'll notice this fuzziness in other programs, too, like MAME in fullscreen mode).<br><br>If you use the SetOrigin method, keep in mind that this only affects drawing operations. SetViewPort(), for example, will have to take this into account by adding the x offset and y offset to the x/y parameters. The payoff is that the display is much cleaner.<br><br>Automatic letterboxing/pillarboxing would be nice, but this is easily done.<br><br>Russell <br><br></td></tr></table><br>
<a name="1024977"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Oddball</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> You mean something like this?<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'Code: David 'Oddball' Williamson
'6th May 2010
'Enjoy!

SuperStrict

Const ASPECT_STRETCH:Int=0
Const ASPECT_LETTERBOX_FILL:Int=2
Const ASPECT_LETTERBOX_BORDER:Int=3
Const ASPECT_BESTFIT_FILL:Int=4
Const ASPECT_BESTFIT_BORDER:Int=5	'Bestfit border may not work with all drivers(i.e. DirectX)

Private

Global _xoffset:Float
Global _yoffset:Float
Global _vwidth:Float
Global _vheight:Float
Global _mode:Int=0
Global _bred:Int=0
Global _bgreen:Int=0
Global _bblue:Int=0

Public

Function SetVirtualResolution( width:Float, height:Float, mode:Int=ASPECT_STRETCH )
	If mode=ASPECT_STRETCH
		_xoffset=0;_yoffset=0;_vwidth=width;_vheight=height;_mode=mode
		brl.max2d.SetVirtualResolution(width,height)
		brl.max2d.SetViewport _xoffset,_yoffset,_vwidth,_vheight
		brl.max2d.SetOrigin _xoffset,_yoffset
		Return
	EndIf
	Local gwidth:Int=GraphicsWidth()
	Local gheight:Int=GraphicsHeight()
	_vwidth=width
	_vheight=height
	_mode=mode
	Local wratio:Float=gwidth/_vwidth
	Local hratio:Float=gheight/_vheight
	If wratio&lt;hratio Then hratio=wratio
	If _mode&amp;4
		hratio=Floor(hratio)
		hratio=Max(hratio,1)
	EndIf
	height=gheight/hratio
	width=gwidth/hratio
	
	_xoffset=(width-_vwidth)*.5
	_yoffset=(height-_vheight)*.5
	If _mode&amp;4
		_xoffset=Floor(_xoffset)
		_yoffset=Floor(_yoffset)
	EndIf
	brl.max2d.SetVirtualResolution(width,height)
	
	If _mode&amp;1
		Local red:Int,green:Int,blue:Int
		GetClsColor red,green,blue
		SetClsColor _bred,_bgreen,_bblue
		Cls
		SetClsColor red,green,blue
		brl.max2d.SetViewport _xoffset,_yoffset,_vwidth,_vheight
	Else
		brl.max2d.SetViewport 0,0,brl.max2d.VirtualResolutionWidth(),brl.max2d.VirtualResolutionHeight()
	EndIf
	
	brl.max2d.SetOrigin _xoffset,_yoffset
	
End Function

Function SetBorderColor( red:Int, green:Int, blue:Int )
	_bred=red
	_bgreen=green
	_bblue=blue
End Function

Function SwitchAspectMode( mode:Int )
	SetVirtualResolution _vwidth,_vheight,mode
End Function

Function Flip( sync:Int=-1 )
	brl.Graphics.Flip sync
	If _mode&amp;1
		brl.max2d.SetViewport 0,0,brl.max2d.VirtualResolutionWidth(),brl.max2d.VirtualResolutionHeight()
		Local red:Int,green:Int,blue:Int
		GetClsColor red,green,blue
		SetClsColor _bred,_bgreen,_bblue
		Cls
		SetClsColor red,green,blue
		brl.max2d.SetViewport _xoffset,_yoffset,_vwidth,_vheight
	EndIf
End Function

Function SetOrigin( x:Float, y:Float )
	brl.max2d.SetOrigin x+_xoffset,y+_yoffset
End Function

Function GetOrigin( x:Float Var, y:Float Var )
	brl.max2d.GetOrigin x,y
	x:-_xoffset
	y:-_yoffset
End Function

Function SetViewport( x:Int, y:Int, width:Int, height:Int )
	Local x2:Float=Min(x+width,_xoffset+_vwidth)
	Local y2:Float=Min(y+height,_yoffset+_vheight)
	x=Max(x,_xoffset)
	y=Max(y,_yoffset)
	brl.max2d.SetViewport x,y,x2-x,y2-y
End Function

Function GetViewport( x:Int Var,y:Int Var,width:Int Var,height:Int Var )
	brl.max2d.GetViewport x,y,width,height
	x:-_xoffset
	y:-_yoffset
End Function

Function VirtualMouseX:Float()
	Return brl.max2d.VirtualMouseX()-_xoffset
End Function

Function VirtualMouseY:Float()
	Return brl.max2d.VirtualMouseY()-_yoffset
End Function

Function VirtualResolutionWidth:Float()
	Return _vwidth
End Function

Function VirtualResolutionHeight:Float()
	Return _vheight
End Function

'-----------------------
'Test code
'-----------------------
SeedRnd MilliSecs()
Graphics Rand(320,DesktopWidth()),Rand(240,DesktopHeight())

SetBorderColor 32,32,32
SetClsColor 223,223,223
SetColor 255,127,0

SetVirtualResolution 320,240,ASPECT_LETTERBOX_BORDER

Repeat
	If KeyHit(KEY_1) Then SwitchAspectMode ASPECT_STRETCH
	If KeyHit(KEY_2) Then SwitchAspectMode ASPECT_LETTERBOX_FILL
	If KeyHit(KEY_3) Then SwitchAspectMode ASPECT_LETTERBOX_BORDER
	If KeyHit(KEY_4) Then SwitchAspectMode ASPECT_BESTFIT_FILL
	If KeyHit(KEY_5) Then SwitchAspectMode ASPECT_BESTFIT_BORDER
	Cls
	DrawText "Aspect correction demo",5,5
	DrawText VirtualMouseX()+","+VirtualMouseY(),VirtualMouseX(),VirtualMouseY()
	Flip
Until KeyHit(KEY_ESCAPE)
</textarea><br><br>I literally just spewed the code into the ide so haven't tested it that much. Let me know if anything needs correcting or adding.<br><br><b>EDIT:</b> Just corrected a couple of bugs.<br><br>Enjoy! <br><br></td></tr></table><br>
<a name="1025016"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CyBeRGoth</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cheers guys and thanks to oddball for the code, that is some nice stuff and does exactly what we need :)<br><br>I am just wondering now though if creating a game that is developed from the ground up to be played at a 16:9 aspect is a good idea, seeing as we mostly make casual games, do the people who buy casual games generally have access to widescreen?<br><br>I know the steam statistics thing released a while ago suggested that a lot of people do use 16:9, on steam, but then casual buyers tend not to be hardcore like a lot of people who play stuff on steam.<br><br>Anyhoo just rambling, thanks again for the soloution :) <br><br></td></tr></table><br>
<a name="1025022"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> For the past casual games Ive made Ive stuck with 800 x 600 (although I did do Gunslinger originally as 1024 x 768) as most casual games do this res...<br><br>@Oddball, nice code there - my only issue is that you are using SetViewport and there have been reported problems with this... <br><br></td></tr></table><br>
<a name="1025023"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GfK</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> I am just wondering now though if creating a game that is developed from the ground up to be played at a 16:9 aspect is a good idea, seeing as we mostly make casual games, do the people who buy casual games generally have access to widescreen? <br></div><br>Widescreen is probably the de-facto standard now, although 16:10 (or 8:5, if you like) is more common than 16:9 as far as computer resolutions are concerned.<br><br>That said, I still code for 4:3 as there is still a not insignificant number of 4:3 monitors out there. <br><br></td></tr></table><br>
<a name="1025024"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Oddball</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> On the subject of which ratio to target when developing I always go for 16:9 so that on almost all other shape monitors it'll black band at the top and bottom, which most casual gamers are already used to seeing on films.<br><br><a href="/Account/showuser.php?id=10732" target="_blank"><b>therevills</b></a><b> wrote:</b><div class="quote"> @Oddball, nice code there - my only issue is that you are using SetViewport and there have been reported problems with this... <br></div>The only issue that should occur with this is when using ASPECT_BESTFIT_BORDER and DirectX as it sometimes does not clip two of the sides. All other modes should be fine and work consistently across all systems. If I get a little time I might convert this into a module and also a module tweak for Max2D. <br><br></td></tr></table><br>
<a name="1025040"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Oddball</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sorry to double post, but I made this into a module for those that find them more convenient.<br><br><a href="http://www.gooeyblob.com/temp/aspectmodule.zip" target="_blank"><b>&gt;&gt;Get it here&lt;&lt;</b></a><br><br>Enjoy! <br><br></td></tr></table><br>
<a name="1025106"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> work consistently across all systems <br></div><br><br>I was under the impression that the SetViewPort commands doesnt work consistently across all graphics cards - esp old ones and the on-board intels ones...<br><br>OT: Can you remind how your "IF" statements are working:<br><br><pre class=code>
If _mode&amp;4
</pre><br><br>Thanks! <br><br></td></tr></table><br>
<a name="1025120"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Oddball</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> <i>SetViewport</i> works on every machine <i>I've</i> tested it on in both DirectX and OpenGL, but I have been told that it only clips two sides when using DirectX on some machines. I have no reason to suspect they are lying hence my proviso on best fit with borders. The code is there so feel free to test it on any machine you can get your hands on. I'd certainly be interested to know how widespread this issue is. Either way only the two bordered modes use <i>SetViewport</i> if you feel this is an issue stick with the other modes and draw your own borders just before flipping.<br><br>That if statement is merely performing a bit flag test on the third bit of <i>_mode</i>. The best fit constants both have the third bit on and both bordered modes have the first bit set to on. The code just tests the individual bit to see if it is set. So <i>_mode&amp;4</i> is true if <i>_mode</i> is either of the best fit modes and <i>_mode&amp;1</i> is true if it is either of the bordered modes. <br><br></td></tr></table><br>
<a name="1025191"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MGE</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> SetViewPort is not something you should rely on in a commercial game, according to everything I've read on it at least. <br><br></td></tr></table><br>
<a name="1025219"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grafos</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> I had problems with setviewport on the DX7 drivers, but it seems to work great with the dx9 one on my machnine <br><br></td></tr></table><br>
<a name="1304956"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chapman7</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sorry for bringing back an old thread but I am trying to get this to work with MaxGUI and I am having some issues with it...<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'Code: David 'Oddball' Williamson
'6th May 2010
'Enjoy!

SuperStrict

Import MaxGUI.Drivers

Const ASPECT_STRETCH:Int=0
Const ASPECT_LETTERBOX_FILL:Int=2
Const ASPECT_LETTERBOX_BORDER:Int=3
Const ASPECT_BESTFIT_FILL:Int=4
Const ASPECT_BESTFIT_BORDER:Int=5	'Bestfit border may not work with all drivers(i.e. DirectX)

Private

Global _xoffset:Float
Global _yoffset:Float
Global _vwidth:Float
Global _vheight:Float
Global _mode:Int=0
Global _bred:Int=0
Global _bgreen:Int=0
Global _bblue:Int=0

Public

Function SetVirtualResolution( width:Float, height:Float, mode:Int=ASPECT_STRETCH )
	If mode=ASPECT_STRETCH
		_xoffset=0;_yoffset=0;_vwidth=width;_vheight=height;_mode=mode
		brl.max2d.SetVirtualResolution(width,height)
		brl.max2d.SetViewport _xoffset,_yoffset,_vwidth,_vheight
		brl.max2d.SetOrigin _xoffset,_yoffset
		Return
	EndIf
	Local gwidth:Int=GraphicsWidth()
	Local gheight:Int=GraphicsHeight()
	_vwidth=width
	_vheight=height
	_mode=mode
	Local wratio:Float=gwidth/_vwidth
	Local hratio:Float=gheight/_vheight
	If wratio&lt;hratio Then hratio=wratio
	If _mode&amp;4
		hratio=Floor(hratio)
		hratio=Max(hratio,1)
	EndIf
	height=gheight/hratio
	width=gwidth/hratio
	
	_xoffset=(width-_vwidth)*.5
	_yoffset=(height-_vheight)*.5
	If _mode&amp;4
		_xoffset=Floor(_xoffset)
		_yoffset=Floor(_yoffset)
	EndIf
	brl.max2d.SetVirtualResolution(width,height)
	
	If _mode&amp;1
		Local red:Int,green:Int,blue:Int
		GetClsColor red,green,blue
		SetClsColor _bred,_bgreen,_bblue
		Cls
		SetClsColor red,green,blue
		brl.max2d.SetViewport _xoffset,_yoffset,_vwidth,_vheight
	Else
		brl.max2d.SetViewport 0,0,brl.max2d.VirtualResolutionWidth(),brl.max2d.VirtualResolutionHeight()
	EndIf
	
	brl.max2d.SetOrigin _xoffset,_yoffset
	
End Function

Function SetBorderColor( red:Int, green:Int, blue:Int )
	_bred=red
	_bgreen=green
	_bblue=blue
End Function

Function SwitchAspectMode( mode:Int )
	SetVirtualResolution _vwidth,_vheight,mode
End Function

Function Flip( sync:Int=-1 )
	brl.Graphics.Flip sync
	If _mode&amp;1
		brl.max2d.SetViewport 0,0,brl.max2d.VirtualResolutionWidth(),brl.max2d.VirtualResolutionHeight()
		Local red:Int,green:Int,blue:Int
		GetClsColor red,green,blue
		SetClsColor _bred,_bgreen,_bblue
		Cls
		SetClsColor red,green,blue
		brl.max2d.SetViewport _xoffset,_yoffset,_vwidth,_vheight
	EndIf
End Function

Function SetOrigin( x:Float, y:Float )
	brl.max2d.SetOrigin x+_xoffset,y+_yoffset
End Function

Function GetOrigin( x:Float Var, y:Float Var )
	brl.max2d.GetOrigin x,y
	x:-_xoffset
	y:-_yoffset
End Function

Function SetViewport( x:Int, y:Int, width:Int, height:Int )
	Local x2:Float=Min(x+width,_xoffset+_vwidth)
	Local y2:Float=Min(y+height,_yoffset+_vheight)
	x=Max(x,_xoffset)
	y=Max(y,_yoffset)
	brl.max2d.SetViewport x,y,x2-x,y2-y
End Function

Function GetViewport( x:Int Var,y:Int Var,width:Int Var,height:Int Var )
	brl.max2d.GetViewport x,y,width,height
	x:-_xoffset
	y:-_yoffset
End Function

Function VirtualMouseX:Float()
	Return brl.max2d.VirtualMouseX()-_xoffset
End Function

Function VirtualMouseY:Float()
	Return brl.max2d.VirtualMouseY()-_yoffset
End Function

Function VirtualResolutionWidth:Float()
	Return _vwidth
End Function

Function VirtualResolutionHeight:Float()
	Return _vheight
End Function

'-----------------------
'Test code
'-----------------------
SeedRnd MilliSecs()
'Graphics Rand(320,DesktopWidth()),Rand(240,DesktopHeight())

Local Window:TGadget = CreateWindow("Test", 0, 0, Rand(320,DesktopWidth()), Rand(240,DesktopHeight()), Null, WINDOW_DEFAULT|WINDOW_CENTER)
Local canvas:TGadget = CreateCanvas(0, 0, ClientWidth(Window), ClientHeight(Window), Window)

CreateTimer 60
SetGraphics CanvasGraphics(canvas)


SetBorderColor 32,32,32
SetClsColor 223,223,223
SetColor 255,127,0

SetVirtualResolution 320,240,ASPECT_LETTERBOX_BORDER


While WaitEvent()
	Select EventID()
		Case EVENT_TIMERTICK
			RedrawGadget canvas
		Case EVENT_GADGETPAINT
			SetGraphics CanvasGraphics(canvas)
			Cls
			DrawText "Aspect correction demo",5,5
			DrawText VirtualMouseX()+","+VirtualMouseY(),VirtualMouseX(),VirtualMouseY()
			Flip
	EndSelect
	
	If KeyHit(KEY_1) Then SwitchAspectMode ASPECT_STRETCH
	If KeyHit(KEY_2) Then SwitchAspectMode ASPECT_LETTERBOX_FILL
	If KeyHit(KEY_3) Then SwitchAspectMode ASPECT_LETTERBOX_BORDER
	If KeyHit(KEY_4) Then SwitchAspectMode ASPECT_BESTFIT_FILL
	If KeyHit(KEY_5) Then SwitchAspectMode ASPECT_BESTFIT_BORDER
Wend
</textarea><br><br>any idea? <br><br></td></tr></table><br>
<a name="1305095"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chapman7</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> I got it<br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'Code: David 'Oddball' Williamson
'6th May 2010
'Enjoy!

SuperStrict

Import MaxGUI.Drivers

Const ASPECT_STRETCH:Int=0
Const ASPECT_LETTERBOX_FILL:Int=2
Const ASPECT_LETTERBOX_BORDER:Int=3
Const ASPECT_BESTFIT_FILL:Int=4
Const ASPECT_BESTFIT_BORDER:Int=5	'Bestfit border may not work with all drivers(i.e. DirectX)

Local ModeSet:Int = 3

Private

Global _xoffset:Float
Global _yoffset:Float
Global _vwidth:Float
Global _vheight:Float
Global _mode:Int=0
Global _bred:Int=0
Global _bgreen:Int=0
Global _bblue:Int=0

Public

Function SetVirtualResolution( width:Float, height:Float, mode:Int=ASPECT_STRETCH )
	If mode=ASPECT_STRETCH
		_xoffset=0;_yoffset=0;_vwidth=width;_vheight=height;_mode=mode
		brl.max2d.SetVirtualResolution(width,height)
		brl.max2d.SetViewport _xoffset,_yoffset,_vwidth,_vheight
		brl.max2d.SetOrigin _xoffset,_yoffset
		Return
	EndIf
	Local gwidth:Int=GraphicsWidth()
	Local gheight:Int=GraphicsHeight()
	_vwidth=width
	_vheight=height
	_mode=mode
	Local wratio:Float=gwidth/_vwidth
	Local hratio:Float=gheight/_vheight
	If wratio&lt;hratio Then hratio=wratio
	If _mode&amp;4
		hratio=Floor(hratio)
		hratio=Max(hratio,1)
	EndIf
	height=gheight/hratio
	width=gwidth/hratio
	
	_xoffset=(width-_vwidth)*.5
	_yoffset=(height-_vheight)*.5
	If _mode&amp;4
		_xoffset=Floor(_xoffset)
		_yoffset=Floor(_yoffset)
	EndIf
	brl.max2d.SetVirtualResolution(width,height)
	
	If _mode&amp;1
		Local red:Int,green:Int,blue:Int
		GetClsColor red,green,blue
		SetClsColor _bred,_bgreen,_bblue
		Cls
		SetClsColor red,green,blue
		brl.max2d.SetViewport _xoffset,_yoffset,_vwidth,_vheight
	Else
		brl.max2d.SetViewport 0,0,brl.max2d.VirtualResolutionWidth(),brl.max2d.VirtualResolutionHeight()
	EndIf
	
	brl.max2d.SetOrigin _xoffset,_yoffset
	
End Function

Function SetBorderColor( red:Int, green:Int, blue:Int )
	_bred=red
	_bgreen=green
	_bblue=blue
End Function

Function SwitchAspectMode( mode:Int )
	SetVirtualResolution _vwidth,_vheight,mode
End Function

Function Flip( sync:Int=-1 )
	brl.Graphics.Flip sync
	If _mode&amp;1
		brl.max2d.SetViewport 0,0,brl.max2d.VirtualResolutionWidth(),brl.max2d.VirtualResolutionHeight()
		Local red:Int,green:Int,blue:Int
		GetClsColor red,green,blue
		SetClsColor _bred,_bgreen,_bblue
		Cls
		SetClsColor red,green,blue
		brl.max2d.SetViewport _xoffset,_yoffset,_vwidth,_vheight
	EndIf
End Function

Function SetOrigin( x:Float, y:Float )
	brl.max2d.SetOrigin x+_xoffset,y+_yoffset
End Function

Function GetOrigin( x:Float Var, y:Float Var )
	brl.max2d.GetOrigin x,y
	x:-_xoffset
	y:-_yoffset
End Function

Function SetViewport( x:Int, y:Int, width:Int, height:Int )
	Local x2:Float=Min(x+width,_xoffset+_vwidth)
	Local y2:Float=Min(y+height,_yoffset+_vheight)
	x=Max(x,_xoffset)
	y=Max(y,_yoffset)
	brl.max2d.SetViewport x,y,x2-x,y2-y
End Function

Function GetViewport( x:Int Var,y:Int Var,width:Int Var,height:Int Var )
	brl.max2d.GetViewport x,y,width,height
	x:-_xoffset
	y:-_yoffset
End Function

Function VirtualMouseX:Float()
	Return brl.max2d.VirtualMouseX()-_xoffset
End Function

Function VirtualMouseY:Float()
	Return brl.max2d.VirtualMouseY()-_yoffset
End Function

Function VirtualResolutionWidth:Float()
	Return _vwidth
End Function

Function VirtualResolutionHeight:Float()
	Return _vheight
End Function

'-----------------------
'Test code
'-----------------------
SeedRnd MilliSecs()
'Graphics Rand(320,DesktopWidth()),Rand(240,DesktopHeight())
SetGraphicsDriver(GLMax2DDriver())

Local Window:TGadget = CreateWindow("Test", 0, 0, Rand(320,DesktopWidth()), Rand(240,DesktopHeight()), Null, WINDOW_DEFAULT|WINDOW_CENTER)
Local canvas:TGadget = CreateCanvas(0, 0, ClientWidth(Window), ClientHeight(Window), Window)
SetGadgetLayout(Canvas, 1, 1, 1, 1)

CreateTimer 60
SetGraphics CanvasGraphics(canvas)


SetBorderColor 32,32,32
SetClsColor 223,223,223
SetColor 255,127,0

SetVirtualResolution 320,240,ModeSet 

EnablePolledInput()
While WaitEvent()
	Select EventID()
		Case EVENT_TIMERTICK
			RedrawGadget canvas
		Case EVENT_GADGETPAINT
			SetGraphics CanvasGraphics(canvas)
			SetVirtualResolution 320,240,ModeSet 
			Cls
			DrawText "Aspect correction demo",5,5
			DrawText VirtualMouseX()+","+VirtualMouseY(),VirtualMouseX(),VirtualMouseY()
			Flip
		Case EVENT_KEYDOWN
			If KeyHit(KEY_1) Then ModeSet = 1
			If KeyHit(KEY_2) Then ModeSet = 2
			If KeyHit(KEY_3) Then ModeSet = 3
			If KeyHit(KEY_4) Then ModeSet = 4
			If KeyHit(KEY_5) Then ModeSet = 5
			SwitchAspectMode ModeSet
	EndSelect
	

Wend

</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
