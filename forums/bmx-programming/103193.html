<!DOCTYPE html><html lang="en" ><head ><title >Help with shader-based GLMax2D</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Help with shader-based GLMax2D</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Help with shader-based GLMax2D</a><br><br>
<a name="1240316"></a>

<a name="1240420"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hallo :o)<br><br>Here's what I currently have in the way of a shader-based GLMax2D module (which I've called GL2GLMax2D).<br><br>I know there are some rather talented individuals around who know their stuff, and I would appreciate some help getting it working with textures and the other drawing commands.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Strict

Rem
bbdoc: Graphics/OpenGL 2+ Max2D
about:
The OpenGL Max2D module provides an OpenGL 2+ driver for #Max2D with shader support.
End Rem
Module BRL.GL2Max2D

ModuleInfo "Version: 1.00"
ModuleInfo "Author: Mark Sibly, Bruce Henderson, Emil Andersson"
ModuleInfo "License: zlib/libpng"
ModuleInfo "Copyright: Blitz Research Ltd"

ModuleInfo "History: 1.00"
ModuleInfo "History: Initial version."

Import BRL.Max2D
?Not linuxarm
Import BRL.GLGraphics
Import pub.glew
?linuxarm
Import BRL.EGLGraphics
?
Import brl.standardio
Private

Global _driver:TGL2Max2DDriver

'Naughty!
Const GL_BGR=$80E0
Const GL_BGRA=$80E1
Const GL_CLAMP_TO_EDGE=$812F
Const GL_CLAMP_TO_BORDER=$812D

Global ix#,iy#,jx#,jy#
Global color4ub:Byte[4]

Global state_blend
Global state_boundtex
Global state_texenabled

Function BindTex( name )
	If name=state_boundtex Return
	glActiveTexture GL_TEXTURE0
	glBindTexture GL_TEXTURE_2D,name
	state_boundtex=name
End Function

Function EnableTex( name )
	BindTex name
	If state_texenabled Return
	glEnable GL_TEXTURE_2D
	state_texenabled=True
End Function

Function DisableTex()
	If Not state_texenabled Return
	glDisable GL_TEXTURE_2D
	state_texenabled=False
End Function

Function Pow2Size( n )
	Local t=1
	While t&lt;n
		t:*2
	Wend
	Return t
End Function

Global dead_texs[],n_dead_texs,dead_tex_seq

'Enqueues a texture for deletion, to prevent release textures on wrong thread.
'
'Not thread safe, but that's OK because all threads are stopped when TGLImageFrame.Delete()
'is called, which is what calls us.
'
Function DeleteTex( name,seq )

	If seq&lt;&gt;dead_tex_seq Return

	'add tex to queue
	If dead_texs.length=n_dead_texs
		dead_texs=dead_texs[..n_dead_texs+10]
	EndIf
	dead_texs[n_dead_texs]=name
	n_dead_texs:+1
	'
End Function

Function CreateTex( width,height,flags )
	'alloc new tex
	Local name
	glGenTextures 1,Varptr name
	
	'flush dead texs
	If dead_tex_seq=GraphicsSeq
		For Local i=0 Until n_dead_texs
			glDeleteTextures 1,Varptr dead_texs[i]
		Next
	EndIf
	n_dead_texs=0
	dead_tex_seq=GraphicsSeq

	'bind new tex
	BindTex name
	
	'set texture parameters
	glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_WRAP_S,GL_CLAMP_TO_EDGE
	glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_WRAP_T,GL_CLAMP_TO_EDGE
	
	If flags &amp; FILTEREDIMAGE
		glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_MAG_FILTER,GL_LINEAR
		If flags &amp; MIPMAPPEDIMAGE
			glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_MIN_FILTER,GL_LINEAR_MIPMAP_LINEAR
		Else
			glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_MIN_FILTER,GL_LINEAR
		EndIf
	Else
		glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_MAG_FILTER,GL_NEAREST
		If flags &amp; MIPMAPPEDIMAGE
			glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_MIN_FILTER,GL_NEAREST_MIPMAP_NEAREST
		Else
			glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_MIN_FILTER,GL_NEAREST
		EndIf
	EndIf

	Local mip_level

	Repeat
		glTexImage2D GL_TEXTURE_2D,mip_level,GL_RGBA,width,height,0,GL_RGBA,GL_UNSIGNED_BYTE,Null
		If Not (flags &amp; MIPMAPPEDIMAGE) Exit
		If width=1 And height=1 Exit
		If width&gt;1 width:/2
		If height&gt;1 height:/2
		mip_level:+1
	Forever

	Return name
End Function

Function UploadTex( pixmap:TPixmap,flags )
	Local mip_level
?Not linuxarm
	Repeat
		glPixelStorei GL_UNPACK_ROW_LENGTH,pixmap.pitch/BytesPerPixel[pixmap.format]
		glTexSubImage2D GL_TEXTURE_2D,mip_level,0,0,pixmap.width,pixmap.height,GL_RGBA,GL_UNSIGNED_BYTE,pixmap.pixels
		If Not (flags &amp; MIPMAPPEDIMAGE) Exit
		If pixmap.width&gt;1 And pixmap.height&gt;1
			pixmap=ResizePixmap( pixmap,pixmap.width/2,pixmap.height/2 )
		Else If pixmap.width&gt;1
			pixmap=ResizePixmap( pixmap,pixmap.width/2,pixmap.height )
		Else If pixmap.height&gt;1
			pixmap=ResizePixmap( pixmap,pixmap.width,pixmap.height/2 )
		Else
			Exit
		EndIf
		mip_level:+1
	Forever
	glPixelStorei GL_UNPACK_ROW_LENGTH,0
?
End Function

Function AdjustTexSize( width Var,height Var )
	'calc texture size
	width=Pow2Size( width )
	height=Pow2Size( height )
	Repeat
		Local t
		glTexImage2D GL_TEXTURE_2D,0,4,width,height,0,GL_RGBA,GL_UNSIGNED_BYTE,Null
?Not linuxarm
		glGetTexLevelParameteriv GL_TEXTURE_2D,0,GL_TEXTURE_WIDTH,Varptr t
?
		If t Return
		If width=1 And height=1 RuntimeError "Unable to calculate tex size"
		If width&gt;1 width:/2
		If height&gt;1 height:/2
	Forever
End Function

Function DefaultVShaderSource:String()

	Local str:String=""

?linuxarm	
	str:+"#version 100~n"
?Not linuxarm
	str:+"#version 120~n"
?
	str:+"attribute vec2 vertex_pos;~n"
	str:+"attribute vec4 vertex_col;~n"
	str:+"varying vec4 f_col;~n"
	
	str:+"uniform mat4 pmatrix;~n"
	str:+"void main(void) {~n"
	str:+"	gl_Position=pmatrix*vec4(vertex_pos, -1.0, 1.0);~n"
	str:+"	f_col=vertex_col;~n"
	str:+"}"
	
	Return str

End Function

Function DefaultFShaderSource:String()

	Local str:String=""
	
?linuxarm	
	str:+"#version 100~n"
	str:+"precision mediump float;~n"
	str:+"varying vec4 f_col;~n"
	str:+"void main(void) {~n"
	str:+"	gl_FragColor=vec4(f_col);~n"
	str:+"}~n"
?Not linuxarm
	str:+"#version 120~n"
	str:+"varying vec4 f_col;~n"
	str:+"void main(void) {~n"
	str:+"	gl_FragColor=f_col;~n"
	str:+"}~n"
?
	
	Return str

End Function

Function DefaultTextureVShaderSource:String()

	Local str:String=""

?linuxarm	
	str:+"#version 100~n"
?Not linuxarm
	str:+"#version 120~n"
?
	str:+"attribute vec2 vertex_pos;~n"
	str:+"attribute vec2 vertex_uv;~n"
	str:+"varying vec2 vertex_texcoord;~n"
	
	str:+"uniform mat4 pmatrix;~n"
	str:+"void main(void) {~n"
	str:+"	gl_Position=pmatrix*vec4(vertex_pos, -1.0, 1.0);~n"
	str:+"	vertex_texcoord = vertex_uv;~n"
	str:+"}"
	
	Return str
End Function

Function DefaultTextureFShaderSource:String()

	Local str:String=""
	
?linuxarm	
	str:+"#version 100~n"
	str:+"precision mediump float;~n"
	str:+"uniform sampler2D u_texture;~n"
	str:+"varying vec2 vertex_texcoord;~n"
	str:+"void main(void) {~n"
	str:+"	gl_FragColor=texture2D(u_texture, vertex_texcoord);~n"
	str:+"}~n"
?Not linuxarm
	str:+"#version 120~n"
	str:+"uniform sampler2D u_texture;~n"
	str:+"varying vec2 vertex_texcoord;~n"
	str:+"void main(void) {~n"
	str:+"	gl_FragColor=texture2D(u_texture, vertex_texcoord);~n"
	str:+"}~n"
?
	
	Return str
End Function

Public

Type TGLImageFrame Extends TImageFrame

	Field u0#,v0#,u1#,v1#,uscale#,vscale#

	Field name,seq
	
	Method New()
		seq=GraphicsSeq
	End Method
	
	Method Delete()
		If Not seq Return
		DeleteTex name,seq
		seq=0
	End Method
	
	Method Draw( x0#,y0#,x1#,y1#,tx#,ty#,sx#,sy#,sw#,sh# )
		Assert seq=GraphicsSeq Else "Image does not exist"
Print "Draw"
		Local u0#=sx * uscale
		Local v0#=sy * vscale
		Local u1#=(sx+sw) * uscale
		Local v1#=(sy+sh) * vscale
		
		EnableTex name
'		glBegin GL_QUADS
'		glTexCoord2f u0,v0
'		glVertex2f x0*ix+y0*iy+tx,x0*jx+y0*jy+ty
'		glTexCoord2f u1,v0
'		glVertex2f x1*ix+y0*iy+tx,x1*jx+y0*jy+ty
'		glTexCoord2f u1,v1
'		glVertex2f x1*ix+y1*iy+tx,x1*jx+y1*jy+ty
'		glTexCoord2f u0,v1
'		glVertex2f x0*ix+y1*iy+tx,x0*jx+y1*jy+ty
'		glEnd

	_driver.DrawTexture(u0, v0, u1, v1, x0, y0, x1, y1, tx, ty)
Rem
		Local in:Int = index * 4

		uv_array[in] = u0
		uv_array[in + 1] = v0
		uv_array[in + 2] = u1
		uv_array[in + 3] = v0
		uv_array[in + 4] = u1
		uv_array[in + 5] = v1
		uv_array[in + 6] = u0
		uv_array[in + 7] = v1
		
		vert_array[in]= x0*ix+y0*iy+tx
		vert_array[in+1]= x0*jx+y0*jy+ty
		vert_array[in+2]= x1*ix+y0*iy+tx
		vert_array[in+3]= x1*jx+y0*jy+ty
		vert_array[in+4]= x1*ix+y1*iy+tx
		vert_array[in+5]= x1*jx+y1*jy+ty
		vert_array[in+6]= x0*ix+y1*iy+tx
		vert_array[in+7]= x0*jx+y1*jy+ty
End Rem
	End Method
	
	Function CreateFromPixmap:TGLImageFrame( src:TPixmap,flags )
		'determine tex size
		Local tex_w=src.width
		Local tex_h=src.height
		AdjustTexSize tex_w,tex_h
		
		'make sure pixmap fits texture
		Local width=Min( src.width,tex_w )
		Local height=Min( src.height,tex_h )
		If src.width&lt;&gt;width Or src.height&lt;&gt;height src=ResizePixmap( src,width,height )

		'create texture pixmap
		Local tex:TPixmap=src
		
		'"smear" right/bottom edges if necessary
		If width&lt;tex_w Or height&lt;tex_h
			tex=TPixmap.Create( tex_w,tex_h,PF_RGBA8888 )
			tex.Paste src,0,0
			If width&lt;tex_w
				tex.Paste src.Window( width-1,0,1,height ),width,0
			EndIf
			If height&lt;tex_h
				tex.Paste src.Window( 0,height-1,width,1 ),0,height
				If width&lt;tex_w 
					tex.Paste src.Window( width-1,height-1,1,1 ),width,height
				EndIf
			EndIf
		Else
			If tex.format&lt;&gt;PF_RGBA8888 tex=tex.Convert( PF_RGBA8888 )
		EndIf
		
		'create tex
		Local name=CreateTex( tex_w,tex_h,flags )
		
		'upload it
		UploadTex tex,flags

		'done!
		Local frame:TGLImageFrame=New TGLImageFrame
		frame.name=name
		frame.uscale=1.0/tex_w
		frame.vscale=1.0/tex_h
		frame.u1=width * frame.uscale
		frame.v1=height * frame.vscale
		Return frame

	End Function

End Type

Type TGL2Max2DDriver Extends TMax2DDriver

	Const BATCHSIZE:Int = 65536 ' how many entries that can be stored in batch before a draw call is required

	Field vert_array:Float Ptr = Float Ptr(MemAlloc(4 * BATCHSIZE*3))
	Field uv_array:Float Ptr = Float Ptr(MemAlloc(4 * BATCHSIZE*2))
	Field col_array:Float Ptr = Float Ptr(MemAlloc(4 * BATCHSIZE*4))

	' constants for primitive rendering
	
	Const PRIMITIVE_PLAIN_TRIANGLE:Int=1
	Const PRIMITIVE_DOT:Int=2
	Const PRIMITIVE_LINE:Int=3
	Const PRIMITIVE_IMAGE:Int=4
	Const PRIMITIVE_TRIANGLE_FAN:Int=5
	Const PRIMITIVE_TRIANGLE_STRIP:Int=6

	Field pmatrix:TMatrix
	
	Field vert_buffer:Int
	Field uv_buffer:Int
	Field col_buffer:Int
	Field element_buffer:Int

	' current color for drawing
	
	Field red:Float
	Field green:Float
	Field blue:Float
	Field alpha:Float
	
	' current z layer for drawing
	
	Field layer:Float

	'Field active:Int
	'Field plain:Int
	'Field texture:Int
	
	Field index:Int
	
	Field element_array:Int[BATCHSIZE*2]
	Field element_index:Int
	
	Field primitive:Int
	Field texture_id:Int
	
	Field inited:Int

	Field defaultVShader:TGLSLShader
	Field defaultFShader:TGLSLShader
	Field defaultProgram:TGLSLProgram
	Field activeProgram:TGLSLProgram

	Field defaultTextureVShader:TGLSLShader
	Field defaultTextureFShader:TGLSLShader
	Field defaultTextureProgram:TGLSLProgram

	Method Create:TGL2Max2DDriver()
?Not linuxarm
		If Not GLGraphicsDriver() Return Null
?linuxarm
		If Not EGLGraphicsDriver() Return Null
?		
		Return Self
	End Method

	'graphics driver overrides
	Method GraphicsModes:TGraphicsMode[]()
?Not linuxarm
		Return GLGraphicsDriver().GraphicsModes()
?linuxarm
		Return EGLGraphicsDriver().GraphicsModes()
?		
	End Method
	
	Method AttachGraphics:TMax2DGraphics( widget,flags )
?Not linuxarm
		Local g:TGLGraphics=GLGraphicsDriver().AttachGraphics( widget,flags )
?linuxarm
		Local g:TEGLGraphics=EGLGraphicsDriver().AttachGraphics( widget,flags )
?		
		If g Return TMax2DGraphics.Create( g,Self )
	End Method
	
	Method CreateGraphics:TMax2DGraphics( width,height,depth,hertz,flags )
?Not linuxarm
		Local g:TGLGraphics=GLGraphicsDriver().CreateGraphics( width,height,depth,hertz,flags )
?linuxarm
		Local g:TEGLGraphics=EGLGraphicsDriver().CreateGraphics( width,height,depth,hertz,flags )
?		
		If g Return TMax2DGraphics.Create( g,Self )
	End Method
	
	Method SetGraphics( g:TGraphics )
		If Not g
			TMax2DGraphics.ClearCurrent
?Not linuxarm
			GLGraphicsDriver().SetGraphics Null
?linuxarm
			EGLGraphicsDriver().SetGraphics Null
?
			Return
		EndIf
	
		Local t:TMax2DGraphics=TMax2DGraphics(g)
?Not linuxarm
		Assert t And TGLGraphics( t._graphics )
?

?Not linuxarm
		GLGraphicsDriver().SetGraphics t._graphics
?linuxarm
		EGLGraphicsDriver().SetGraphics t._graphics
?
		ResetGLContext t
		
		t.MakeCurrent
	End Method
	
	Method ResetGLContext( g:TGraphics )

		Local gw,gh,gd,gr,gf
		g.GetSettings gw,gh,gd,gr,gf

		If Not inited Then
			Init()
			inited = True
		End If

		state_blend=0
		state_boundtex=0
		state_texenabled=0
		glDisable GL_TEXTURE_2D
'		glMatrixMode GL_PROJECTION
'		glLoadIdentity
'		glOrtho 0,gw,gh,0,-1,1
'		glMatrixMode GL_MODELVIEW
'		glLoadIdentity
'		glViewport 0,0,gw,gh

		pmatrix=New TMatrix
		pmatrix.SetOrthographic(0, gw, 0, gh, -1, 1)

	End Method
	
	Method Init()

		?Not linuxarm
		glewinit()
		?
	
		red=1.0
		green=1.0
		blue=1.0
		alpha=1.0
		
		' set up shaders
		defaultVShader = New TGLSLShader.Create(DefaultVShaderSource(), GL_VERTEX_SHADER)
		defaultFShader = New TGLSLShader.Create(DefaultFShaderSource(), GL_FRAGMENT_SHADER)
		
		defaultProgram = New TGLSLProgram.Create(defaultVShader, defaultFShader)

		defaultTextureVShader = New TGLSLShader.Create(DefaultTextureVShaderSource(), GL_VERTEX_SHADER)
		defaultTextureFShader = New TGLSLShader.Create(DefaultTextureFShaderSource(), GL_FRAGMENT_SHADER)
		
		defaultTextureProgram = New TGLSLProgram.Create(defaultTextureVShader, defaultTextureFShader)
		
		index=0
		primitive=0
		texture_id=-1
	End Method
	
	Method FlushTest(kind:Int)
		If primitive&lt;&gt;kind Or index&gt;BATCHSIZE-256 Or texture_id&gt;-1 Then
			Flush()
			primitive = kind
		End If
	End Method
	
	Method Flush()
		Select primitive
			Case PRIMITIVE_PLAIN_TRIANGLE
				If activeProgram&lt;&gt;defaultProgram Then
					activeProgram=defaultProgram
					activeProgram.Use()
					activeProgram.UpdateLayout()
				EndIf
			Case PRIMITIVE_DOT
				If activeProgram &lt;&gt;defaultProgram Then
					activeProgram =defaultProgram
					activeProgram.Use()
					activeProgram.UpdateLayout()
				EndIf
			Case PRIMITIVE_LINE
				If activeProgram &lt;&gt;defaultProgram Then
					activeProgram =defaultProgram
					activeProgram.Use()
					activeProgram.UpdateLayout()
				EndIf
			Case PRIMITIVE_TRIANGLE_FAN
				If activeProgram &lt;&gt;defaultProgram Then
					activeProgram =defaultProgram
					activeProgram.Use()
					activeProgram.UpdateLayout()
				EndIf
		End Select

		UpdateBuffers()
		
		If activeProgram Then
			activeProgram.EnableData(vert_buffer, uv_buffer, col_buffer, pmatrix.grid)
			
			' additional tests. validate shaderprogram and buffer. shader program validation takes
			' context into consideration, so do it right before drawing
			
			activeProgram.Validate()
			
			' somewhat interesting? default framebuffer should not return any errors
			' NOTE: 36062 seems to be an erroneous error code (ie opengl returns something it shouldnt)
			'Local status:Int=glCheckFramebufferStatus(GL_FRAMEBUFFER)
			'Select status
			'Case GL_FRAMEBUFFER_COMPLETE
				'Print "valid framebuffer"
			'Default
				'Print "status: "+status
			'End Select
			
			Select primitive
			Case PRIMITIVE_PLAIN_TRIANGLE
				glDrawArrays(GL_TRIANGLES, 0, index)
			Case PRIMITIVE_DOT
				glDrawArrays(GL_POINTS, 0, index)
			Case PRIMITIVE_LINE
				glDrawArrays(GL_LINES, 0, index)
			Case PRIMITIVE_TRIANGLE_FAN
				glDrawArrays(GL_TRIANGLE_FAN, 0, index)
			Case 0,PRIMITIVE_TRIANGLE_STRIP
				glDrawArrays(GL_TRIANGLE_STRIP, 0, index)
			End Select
			
			activeProgram.DisableData()
		End If
		
		index=0
		element_index=0
	End Method
	
	Method Flip( sync )
		Flush()
?Not linuxarm
		GLGraphicsDriver().Flip sync
?linuxarm
		EGLGraphicsDriver().Flip sync
?
	End Method
	
	Method ToString$()
		Return "OpenGL"
	End Method

	Method CreateFrameFromPixmap:TGLImageFrame( pixmap:TPixmap,flags )
		Local frame:TGLImageFrame
		frame=TGLImageFrame.CreateFromPixmap( pixmap,flags )
		Return frame
	End Method

	Method SetBlend( blend )
		If blend=state_blend Return
		state_blend=blend
		Select blend
?Not linuxarm
		Case MASKBLEND
			glDisable GL_BLEND
			glEnable GL_ALPHA_TEST
			glAlphaFunc GL_GEQUAL,.5
?
		Case SOLIDBLEND
			glDisable GL_BLEND
?Not linuxarm
			glDisable GL_ALPHA_TEST
?
		Case ALPHABLEND
			glEnable GL_BLEND
			glBlendFunc GL_SRC_ALPHA,GL_ONE_MINUS_SRC_ALPHA
?Not linuxarm
			glDisable GL_ALPHA_TEST
?
		Case LIGHTBLEND
			glEnable GL_BLEND
			glBlendFunc GL_SRC_ALPHA,GL_ONE
?Not linuxarm
			glDisable GL_ALPHA_TEST
?
		Case SHADEBLEND
			glEnable GL_BLEND
			glBlendFunc GL_DST_COLOR,GL_ZERO
?Not linuxarm
			glDisable GL_ALPHA_TEST
?
		Default
			glDisable GL_BLEND
?Not linuxarm
			glDisable GL_ALPHA_TEST
?
		End Select
	End Method

	Method SetAlpha( alpha# )
?Not linuxarm
		If alpha&gt;1.0 alpha=1.0
		If alpha&lt;0.0 alpha=0.0
		color4ub[3]=alpha*255
		glColor4ubv color4ub
?
	End Method

	Method SetLineWidth( width# )
		glLineWidth width
	End Method
	
	Method SetColor( pr,pg,pb )
		red=pr/255.0
		green=pg/255.0
		blue=pb/255.0
	End Method

	Method SetClsColor( red,green,blue )
		red=Min(Max(red,0),255)
		green=Min(Max(green,0),255)
		blue=Min(Max(blue,0),255)
		glClearColor red/255.0,green/255.0,blue/255.0,1.0
	End Method
	
	Method SetViewport( x,y,w,h )
		If x=0 And y=0 And w=GraphicsWidth() And h=GraphicsHeight()
			glDisable GL_SCISSOR_TEST
		Else
			glEnable GL_SCISSOR_TEST
			glScissor x,GraphicsHeight()-y-h,w,h
		EndIf
	End Method

	Method SetTransform( xx#,xy#,yx#,yy# )
		ix=xx
		iy=xy
		jx=yx
		jy=yy
	End Method

	Method Cls()
		glClear(GL_COLOR_BUFFER_BIT|GL_DEPTH_BUFFER_BIT)
	End Method

	Method Plot( px:Float, py:Float )

		FlushTest(PRIMITIVE_DOT)
		
		Local in:Int
		
		in=index*2
		
		vert_array[in+0]=px
		vert_array[in+1]=py
		
		in=index*4
		
		col_array[in+00]=red
		col_array[in+01]=green
		col_array[in+02]=blue
		col_array[in+03]=alpha		
		
		index:+1
	End Method

	Method DrawLine( x0#,y0#,x1#,y1#,tx#,ty#)
	
		FlushTest(PRIMITIVE_LINE)
		
		Local in:Int
		
		in=index*2
		
		vert_array[in+0]=x0*ix+y0*iy+tx+.5
		vert_array[in+1]=x0*jx+y0*jy+ty+.5
		
		vert_array[in+2]=x1*ix+y1*iy+tx+.5
		vert_array[in+3]=x1*jx+y1*jy+ty+.5
		
		in=index*4
		
		col_array[in+00]=red
		col_array[in+01]=green
		col_array[in+02]=blue
		col_array[in+03]=alpha
		
		col_array[in+04]=red
		col_array[in+05]=green
		col_array[in+06]=blue
		col_array[in+07]=alpha
		
		index:+2
	End Method

	Method DrawRect( x0#,y0#,x1#,y1#,tx#,ty# )
		
		' append data to batch arrays
		
		FlushTest(PRIMITIVE_PLAIN_TRIANGLE)
		
		Local in:Int
		
		in=index*2
		
		vert_array[in+00]= x0*ix+y0*iy+tx
		vert_array[in+01]= x0*jx+y0*jy+ty
		
		vert_array[in+02]= x1*ix+y0*iy+tx
		vert_array[in+03]= x1*jx+y0*jy+ty
		
		vert_array[in+04]= x1*ix+y1*iy+tx
		vert_array[in+05]= x1*jx+y1*jy+ty
		
		vert_array[in+06]= x1*ix+y1*iy+tx
		vert_array[in+07]= x1*jx+y1*jy+ty
		
		vert_array[in+08]= x0*ix+y1*iy+tx
		vert_array[in+09]= x0*jx+y1*jy+ty
		
		vert_array[in+10]= x0*ix+y0*iy+tx
		vert_array[in+11]= x0*jx+y0*jy+ty
		
		in=index*4

		col_array[in+00]=red
		col_array[in+01]=green
		col_array[in+02]=blue
		col_array[in+03]=alpha
		
		col_array[in+04]=red
		col_array[in+05]=green
		col_array[in+06]=blue
		col_array[in+07]=alpha
		
		col_array[in+08]=red
		col_array[in+09]=green
		col_array[in+10]=blue
		col_array[in+11]=alpha
		
		col_array[in+12]=red
		col_array[in+13]=green
		col_array[in+14]=blue
		col_array[in+15]=alpha
		
		col_array[in+16]=red
		col_array[in+17]=green
		col_array[in+18]=blue
		col_array[in+19]=alpha
		
		col_array[in+20]=red
		col_array[in+21]=green
		col_array[in+22]=blue
		col_array[in+23]=alpha

		index:+6
	End Method
	
	Method DrawOval( x0#,y0#,x1#,y1#,tx#,ty# )

		FlushTest(PRIMITIVE_PLAIN_TRIANGLE)
		
		Local xr#=(x1-x0)*.5
		Local yr#=(y1-y0)*.5
		Local segs=Abs(xr)+Abs(yr)
		
		segs=Max(segs,12)&amp;~3

		x0:+xr
		y0:+yr
		
'		DisableTex
'		glBegin GL_POLYGON
'		For Local i=0 Until segs
'			Local th#=i*360#/segs
'			Local x#=x0+Cos(th)*xr
'			Local y#=y0-Sin(th)*yr
'			glVertex2f x*ix+y*iy+tx,x*jx+y*jy+ty
'		Next
'		glEnd

	
	End Method
	
	Method DrawPoly( xy#[],handle_x#,handle_y#,origin_x#,origin_y# )
		If xy.length&lt;6 Or (xy.length&amp;1) Return
		
'		DisableTex
'		glBegin GL_POLYGON
'		For Local i=0 Until Len xy Step 2
'			Local x#=xy[i+0]+handle_x
'			Local y#=xy[i+1]+handle_y
'			glVertex2f x*ix+y*iy+origin_x,x*jx+y*jy+origin_y
'		Next
'		glEnd
	End Method
		
	Method DrawPixmap( p:TPixmap,x,y )
		Local blend=state_blend
		DisableTex
		SetBlend SOLIDBLEND
	
		Local t:TPixmap=p
		If t.format&lt;&gt;PF_RGBA8888 t=ConvertPixmap( t,PF_RGBA8888 )

'		glPixelZoom 1,-1
'		glRasterPos2i 0,0
'		glBitmap 0,0,0,0,x,-y,Null
'		glPixelStorei GL_UNPACK_ROW_LENGTH, t.pitch Shr 2
'		glDrawPixels t.width,t.height,GL_RGBA,GL_UNSIGNED_BYTE,t.pixels
'		glPixelStorei GL_UNPACK_ROW_LENGTH,0
'		glPixelZoom 1,1
		
		SetBlend blend
	End Method
	
	Method DrawTexture(u0:Float, v0:Float, u1:Float, v1:Float, x0:Float, y0:Float, x1:Float, y1:Float, tx:Float, ty:Float)
	
		FlushTest(0)
		activeProgram = defaultTextureProgram
		
		Local in:Int = index * 4

		uv_array[in] = u0
		uv_array[in + 1] = v0
		uv_array[in + 2] = u1
		uv_array[in + 3] = v0
		uv_array[in + 4] = u1
		uv_array[in + 5] = v1
		uv_array[in + 6] = u0
		uv_array[in + 7] = v1
		

		in = index * 4

		vert_array[in]= x0*ix+y0*iy+tx
		vert_array[in+1]= x0*jx+y0*jy+ty

		vert_array[in+2]= x1*ix+y0*iy+tx
		vert_array[in+3]= x1*jx+y0*jy+ty

		vert_array[in+4]= x1*ix+y1*iy+tx
		vert_array[in+5]= x1*jx+y1*jy+ty

		vert_array[in+6]= x0*ix+y1*iy+tx
		vert_array[in+7]= x0*jx+y1*jy+ty

		index :+ 4
		Flush()
	End Method

	Method GrabPixmap:TPixmap( x,y,w,h )
		Local blend=state_blend
		SetBlend SOLIDBLEND
		Local p:TPixmap=CreatePixmap( w,h,PF_RGBA8888 )
		glReadPixels x,GraphicsHeight()-h-y,w,h,GL_RGBA,GL_UNSIGNED_BYTE,p.pixels
		p=YFlipPixmap( p )
		SetBlend blend
		Return p
	End Method
	
	Method SetResolution( width#,height# )
		pmatrix.SetOrthographic(0, width, 0, height, -1, 1)
	End Method

	Method UpdateBuffers()
	
		If vert_buffer=0 Then glGenBuffers(1, Varptr vert_buffer)
		If uv_buffer=0 Then glGenBuffers(1, Varptr uv_buffer)
		If col_buffer=0 Then glGenbuffers(1, Varptr col_buffer)
		If element_buffer=0 Then glGenbuffers(1, Varptr element_buffer)
		
		glBindBuffer(GL_ARRAY_BUFFER, vert_buffer)
		glBufferData(GL_ARRAY_BUFFER, index*8, vert_array, GL_DYNAMIC_DRAW)
		
		glBindBuffer(GL_ARRAY_BUFFER, uv_buffer)
		glBufferData(GL_ARRAY_BUFFER, index*12, uv_array, GL_DYNAMIC_DRAW)
		
		glBindBuffer(GL_ARRAY_BUFFER, col_buffer)
		glBufferData(GL_ARRAY_BUFFER, index*16, col_array, GL_DYNAMIC_DRAW)
		
		glBindBuffer(GL_ELEMENT_ARRAY_BUFFER, element_buffer)
		glBufferData(GL_ELEMENT_ARRAY_BUFFER, element_index*12, element_array, GL_DYNAMIC_DRAW)
			
	End Method

End Type

Type TMatrix

	Field grid:Float Ptr = Float Ptr(MemAlloc(4 * 16))
	
	Method SetOrthographic(pl:Float, pr:Float, pt:Float, pb:Float, pn:Float, pf:Float)
		LoadIdentity()
		grid[00] = 2.0/(pr-pl)
		grid[05] = 2.0/(pt-pb)
		grid[10] =-2.0/(pf-pn)
		grid[15] = 1.0
		grid[12]=-((pr+pl)/(pr-pl))
		grid[13]=-((pt+pb)/(pt-pb))
		grid[14]=-((pf+pn)/(pf-pn))
	End Method
	
	Method Clear()
		For Local i:Int=0 To 15
			grid[i]=0.0
		Next
	End Method
	
	Method LoadIdentity()
		Clear()
		grid[00]=1.0
		grid[05]=1.0
		grid[10]=1.0
		grid[15]=1.0
	End Method

End Type

Type TGLSLShader

	Field source:String
	Field kind:Int
	
	Field id:Int
	
	Method Create:TGLSLShader(source:Object, kind:Int)
		Self.kind = kind
		If Not Load(source)
			Return Null
		End If
		
		Compile()
		
		If Not id
			Return Null
		End If
		
		Return Self
	End Method
	
	Method Load:Int(source:Object)
		If String(source) Then
			Self.source = String(source)
			Return True
		End If
		
		Return False
	End Method

	Method Compile()
		
		If source="" Then
			'Print "ERROR (CompileShader) No shader source!"
			Return 0
		EndIf
		
		Select kind
		Case GL_VERTEX_SHADER
			'Print "(CompileShader) Compiling vertex shader"
		Case GL_FRAGMENT_SHADER
			'Print "(CompileShader) Compiling fragment shader"
		Default 
			'Print "(CompileShader) Invalid shader type!"
			Return 0
		End Select
		
		id=glCreateShader(kind)
		Local str:Byte Ptr=source.ToCString()
		
		glShaderSource(id, 1, Varptr str, Null)
		glCompileShader(id)
		
		MemFree str
		
		Local success:Int=0
		glGetShaderiv(id, GL_COMPILE_STATUS, Varptr success)
		
		If Not success Then
			'Print GetShaderErrorLog(id)
			Return 0
		EndIf
		
		'Print "(CompileShader) Successfully compiled shader!"
		'Return id
		
	End Method
	
	Method GetErrorLog:String(pid:Int)
	
		Local logsize:Int=0
		glGetShaderiv(pid, GL_INFO_LOG_LENGTH, Varptr logsize)
		
		Local msg:Byte[logsize]
		Local size:Int=0
		
		glGetShaderInfoLog(pid, logsize, Varptr size, Varptr msg[0])
		
		Local str:String=""
		
		For Local i:Int=0 To msg.length-1
			str:+Chr(msg[i])
		Next
		
		Return str
	
	End Method
	
End Type

Type TGLSLProgram

	Field id:Int

	Field attrib_pos:Int
	Field attrib_uv:Int
	Field attrib_col:Int

	Field unif_projection:Int
	Field unif_tex:Int

	Method Create:TGLSLProgram(pvshader:TGLSLShader, pfshader:TGLSLShader)

		If glIsShader(pvshader.id)=GL_FALSE Then 
			'Print "ERROR (CreateShaderProgram) pvshader is not a valid shader!"
			Return Null
		EndIf
		
		If glIsShader(pfshader.id)=GL_FALSE Then
			'Print "ERROR (CreateShaderProgram) pfshader is not a valid shader!"
			Return Null
		EndIf
		
		id = glCreateProgram()

		glAttachShader(id, pvshader.id)
		glAttachShader(id, pfshader.id)
		
		glLinkProgram(id)
		
		'Return id
		Return Self
		
	End Method

	Method Validate()

		If glIsProgram(id)=GL_FALSE Then
			'Print "ERROR (ValidateShaderProgram) Supplied id is not a shader program!"
			Return
		EndIf
		
		Local status:Int
		
		glValidateProgram(id)
		glGetProgramiv(id, GL_VALIDATE_STATUS, Varptr status)
		
		If status=GL_FALSE Then
			'Print "ERROR (ValidateShaderprogram) Supplied program is not valid! (in context)"
			Return
		EndIf
		
		Return
	
	End Method

	Method Use()
		glUseProgram(id)
	End Method

	Method UpdateLayout()

		If Not glIsProgram(id) Then
			'Print "(UpdateShaderLayout) Active is not a valid shader program!"
			Return
		EndIf
		
		attrib_pos=glGetAttribLocation(id, "vertex_pos")
		attrib_uv=glGetAttribLocation(id, "vertex_uv")
		attrib_col=glGetAttribLocation(id, "vertex_col")
		
		unif_projection=glGetUniformLocation(id, "pmatrix")
		unif_tex=glGetUniformLocation(id, "u_texture")
		
	End Method
	
	Method EnableData(vert_buffer:Int, uv_buffer:Int, col_buffer:Int, matrix:Float Ptr)

		If attrib_pos&gt;=0 Then
			glEnableVertexAttribArray(attrib_pos)
			glBindBuffer(GL_ARRAY_BUFFER, vert_buffer)
			glVertexAttribPointer(attrib_pos, 2, GL_FLOAT, GL_FALSE, 0, Null)
		EndIf
		
		If attrib_uv&gt;=0 Then
			glEnableVertexAttribArray(attrib_uv)
			glBindBuffer(GL_ARRAY_BUFFER, uv_buffer)
			glVertexAttribPointer(attrib_uv, 2, GL_FLOAT, GL_FALSE, 0, Null)
		EndIf
		
		If attrib_col&gt;=0 Then
			glEnableVertexAttribArray(attrib_col)
			glBindBuffer(GL_ARRAY_BUFFER, col_buffer)
			glVertexAttribPointer(attrib_col, 4, GL_FLOAT, GL_FALSE, 0, Null)
		EndIf
		
		If unif_projection&gt;=0 Then
			'glUniformMatrix4fv(unif_projection, 16, False, pmatrix.grid) ' bad
			glUniformMatrix4fv(unif_projection, 1, False, matrix)
		EndIf
		
		If unif_tex&gt;=0 Then
			glUniform1i(unif_tex, 0)
		EndIf
		
	End Method
	
	Method DisableData()

		If attrib_pos&gt;=0 Then
			glDisableVertexAttribArray(attrib_pos)
		EndIf
		
		If attrib_uv&gt;=0 Then
			glDisableVertexAttribArray(attrib_uv)
		EndIf
		
		If attrib_col&gt;=0 Then
			glDisableVertexAttribArray(attrib_col)
		EndIf
	
	End Method

End Type

Rem
bbdoc: Get OpenGL Max2D Driver
about:
The returned driver can be used with #SetGraphicsDriver to enable OpenGL Max2D 
rendering.
End Rem
Function GL2Max2DDriver:TGL2Max2DDriver()
	Global _done
	If Not _done
		_driver=New TGL2Max2DDriver.Create()
		_done=True
	EndIf
	Return _driver
End Function

Local driver:TGL2Max2DDriver=GL2Max2DDriver()
If driver SetGraphicsDriver driver
</textarea><br><br>Thanks! <br><br></td></tr></table><br>
<a name="1240397"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LT</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for posting this.  I won't have a chance to look at in depth until tomorrow evening, but I'm definitely motivated to do so.  :) <br><br></td></tr></table><br>
<a name="1240399"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> It's a bit of a mess. I usually tidy up once everything is working properly (and tend to refactor a lot too).<br><br>At the moment I see the texture drawing as a single thing, but I dunno if it is at all possible to batch them too?<br>Anything we can batch would be more efficient. As you can see, the plot and drawlines are effectively batched. <br><br></td></tr></table><br>
<a name="1240404"></a>

<a name="1240405"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ideally non WRAP textures need to be arranged on an Atlas but BlitzMax game authors do that better than engines...<br><br>BlitzMax DX modules feature batching code that could be integrated, they batch between texture state changes in the form of large vertex buffers. <br><br>I plan to come back to this thread in the weekend cos I like:) <br><br></td></tr></table><br>
<a name="1240406"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GfK</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> I plan to come back to this thread in the weekend cos I like:)  <br></div>Music to my technically incompetent ears. <br><br></td></tr></table><br>
<a name="1240407"></a>

<a name="1240408"></a>

<a name="1240409"></a>

<a name="1240411"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LT</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> It might help to know what is happening for you.  Right now, I can't see it doing anything.  For example, should line draws be working?<br><br>I only spent about ten minutes, but I compiled the module and then attempted to test it with the BreakOut game and with my own engine as a replacement for GLMax2D.  Of course, BreakOut uses textures so I wasn't <i>expecting</i> to see anything and that's just what happened - a more or less blank screen.<br><br>I was actually expecting my engine to work, though, because the only part of GLMax2D that I'm using is the DrawLine functionality.  The result was similar to BreakOut, which tells me that the GLGraphics context is somehow different.<br><br>EDIT: I'm also using shaders and have that set up in a different place, so I suppose they could also be interfering with each other. <br><br></td></tr></table><br>
<a name="1240414"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LT</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> By the way, I had to change...<br><br><pre class=code>Method AttachGraphics:TMax2DGraphics( widget:Byte Ptr, flags )</pre>to...<pre class=code>Method AttachGraphics:TMax2DGraphics( widget, flags )</pre><br>In order to get it to compile at all.  Otherwise, it gives me an "overriding method parameter type" error.<br><br>Is there something else I'm missing here? <br><br></td></tr></table><br>
<a name="1240419"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote">  Is there something else I'm missing here? <br></div><br>The example code was set to build against bmx_ng. "widget" is really a pointer, and has to be declared as such to support 64-bit apps.<br><br>I'll change the example here as everyone is using the "official" BlitzMax at the moment ;-) <br><br></td></tr></table><br>
<a name="1240441"></a>

<a name="1240442"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >zzz</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> I posted the initial module hack I did in my old thread: <a href="http://www.blitzbasic.com/Community/post.php?topic=102876&amp;post=1240436" target="_blank">http://www.blitzbasic.com/Community/post.php?topic=102876&amp;post=1240436</a><br><br>Might or might not be work looking at :) <br><br></td></tr></table><br>
<a name="1240486"></a>

<a name="1240487"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> You did some more work on it? <br><br></td></tr></table><br>
<a name="1240491"></a>

<a name="1240495"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamStrange</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Brucey - compiled fine here on mac<br>do you have some test files for it. I can do some optimising first though :)<br><br>I can also do some opengl and add a number of functions as well? <br><br></td></tr></table><br>
<a name="1240504"></a>

<a name="1240505"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Compiled here too.<br><br>drawing a rectangle + "rectangle numbers" (the fps :D)<br><br>gl 2900<br>gl2: 2600<br><br>Not that these numbers count, but you see that it is "just" about 10% slower. When rendering 100 rectangles at random positions with random colors, it decreases to:<br>gl: 1870<br>gl2: 1750<br><br>So batching is already used? And if yes: is it slower because of using shaders?<br><br><br>Hmpf again something I cannot be a real helping hand. <br><br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="1240507"></a>

<a name="1240508"></a>

<a name="1240509"></a>

<a name="1240510"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> To make "pixmaps" get drawn on my system, I replaced just one line:<br><br><br><pre class=code>
	Method DrawPixmap( p:TPixmap,x,y )
...
		glRasterPos2i 0,0   'old
		glRasterPos2i -1,1   'new
</pre><br><br>This made pixmaps get drawn at the right spot, somehow raster "0,0" means centered at the screen, instead of bottom-left, so we moved to the left, and to the "top" - i think so.<br><br>When not using "SetBlend SOLIDBLEND" you can even ALPHABLEND Pixmaps (with alpha channels). Somehow I think the intention is to replace  that whole drawPixmap-part with something "shader"-ish.<br><br><br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="1240520"></a>

<a name="1240527"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >zzz</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Brucey<br>Not really, its the same thing I mailed you ages ago. It do have working textures (for me at least), so Ill see if I can figure out what is breaking it for other people.<br><br>Btw, the batching example should do image batching just fine (if textures starts to work that is). Just have a new texture id trigger a flush. <br><br></td></tr></table><br>
<a name="1240561"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> drawing a rectangle + "rectangle numbers" (the fps :D) <br></div><br>And what difference do you get when you draw 500 rectangles per frame? <br><br></td></tr></table><br>
<a name="1240567"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>
rects	gl		gl2
@10     2800    2510
@100    1390    1300
@1000   200     200
@10000  22      22
@50000  5-6     5
</pre><br><br>Seems it takes a while until the batching-overhead is no longer an issue. Maybe this changes with more realistic scenarios (textured quads).<br><br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="1240588"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LT</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> Found the problem.  UpdateLayout() and Use() are never getting called by the DrawTexture() method.  It should start like this...<br><pre class=code>
Method DrawTexture( u0:Float, v0:Float, u1:Float, v1:Float, x0:Float, y0:Float, x1:Float, y1:Float, tx:Float, ty:Float )

  FlushTest( 0 )
  activeProgram = defaultTextureProgram
  activeProgram.Use()
  activeProgram.UpdateLayout()
</pre><br>Looks like there are some issues with the layout of the verts, but otherwise textures are working fine. <br><br></td></tr></table><br>
<a name="1240590"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cool :-)<br><br><div class="quote">  Found the problem. <br></div><br>This is what happens when I just type randomly at the keyboard... (on account of not really knowing what I'm doing) <br><br></td></tr></table><br>
<a name="1240596"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> @LT<br>What is your change supposed to do (just for the personal matter of being interested). Should it fix some image drawing issues (they are drawn fine here before and after adding your changes).<br><br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="1240601"></a>

<a name="1240602"></a>

<a name="1240603"></a>

<a name="1240604"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LT</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> I was testing DrawText, which uses Draw, which uses DrawTexture.  Without the change, DrawText doesn't work.  I was also testing it with the BreakOut sample.  Before the change, nothing draws.  After the change, it works but the vertices are in the wrong order, so that's a separate issue (but easily fixed).<br><br>Draw image does not appear to be using shaders at all, btw. <br><br></td></tr></table><br>
<a name="1240638"></a>

<a name="1240639"></a>

<a name="1240640"></a>

<a name="1240641"></a>

<a name="1240642"></a>

<a name="1240643"></a>

<a name="1240644"></a>

<a name="1240645"></a>

<a name="1240646"></a>

<a name="1240647"></a>

<a name="1240648"></a>

<a name="1240651"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ahh ok ... I assumed "Type TGLImageFrame Extends TImageFrame" automagically takes care of "DrawImage" ...<br><br><br>LoadImage -&gt; TImage.Load -&gt; TImage.Create (which calls "New TImageFrame")<br><br>In our case we use "gl2max2d" - which has "TGLImageFrame" as extension of TImageFrame (needed because "draw" is abstract in TImageFrame).<br><br>This TGLImageFrame uses "_driver.DrawTexture(...)" to draw itself.<br><br><br>When calling "DrawImage" this is done:<br>DrawImage -&gt; ImageFrame.Draw -&gt; _driver.DrawTexture()<br><br>This means that "DrawImage" calls the "DrawTexture()"-method you appended that two lines (you could also uncomment that "print draw" and include a DrawImage in your loop) .<br><br>Hmm did I overlook something?<br><br><br>But like said: I got images displayed before and after your changes.<br>But I have to admit: you are right that displaying "fonts" does not work at all without your lines - using them the "basic system font" looks kind of garbled (missing parts).<br><br><br>EDIT: somehow when removing your edits .. it no longer displays the image (which did before... hmpff I surely mixed something before so it used "vanilla code") .<br><br>EDIT2: Now my image is also only displayed like a "pacman" (like if a piece of a pie is missing). Seems your vertice-assumption is correct.<br><br>EDIT3: to save having a local image:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
		local glyphX:int = 0, glyphY:int = 0
		for local glyph:TImageGlyph = EachIn GetImageFont()._glyphs
			if glyphX + 10 &gt;= GraphicsWidth()
				glyphX = 0
				glyphY :+ 15
			endif
			DrawImage(glyph._image, glyphX, glyphY)
			glyphX :+ 10
		Next
</textarea><br><br><br>EDIT4:<br>Ok, so "DrawTexture" does two things (next to the shaders):<br>- assigns UV coordinates (topleft, topright, bottomright, bottomlleft. values are scaled - mabye because of "power of two" textures)<br>- assigns screen position of 4 vertices (topleft, topright, bottomright, bottomleft)<br><br><br>-&gt; uv values seem correct<br>-&gt; vertice position seems right, uv too - but as it does not draw 2 triangles but something else... I assume something is wrong - eg vertice order (order for two triangles should be v0, v1, v2, then v2, v1, v3)<br><br>But I think it is more a problem of "glDrawArrays(GL_TRIANGLE_STRIP, 0, index)" - which is used to do degenerated quads - for sprite batching or so.<br><br>When playing a bit with the UV values (just set them to 0,0 or screenwidth,0 etc.) you will see that there are 3 triangles each getting the same "uv texture" - but of course filling each of the triangles individually).<br><br>EDIT5: Ok, you need to adjust uv AND vertices. Will post the change later.<br><br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="1240653"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> Replace DrawTexture with this one to make images (includes Imagefonts)  work<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

	Method DrawTexture(u0:Float, v0:Float, u1:Float, v1:Float, x0:Float, y0:Float, x1:Float, y1:Float, tx:Float, ty:Float)
	
		FlushTest( 0 )
		activeProgram = defaultTextureProgram
		activeProgram.Use()
		activeProgram.UpdateLayout()	

		
		Local in:Int = index * 4
		'with "top being 0:"
		rem
		uv_array[in + 0] = u0		'topleft x
		uv_array[in + 1] = v1		'topleft y
		uv_array[in + 2] = u1		'topright x
		uv_array[in + 3] = v1		'topright y
		uv_array[in + 4] = u0		'bottomleft x
		uv_array[in + 5] = v0		'bottomleft y
		uv_array[in + 6] = u1		'bottomright x
		uv_array[in + 7] = v0		'bottomright y
		endrem
		
		'with "top being DeviceHeight"
		uv_array[in + 0] = u0		'topleft x
		uv_array[in + 1] = v0		'topleft y
		uv_array[in + 2] = u1		'topright x
		uv_array[in + 3] = v0		'topright y
		uv_array[in + 4] = u0		'bottomleft x
		uv_array[in + 5] = v1		'bottomleft y
		uv_array[in + 6] = u1		'bottomright x
		uv_array[in + 7] = v1		'bottomright y

		in = index * 4
		vert_array[in+0]= x0*ix+y0*iy+tx	'topleft x
		vert_array[in+1]= x0*jx+y0*jy+ty	'topleft y
		vert_array[in+2]= x1*ix+y0*iy+tx	'topright x
		vert_array[in+3]= x1*jx+y0*jy+ty	'topright y
		vert_array[in+4]= x0*ix+y1*iy+tx	'bottomleft x
		vert_array[in+5]= x0*jx+y1*jy+ty	'bottomleft y
		vert_array[in+6]= x1*ix+y1*iy+tx	'bottomright x
		vert_array[in+7]= x1*jx+y1*jy+ty	'bottomright x

		index :+ 4
		Flush()
	End Method
</textarea> <br><br></td></tr></table><br>
<a name="1240675"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LT</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#23">[#23]</a></td></tr></table></td></tr><tr ><td class="posttext"> Apologies ... I was looking at DrawPixmap().<br><br>This version of DrawTexture() is better, but doesn't take into account the "frame" parameter in DrawImage().  Try the BreakOut sample to see what I mean. <br><br></td></tr></table><br>
<a name="1240700"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#24">[#24]</a></td></tr></table></td></tr><tr ><td class="posttext"> think the  problem is something else ... as soon as I try to draw a "normal image" and an "animimage" it corrupts both images - this sounds as if a state does not get processed finally/totally.<br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
