<!DOCTYPE html><html lang="en" ><head ><title >Any way to create a pointer to a method?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Any way to create a pointer to a method?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Any way to create a pointer to a method?</a><br><br>
<a name="1182305"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> In BMax, I can do this<br><pre class=code>Function AFunction:Int(B:Int)
	Return B+7
End Function

Local BFunction:Int(B:Int) = AFunction

Print BFunction(6)
</pre><br>I can even reference functions inside a Type.<br><pre class=code>Type MyType
	Global C:Int = 7
	
	Function AFunction:Int(B:Int)
		Return B+C
	End Function
End Type

Local BFunction:Int(B:Int) = MyType.AFunction

Print BFunction(6)
</pre><br>However, I can't seem to reference a Method, even when creating an instance of one.<br><pre class=code>Type MyType
	Global C:Int = 7
	
	Method AFunction:Int(B:Int)
		Return B+C
	End Method
End Type

Local BFunction:Int(B:Int) = MyType.AFunction

Print BFunction(6)
</pre><pre class=code>Type MyType
	Global C:Int = 7
	
	Method AFunction:Int(B:Int)
		Return B+C
	End Method
End Type

Local AType:MyType = New MyType

Local BFunction:Int(B:Int) = AType.AFunction

Print BFunction(6)
</pre><br>So, how do I create a pointer to that Method? <br><br></td></tr></table><br>
<a name="1182309"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> You cant.  At least not directly.<br><br>You can use BRL.Reflection though, to get at the pointer.<br>but the default implementation is a bit lacking, so youd need some extras.<br><br>And the resulting function prototype must include the type as an argument, since its a method.<br><br><pre class=code>
Import BRL.Reflection

Type MyType
	Global C:Int = 7
	
	Method AMethod:Int(B:Int)
		Return B+C
	EndMethod
End Type

Local AType:MyType = New MyType

Local AMethod:Int( this:MyType, B:Int) = GetMethodPointer( AType, "AMethod")

Print AMethod( AType, 6)


Function GetMethodPointer:Byte Ptr( obj:Object, name:String)
	Extern "C"
		Function bbRefMethodPtr:Byte Ptr( obj:Object, index:Int )
	EndExtern
	
	Local t:TTypeId = TTypeId.ForObject(obj)
	If Not t Then Return Null
	
	Local m:TMethod = t.FindMethod(name)
	If Not m Then Return Null
	
	Return bbRefMethodPtr( obj, m._index)
EndFunction

</pre> <br><br></td></tr></table><br>
<a name="1182311"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks a lot.  I knew reflection had to be the answer, but just couldn't figure it out.  This also answers my second question, how to access more than one instance through the reference function.<br><pre class=code>SuperStrict
Import BRL.Reflection

Type MyType
	Field C:Int
	
	Method AMethod:Int(B:Int)
		Return B+C
	EndMethod
End Type

Local AType:MyType[4]
For Local i:Int = 0 To 3
	AType[i] = New MyType
	AType[i].C = i*2
Next

Local AMethod:Int( this:MyType, B:Int) = GetMethodPointer(AType[0],"AMethod")

For Local i:Int = 0 To 3
	Print AMethod(AType[i],6)
Next




Function GetMethodPointer:Byte Ptr( obj:Object, name:String)
	Extern "C"
		Function bbRefMethodPtr:Byte Ptr( obj:Object, index:Int )
	EndExtern
	
	Local t:TTypeId = TTypeId.ForObject(obj)
	If Not t Then Return Null
	
	Local m:TMethod = t.FindMethod(name)
	If Not m Then Return Null
	
	Return bbRefMethodPtr( obj, m._index)
EndFunction
</pre> <br><br></td></tr></table><br>
<a name="1182339"></a>

<a name="1182341"></a>

<a name="1182353"></a>

<a name="1182354"></a>

<a name="1182355"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> The "correct" way to do this in a language like BlitzMax is to make your "methods" explicit as simple functions that take an explicit self object as the first parameter.<br><br>To go into this further, consider: what are you actually hoping to achieve by doing this?<br><br>The difference between a method and a function in BlitzMax is that a method has implicit user data associated with it, while a function is just a code vector. When you call a method, the first thing it gets access to - even before its parameters - is its host object. So presumably what you want is to be able to access that function+implicit data after separating it from its containing object, as though it were simply contained in a data field and you were extracting it.<br><br>Unfortunately this won't work because the internal implementation of the method <i>is</i> just a function with a hidden first parameter. The method data doesn't actually contain a built-in link to the host object; when you call a method on an object, the dot syntax doesn't <i>just</i> mean "extract this from the structure", it's actually a completely different operation that includes passing the structure it was looked up in.<br><br>In other words, the entire object-dot-method construct is required for the "implicit data" to work; that is how it is made available. If you take the machine code vector out of the object and use it on its own, it will <i>no longer have</i> magical hidden data associated with it, and become just a dumb function (as you can see above in the reflective code, it needs obj to be passed explicitly again even thought was written as a method).<br><br>Unfortunately the entire concept of a "method pointer" is logically unsound. A method isn't an entity that has any kind of independent existence. It can logically exist only in the context of a parent scope object. Without that parent, it's exactly the same as a function with explicit self.<br><br><br>"But Yasha, I can do this in JavaScript!" No, you can't. In JavaScript, <i>all</i> functions are methods of at least one hidden scope, and the function call operation is therefore <i>always</i> used to access that parent scope (this can be in addition to the "this" object because JavaScript is multi-paradigm). In other words, in a language where it looks like you can do this, function calls are more than just jumps to a code vector the way they are in BlitzMax or C: they're a more involved operation (this also makes them slower) that also has to look up the self object the way a BlitzMax method does explicitly with the dot. In effect JavaScript (and Scheme, and Clang, and {edit} for that matter the code in the post below ...) give you this power <i>at the cost of</i> "real" fast procedure calls.<br><br>The correct object-oriented way to associate data+action is to bundle them together into an object with a method that does that thing, and call the method of the object when you want to use them together. The idea of functions that carry data with them is from "functional programming", and FP and OOP are orthogonal, i.e. anything you can do with one, you can do with the other: in this case, an FP "closure" function is directly equivalent to an OOP object with a single "call" method, except that you can invoke the "call" method using bracket syntax.<br><br><br>To summarise:<br>-- method pointers are not a logical concept, as methods have no independent existence<br>-- if you cheat using reflection, they stop being methods (implementation detail!)<br>-- other languages that look like they can do this are actually hiding more stuff under the function call syntax that BlitzMax doesn't have<br>-- using a method on an object <i>is</i> the idiomatic way to associate an action with data in this language<br><br>So. If you want to associate data with action... you should design your code to expect an action delegate object. If you want to extract and potentially swap functionality <i>within</i> an object, write it with functions as members, <i>not</i> methods (there are a whole load of other reasons why using methods for this one would be logically incoherent), and give the functions an explicit self parameter.<br><br>If you need to take apart someone else's code, then reflection is probably the right way to do it, and also none of what I said above is relevant to your situation anyway. (But please consider a rewrite if you're using reflection on your own code: pretty much without exception it means you're doing something in a very inefficient and roundabout way compared to how it "should" be done.) <br><br></td></tr></table><br>
<a name="1182340"></a>

<a name="1182480"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> If you need <b>real</b> method references (function pointers linked to instances) or just cant stand passing the <i>this</i> argument explicitly,<br>you can use the functions below:<br><pre class=code>
Function GetMethodReference:Byte Ptr( obj:Object, name:String)
	Extern "C"
		Function bbGCRetain( obj:Object)
	EndExtern
	
	Function get_displacement:Int( where:Byte Ptr, with:Byte Ptr, size:Int)
		Return (with - where) - size
	EndFunction

	Local fn:Byte Ptr = GetMethodPointer( obj, name)
	If Not fn Then Return Null

	Local p:Int Ptr = Int Ptr MemAlloc(32)
	
	' store a reference to the object instance
	bbGCRetain obj	
	p[0] = Int Ptr(Varptr obj)[0]		
	p :+ 1
		
	' create method trampoline
	Local o:Byte Ptr = Byte Ptr(p)

	' pop EAX
	o[0] = $58; o :+ 1	
	' push &lt;obj&gt;
	o[0] = $68; o :+ 1
	Int Ptr(o)[0] = p[-1]; o :+ 4
	' push EAX
	o[0] = $50; o :+ 1	
	' jmp &lt;fn&gt;
	o[0] = $E9; o :+ 1	
	Int Ptr(o)[0] = get_displacement( o, fn, 4)

	Return p
EndFunction

Function FreeMethodReference( ref:Byte Ptr)
	Extern "C"
		Function bbGCRelease( obj:Int)
	EndExtern
	
	If Not ref Then Return	
	Local p:Int Ptr = Int Ptr(ref) - 1
	bbGCRelease p[0]
	MemFree p
EndFunction
</pre> <br><br></td></tr></table><br>
<a name="1182364"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am actually trying to create a VM in which I have a type CPU. The function pointers are stored in an array with the index corresponding to an opcode.  Figured this would be a faster and more elegant way of doing things rather than a list of Select..Case commands.  Here is a tiny version of what I did using type functions.  This version uses just 3 opcodes, 0 = Print contents of buffer, 1 = stuff the next byte onto the buffer as an ASCII character, 3 = end program.  Doesn't do much, but like I said, it is a simplified version for an example.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

Type CPU
	Global CallFunction()[] = [CPU.FunctionPrint,CPU.FunctionBuffer,CPU.FunctionEnd] 'list of functions ordered by opcode
	
	Global MemoryBank:TBank = CreateBank(1024) 'Program memory
	Global PC:Int = 0 'Program Counter
	Global Buffer:String 'A place to hold the list of characters, stuffing ASCII 0 will clear the buffer
	
	Function Run() 'Start the CPU
		Local OpCode:Int
		
		PC = 0
		Repeat
			OpCode = ReadPC() 'Read the opcode at the PC
			If OpCode &gt;= CallFunction.Length Then RuntimeError("Unknown Opcode "+Opcode+" at position "+PC) 'Error checking
			CallFunction[Opcode]()
		Forever
	End Function
	
	'This function reads the next code from memory and increases the PC by 1
	Function ReadPC:Int()
		If PC &gt;= MemoryBank.Size() Then RuntimeError("Reached end of memory")
		Local Code:Int = PeekByte(MemoryBank,PC)
		PC :+ 1
		Return Code
	End Function
	
	'this function prints the content of the buffer
	Function FunctionPrint() 
		If Buffer Then Print Buffer
	End Function
	
	'this function stuffs a character onto the buffer
	Function FunctionBuffer()
		Local c:Int = ReadPC()
		If c = 0
			buffer = ""
		Else
			buffer :+ Chr(c)
		End If
	End Function
	
	'This function ends the program
	Function FunctionEnd()
		End
	End Function
End Type

'Fill memory with the program
Local c:Int
Local pc:Int = 0
Repeat
	ReadData c
	If c = -1 Then Exit
	PokeByte(CPU.MemoryBank,pc,c)
	PC :+ 1
Forever

'Start the program
CPU.Run()

'The program
DefData 1,72,1,101,1,108,1,108,1,111,1,32,1,87,1,111,1,114,1,108,1,100,1,33,0,2,-1
</textarea><br>I thought I might try create multiple CPUs using threading, but that would seem to require more than one instance of the CPU type, hence the need for pointers to Methods rather than Functions. <br><br></td></tr></table><br>
<a name="1182365"></a>

<a name="1182372"></a>

<a name="1182388"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's a "more OO" way to do that using delegates (probably full of errors but you should get the general idea):<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Type CPU
	Field CallFunction:CPUDelegate[]	'list of functions ordered by opcode
	
	Field MemoryBank:TBank 'Program memory
	Field PC:Int = 0 'Program Counter
	Field Buffer:String 'A place to hold the list of characters, stuffing ASCII 0 will clear the buffer
	
	Method Init()
		CallFunction = [..
			New FunctionPrint.init(Self),..
			New FunctionBuffer.init(Self),..
			New FunctionEnd.init(Self)..
		]
		MemoryBank = CreateBank(1024)
	End Method
	
	Method Run() 'Start the CPU
		Local OpCode:Int
		
		PC = 0
		Repeat
			OpCode = ReadPC() 'Read the opcode at the PC
			If OpCode &gt;= CallFunction.Length Then RuntimeError("Unknown Opcode "+Opcode+" at position "+PC) 'Error checking
			CallFunction[Opcode].Do()
		Forever
	End Method
	
	'This function reads the next code from memory and increases the PC by 1
	Method ReadPC:Int()
		If PC &gt;= MemoryBank.Size() Then RuntimeError("Reached end of memory")
		Local Code:Int = PeekByte(MemoryBank,PC)
		PC :+ 1
		Return Code
	End Method
End Type
	
Type CPUDelegate Abstract
	Field host:CPU
	Method Do() Abstract
	Method init:CPUDelegate(h:CPU)
		host = h ; Return Self
	End Method
End Type

Type FunctionPrint Extends CPUDelegate
	Method Do() 
		If host.Buffer Then Print host.Buffer
	End Method
End Type
	
'this function stuffs a character onto the buffer
Type FunctionBuffer Extends CPUDelegate
	Method Do()
		Local c:Int = host.ReadPC()
		If c = 0
			host.buffer = ""
		Else
			host.buffer :+ Chr(c)
		End If
	End Method
End Type
	
'This function ends the program
Type FunctionEnd Extends CPUDelegate
	Method Do()
		End
	End Method
End Type


Local vm:CPU = New CPU
vm.init

'Fill memory with the program
Local c:Int
Local pc:Int = 0
Repeat
	ReadData c
	If c = -1 Then Exit
	PokeByte(vm.MemoryBank,pc,c)
	PC :+ 1
Forever

'Start the program
vm.Run()

'The program
DefData 1,72,1,101,1,108,1,108,1,111,1,32,1,87,1,111,1,114,1,108,1,100,1,33,0,2,-1

</textarea><br><br>It also illutrates the point that a delegate/closure/first-class-function in other languages is isomorphic to an object with an application method.<br><br>In this case though, it would have been easy to just make all of the opcode function pointers accept a single CPU object as their sole parameter: depending on how similar this is to the real thing I would have thought that would still be the best way to do this.<br><br><br>Not that I wish to suggest grable's mini-JIT is anything other than sheer unbridled awesome, but it's not exactly the "cleanest" way to go about doing something as (comparatively) simple as this. <br><br></td></tr></table><br>
<a name="1182486"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Not that I wish to suggest grable's mini-JIT is anything other than sheer unbridled awesome, but it's not exactly the "cleanest" way to go about doing something as (comparatively) simple as this. <br></div>It is indeed a hack ;)<br><br>There was even a bug in the trampoline it generated (pushing wrong this pointer), but the sample was so simple so it worked anyway hehe<br><br><br><div class="quote"> In this case though, it would have been easy to just make all of the opcode function pointers accept a single CPU object as their sole parameter: depending on how similar this is to the real thing I would have thought that would still be the best way to do this. <br></div>Thats what i would do too, and maybe even forgo the bytecode dispatch and have the code stream be function pointers directly (if speed is your thing, or just feeling adventures) <br><br></td></tr></table><br>
<a name="1182521"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ziggy</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> I had the same problem when designing Jungle Gui with Monkey and finally did this workaround to "emulate" delegates:<br><a href="http://code.google.com/p/junglegui/source/browse/eventhandler.monkey" target="_blank">http://code.google.com/p/junglegui/source/browse/eventhandler.monkey</a><br><br>Also implemented an eventhandler class. It's Monkey but it is very similar to BlitzMax <br><br></td></tr></table><br>
<a name="1182681"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> In this case though, it would have been easy to just make all of the opcode function pointers accept a single CPU object as their sole parameter: depending on how similar this is to the real thing I would have thought that would still be the best way to do this. <br></div>This seems to be the best way to accomplish what I want to do.  Here is a threaded version of my previous demo with multiple CPUs<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

Type TCPU
	Global CallFunction(CPU:TCPU)[] = [TCPU.FunctionPrint,TCPU.FunctionBuffer,TCPU.FunctionEnd] 'list of functions ordered by opcode
	Global List:TList = CreateList()
	Global ThreadList:TList = CreateList()
	
	Field MemoryBank:TBank = CreateBank(1024) 'Program memory
	Field PC:Int = 0 'Program Counter
	Field Buffer:String 'A place to hold the list of characters, stuffing ASCII 0 will clear the buffer
	Field EndCPU:Int = False
	
	
	Function Create:TCPU()
		Local CPU:TCPU = New TCPU
		List.AddLast(CPU)
		Return CPU
	End Function
	
	Function Start() 'Start the CPU
		For Local CPU:TCPU = EachIn List
			ThreadList.AddLast(CreateThread(Run,CPU))
		Next
		For Local Thread:TThread = EachIn ThreadList
			WaitThread(Thread)
		Next
	End Function

	
	Function Run:Object(oCPU:Object)
		Local CPU:TCPU = TCPU(oCPU)
		Local OpCode:Int
		
		CPU.PC = 0
		Repeat
			OpCode = ReadPC(CPU) 'Read the opcode at the PC
			If OpCode &gt;= CallFunction.Length Then RuntimeError("Unknown Opcode "+Opcode+" at position "+CPU.PC) 'Error checking
			CallFunction[Opcode](CPU)
		Until CPU.EndCPU
	End Function	
	
	'This function reads the next code from memory and increases the PC by 1
	Function ReadPC:Int(CPU:TCPU)
		If CPU.PC &gt;= CPU.MemoryBank.Size() Then RuntimeError("Reached end of memory")
		Local Code:Int = PeekByte(CPU.MemoryBank,CPU.PC)
		CPU.PC :+ 1
		Return Code
	End Function
	
	'this function prints the content of the buffer
	Function FunctionPrint(CPU:TCPU) 
		If CPU.Buffer Then Print CPU.Buffer
	End Function
	
	'this function stuffs a character onto the buffer
	Function FunctionBuffer(CPU:TCPU)
		Local c:Int = ReadPC(CPU)
		If c = 0
			CPU.buffer = ""
		Else
			CPU.buffer :+ Chr(c)
		End If
	End Function
	
	'This function ends the program
	Function FunctionEnd(CPU:TCPU)
		CPU.EndCPU = True
	End Function
End Type

'Fill memory with the program
Local c:Int
Local pc:Int = 0

For Local i:Int = 1 To 3
	Local CPU:TCPU = TCPU.Create()
	Repeat
		ReadData c
		If c = -1 Then Exit
		PokeByte(CPU.MemoryBank,pc,c)
		PC :+ 1
	Forever
	PC = 0
Next
'Start the program
TCPU.Start()

'The program
DefData 1,72,1,101,1,108,1,108,1,111,1,32,1,87,1,111,1,114,1,108,1,100,1,33,0,2,-1

DefData 1,72,1,101,1,108,1,108,1,111,1,32,1,72,1,101,1,108,1,108,1,111,1,33,0,2,-1
DefData 1,72,1,101,1,108,1,108,1,111,1,32,1,69,1,97,1,114,1,116,1,104,1,33,0,2,-1

</textarea> <br><br></td></tr></table><br>
<a name="1182740"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Instead of pointers to methods, to handle multiple virtual cpu's, you could just always pass an object reference to every function? <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
