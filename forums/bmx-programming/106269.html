<!DOCTYPE html><html lang="en" ><head ><title >Woody - openAL physical modeling synthesis</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Woody - openAL physical modeling synthesis</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Woody - openAL physical modeling synthesis</a><br><br>
<a name="1306095"></a>

<a name="1306096"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi everyone, <br><br>I will post a tutorial to this (probably) in a while. I was just playing around with some ideas and came up with this:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
Framework BRL.GLMax2D
Import pub.glew
Import pub.OpenAL

SetGraphicsDriver GLMax2DDriver()
AppTitle="Woody - openAL physical modeling synthesis"

Graphics(800,240,0,60) 
Type vec
	Field x:Double 
	Field y:Double 
	Field cv:vec 
	
	Method set(_x:Double, _y:Double)
		x = _x
		y = _y
	End Method
	
	Method clear()
		x=0
		y=0
	End Method
	
	Method length:Double()
		Return Sqr(x*x+y*y)
	End Method
	
	Method length_sqr:Double()
		Return (x*x+y*y)
	End Method
	
	Method sub:vec(_v:vec)
		Local v:vec = New vec 
		v.set(x-_v.x, y-_v.y)
		Return v
	End Method
	Function Create:vec(_x:Double, _y:Double)
		Local v:vec = New vec
		v.set(_x, _y)
		v.cv = New vec
		Return v 
	End Function
EndType 

'============================ AUDIO setup ====================================
Global audiotrue:Byte = 0

If OpenALInstalled()
	audiotrue=1
Else
	GLDrawText"Audio OFF: OpenAL audio is not installed. &lt;hit any key to continue!&gt;",0,120-17
	Flip 0
	WaitKey()
	End
End If

'============================ The Real Deal ==================================
Const BUFFER_SIZE:Int=2800
Const SAMPLING_FRQ:Int=44100

Global source:Int
Global buffers:Int[2]
Global frequency:Int
Global format:Int
Global buf:Byte Ptr

Global ALDevice:Int =alcOpenDevice(Null) 
Global ALContext:Int =alcCreateContext(ALDevice, Null) 
alcMakeContextCurrent(ALContext)

alGenBuffers(2,buffers)
alGenSources(1, Varptr source)

alListenerfv(AL_POSITION,[0.0,0.0,0.0])
alListenerfv(AL_VELOCITY,[0.0,0.0,0.0])
alListenerfv(AL_ORIENTATION,[0.0,0.0,-1.0])
alListenerf(AL_GAIN,4.0)

Global buffer_A:Short[BUFFER_SIZE]

format = AL_FORMAT_MONO16
buf = Byte Ptr buffer_A
For Local i:Int = 0 Until BUFFER_SIZE
		buffer_A[i] = 0
Next
alBufferData(buffers[0], format, buf,BUFFER_SIZE*2,SAMPLING_FRQ)
alBufferData(buffers[1], format, buf,BUFFER_SIZE*2,SAMPLING_FRQ)

alSourceQueueBuffers(source, 1, Varptr buffers[0])
alSourceQueueBuffers(source, 1, Varptr buffers[1])
alSourcePlay(source)
'============================---------------==================================
AddHook FlipHook,process
Function process:Object(id:Int,data:Object,context:Object )'data:Object = Null)
		Local buffer:Int
		Local val:Int
		
		alGetSourcei(source, AL_BUFFERS_PROCESSED, Varptr val)
			While(val)
				alSourceUnqueueBuffers(source,1, Varptr buffer)
				For Local i:Int = 0 Until BUFFER_SIZE
					
					buffer_A[i]=0
					For Local t:tone = EachIn tonelist
						buffer_A[i]:+t.solve()*10*$7fff
					Next
				Next
				alBufferData(buffer, format, buf, BUFFER_SIZE*2, SAMPLING_FRQ)
				alSourceQueueBuffers(source,1, Varptr buffer)
				val:-1
			Wend
		If val&lt;=0
			alGetSourcei(source, AL_SOURCE_STATE, Varptr val)
			If val&lt;&gt; AL_PLAYING
				alSourcePlay(source)
			End If
		End If	
End Function

Function hit()
		mouse_index = MouseX()/(800/spring_num1)
		mouse_index = Min(spring_num1,Max(1,mouse_index))
		mastertone = tone.Create(spring_num,2,2.0)
		
		mastertone.tension= 2^(note/6)
		mastertone.tension:*16.0
		mastertone.damp = 1.0-mastertone.tension^0.973*0.000055
		mastertone.tension:*0.0001
		Local boost:Float = 1.5-(0.5*Float(Abs(mouse_index-spring_numh))/spring_numh)
		mastertone.goy = (mastertone.tension^0.5*0.015*(MouseY()-120.0))*boost
		mastertone.gox = mastertone.tension^0.5*0.04*((MouseX()-400.0)/400.0)
		mastertone.goon = 10
		ListAddLast(tonelist,mastertone)
		displaytone = tone.Create(spring_num,2,2.0)
		displaytone.damp = mastertone.damp 
		displaytone.tension = mastertone.tension 
		displaytone.goy  = mastertone.goy  
		displaytone.gox  = mastertone.gox  
		displaytone.goon = mastertone.goon 
		
		px=0; pm.ClearPixels(0)	
End Function

Global gox:Float
Global goy:Float 
Global goon:Float =0

Global px:Double = 0
Global py:Double = 0 

Global pm:TPixmap = CreatePixmap(800,400,PF_BGRA8888)
Global spring_num:Int = 9
Global spring_num1:Int = spring_num-1
Global spring_numh:Int = spring_num1*0.5
Global pickup:Int = 1

Global note:Double 
Global mouse_index:Int = 0
Global tension:Double = 13
Global damp:Double = 0.0
	
Global tonelist:TList = CreateList()
Type tone
Field index:Int
Field num:Int
Field springs:vec[]
Field reps:Int
Field decay:Float 
Field output:Float 
Field sample_index:Int 
Field goon:Int 
Field gox:Float 
Field goy:Float 
Field tension:Float
Field damp:Float 
Field plate_vel:Float
Field plate:Float 

	Method solve:Float()
		sample_index:+1
		If (sample_index&gt; decay*SAMPLING_FRQ)
			tonelist.Remove(Self)
			Return 0
		Else
			If goon&gt;0
			 	springs[mouse_index].cv.x:+gox
			 	springs[mouse_index].cv.y:+goy
				goon:-1
			EndIf 
			For Local r:Int = 0 Until reps
				For Local i:Int=1 Until num-1
				 	springs[i].cv.x:*damp
				 	springs[i].cv.y:*damp
				 	springs[i].cv.x:+ tension*(springs[i-1].x+(springs[i+1].x-springs[i-1].x)*0.5 - springs[i].x ) 
				 	springs[i].cv.y:+ tension*(springs[i-1].y+(springs[i+1].y-springs[i-1].y)*0.5 - springs[i].y )
					springs[i].x:+springs[i].cv.x
					springs[i].y:+springs[i].cv.y
				Next
			Next
			Local c:vec = vec.Create(springs[0].x,springs[0].y)
			c = c.sub(springs[num/2]) 
			Local clen:Float = Min(1.0,Max(-1.0,(c.length_sqr()-(800.0/2)^2)*0.0002))
			
			plate_vel:*plate_vel'*0.99999
			plate_vel:+(clen-plate)*0.25
			plate:+plate_vel
			
			Return plate_vel
		EndIf 
	End Method
	
	Function Create:tone(_num:Int=13, _reps:Int = 2, _decay:Float = 5.0)
		Local t:tone = New tone
		t.num = _num
		t.springs = New vec[_num]
		For Local i:Int = 0 Until _num
			t.springs[i] = vec.Create(i*(800.0/(_num-1)),0.0)
		Next
		t.reps = _reps
		t.decay = _decay
		t.sample_index = 0
		t.goon = 0
		Return t
	EndFunction 
EndType 
Global mastertone:tone
Global displaytone:tone

SetBlend ALPHABLEND
Repeat
	SetColor 0,0,0
	SetAlpha 0.005
	DrawRect 0,0,800,240
	SetAlpha 1.0
	DrawPixmap(pm,0,0)

	SetColor 30,66,122
	DrawLine(0,120,800,120)
	
	SetColor 160,150,50
	DrawLine(400,120,MouseX(),MouseY())
	
	If displaytone
		SetColor 160,150,50
		SetAlpha 0.125
		For Local i:Int = 0 Until BUFFER_SIZE/120
			Local piy:Float = displaytone.solve() * 12000.0
			For Local i:Int=1 Until displaytone.num
				If(i Mod 2) Then SetColor 160,80,50 Else SetColor 160,150,50
				DrawLine(displaytone.springs[i-1].x,120+displaytone.springs[i-1].y,displaytone.springs[i].x, 120+displaytone.springs[i].y)	 
			Next
			Local col:Byte = 0.5*$ff
			Local ya:Int = py
			Local yb:Int = piy
			If py&gt;piy Then ya = piy; yb= py
			For Local iy:Int = ya Until yb 
				
				WritePixel(pm,px,Min(399, Max(0, 120+iy)),(col)|(col Shl 8)|(col Shl 16))
			Next 
			py =  piy
			px:+0.03
		Next
	End If
		SetAlpha 1.0
	
		If MouseHit(1)
			hit()
		EndIf 
		If KeyHit(KEY_A)		note = 1 ;hit()
		If KeyHit(KEY_W)		note = 2 ;hit()
		If KeyHit(KEY_S)		note = 3 ;hit()
		If KeyHit(KEY_E)		note = 4 ;hit()
		If KeyHit(KEY_D)		note = 5 ;hit()
		If KeyHit(KEY_F)		note = 6 ;hit()
		If KeyHit(KEY_T)		note = 7 ;hit()
		If KeyHit(KEY_G)		note = 8 ;hit()
		If KeyHit(KEY_Y)		note = 9 ;hit()
		If KeyHit(KEY_H)		note = 10;hit()
		If KeyHit(KEY_U)		note = 11;hit()
		If KeyHit(KEY_J)		note = 12;hit()
		If KeyHit(KEY_K)		note = 13;hit()
		If KeyHit(KEY_O)		note = 14;hit()
		If KeyHit(KEY_L)		note = 15;hit()
		If KeyHit(KEY_P)		note = 16;hit()
		If KeyHit(KEY_SEMICOLON)note = 17;hit()
		If KeyHit(KEY_QUOTES)	note = 18;hit()

		
	SetColor 60,80,150
		GLDrawText(tonelist.Count(),10,10)
	Flip 0
Until KeyHit(KEY_ESCAPE) Or AppTerminate()
	alcDestroyContext(ALContext)
	alcCloseDevice(ALDevice)
	ALContext=0
	ALDevice=0
End                   
        
</textarea> <br><br></td></tr></table><br>
<a name="1306099"></a>

<a name="1306103"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Now I'm using BlitzMax NG, which immediately works fine at x32.<br>Once I switch to x64 architecture, it compiles, but with no sound. <br>Any ideas?<br><br>Whoops, it's even worse... now the x32 built has no audio anymore either?! <br>Hmmm...this is not as easy as it seemed.<br><br>Also it has some compiling issues as you may notice, if you try. Seems like the openAL mod is not the same as before, or something else is weird. <br><br>For example...<br>original openal.bmx goes:<br>Global alcOpenDevice( devicename$z )=P("alcOpenDevice")<br><br>NG openal.bmx goes:<br>Global alcOpenDevice:Byte Ptr( devicename$z )=P("alcOpenDevice")<br><br>This is just an example, maybe pointing towards some other incompatibilities? <br><br></td></tr></table><br>
<a name="1306110"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hiya,<br><br>Yes, you need to be aware that any object that is a pointer in the c/c++ code should also be a pointer in blitzmax code. Pointers are different sizes depending on which cpu you compile for. In legacy 'max everything was 32bit and so an object returned from a c/c++ call would be a 32 bit pointer that would also conveniently fit nicely into an 32 bit Int. While this works well in the legacy compiler it is technically wrong as the returned type should be a pointer. So now if those return value types aren't being correctly set to a pointer type then x64 versions will see errors as an x64 pointer is a 64bit value and not a 32bit integer value! By setting the type to a pointer type means it will handle the difference architecture automatically and correctly.<br><br>Things to look out for are any object instances returned from a library made in another language or from imported code that's written in another language. This usually relates to c/c++ object/struct instances and sometimes strings that are a part of POD types. <br><br></td></tr></table><br>
<a name="1306112"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I totally thought of that, int32 being the hickup. <br>However, I wonder what went wrong that I can't even make the 32bit compile have sound anymore?<br>Have you tried compiling the above with NG?<br><br>OH, and thanks, of course! <br><br></td></tr></table><br>
<a name="1306114"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> No I've not tried it, at work for a little while longer. I assume you've tried rebuilding all of the modules? <br><br></td></tr></table><br>
<a name="1306115"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yep, did that, too.<br>Ah well, looking forward to where this goes for sure!<br><br>I hope BLide will get a proper implementation to compile NG 64bit, too!<br>Feels so dreadful in the old IDE, hehe...&gt;.&lt; <br><br></td></tr></table><br>
<a name="1306125"></a>

<a name="1306126"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just curious...<br>What OS are you using?<br><br>edit: I'm using Win10 x64 and I get the same results as yourself - audio in x86 builds but silence in x64 builds. However building for x86 then I get audio every time, regardless of how many times I build for x64. <br><br></td></tr></table><br>
<a name="1306128"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GW</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Taron, I use NG with blide. I just made a tweak to the NG BMK in that It defaults some flags to true. "-G x86" and "-quick" I think.. going from memory. <br>Then blide will compile with NG. but just 32bit. If I need 64bit, I have my main file also open in the NG ide and just hit compile there. It's bit hacky but it works. <br><br></td></tr></table><br>
<a name="1306137"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> That's cool, I will give it a try with Blide! Would be exciting!<br><br>But, yeah, what ever I did to mine, can't get sound back. &lt;shrugs&gt;<br>At the end, though, for what I wanted to do, I need x64, also hoping to get rtMidi to work. Then the party could really start! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
