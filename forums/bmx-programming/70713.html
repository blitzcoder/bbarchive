<!DOCTYPE html><html lang="en" ><head ><title >Diablo style item spawning</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Diablo style item spawning</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Diablo style item spawning</a><br><br>
<a name="790439"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >verfum</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi there, I'm battling different methods of spawning items from an object when either you make contact, or kill an enemy, very similar to Diablo, but I can't get the random item creating side of it working, well, I cant even begin to figure it out, it needs to be a method which also looks at the rarity of the items, so for example, we create an array of 1000 you then populate that array with random items based on rare values, so more health and mana items would be in the array than rings or rare weapons, from there we pick 5 random items out of that array and spawn them, or create them so to speak.  But I cant get my head around it, does anyone know of any sample code or examples that could put me on the right track?  Thank you. <br><br></td></tr></table><br>
<a name="790442"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> If you have 3 items : Gem, Potion and Weapon.<br>Gems are given 60% of the time<br>Potions are given 30% of the time<br>Weapons are given 10% of the time.<br>  x=Randomise (1,100)<br>  If x&gt;10 then give weapon<br>  elseif X&lt; 10 and &gt; 30 then give potion <br> else give gem.<br>I think you could give ranges for select statements which would make it more readable. <br><br></td></tr></table><br>
<a name="790443"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >North</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> You need<br><br>a table of all items and their stats<br>an algorithm to identify possible loot for the current player level<br><br>you can enhance this with<br><br>subsets of items for certain zones, monster types<br>giving certain npcs(monsters) a fixed table of possible loot specific to that npc<br>add a variable amount of gold separately to the looting algorithm<br>etc etc etc<br><br>I'd just use a "loot-array" consisting of index numbers  which point to the big item-table for every monster type.<br>If the monster types raise in level like in diablo you need to take that into account and prepare the npc-specific loot-array accordingly. <br><br></td></tr></table><br>
<a name="790444"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >verfum</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yikes!  This is exactly the part I'm having trouble doing though :( I know that I need this it's just actually doing it, my head hurts just thinking about it!  Problem is I haven't done this before, so I'm sure where to start.  I began with this:<br><pre class=code>
Strict

Graphics 640,480

SeedRnd MilliSecs()
Global items:Int[1000]

Get_items()



While Not KeyHit(KEY_ESCAPE)

	Cls

	Local count:Int=0

	While count&lt;20

		DrawText ("Purple = wood",20,20)
		DrawText ("Yellow = silver",20,40)
		DrawText ("Red    = gold",20,60)

		If items[count]=1 Then SetColor 255,0,0
		If items[count]=2 Then SetColor 255,255,0
		If items[count]=3 Then SetColor 255,0,255

	        DrawRect ((10*count),100,10,10)
		count = count +1


	Wend

	Flip

Wend


Function Get_items()

	Local count:Int = 0

	Local random_value:Int

	'This generates an array that has 100 1's in it, 200 2's and 700 3's. So 1 
	'is rarer than 2 in the array

	While count&lt;1000

		random_value = Rnd(0,1000)

		If random_value &gt;   0  And random_value &lt;   50   Then items[count]=1
		If random_value &gt;= 50  And random_value &lt;  400   Then items[count]=2
		If random_value &gt;= 400 And random_value &lt;= 1000  Then items[count]=3

     		count=count+1

	Wend

End Function
</pre><br>Thats as far as I got, naturally this has no actualy object creating, it's like a sketch example of what needs doing, but technically I cant seem to grasp it, what I need is a clever way of spawning items which are extended from an Item Type.  I'm getting dizzy :( <br><br></td></tr></table><br>
<a name="790445"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >klepto2</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Maybe this helps you a bit. It is thrown together very fast and I don't have much time to explain it currently. But take a look:<br><br><pre class=code>
Type TItem
	Field Name:String
	Field Multiplier:Float
	Field FireDmg:Float
	Field IceDmg:Float
	
	Field Class:String
	Field AttackMin:Int
	Field AttackMAx:Int
End Type


Type TMultiplier
	Global Map:TMap = CreateMap()
	Global List:TList = New TList
	
	Field Multi:Float
	Field Name:String
	
	Function Add(Name:String,Multi:Float,chance:Int=1)
		Local M:TMultiplier = New TMultiplier
		M.Multi = Multi
		M.Name = Name
		
		Map.Insert(Name,M)
		
		For Local I:Int = 0 To Chance-1
			List.addlast(M.Name)
		Next
	End Function
	
	Function Get:TMultiplier()
		Local I:Int = Rand(0,CountList(TMultiplier.List)-1)
		Return TMultiplier(MapValueForKey(TMultiplier.Map,String(TMultiplier.List.ValueatIndex(I))))
	End Function
End Type

Type TClass
	Global Map:TMap = CreateMap()
	Global List:TList = New TList
	
	
	Field Name:String
	
	Function Add(Name:String,chance:Int=1)
		Local M:TClass = New TClass
		M.Name = Name
		
		Map.Insert(Name,M)
		
		For Local I:Int = 0 To Chance-1
			List.addlast(M.Name)
		Next
	End Function
	
	Function Get:TClass()
		Local I:Int = Rand(0,CountList(TClass.List)-1)
		Return TClass(MapValueForKey(TClass.Map,String(TClass.List.ValueatIndex(I))))
	End Function
End Type

Type TExtra
	Global Map:TMap = CreateMap()
	Global List:TList = New TList
	
	Field Name:String
	
	Function Add(Name:String,chance:Int=1)
		Local M:TExtra= New TExtra
		M.Name = Name
		
		Map.Insert(Name,M)
		
		For Local I:Int = 0 To Chance-1
			List.addlast(M.Name)
		Next
	End Function
	
	Function Get:TExtra()
		Local I:Int = Rand(0,CountList(TEXtra.List)-1)
		Return TExtra(MapValueForKey(TExtra.Map,String(TExtra.List.ValueatIndex(I))))
	End Function
End Type





TMultiplier.Add("Rare",5.0,1.0)
TMultiplier.Add("Regular",2.5,4.0)
TMultiplier.Add("Bad",1.0,10.0)

TClass.Add("Sword",1.0)
TClass.Add("Axe",1.0)

TExtra.Add("Fire",5.0)
TExtra.Add("Ice",5.0)
TExtra.Add("the Destructor",1.0)


For Local I:Int = 0 To 99
	Local M:TMultiplier = TMultiplier.Get()
	Local C:TClass = TClass.Get()
	Local E:TExtra = TExtra.Get()
	Print M.Name + " " + C.Name + " of " + E.Name
Next
</pre> <br><br></td></tr></table><br>
<a name="790446"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >verfum</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey that's some pretty smart stuff, a few alien things in there, I'm not familiar with Tmaps, but I'll study it, thanks klepto.<br><br>btw. you threw that together :O <br><br></td></tr></table><br>
<a name="790512"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chroma</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think what you're saying is you want each monster to have a loot table.  And then have him drop a certain number of items from his loot table based on the specific drop rate of each item.  For that I would use a weighted numbers system.<br><br>Here's the monster loot table:<br>nothing = 1 <br>gold = 2<br>gem = 3<br>sword = 4<br>potion = 5<br><br>Here's the drop chance for each item:<br>MonsterLoot:String = "1111222345"<br><br>Here you actually see what dropped:<br>item:Int = Rand( Len( MonsterLootTable ) )<br><br>This gives the following drop rates:<br>nothing 40%<br>gold 30%<br>gem 10%<br>sword 10%<br>potion 10%<br><br>At least that's how I would do it.  This way each monster can have it's own custom loot table and you can have custom drop rates for each item too. <br><br></td></tr></table><br>
<a name="790577"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chroma</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's some code I whipped up.  It isn't complete but you can see how you could have a creature spawn with a random loot table already populated.  Which would be cool because you could then have the monster running around showing a sword that he's actually carrying.<br><br>First you would create a zone.  The zone is then populated with monsters using the zone.AddMonster() command.  Each monster would be carrying around the loot with him.  The player kills the monster, then you access the dead monsters lootlist to see what he was carrying.<br><br><pre class=code>

Local DeadlyForest:TZone = New TZone
DeadlyForest.AddMonster( 2 )

Type TZone

	Field monsterList:TList = New TList
	
	Method AddMonster( monID:Int )
		self.monsterList.AddLast( TMonster.Create( monID ) )
	End Method

End Type



Type TMonster

	Field id:Int
	Field name:String
	Field maxdrops:Int
	Field lootList:TList = New lootList

	Method Create:TMonster( _id:Int )
		Local monster:TMonster = New TMonster

		Select _id
			
			Case 1	'Goblin
				monster.name = "a goblin"
				monster.maxdrops = 1
				monster.PopulateLootList( "1=60%, 8=30%, 322=10%" )
				
			Case 2	'Whispering Ghost
				monster.name = "a whispering ghost"
				monster.maxdrops = 3
				monster.PopulateLootList( "1=40%, 15=30%, 322=10%, 99882=20%" )
			
			Case 3	'Small Red Dragon		
				monster.name = "a small red dragon"
				monster.maxdrops = 6
				monster.PopulateLootList( "1=20%, 66=30%, 321=10%, 3542=10%, 28374=10%, 382333=20%" )

		End Select
	End Method

	Method PopulateLootList( lootKey:String )

		Local i:Int
		Local tempItem:TItem

		'Build Loot Key String


		'Populate the Monster Loot List
		For i = 1 To monster.maxdrops
			Local num:Int = Rand(100)
			tempItem = TItem.Get( num )
			If tempItem &lt;&gt; Null Then self.MonsterLootList.AddLast( tempItem )
		Next
	End Method

End Type


Type TItem
	Field id:Int
	Field name:Int

	Method Get:TItem( _id:Int )
		Local item:TItem
		For item = EachIn MasterItemList
			If item.id = _id Then Return item
		Next
	End Method
End Type
</pre> <br><br></td></tr></table><br>
<a name="790579"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >deps</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Chroma, nice idea there with the loot table! I must remember it.<br><br>For the code there, I would remove the select _id thing and just create a bunch more types, inheriting stuff from TMonster:<br><br><pre class=code>
Type TMonster
  ' ...
endtype

Type TGoblin extends TMonster
  method new()
    name = "Butt-Ugly goblin guard"
    maxdrops = 1
    ' ...
  endmethod
endtype

Type TOgre extends TMonster
  method new()
    name = "Smelly Ogre with stick"
    ' and so on...
  endmethod
endtype
</pre><br>Perhaps you already knew this, but anyway. :) <br><br></td></tr></table><br>
<a name="790582"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chroma</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Peculiar.  I'm not too familiar with the Extends command.  But I'm willing to listen. :)<br><br>So....you make a type for each race of monster and can set fields that are present in the TMonster type. <br><br></td></tr></table><br>
<a name="790588"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >deps</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Types makes everything much more pretty and simple, and extending types makes stuff even more easy and pretty. :)<br><br>Everything inherits from TMonster. All fields and methods is available in sub types.<br>Fields and methods that appear in subtypes cannot be accessed from parent types.<br><pre class=code>
Type TMonster
  field name:string
  field hp:int
  field dmg:int

  method attack( other:TMonster )
    print name + " attacks " + other.name
    other.get_hurt( dmg )
  endmethod

  method get_hurt( d:int )
    hp:-d
    print name + " yells ow when hurt by "+d+"points! HP is reduced to "+hp
  endmethod
endtype

type TOgre extends TMonster
  method new()
    name = "Ogre"
    hp = 10
    dmg = 2
  endmethod
endtype

type TDragon extends TMonster
  method new()
    name = "Red dragon"
    hp = 10000
    dmg = 2000
  endmethod

  method fly()
    print "*flaps wings*"
  endmethod
endtype

d:TMonster = new TDragon
o:TMonster = new TOgre

o.attack( d )
d.attack( o )

d.fly() ' Not certain you can do this, you might need to do it like this instead: TDragon(d).fly()   
' to cast it to the correct type. (It's a TMonster, but a TDragon behind the scenes)
</pre><br>This is a really really good tutorial: <a href="http://blitzmax.com/Community/posts.php?topic=42519" target="_blank">http://blitzmax.com/Community/posts.php?topic=42519</a> <br><br></td></tr></table><br>
<a name="790613"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dmaz</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> while extending is great I disagree with deps usage.  you don't what to extend to a specific usage like "type TDragon extends TMonster"... you will find that won't play out very well later in your design. think about this... if you want a random monster to appear, how do you want to code it?<br><br>a=rnd(10)<br>select a <br>case 1 d:TMonster = new TDragon<br>case 2 d:TMonster = new Torge<br>case 3 d:....<br>(also I think it's better not to get in the habit of downcasting unless you really need to.)<br><br>or<br><br>d:TMonster = TMonster.create(rnd(10))<br><br><br><br>basically if you are just changing data there is no reason to extend.  the fly() method of TDragon is a good reason because it changes the capabilities of the type.  But I would do something along the lines of <br>TFlyingMonster extends TMonster <br><br></td></tr></table><br>
<a name="790673"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >verfum</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Fanstastic, I wan't expecting this much attention on it, thanks for the code guys, I'll test it in the game :D <br><br></td></tr></table><br>
<a name="790678"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >QuietBloke</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> dmaz,<br><br>just me being thick I suspect but...<br>Im not sure I follow your reasoning on why you shouldnt extend to specific monsters ?<br>I cant see why your argument would work... you say to create a random monster you would need a select/case to create each type of monster but surely if instead you just had an id as in Chroma's code you would still need a select/case on the id to populate the monster attributes.  <br><br>? <br><br></td></tr></table><br>
<a name="790724"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chroma</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's some code that will create a spawn points in the same style as EQ.  You can set a list of monsters and what the probabilities are for each to spawn.  Again, the spawnKey parser isn't written but it wouldn't be hard to do so.<br><br><pre class=code>
'Zone and Spawnpoints similar to EverQuest

Graphics 800,600

'CurrentZone is always the zone the player is in
Global CurrentZone:TZone = TZone.Load("BlackForest.zone")
CurrentZone.AddSpawnPoint( 100,200,20,"223=80%, 496=20%", 60*5 )
'I use 60*5 to specify a five minute respawn time

'This sets an 80% chance for monster number 223 to spawn 
'And a 20% chance For monster number 496 To spawn
'I would have monster 223 be a goblin and monster 496 be The Goblin King
'This way he is semi-rare and you can have him drop a really cool weapon, etc etc

'******************************************************************

While Not KeyDown(KEY_ESCAPE)
Cls

'CurrentZone.Logic()

Flip
Wend
End

'******************************************************************

Type TZone
	Field PlayerList:TList = New TList			'Everyone who's in this zone with you
	Field SpawnPointList:TList = New TList		'All spawn points in this zone
	Field MonsterList:TList = New TList			'All monsters in this zone
	
	Function Load:TZone( thisZoneFile:String)
		Local zone:TZone = New TZone
		'Load in all the spawn points
		'self.spawnList.AddList( spawnpoint )
		Return zone
	End Function


	Method AddSpawnPoint( xpos:Int, ypos:Int, zpos:Int, spawnKey:String, spawnFreq:Int )
		Local spawnpoint:TSpawnPoint
		spawnpoint.x = xpos
		spawnpoint.y = ypos
		spawnpoint.z = zpos
		spawnpoint.spawnKey = _spawnKey
		spawnpoint.spawnFreq = _spawnFreq
		self.SpawnPointList.AddLast( spawnpoint )
	End Method


	Method Logic()
		'Update spawn points
		Local spawnpoint:TSpawnPoint
		For spawnpoint = EachIn self.SpawnPointList
			spawnpoint.logic()
		Next
	End Method
End Type

'******************************************************************


Type TSpawnPoint
	Field x:Int, y:Int, z:Int	'coordinates in 3D space of the spawn point
	Field spawnTrigger:Int		'triggers if the monster from this spawn point dies
	Field spawnFreq:Int			'new monster spawns after this time
	Field spawnTimer:Float		'spawn timer to trigger a new monster 
	Field spawnKey:String		

	Method AddMonster()
	
	End Method

	
	Method Logic()
		If self.spawnTrigger = True
			self.spawnTimer :+ dt
			If self.spawnTimer &gt; spawnFreq
				self.spawnTrigger = False
				self.spawnTimer = 0
				self.AddMonster()
			EndIf
		EndIf
	End Method

End Type

'******************************************************************


Type TMonster
	Field id:Int
	Field name:String
	Field spawnedFrom:TSpawnPoint
	
	Method Died()
		ResetSpawnPoint( spawnedFrom )
	End Method
End Type

'******************************************************************


Function ResetSpawnPoint( tempSpawnPoint:TSpawnPoint )
	Local spawnpoint:TSpawnPoint
	For spawnpoint = EachIn CurrentZone.SpawnPointList
		If spawnpoint = tempSpawnPoint
			spawnpoint.spawnTrigger = True
			Return 0
		EndIf
	Next
End Function
</pre> <br><br></td></tr></table><br>
<a name="790780"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dmaz</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Im not sure I follow your reasoning on why you shouldnt extend to specific monsters ? <br></div>you know, it looks good on the surface and in the code but I think you will learn it's not very practical at that level.<br><br>say you had 50 different monster types... that select case is going to be huge and very unreadable.  also, what if you didn't want your monster type hard coded, say you wanted them in non compiled external config files?  How are you going to "new" that type?.... again you would have 50 case statements in that select case while I would not have any.  what I would do is have a base class called TCharacter that TPlayer,TMonster,TNPC... extend from.  then for TMonster and TNPC I would just use data statements to define them.  for "Update"ing of a specific TMonster or TNPC type, it would be as simple as having function pointers defined inside those data types.  With this I can easily add a whole new monster by adding a data statement or modifying the config file.  Even better, defining the new monster in an external editor like the map editor... <br><br></td></tr></table><br>
<a name="790794"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >QuietBloke</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> ahhh I see.. I think.. <br>so with your method you may have 10 monsters with thier default stats held in external config files. When you create the monster it populates the monster stats from the config file.<br>Then specific game logic like a simple AI .. you might have a guard which might tend to stay close to whatever it is guarding and will only attack when provoked that code will be in a specific function. you might also have a passive monster, aggresive etc... and each of those would be a seperate function for the monster type. When you create a monster the config file would also specify which function it should use....<br><br>I guess you could then extend the whole thing so instead of a hardcoded function it would point to a specific Lua script.<br><br>is that right ?<br><br>I suppose Im so used to just writing hard coded stuff where what you see is what you get I never really think about it in terms of customisation in the future. <br><br></td></tr></table><br>
<a name="790811"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> I use Chroma's method for my games. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
