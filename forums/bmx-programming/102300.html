<!DOCTYPE html><html lang="en" ><head ><title >Moving Cursor /w Zoom and Scroll</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Moving Cursor /w Zoom and Scroll</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Moving Cursor /w Zoom and Scroll</a><br><br>
<a name="1220997"></a>

<a name="1220998"></a>

<a name="1220999"></a>

<a name="1221000"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >FBEpyon</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> ***Double Post*** <br><br></td></tr></table><br>
<a name="1221001"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >FBEpyon</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello,<br><br>Can someone help me fix this?<br><br>I'm trying to capture the location of an tile that has been moved and zoomed...<br><br>Here is me prototype of what I'm trying to do, as you can see the cursor isn't lining up :(<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Strict

Global z:Float = 1
Global orgx:Float, orgy:Float

Type Map

	Field data:Int[]
	Field grid:Int[]
	Field x:Int, y:Int
	Field width:Int, height:Int
	Field offx:Int, offy:Int
	Field zoom:Float
	
	Method Init:Map(x:Int, y:Int)
	
		Self.x = x
		Self.y = y
		Self.width = 120
		Self.height = 80
		Self.zoom = 1
		Self.data = New Int[160 + 80 * 160]
		Self.grid = New Int[160 + 80 * 160]
		
		For Local gx:Int = 0 Until Self.width
		For Local gy:Int = 0 Until Self.height
			Self.SetGrid(gx, gy, Rnd(2))
		Next
		Next
		
		Return Self
	
	End Method
	
	Method Render:Map()
	
		SetScale Self.zoom, Self.zoom
	
		For Local x:Int = 0 Until Self.width
		For Local y:Int = 0 Until Self.height
			If grid[x + y * Self.width] = 1
				DrawRect ( (Self.x+(x*16))*Self.zoom, (Self.y+(y*16))*Self.zoom, (16), (16) )
			EndIf
		Next
		Next
		
		SetScale 1, 1
	
	End Method
	
	Method GetTile:TMap(nx:Float Var, ny:Float Var, tx:Int, ty:Int)
		nx = ((Self.x+(tx*16))*Self.zoom)
		ny = ((Self.y+(ty*16))*Self.zoom)
	End Method
	
	Method Move:Map(x:Int, y:Int)
		Self.x :+ x
		Self.y :+ y
	End Method
	
	Method SetGrid:Map(x:Int, y:Int, v:Int)
		Self.grid[x + y * Self.width] = v
		Return Self
	End Method
	
	Method SetZoom:Int(v:Float)
		If Self.zoom &gt;= 0.5 And Self.zoom &lt;= 5.0
			Self.zoom :+ v
		EndIf
		If Self.zoom &lt; 0.5
			Self.zoom = 0.5
		EndIf
		If Self.zoom &gt; 5.0
			Self.zoom = 5.0
		EndIf
		DebugLog zoom
	End Method
	
	Method GetZoom:Int(z:Float Var)
		Return Self.zoom
	End Method

End Type

Global m:Map = New Map.Init(-60*16,-40*16)

Graphics 1280,720

While Not KeyHit(KEY_ESCAPE)

	'SetOrigin (GraphicsWidth()*0.5, GraphicsHeight()*0.5)

	If KeyDown(KEY_A) Then m.move(2,0)
	If KeyDown(KEY_D) Then m.move(-2, 0)
	If KeyDown(KEY_W) Then m.move(0, 2)
	If KeyDown(KEY_S) Then m.move(0, -2)
	
	z = Float(MouseZSpeed())
	
		If z &gt; 0 Or z &lt; 0 Then m.SetZoom(z*0.2)
		
	MouseZSpeed()

	Cls
	
		SetOrigin (GraphicsWidth()*0.5, GraphicsHeight()*0.5)
	
		m.Render()
		
		'SetOrigin(0,0)
		
		m.GetTile(orgx, orgy, MouseX()/GraphicsWidth(), MouseY())
		
		SetScale m.zoom, m.zoom
				
		SetColor 255,0,255
			DrawRect ( orgx, orgy, 16, 16 )
			'DebugLog (m.x+(MouseX()/16)*16)
		SetColor 255,255,255
		
		SetScale 0,0
		
	Flip

Wend

EndGraphics
End

</textarea><br><br>** UPDATED THE CODE A BIT, BUT STILL DOESN'T WORK ** <br><br></td></tr></table><br>
<a name="1221006"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think, it is like this:<br><br><pre class=code>Method GetTile:TMap(nx:Float Var, ny:Float Var, tx:Int, ty:Int)
		nx = tx/Self.zoom-Self.X
		ny = ty/Self.zoom-Self.Y
End Method
</pre> <br><br></td></tr></table><br>
<a name="1221048"></a>

<a name="1221088"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >FBEpyon</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Tried that, it doesn't work.. :(<br><br>:UPDATED:<br><br>So I tried a few other things, Distance formulas which seem to work, but would like to have another option for displaying the cursor along the tile being selected. <br><br></td></tr></table><br>
<a name="1221103"></a>

<a name="1221104"></a>

<a name="1221105"></a>

<a name="1221106"></a>

<a name="1221107"></a>

<a name="1221108"></a>

<a name="1221110"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> The function is working...<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Global z:Float = 1
Global orgx:Float, orgy:Float

Type Map

	Field grid:Int[]
	Field x:Int, y:Int
	Field width:Int, height:Int
	Field offx:Int, offy:Int
	Field zoom:Float, SelectedX%, SelectedY%
	
	Method Init:Map(x:Int, y:Int)
	
		Self.x = x
		Self.y = y
		Self.width = 30
		Self.height = 30
		Self.zoom = 2
		Self.grid = New Int[width*height]
		
		For Local gx:Int = 0 Until Self.width
			For Local gy:Int = 0 Until Self.height
				Self.SetGrid(gx, gy, Rnd(2))
			Next
		Next
		
		Return Self
	
	End Method



	
	Method Render:Map()
	
		SetScale Self.zoom, Self.zoom
		SetColor 255,255,255
		For Local x:Int = 0 Until Self.width
			For Local y:Int = 0 Until Self.height
				If grid[x + y * Self.width] = 1
					DrawRect ( (Self.x+(x*16))*Self.zoom, (Self.y+(y*16))*Self.zoom, (16), (16) )
				EndIf
				If x=SelectedX And y=SelectedY
					SetColor 0,255,0
					DrawRect ( (Self.x+(x*16))*Self.zoom, (Self.y+(y*16))*Self.zoom, (16), (16) )
					SetColor 255,255,255
				EndIf
			Next
		Next
		SetScale 1, 1
	End Method



	
	Method GetTile:TMap(nx:Float Var, ny:Float Var, tx:Int, ty:Int)
			nx = (tx/zoom)-Self.X
			ny = (ty/zoom)-Self.Y
			SelectedX = nx/16
			SelectedY = ny/16
	End Method


	
	Method Move:Map(x:Int, y:Int)
		Self.x :+ x
		Self.y :+ y
	End Method


	
	Method SetGrid:Map(x:Int, y:Int, v:Int)
		Self.grid[x + y * Self.width] = v
	End Method


	
	Method SetZoom(v:Float)
		zoom :+ v
		If zoom &lt; 0.5
			zoom = 0.5
		ElseIf zoom &gt; 5.0
			zoom = 5.0
		EndIf
	End Method


	



End Type

Global m:Map = New Map.Init(-160,-160)
'Global m:Map = New Map.Init(0,0)

Graphics 800,600
Global FPS:TTimer=CreateTimer(10)


While Not KeyHit(KEY_ESCAPE)

	If KeyDown(KEY_A) Then m.move(2,0)
	If KeyDown(KEY_D) Then m.move(-2, 0)
	If KeyDown(KEY_W) Then m.move(0, 2)
	If KeyDown(KEY_S) Then m.move(0, -2)
	
	z = Float(MouseZSpeed())
	If z &gt; 0 Or z &lt; 0 Then m.SetZoom(z*0.2)

	Cls
		SetOrigin (GraphicsWidth()*0.5, GraphicsHeight()*0.5)
		m.Render()
		m.GetTile(orgx, orgy, MouseX()-GraphicsWidth()*0.5, MouseY()-GraphicsHeight()*0.5)
		SetOrigin 0,0
		SetColor 255,0,0
		DrawText  "X=" +  orgx  + " Y= " +  orgy,100,100
	Flip 0
	WaitTimer FPS
Wend
</textarea><br><br><br><br>Additional I would suggest not to work with two offset systems like "Self.X" and "GraphicsWidth()/2". Self.X alone is enough to do the job.<br><pre class=code>	Cls
		m.Render()
		m.GetTile(orgx, orgy, MouseX(), MouseY() )
		SetColor 255,0,0
		DrawText  "X=" +  orgx  + " Y= " +  orgy,100,100
	Flip 0
</pre><br><br><br><br>Do you know, that you do not need all this "Self" inside  Methods? See my version of SetZoom() function...<br><pre class=code>	Method SetZoom(v:Float)
		zoom :+ v
		If zoom &lt; 0.5
			zoom = 0.5
		ElseIf zoom &gt; 5.0
			zoom = 5.0
		EndIf
	End Method
</pre> <br><br></td></tr></table><br>
<a name="1221115"></a>

<a name="1221116"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >FBEpyon</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you so much for helping me out wit this..<br><br>I was pulling my hair out, I was getting their and then the zoom kept messing me up..<br><br>And Yes LOL .. I know I don't need all the selfs, My OCD kicks in and then I have to start adding them..<br><br>Once again thank you for all your help on this..<br><br>BTW the 2nd offset is to make up for the centering, I was trying to correct it so it zooms into the center of the screen<br><br>Regards,<br><br>FBEpyon <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
