<!DOCTYPE html><html lang="en" ><head ><title >need a little mathy help.</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >need a little mathy help.</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >need a little mathy help.</a><br><br>
<a name="1113927"></a>

<a name="1113928"></a>

<a name="1113929"></a>

<a name="1113930"></a>

<a name="1113931"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Paul "Taiphoz"</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Did a quick google to get an image for you to use, its a gif so you will need to change the code a little, but its got the same orientation as the sprite I am using.<br><br>Ok the problem, as you know blitz angles/degree's have zero at the right, and 180 at the left, and the range is -180 to 180, this always gets me all confused when trying to use these values to control the direction of travel and rotation of an image.<br><br>In short I want the little sprite to go after the mouse, but I'm going wrong with the math some where and not being brilliant at math cant seem to work out where.<br><br><img src="http://www.dci.org.uk/images/redArrow.gif"><br><br><pre class=code>
Strict	
Framework BRL.GLMax2D
Import BRL.PNGLoader


SetGraphicsDriver GLMax2DDriver()
Global GFX_Width		:Int 	= 	500
Global GFX_Height		:Int 	= 	500
Graphics GFX_Width , GFX_Height , 0


AutoMidHandle(True)
Global ImageShip:TImage	 = 	LoadImage("ship.png")


Type TShip
	Field x:float
	Field y:float
	Field vx:float
	Field vy:float
	Field fAngle:float
	Field tAngle:float
	Field turnrate:float
	Field speed:float
End type


Global Ship:TShip = New TShip
Ship.x = 100
Ship.y = 100
Ship.fAngle = 0
Ship.tAngle = 0
Ship.turnrate = 0
Ship.speed = 1


repeat
	cls

	turnship()
	moveship()
	
	SetRotation -Ship.fAngle
	DrawImage ImageShip,Ship.x,Ship.y
	SetRotation 0
	
	flip
Until KeyHit(KEY_ESCAPE)



Function turnship()

		Ship.vx:+ Cos(Ship.fAngle)*Ship.speed
		Ship.vy:+ Sin(Ship.fAngle)*Ship.speed
		
		Ship.x:+ Ship.vx 
		Ship.y:+ Ship.vy 
				
		'Set Rotation
		Ship.tAngle = (ATan2(Ship.y-MouseY(),Ship.x-MouseX())+180.0) Mod 360.0		
		Local difference:float = Abs(Ship.tAngle-Ship.fAngle)

		If Ship.tAngle &lt; Ship.fAngle				
			If difference &gt; 180.0 Ship.turnrate=2 Else Ship.turnrate=-2
		ElseIf Ship.tAngle &gt; Ship.fAngle
			If difference &gt; 180.0 Ship.turnrate=-2 Else Ship.turnrate=2
		EndIf
		
		Ship.fAngle = (Ship.fAngle - Ship.turnrate +360) Mod 360	
End function


Function moveship()	
	
	Ship.vx = - Cos(Ship.fAngle) * Ship.speed
	Ship.vy = - Sin(Ship.fAngle) * Ship.speed
	
	Ship.x:- Ship.vx
	Ship.y:+ Ship.vy
	
	DrawText Ship.fAngle , 1 , 1
	DrawText Ship.tAngle , 1 , 30
	
End function


</pre><br><br><font class="tiny">Last edited 2011</font><br><br><font class="tiny">Last edited 2011</font><br><br><font class="tiny">Last edited 2011</font><br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1113958"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Paul "Taiphoz"</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Solved it, although I am sure the way I fudged this to get it working is not the best way, so if anyone spots a much easier way please let me know.<br><br><pre class=code>
Strict	
Framework BRL.GLMax2D
Import BRL.PNGLoader


SetGraphicsDriver GLMax2DDriver()
Global GFX_Width		:Int 	= 	500
Global GFX_Height		:Int 	= 	500
Graphics GFX_Width , GFX_Height , 0


AutoMidHandle(True)
Global ImageShip:TImage	 = 	LoadImage("ship.png")


Type TShip
	Field x:Float
	Field y:Float
	Field vx:Float
	Field vy:Float
	Field fAngle:Float
	Field tAngle:Float
	Field turnrate:Float
	Field speed:Float
End Type


Global Ship:TShip = New TShip
Ship.x = 100
Ship.y = 100
Ship.fAngle = 0
Ship.tAngle = 0
Ship.turnrate = 0
Ship.speed = 1


Repeat
	Cls

	turnship()
	moveship()
	
	SetRotation Ship.fAngle-180
	DrawImage ImageShip,Ship.x,Ship.y
	SetRotation 0
	
	Flip
Until KeyHit(KEY_ESCAPE)



Function turnship()

		Ship.vx:+ Cos(Ship.fAngle)*Ship.speed
		Ship.vy:+ Sin(Ship.fAngle)*Ship.speed
		
		Ship.x:+ Ship.vx 
		Ship.y:+ Ship.vy 
				
		'Set Rotation
		Ship.tAngle = (ATan2(Ship.y-MouseY(),Ship.x-MouseX())+180.0) Mod 360.0		
		Local difference:Float = Abs(Ship.tAngle-Ship.fAngle)

		If Ship.tAngle &lt; Ship.fAngle				
			If difference &gt; 180.0 Ship.turnrate=2 Else Ship.turnrate=-2
		ElseIf Ship.tAngle &gt; Ship.fAngle
			If difference &gt; 180.0 Ship.turnrate=-2 Else Ship.turnrate=2
		EndIf
		
		Ship.fAngle = (Ship.fAngle - Ship.turnrate +360) Mod 360	
End Function


Function moveship()	
	
	Ship.vx = - Cos(Ship.fAngle) * Ship.speed
	Ship.vy = - Sin(Ship.fAngle) * Ship.speed
	
	Ship.x:+ Ship.vx
	Ship.y:+ Ship.vy
	
	DrawText Ship.fAngle , 1 , 1
	DrawText Ship.tAngle , 1 , 30
	
End Function


</pre> <br><br></td></tr></table><br>
<a name="1113973"></a>

<a name="1113974"></a>

<a name="1113976"></a>

<a name="1113977"></a>

<a name="1113979"></a>

<a name="1113980"></a>

<a name="1113981"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> You are calculating the ship movement twice, once in TurnShip which gets overridden by the one in MoveShip. I suppose there is an optimisation to cut out all the if statements if you were bothered by them :-<br><br><pre class=code>
Function turnship()				
		'Set Rotation
		Ship.tAngle = (ATan2(Ship.y-MouseY(),Ship.x-MouseX())+180.0) Mod 360.0
		Local difference:Float = Abs(Ship.tAngle-Ship.fAngle)
		
		Local sign:Int = ((Ship.tAngle&lt;Ship.fAngle)*-1) Or Ship.tAngle&gt;Ship.fAngle
		sign :* ((difference &gt; 180)*-1) Or 1
		Ship.turnrate = 2 * sign
				
		Ship.fAngle = (Ship.fAngle - Ship.turnrate +360) Mod 360	
End Function
</pre><br><br>other than that, looks ok enough to me :-)<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1113997"></a>

<a name="1271463"></a>

<a name="1271464"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Do yourself a favor and simply accept that angles should be in the range -180 to +180, as indicated by ATan2. Don't try to hammer them into some other form.<br><br>I wrote a little function to wrap angles to the the same range as ATan2. It is used to keep fAngle and difference working consistently.<br><br><pre class=code>
Strict	

Global GFX_Width		:Int 	= 	700
Global GFX_Height		:Int 	= 	700
Graphics GFX_Width , GFX_Height , 0

AutoMidHandle (True)
Global ImageShip:TImage	 = 	LoadImage("temp.jpg")

Type TShip
	Field x:Float
	Field y:Float
	Field vx: Float
	Field vy: Float
	Field fAngle: Float
	Field tAngle:Float
	Field turnrate:Float
	Field speed:Float
End Type

Global Ship:TShip = New TShip
Ship.x = 100
Ship.y = 100
Ship.speed = 1

Repeat

	Cls

	turnship()
	moveship()
	
	SetRotation Ship.fAngle
	DrawImage ImageShip,Ship.x,Ship.y
	SetRotation 0
	
	Flip
Until KeyHit(KEY_ESCAPE)


Function turnship()

		Ship.tAngle = ATan2( MouseY() - Ship.y, MouseX() - Ship.x )
		Local difference:Float = WrapAngle( Ship.tAngle - Ship.fAngle )
		Ship.turnrate = 2 * Sgn( difference )
		Ship.fAngle = WrapAngle( Ship.fAngle + Ship.turnrate )
		
End Function

Function moveship()	
	
	Ship.vx = Cos(Ship.fAngle) * Ship.speed
	Ship.vy = Sin(Ship.fAngle) * Ship.speed
	
	Ship.x:+ Ship.vx
	Ship.y:+ Ship.vy

End Function

Function WrapAngle:Float( a:Float )   ' to range ( -180, +180 ] just like ATan2
	a:Mod 360
	If a &gt;  +180 Then Return a - 360
	If a &lt;= -180 Then Return a + 360
	Return a
End Function</pre> <br><br></td></tr></table><br>
<a name="1114015"></a>

<a name="1114016"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Paul "Taiphoz"</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> This community is simply the best I have seen for development, Iv been on a few others, c++ and PHP but this community beats the all hands down.<br><br>Thanks. that helped a lot.<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
