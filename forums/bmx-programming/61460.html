<!DOCTYPE html><html lang="en" ><head ><title >Joy Axis hit?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Joy Axis hit?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Joy Axis hit?</a><br><br>
<a name="686818"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Robb</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm currently setting up my game to support both keyboard and joypad control and so far it’s working flawlessly, save for one thing...<br><br>If for example I need to call Keyhit(KEY_UP), I can't see any obvious equivalent command for the joypad, only JoyY()=-1 which is effectively the same as KeyDown(KEY_UP).<br><br>Does anyone have any idea of an easy way to make a JoyAxisHit() command that will only register the joystick movement once?<br><br>Thanks! <br><br></td></tr></table><br>
<a name="686823"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Isn't it Joyhit (pub.freejoy 1.08)? <br><br></td></tr></table><br>
<a name="686826"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Robb</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> As far as I am aware, Joyhit just works for jostick buttons, not the axis :( <br><br></td></tr></table><br>
<a name="686848"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yan</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> <textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Const joyDeadZone# = 0.5

Rem
'You could do something like this instead
	Global joyDeadZone#
	
	For c=1 To 1000
		If Abs(JoyY()) &gt; joyDeadZone Then joyDeadZone = Abs(JoyX())
		If Abs(JoyY()) &gt; joyDeadZone Then joyDeadZone = Abs(JoyY())
	Next

	joyDeadZone :* 1.1 ' Just to be on the safe side
End Rem

Graphics 800, 600, 0

Local ox = 400, oy = 300

Repeat
	ox :+ (JoyXHit() * 40)
	oy :+ (JoyYHit() * 40)
	
	Cls
	SetColor 200, 200, 0
	DrawOval ox - 20, oy - 20, 40, 40
    
	Flip
Until KeyHit(KEY_ESCAPE)

End


Function JoyXHit(port=0)
	Global centered = True
	Local jx# = JoyX(port)
	
	If centered
		If jx# &gt; joyDeadZone#
			centered = False
			Return 1
		ElseIf jx# &lt; -joyDeadZone#
			centered = False
			Return -1
		EndIf
	ElseIf (jx# &gt;= -joyDeadZone#) And (jx# &lt;= joyDeadZone#)
		centered = True		
	EndIf
End Function

Function JoyYHit(port=0)
	Global centered = True
	Local jy# = JoyY(port)
	
	If centered
		If jy# &gt; joyDeadZone#
			centered = False
			Return 1
		ElseIf jy# &lt; -joyDeadZone#
			centered = False
			Return -1
		EndIf
	ElseIf (jy# &gt;= -joyDeadZone#) And (jy# &lt;= joyDeadZone#)
		centered = True		
	EndIf
End Function</textarea> <br><br></td></tr></table><br>
<a name="686853"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Robb</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Awesome! Thanks very much, that's got it sorted :) <br><br></td></tr></table><br>
<a name="686857"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Robb</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Actually, this doesn't work correctly...<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Const joyDeadZone# = 0.5

Graphics 800, 600, 0

Global jdir$="NOTHING"

Repeat
	
	
	DrawText jdir+" was the last key to be pressed",10,300
	
	If joyxhit()=1 Then jdir="RIGHT"
	If joyxhit()=-1 Then jdir="LEFT"
	If joyyhit()=1 Then jdir="DOWN"
	If joyyhit()=-1 Then jdir="UP"

	Flip
	Cls

Until KeyHit(KEY_ESCAPE)

Function JoyXHit(port=0)
	Global centered = True
	Local jx# = JoyX(port)
	
	If centered
		If jx# &gt; joyDeadZone#
			centered = False
			Return 1
		ElseIf jx# &lt; -joyDeadZone#
			centered = False
			Return -1
		EndIf
	ElseIf (jx# &gt; -joyDeadZone#) And (jx# &lt; joyDeadZone#)
		centered = True		
	EndIf
End Function

Function JoyYHit(port=0)
	Global centered = True
	Local jy# = JoyY(port)
	
	If centered
		If jy# &gt; joyDeadZone#
			centered = False
			Return 1
		ElseIf jy# &lt; -joyDeadZone#
			centered = False
			Return -1
		EndIf
	ElseIf (jy# &gt; -joyDeadZone#) And (jy# &lt; joyDeadZone#)
		centered = True		
	EndIf
End Function
</textarea><br><br>the if Statements only work for the positive values (whichever comes first in the code)....curious. Any ideas on how I can fix that? <br><br></td></tr></table><br>
<a name="687068"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mark1nc</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Maybe just call joyxhit() once, store the returned value and use that in the if statement.  <br><br>xret = joyxhit()<br>if xret = 1...<br>if xret = -1... <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
