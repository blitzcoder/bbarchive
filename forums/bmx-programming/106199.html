<!DOCTYPE html><html lang="en" ><head ><title >B3DHLP.dll in BlitzMax Help</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >B3DHLP.dll in BlitzMax Help</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >B3DHLP.dll in BlitzMax Help</a><br><br>
<a name="1304852"></a>

<a name="1304853"></a>

<a name="1304854"></a>

<a name="1304855"></a>

<a name="1304856"></a>

<a name="1304857"></a>

<a name="1304858"></a>

<a name="1304859"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stickman</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello all<br>Been a while since Iv been here , Im a former B3D user and all<br>New to Blitz Max .<br><br>Currently I'm trying to get the B3DHLP.dll to load AVI frames into Blitz Max to work .<br><br>I got this working in another IDE called XOJO , and it worked great.<br><br>So Far in Bmx I seem to have things going but can't seem to<br>get the "AVIGETFRAME" to load .<br><br>In XoJo I used a MemoryBlock to load the frame , basically same thing as a Ptr or Bank In Blitz I think .<br><br>In my code Im not sure what the p_bits and the AVIGETFRAME variable needs to be ... Byte Ptr , Bank , Int ?????<br><br>Any help would be grateful and sorry if my code is<br>a little sloppy , just using this to see how things work in Bmx .<br><br>here is info about the .dll<br><a href="/logs/userlog.php?user=7138&amp;log=1254" target="_blank">dll info</a><br>here is a link to the <a href="http://tinyurl.com/c9mkqou" target="_blank">B3DHLP.dll</a><br><br>And of course some code ( can't expect much help with out it )<br>... Feels good to be a Blitzer again :)<br><br><pre class=code>
Import maxgui.Drivers
Import pub.Win32

lib = LoadLibraryA ("B3DHLP.dll")

Global OpenAVI:Int(f$z) = GetProcAddress (lib, "AVIOPEN")
Global AVIGetFrameRate:Int(AVIObject:Int) = GetProcAddress(lib, "AVIGETFRAMERATE")
Global AVIGetTotalFrames:Int(AVIObject:Int) = GetProcAddress(lib, "AVIGETTOTALFRAMES")

Global AVIGetImageSize:Int(AVIObject:Int) = GetProcAddress(lib, "AVIGETIMAGESIZE")
Global AVIGetWidth:Int(AVIObject:Int) = GetProcAddress(lib, "AVIGETWIDTH")
Global AVIGetHeight:Int(AVIObject:Int) = GetProcAddress(lib, "AVIGETHEIGHT")
Global AVIClose:Int(AVIObject:Int) = GetProcAddress(lib, "AVICLOSE")

'Don't know what variable to make these 
Global AVIGetFrame:Int(AVIObject:Int, Pos:Int) = GetProcAddress(lib, "AVIGETFRAME")
Global p_bits:Int
'????????????????

Global hAVI:Int, FrameSize:Int, AVIRate:Int, FrameWidth:Int, FrameHeight:Int, AVIDuration:Int



Global Window1:TGadget = CreateWindow("My Project", 100, 100, 800, 600, Null, WINDOW_TITLEBAR | WINDOW_RESIZABLE | WINDOW_CENTER | WINDOW_CLIENTCOORDS | WINDOW_STATUS)

Local Button1:TGadget = CreateButton("OpenAVI", 10, 260, 96, 24, Window1, BUTTON_OK)

Local Canvas1:TGadget = CreateCanvas(10, 10, 320, 240, Window1)

Local Textfield1:TGadget = CreateTextField(110, 260, 120, 22, Window1)

Local TextArea1:TGadget = CreateTextArea(110, 290, 120, 120, Window1)


Global q:Int = 0
Global f:String = ""

CreateTimer 60
	
Repeat
WaitEvent()
SetStatusText Window1, CurrentEvent.ToString()
		
		
Select EventID()
		
	Case EVENT_TIMERTICK
		RedrawGadget Canvas1

	Case EVENT_GADGETACTION
		
		Select EventSource()
			Case Button1
				q = 1 - q
				f = RequestFile("Select AVI File", "avi,AVI")
				
				hAVI = OpenAVI(f)
				AVIRate = AVIGetFrameRate(hAVI)
				AVIDuration = AVIGetTotalFrames(hAVI)
				
				p_bits = AVIGetFrame(hAVI, 0)
				
                                'Blocked out till a frame is loaded or it crashes
				'FrameSize = AVIGetImageSize(hAVI)
				'FrameWidth = AVIGetWidth(hAVI)
				'FrameHeight = AVIGetHeight(hAVI)
			    
				SetGadgetText(Textfield1, "AVI Oppened")
				AddTextAreaText(TextArea1, "Rate " + AVIRate + "~n")
				AddTextAreaText(TextArea1, "Duration " + AVIDuration + "~n")
				
				AddTextAreaText(TextArea1, "Size " + FrameSize + "~n")
				AddTextAreaText(TextArea1, "Width " + FrameWidth + "~n")
				AddTextAreaText(TextArea1, "Height " + FrameHeight + "~n")
				AddTextAreaText(TextArea1, "Compleat~n")
				
		EndSelect

	Case EVENT_GADGETPAINT

		g = CanvasGraphics(Canvas1)
		SetGraphics g
		Cls
		        
		'This is just for testing
		If q = 1 Then
			DrawText "AVI", 0, 0
		End If
				
		Flip
			
		
		
	Case EVENT_WINDOWCLOSE, EVENT_APPTERMINATE
			
		If hAVI &lt;&gt; Null Then AVIClose(hAVI)
		FreeLibrary (Lib)
			
		End
EndSelect
Forever
</pre> <br><br></td></tr></table><br>
<a name="1304880"></a>

<a name="1304881"></a>

<a name="1304882"></a>

<a name="1304884"></a>

<a name="1304886"></a>

<a name="1304887"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hiya,<br><br>I'm sure for dll's you should be using the __stdcall convention. I have used dll's that require the __cdecl convention but they are the exception as opposed to the norm. Fortunately in BlitzMax we can specify the calling convention and __stdcall equates to "Win32" in BlitzMax. If you have the wrong calling convention then the stack gets screwed up when returning from the function. Sometimes this has a noticeable effect further down in the code, sometimes you see an error immediately.<br><br>So for all the dll functions that you want to use add "Win32" after the function definition, for eg...<br><br>Global OpenAVI:Int(f$z)<b>"Win32"</b> = GetProcAddress (lib, "AVIOPEN")<br>Global AVIGetFrameRate:Int(AVIObject:Int)<b>"Win32"</b> = GetProcAddress(lib, "AVIGETFRAMERATE")<br>etc...<br><br><br>I'm not tried your code so there may be other issues too.<br><br>ps. the BlitzMax equivalent of the __cdecl convention is "C" as opposed to "Win32" and if you don't specify a calling convention, as in your example code above, then the "C" is the default. <br><br></td></tr></table><br>
<a name="1304900"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stickman</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok cool will give this a try when i get hame tonight. <br><br></td></tr></table><br>
<a name="1304955"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stickman</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yep That did it ... Thanks a Lot Dave . <br><br></td></tr></table><br>
<a name="1305041"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stickman</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well maby not out of the woods yet ...<br>Just realized that the AVIGETFRAME returns a byte value<br>from 1 to 256 and blitz bytes are from 0 to 255<br><br>I tried using a bitshift on a Int ptr ...somthing like.<br>&amp; $FFFF ....that gave me same results as the byte ptr .<br><br>Oh well . hope sombody reads this and can help me figure<br>this onne out . <br><br></td></tr></table><br>
<a name="1305077"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> If its larger than 255 then its not a Byte (8-bits) really.<br><br>After looking through the samples on that page, <b>AVIGetFrame</b> seems to return a pointer to the pixels of a frame.<br>Maybe <b>AVIGetImageSize</b> returns the total size of a frame, and you can use that to copy it over to a TPixmap or something.<br><br>Figuring out the pixelformat would be your next step.<br><br><b>AVIBltFrameToTexture/Image</b> gets a DC from something, probably the internal DDrawSurface of the image/texture, and then StretchDIBits the frame over.<br>So it looks like you would have to reimplement it... <br><br></td></tr></table><br>
<a name="1305096"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stickman</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have this working in another programe ( XoJo ) and it works fine ....<br>So I did a little research and found that XoJo's MemoryBlock.Byte <br>actualy stores the ByteValues as Integers and returns the Value as a Byte ????<br><br>Ya let that spin around your head a Minute .<br><br>So all I need to do is make that Happen in Blitz .... Sounds Easy.<br><br>  So now where do I start ???? <br>Int Ptr ...Then What ????? <br><br></td></tr></table><br>
<a name="1305102"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have tried it on all the AVI files i have (not that many) but it fails to get Image Size and Frame, both returning 0.<br>The other stats return fine, like its Dimensions, Duration and Rate.<br><br>Do you have a link to a working AVI file i can try it on perhaps?<br><br>This is what i have so far, notice that i assume the frame is 32-bits per pixel which is unlikely.<br>If it gets a pointer to a frame it simply copies it over to a pixmap and tries to draw it.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

SuperStrict

Import maxgui.Drivers
Import pub.Win32

Global lib:Int = LoadLibraryA ("B3DHLP.dll")

Global AVIOpenLib:Int() "win32" = GetProcAddress (lib, "AVIOPENLIB")
Global AVICloseLib:Int() "win32" = GetProcAddress (lib, "AVICLOSELIB")

Global OpenAVI:Int(f$z) "win32" = GetProcAddress (lib, "AVIOPEN")
Global AVIGetFrameRate:Int(AVIObject:Int) "win32" = GetProcAddress(lib, "AVIGETFRAMERATE")
Global AVIGetTotalFrames:Int(AVIObject:Int) "win32" = GetProcAddress(lib, "AVIGETTOTALFRAMES")

Global AVIGetImageSize:Int(AVIObject:Int) "win32" = GetProcAddress(lib, "AVIGETIMAGESIZE")
Global AVIGetWidth:Int(AVIObject:Int) "win32" = GetProcAddress(lib, "AVIGETWIDTH")
Global AVIGetHeight:Int(AVIObject:Int) "win32" = GetProcAddress(lib, "AVIGETHEIGHT")
Global AVIClose:Int(AVIObject:Int) "win32" = GetProcAddress(lib, "AVICLOSE")

'Don't know what variable to make these 
Global AVIGetFrame:Byte Ptr(AVIObject:Int, Pos:Int) "win32" = GetProcAddress(lib, "AVIGETFRAME")
Global p_bits:Byte Ptr

Global FreePointer( p:Byte Ptr) "win32" = GetProcAddress( lib, "FREEPOINTER")
Global ReleaseDLL() "win32" = GetProcAddress( lib, "RELEASEDLL")

Global hAVI:Int, FrameSize:Int, AVIRate:Int, FrameWidth:Int, FrameHeight:Int, AVIDuration:Int



Global Window1:TGadget = CreateWindow("My Project", 100, 100, 800, 600, Null, WINDOW_TITLEBAR | WINDOW_RESIZABLE | WINDOW_CENTER | WINDOW_CLIENTCOORDS | WINDOW_STATUS)

Local Button1:TGadget = CreateButton("OpenAVI", 10, 260, 96, 24, Window1, BUTTON_OK)

Local Canvas1:TGadget = CreateCanvas(10, 10, 320, 240, Window1)

Local Textfield1:TGadget = CreateTextField(110, 260, 220, 22, Window1)

Local TextArea1:TGadget = CreateTextArea(110, 290, 220, 220, Window1)


Global f:String = ""

Global pix:TPixmap

CreateTimer 60

AVIOpenLib()

Repeat
WaitEvent()
SetStatusText Window1, CurrentEvent.ToString()
		
		
Select EventID()
		
	Case EVENT_TIMERTICK
		RedrawGadget Canvas1

	Case EVENT_GADGETACTION
		
		Select EventSource()
			Case Button1
				f = RequestFile("Select AVI File", "AVI Files:avi")
				If f Then
					hAVI = OpenAVI(f)
					If hAVI Then					
						AVIRate = AVIGetFrameRate(hAVI)
						AVIDuration = AVIGetTotalFrames(hAVI)
						FrameSize = AVIGetImageSize(hAVI)
						FrameWidth = AVIGetWidth(hAVI)
						FrameHeight = AVIGetHeight(hAVI)
						
						p_bits = AVIGetFrame( hAVI, 1)
						Print "p_bits=" + Hex(Int p_bits)
						If p_bits Then
							pix = CreatePixmap( FrameWidth,FrameHeight, PF_RGBA8888)
							MemCopy pix.pixels, p_bits, FrameWidth * FrameHeight * 4
							FreePointer(p_bits)
						EndIf
					
						SetGadgetText(Textfield1, "AVI Oppened")
						AddTextAreaText(TextArea1, "Rate " + AVIRate + "~n")
						AddTextAreaText(TextArea1, "Duration " + AVIDuration + "~n")
						
						AddTextAreaText(TextArea1, "Size " + FrameSize + "~n")
						AddTextAreaText(TextArea1, "Width " + FrameWidth + "~n")
						AddTextAreaText(TextArea1, "Height " + FrameHeight + "~n")
						AddTextAreaText(TextArea1, "Compleat~n")
					EndIf
				EndIf
		EndSelect

	Case EVENT_GADGETPAINT

		SetGraphics CanvasGraphics(Canvas1)
		Cls
		
		If pix Then DrawPixmap pix, 0,0

		Flip
		
		
	Case EVENT_WINDOWCLOSE, EVENT_APPTERMINATE
			
		If hAVI Then AVIClose(hAVI)
		AVICloseLib()
		ReleaseDLL()
		FreeLibrary(Lib)
		End
EndSelect
Forever
</textarea> <br><br></td></tr></table><br>
<a name="1305105"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stickman</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cool Grable ,<br><br> Was just working with Pixmaps myself and got it to work using<br>CreateStaticPixmap using the p_bits with a Format of PF_RGB888<br>then used loadImage to load the New Pixmap.<br><br>Also to get the Image Size and FrameWidth/Height you have to have <br>AVIGetFrame Before the call to AVIGetWidth/Height/Size.<br><br>I guess the Frame has to be loaded before it can tell what size ,etc it is.<br><br>Here is what I got working ( copied some of your coding to make it more official ) But using CreateStaticPixmap() and loadImage() instead<br><br>Not sure what way is faster but got it working non the less ....<br>With out this , converting all my code from XoJo to Blitz was meaningless.<br><br>Thanks a Million Grable .<br><br><br><pre class=code>SuperStrict

Import maxgui.Drivers
Import pub.Win32

Global lib:Int = LoadLibraryA ("B3DHLP.dll")

Global AVIOpenLib:Int() "win32" = GetProcAddress (lib, "AVIOPENLIB")
Global AVICloseLib:Int() "win32" = GetProcAddress (lib, "AVICLOSELIB")
Global OpenAVI:Int(f$z) "Win32" = GetProcAddress (lib, "AVIOPEN")
Global AVIGetFrameRate:Int(AVIObject:Int) "Win32" = GetProcAddress(lib, "AVIGETFRAMERATE")
Global AVIGetTotalFrames:Int(AVIObject:Int) "Win32" = GetProcAddress(lib, "AVIGETTOTALFRAMES")
Global AVIGetFrame:Int Ptr(AVIObject:Int, Pos:Int) "Win32" = GetProcAddress(lib, "AVIGETFRAME")
Global AVIGetImageSize:Int(AVIObject:Int) "Win32" = GetProcAddress(lib, "AVIGETIMAGESIZE")
Global AVIGetWidth:Int(AVIObject:Int) "Win32" = GetProcAddress(lib, "AVIGETWIDTH")
Global AVIGetHeight:Int(AVIObject:Int) "Win32" = GetProcAddress(lib, "AVIGETHEIGHT")
Global AVIClose:Int(AVIObject:Int) "Win32" = GetProcAddress(lib, "AVICLOSE")
Global FreePointer(p:Byte Ptr) "win32" = GetProcAddress(lib, "FREEPOINTER")
Global ReleaseDLL() "win32" = GetProcAddress( lib, "RELEASEDLL")
 
Global p_bits:Int Ptr

Global hAVI:Int, FrameSize:Int, AVIRate:Int, FrameWidth:Int, FrameHeight:Int, AVIDuration:Int

Global Window1:TGadget = CreateWindow("My Project", 100, 100, 800, 600, Null, WINDOW_TITLEBAR | WINDOW_RESIZABLE | WINDOW_CENTER | WINDOW_CLIENTCOORDS | WINDOW_STATUS)

Local Button1:TGadget = CreateButton("OpenAVI", 10, 260, 96, 24, Window1, BUTTON_OK)

Local Canvas1:TGadget = CreateCanvas(10, 10, 320, 240, Window1)

Local Textfield1:TGadget = CreateTextField(110, 260, 420, 22, Window1)

Local TextArea1:TGadget = CreateTextArea(110, 290, 120, 120, Window1)


Global f:String
Global im:TImage
Global pm:TPixmap

CreateTimer 60
AVIOpenLib()

Repeat
WaitEvent()
SetStatusText Window1, CurrentEvent.ToString()
		
		
Select EventID()
		
	Case EVENT_TIMERTICK
		RedrawGadget Canvas1

	Case EVENT_GADGETACTION
		
		Select EventSource()
			Case Button1
				f = RequestFile("Select AVI File", "AVI Files:avi")
				
				hAVI = OpenAVI(f)
				p_bits = AVIGetFrame(hAVI, 0)
				AVIRate = AVIGetFrameRate(hAVI)
				AVIDuration = AVIGetTotalFrames(hAVI)
				FrameWidth = AVIGetWidth(hAVI)
				FrameHeight = AVIGetHeight(hAVI)
				FrameSize = AVIGetImageSize(hAVI)
 
				If p_bits Then
                 pm:TPixmap = CreateStaticPixmap(p_bits, FrameWidth, FrameHeight, FrameWidth * 3, PF_RGB888)
				 im = LoadImage(pm)
                 FreePointer(p_bits)
				End If
				
				SetGadgetText(Textfield1, "AVI Loaded")
				AddTextAreaText(TextArea1, "Rate " + AVIRate + "~n")
				AddTextAreaText(TextArea1, "Duration " + AVIDuration + "~n")
				AddTextAreaText(TextArea1, "Size " + FrameSize + "~n")
				AddTextAreaText(TextArea1, "Width " + FrameWidth + "~n")
				AddTextAreaText(TextArea1, "Height " + FrameHeight + "~n")
				AddTextAreaText(TextArea1, "Compleat~n")
				
		EndSelect

	Case EVENT_GADGETPAINT

		SetGraphics CanvasGraphics(Canvas1)
		Cls
		        
		'This is just for testing
		If im Then DrawImage(im, 0, 0)
				
		Flip
			
		
		
	Case EVENT_WINDOWCLOSE, EVENT_APPTERMINATE
			
		If hAVI Then AVIClose(hAVI)
		AVICloseLib()
		ReleaseDLL()
		FreeLibrary(Lib)
			
		End
EndSelect
Forever </pre> <br><br></td></tr></table><br>
<a name="1305107"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> I still cant get FrameSize or Frame though. I guess the original API is rather picky about what files it can read.<br><br>Just a heads up, <b>CreateStaticPixmap</b> uses the pointer supplied directly, so you cant free it until your done with the pixmap itself (which is why i did a copy).<br><br>Glad you got it to work at least :) <br><br></td></tr></table><br>
<a name="1305118"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stickman</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok , didn't know that with CreateStaticPixMap <br><br>Im creating an Image Stacking software program for <br>Astronomy .<br><br>How it works is you use a Small SCTV camera in place of<br>an eyepiece on a telescope then take an short 1 to 5 min AVI<br>of what ever the telescope camera sees , <br><br>Then you load that AVI and stack each frame , the end results<br>are amazing .....<br><br>This program will also be able to do live stacking as well , <br><br>the end results can look something like this .....<br>This Image was a LIVE stack of about 2min , you see this<br>live on a TV screen .<br><br>This type of viewing is known as EAA ( Electronic Assisted Astronomy )<br><br><img src="http://www.cloudynights.com/uploads/monthly_04_2016/post-218143-0-69870300-1461176851_thumb.jpg"> <br><br></td></tr></table><br>
<a name="1305130"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Not meaning to derail the thread but did you take that 'image' yourself? That is an awesome pic!<br>I remember when I first saw Saturn through a telescope, I mean you know it's out there somewhere right, you read about it in books etc, but to actually see it with your own eyes is something completely different. <br><br></td></tr></table><br>
<a name="1305202"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stickman</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> No I didn't take that Pic , I just scrambled to look for one on the Forums , but I did take this one .....<br>Its a 2 Min Stack of the M57 galaxy....<br><br>If anyone is Interested there is a lot of info and PLENTY of Images<br>on this Forum <a href="http://www.cloudynights.com/forum/73-electronically-assisted-astronomy/" target="_blank">CLOUDY NIGHTS</a> <br>I go by the user name Submission there.<br><br><img src="http://www.cloudynights.com/uploads/monthly_03_2016/post-199308-0-64241600-1457250073.jpg"> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
