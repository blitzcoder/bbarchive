<!DOCTYPE html><html lang="en" ><head ><title >Slider Issues?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Slider Issues?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Slider Issues?</a><br><br>
<a name="1336934"></a>

<a name="1336935"></a>

<a name="1336936"></a>

<a name="1336939"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Caton</td><td align="right"><font class=tiny>(Posted March)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> <textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import MaxGui.Drivers
cpwin=CreateWindow("Control Panel",0,0,320,240)
CreateLabel("X",1,3,12,12,cpwin)
sli=CreateSlider(117,0,16,20,cpwin,SLIDER_STEPPER)
While Not AppTerminate()
WaitEvent()
Select EventID()
		Case EVENT_WINDOWCLOSE
			End
	End Select
DebugLog(SliderValue(sli))
Wend
End
</textarea><br><br>It seems to start five and only go to 10. and how can I get it to use cont up by float instead of int.<br><br>Just found out you must use the range function but how to use cont by floats like 0.5 <br><br></td></tr></table><br>
<a name="1336944"></a>

<a name="1336945"></a>

<a name="1336946"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted March)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi<br><br>you need to use SetSliderRange(min, max) and SetSliderValue(value) to determine the limits and the starting value<br><br><pre class=code>
' createslider.bmx

Import MaxGui.Drivers

Strict 

Local window:TGadget=CreateWindow("My Window",0,0,240,240,,WINDOW_TITLEBAR)


Local stepper:TGadget
stepper=CreateSlider(100,120,24,24,window,SLIDER_STEPPER)
Local lbl_stepper:Tgadget=CreateLabel("Value:  ---",10,120,80,24,window)
			

SetSliderRange stepper,0,9
SetSliderValue stepper,0


While WaitEvent()
	Print CurrentEvent.ToString()
	Select EventID()
		Case EVENT_GADGETACTION
			Select EventSource()
				Case stepper
						SetGadgetText lbl_stepper,"Value: "+SliderValue(stepper)
			
			End Select
		
		Case EVENT_WINDOWCLOSE
			End
	End Select
Wend

</pre><br><br>Stepper values are always (internally) as Integer.<br>You should need to change the range and manage the SliderValue() in a different way<br><br><pre class=code>
Import MaxGui.Drivers

Strict 

Local window:TGadget=CreateWindow("My Window",0,0,240,240,,WINDOW_TITLEBAR)


Local stepper:TGadget
stepper=CreateSlider(150,120,30,30,window,SLIDER_STEPPER)
Local lbl_stepper:Tgadget=CreateLabel("Value:  ---",10,120,120,30,window)
			

SetSliderRange stepper,0,99 '&lt;--- see the new range
SetSliderValue stepper,0

While WaitEvent()
	Print CurrentEvent.ToString()
	Select EventID()
		Case EVENT_GADGETACTION
			Select EventSource()
				Case stepper
						
						SetGadgetText lbl_stepper,"Value #" +Float(SliderValue(stepper))/10 '&lt;--- see this count
			
			End Select
		
		Case EVENT_WINDOWCLOSE
			End
	End Select
Wend

</pre> <br><br></td></tr></table><br>
<a name="1337026"></a>

<a name="1337027"></a>

<a name="1337028"></a>

<a name="1337029"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >cps</td><td align="right"><font class=tiny>(Posted March)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I assume you have come across the image freezing as you drag the scroll bar. There is a really good thread on using event hooks for scrolling, that gets round this problem. The following is based on that thread.<br>NOTE: This is based on work done by others in the forum, on a thread that I can't remember the name of. <br>'Current.JPG' is a 1200 by 900 pixel image. If your image is a different size set the variables 'CanvasWidth' and 'CanvasHeight' to match your image size.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
Import MaxGui.Drivers
Local Temp1%=0; Local Temp2%=0
Local MyDir$=CurrentDir()
Local Pix:TPixmap=LoadPixmap(Mydir+"\current.jpg")
Local MyWindow:TGadget=CreateWindow("Scrolling Canvas Example",  100, 10, 400,430)
Local MyPanel:TGadget=CreatePanel(30, 20, 320, 240, MyWindow, PANEL_ACTIVE|PANEL_BORDER)
Local CanvasHeight:Int=900
Local CanvasWidth:Int=1200
Local But1:Tgadget=CreateButton("xval",300,300,50,30,Mywindow)
Local MyCanvas:TGadget=CreateCanvas(0, 0, CanvasWidth, CanvasHeight, MyPanel)
Local VScroller:TGadget=CreateSlider(GadgetWidth(MyPanel)+30, 20, 20, GadgetHeight(MyPanel), MyWindow, SLIDER_VERTICAL)
Local HScroller:TGadget=CreateSlider(30,GadgetHeight(MyPanel)+20, GadgetWidth(MyPanel), 20, MyWindow, SLIDER_HORIZONTAL)
SetSliderRange VScroller, GadgetHeight(MyPanel), CanvasHeight
SetSliderRange HScroller, GadgetWidth(MyPanel), CanvasWidth
ActivateGadget MyCanvas
SetGraphics CanvasGraphics(MyCanvas)
SetAlpha(1) 
SetMaskColor 255,255,255' adjust for best images !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
Cls
Flip
DrawPixmap Pix,0,0
Flip
Local zz:hookcon =New hookcon ' scroll bars
zz.initilise(HScroller,VScroller,MyCanvas)
AddHook EmitEventHook, hookcon.FilterEvent, zz' scroll bars
CreateTimer 60
Repeat
	Delay 10 ' allow other apps some cycles to minimize CPU use
	PollEvent
  	Select EventID()
 	Case EVENT_WINDOWCLOSE
     	End
	Case EVENT_TIMERTICK
		RedrawGadget(MyCanvas)	
	Case EVENT_GADGETPAINT
		zz.DoDraw()		
  	 Case EVENT_MOUSEDOWN
		Select EventData()
			Case 1' left click
				Print "X = "+EventX()+"   Y = "+EventY()		
				'Temp1=GameHex.GetHexNum(EventX(),EventY())' temp1 = Hex number				
			Case 2' right click(not on a mac)
				Temp1=EventX()+zz.getpicx(); Temp2=EventY()+zz.getpicy()
				Print "X = "+Temp1+"   Y = "+Temp2									
		End Select			
	Case EVENT_GADGETACTION
		Select EventSource()
			Case But1 Print zz.getpicx()	
		End Select	
   	End Select
   	SetStatusText MyWindow, "ImgX="+zz.getpicx()+", ImgY="+zz.getpicy()
Forever

Type hookcon' hook for scroll bars to move image in the canvas
	Global Gad:TGadget[3]' number of gadgets in use
	Global PicX%; Global PicY%' x and y for image in canvas displacement
	Method initilise(Tv0:Tgadget,Tv1:Tgadget,Tv2:Tgadget)
		Gad[0]=Tv0'Hscroll
		Gad[1]=Tv1'Vscroll
		Gad[2]=Tv2'canvas that contains the game board
		PicX=0; PicY=0' x and y displacement of image/board due to scroll bars	
	End Method
Function FilterEvent:Object(iId:Int,tData:Object,tContext:Object)
  		Local Event:TEvent=TEvent(tData)
  		If Event.source=Gad[0] And Event.ID=EVENT_GADGETACTION
			PicX=event.data 
			DoDraw()		   
    	 	Return Null
  		Else If Event.source=Gad[1] And Event.ID=EVENT_GADGETACTION
    	   	PicY=event.data     
			DoDraw()		
     		Return Null
		Else
	 		Return tData
		EndIf
	End Function
	Function DoDraw()
			SetGraphics CanvasGraphics(Gad[2])
  			SetGadgetShape Gad[2],-PicX, -PicY,GadgetWidth(Gad[2]),GadgetHeight(Gad[2])	
			Flip
	End Function
	Method GetPicX%()' get curent X displacement
		Local T1%=PicX; Return T1
	End Method
	Method GetPicY%()
		Local T1%=PicY; Return T1' get curent Y displacement
	End Method
End Type
</textarea><br>Have fun cps <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
