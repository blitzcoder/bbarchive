<!DOCTYPE html><html lang="en" ><head ><title >layered TList</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >layered TList</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >layered TList</a><br><br>
<a name="692772"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dmaz</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm not sure if I re-invented someones wheel here... I'm not sure how helpful this will be but I thought I would share anyway.<br><br>Normally I use separate renderLists for different types of objects in my code... like a list of hud objs, enemy sprite, bullets, particles...  This time I was playing around where all sprites where in the same list but I still wanted basic z-ordering. well, not really z-ordering, more like layers.  so I extended TList to incorporate layers that are pre-sorted so you don't have to ever do the a Sort() (too slow for many objects) on them.<br><br>When I create sprite renderLists I always keep track of the TLink from the list so I can remove the sprite (among other things) without having to search the list with ListRemove (very bad).  I would imagine that many of us do this.<br><br><pre class=code>Type TSprite Extends TObject
	Global renderList:TList		= New TList
	
	Field renderListLink:TLink	= Null

...
	
	Method New()
		renderListLink = renderList.AddFirst(Self)
		count :+ 1
	End Method
	
	Method Remove()
		renderListLink.Remove	'remove self from renderList
		count :- 1
	End Method
...</pre>so the idea was to just put some fake objects in the list first and keep track of the links so I could then call InsertBeforeLink(). All I do is keep track of the TLinks of a few fake objects in the list using the array layers.  then when adding an object I insert it before the layers TLink... I do it before to perserve the sorting of that layer (first in first to draw)<br><pre class=code>Type TLayerMeta
	Field nothing:Int
End Type

Type TLayerList Extends TList
	Field totalLayers:Int = 0
	Field layers:TLink[]
	Field userObjectLink:TLink 'TLink to users inserted object
	
	' funtions -------------------------------------
	Function Create:TLayerList( totalLayers:Int=4 )
		Local list:TLayerList = New TLayerList
		list.totalLayers = totalLayers
		list.layers = New TLink[totalLayers]
		
		For Local i:Int = 0 To totalLayers-1
			list.layers[i] = list.AddLast(New TLayerMeta)
		Next
		
		Return list
	End Function

	' methods --------------------------------------	
	Method AddToLayer:TLink( value:Object,layer:Int )
		userObjectLink = InsertBeforeLink( value,layers[layer] )
		Return userObjectLink
	End Method
	
	Method RemoveFromLayer()
		userObjectLink.Remove
	End Method
	
End Type</pre>It worked great, but I have one question.  can anybody tell me why when I do this<br><pre class=code>For Local box:TBox = EachIn TBox.renderList</pre>I only seem to be getting back actual TBox types... none of the TLayerMeta come back... this is good, makes it cleaner but does EachIn only return the ask for types or is something wrong here?<br><br>here is a full test program, any thoughts?<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

Type TLayerMeta
	Field nothing:Int
End Type

Type TLayerList Extends TList
	Field totalLayers:Int = 0
	Field layers:TLink[]
	Field userObjectLink:TLink
	
	' funtions -------------------------------------
	Function Create:TLayerList( totalLayers:Int=4 )
		Local list:TLayerList = New TLayerList
		list.totalLayers = totalLayers
		list.layers = New TLink[totalLayers]
		
		For Local i:Int = 0 To totalLayers-1
			list.layers[i] = list.AddLast(New TLayerMeta)
		Next
		
		Return list
	End Function

	' methods --------------------------------------	
	Method AddToLayer:TLink( value:Object,layer:Int )
		userObjectLink = InsertBeforeLink( value,layers[layer] )
		Return userObjectLink
	End Method
	
	Method RemoveFromLayer()
		userObjectLink.Remove
	End Method
	
End Type

Type TBox
	Global renderList:TLayerList = TLayerList.Create(5)
	
	Field x:Float
	Field y:Float
	Field width:Float
	Field height:Float
	Field layer:Int
	
	' funtions -------------------------------------
	Function Create:TBox( x:Int,y:Int,width:Int,height:Int,layer:Int=0 )
		Local box:TBox = New TBox
		box.x = x
		box.y = y
		box.width = width
		box.height = height
		box.layer = layer
		renderList.AddToLayer(box,layer)
		Return box
	End Function
	
	Function DrawAll()
		For Local box:TBox = EachIn TBox.renderList
			box.Draw()
		Next	
	End Function
	
	' methods --------------------------------------	
	Method Remove()
		renderList.RemoveFromLayer()
	End Method
	
	Method Draw()
		SetColor 50+layer*40,50+layer*40,50+layer*40
		DrawRect(x,y,width,height)
		SetColor 0,0,0
		DrawText(layer,x,y)
	End Method
	
End Type

Graphics 800,600

TBox.Create(50,20,50,50,2)
TBox.Create(10,10,50,50,1)
TBox.Create(0,0,150,150,0) ' even though this is insert in the middle it will actually be at the beginning of the list.
Local b:TBox = TBox.Create(30,50,50,50,1)


While Not KeyHit(KEY_ESCAPE)
	Cls
	TBox.DrawAll()
	If KeyDown(KEY_SPACE) And b&lt;&gt;Null
		b.Remove
		b = Null
	End If
	
	Flip 1
Wend</textarea> <br><br></td></tr></table><br>
<a name="692874"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Paposo</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sorry for my bad english.<br><br>I have certain code that is useful for you.<br>The code is a prioritary queue. Is posible whit this traverse the que in certain order and modify the order of any object. This is very eficient for typical zorder draws.<br><br>if you need this code mail to me in comun@...<br><br>bye,<br>     Paposo <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
