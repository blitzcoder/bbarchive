<!DOCTYPE html><html lang="en" ><head ><title >Application continue processing when dragging</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Application continue processing when dragging</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Application continue processing when dragging</a><br><br>
<a name="1133637"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I know... I had started a thread about this 4-5 years ago - but there is still no solution.<br><br>When moving a windowed application/game in Windows the content is not being redrawn and also not processing actions.<br><br>I tried the timer method from this forums to emit timer-tick-events but on Linux it stops responding after about 200 events.<br><br><pre class=code>
While Not Ending
  WaitSystem
Wend


Function Hook:Object ( id:int , data:Object , context:Object )
  Local Event:TEvent = TEvent ( data )
  Select Event.source
    Case Null
      If Event.id = EVENT_APPTERMINATE then Ending = True
      If Event.id = EVENT_KEYDOWN And Event.data = KEY_ESCAPE then Ending = True
    Case Timer
      If Event.id = EVENT_TIMERTICK
        StopTimer Timer
        GameLoop()
		Timer = CreateTimer ( HERTZ )
      EndIf
  EndSelect
  Return data
EndFunction
</pre><br><br>Standard method works fine: <br><pre class=code>
	Repeat
		If Not GameLoop() Then Exit
	Until AppTerminate() Or ExitGame = 1
</pre><br><br><br>GameLoop just updates AppLoop and custom EventManager (custom events, no derivates of TEvent)<br><br><pre class=code>
	Method Loop()
		self.newTime		= MilliSecs()
		if self.oldTime = 0.0 then self.oldTime = self.newTime - 1
		self.secondGone		:+ (self.newTime - self.oldTime)
		self.loopTime		= (self.newTime - self.oldTime) / 1000.0
		self.oldTime		= self.newTime

		if self.secondGone &gt;= 1000.0 'in ms
			self.secondGone = 0.0
			self.fps		= self.timesDrawn
			self.ups		= self.timesUpdated
			self.deltas = 0.0
			self.timesDrawn = 0
			self.timesUpdated = 0
		endif

		'fill time available for this loop
		self.loopTime = Min(0.25, self.loopTime)	'min 4 updates per seconds 1/4
		self.accumulator :+ self.loopTime

		'update gets deltatime - fraction of a second (speed = pixels per second)
		While self.accumulator &gt;= self.deltaTime
			self.totalTime :+ self.deltaTime
			self.accumulator :- self.deltaTime
			self.timesUpdated :+ 1
			EventManager.triggerEvent( "App.onUpdate", TEventSimple.Create("App.onUpdate",null))
		Wend
		'how many % of ONE update are left - 1.0 would mean: 1 update missing
		self.tweenValue = self.accumulator / self.deltaTime

		'draw gets tweenvalue (0..1)
		self.timesDrawn :+1
		EventManager.triggerEvent( "App.onDraw", TEventSimple.Create("App.onDraw", string(self.tweenValue) ) )
		'Delay(1)
	End Method
</pre><br><br><br>Another thing I don't want is that the timer method limits frame rate to the timers hertz rate.<br><br><br><br>So what kind of possiblities do I have to process messages of my application even if its in background or being dragged around?<br><br>On Linux the app is processing while being dragging the window around... so I guess it is a Windows thing.<br><br>Graphical processing is not that important, but overall processing (physics, entity positions etc.) should be possible (as the app clients are able to control custom figures by AIscripts and so on).<br><br><br>Any ideas?<br><br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="1133638"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ima747</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I believe you can detect the actual dragging events... though they won't emit if it's standing still. Combined with tweening you may be able to fudge something together, but I don't think it will be optimal unless changes are made to the event manager on windows...<br><br>Another possibility could be to multithread the things that need continuous updates so they're unteathered from the main thread and therefore won't get suspended when the window is being moved... that might require a total overhaul of your code and opens up the possibility of threading bugs of course.<br><br>What is the root need for continuous updates while dragging the window? It seems to me that moving a window around generally means the user is not engaged with it's contents, but rather their user space, as a result holding the processing in that time would generally be a favorable experience. I can see how if you have time sensitive puzzles or something, where extra time looking at it would be bad then you don't want a free pause effect, but then if you tween the game time so they just loose whatever time they spent dragging it shouldn't be a problem... uncoupling logic/game time from draw is beneficial not just for performance adjustment but for maintaining control as well... Root cause of your issue might provide an easier solution than trying to resolve problems with Windows... <br><br></td></tr></table><br>
<a name="1133640"></a>

<a name="1133643"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Drawing is decoupled from physics...<br>Network is decoupled from physics...<br><br>I did not know that multithreading could get the thing...<br><br>To your question of the need:<br>Every player in my game controls a figure, master controlls some more.<br>If a player figure reaches a certain spot (or clicks to an entity - which is no problem here) an event is emitted and a networkpacket is spread around informing that player figure is doing something important.<br>Although master is knowing such things too and can spread around the same info -- this info might be obsolete (player clicked -&gt; informs master -&gt; master informs others -&gt; player packet reaches master -&gt; master informs others).<br><br>If Game master is having a freezed window he is not able to send events for AI controlled figures or entities reaching a state where they have to sync with all clients (event based sync, not time based - to lower packet amount)<br>Next to figures there are other events which happen to certain "in game times".<br><br>what is more important: if no processing is done, client would drop out of the game after a certain amount of time (no response...).<br><br><br>bye<br>Ron<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1133657"></a>

<a name="1133661"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ima747</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Re: processing drop out, give an allowance when the events are being blocked. You can detect the start/end of dragging so just restart the timers for player drop out after the release to prevent drags from timing them out.<br><br>If your drawing (has to be main thread) and networking (can be wherever) are decoupled then your networking should theoretically be possible to offload to another thread. You will need to manage some form of inter-thread communications (probably a mutex locked event/command queue) to safely talk with it, but that shouldn't be too difficult in theory, though it depends on how you're currently doing things... shoehorning multithreading into existing systems can be a nightmare if things don't line up. I think I posted some multithreading tutorials a while back in the tutorials forums, I would recommend you start there. Something like this should in theory fit from what you've described I think:<br><br>Main Thread:<br>Call function to post network message<br>Network post function locks mutex (which holds main thread if the mutex is in use)<br>Posts message to que<br>Unlocks mutex<br>function return<br><br>Networking thread:<br>At the top of each cycle attempts to lock the mutex<br>If it locks then copy the messages into a local que, clear the shared sue, unlock mutex so it's free to be posted to again<br>if it can't lock it (the main thread is currently using it) skip the que for this pass and do whever needs doing (look for new network messages, etc.)<br><br><br>I would advise using a que rather than locks for everything that might want to be changed on either side because a) it's a HELLUVA lot easier to keep track of what's going on and b) it allows both threads to run totally independently, no chance for race conditions, mutex fights, etc. That said the bmax multithreaded garbage collector has some gremlins so if you do a lot of thrashing especially on child threads things can get weird.<br><br>One more thing: If you're using 3rd party modules there's always a chance they might not be thread safe... additionally not all things can happen on child threads... Quite often if you keep things on a given thread they're fine even if they're not properly thread safe (don't cross the beams...) but again, threading can bring up some monsters from the deep.<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1133660"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> You can make this work in MaxGUI. However, the way to make it work is to NOT use the standard widgets/titlebar but instead to implement your own generic widget that looks like a title bar, or a window scaling widget, and then I believe it was possible to have it not block the program from processing while you do stuff. <br><br></td></tr></table><br>
<a name="1133663"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ima747</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> If there's a work around through some maxgui fumbling I would urge that over trying to cram in threading... unless you're really interested in threading and up for some weird headaches :0) <br><br></td></tr></table><br>
<a name="1133995"></a>

<a name="1134000"></a>

<a name="1134014"></a>

<a name="1134015"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Currently I'm not able to try it.<br>I thought about locking an update mutex when updating and do only drawing/mainloop if mutex is not locked.<br><br>But as bruceys libxml crashes when having multiple instances of txmldoc in multithreading... I have to recode that first ;D<br>Edit: its not libxml its a "calling currentTime()"-thingy which is crashing when having loaded a xml file with links to other xml files in there (which are then getting parsed)...investigating<br>Edit2: as it crashes at random points (no mutexes or brl.threads used/imported, just compiling threaded) within the xml-processing I still think its there... but not meant as offense to brucey when I state it's libxml :D<br>Edit3: *blushing* ... seems noel cowers bufferedglmax really (as he wrote) isn't thread safe - replacing it with the default maxgl made the crashes go away (windows build uses normal opengl...and run fine)<br><br>bye<br>Ron<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1134042"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> If you use the native widgets it title bar, resize gadget etc, the operating system will get all the cpu time until you let go of the widget. That's why you have to do your own widgets which you poll each frame. <br><br></td></tr></table><br>
<a name="1134057"></a>

<a name="1134062"></a>

<a name="1134064"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok so I finally coded something which works ... ima747 was right about additional threads...<br><br>I hope I didn't coded something wrong, so feel free to give suggestions.<br>The following code contains delta timing functions, fps counter and a game loop using that things. <br>If compiled without threading, the singlethreaded loop will get compiled else the one for threading.<br><br>(instead of the emit of events just use your update/draw calls at that spots)<br><pre class=code>
superstrict
?Threaded
Import Brl.threads
?
'class for smooth framerates, includes loop
Type TDeltaTimer
	field newTime:int 			= 0
	field oldTime:int 			= 0.0
	field loopTime:float			= 0.1
	field deltaTime:float			= 0.1		'10 updates per second
	field accumulator:float			= 0.0
	field tweenValue:float			= 0.0		'between 0..1 (0 = no tween, 1 = full tween)

	field fps:int 				= 0
	field ups:int				= 0
	field deltas:float 			= 0.0
	field timesDrawn:int 			= 0
	field timesUpdated:int 			= 0
	field secondGone:float 			= 0.0

	field totalTime:float 			= 0.0

	?Threaded
	Global UpdateThread:TThread
	Global drawMutex:TMutex 		= CreateMutex()
	Global useDeltaTimer:TDeltaTimer= null
	?

	Function Create:TDeltaTimer(physicsFps:int = 60)
		local obj:TDeltaTimer	= new TDeltaTimer
		obj.deltaTime		= 1.0 / float(physicsFps)
		obj.newTime		= MilliSecs()
		obj.oldTime		= 0.0
		return obj
	End Function

	?Threaded
	Function RunUpdateThread:Object(Input:Object)
		repeat
			useDeltaTimer.newTime		= MilliSecs()
			if useDeltaTimer.oldTime = 0.0 then useDeltaTimer.oldTime = useDeltaTimer.newTime - 1
			useDeltaTimer.secondGone	:+ (useDeltaTimer.newTime - useDeltaTimer.oldTime)
			useDeltaTimer.loopTime		= (useDeltaTimer.newTime - useDeltaTimer.oldTime) / 1000.0
			useDeltaTimer.oldTime		= useDeltaTimer.newTime

			if useDeltaTimer.secondGone &gt;= 1000.0 'in ms
				useDeltaTimer.secondGone 	= 0.0
				useDeltaTimer.fps		= useDeltaTimer.timesDrawn
				useDeltaTimer.ups		= useDeltaTimer.timesUpdated
				useDeltaTimer.deltas		= 0.0
				useDeltaTimer.timesDrawn 	= 0
				useDeltaTimer.timesUpdated	= 0
			endif

			'fill time available for this loop
			useDeltaTimer.loopTime = Min(0.25, useDeltaTimer.loopTime)	'min 4 updates per seconds 1/4
			useDeltaTimer.accumulator :+ useDeltaTimer.loopTime

			if useDeltaTimer.accumulator &gt;= useDeltaTimer.deltaTime
				'force lock as physical updates are crucial
				LockMutex(drawMutex)
				While useDeltaTimer.accumulator &gt;= useDeltaTimer.deltaTime
					useDeltaTimer.totalTime		:+ useDeltaTimer.deltaTime
					useDeltaTimer.accumulator	:- useDeltaTimer.deltaTime
					useDeltaTimer.timesUpdated	:+ 1
					EventManager.triggerEvent( "App.onUpdate", TEventSimple.Create("App.onUpdate",null))
				Wend
				UnLockMutex(drawMutex)
			endif
			delay(1)
		forever
	End Function

	Method Loop()
		'init update thread
		if not self.UpdateThread OR not ThreadRunning(self.UpdateThread)
			useDeltaTimer = self
			print " - - - - - - - - - - - - "
			print "Start Updatethread: create thread"
			print " - - - - - - - - - - - - "
			self.UpdateThread = CreateThread(self.RunUpdateThread, Null)
		endif

		'if we get the mutex (not updating now) -&gt; send draw event
		if TryLockMutex(drawMutex)
			'how many % of ONE update are left - 1.0 would mean: 1 update missing
			self.tweenValue = self.accumulator / self.deltaTime

			'draw gets tweenvalue (0..1)
			self.timesDrawn :+1
			EventManager.triggerEvent( "App.onDraw", TEventSimple.Create("App.onDraw", string(self.tweenValue) ) )
			UnlockMutex(drawMutex)
		endif
		delay(1)
	End Method
	?

	?not Threaded
	Method Loop()
		self.newTime		= MilliSecs()
		if self.oldTime = 0.0 then self.oldTime = self.newTime - 1
		self.secondGone		:+ (self.newTime - self.oldTime)
		self.loopTime		= (self.newTime - self.oldTime) / 1000.0
		self.oldTime		= self.newTime

		if self.secondGone &gt;= 1000.0 'in ms
			self.secondGone 	= 0.0
			self.fps		= self.timesDrawn
			self.ups		= self.timesUpdated
			self.deltas		= 0.0
			self.timesDrawn 	= 0
			self.timesUpdated	= 0
		endif

		'fill time available for this loop
		self.loopTime = Min(0.25, self.loopTime)	'min 4 updates per seconds 1/4
		self.accumulator :+ self.loopTime

		'update gets deltatime - fraction of a second (speed = pixels per second)
		While self.accumulator &gt;= self.deltaTime
			self.totalTime		:+ self.deltaTime
			self.accumulator	:- self.deltaTime
			self.timesUpdated	:+ 1
			EventManager.triggerEvent( "App.onUpdate", TEventSimple.Create("App.onUpdate",null))
		Wend
		'how many % of ONE update are left - 1.0 would mean: 1 update missing
		self.tweenValue = self.accumulator / self.deltaTime

		'draw gets tweenvalue (0..1)
		self.timesDrawn :+1
		EventManager.triggerEvent( "App.onDraw", TEventSimple.Create("App.onDraw", string(self.tweenValue) ) )
		'Delay(1)
	End Method
	?
	'tween value = oldposition*tween + (1-tween)*newPosition
	'so its 1 if no movement, 0 for full movement to new position
	'each drawing function has to take care of it by itself
	Method getTween:float()
		return self.tweenValue
	End Method

	'time between physical updates as fraction to a second
	'used to be able to give "velocity" in pixels per second (dx = 100 means 100px per second)
	Method getDeltaTime:float()
		return self.deltaTime
	End Method
End Type
</pre><br><br>What made me wonder is that on windows a debug+mt-build runs 15 graphical fps (thread 1) and &lt;5 physical fps (thread 2) on my virtual machine - release build runs way faster (100 for graphics and the forced/limited 60 physical updates per second). On Linux there is nearly no difference between debug and release.<br><br><br>Edit: Singlethreaded I get (in a ingamescene) 250-270fps, multithreaded it drops to 170-190 - but that's because I need to "delay(1)" drawing and updates as else updates wont occour steady (30-65 instead 59-61) and therefor spritemovement is far away from smooth.<br>Ideas how to avoid the delay within the drawing loop so I can see where bottlenecks occour (later I can limit it the same way as I do within the updates loop) ? Without delay(1) in the main thread, i get 180-300fps so removing the updatepart from the first thread is able to speed up the app again (but multicore cpus are certainly able to run the app smooth enough :D).<br><br>bye<br>Ron<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1134065"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Zeke</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> <textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

SeedRnd MilliSecs()

Graphics 800, 600

Global hwnd:Int = GetActiveWindow() 'get gfx window hwnd

Local style:Int
'style = GetWindowLongW(hwnd, GWL_STYLE) 'read window style

style = WS_VISIBLE | WS_BORDER 'new style

SetWindowLongW(hwnd, GWL_STYLE, style) 'change window style

SetWindowPos(hwnd, Null, 0, 0, 0, 0, SWP_NOSIZE | SWP_NOMOVE | SWP_NOZORDER | SWP_NOACTIVATE | SWP_FRAMECHANGED) 'update window



Global dragwindow:Int
Global lhit:Int
Global dx:Int, dy:Int

While Not (KeyHit(KEY_ESCAPE) Or AppTerminate())
	lhit = MouseHit(MOUSE_LEFT)

	If dragwindow = True Then 'window dragging
	
		If MouseDown(MOUSE_LEFT) Then
			Local rect:Int[4]
			Local result:Int = GetWindowRect(hwnd, rect) 'read window position
			Local pos:Int[2]
			GetCursorPos_(pos) 'read mouseposition
			SetWindowPos(hwnd, Null, pos[0] - dx, pos[1] - dy, 0, 0, SWP_NOSIZE) 'move window
		Else
			dragwindow = False
		End If
	EndIf
	
	If Not dragwindow And MouseY() &lt; 20 Then
		If MouseDown(MOUSE_LEFT) Then
			If Not dragwindow And lhit Then 'clicked "titlebar" -&gt; dragwindow
				Local rect:Int[4]
				GetWindowRect(hwnd, rect) 'read window position
				Local pos:Int[2]
				GetCursorPos_(pos) 'read mouse position
				dx = pos[0] - rect[0]
				dy = pos[1] - rect[1] 'save mousepos-windowpos
				dragwindow = True
			End If
		EndIf
	End If
	
	'## RENDER ##
	Cls
	
	'draw stuff
	For Local i:Int = 0 To 20
		SetColor Rand(255), Rand(255), Rand(255)
		DrawRect Rand(800), Rand(600), Rand(200), Rand(200)
	Next
	SetColor 255, 255, 255
	DrawText "time: " + MilliSecs(), 0, 100
	'## END RENDER ##
	
	DrawTitle(dragwindow Or MouseY() &lt; 20) 'draw titlebar
	MouseXSpeed() ; MouseYSpeed() 'update window position
	Flip 1
Wend

Function DrawTitle(over:Int)
	SetColor 100, 100, 100
	If over Then SetColor 150, 150, 150
	DrawRect 0, 0, 800, 20
	SetColor 255, 255, 255
	DrawText AppTitle, 0, 2 'draw apptitle
	'TODO: close/minimize buttons
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1134069"></a>

<a name="1134071"></a>

<a name="1134072"></a>

<a name="1134073"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> thanks Zeke ... but although your code works it seems to have a disadvantage.<br><br>Above the "titleBar" I have black area (where I can drag the window by pressing the mouse button) - am I able to style it (custom "application bar")?<br><br>Edit: it only happens when using GLMax2dDriver instead of DX... using the default one like in your setup, the code works as expected.<br>Problem then is the displacement which have to be done for all objects - or to handle a "mouseover"-dropdown-widget.<br>So now the question would be: how to handle opengl so it does not have that black border?<br><br>And does somebody know how a Mac handles dragging and meanwhile updating? <br><br>bye<br>Ron<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
