<!DOCTYPE html><html lang="en" ><head ><title >.b3d Animations</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >.b3d Animations</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >.b3d Animations</a><br><br>
<a name="622209"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AntonyWells</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm doing a b3d loader for t2 and so far it loads static meshes perfectly but I'm about to do animations.<br>I can understand and load the data contained within the b3d, the series of keys and bones for each node but I'm not sure how to interpret this data.<br><br>Do I - for each vertex asscoiated with each bone - Transform each vert assigned to this bone through the bone's current rotation and postion.<br>Or do I first offset the position based on the node's current position.<br><br>I.e,<br><br>NewX = Node.X-VertexX<br>FinX = Node.LocalMatrix.Transform( NewX )<br>FinX = Node.X + FinX<br><br>Or simply<br><br>FinX = Node.LocalMatrix.Transform( VertexX )<br><br>It appears wierd to me because each node in a animated b3d has it's own relative position yet the mesh renderes fine with the vertex's default positions. Implying mark did something to the naked data to get it to be represented accurately by the file it's self.<br><br>Confusion.<br>Much.<br>confusion. <br><br></td></tr></table><br>
<a name="622224"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chris C</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> is this some kind of forward compatability allowing offsets that most modelers dont allow for?<br><br>seems strange<br><br>If you've parsed out index and tri vertex info I wouldnt mind implementing b3d in my ode editor <br><br></td></tr></table><br>
<a name="622361"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AntonyWells</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm not sure tbh. This is my first time implementing a skeleton based animation system so maybe it's the norm.<br>Where's mark when you need him.<br><br>[quote]If you've parsed out index and tri vertex info I wouldnt mind implementing b3d in my ode editor[/quote[<br><br>Here's the loader as is. Replace the calls to t2(I.e entity/vertex creation) with your own but it works fine for static entities.<br><br><pre class=code>
Type TLoaderB3D Extends TLoader

	Method New()
		Name = "Blitz3D Mesh Loader Plugin"
		Author = "Antony Wells"
		Ver = "V1.0"
		Ext = "b3d"
	End Method

	Function Create:TLoaderB3D()
		Return New tloaderb3d
	End Function
	Field MediaPath:String
	Field Root:TEntity
	Field Stream:TStream
	Field MatList:TList,TexList:TList
	Method Load:TEntity(file:String)
	
		MediaPath = ExtractDir(file)
		Print "Media Path:"+MediaPath
		If mediapath="&lt;bad_dir&gt;" mediapath=""
		
		Root = Null
		Stream = ReadFile( File )
		If Stream = Null Throw "Unable to open B3D"
		MatList = CreateList()
		TexList = CreateList()
		
		Local Header:bChunk = Self.ReadChunk()
		If Header = Null Throw "Unable to read b3d header"
		
		If Header.Name&lt;&gt;"BB3D"	
			MainLog.Post "Not a valid b3d file "+File
			CloseFile stream
			Return Null
		Else
			MainLog.Post(file+" is a valid b3d file.")
		EndIf
		Local B3d_Version = ReadInt( Stream )
		If B3d_Version&lt;&gt;1 Throw "Trying to load b3d model version "+b3d_Version+" in v1.0 loader"
		
		ParseChunk( Header ) 
				
		
		CloseStream Stream

		Return Root
		
	End Method
	
	Method ReadChunk:bChunk()
		Local Out:bChunk = New bChunk
		For Local J=1 To 4
			Out.Name=Out.Name+Chr( ReadByte(Stream) )
		Next
		out.siz = ReadInt( Stream )
		out.begin = StreamPos( Stream )
		out.fin = out.begin+out.siz
		Return out
	End Method
	
	Method ParseChunk( From:BChunk )
	
		Local Cur:bChunk
		Local Mat:TMaterial
		Local Textures:Int
		Local NewEnt:Int
		If StreamPos(Stream) =&gt; StreamSize(Stream) Return
		Repeat
			
			Cur = ReadChunk()
			Select cur.name
				Case "BRUS" 'Material In Vivid Terms
					Print "Brush Reached."
					Local Textures = ReadInt( Stream )
					MainLog.Post("Textures Per Brush:"+Textures)
					
					
					While StreamPos( Stream ) &lt; Cur.Fin
					    Local Mat:TMaterial = TMaterial.create()
						Local NullString:String = ReadText()
						Mat.Diffuse( ReadFloat( Stream ),ReadFloat( Stream ),ReadFloat( Stream ),ReadFloat( Stream ) )
						Mat.Shininess( ReadFloat( Stream ) )
						Mat.Name = NullString
						MainLog.Post "Material Called:"+NullString,0
						Local Blend:Int = ReadInt( Stream )
						Select Blend
							Case 1
							Mat.Blend( Blend_Normal )
							Case 2
							Mat.Blend( Blend_Add )
							Case 3 
							Mat.Blend( Blend_ADD )
							Default
							Throw "Unkonw blend mode&gt;"+Blend
						End Select
										
						Local Fx:Int = ReadInt( Stream ) 
						If (fx &amp; 1) = 1
							Mat.Ambient(255,255,255)
							Mat.Specular(255,255,255)
							Mat.Color(255,255,255)
						EndIf
						If (fx &amp; 2) = 2
							Mat.ColorMode( 0)		
						Else
							Mat.ColorMode( 1)
						End If
						
						If (fx &amp; 4) =4
							Mat.Shade( Shade_Flat )
						End If
						
						If (fx &amp; 16) =16
							Mat.Cull( Cull_Off )
						Else
							Mat.Cull( Cull_Off )
						End If
											
						
					'	Main_Error.invoke "Has "+Textures+" Textures"
						For Local J:Int = 1 To Textures
							
							Local TextureNum = ReadInt( Stream )
							If TextureNum = -1
					'			Main_Error.invoke "Texture Slot "+J+" Empty"
							Else
								Local ThisTexture:TTexture =TTexture ( TexList.ValueAtIndex( TextureNum ) ) 
									
				'				Mat.AddTexture( ThisTexture )
								mat.AddTexture( ThisTexture)
							
							EndIf
																			
						Next
						
						ListAddLast MatList,Mat
						
					Wend
				

				Case "TEXS"
					Print "Texs"
				
					Local Texture:TTexture
					
					While StreamPos( Stream ) &lt; Cur.Fin
					
						Local File:String = ReadText()
						Local Flags = ReadInt(Stream)
						Local Blend = ReadInt(Stream)
								
						If file&lt;&gt;""
							If (Flags &amp; 64) = 64
								MainLog.Post "Sphere map ignored, not yet supported."
								'Texture = New TSphereMap
							Else
								Texture = New TTexture
							End If
							mainlog.post "Loading Texture : File:"+mediaPath+"/"+file
							'Texture.Load ( MediaPath+"/"+File )
							Texture = TTexture.Load( MediaPath+"/"+file )
							If texture = Null
								texture=TTexture.load( MediaPath+"/"+StripAll(file)+"."+ExtractExt(file) )
								If texture = Null
									texture = ttexture.load(file)
									If texture=Null
										texture = ttexture.load(StripAll(file)+"."+ExtractExt(file) )
										If texture=Null
											MainLog.Post("Could not resolve texture name "+file,False)
										EndIf
									EndIf
								EndIf
							EndIf
								
						'	Texture.ResPath = MediaPath+"\"
							
							Texture.Position( ReadFloat( Stream ),ReadFloat( Stream ),0)
							Texture.Scale( ReadFloat( Stream),ReadFloat( Stream ),0 )
							Texture.Rotate( 0,0,ReadFloat( Stream ) )
						
							If (Flags &amp; 65537) = 65537
								Texture.CoordSet = 1
								MainLog.Post "Using Second Coord set",0
							End If
							
							mainLog.post "Tex blend&gt;"+blend+" flag&gt;"+flags
							Texture.ColorScale=1
							Select Blend 
								Case 0
									Texture.Blend( Texture_Normal )
								Case 1
									Texture.Blend( Texture_Normal )
								Case 2
									Texture.Blend( Texture_Modulated )
								Case 3
									Texture.Blend( Texture_Add )
								Case 4
									Texture.Blend( Texture_Dot3 )
								Case 5
									Texture.Blend( Texture_Modulated )
									Texture.ColorScale =2 
								Case 6
									Texture.Blend( Texture_Modulated )
									Texture.ColorScale= 4 
								Case 7
									Texture.Blend( Texture_Dot3Alpha )
							End Select
													
							ListAddLast TexList,Texture
					
						EndIf
					
					Wend
			
				Case "NODE"	
					Print "Node."
					Local W:Float,X:Float,Y:Float,Z:Float
					Local Name:String = ReadText()
					MainLog.Post "Entity Named&gt;"+Name,0
					Local Entity:TEntity = New TEntity
					'Entity.Name = Name
					'main_error.invoke "Node&gt;"+Entity.Name
					'Entity.IgnorePipeline=True
					Entity.Position( ReadFloat( Stream ),ReadFloat( Stream ),ReadFloat( Stream ) ,True )
					Entity.setScale( ReadFloat( Stream ),ReadFloat( Stream ),ReadFloat( Stream ) )
					aEnt = Entity
					
				'	Main_Error.invoke "X:"+entity.x+" Y:"+entity.y+" Z:"+entity.z,0
					If root = Null
						Root = Entity
					'	main_error.invoke "Was Root"
					Else
						If pStack[ pCount ] = Null
							' "Child entity without parent in b3d loader"
						EndIf					
						Entity.SetParent( pStack[ pCount ] )
					End If
					
									
					W = ReadFloat( Stream )
					X = ReadFloat( Stream )
					Y = ReadFloat( Stream )
					Z = ReadFloat( Stream )
					
					Entity.Quat = New Quaternion
					Entity.Quat.Set( X,Y,Z,W )
					
					
					Entity.RotateFromQuat()
					
					pCount:+1
					pStack[ pCount ] = Entity
					NewEnt = True
					ParseChunk( Cur )

				Case "MESH"
				
					aBrush = ReadInt( Stream )
					Print "Mesh reached."
					'main_Error.invoke "Mesh BrushID:"+aBrush
					ParseChunk( Cur )
				
				Case "VRTS"
					
					Print "Reading Verts"
					Local Flags:Int = ReadInt( Stream )
					Local Sets:Int = ReadInt( Stream )
					Local SetSize:Int = ReadInt( Stream )
				
					Local VertAt
					Local TmpCoord:Float[3]
					TmpSurf:TSurface = TSurface.Create()
					aEnt.vSurf = tmpSurf
					Local VertCIs
					
					While StreamPos( Stream ) &lt; Cur.Fin
					
						VertAt = TmpSurf.AddVertex( ReadFloat(Stream),ReadFloat(Stream),ReadFloat(Stream) )
						VertCIs:+1
						If ( Flags &amp; 1 ) = 1
							
							TmpSurf.VertexNormal( VertAt,ReadFloat( Stream),ReadFloat( stream),-ReadFloat(Stream) )
							
						End If
						
						If ( Flags &amp; 2 ) = 2
						
							TmpSurf.VertexColor( VertAt, ReadFloat( Stream),ReadFloat(Stream),ReadFloat(Stream),ReadFloat(Stream) )
													
						End If
						
						For Local J = 0 To Sets-1
							
							For Local k=0 To SetSize-1
								TmpCoord[k] = ReadFloat( Stream )
							Next
							TmpSurf.VertexCoords(J,VertAt,TmpCoord[0],TmpCoord[1],TmpCoord[2] )
							
						Next
																			
					Wend 
			
			Case "TRIS"
					Print "Reading Tris."
					Local TMat:TMaterial
					Local MatId:Int = ReadInt( Stream )
					Local Mat:TMaterial,Surf:TSurface
					'Main_Error.Invoke "Loading Surface",0
					If MatId = -1
					'	Mat:TMaterial = New TMaterial
					Else
						mainLog.post "MatId&gt;"+MatId
						 TMat = TMaterial( MatList.ValueAtIndex( MatId ) )
 						mainlog.post "Surface material:"+TMat.Name
 					End If
					Local triCIs:Int
					
					If tmpSurf = Null
					Throw "No surface previously defined in b3d loader"
					EndIf
					aSurf = TSurface.create()
					If aSurf = Null 
						Throw "Surface clone failed in b3d loader"
					EndIf
					aEnt.AddSurface( aSurf )
					
					aSurf.verts = tmpSurf.verts
					aSurf.tris = tmpSurf.tris
					aSurf.norms = tmpSurf.norms
					aSurf.cols = tmpSurf.cols
					asurf.coords = tmpSurf.coords
					aSurf.coordsets =tmpsurf.coordsets
					aSurf.VertC = tmpsurf.vertc
					If tMat&lt;&gt;Null
						aSurf.SetMaterial( TMat )
						mainLog.post "USing Material&gt;"+TMat.Name
					EndIf
								
					While StreamPos( Stream )&lt; Cur.Fin
						aSurf.AddTriangle( ReadInt( Stream ),ReadInt( Stream ),ReadInt( Stream ) )
					Wend
				'	main_error.invoke "Read "+tricIs+" tris",0
				    
				Case "ANIM"
				
					Local flags = ReadInt( stream )
					Local frames = ReadInt( stream )
					Local fps# = ReadFloat(stream)
					Print "Anim chunk. Flags:"+Flags+" Frames:"+Frames+" Fps:"+Fps	
				Case "BONE"
					
					aent.bone = New bone
					While StreamPos(Stream)&lt;Cur.fin
						Int VertId = ReadInt()
						Int VertWeight = ReadFloat()
					Wend
				Case "KEYS"
					
					Local flag = ReadInt(stream)
					While StreamPos(stream)&lt;Cur.fin
						Local frame:Int = ReadInt(Stream)
						Local x#,y#,z#
						Local rw#,rx#,ry#,rz#
						Local sx#,sy#,sz#
						If flag &amp; 1
							x
						EndIf
						
					Wend														

			
			End Select

	'		Print Cur.NAme
			If NewEnt 
				pCount:-1
				NewEnt=False 
			EndIf

			SeekStream Stream,Cur.Fin
			
		Until StreamPos( Stream)=&gt;From.Fin

	End Method
	Field pStack:TEntity[10000],pCount:Int
	Field aEnt:TEntity,aBrush
	Field TmpSurf:TSurface
	Field aSurf:TSurface
	Method ReadText:String()
	
	Local C:Byte,Text:String
	
		Repeat
			
			C = ReadByte( Stream )
			If C = 0 
				
				Return Text
			EndIf
			Text:+Chr( C )
			
		Forever
		
		Return ""
	
	End Method
	


End Type
</pre> <br><br></td></tr></table><br>
<a name="622362"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chris C</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> thanks for that! I'll have to modify it a bit to store vertex info in doubles for ode but I've put your code in my snippets folder for later use! <br><br></td></tr></table><br>
<a name="622872"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AntonyWells</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> No problem. You might wanna use floats instead of doubles when using gl btw. I went doubles mad at first but I found it really slowed things down on my gpu. Not sure if modern cards are optimized for that kinda percision though.<br><br>Back to my problem, i now have the animator code complete, and although the actual pivots(entities) joints are animating correctly(I added a box to each joint to visually see it) the actual mesh deformation is kind of whacky.<br><br>It works fine frame 1. Perfectly looks like psonic's freaky zombie should but as soon as I go up the frames it starts to deform badly.<br>Please if you're reading mark let me know where i'm going wrong. You're probably the only one with enough knowledge of the b3d format here.<br><br>Here's the code that creates the matrix to deform each bone by.<br><br><pre class=code>
Method UpdateAnim()
		For Local s:tentity = EachIn subs
			s.calculateWorldMatrix(Null)
		Next
		TEntity.TmpSurf = RenSurf
		TEntity.Bas = VSurf
		For Local s:TEntity = EachIn subs
			s.TFormBones()
		Next
	End Method
	Global TmpSurf:TSurface
	Global Bas:TSurface
	Method CalculateWorldMatrix(Parent:TMatrix)
	
		Local Tmp:TMatrix = LocalRot.CreateCopy()
		Tmp.Multiply( LocalTrans )
		If Parent&lt;&gt;Null
			Tmp.Multiply( Parent )
		EndIf
		World = Tmp.CreateCopy()
				
		For Local s:Tentity = EachIn subs
			s.calculateWorldMatrix( tmp )
		Next
	
	End Method

</pre><br><br>I multiply each entities rotation and translation matrices by it's parent (Exactly in the same order as the entity is visually drawn)<br><br>Then I call the deformer here which is supposed to transform the base surfaces vert into the visual surface based on each bone.<br>IT does not work, is an understatement.<br><br><pre class=code>
Method TFormBones()
		Local os:TSurface = TEntity.Bas
		Local rs:TSurface = TEntity.TmpSurf
		If Bone = Null 
		Return 
		EndIf
		For Local j=0 Until bone.vc
			Local Vert = bone.vertid[ j ]
			World.GetPosition()
			Local ox#,oy#,oz#
			ox = world.x()
			oy = world.y()
			oz = world.z()
			Local vx#,vy#,vz#
			vx = os.vertexX( vert )
			vy = os.vertexy( vert )
			vz = os.vertexz( vert )
			Local nx#,ny#,nz#
			nx = vx - ox
			ny = vy - oy
			nz = vz - oz
			
			world.tformvector( nx,ny,nz )
			nx = world.tformx
			ny = world.tformy
			nz = world.tformz
			vx = ox + nx
			vy = oy + ny
			vz = oz + nz
			rs.moveVertex( vert,vx,vy,vz )
			
		Next
		
		For Local t:Tentity = EachIn subs
			t.tformbones()
		Next
		
	End Method

</pre><br><br>Here's the code that computers the rotation postion based on the current anim time. IT slerps the rotation quat beween the left most and right most keyframes, and interpolates(I think that's the term) between the left and right anim frames position. <br>Scale is not yet implemeted because the model i'm using has no scale keys so it makes no difference.<br><br><pre class=code>
Type TFrame
	Field Rot:Quaternion
	Field PosX#,posY#,posZ#
	Field scalX#,scaly#,scalz#
	Field Time#
	
	Function MakeFrame:TFrame( L:TFrame,R:TFrame,Scal# )
		
		Local out:TFrame = New tframe
		out.rot = New quaternion
		L.Rot.Slerp( out.rot,R.rot,scal )
		
		'out.rot = l.rot
		
		Local xd#,yd#,zd#
		xd = r.posx-l.posx
		yd = r.posy-l.posy
		zd = r.posz-l.posz
		out.posx = l.posx+xd*scal
		out.posy = l.posy+yd*scal
		out.posz = l.posz+zd*scal
		Return out
					
	End Function
	
End Type
Type TAnim
	Field Frame:TFrame[3000],frames
	
	Method FindLeft:TFrame( time# )
		
		For Local j=time To 1 Step -1
			If frame[j]&lt;&gt;Null
				Return frame[j]
			End If
		Next
			
	End Method
	Method FindRight:TFrame( time#,Ignore:TFrame)
		
		For Local j=time To frames
			If frame[j]&lt;&gt;Null And frame[j]&lt;&gt;ignore
				Return frame[j]
			EndIf
		Next
		
	End Method
	
End Type

Method SetAnimTime( time# )
		AnimTime = time
		Print "SetAnimTime Called."
		If animC &lt;&gt;Null
			
			time = Time Mod AnimC.frames
			If time&lt;1 time =1
			If time&gt;animc.frames
				mainlog.post("Mod failed.",True)
			EndIf
			
			For Local s:tentity = EachIn subs
				s.SetFramer( time )
			Next
						
		Else
			MainLog.Post("Entity has no aasscoiated animation tracks.",True)
		EndIf
	
	End Method
	
	Method SetFramer(Time#)
		If NoAnim = True Return 
		If animC&lt;&gt;Null
			mainlog.post("Cannot directly animate top-tier entity.",True)
		End If
		
		Local LeftFrame:TFrame = Anim.FindLeft( Time )
		Local RightFrame:TFrame = Anim.FindRight( Time,LeftFrame )
		If leftFrame = RightFrame
			cframe.rot = leftFrame.rot
			cframe.posx = leftframe.posx
			cframe.posy = leftframe.posy
			cframe.posz = leftframe.posz
		Else
			
			Local dis# = RightFrame.time-LeftFrame.time
			Local ltime# = time-leftframe.time
			Local scal# = ltime/dis
			cframe = TFrame.MakeFrame( LeftFrame,RightFrame,Scal )
			cframe.rot.toAngles()
			
		EndIf
		
		localrot.rotate( cframe.rot.pitch,cframe.rot.yaw,cframe.rot.roll )
		localtrans.position( cframe.posx,cframe.posy,cframe.posz )
				
		For Local s:tentity = EachIn subs
			s.setframer( time )
		Next
													
	End Method
</pre> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
