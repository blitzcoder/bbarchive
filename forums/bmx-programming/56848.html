<!DOCTYPE html><html lang="en" ><head ><title >Who wrote SortList()?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Who wrote SortList()?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Who wrote SortList()?</a><br><br>
<a name="632019"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >sswift</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Because I checked the function source and it appears to use a BUBBLE SORT, the slowest sorting algorithm on the face of the planet!<br><br>You should use this instead.  It's Mergesort for linked lists:<br><a href="http://www.chiark.greenend.org.uk/~sgtatham/algorithms/listsort.html" target="_blank">http://www.chiark.greenend.org.uk/~sgtatham/algorithms/listsort.html</a><br><br>BubbleSort has a worst case time of O(N^2) and is stable.<br>Merge sort has a worst case time of O(n log n) and is also stable. <br>(Stable means two elements with the same value will retain their relative locations in the list.)<br><br>Merge sort for linked lists also does not require more than a few extra variables of storage to perform the sort.  So it is memory efficient.<br><br>See this page for speed comparisons between sorting algorithms:<br><a href="http://linux.wku.edu/~lamonml/algor/sort/sort.html" target="_blank">http://linux.wku.edu/~lamonml/algor/sort/sort.html</a><br><br>Look at how fast Mergesort is there compared to Bubble sort.  Bubble takes at least several seconds to sort 1000 objects, whereas Merge sort can sort over 100,000 objects in that same time frame.<br><br>Quicksort is the fastest, but much more complicated, and recursive, and performs really slowly on lists that are nearly sorted.  I don't see any mention anywhere about Mergesort having those same issues.<br><br>[edit]<br><br>Aha, busted!<pre class=code>ModuleInfo "Author: Mark Sibly"
</pre>[/edit] <br><br></td></tr></table><br>
<a name="632029"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dreamora</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> BubbleSort has one good thing: Its runtime is not a constant no mather of the dataset (as QuickSort has it for example). It can go down to O(N) on a presorted dataset, while QuickSort will still have to run on its O(n lb(n))   (lb for log binary)<br><br>In a game environment I think especially this special thing is a very great thing as (at least I) enter my data once to a dataset and don't rearrange it anymore afterwards. I only add or remove new thing so it will still run on O(N) with once an O(N^2), while QuickSort would always be O(N lb(N)) which results in quite some time lost just because of its incapability of recognizing presorted data. <br><br></td></tr></table><br>
<a name="632090"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >N</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Actually, sswift, I tried adding mergesort (same page as the one you found) to the linked list, and unfortunately it seems to result in decreased speed (by about 600%).<br><br>I am most likely doing something wrong (I'd bet on it if it wasn't such a stupid bet), so here's the code if anyone wants to take a look at it.<br><br><pre class=code>
    Method Sort( ascending=True )
         Local h:TLink = FirstLink( )
         If h = Null Then Return
         If h.NextLink( ) = Null Then Return ' No need to sort one element
         h = ISort( h, _head )
         _head._succ = h
         While h.NextLink( )
             h = h.NextLink( )
         Wend
         If Not ascending Then Swap Reversed( )
     End Method
     
     ' Internal sort function
     Function ISort:TLink( h:TLink, oh:TLink )
         Local p:TLink, q:TLink, e:TLink, tail:TLink, oldhead:TLink
         Local insize%, merges%, psize%, qsize%, i%
         If h = Null Then Return
         insize = 1
         While True
             p = h
             oldhead = h
             h = Null
             tail = Null
             merges = 0
             While p
                 merges :+ 1
                 q = p
                 psize = 0
                 For i = 0 To insize-1
                    psize :+ 1
                    q = q.NextLink( )
                    If Not q Then Exit
                 Next
                 
                 qsize = insize
                 
                 While psize &gt; 0 Or ( qsize &gt; 0 And q )
                     If psize = 0 Then
                        e = q
                        q = q.NextLink( )
                        qsize :- 1
                     ElseIf qsize = 0 Or Not q
                        e = p
                        p = p.NextLink( )
                        psize :- 1
                     Else
                        Local cmp%
                        If p._value And q._value Then
                             cmp = p._value.Compare( q._value )
                         ElseIf p._value
                             cmp = -1
                         ElseIf q._value
                             cmp = 1
                         Else
                             cmp = 0
                         EndIf
                         
                         If cmp &lt;= 0 Then
                             e = p
                             p = p.NextLink( )
                             psize :- 1
                         Else
                             e = q
                             q = q.NextLink( )
                             qsize :- 1
                         EndIf
                     EndIf
                     
                     If tail Then
                         tail._succ = e
                     Else
                         h = e
                     EndIf
                     
                     tail = e
                     
                 Wend
                 
                 p = q
             Wend
             tail._succ = oh
             h._pred = oh
             If merges &lt;= 1 Then Return h
             insize :* 2
         Wend
     End Function
</pre><br><br>At the moment, I don't have time to go over it and ensure it's working 100% correctly. <br><br></td></tr></table><br>
<a name="632121"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Beaker</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> sswift - not exactly related, but..<br><br>If you have a poke around in BlitzMax internals you will find a thing called TMaps:<br>C:\Program Files\BlitzMax\mod\brl.mod\map.mod\map.bmx<br><br>It is very useful for mapping names to Strings/Objects.  Items are automatically sorted into a binary tree for fast access.  In the example below you will notice that the EachIn loop retrieves the entries already sorted.<br><br>Very simple example:<br><pre class=code>sites:TMap = New TMap

sites.Insert("pf","www.playerfactory.co.uk")
sites.Insert("cw","www.codersworkshop.com")
sites.Insert("bb","www.blitzbasic.com")
sites.Insert("goo","www.google.com")

Print String(sites.ValueForKey("bb"))
Print "==============="

For mysite:TNode = EachIn sites
	Print String(mysite.Key())+"  "+String(mysite.Value())
Next</pre> <br><br></td></tr></table><br>
<a name="632130"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >N</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> And, if you would like a hash table:<br><br><pre class=code>
Rem
    HASHTABLE OBJECT
    
    - M.Laurenson/Defoc8 2006
EndRem

Rem
    Defoc would appreciate if you credited him if you
    used this.  No credit is to go to me.
    
    -Noel
EndRem

Strict

Import Brl.Blitz
Import Brl.LinkedList
Import "hash.c"

Extern "C"
    Function __c_hash:Int(key$z, length:Int, initval:Int)="Hash"
EndExtern

Global HashKey:Int(name:String) = gHashKey

Function gHashKey%( name:String )
    Return __c_hash( name, name.Length, 0 )
End Function

Type gHashTable
    'private:

    Field table:TList[]
    
    'protected:
    
    Method _rement(key:Int)
        For Local entry:gHashEntry = EachIn table[key Shr 24]
            If entry.key = key
                entry.node.Remove( )
                Return
            EndIf
        Next
    End Method
    
    Method _addent(key:Int,n:String,o:Object)
        Local entry:gHashEntry = New gHashEntry
        entry.key = key
        entry.obj = o 
        entry.name = n
        entry.node = table[key Shr 24].AddLast(entry)
    End Method
    
    Method _getent:Object(key:Int)
        For Local entry:gHashEntry = EachIn table[key Shr 24]
            If entry.key = key
                Return entry.obj
            EndIf
        Next
        Return Null
    End Method
    
    'public:
    
    Method New()
        table=New TList[256]
        For Local n:Int = 0 To 255
            table[n] = New TList
        Next
    End Method
    
    Method SetEntry( name:String, obj:Object )
        Local key:Int = HashKey( name )
        Local o:Object = _getent(key)
        If o And o &lt;&gt; obj Then _rement(key)
        _addent(key,name,obj)
    End Method
    
    Method InsertEntry(name:String,obj:Object)
        Local key:Int = HashKey( name )
        _addent(key,name,obj)
    End Method
    
    Method RemoveEntry(name:String)
        Local key:Int = HashKey( name )
        _rement(key)
    End Method
    
    Method GetEntry:Object(name:String)
        Local key:Int = HashKey( name )
        Return _getent(key)
    End Method
    
    Method Flush()
        For Local n:Int = 0 To 255
            table[n].Clear( )
        Next
    End Method 
    
    Method GetEntryCount(index:Int)
        Return table[index].Count( )
    End Method
    
    Method ToArray:Object[]( iters%=0 )
        Local amnt%=0
        For Local i:Int = 0 To 255
            amnt :+ table[i].Count( )
        Next
        Local obj:Object[amnt]
        Local c%=0
        For Local i:Int = 0 To 255
            For Local n:gHashEntry = EachIn table[i]
                If iters &gt; 0 Then
                obj[c] = n
                Else
                obj[c] = n.obj
                EndIf
                c:+1
            Next
        Next
        Debuglog " table size: "+c
        Return obj
    End Method
    
    Method ObjectEnumerator:gHashTableEnum( )
        Local e:gHashTableEnum = New gHashTableEnum
        e.arr = ToArray( )
        Return e
    End Method
End Type

Type gHashTableEnum
    Field idx%=0
    Field arr:Object[]
    
    Method HasNext%( )
        If idx &lt; arr.Length Then Return True
        Debuglog "Has no more elements in the enumerator at idx "+idx
        Debuglog "Array length is "+arr.Length
        Return False
    End Method
    
    Method NextObject:Object( )
        Debuglog " idx: "+idx
        idx :+ 1
        Return arr[idx-1]
    End Method
End Type

Type gHashEntry
    Field key:Int
    Field name:String
    Field obj:Object
    Field node:TLink
End Type
</pre> <br><br></td></tr></table><br>
<a name="632131"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >sswift</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Mapping names?  Not related?<br><br>Poking around in it, it seems very related!<br><br>From what I can tell, I'm pretty sure that you could do this:<br><br>sites.Insert(Order, ThisSprite)<br><br>Then each time you add a sprite to it, it would recurse down the binary tree, and insert the sprite at the correct location.  <br><br>And as in your example above, using EachIn to loop through them would return them in the desired order.<br><br>If you then wanted to change the order of one, you would simply remove it from the list, and then reinsert it with the new Order.<br><br><br>This could prove most useful!  But is it supported?  And is it in the docs anywhere?  <br><br>Do I just have to guess at what the methods do? :-)<br><br><br>I wonder if it is faster to use a binary tree rather than looping through a list and simply inserting the sprite at the right location?  My intutution says "likely", but I'm not sure.  I guess if you end up with a balanced tree, the answer is yes, but if you insert everything in the correct order in the first place you might end up with a string instead of a tree.<br><br>Unfortunately, I'm not very knowledgeable when it comes to binary trees.  I know the basics, but I'd have to go research them to figure out how stepping down a tree and inserting values to the left or right branch if they are greater or less than the current node, leads to a list which is sorted.<br><br>Are you SURE that that thing always sorts the strings correctly? <br><br></td></tr></table><br>
<a name="632133"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >sswift</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> You know reading all marks code, and noel's code... I'm not really sure this OOP stuff actually makes code easier to understand. :-) <br><br></td></tr></table><br>
<a name="632134"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cajun17</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've found that a good sort for a linked list type structure is the selection sort.  The idea is to run through the list and find the smallest or largest element, reduce the size of the unsorted list by 1 and repeat.<br><br>It always does N swaps and (N^2)/2 comparisons.  It's simple to implement so there's no overhead or recursion or function calls. <br><br></td></tr></table><br>
<a name="632146"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> LOL i have allso used merge sort from the same page ;)<br><br>heres my result, its just a little faster then your version noel :/ it must be the overhead of using a double linked list.<br><br>some timings of sorting 100000 integers in a type:<br>mine = 0.199000001<br>noels = 0.257999986<br>array = 0.0869999975<br>array of ints = 0.0120000001<br><br><pre class=code>
Function MergeSortList( slist:TList)
	Local p:TLink, q:TLink, e:TLink, tail:TLink, oldhead:TLink
	Local insize:Int, nmerges:Int, psize:Int, qsize:Int, i:Int
	Local list:TLink
	
	If slist = Null Then Return
	If slist._head._succ &lt;&gt; slist._head Then list = slist._head._succ
	If list = Null Then Return
	
	insize = 1
	
	Repeat
		p = list
		oldhead = list
		list = Null
		tail = Null
		
		nmerges = 0
		
		While p &lt;&gt; Null
			nmerges :+ 1
			q = p
			psize = 0
			For i = 0 Until insize
				psize :+ 1
				If q._succ = oldhead Then
					q = Null
				Else
					q = q._succ
				EndIf
				If q = Null Then Exit
			Next
			
			qsize = insize
			While (psize &gt; 0) Or ((qsize &gt; 0) And (q &lt;&gt; Null))
				If psize = 0 Then
					e = q
					q = q._succ
					qsize :- 1
					If q = oldhead Then q = Null
				ElseIf (qsize = 0) Or (q = Null) Then
					e = p
					p = p._succ
					psize :- 1
					If p = oldhead Then p = Null
				ElseIf p._value.Compare( q._value) &lt;= 0 Then
					e = p
					p = p._succ
					psize :- 1
					If p = oldhead Then p = Null
				Else
					e = q
					q = q._succ
					qsize :- 1
					If q = oldhead Then q = Null
				EndIf
								
				If tail &lt;&gt; Null Then
					tail._succ = e
				Else
					list = e
				EndIf
				e._pred = tail				
				tail = e					
			Wend
			p = q
		Wend
		tail._succ = list
		list._pred = tail
		If nmerges &lt;= 1 Then Return
		insize :* 2
	Forever	
EndFunction
</pre> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
