<!DOCTYPE html><html lang="en" ><head ><title >Going crazy over here...</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Going crazy over here...</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Going crazy over here...</a><br><br>
<a name="1047551"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm trying to reproduce B3D 'entity' system (with parent-child assignments) in 2D for BlitzMax - but something is wrong, and I can't wrap my head around it to find the solution!<br><br>This is what I have:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Type fwEntity
	Field childList:TList
	Field x#,y#,z#
	Field angle#
	Field parent:fwEntity
	
	Field radius#
	Field colMethod%
	
	Method New()
		childList = CreateList()
		parent = Null
	End Method

	Method SetParent(p:fwEntity)
		If parent&lt;&gt;Null And p=Null Then
		   ' new parent is 'Null' (which means no parent)
		   ' remove this entity from ChildList
		   ListRemove parent.childList, Self
		End If
		
		' set new parent
		parent = p
		
		' add this entity on parent's child list
		If p&lt;&gt;Null Then ListAddLast parent.childList, Self
	End Method

	Method Translate(fx#, fy#, fz#, iGlobal%=True)
		If iGlobal Or parent=Null Then
			x:+fx
			y:+fy
			z:+fz
		Else
			' // translate relative to parent???
		End If

		' // also translate children if any
		If childList.IsEmpty()&lt;&gt;True Then 			
			For Local fwe:fwEntity = EachIn childList
				fwe.Translate fx,fy,fz
			Next	
		End If

	End Method

	Method Position(fx#, fy#, fz#, iGlobal%=True)
	Local oldX#=x, oldY#=y, oldZ#=z
	
		If iGlobal Or parent=Null Then
			x = fx
			y = fy
			z = fz
		Else
			If parent&lt;&gt;Null Then
			   ' translate
			   x=(parent.x + fx)
			   y=(parent.y + fy)
			   z=(parent.z + fz)
			End If
		End If
		
		' update children
		If childList.IsEmpty()&lt;&gt;True Then 			
			For Local fwe:fwEntity = EachIn childList
				fwe.translate x - oldx, y - oldy, z - oldz
			Next	
		End If
	End Method

	Method Turn(turnAngle#)
	Local oldAngle# = angle
		
		angle:+turnAngle		
		
		' update children
		If childList.IsEmpty()&lt;&gt;True Then 			
		
			For Local fwe:fwEntity = EachIn childList		
				
				Local dx# = fwe.x - x
				Local dy# = fwe.y - y
				
				Local dist# = Sqr(dx*dx+dy*dy)
				
				fwe.position x + Cos(angle) * dist, y + Sin(angle) * dist, 0

				fwe.angle:+turnAngle
			Next	
		End If
	End Method

End Type

Graphics 800,600

Local e1:fwEntity = New fwEntity
Local e2:fwEntity = New fwEntity

	e1.Position 400,300,0
	
	e2.setParent(e1)
	e2.Position -50, 50,0,False

	While Not KeyHit(KEY_ESCAPE)	
		Cls
		
		SetRotation e1.angle
		SetColor 255,255,255
		DrawRect e1.x, e1.y, 20, 20
		
		SetRotation e2.angle
		SetColor 255,0,0
		DrawRect e2.x, e2.y, 20, 20
		
		SetRotation 0
		SetColor 255,255,255
		DrawText "Hold [SPACE] to turn it", 10,10
		Flip 
		
		If KeyDown(KEY_SPACE) Then e1.turn 1
	Wend

End
</textarea><br><br>So when I turn one object, it's child should turn also, and it's position should be also updated, to keep the original distance from it's parent.<br><br>This B3D code shows how it should behave:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800,600,0,2


cam=CreateCamera()
MoveEntity cam,0,0,-10
CameraProjMode cam, 2

cube1=CreateCube()
cube2=CreateCube(cube1)
MoveEntity cube2, -4,-4,0

ScaleEntity cube1, 0.1,0.1,0.1

While Not KeyHit(1)

	Cls
	RenderWorld
	Text 10,10,"Hold [SPACE] to turn it"
	Flip
	
	If KeyDown(57) Then TurnEntity cube1, 0, 0, -1
Wend

End
</textarea><br><br>Does anyone know how to correct calculate the child position? I'm completely lost here... <br><br></td></tr></table><br>
<a name="1047582"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> can you post running b3d code. I don't own it so I can't tell what you are doing different. I know there are a couple of issues with your code which I believe I resolved but I could be wrong:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Type fwEntity
	Field childList:TList
	Field x#,y#,z#
	Field angle#
	Field parent:fwEntity
	
	Field radius#
	Field colMethod%

	Method New()
		childList = CreateList()
		parent = Null
	End Method

	Method SetParent(p:fwEntity) '*******************************modified 
		' set new parent
		If p &lt;&gt; Null
			parent = p
			'add this entity on parent's child list
			ListAddLast p.childList, Self
		EndIf
	End Method

	Method Translate(fx#, fy#, fz#, iGlobal%=True)
		If iGlobal Or parent=Null Then
			x:+fx
			y:+fy
			z:+fz
		Else
			' // translate relative to parent???
		End If

		' // also translate children if any
		If childList.IsEmpty()&lt;&gt;True Then 			
			For Local fwe:fwEntity = EachIn childList
				fwe.Translate fx,fy,fz
			Next	
		End If

	End Method
	
	Method Position(fx#, fy#, fz#, iGlobal%=True)
		Local oldX#=x, oldY#=y, oldZ#=z
	
		If iGlobal Or parent=Null Then
			x = fx
			y = fy
			z = fz
		Else
			If parent&lt;&gt;Null Then
			   ' translate
			   x=(parent.x + fx)
			   y=(parent.y + fy)
			   z=(parent.z + fz)
			End If
		End If
		
		' update children
		If childList.IsEmpty()&lt;&gt;True Then 			
			For Local fwe:fwEntity = EachIn childList
				fwe.translate x - oldx, y - oldy, z - oldz
			Next	
		End If
	End Method

	Method Turn(turnAngle#)
		angle:+turnAngle		
		
		' update children
		If childList.IsEmpty()&lt;&gt;True Then 			
		
			For Local fwe:fwEntity = EachIn childList		
				
				Local dx# = fwe.x - x
				Local dy# = fwe.y - y
				Local ang# = ATan2(dy,dx)'*****************************************************
				Local dist# = Sqr(dx*dx+dy*dy)

				fwe.position x + Cos(turnAngle+ang) * dist, y + Sin(turnAngle+ang) * dist, 0

				fwe.turn(turnAngle) '*********************************************************
			Next	
		End If
	End Method

End Type

Graphics 800,600

Local e1:fwEntity =New  fwEntity
Local e2:fwEntity =New  fwEntity
Local e3:fwEntity =New  fwEntity	
	e1.Position 400,300,0
	e2.setParent(e1)	
	e2.Position -50, 50,0,False
	e3.setParent(e2)
	e3.position 25, 25,0,False


	While Not KeyHit(KEY_ESCAPE)	
		Cls
		
		SetRotation e1.angle
		SetColor 255,255,255
		DrawRect e1.x, e1.y, 20, 20
		
		SetRotation e2.angle
		SetColor 255,0,0
		DrawRect e2.x, e2.y, 20, 20

		SetRotation e3.angle
		SetColor 0,255,0
		DrawRect e3.x, e3.y, 20, 20
		
		SetRotation 0
		SetColor 255,255,255
		DrawText "Hold [SPACE] to turn it", 10,10
		Flip 
		
		If KeyDown(KEY_SPACE) Then e1.turn 1
	Wend

End

</textarea> <br><br></td></tr></table><br>
<a name="1047588"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes, that did it! Thank you so much!!!!<br><br>I just didn't change SetParent function - I want "SetParent null" to remove the entity parent ;)<br><br>Once again, thank you! <br><br></td></tr></table><br>
<a name="1047604"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> you are welcome.<br><br><div class="quote"> <br>I just didn't change SetParent function - I want "SetParent null" to remove the entity parent ;)<br> <br></div><br>I didn't quite understand that part when I first looked at the code so I just changed it to something that made sense to me but now I get it. <br><br>I like the code. I am going to integrate it in my code and use it for examples and demos if its OK with you. <br><br></td></tr></table><br>
<a name="1047636"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sure, go ahead :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
