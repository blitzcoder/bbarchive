<!DOCTYPE html><html lang="en" ><head ><title >AbstractStream update</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >AbstractStream update</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >AbstractStream update</a><br><br>
<a name="817486"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> This modification will allow you to either specify just the file name to load:<br><br>ReadFile("abstract::test.md3")<br><br>Or you can also just specify the name with no extension:<br><br>ReadFile("abstract::test")<br><br>Obviously you can only have one file named "test.xxx" if you only use the name with no extension.<br><br>This is useful for loading media when you don't know or care exactly where it may be located.  It will automatically detect and read files within zip packages (requires gman.zipengine and koriolis.zipstream).<br><br><br><pre class=code>Private
Global AbstractFileMap:TMap[2]
AbstractFileMap[0]=New TMap
AbstractFileMap[1]=New TMap
Public

Type TAbstractStreamFactory Extends TStreamFactory
	
	Method CreateStream:TStream( url:Object,proto$,path$,readable,writeable )
		If proto&lt;&gt;"abstract" Return Null
		Local filename$
		Local p
		filename=Lower(StripDir(String(url)))
		p=Instr(filename,"::")
		If p filename=Right(filename,Len(filename)-p-1)
		If filename="" Return Null
		If ExtractExt(filename)=""
			filename=String(MapValueForKey(AbstractFileMap[1],filename))
		Else
			filename=String(MapValueForKey(AbstractFileMap[0],filename))
		EndIf
		If filename="" Return Null
		Return ReadStream(filename)
	EndMethod
	
EndType

New TAbstractStreamFactory

Function AbstractFileExists:Int(name$)
	If ExtractExt(name)=""
		If String(MapValueForKey(AbstractFileMap[1],Lower(name)))&lt;&gt;"" Return 1
	Else
		If String(MapValueForKey(AbstractFileMap[0],Lower(name)))&lt;&gt;"" Return 1
	EndIf
EndFunction

Function RegisterAbstractPath(filepath$)	
	Local name$,filename$
	Local d
	Local zrObject:ZipReader
	Local packagefile$
	d=ReadDir(filepath)
	If Not d Return
	If Right(filepath,1)="/" filepath=Left(filepath,Len(filepath)-1)
	If Right(filepath,1)="\" filepath=Left(filepath,Len(filepath)-1)
	Repeat
		filename$=NextFile(d)
		If filename="" Exit
		Select FileType(filepath+"/"+filename)
			Case 0 Exit
			Case 1
				name=Lower(StripDir(filename))
				
				If MapValueForKey(AbstractFileMap[0],name) Continue
				MapInsert AbstractFileMap[0],name,RealPath(filepath+"/"+filename)
				
				If Not MapValueForKey(AbstractFileMap[1],StripExt(name))
					MapInsert AbstractFileMap[1],StripExt(name),RealPath(filepath+"/"+filename)				
				EndIf
				
				'Zip stream support
				Select Lower(ExtractExt(filename))
				Case "zip","pk3"
					packagefile=filename
					zrObject=New ZipReader
					If zrObject.OpenZip(filename)
						For Local i:Int=0 To zrObject.getFileCount()-1
							filename$=zrObject.getFileInfo(i).zipFileName
							If ExtractExt(filename)&lt;&gt;""
								name=Lower(StripDir(filename))
								filename="zip::"+packagefile+"//"+filename								
								If MapValueForKey(AbstractFileMap[0],name) Continue
								MapInsert AbstractFileMap[0],name,filename

								If Not MapValueForKey(AbstractFileMap[1],StripExt(name))
									MapInsert AbstractFileMap[1],StripExt(name),filename			
								EndIf
								
							EndIf
						Next
						zrObject.CloseZip()
					EndIf
				EndSelect
				
			Case 2
				If filename&lt;&gt;"." And filename&lt;&gt;".."
					RegisterAbstractPath(filepath+"/"+filename)
				EndIf
		EndSelect
	Forever
	CloseDir d
EndFunction

Rem
'-----------------------------------------------
' Test Program
'-----------------------------------------------

Local stream:TStream

RegisterAbstractPath(AppDir)

stream=ReadFile("abstract::readme.txt")
If Not stream
	Notify "Failed to open abstract stream ~qreadme~q.",1
	End
EndIf

While Not stream.eof()
	Notify stream.readline()
Wend

End

endrem</pre> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
