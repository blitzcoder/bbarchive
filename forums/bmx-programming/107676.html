<!DOCTYPE html><html lang="en" ><head ><title >[Windows] Layered windows with per-pixel alpha</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >[Windows] Layered windows with per-pixel alpha</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >[Windows] Layered windows with per-pixel alpha</a><br><br>
<a name="1334757"></a>

<a name="1334758"></a>

<a name="1334760"></a>

<a name="1334804"></a>

<a name="1334811"></a>

<a name="1334816"></a>

<a name="1334906"></a>

<a name="1334907"></a>

<a name="1334908"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> This sample uses UpdateLayeredWindow for per-pixel alpha transparency of a window.<br>Im posting it here as well for future reference.<br><br>The image i used: <a href="http://sites.google.com/site/grable0/images/coca.png" target="_blank">coca.png</a><br>But you can use any image with an alpha channel.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

SuperStrict

Import MaxGUI.Drivers

Local pix:TPixmap = LoadPixmap("coca.png").Convert(PF_BGRA8888)
If Not pix Then Throw "no image!"

Local window:TGadget = CreateWindow( "LayeredWindow", 0,0, 256,256, Null, WINDOW_HIDDEN)
Local panel:TGadget = CreatePanel( 0,0, window.Width,window.Height, window, PANEL_ACTIVE) ' for moving the window

' prepare for layered window effects
Local hwnd:Int = window.Query(QUERY_HWND)
Local screendc:Int = GetDC(0)

' create bitmap to hold image with alpha
Local bmp:Int = BitmapFromPixmap32(pix)
Local bmpdc:Int = CreateCompatibleDC(screendc)
SelectObject( bmpdc, bmp)

' init layered window effects
Local zero:Int[2]
Local pos:Int[2]
Local size:Int[] = [ pix.Width, pix.Height]

Local blend:TBlendFunction = New TBlendFunction
blend.BlendOp = AC_SRC_OVER
blend.SourceConstantAlpha = 255
blend.AlphaFormat = AC_SRC_ALPHA

SetWindowLayered hwnd, True
UpdateLayeredWindow hwnd, screendc, pos, size, bmpdc, zero, 0, blend, ULW_ALPHA
ShowWindow( hwnd, True)

' GO!
Local ismoving:Int
Local movex:Int, movey:Int
Local angle:Int

CreateTimer(60)
While WaitEvent()
	Select CurrentEvent.ID
		Case EVENT_MOUSEDOWN
			Local p:Int[2]
			GetCursorPos(p)
			movex = p[0] - pos[0]
			movey = p[1] - pos[1]
			ismoving = True

		Case EVENT_MOUSEUP
			ismoving = False
			
		Case EVENT_KEYDOWN
			If CurrentEvent.Data = KEY_ESCAPE Then Exit
			
		Case EVENT_TIMERTICK
			If ismoving Then
				Local p:Int[2]
				GetCursorPos(p)
				
				pos[0] = p[0] - movex
				pos[1] = p[1] - movey
			EndIf
			
			angle :+ 1
			If angle &gt;= 360 Then angle = 0
			Local s:Float = (3.0 + Sin(angle)) / 4.0
			Local alpha:Int = s * 255
			
			SetPixmapAlpha pix, alpha
			UpdateBitmapFromPixmap bmpdc, bmp, pix
			
			If Not UpdateLayeredWindow( hwnd, screendc, pos, size, bmpdc, zero, 0, blend, ULW_ALPHA) Then				
				PrintLastError("UpdateLayeredWindow")
			EndIf
			' for Windows 7, since it doesnt move the window like Windows 10 does.
			SetGadgetShape window, pos[0],pos[1], window.Width,window.Height

		Case EVENT_WINDOWCLOSE
			Exit			
	EndSelect
Wend
ReleaseDC( 0, screendc)
DeleteObject(bmp)
DeleteDC( bmpdc)
End

Function SetPixmapAlpha( pix:TPixmap, alpha:Byte, lowbound:Byte = 0)
	For Local y:Int = 0 Until pix.Height
		For Local x:Int = 0 Until pix.Width
			Local p:Byte Ptr = pix.PixelPtr(x,y)
			If p[3] &gt; lowbound Then
				p[3] = alpha
			EndIf
		Next
	Next
EndFunction



Type TBlendFunction
	Field BlendOp:Byte
	Field BlendFlags:Byte
	Field SourceConstantAlpha:Byte
	Field AlphaFormat:Byte
EndType

Extern "Win32"
	Const GWL_EXSTYLE:Int = -20
	Const GWL_STYLE:Int = -16
	
	Const WS_EX_TRANSPARENT:Int = $20
	Const WS_EX_LAYERED:Int = $80000
	
	Const LWA_COLORKEY:Int = $1
	Const LWA_ALPHA:Int = $2
	
	Const ULW_ALPHA:Int = $2
	Const ULW_COLORKEY:Int = $1
	Const ULW_OPAQUE:Int = $4
	Const ULW_EX_NORESIZE:Int = $8
	
	Const AC_SRC_ALPHA:Int = $1
	Const AC_SRC_OVER:Int = $0
	
	Function GetWindowLong:Int( hwnd:Int, index:Int) = "GetWindowLongA@8"
	Function SetWindowLong:Int( hwnd:Int, index:Int, value:Int) = "SetWindowLongA@12"

	Function UpdateLayeredWindow:Int( hwnd:Int, destdc:Int, destpoint:Byte Ptr, size:Byte Ptr, srcdc:Int, srcpoint:Byte Ptr, ckey:Int, blendfunc:Byte Ptr, flags:Int)

	Function UpdateLayeredWindowIndirect:Int( hwnd:Int, info:Byte Ptr)
	Function ReleaseDC:Int( hwnd:Int, dc:Int)
	
	Function CreateBitmap:Int( width:Int, height:Int, planes:Int, bitsperpel:Int, pixels:Byte Ptr)
	Function GetCursorPos:Int( p:Byte Ptr)
EndExtern


Function SetWindowLayered( hwnd:Int, layered:Int, transparent:Int = False)
	Local style:Int = GetWindowLong( hwnd, GWL_EXSTYLE)
	If layered Then 
		style :| WS_EX_LAYERED
	Else
		style :&amp; ~WS_EX_LAYERED
	EndIf
	If transparent Then 
		style:| WS_EX_TRANSPARENT
	Else
		style :&amp; ~WS_EX_TRANSPARENT
	EndIf
	SetWindowLong( hwnd, GWL_EXSTYLE, style)
EndFunction

Function BitmapFromPixmap32:Int( pix:TPixmap)
	pix = ConvertPixmap(pix,PF_BGRA8888)
	Return CreateBitmap( pix.Width, pix.Height, 1, 32, pix.Pixels)
EndFunction

Function UpdateBitmapFromPixmap( dc:Int, bmp:Int, pix:TPixmap)
	Local bi:BITMAPINFOHEADER = New BITMAPINFOHEADER
	bi.biSize = SizeOf(bi)
	bi.biWidth = pix.width
	bi.biHeight = -pix.height
	bi.biPlanes = 1
	bi.biBitCount = 32
	bi.biCompression = BI_RGB

	If Not SetDIBits( dc, bmp, 0,pix.Height, pix.pixels, bi, DIB_RGB_COLORS) Then
		PrintLastError("SetDIBits")
	EndIf
EndFunction

Function PrintLastError( funcname:String)
	Const FORMAT_MESSAGE_ALLOCATE_BUFFER:Int = $00000100
	Const FORMAT_MESSAGE_IGNORE_INSERTS:Int = $00000200
	Const FORMAT_MESSAGE_FROM_SYSTEM:Int = $00001000
	
	Const LMEM_ZEROINIT:Int = $40
	
	Extern "C"
		Function wcslen:Int( s:Short Ptr)
		Function fputws( s:Short Ptr, stream:Int)
		Function StringCchPrintfW( dst:Short Ptr, sz:Int, fmt$w, a$w, b:Int, c:Short Ptr)
	EndExtern
	
	Extern "Win32"
		Function LocalAlloc:Byte Ptr( flags:Int, sz:Int)
		Function LocalFree:Int( p:Byte Ptr)
		Function LocalSize:Int( p:Byte Ptr)
		Function GetLastError:Int()
		Function FormatMessageW:Int( flags:Int, source:Short Ptr, msg:Int, lang:Int, buf:Short Ptr Var, size:Int, valist:Byte Ptr)
	EndExtern

	Local msgbuf:Short Ptr
	Local err:Int = GetLastError()
	
	FormatMessageW( FORMAT_MESSAGE_ALLOCATE_BUFFER | FORMAT_MESSAGE_FROM_SYSTEM | FORMAT_MESSAGE_IGNORE_INSERTS, ..
							Null, err, $400, msgbuf, 0, Null)
	
	Local sz:Int = (wcslen(msgbuf) + funcname.Length + 40) * 2
	Local dispbuf:Short Ptr = Short Ptr LocalAlloc( LMEM_ZEROINIT, sz)
	StringCchPrintfW( dispbuf, LocalSize(dispbuf) / 2, "%s failed with error %d: %s", funcname, err, msgbuf)
	fputws( dispbuf, stdout_)
	
	LocalFree(msgbuf)
	LocalFree(dispbuf)
EndFunction

</textarea> <br><br></td></tr></table><br>
<a name="1334776"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Henri</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>at some point you can't drag the picture anymore ?<br><br>-Henri <br><br></td></tr></table><br>
<a name="1334802"></a>

<a name="1334807"></a>

<a name="1334808"></a>

<a name="1334809"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted February)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hmm, a friend of mine complained about the same thing, he is also running Windows 7.<br>Im on Windows 10 and i can drag it as much as i want :/<br><br>Must be some incompatibility between UpdateLayeredWindow in W7 and W10 then....<br><br>Could you check if the various move states are entered?<br>MouseDown/Up, <b>ismoving</b> and that <b>pos</b> really is changing.<br><br>Other than that its UpdateLayeredWindow that is moving the image.<br><br>EDIT: I added some error checking, it should print an error if any of the APIs complain. <br><br></td></tr></table><br>
<a name="1334815"></a>

<a name="1334817"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted February)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think i nailed it down.<br><br>In Windows 10, UpdateLayeredWindow moves the window in addition to drawing at the supplied position, Windows 7 does not.<br>This ends up with MOUSE_UP not firing when moving outside the 256x256 region, the size of the window.<br><br>Adding this after UpdateLayeredWindow on Windows 7 will make it work the same as Windows 10.<pre class=code>
SetGadgetShape window, pos[0],pos[1], window.Width,window.Height
</pre>Added it to top post. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
