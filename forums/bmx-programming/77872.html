<!DOCTYPE html><html lang="en" ><head ><title >Internet GNet Connection</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Internet GNet Connection</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Internet GNet Connection</a><br><br>
<a name="872121"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rimmsy</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, I'm starting to feel the pangs of intense frustration setting in. I've successully been testing my game using Gnet by connecting to 127.0.0.1 but when I attempt to connect to someone else on the internet I don't get any kind of connection. I've also tried connecting to myself using my non-local ip address which doesn't work.<br><br>I'm using mark's gnetchat.bmx as a base to attempt this but everything I do leads me to the inexorable conclusion that it doesn't work.<br><br>I've tried shutting off my windows firewall - doesn't work<br>I've tried opening a port in the firewall - doesn't work<br>I've tried allowing firewall exception access to the gnetchat.debug.exe file - doesn't work<br>I've tried opening the port on my router - doesn't work<br>I've tried setting my router to DMZ - doesn't work<br><br>Am I missing something? Has anyone got this working successully? Is it my setup? Am I protecting my pc so much that I might make it impossible to handle multiplayer games?<br><br>* run the chat and listen<br>* run another chat and connect to the remote IP it states.<br>* no connection<br><br>Any help would be greatly appreciated.<br><br>See below for Mark's code:<br><br>Matt<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

AppTitle="GNet Test2"

Local host:TGNetHost=CreateGNetHost()

Local me:TGNetObject
Local chat$,info$

Graphics 520,200,0,15

Local ip$=GetIP()

Repeat

	Local c=GetChar()
	
	Select c
	Case 8
		If chat chat=chat[..chat.length-1]
	Case 27
		'If Confirm( "Quit?" )
			CloseGNetHost host
			End
		'EndIf
	Case 13
		If chat.find("/")=0
			chat=chat[1..]
			Local cmd$=chat
			Local arg$
			Local i=chat.find(" ")
			If i&lt;&gt;-1
				cmd=chat[..i]
				arg=chat[i+1..]
			EndIf
			Select cmd
			Case "create"
				If me
					info="Already created"
				Else
					me=CreateGNetObject( host )
					SetGNetString me,0,arg
					SetGNetString me,1,"Ready"
				EndIf
			Case "close"
				If me
					CloseGNetObject me
					me=Null
				Else
					info="Not created"
				EndIf
			Case "quit","exit"
				CloseGNetHost host
				End
			Case "nick"
				If arg
					If me SetGNetString me,0,arg
					info="Nick changed to "+arg
				Else
					info="Expecting arg"
				EndIf
			Case "listen"
				Local port=17263
				If arg port=Int(arg)
				If GNetListen( host,port )
					info="Listening on port "+port+" / "+ip
				Else
					info="Listen failed"
				EndIf
			Case "connect"
				If arg
					Local addr$=arg
					Local port=17263
					Local i=arg.find(":")
					If i&lt;&gt;-1
						addr=arg[..i]
						port=Int(arg[i+1..])
					EndIf
					If GNetConnect( host,addr,port )
						info="Connected to "+addr+":"+port
					Else
						info="Failed to connect to "+addr+":"+port
					EndIf
				Else
					info="Expecting arg"
				EndIf
			Default
				info="Unrecognized command '"+cmd+"'"
			End Select
		Else
			If me SetGNetString me,1,chat
		EndIf
		chat=""
	Default
		If c&gt;31 And c&lt;127 chat:+Chr(c)
	End Select
	
	GNetSync host
	
	Cls

	Local y,h=GraphicsHeight()
	
	For Local obj:TGNetObject=EachIn GNetObjects( host,GNET_ALL )
		If obj.state()=GNET_CLOSED Continue
		If obj=me
			SetColor 255,255,255
		Else
			SetColor 0,128,255
		EndIf
		DrawText GetGNetString( obj,0 )+":"+GetGNetString( obj,1 ),0,y
		y:+16
	Next
	
	SetColor 255,0,0
'	Local cons:TList=GNetPeers( host )
	
	SetColor 255,255,0
	DrawText info,0,h-32
	
	SetColor 0,255,0
	DrawText "&gt;"+chat,0,h-16
	DrawRect TextWidth("&gt;"+chat),h-16,8,16
	DrawText "/create nick    /listen    /connect host    /quit    /nick newnick",0,h-48
	
	Flip
	
Forever


Function getIP$()
		Local ip$=""
		'get internet ip address
		'*** you will need to specify your own URl here &amp; change the text search below ***
		Local ipStream:TStream = ReadStream("http::www.whatismyip.com/automation/n09230945.asp") 
	
		If ipStream
			ip$ =ReadLine(ipStream)
		EndIf
		
		CloseStream ipStream
		Return ip
End Function

</textarea> <br><br></td></tr></table><br>
<a name="872142"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dreamora</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> the one that is hosting the server must have portforwarding enabled, it helps nothing if you set the stuff at your end and connect to him as his router / firewall will just drop you. <br><br></td></tr></table><br>
<a name="872144"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >InvisibleKid</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> works fine for me, are you using your real ip or the one your router gives you?<br><br>goto <a href="http://www.whatismyip.com/" target="_blank">http://www.whatismyip.com/</a> to get your ip, it atleast worked for me using the ip it gives me. hope that helps<br><br>EDIT:<br>just reread your post and it sounds like you've already done that. for me and my game that i'm working on my friend can connect to me using the ip that that website gives me.<br><br>heres some test code  i typed up its not much and its not perfect, but it works.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Import BRL.GNet
AppTitle="multitest"
Graphics 640,480
SeedRnd MilliSecs()

'gfx
Global character = LoadImage("character.png")

'var
Global p1x=Rand(0,575),p1y=Rand(0,416)
Global p2x,p2y
Global oldx,oldy
Global numplayers = 1
Global multiOn = 0
Global myStatus$ = "not connected"
Global connected = 0
Global host:TGNetHost = CreateGNetHost()
Global port = 12345
Global ip$ = "127.0.0.1"
Global timeout_ms = 10000 
Global hosting

Global localObj:TGNetObject = CreateGNetObject:TGNetObject( host )
Global remoteObj:TGNetObject
Global objList:TList = New TList 

Const SLOT_CONNECT = 0
Const SLOT_X = 1
Const SLOT_Y = 2

While Not AppTerminate()
Cls
	If multiOn = 0 Or multiOn = 2
		If numplayers = 2
			'player2
			SetColor 255,0,0
			DrawImage character,p2x,p2y
		EndIf
		
		'player1
		SetColor 255,255,255
		DrawImage character,p1x,p1y		
	EndIf	
		
	Keys()		
	
	If connected = 1
		SendInfo()	
		GNetSync host
		ReceiveInfo()		
	EndIf	
			
	If multiOn = 1
		DrawText "[H]ost or [Join]",200,200		
	EndIf
	
SetColor 255,255,255

'debug info
DrawText "p1x: "+p1x,10,50
DrawText "p1y: "+p1y,10,60
DrawText "oldx: "+oldx,10,70
DrawText "oldy: "+oldy,10,80
DrawText "p2x: "+p2x,10,100
DrawText "p2y: "+p2y,10,110

'connection status
DrawText "Players: "+numplayers,10,10
DrawText "Status: "+myStatus$,10,440
If multiOn = 0
	DrawText"M - Start Multiplayer.   Arrow Keys - Move Player.",10,460
Else
	DrawText"Arrow Keys - Move Player.",10,460
EndIf
Flip
Wend
End

Function ConnectTo()
	CloseGNetHost(host)
	host:TGNetHost=CreateGNetHost()
	localObj = CreateGNetObject:TGNetObject( host )

	If hosting = 1
		success = GNetListen( host, port )
		If Not success
			connected = 0
			myStatus$ = "GNetListen failed"			
			multiOn = 0
		Else
			connected = 1			
			myStatus$ = "connected -- HOST"			
			SetGNetInt(localObj, SLOT_CONNECT,1)
			SendInfo()
			multiOn = 2			
		EndIf
	Else
		success = GNetConnect( host, ip$, port, 10000 )
		If Not success
			connected = 0
			myStatus$ = "GNetConnect failed"
			multiOn = 0
		Else
			connected = 1
			myStatus$ = "connected -- CLIENT"
			SetGNetInt(localObj, SLOT_CONNECT,1)
			SendInfo()
			multiOn = 2
		EndIf
	EndIf		
EndFunction

Function Keys()
	If multiOn = 0  Or multiOn = 2
		If KeyDown(key_Left)
			p1x:-10
			If p1x &lt;0 Then p1x = 0
		EndIf
		If KeyDown(key_Right)
			p1x:+10
			If p1x &gt; 575 Then p1x = 575
		EndIf		
		If KeyDown(key_Up)
			p1y:-10
			If p1y &lt;0 Then p1y = 0
		EndIf
		If KeyDown(key_down)
			p1y:+10
			If p1y &gt; 416 Then p1y = 416
		EndIf
		
		If multiOn = 0
			If KeyDown(key_M)
				multiOn = 1
			EndIf
		EndIf
	EndIf
	
	If multiOn = 1
		If KeyDown(key_H)
			hosting = 1
			ConnectTo()
		EndIf
		If KeyDown(key_J)
			hosting = 2
			EnterIP()
		EndIf
	EndIf
	
	If KeyDown(key_Escape) Then End
EndFunction

Function EnterIP()
Local done = 0

	Repeat
	Cls
		DrawText"Enter IP:&gt; "+ip$+"*",200,200
		
		c=GetChar()
		If c
			If c = 8
				If Len(ip$)&gt;0 Then ip$=Left$(ip$,Len(ip$)-1)
			EndIf
			Select Chr(c)
				Case 1
					ip$ = ip$ +Chr(c)
				Case 2
					ip$ = ip$ +Chr(c)
				Case 3
					ip$ = ip$ +Chr(c)
				Case 4
					ip$ = ip$ +Chr(c)
				Case 5
					ip$ = ip$ +Chr(c)
				Case 6
					ip$ = ip$ +Chr(c)
				Case 7
					ip$ = ip$ +Chr(c)
				Case 8
					ip$ = ip$ +Chr(c)
				Case 9
					ip$ = ip$ +Chr(c)
				Case 0
					ip$ = ip$ +Chr(c)
				Case "."
					ip$ = ip$ +Chr(c)
			EndSelect
		EndIf
		
		If KeyHit(key_Enter)			
			done = 1
			Cls
			DrawText"attempting to connect to ip: "+ip$,200,200
		EndIf
	Flip
	Until done = 1
	
	ConnectTo()
EndFunction

Function SendInfo()
	If p1x &lt;&gt; oldx
		oldx = p1x
		SetGNetInt localObj,SLOT_X,p1x
	EndIf
	If p1y &lt;&gt; oldy
		oldy = p1y
		SetGNetInt localObj,SLOT_Y,p1y
	EndIf	
EndFunction

Function ReceiveInfo()
numplayers = 0
objList = GNetObjects( host, GNET_ALL )
	For remoteObj = EachIn objList
		'Local c = GetGNetInt(remoteObj,SLOT_CONNECT)
		'If c = 1 Then numplayers:+1
		numplayers:+GetGNetInt(remoteObj,SLOT_CONNECT)
	Next
	
	objList = GNetObjects( host, GNET_MODIFIED )
	For remoteObj = EachIn objList
		'Local c = GetGNetInt(remoteObj,SLOT_CONNECT)
		'If c = 1 Then numplayers:+1
		'numplayers:+GetGNetInt(remoteObj,SLOT_CONNECT)
		
		p2x = GetGNetInt(remoteObj, SLOT_X)
		p2y = GetGNetInt(remoteObj, SLOT_Y)
	Next	
EndFunction

</textarea> <br><br></td></tr></table><br>
<a name="872148"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rimmsy</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is where it gets frustrating. I have forwarded the port on the router and can't think of anything that might help fix this problem. Even *disabling* firewalls doesn't work!<br><br>What firewall are you using King? The windows firewall and he router firewall? <br><br></td></tr></table><br>
<a name="872152"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >InvisibleKid</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> im using both windows firewall and sygate, sometimes i disable them and sometimes i don't for testing. when i tested your code i didn't disable anything the usual "blah trying blah connect blah...." warnings came up, i allowed and it woked fine. on my router i've tested changing the dmz and not changing before and either way seems to work for me.<br><br>EDIT:<br>i've gotta addmit though, i don't know/understand much about networking and routers ( port forwarding and all that). i just try things and they either work or don't, if they don't i keep trying untill i find a working solution. <br><br></td></tr></table><br>
<a name="872239"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rimmsy</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well thanks for your help, King. I'm about to give up. I've tried for two days and I've had no luck. <br><br></td></tr></table><br>
<a name="872309"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Volker</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Can you ping the IP you want to connect to?<br>This would be a low level test. <br><br></td></tr></table><br>
<a name="872318"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rimmsy</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah, I can ping it and it returns within one hop and under 1ms. I can also ping my local IP, 192.168.1.4 and it also returns within one hop.<br><br>The port is open.<br>the firewall is off<br>the server is listening on the assigned port<br>but I can't connect to the IP.<br><br>It's really quite, quite annoying. <br><br></td></tr></table><br>
<a name="921300"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Foolish</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> I see a lot of demo apps, especially with chat.  But has anybody built a multiplayer game / example App with GNet or RakNet they would be willing to share? <br><br></td></tr></table><br>
<a name="971846"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drackbolt</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm not sure about connecting to anyone else, but to connect to yourself through your router, you probably need to turn off loopback-protection on your router.  On my WRT54G it's a checkbox called "filter internet NAT redirection" and I need to keep it disabled.  It's meant to stop people from soaking up massive bandwidth when reaching local servers by going through the external connection when they should be using local access. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
