<!DOCTYPE html><html lang="en" ><head ><title >the mysterious hanging code</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >the mysterious hanging code</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >the mysterious hanging code</a><br><br>
<a name="1144581"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pineapple</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've got this bit of code I've written.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict
Import brl.math
Import brl.linkedlist
Import "mergelists.bmx"
Import "interpolate.bmx"
Import "colorspace.bmx"
Import brl.random


Graphics 640,480
Local spark:PartTemplate=New PartTemplate
spark.colorspace="hcl"
Local t1%=300
Local t2%=700
Local t3%=950
spark.lifetime=t3+5
spark.blend=lightblend
spark.c0=parameter.Create(4)
spark.c0.setvalue 0,0.135
spark.c0.setvalue 1,0.050,t1
spark.c0.setvalue 2,0.000,t2
spark.c0.setvalue 3,0.000,t3
spark.c0.setoffset -0.08,0.08
spark.c1=parameter.Create(4)
spark.c1.setvalue 0,0.915
spark.c1.setvalue 1,1.000,t1
spark.c1.setvalue 2,0.710,t2
spark.c1.setvalue 3,0.045,t3
spark.c2=parameter.Create(4)
spark.c2.setvalue 0,0.930
spark.c2.setvalue 1,0.690,t1
spark.c2.setvalue 2,0.365,t2
spark.c2.setvalue 3,0.020,t3
spark.c2.setvariation -0.1,0.1
spark.alpha=parameter.Create(3)
spark.alpha.setvalue 0,1.00
spark.alpha.setvalue 0,0.85,t2
spark.alpha.setvalue 0,0.00,t3
spark.vel=parameter.Create(1)
spark.vel.setvalue 0,-1
spark.vel.setoffset -1,0
spark.dir=parameter.Create(1)
spark.vel.setoffset -0.1,0.1
spark.vel.setvariation -1.2,1.2
Local spawn:spawner=spawner.Create(320,320)
Local emit:emitter=spawn.addemitter(emitter.Create(spark))
emit.setquantity 3,6
emit.settime 20,180
emit.setspread 0,6.0,0,1

Local i%=0
Repeat
	Cls
	spawn.update
	setstuff
	Print i;i:+1
	DrawText "FIYAH",2,2
	Flip
Until KeyDown(27) Or AppTerminate()

Function setstuff()
	SetColor 255,255,255
	SetRotation 0
	SetScale 1,1
	SetAlpha 1
	SetBlend alphablend
End Function


Type Spawner
	Field x#,y#,vel#,dir#,seed%
	Field emissions:TList=CreateList()
	Field parts:TList=CreateList()
	Method update(xoffset#=0,yoffset#=0)
		SeedRnd seed
		For Local e:Emitter=EachIn emissions
			listappendlist parts,e.update(x,y,vel,dir)
		Next
		Local link:TLink=parts._head._succ
		While link&lt;&gt;link._value
			Local p:Particle=Particle(link._value)
			Local nlink:TLink=link._succ
			If p.handle(xoffset,yoffset) Then link.remove()
			link=nlink
		Wend
	End Method
	Function Create:spawner(x#,y#,vel#=0,dir#=0)
		Local n:Spawner=New Spawner
		n.x=x;n.y=y
		n.vel=vel;n.dir=dir
		n.seed=0'MilliSecs()
		Return n
	End Function
	Method addemitter:Emitter(e:Emitter)
		emissions.addlast e
		Return e
	End Method
	Method addparticle(p:Particle)
		parts.addlast p
	End Method
End Type

Type Emitter
	Field part:PartTemplate
	Field min_time%,max_time% ' min,max time between emissions
	Field nextemit%=0
	Field min_quantity%=1,max_quantity%=1 ' min,max number of particles created for each emission
	Field min_xspreadradius#,max_xspreadradius#
	Field min_yspreadradius#,max_yspreadradius#
	Field min_vel#,max_vel#
	Field min_dir#,max_dir#
	Method settime(mint%,maxt%)
		min_time=mint
		max_time=maxt
	End Method
	Method setspread(minx#,maxx#,miny#=-1,maxy#=-1)
		min_xspreadradius=minx
		max_xspreadradius=maxx
		If miny=-1 And maxy=-1
			min_yspreadradius=minx
			max_yspreadradius=maxx
		Else
			min_yspreadradius=miny
			max_yspreadradius=maxy
		EndIf
	End Method
	Method setvel(minv#,maxv#)
		min_vel=minv
		max_vel=maxv
	End Method
	Method setdir(mind#,maxd#)
		min_dir=mind
		max_dir=maxd
	End Method
	Method setquantity(minq%,maxq%)
		min_quantity=minq
		max_quantity=maxq
	End Method
	Method setparticle(p:PartTemplate)
		part=p
	End Method
	Function Create:Emitter(part:PartTemplate,initemit%=0)
		Local n:Emitter=New Emitter
		n.part=part
		n.nextemit=MilliSecs()+initemit
		Return n
	End Function
	Method update:TList(x#,y#,vel#,dir#)
		If MilliSecs()=&gt;nextemit Then 
			nextemit=MilliSecs()+Rnd(min_time,max_time+1)
			Return emit(x,y,vel,dir)
		EndIf
		Return Null
	End Method
	Method emit:TList(x#,y#,vel#,dir#)
		Local ret:TList=CreateList()
		For Local x%=1 To getquantity()
			ret.addlast spawn(x,y,vel,dir)
		Next
		Return ret
	End Method
	Method getquantity%()
		Return Rnd(min_quantity,max_quantity+1)
	End Method
	Method spawn:Particle(x#,y#,vel#,dir#)
		Local p:Particle=New Particle
		p.part=part
		Local spreadang#=Rnd(0,360)
		p.x=x+Rnd(min_xspreadradius,max_xspreadradius)
		p.y=y+Rnd(min_yspreadradius,max_yspreadradius)
		p.vel=vel+Rnd(min_vel,max_vel)
		p.dir=dir+Rnd(min_dir,max_dir)
		p.birth=MilliSecs()
		p.lastupdate=p.birth
		Return p
	End Method
End Type

Type Particle
	Field birth% ' when the particle was created
	Field lastupdate% ' when the particle was last updated, in milliseconds
	Field part:PartTemplate
	Field x#,y#,vel#,dir#
	Method time%()
		Return MilliSecs()-birth
	End Method
	Method update%() 'returns 1 if it's expired
		If time()&gt;part.lifetime Then Return 1
		Local now%=MilliSecs()-birth
		Local itime%=now-lastupdate
		lastupdate=now
		Local xy#[]=getvelocity(now)
		x:+xy[0]*itime
		y:+xy[1]*itime
	End Method
	Method draw(xoffset#=0,yoffset#=0)
		Local now%=MilliSecs()-birth
		part.draw x+xoffset,y+yoffset,now
	End Method
	Method handle%(offsetx#=0,offsety#=0)
		If time()&gt;part.lifetime Then Return 1
		draw offsetx,offsety
		update()
		Return 0
	End Method
	Function Create:Particle(template:PartTemplate,x#,y#)
		Local n:Particle=New Particle
		n.part=template
		n.birth=MilliSecs()
		n.x=x
		n.y=y
		Return n
	End Function
	Method getvelocity#[](when%)
		Local xv#,yv#
		If Not part.velmode Then
			xv=vel*Cos(dir)
			yv=vel*Sin(dir)
		EndIf
		If part.vel
			Local v#=part.vel.get(when)
			If part.dir
				Local a#=part.dir.get(when)
				xv:+v*Cos(a)
				yv:+v*Cos(a)
			Else
				xv:+v*Cos(dir)
				yv:+v*Sin(dir)
			EndIf
		EndIf
		Return [xv,yv]
	End Method
End Type
Type PartTemplate
	Field img:TImage,animrate%=40
	Field scale_x:Parameter,scale_y:Parameter
	Field c0:Parameter,c1:Parameter,c2:Parameter,colorspace$="hsl"
	Field alpha:Parameter
	Field rotation:Parameter
	Field velmode%=0 ' 0 means relative, 1 means absolute
	Field vel:Parameter
	Field dir:Parameter
	Field lifetime%=1
	Field blend%=ALPHABLEND
	Method init()
		SetImageHandle img,ImageWidth(img)/2.0,ImageHeight(img)/2.0
	End Method
	Method getframe%(when%)
		Return (when/animrate) Mod img.pixmaps.length
	End Method
	Method draw(x%,y%,when%)
		SetColorRGB ColorRGB(when)
		SetScale getscalex(when),getscaley(when)
		SetAlpha GetAlpha(when)
		SetRotation GetRotation(when)
		SetBlend blend
		render x,y,when
	End Method
	Method render(x%,y%,when%)
		If Not img Then
			Local scx#,scy#;GetScale scx,scy
			DrawRect x-scx/2,y-scy/2,1,1
		Else
			DrawImage img,x,y,getframe(when)
		EndIf
	End Method
	Method GetParam#(what:Parameter,when%,defaultvalue#=0)
		If Not what Then Return defaultvalue
		Return what.get(when)
	End Method
	Method GetAlpha#(when%)
		Return getparam(alpha,when,1)
	End Method
	Method GetRotation#(when%)
		Return getparam(rotation,when,0)
	End Method
	Method getscalex#(when%)
		Return getparam(scale_x,when,1)
	End Method
	Method getscaley#(when%)
		Return getparam(scale_y,when,1)
	End Method
	Method ColorRGB#[](when%)
		Local rgb#[]
		Local values#[]=[c0.get(when),c1.get(when),c2.get(when)]
		Select colorspace
			Case "rgb" rgb=values
			Case "cmy" rgb=cmytorgb(values)
			Case "cmyk" rgb=cmyktorgb(values)
			Case "hsv" rgb=hsvtorgb(values)
			Case "hsl" rgb=hsltorgb(values)
			Case "hcl" rgb=hcltorgb(values)
			Case "xyz" rgb=xyztorgb(values)
			Case "lab" rgb=labtorgb(values)
			Default rgb=[0.0,0.0,0.0]
		End Select
		Return rgb
	End Method
End Type

Type Parameter
	Field value#[] ' value to use
	Field interpolate@[] ' interpolate from current value to next? 1=linear, 2=cosine, 3=cubic
	Field time%[] ' at what time to start using whatever value (first index is discarded since it should always be 0)
	Field offset_min#=0,offset_max#=0 ' offset: unique to a particle
	Field variation_min#=0,variation_max#=0 ' variation: unique to a frame
	Function Create:Parameter(values%=1)
		Local n:Parameter=New Parameter
		n.value=New Float[values]
		n.interpolate=New Byte[values]
		n.time=New Int[values-1]
		Return n
	End Function
	Method setoffset(omin#,omax#)
		offset_min=omin
		offset_max=omax
	End Method
	Method setvariation(vmin#,vmax#)
		variation_min=vmin
		variation_max=vmax
	End Method
	Method setvalue(i%,val#,tim%=0,interp%=1)
		If i&gt;0 Then time[i-1]=tim
		value[i]=val
		interpolate[i]=interp
	End Method
	Method get#(when%)
		Local offset#=getoffset()+getvariation()
		If value.length=1 Then Return value[0]+offset
		Local i%=0,prevtime%=0
		While i&lt;time.length-1 And time[i]&lt;when
			prevtime=time[i]
			i:+1
		Wend
		If interpolate[i]=0 Then Return value[i]
		Local succ%=i+1 ' successor
		If succ&gt;=value.length Then succ=value.length-1
		If succ=i Then Return value[i]+offset ' end of the line
		Local x!=(when-prevtime)/Float(time[i]-prevtime)
		Select interpolate[i]
			Case 1 ' linear
				Return interpolate_linear(value[i],value[succ],x)+offset
			Case 2 ' cosine
				Return interpolate_cosine(value[i],value[succ],x)+offset
			Case 3 ' cubic
				Local prev%=i-1,succ2%=succ+1
				If prev&lt;0 Then prev=0
				If succ2&gt;=value.length Then succ2=value.length-1
				Return interpolate_cubic(value[prev],value[i],value[succ],value[succ2],x)+offset
			Default ' default
				Return offset
		End Select
	End Method
	Method getoffset#()
		Return Rnd(offset_min,offset_max)
	End Method
	Method getvariation#()
		Return Rnd(variation_min,variation_max)
	End Method
End Type</textarea><br><br>it is not consistent, but with the random seed set to 0 like it is, it *usually* hangs immediately before the line Return [xv,yv] inside the method getvelocity#[](when%), at least on my computer<br><br>the imported files:<br><br>mergelists.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">'credit: <a href="http://blitzbasic.com/codearcs/codearcs.php?code=2351" target="_blank">http://blitzbasic.com/codearcs/codearcs.php?code=2351</a>
Function ListAppendList:Int(dest:TList, from:TList)
	Local copy:TList
	If dest = Null Or from = Null Or from.isempty() Then Return False
	copy = from.Copy()
	InsertListBeforeLink(copy, dest._head)
	Return True
End Function
Function InsertListBeforeLink(thislist:TList, beforethis:TLink)
	Local this:TLink = thislist.FirstLink(), last:TLink = thislist.LastLink()
	this._pred = beforethis._pred
	this._pred._succ = this
	last._succ = beforethis
	beforethis._pred = last
End Function
</textarea><br><br>interpolate.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'adapted from the interpolation functions described at <a href="http://freespace.virgin.net/hugo.elias/models/m_perlin.htm" target="_blank">http://freespace.virgin.net/hugo.elias/models/m_perlin.htm</a>

SuperStrict

Import brl.math

' a --- b
Function interpolate_linear!(a#,b#,x!)
	Return a*(1-x)+b*x
End Function
' a --- b
Function interpolate_cosine!(a#,b#,x!)
	Local f!=(1-Cos(x*Pi))*.5
	Return a*(1-f)+b*f
End Function
' a     b --- c     d
Function interpolate_cubic!(a#,b#,c#,d#,x!)
	Local p#=(d-c)-(a-b)
	Return p*x*x*x+((a-b)-p)*x*x+(c-a)*x+b
End Function
</textarea><br><br>colorspace.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
Import brl.retro

' For your convenience, SetColor for an array containing 3 floats indicating rgb
Function SetColorRGB(rgb#[])
	SetColorRGBv rgb[0],rgb[1],rgb[2]
End Function

' SetColor for 3 floats indicating rgb
Function SetColorRGBv(br#,bg#,bb#)
	Local r%=br*255,g%=bg*255,b%=bb*255
	If r&lt;0 r=0
	If g&lt;0 g=0
	If b&lt;0 b=0
	If r&gt;255 r=255
	If g&gt;255 g=255
	If b&gt;255 b=255
	SetColor r,g,b
End Function

' Turn a float array indicating RGB into a byte array
Function ByteRGB@[](rgb#[])
	Return [Byte(rgb[0]*255),Byte(rgb[1]*255),Byte(rgb[2]*255)]
End Function

' Turn a float array indicating RGB into an int array
Function IntRGB%[](rgb#[])
	Return [Int(rgb[0]*255),Int(rgb[1]*255),Int(rgb[2]*255)]
End Function

' the following code was adapted from the examples found at:
' <a href="http://easyrgb.com/index.php?X=MATH&amp;H=02#text2" target="_blank">http://easyrgb.com/index.php?X=MATH&amp;H=02#text2</a>

' observer = 2 degrees, illuminant = D65
Const refx#=95.047
Const refy#=100.0
Const refz#=108.883

' assumes rgb in the range [0,1]
' returns xyz in the range [0,1]
Function rgbtoxyz#[](rgb#[])
	Local r#=rgb[0],g#=rgb[1],b#=rgb[2]
	If(r&gt;0.04045) Then 
		r=((r+0.055)/1.055)^2.4
	Else
		r:/12.92
	EndIf
	If(g&gt;0.04045) Then 
		g=((g+0.055)/1.055)^2.4
	Else
		g:/12.92
	EndIf
	If(b&gt;0.04045) Then 
		b=((b+0.055)/1.055)^2.4
	Else
		b:/12.92
	EndIf
	r:*100
	g:*100
	b:*100
	Return [(r*0.4124+g*0.3576+b*0.1805)/refx,(r*0.2126+g*0.7152+b*0.0722)/refy,(r*0.0193+g*0.1192+b*0.9505)/refz]
End Function
' assumes xyz in the range [0,1]
' returns rgb in the range [0,1]
Function xyztorgb#[](xyz#[])
	Local x#=xyz[0]*refx/100.0,y#=xyz[1]*refy/100.0,z#=xyz[2]*refz/100.0
	Local r#=x* 3.2406+y*-1.5372+z*-0.4986
	Local g#=x*-0.9689+y* 1.8758+z* 0.0415
	Local b#=x* 0.0557+y*-0.2040+z* 1.0570
	If r&gt;0.0031308 Then
		r=1.055*(r^(0.416666667))-.055
	Else
		r:*12.92
	EndIf
	If g&gt;0.0031308 Then
		g=1.055*(g^(0.416666667))-.055
	Else
		g:*12.92
	EndIf
	If b&gt;0.0031308 Then
		b=1.055*(b^(0.416666667))-.055
	Else
		b:*12.92
	EndIf
	Return [r,g,b]
End Function
' assumes xyz in the range [0,1]
' returns l*ab in a very varying range
Function xyztolab#[](xyz#[])
	Local x#=xyz[0],y#=xyz[1],z#=xyz[2]
	If(x&gt;0.008856) Then
		x=x^0.333333333
	Else
		x=(x*7.787)+0.137931034
	EndIf
	If(y&gt;0.008856) Then
		y=y^0.333333333
	Else
		y=(y*7.787)+0.137931034
	EndIf
	If(z&gt;0.008856) Then
		z=z^0.333333333
	Else
		z=(z*7.787)+0.137931034
	EndIf
	Return [116*y-16,500*(x-y),200*(y-z)]
End Function
' assumes l*ab in a very varying range
' assumes xyz in the range [0,1]
Function labtoxyz#[](lab#[])
	Local y#=(lab[0]+16)/116.0
	Local x#=lab[1]/500.0+y
	Local z#=y-lab[2]/200.0
	If x^3&gt;.008856 Then
		x=x^3
	Else
		x=(x-0.137931034)/7.787
	EndIf
	If y^3&gt;.008856 Then
		y=y^3
	Else
		y=(y-0.137931034)/7.787
	EndIf
	If z^3&gt;.008856 Then
		z=z^3
	Else
		z=(z-0.137931034)/7.787
	EndIf
	Return [x,y,z]
End Function
' assumes rgb in the range [0,1]
' returns l*ab in a very varying range
Function rgbtolab#[](rgb#[])
	Return xyztolab(rgbtoxyz(rgb))
End Function
' assumes l*ab in a very varying range
' returns rgb in the range [0,1]
Function labtorgb#[](lab#[])
	Return xyztorgb(labtoxyz(lab))
End Function
' assumes rgb in the range [0,1]
' returns cmy in the range [0,1]
Function rgbtocmy#[](rgb#[])
	Return [1-rgb[0],1-rgb[1],1-rgb[2]]
End Function
' assumes cmy in the range [0,1]
' returns rgb in the range [0,1]
Function cmytorgb#[](cmy#[])
	Return [1-cmy[0],1-cmy[1],1-cmy[2]]
End Function
' assumes cmy in the range [0,1]
' returns cmyk in the range [0,1]
Function cmytocmyk#[](cmy#[])
	Local c#=cmy[0],m#=cmy[1],y#=cmy[2]
	Local k#=1
	If c&lt;k Then k=c
	If m&lt;k Then k=m
	If y&lt;k Then k=y
	If k=1 Then Return [0.0,0.0,0.0,k]
	Local k1#=1-k
	Return [(c-k)/k1,(m-k)/k1,(y-k)/k1,k]
End Function
' assumes cmyk in the range [0,1]
' returns cmy in the range [0,1]
Function cmyktocmy#[](cmyk#[])
	Local k1#=1-cmyk[3]
	Return [cmyk[0]*k1+cmyk[3],cmyk[1]*k1+cmyk[3],cmyk[2]*k1+cmyk[3]]
End Function
' assumes rgb in the range [0,1]
' returns cmyk in the range [0,1]
Function rgbtocmyk#[](rgb#[])
	Return cmytocmyk(rgbtocmy(rgb))
End Function
' assumes cmyk in the range [0,1]
' returns rgb in the range [0,1]
Function cmyktorgb#[](cmyk#[])
	Return cmytorgb(cmyktocmy(cmyk))
End Function

' the following code was adapted from the examples found at:
' <a href="http://mjijackson.com/2008/02/rgb-to-hsl-and-rgb-to-hsv-color-model-conversion-algorithms-in-javascript" target="_blank">http://mjijackson.com/2008/02/rgb-to-hsl-and-rgb-to-hsv-color-model-conversion-algorithms-in-javascript</a>

' assumes rgb in the range [0,1]
' returns hsl in the range [0,1]
Function rgbtohsl#[](rgb#[])
	Local r#=rgb[0],g#=rgb[1],b#=rgb[2]
	Local hsl#[]=New Float[3]
	Local ax#=Max(Max(r,g),b),in#=Min(Min(r,g),b)
	hsl[2]=(ax+in)/2.0
	If(ax=in) Then Return hsl
	Local d#=ax-in
	If hsl[2]&gt;.5 Then hsl[1]=d/(2.0-ax-in) Else hsl[1]=d/(ax+in)
	Select ax
		Case r
			Local gv%=0
			If g&lt;b Then gv=6
			hsl[0]=(g-b)/d+gv
		Case g
			hsl[0]=(b-r)/d+2
		Case b
			hsl[0]=(r-g)/d+4
	End Select
	hsl[0]:/6.0
	Return hsl
End Function
' assumes hsl in the range [0,1]
' returns rgb in the range [0,1]
Function hsltorgb#[](hsl#[])
	Local h#=hsl[0],s#=hsl[1],l#=hsl[2]
	If s=0 Then Return [l,l,l]
	Local q#
	If l&lt;.5 Then q=l*(1+s) Else q=(l+s-l*s)
	Local p#=2*l-q
	Return [_huetorgb(p,q,h+0.333333333),_huetorgb(p,q,h),_huetorgb(p,q,h-0.333333333)]
End Function
Function _huetorgb#(p#,q#,t#)
	If t&lt;0 t:+1
	If t&gt;1 t:-1
	If t&lt;0.166666667 Return p+(q-p)*6*t
	If t&lt;0.5 Return q
	If t&lt;0.666666667 Return p+(q-p)*(0.666666667-t)*6
	Return p
End Function
' assumes rgb in the range [0,1]
' returns hsv in the range [0,1]
Function rgbtohsv#[](rgb#[])
	Local r#=rgb[0],g#=rgb[1],b#=rgb[2]
	Local hsv#[]=New Float[3]
	Local ax#=Max(Max(r,g),b),in#=Min(Min(r,g),b)
	hsv[2]=ax
	Local d#=ax-in
	hsv[1]=0
	If ax&lt;&gt;0 Then hsv[1]=d/ax
	If(ax=in) Then hsv[0]=0;Return hsv
	Select ax
		Case r
			Local gv%=0
			If g&lt;b Then gv=6
			hsv[0]=(g-b)/d+gv
		Case g
			hsv[0]=(b-r)/d+2
		Case b
			hsv[0]=(r-g)/d+4
	End Select
	hsv[0]:/6.0
	Return hsv
End Function
' assumes hsv in the range [0,1]
' returns rgb in the range [0,1]
Function hsvtorgb#[](hsv#[])
	Local h#=hsv[0],s#=hsv[1],v#=hsv[2]
	Local h6#=h*6
	Local i%=h6
	Local f#=h6-i
	Local p#=v*(1-s)
	Local q#=v*(1-f*s)
	Local t#=v*(1-(1-f)*2)
	Select i Mod 6
		Case 0 Return [v,t,p]
		Case 1 Return [q,v,p]
		Case 2 Return [p,v,t]
		Case 3 Return [p,q,v]
		Case 4 Return [t,p,v]
		Case 5 Return [v,p,q]
	End Select
	Return [0.0,0.0,0.0]
End Function

' the following code was derived from the information found at:
' <a href="http://en.wikipedia.org/wiki/HSL_and_HSV#From_luma.2Fchroma.2Fhue" target="_blank">http://en.wikipedia.org/wiki/HSL_and_HSV#From_luma.2Fchroma.2Fhue</a>

Const _b_r#=.3,_b_g#=.59,_b_b#=.11

' assumes hcl in the range [0,1]
' returns rgb in the range [0,1]
Function hcltorgb#[](hcl#[])
	Local h#=hcl[0]*6,c#=hcl[1]
	Local x#=c*(1-Abs((h Mod 2)-1))
	Local rgb#[]
	If 0&lt;=h And h&lt;1
		rgb=[c,x,0.0]
	ElseIf 1&lt;=h And h&lt;2
		rgb=[x,c,0.0]
	ElseIf 2&lt;=h And h&lt;3
		rgb=[0.0,c,x]
	ElseIf 3&lt;=h And h&lt;4
		rgb=[0.0,x,c]
	ElseIf 4&lt;=h And h&lt;5
		rgb=[x,0.0,c]
	ElseIf 5&lt;=h And h&lt;=6
		rgb=[c,0.0,x]
	Else
		rgb=[0.0,0.0,0.0]
	EndIf
	Local m#=hcl[2]-(_b_r*rgb[0]+_b_g*rgb[1]+_b_b*rgb[2])
	rgb[0]:+m
	rgb[1]:+m
	rgb[2]:+m
	Return rgb
End Function
' assumes rgb in the range [0,1]
' returns hcl in the range [0,1]
Function rgbtohcl#[](rgb#[])
	Local r#=rgb[0],g#=rgb[1],b#=rgb[2]
	Local hcl#[]=New Float[3]
	Local m#=Max(Max(r,g),b)
	hcl[1]=m-Min(Min(r,g),b)
	If hcl[1]=0 Then
		hcl[0]=0
	ElseIf m=r
		hcl[0]=((g-b)/hcl[1]) Mod 6
	ElseIf m=g
		hcl[0]=((b-r)/hcl[1])+2
	ElseIf m=b
		hcl[0]=((r-g)/hcl[1])+4
	EndIf
	hcl[0]:/6.0
	hcl[2]=_b_r*r+_b_g*g+_b_b*b
	Return hcl
End Function
</textarea><br><br>what on earth is making it do this? I've debugged it to hell and back and can't figure it out. <br><br></td></tr></table><br>
<a name="1144614"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hiya,<br><br>The problem seems to be in the ListAppendList function.<br>Is there any reason you don't use this version by the same author...<br><pre class=code>
Function ListAppendList:Int(dest:TList, from:TList)
	If dest = Null Or from = Null Or from.Count() = 0 Then Return False
	For Local obj:Object = EachIn from
		dest.AddLast(obj)
	Next
	Return True
End Function
</pre> <br><br></td></tr></table><br>
<a name="1144616"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pineapple</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> well, now I feel silly.<br><br>why on earth wasn't it behaving the same? shouldn't count()=0 be exactly the same as isempty(), just without the whole iterating through the entire list? <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
