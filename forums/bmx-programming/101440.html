<!DOCTYPE html><html lang="en" ><head ><title >MaxLua and pub.Lua</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >MaxLua and pub.Lua</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >MaxLua and pub.Lua</a><br><br>
<a name="1204645"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >juankprada</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Is it possible to mix both?<br>I need to register an BlitzMax object to be used in a Lua script which I see (maybe I am wrong) its only possible with MaxLua. But at the same time I would like the flexibility of pub.Lua (registering floats, integers, strings, etc) <br><br></td></tr></table><br>
<a name="1204663"></a>

<a name="1204665"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is surely not bugfree - but my current implemementation:<br><br>uses pub.lua<br>allows: registering objects in complete or selected ("all", "all but private", "all selected")<br><br><br>The .c-part may be done in a different way:<br><br>basefunctions_lua.c<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
#include &lt;brl.mod/blitz.mod/blitz.h&gt;

#include &lt;pub.mod/lua.mod/lua-5.1.4/src/lua.h&gt;

void lua_boxobject( lua_State *L,BBObject *obj ){
	void *p;
	BBRETAIN( obj );
	p=lua_newuserdata( L,4 );
	*(BBObject**)p=obj;
}

BBObject *lua_unboxobject( lua_State *L,int index ){
	void *p;
	p=lua_touserdata( L,index );
	return *(BBObject**)p;
}

void lua_pushlightobject( lua_State *L,BBObject *obj ){
	lua_pushlightuserdata( L,obj );
}

BBObject *lua_tolightobject( lua_State *L,int index ){
	return (BBObject*)( lua_touserdata( L,index ) );
}

void lua_gcobject( lua_State *L ){
	BBObject *obj=lua_unboxobject( L,1 );
	BBRELEASE( obj );
}
</textarea><br><br><br>basefunctions_lua_threaded.c<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
#ifndef THREADED
	#define THREADED
#endif

#include &lt;brl.mod/blitz.mod/blitz.h&gt;

#include &lt;pub.mod/lua.mod/lua-5.1.4/src/lua.h&gt;

void lua_boxobject( lua_State *L,BBObject *obj ){
	void *p;
	BBRETAIN( obj );
	p=lua_newuserdata( L,4 );
	*(BBObject**)p=obj;
}

BBObject *lua_unboxobject( lua_State *L,int index ){
	void *p;
	p=lua_touserdata( L,index );
	return *(BBObject**)p;
}

void lua_pushlightobject( lua_State *L,BBObject *obj ){
	lua_pushlightuserdata( L,obj );
}

BBObject *lua_tolightobject( lua_State *L,int index ){
	return (BBObject*)( lua_touserdata( L,index ) );
}

void lua_gcobject( lua_State *L ){
	BBObject *obj=lua_unboxobject( L,1 );
	BBRELEASE( obj );
}
</textarea><br><br><br>basefunctions_lua.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
Import Pub.Lua
Import BRL.Reflection
Import brl.retro
'from maxlua
?threaded
Import "basefunctions_lua_threaded.c"
'Import "basefunctions_lua.c"
?not threaded
Import "basefunctions_lua.c"
?
Extern
	Function lua_boxobject( L:Byte Ptr,obj:Object )
	Function lua_unboxobject:Object( L:Byte Ptr,index:Int)
	Function lua_pushlightobject( L:Byte Ptr,obj:Object )
	Function lua_tolightobject:Object( L:Byte Ptr,index:Int )
	Function lua_gcobject( L:Byte Ptr )
End Extern
'end from maxlua


Type TLuaEngine
	Global list:TList	= CreateList()
	Global lastID:Int = 0
	Field id:Int = 0

	Field _luaState:Byte Ptr		'Pointer to current lua environment
	Field _source:String	= ""	'current code
	Field _chunk:Int		= 0		'current code loaded as chunk
	Field _initDone:Int		= 0		'meta table set up finished?
	Field _objMetaTable:Int			'for GC ... from MaxLua
	Field _fenv:Int					'we store other objects in our metatable - we are responsible for them
									'fenv should be known from Lua itself


	Function Create:TLuaEngine( source:String )
		Local obj:TLuaEngine = New TLuaEngine.SetSource( source )
		'init fenv and register self
		obj.RegisterToLua()
		obj.lastID :+1
		obj.id = obj.lastID

		Self.list.addLast(obj)
		Return obj
	End Function


	Method Delete()
		luaL_unref( getLuaState(),LUA_REGISTRYINDEX, _fenv )
		luaL_unref( getLuaState(),LUA_REGISTRYINDEX, _chunk )
	End Method


	Function FindEngine:TLuaEngine(LuaState:Byte Ptr)
		'local num:int = 1
		For Local engine:TLuaEngine = EachIn TLuaEngine.list
			If engine._luaState = LuaState
				'print "engine number "+num
				Return engine
			EndIf
			'num:+1
		Next
		Print "engine not found"
		Return Null
	End Function


	Method getLuaState:Byte Ptr()
		If Not Self._luaState
			Self._luaState=luaL_newstate()
			luaL_openlibs Self._luaState
		EndIf
		Return Self._luaState
	End Method


	Method getSource:String()
		Return _source
	End Method


	Method SetSource:TLuaEngine( source:String )
		_source=source
		If _chunk
			luaL_unref( getLuaState(),LUA_REGISTRYINDEX,_chunk )
			_chunk=0
		EndIf

		Self.RegisterToLua()

		Return Self
	End Method


	'we are parent of other registered objects
	Method RegisterToLua:Int()
		'push class block
		If Not lua_pushchunk() Then Return Null

		'create fenv table
		lua_newtable( getLuaState() )

		'save it
		lua_pushvalue( getLuaState(),-1 )
		_fenv	= luaL_ref( getLuaState(),LUA_REGISTRYINDEX )

		'set self/super object
		lua_pushvalue( getLuaState(),-1 )
		lua_setfield( getLuaState(),-2,"self" )
		lua_pushobject( Self )
		lua_setfield( getLuaState(),-2,"super" )
		'set meta indices
		lua_pushcfunction( getLuaState(), IndexSelf )
		lua_setfield( getLuaState(),-2,"__index" )
		lua_pushcfunction( getLuaState(), NewIndexSelf )
		lua_setfield( getLuaState(),-2,"__newindex" )

		'set fenv metatable
		lua_pushvalue( getLuaState(),-1 )
		lua_setmetatable( getLuaState(),-2 )

		'ready!
		lua_setfenv( getLuaState(),-2 )
		If lua_pcall( getLuaState(),0,0,0 ) Then DumpError()
	End Method


	Method DumpError()
		Print "#################################"
		WriteStdout "LUA ERROR in Engine "+Self.id+"~n"
		WriteStdout lua_tostring( getLuaState(),-1 )+"~n"
		Print "#################################"
	End Method


	Method lua_pushChunk:Int()
		If Not _chunk
			If luaL_loadstring( getLuaState(),_source )
				WriteStdout "Error loading script :~n" + lua_tostring( getLuaState(),-1 ) + "~n"
				lua_pop getLuaState(),1
				Return False
			EndIf
			_chunk=luaL_ref( getLuaState(),LUA_REGISTRYINDEX )
		EndIf
		lua_rawgeti( getLuaState() ,LUA_REGISTRYINDEX,_chunk )
		Return True
	End Method


	' create a table and load with array contents
	Method lua_pushArray( obj:Object )
		Local typeId:TTypeId=TTypeId.ForObject( obj )
		Local size:Int = typeId.ArrayLength(obj)

		lua_createtable( getLuaState(),size+1,0 )

		'lua is not zero based as BlitzMax is... so we have to add one entry at the first pos
		lua_pushinteger( getLuaState(), 0 )
		lua_pushinteger( getLuaState(), -1 )
		lua_settable( getLuaState(), -3 )


		For Local i:Int = 0 until size

			' the index +1 as not zerobased
			lua_pushinteger( getLuaState(), i+1)

			Select typeId.ElementType()
				Case IntTypeId, ShortTypeId, ByteTypeId, LongTypeId
					lua_pushinteger( getLuaState(), typeId.GetArrayElement(obj, i).ToString().ToInt() )
				Case FloatTypeId
					lua_pushnumber( getLuaState(), typeId.GetArrayElement(obj, i).ToString().ToFloat() )
				Case DoubleTypeId
					lua_pushnumber( getLuaState(), typeId.GetArrayElement(obj, i).ToString().ToDouble() )
				Case StringTypeId
					Local s:String = typeId.GetArrayElement(obj, i).ToString()
					lua_pushlstring( getLuaState(), s, s.length )
				Case ArrayTypeId
					Self.lua_pushArray( typeId.GetArrayElement(obj, i) )
				Default ' for everything else, we just push the object..
					Self.lua_pushobject( typeId.GetArrayElement(obj, i) )
			End Select

			lua_settable( getLuaState(), -3 )
		Next
	End Method


	'calls getobjmetatable
	Method lua_pushobject( obj:Object )
		If obj = null then 'mv 06.11.2012: Es ist nötig "null"-Werte in Lua-kompatible "nil"-Werte umzuwandeln.
			lua_pushnil( getLuaState() )
		Else
			lua_boxobject( getLuaState(),obj )
			lua_rawgeti( getLuaState(),LUA_REGISTRYINDEX, getObjMetaTable())
			lua_setmetatable( getLuaState(),-2 )
		Endif
	End Method


	'===============
	'from MaxLua
	'but added _private / _expose ... checks
			Method getObjMetaTable:Int()
				If Not _initDone
					lua_newtable( getLuaState() )
					lua_pushcfunction( getLuaState(),lua_gcobject )
					lua_setfield( getLuaState(),-2,"__gc" )
					lua_pushcfunction( getLuaState(), IndexObject )
					lua_setfield( getLuaState(),-2,"__index" )
					lua_pushcfunction( getLuaState(), NewIndexObject )
					lua_setfield( getLuaState(),-2,"__newindex" )
					_objMetaTable = luaL_ref( getLuaState(), LUA_REGISTRYINDEX )

					_initDone=True
				EndIf
				Return _objMetaTable
			End Method

			'adding a new method/field/func to lua
			Method Index:Int( )
				Local obj:Object		= lua_unboxobject( getLuaState(),1 )
				Local typeId:TTypeId	= TTypeId.ForObject( obj )
				Local ident:String		= lua_tostring( getLuaState(),2 )

				'only expose if type set to get exposed
				if not typeId.MetaData("_exposeToLua") then return false
				local exposeType:string = typeId.MetaData("_exposeToLua")

			'	print "registering ... "+ident

				'PRIVATE...do not add private functions/methods
				'so method _myMethod() is private, same for _myField:int = 0
				'lua constant/var to access global: _G
				if Chr( ident[0] ) =  "_" and ident &lt;&gt; "_G" then return True

				Local mth:TMethod = typeId.FindMethod( ident )
				'thing we have to push is a method
				If mth
					'PRIVATE...do not add private functions/methods
					if mth.MetaData("_private") then return True
					'only expose the children with explicit mention
					if exposeType = "selected" AND not mth.MetaData("_exposeToLua") then return True

					lua_pushvalue( getLuaState(),1 )
					lua_pushlightobject( getLuaState(),mth )
					lua_pushcclosure( getLuaState(),Invoke,2 )
					Return True
				EndIf

				'thing we have to push is a field
				Local fld:TField = typeId.FindField( ident )
				If fld= Null Then Return False

				'PRIVATE...do not add private functions/methods
				if fld.MetaData("_private") then return True
				'only expose the children with explicit mention
				if exposeType = "selected" AND not fld.MetaData("_exposeToLua") then return True

				Select fld.TypeId() ' BaH - added more types
					Case IntTypeId, ShortTypeId, ByteTypeId, LongTypeId
						lua_pushinteger( getLuaState(),fld.GetInt(obj) )
					Case FloatTypeId
						lua_pushnumber( getLuaState(),fld.GetFloat(obj) )
					Case DoubleTypeId
						lua_pushnumber( getLuaState(),fld.GetDouble(obj) )
					Case StringTypeId
						Local t:String = fld.GetString( obj )
						lua_pushlstring( getLuaState(),t,t.length )
					Case ArrayTypeId
						lua_pushArray( fld.Get(obj) )
					Default
						lua_pushobject( fld.Get(obj) )
				End Select
				Return True
			End Method

			Method NewIndex:Int( )
				Local obj:Object		= lua_unboxobject( getLuaState(),1 )
				Local typeId:TTypeId	= TTypeId.ForObject( obj )

				Local ident:String		= lua_tostring( getLuaState(),2 )
				Local mth:TMethod		= typeId.FindMethod( ident )
				If mth Then Throw "newIndex ERROR"

				'only expose if type set to get exposed
				if not typeId.MetaData("_exposeToLua") then print "Lua: Type "+typeId.name()+" not exposed to Lua"; return false
				local exposeType:string = typeId.MetaData("_exposeToLua")


				Local fld:TField=typeId.FindField( ident )
				If fld
					'PRIVATE...do not allow write to  private functions/methods
					'check could be removed if performance critical
					if fld.MetaData("_private") then return True
					'only set values of children with explicit mention
					if exposeType = "selected" AND not fld.MetaData("_exposeToLua") then return True
					if fld.MetaData("_exposeToLua")&lt;&gt;"rw" then print "LUA: "+typeId.name()+"."+ident+" is read-only";return true

					Select fld.TypeId()
						Case IntTypeId, ShortTypeId, ByteTypeId, LongTypeId
							fld.SetInt( obj,lua_tointeger( getLuaState(),3 ) )
						Case FloatTypeId
							fld.SetFloat( obj,lua_tonumber( getLuaState(),3 ) )
						Case DoubleTypeId
							fld.SetDouble( obj,lua_tonumber( getLuaState(),3 ) )
						Case StringTypeId
							fld.SetString( obj,lua_tostring( getLuaState(),3 ) )
						Default
							fld.Set( obj,lua_unboxobject( getLuaState(),3 ) )
					End Select
					Return True
				EndIf
				Print "newindex: ident not found: "+ident
			End Method

			'functions so we can push them to lua

				'they get called if a script tries to run a method/func/field from a blitzmax object
				Function IndexObject:Int( fromLuaState:Byte Ptr )
					Local engine:TLuaEngine = TLuaEngine.FindEngine(fromLuaState)
					If engine And engine.Index( ) Then Return 1
				End Function

				Function NewIndexObject:Int( fromLuaState:Byte Ptr)
					Local engine:TLuaEngine = TLuaEngine.FindEngine(fromLuaState)
					If engine And engine.NewIndex( ) Then Return True
					Throw "newindexobject ERROR"
				End Function


				Function IndexSelf:Int( fromLuaState:Byte Ptr )
					Local engine:TLuaEngine = TLuaEngine.FindEngine(fromLuaState)

					lua_getfield( engine.getLuaState(),1,"super" )
					lua_replace( engine.getLuaState(),1 )
					If engine.Index( ) Then Return 1

					lua_remove( engine.getLuaState(),1 )
					lua_gettable( engine.getLuaState(),LUA_GLOBALSINDEX )
					Return 1
				End Function

				Function NewIndexSelf( fromLuaState:Byte Ptr )
					'local engine:TLuaEngine = TLuaEngine.FindEngine(fromLuaState)
					lua_rawset( fromLuaState,1 )
				End Function

				Function Invoke:Int( fromLuaState:Byte Ptr )
					Local engine:TLuaEngine = TLuaEngine.FindEngine(fromLuaState)
					Return engine._Invoke()
				End Function


			Method _Invoke:Int()
				Local obj:Object		= lua_unboxobject( getLuaState(),LUA_GLOBALSINDEX-1 )
				Local meth:TMethod		= TMethod( lua_tolightobject( getLuaState(),LUA_GLOBALSINDEX-2 ) )
				Local tys:TTypeId[]		= meth.ArgTypes()
				Local args:Object[tys.length]

				For Local i:Int = 0 Until args.length
					Select tys[i]
						Case IntTypeId, ShortTypeId, ByteTypeId, LongTypeId
							args[i]=String.FromInt( lua_tointeger( getLuaState(),i+1 ) )
						Case FloatTypeId
							args[i]=String.FromFloat( lua_tonumber( getLuaState(),i+1 ) )
						Case DoubleTypeId
							args[i]=String.FromDouble( lua_tonumber( getLuaState(),i+1 ) )
						Case StringTypeId
							args[i]=lua_tostring( getLuaState(),i+1 )
						Default
							args[i]=lua_unboxobject( getLuaState(),i+1 )
					End Select
				Next
				Local t:Object=meth.Invoke( obj,args )
				local typeId:TTypeID = meth.TypeId()
				if object[](t).length &gt; 0 then typeId = ArrayTypeId

				Select typeId
					Case IntTypeId, ShortTypeId, ByteTypeId, LongTypeId
						lua_pushinteger( getLuaState(),t.ToString().ToInt() )
					Case FloatTypeId
						lua_pushnumber( getLuaState(),t.ToString().ToFloat() )
					Case DoubleTypeId
						lua_pushnumber( getLuaState(),t.ToString().ToDouble() )
					Case StringTypeId
						Local s:String = t.ToString()
						lua_pushlstring( getLuaState(),s,s.length )
					Case ArrayTypeId
						lua_pushArray( t )
					Default
						lua_pushobject( t )
				End Select
				Return True
			End Method
	'end maxlua import
	'===============


	Method CallLuaFunction:Object(name:String, args:Object[] = Null)
		'push fenv
		lua_rawgeti( getLuaState(),LUA_REGISTRYINDEX,_fenv )


		lua_getfield( getLuaState(),-1,name )
		If lua_isnil( getLuaState(),-1 )
			lua_pop( getLuaState(),2 )
			Return Null
		EndIf
		For Local i:Int = 0 Until args.length
			Local typeId:TTypeId = TTypeId.ForObject( args[i] )
			Select typeId
				Case IntTypeId, ShortTypeId, ByteTypeId, LongTypeId
					lua_pushinteger( getLuaState(), args[i].ToString().ToInt() )
				Case FloatTypeId
					lua_pushnumber( getLuaState(), args[i].ToString().ToFloat() )
				Case DoubleTypeId
					lua_pushnumber( getLuaState(), args[i].ToString().ToDouble() )
				Case StringTypeId
					Local s:String = args[i].ToString()
					lua_pushlstring( getLuaState() ,s,s.length )
				Case ArrayTypeId
					Self.lua_pushArray( args[i] )
				Default
					Self.lua_pushobject( args[i] )
			End Select
		Next
		If lua_pcall( getLuaState(),args.length,1,0 ) Then DumpError()

		Local ret:Object
		If Not lua_isnil( getLuaState(),-1 ) Then ret = lua_tostring( getLuaState(), -1 )

		' pop the result
		lua_pop( getLuaState(),1 )

		' pop the fenv ?
		lua_pop( getLuaState(),1 )

		Return ret
	End Method


	'Once registered, the object can be accessed from within Lua scripts using the @ObjName identifer.
	Method RegisterBlitzmaxObject(Obj:Object, ObjName:String)
		lua_pushobject( obj )
		lua_setglobal( getLuaState(),ObjName )
	End Method


	Method RegisterInt( name:String, value:Int )
		lua_pushinteger getLuaState(),value
		lua_setfield getLuaState(),LUA_GLOBALSINDEX,name
	End Method


	Method RegisterFunction( name:String,value:Byte Ptr )
		lua_pushcclosure( getLuaState(),value,0 )
		lua_setfield( getLuaState(),LUA_GLOBALSINDEX,name )
	End Method
End Type
</textarea><br><br><br><br>Example<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Type TLuaFunctionsAll {_exposeToLua}
	Field readableField:int
	Field writeableField:int {_exposeToLua="rw"}
	Field hiddenField:int {_private}

	Method Test:int()
		return 1
	End Method
End Type

Type TLuaFunctions {_exposeToLua="selected"}
	Field useablefield:int {_exposeToLua}
	Field writeableField:int {_exposeToLua="rw"}
	Field hiddenField:int 'when expose is selected, everything is private except exposed separately

	Method Test:int() {_exposeToLua}
		return 1
	End Method
End Type

local LuaFunctions:TLuaFunctions = new TLuaFunctions
local LuaFunctionsAll:TLuaFunctions = new TLuaFunctionsAll
local LuaScriptString = LoadText("my.lua")

local LuaEngine:TLuaEngine
LuaEngine.RegisterBlitzmaxObject(LuaFunctionsAll, "customFunctions")
LuaEngine.RegisterBlitzmaxObject(LuaFunctions, "otherCustomFunctions")
LuaEngine.setSource(LuaScriptString)

'to call a function within that lua script
'eg. an event handler for save requests (so script serializes its state)
Local args:Object[1]
args[0] = "saveall"
LuaEngine.CallLuaFunction("OnSave", args)
</textarea><br><br><br>What is important: CONST and GLOBALS are not useable in my code, so you have to use Getters for them.<br><br>Most of the code is done by MaxLua already, I think many other thinks I had found in other code.<br><br>Ah, and many users will suggest you to just use the Lua-Glue-Creator also available in the forums but that is a thing of creating code dynamically - so changes need a "rebuild" ...<br><br><br>bye<br>Ron<br><br><br>EDIT:<br>If your "exposed" Type is eg. "TGame"  (as "Game") and this type contains a list of "players:TList" and this TPlayers is exposed too (better with a helper like TGame.GetPlayer:TPlayer(i:int) ... the LUA-Script can even do something like:<br>Game.GetPlayer(1).aPublicFieldOfPlayer.<br><br>So are able to make kind of "collections". <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
