<!DOCTYPE html><html lang="en" ><head ><title >Map Zoom Issue</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Map Zoom Issue</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Map Zoom Issue</a><br><br>
<a name="1283593"></a>

<a name="1283594"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am trying to display a map that I can move around and zoom in. I've got most of it working, but when I zoom it is zooming based on the top left corner and I want it either where the mouse is or in the center of the screen.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

Const SCREEN_WIDTH:Int = 800
Const SCREEN_HEIGHT:Int = 600

Global game:TGame = New TGame

SeedRnd MilliSecs()

game.Init()
game.Update()

End

Type TGame
	Field mapX:Float, mapY:Float
	Field omx:Float, omy:Float
	Field map:TImage
	Field maxTiles:Int
	Field topBorder:Float
	Field bottomBorder:Float
	Field leftBorder:Float
	Field rightBorder:Float
	
	Field zoom:Float = 1
	
	Const MAP_SIZE:Float= 2048
	Const TILE_SIZE:Int = 128

	Method Init()
		Graphics SCREEN_WIDTH, SCREEN_HEIGHT
		
		Local noOfFrames:Int = MAP_SIZE / TILE_SIZE

		maxTiles = noOfFrames

		map = LoadAnimImage("map.jpg", TILE_SIZE, TILE_SIZE, 0, noOfFrames * noOfFrames, FILTEREDIMAGE)
		Assert map, "Error loading map"
		
		For Local i:Int = 1 To 5
			Local t:TTarget = TTarget.Create(Rnd(0, MAP_SIZE), Rnd(0, MAP_SIZE), i)
			t.offx = mapX
			t.offy = mapY
			t.r = Rand(0, 100)
			t.g = Rand(0, 100)
			t.b = Rand(0, 100)
		Next
		
		rightBorder = -MAP_SIZE + SCREEN_WIDTH
		bottomBorder = -MAP_SIZE + SCREEN_HEIGHT
		topBorder = 0
		leftBorder = 0
		
	EndMethod

	Method MoveMap()
	
		Local sx:Int = MouseXSpeed()
		Local sy:Int = MouseYSpeed()
		
		Local minAmount:Float = 0.001
		
		If MouseDown(1)
			mapX :+ sx
			mapY :+ sy
			
			If Abs(sx) &gt; 0 Then omx = sx / 8
			If Abs(sy) &gt; 0 Then omy = sy / 8
		Else
			If Abs(omx) &gt; minAmount
				mapX:+omx
				omx:/1.03
			Else If Abs(omx) &lt;= minAmount
				mapX = Int(mapX)
			EndIf
			If Abs(omy) &gt; minAmount
				mapY:+omy
				omy:/1.03
			Else If Abs(omy) &lt;= minAmount
				mapY = Int(mapY)
			EndIf
		EndIf

		If mapX &lt; rightBorder Then mapX = rightBorder
		If mapX &gt; leftBorder Then mapX = leftBorder
		If mapY &lt; bottomBorder Then mapY = bottomBorder
		If mapY &gt; topBorder Then mapY = topBorder

		Local z:Int = MouseZSpeed()
		If z &gt; 0 Or KeyDown(KEY_W)
			UpdateZoom(0.01)
		ElseIf z &lt; 0 Or KeyDown(KEY_S)
			UpdateZoom(-0.01)			
		EndIf
	EndMethod
	
	Method UpdateZoom(delta:Float)
		zoom :+ delta
		
		Local minScale:Float = SCREEN_WIDTH / MAP_SIZE

		If zoom &lt; minScale Then zoom = minScale
		If zoom &gt; 2 Then zoom = 2
		
		rightBorder = -MAP_SIZE
		rightBorder :* zoom
		rightBorder :+ SCREEN_WIDTH

		bottomBorder = -MAP_SIZE
		bottomBorder :* zoom
		bottomBorder :+ SCREEN_HEIGHT
		
	EndMethod

	Method Update()
		While (Not AppTerminate() And Not KeyHit(KEY_ESCAPE))
			MoveMap()
			TTarget.UpdateAll(mapX, mapY, zoom)
			Render()
		Wend
	EndMethod
	
	Method Render()
		Cls		
			RenderMap()
			TTarget.Render()
			RenderHUD()
		Flip
	EndMethod
	
	Method RenderHUD()
		Local y:Int = 10
		Local gapY:Int = 15
		DrawShadowText("Mouse Fling to Move the Map", 10, y)
		y:+gapY
		DrawShadowText("Map = " + mapX + ", " + mapY, 10, y)
		y:+gapY
		DrawShadowText("Zoom = " + zoom, 10, y)
	EndMethod
	
	Method DrawShadowText(txt:String, x:Int, y:Int)
		SetColor 0, 0, 0
		DrawText txt, x + 2, y + 2
		SetColor 255, 255, 255
		DrawText txt, x, y
	EndMethod
	
	Method RenderMap()
		SetScale(zoom, zoom)		
		Local frame:Int = 0
		For Local j:Int = 0 To maxTiles - 1
			For Local i:Int = 0 To maxTiles - 1
				Local x:Float = mapX + (TILE_SIZE * i) * zoom
				Local y:Float = mapY + (TILE_SIZE * j) * zoom

				DrawImage map, x, y, frame
				frame:+1
			Next
		Next
		SetScale(1, 1)
	EndMethod
EndType

Type TTarget Extends TSprite
	Global targets:TList = New TList
	
	Field id:Int
	Field drawArrow:Int
	Field arrowX:Int, arrowY:Int
	Field arrowAngle:Float
	Field r:Int, g:Int, b:Int
	
	
	Function Create:TTarget(x:Float, y:Float, id:Int)
		Local t:TTarget = New TTarget 
		targets.AddLast(t)
		t.x = x
		t.y = y
		t.ox = x
		t.oy = y
		t.w = 20
		t.h = 20
		t.id = id
		Return t
	EndFunction
	
	Method Draw()
		SetColor r, g, b

		DrawOval (x - w / 2) + offx, (y - h / 2) + offy, w, h
		
		If drawArrow
			SetRotation arrowAngle
			DrawRect(arrowX, arrowY, 40, 4)
			SetRotation 0
			SetColor 255, 255, 255
			DrawText(id, arrowX, arrowY)
		EndIf
		
		SetColor 255, 255, 255
	EndMethod
	
	Function Render()
		For Local t:TTarget= EachIn TTarget.targets
			t.Draw()		
		Next
	EndFunction
	
	Method Update(offx:Float, offy:Float, zoom:Float)
		Self.offx = offx
		Self.offy = offy
		Self.x = Self.ox * zoom
		Self.y = Self.oy * zoom
		
		If TargetOffScreen()
			drawArrow = True
		Else
			drawArrow = False
		EndIf
	EndMethod
	
	Method CalcArrow(ax:Float, ay:Float)
		Local centerX:Float = ax - (SCREEN_WIDTH / 2)
		Local centerY:Float = ay - (SCREEN_HEIGHT / 2)

		' find slope
		Local slope:Float = centerY / centerX
		
		Local pad:Int = 200
		Local paddingX:Int = pad
		Local paddingY:Int = pad
		
		Local padWidth:Float = SCREEN_WIDTH - paddingX
		Local padHeight:Float = SCREEN_HEIGHT - paddingY
		
		If centerY &lt; 0 'top of the screen
			arrowX = (-padHeight / 2) / slope
			arrowY = -padHeight / 2
		Else ' bottom of the screen
			arrowX = (padHeight / 2) / slope
			arrowY = padHeight / 2
		EndIf
		
		If arrowX &lt; - padWidth / 2 ' left side
			arrowX  = -padWidth / 2
			arrowY  = slope * -padWidth / 2
		ElseIf arrowX &gt; padWidth / 2' right side
			arrowX = padWidth / 2
			arrowY = slope * padWidth / 2
		EndIf
		
		arrowAngle = GetAngle2D(SCREEN_WIDTH / 2, SCREEN_HEIGHT/ 2, ax, ay)
		arrowX = arrowX + SCREEN_WIDTH / 2
		arrowY = arrowY + SCREEN_HEIGHT / 2
	EndMethod
	
	Method TargetOffScreen:Int(border:Int = 75)
		Local rv:Int = False
		Local ssx:Float = x + offx
		Local ssy:Float = y + offy
		
		If ssx &lt; border Or ssx &gt; SCREEN_WIDTH - border Or ssy &lt; border Or ssy &gt; SCREEN_HEIGHT - border
			rv = True
		EndIf
		
		If rv
			CalcArrow(ssx, ssy)
		EndIf
		
		Return rv
	EndMethod
	
	Function UpdateAll(offx:Float, offy:Float, zoom:Float)
		For Local t:TTarget= EachIn TTarget.targets
			t.Update(offx, offy, zoom)		
		Next
	EndFunction

EndType

Type TSprite
	Field x:Float, y:Float
	Field w:Float, h:Float
	Field ox:Float, oy:Float
	Field offx:Float, offy:Float
EndType

Function GetAngle2D:Float(x1:Float, y1:Float, x2:Float, y2:Float)
	Local dx:Float = x2 - x1
	Local dy:Float = y2 - y1
	Return ATan2(dy, dx) + 360 Mod 360
EndFunction</textarea><br><br>Image: <a href="https://dl.dropboxusercontent.com/u/35103024/mx/bmx/map/map.zip" target="_blank">https://dl.dropboxusercontent.com/u/35103024/mx/bmx/map/map.zip</a><br><br>I know I need to either offset the mapX/mapY variables or change them when zooming, but I cant work out the math... <br><br></td></tr></table><br>
<a name="1283598"></a>

<a name="1283618"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> This seems to work :<br><pre class=code>
	Method UpdateZoom(delta:Float, mx:Float, my:Float)

		Local oldZoom:Float = zoom

		' coords relative to map
		Local mpx:Float = (mx - mapX) / zoom
		Local mpy:Float = (my - mapY) / zoom

		zoom :+ delta
		
		Local minScale:Float = SCREEN_WIDTH / MAP_SIZE

		If zoom &lt; minScale Then zoom = minScale
		If zoom &gt; 2 Then zoom = 2

		Local realDelta:Float = zoom - oldZoom

		mapX :- (mpx * realDelta)
		mapY :- (mpy * realDelta)

		rightBorder = -MAP_SIZE
		rightBorder :* zoom
		rightBorder :+ SCREEN_WIDTH

		bottomBorder = -MAP_SIZE
		bottomBorder :* zoom
		bottomBorder :+ SCREEN_HEIGHT
		
	EndMethod
</pre><br> Where mx and my are MouseX() and MosuseY()<br><br>.. but ...<br><br>... only if you comment out the lines :<br><pre class=code>
mapX = Int(mapX)
...
mapY = Int(mapY)
</pre><br>in MoveMap().<br>Either that, or you'd have to introduce more variables to keep track of the real offsets. <br><br></td></tr></table><br>
<a name="1283599"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bobysait</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Also, you should check your border tests position in your "movemap" method.<br>Not sure of that, but you seems to test borders before the updatezoom (where you actually update the borders) <br><br></td></tr></table><br>
<a name="1283673"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks a lot Brucey!! I did at one stage have something similar to the realDelta but didnt think of doing the offset on how you did it!<br><br>And thanks Boby, I've moved the UpdateZoom to the top of MoveMap.<br><br>Full updated code (with mouse over):<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

Const SCREEN_WIDTH:Int = 800
Const SCREEN_HEIGHT:Int = 600

Global game:TGame = New TGame

SeedRnd MilliSecs()

game.Init()
game.Update()

End

Type TGame
	Field mapX:Float, mapY:Float
	Field omx:Float, omy:Float
	Field map:TImage
	Field maxTiles:Int
	Field topBorder:Float
	Field bottomBorder:Float
	Field leftBorder:Float
	Field rightBorder:Float
	
	Field zoom:Float = 1
	
	Const MAP_SIZE:Float= 2048
	Const TILE_SIZE:Int = 128

	Method Init()
		Graphics SCREEN_WIDTH, SCREEN_HEIGHT
		
		Local noOfFrames:Int = MAP_SIZE / TILE_SIZE

		maxTiles = noOfFrames

		map = LoadAnimImage("map.jpg", TILE_SIZE, TILE_SIZE, 0, noOfFrames * noOfFrames, FILTEREDIMAGE)
		Assert map, "Error loading map"
		
		For Local i:Int = 1 To 5
			Local t:TTarget = TTarget.Create(Rnd(0, MAP_SIZE), Rnd(0, MAP_SIZE), i)
			t.offx = mapX
			t.offy = mapY
			t.r = Rand(0, 100)
			t.g = Rand(0, 100)
			t.b = Rand(0, 100)
			t.oor = t.r
			t.oog = t.r
			t.oob = t.r
		Next
		
		rightBorder = -MAP_SIZE + SCREEN_WIDTH
		bottomBorder = -MAP_SIZE + SCREEN_HEIGHT
		topBorder = 0
		leftBorder = 0
		
	EndMethod

	Method MoveMap()
		Local z:Int = MouseZSpeed()
		If z &gt; 0 Or KeyDown(KEY_W)
			UpdateZoom(0.05, MouseX(), MouseY())
		ElseIf z &lt; 0 Or KeyDown(KEY_S)
			UpdateZoom(-0.05, MouseX(), MouseY())
		EndIf
		
		Local sx:Int = MouseXSpeed()
		Local sy:Int = MouseYSpeed()
		
		Local minAmount:Float = 0.001
		
		If MouseDown(1)
			mapX :+ sx
			mapY :+ sy
			
			If Abs(sx) &gt; 0 Then omx = sx / 8
			If Abs(sy) &gt; 0 Then omy = sy / 8
		Else
			If Abs(omx) &gt; minAmount
				mapX:+omx
				omx:/1.03
			EndIf
			If Abs(omy) &gt; minAmount
				mapY:+omy
				omy:/1.03
			EndIf
		EndIf

		If mapX &lt; rightBorder Then mapX = rightBorder
		If mapX &gt; leftBorder Then mapX = leftBorder
		If mapY &lt; bottomBorder Then mapY = bottomBorder
		If mapY &gt; topBorder Then mapY = topBorder
	EndMethod
	
	Method UpdateZoom(delta:Float, mx:Float, my:Float)
		Local oldZoom:Float = zoom
		
		' coords relative to map
		Local mpx:Float = (mx - mapX) / zoom
		Local mpy:Float = (my - mapY) / zoom
		
		zoom :+ delta
		
		Local minScale:Float = SCREEN_WIDTH / MAP_SIZE

		If zoom &lt; minScale Then zoom = minScale
		If zoom &gt; 4 Then zoom = 4
		
		Local realDelta:Float = zoom - oldZoom
		
		mapX :- (mpx * realDelta)
		mapY :- (mpy * realDelta)
		
		rightBorder = -MAP_SIZE
		rightBorder :* zoom
		rightBorder :+ SCREEN_WIDTH

		bottomBorder = -MAP_SIZE
		bottomBorder :* zoom
		bottomBorder :+ SCREEN_HEIGHT
		
	EndMethod

	Method Update()
		While (Not AppTerminate() And Not KeyHit(KEY_ESCAPE))
			MoveMap()
			TTarget.UpdateAll(mapX, mapY, zoom)
			Render()
		Wend
	EndMethod
	
	Method Render()
		Cls		
			RenderMap()
			TTarget.Render()
			RenderHUD()
		Flip
	EndMethod
	
	Method RenderHUD()
		Local y:Int = 10
		Local gapY:Int = 15
		DrawShadowText("Mouse Fling to Move the Map", 10, y)
		y:+gapY
		DrawShadowText("Map = " + mapX + ", " + mapY, 10, y)
		y:+gapY
		DrawShadowText("Zoom = " + zoom, 10, y)
	EndMethod
	
	Method DrawShadowText(txt:String, x:Int, y:Int)
		SetColor 0, 0, 0
		DrawText txt, x + 2, y + 2
		SetColor 255, 255, 255
		DrawText txt, x, y
	EndMethod
	
	Method RenderMap()
		SetScale(zoom, zoom)		
		Local frame:Int = 0
		For Local j:Int = 0 To maxTiles - 1
			For Local i:Int = 0 To maxTiles - 1
				Local x:Float = mapX + (TILE_SIZE * i) * zoom
				Local y:Float = mapY + (TILE_SIZE * j) * zoom

				DrawImage map, x, y, frame
				frame:+1
			Next
		Next
		SetScale(1, 1)
	EndMethod
EndType

Type TTarget Extends TSprite
	Global targets:TList = New TList
	
	Field id:Int
	Field drawArrow:Int
	Field arrowX:Int, arrowY:Int
	Field arrowAngle:Float
	Field r:Int, g:Int, b:Int
	Field oor:Int, oog:Int, oob:Int
	
	
	Function Create:TTarget(x:Float, y:Float, id:Int)
		Local t:TTarget = New TTarget 
		targets.AddLast(t)
		t.x = x
		t.y = y
		t.ox = x
		t.oy = y
		t.w = 20
		t.h = 20
		t.id = id
		Return t
	EndFunction
	
	Method Draw()
		SetColor r, g, b

		DrawOval (x - w / 2) + offx, (y - h / 2) + offy, w, h
		
		If drawArrow
			SetRotation arrowAngle
			DrawRect(arrowX, arrowY, 40, 4)
			SetRotation 0
			SetColor 255, 255, 255
			DrawText(id, arrowX, arrowY)
		EndIf
		
		SetColor 255, 255, 255
	EndMethod
	
	Function Render()
		For Local t:TTarget= EachIn TTarget.targets
			t.Draw()		
		Next
	EndFunction
	
	Method Update(offx:Float, offy:Float, zoom:Float)
		Self.offx = offx
		Self.offy = offy
		Self.x = Self.ox * zoom
		Self.y = Self.oy * zoom
		
		If TargetOffScreen()
			drawArrow = True
		Else
			drawArrow = False
		EndIf
		
		If RectsOverlap(MouseX() - offx, MouseY() - offy, 2, 2, x - w / 2, y - h / 2, w, h)
			w = 40
			h = 40
		Else
			w = 20
			h = 20
		EndIf
		
	EndMethod
	
	Method CalcArrow(ax:Float, ay:Float)
		Local centerX:Float = ax - (SCREEN_WIDTH / 2)
		Local centerY:Float = ay - (SCREEN_HEIGHT / 2)

		' find slope
		Local slope:Float = centerY / centerX
		
		Local pad:Int = 200
		Local paddingX:Int = pad
		Local paddingY:Int = pad
		
		Local padWidth:Float = SCREEN_WIDTH - paddingX
		Local padHeight:Float = SCREEN_HEIGHT - paddingY
		
		If centerY &lt; 0 'top of the screen
			arrowX = (-padHeight / 2) / slope
			arrowY = -padHeight / 2
		Else ' bottom of the screen
			arrowX = (padHeight / 2) / slope
			arrowY = padHeight / 2
		EndIf
		
		If arrowX &lt; - padWidth / 2 ' left side
			arrowX  = -padWidth / 2
			arrowY  = slope * -padWidth / 2
		ElseIf arrowX &gt; padWidth / 2' right side
			arrowX = padWidth / 2
			arrowY = slope * padWidth / 2
		EndIf
		
		arrowAngle = GetAngle2D(SCREEN_WIDTH / 2, SCREEN_HEIGHT/ 2, ax, ay)
		arrowX = arrowX + SCREEN_WIDTH / 2
		arrowY = arrowY + SCREEN_HEIGHT / 2
	EndMethod
	
	Method TargetOffScreen:Int(border:Int = 75)
		Local rv:Int = False
		Local ssx:Float = x + offx
		Local ssy:Float = y + offy
		
		If ssx &lt; border Or ssx &gt; SCREEN_WIDTH - border Or ssy &lt; border Or ssy &gt; SCREEN_HEIGHT - border
			rv = True
		EndIf
		
		If rv
			CalcArrow(ssx, ssy)
		EndIf
		
		Return rv
	EndMethod
	
	Function UpdateAll(offx:Float, offy:Float, zoom:Float)
		For Local t:TTarget= EachIn TTarget.targets
			t.Update(offx, offy, zoom)		
		Next
	EndFunction

EndType

Type TSprite
	Field x:Float, y:Float
	Field w:Float, h:Float
	Field ox:Float, oy:Float
	Field offx:Float, offy:Float
EndType

Function GetAngle2D:Float(x1:Float, y1:Float, x2:Float, y2:Float)
	Local dx:Float = x2 - x1
	Local dy:Float = y2 - y1
	Return ATan2(dy, dx) + 360 Mod 360
EndFunction

Function RectsOverlap:Int(x0:Float, y0:Float, w0:Float, h0:Float, x2:Float, y2:Float, w2:Float, h2:Float)
	If x0 &gt; (x2 + w2) Or (x0 + w0) &lt; x2 Then Return False
	If y0 &gt; (y2 + h2) Or (y0 + h0) &lt; y2 Then Return False
	Return True
EndFunction</textarea> <br><br></td></tr></table><br>
<a name="1283677"></a>

<a name="1283678"></a>

<a name="1283679"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'd suggest that you abstract that zoom and moving code into a "game viewport" concept.<br>Your function MoveMap is, conceptually, controlling the player's viewport.<br><br>- The game has a scene. A collection\tree of objects, each with their own properties -- the map, the tiles, the characters, the items.<br>- The game also has a view. The view is a class responsible for using the data and state stored in the scene to display all that to the user in some particular way (like rendering the tiles of the map, the characters etc.). It uses the application window for this.<br><br>The scene and the view are separate classes: you can change the view and how it displays the scene (zoom, screen ratio, rotation, offset etc.), and the scene will remain unchanged. You're not changing characters or tiles, just how the view will display these.<br>When the scene changes -- a character changes state, objects are created or destroyed etc. -- the view updates the game screen to reflect the latest changes (in a game you're usually always updating the screen anyway).<br><br>- - - - -<br><br>The way this works is that you can have multiple views onto the same scene. In an RTS game, for example, you have one view which used for the main screen. You also have a second view, that tiny radar \ minimap in the corner. They are both valid views that are displaying the same scene and interpreting the data stored in the scene in their own particular way. <br><br></td></tr></table><br>
<a name="1283774"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Scaremonger</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> There was a similar post a few years back which might be of some interest:<br><br><a href="/posts.php?topic=80234#909091" target="_blank">Zoom-To-Cursor</a> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
