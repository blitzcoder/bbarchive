<!DOCTYPE html><html lang="en" ><head ><title >What on earth is wrong with my save function?!?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >What on earth is wrong with my save function?!?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >What on earth is wrong with my save function?!?</a><br><br>
<a name="1099406"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AvestheFox</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Apparently I'm doing something wrong here... when this function is called into play it saves a file but there seems to be no data in it... Its almost an exact copy of the save function I was using in Blitz 3D. So what could I be doing wrong here?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Function save_data(tfile$)
	file=WriteFile(tfile)
		For bg:bg_pal=EachIn bg_list
			Writebyte(file,bg.typ)
			Writebyte(file,bg.layer)
		Next
	CloseStream(file)
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1099413"></a>

<a name="1099415"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GfK</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Change closestream to closefile.<br><br>I would also recommend you start using strict mode as it makes debugging a hell of a lot easier.<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1099414"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think we need more information -- it looks fine to me. (I'd have a check for file being non-Null, but if it's creating a file then that's not the problem.)<br><br>This works for me, for example:<br><br><pre class=code>

Global bg_list:TList = CreateList ()

Type bg_pal
	Field typ:Byte
	Field layer:Byte
End Type

Function save_data(tfile$)
	file=WriteFile(tfile)
		For bg:bg_pal=EachIn bg_list
			WriteByte(file,bg.typ)
			WriteByte(file,bg.layer)
		Next
	CloseStream(file)
End Function

For loop = 1 To 10
	bg:bg_pal = New bg_pal
	bg.typ = Rand (0, 255)
	bg.layer = Rand (0, 255)
	ListAddLast bg_list, bg
Next

save_data "test.txt"
</pre><br><br>Is the file 0 bytes in size for you? <br><br></td></tr></table><br>
<a name="1099416"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Probably that bg_list is empty? :-) <br><br></td></tr></table><br>
<a name="1099428"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AvestheFox</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> @blitzsupport<br><br>yeah, the file is empty.. like the data isn't getting out or something. I'll try your code out and see if it fixes it. <br><br></td></tr></table><br>
<a name="1099441"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AvestheFox</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> no progress... so now last resort... here's all the code I have. :P<br><br>Tile Data Editor.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Type typ_button
	Field x,y
	Field img,frame
	Field label$
	Field value
End Type

Type lyr_button
	Field x,y
	Field img,frame
	Field label$
	Field value
End Type

Global typ_list:TList=New TList
Global lyr_list:TList=New TList

Global ttype

Graphics 640,480
Include "palette data.bmx"
Include "tile data.bmx"

setup_palettes(beta_set)
setup_allbuttons()

While Not KeyHit(key_escape)
Cls
If KeyHit(key_up) Then ttype=ttype+1
setup_mouse
render_bgpal(16,16)
render_typbttn(280,16)
render_lyrbttn(280,32)

DrawText "ms_x: "+ms_x,0,0
If KeyHit(key_s) Then save_data("gfx/tilesets/tdata/tset0.tdat")

Flip

Wend
End

Function new_typbttn(name$,value,pos_x,pos_y,width,height)
	typ:typ_button=New typ_button
		typ.label=name
		typ.value=value
		typ.x=pos_x
		typ.y=pos_y
		typ.img=CreateImage(width,height,2)
			SetColor 0,0,100 ; DrawRect 0,0,width,height
			SetColor 255,255,255
			DrawLine 0,0,0,height-1
			DrawLine 0,height-1,width-1,height-1
			DrawLine width-1,height-1,width-1,0
			DrawLine width-1,0,0,0
			DrawText name$,2,2
			GrabImage typ.img,0,0,0
			Cls
			SetColor 0,200,0 ; DrawRect 0,0,width,height
			SetColor 255,255,255
			DrawLine 0,0,0,height-1
			DrawLine 0,height-1,width-1,height-1
			DrawLine width-1,height-1,width-1,0
			DrawLine width-1,0,0,0
			DrawText name$,2,2
			GrabImage typ.img,0,0,1
			Cls
	typ_list.addlast typ
End Function

Function render_typbttn(pos_x,pos_y)
	DrawText "Select tile type:",pos_x+4,pos_y+4
	For typ:typ_button=EachIn typ_list
		If typ.value=curr_typ Then typ.frame=1 Else typ.frame=0
		DrawImage typ.img,typ.x+pos_x,typ.y+pos_y,typ.frame
		If ms_x&gt;typ.x+pos_x And ms_x&lt;typ.x+pos_x+100 And ms_y&gt;typ.y+pos_y And ms_y&lt;typ.y+pos_y+20
			SetColor 255,0,255 
			rect(255,0,100,typ.x+pos_x,typ.y+pos_y,99,19)
			For bg:bg_pal=EachIn bgp_list
				If bg.frame=curr_bg
					If MouseDown(mouse_left) Then bg.typ=typ.value
				End If
			Next
		End If
	Next
End Function

Function new_lyrbttn(name$,value,pos_x,pos_y,width,height)
	lyr:lyr_button=New lyr_button
		lyr.label=name
		lyr.value=value
		lyr.x=pos_x
		lyr.y=pos_y
		lyr.img=CreateImage(width,height,2)
			SetColor 100,0,0 ; DrawRect 0,0,width,height
			SetColor 255,255,255
			DrawLine 0,0,0,height-1
			DrawLine 0,height-1,width-1,height-1
			DrawLine width-1,height-1,width-1,0
			DrawLine width-1,0,0,0
			DrawText name$,2,2
			GrabImage lyr.img,0,0,0
			Cls
			SetColor 0,200,0 ; DrawRect 0,0,width,height
			SetColor 255,255,255
			DrawLine 0,0,0,height-1
			DrawLine 0,height-1,width-1,height-1
			DrawLine width-1,height-1,width-1,0
			DrawLine width-1,0,0,0
			DrawText name$,2,2
			GrabImage lyr.img,0,0,1
			Cls
	lyr_list.addlast lyr
End Function

Function render_lyrbttn(pos_x,pos_y)
	For lyr:lyr_button=EachIn lyr_list
		If lyr.value=curr_layer Then lyr.frame=1 Else lyr.frame=0
		DrawImage lyr.img,lyr.x+pos_x,lyr.y+pos_y,lyr.frame
		If ms_x&gt;lyr.x+pos_x And ms_x&lt;lyr.x+pos_x+100 And ms_y&gt;lyr.y+pos_y And ms_y&lt;lyr.y+pos_y+20
			SetColor 255,0,255 
			rect(255,0,100,lyr.x+pos_x,lyr.y+pos_y,99,19)
			For bg:bg_pal=EachIn bgp_list
				If bg.frame=curr_bg
					If MouseDown(mouse_left) Then bg.layer=lyr.value
				End If
			Next
		End If
	Next
End Function

Function setup_allbuttons()
	new_typbttn("scenery",     scenery,       4,   22, 100,20)
	new_typbttn("obstacle",    obstacle,      4,   44, 100,20)
	new_typbttn("platform",    platform,      4,   66, 100,20)
	new_typbttn("ladder",      ladder,        4,   88, 100,20)
	new_typbttn("lddr_top",    ladder_top,    4,   110,100,20)
	new_typbttn("lddr_bttm",   ladder_bottom, 4,   132,100,20)
	new_typbttn("g_ladder",    g_ladder,      4,   154,100,20)
	new_typbttn("g_ladLeft",   g_ladLeft,     4,   176,100,20)
	new_typbttn("g_ladRight",  g_ladRight,    4,   198,100,20)
	new_typbttn("g_ladTop",    g_ladTop,      4,   220,100,20)
	new_typbttn("g_ladBottom", g_ladBottom,   108, 22, 100,20)
	new_typbttn("g_ladTL",     g_ladTL,       108, 44, 100,20)
	new_typbttn("g_ladTR",     g_ladTR,       108, 66, 100,20)
	new_typbttn("g_ladBL",     g_ladBL,       108, 88, 100,20)
	new_typbttn("g_ladBR",     g_ladBR,       108, 110,100,20)
	new_typbttn("UL_Slope",    UL_Slope,      108, 132,100,20)
	new_typbttn("LL_Slope",    LL_Slope,      108, 154,100,20)
	new_typbttn("UR_Slope",    UR_Slope,      108, 176,100,20)
	new_typbttn("LR_Slope",    LR_Slope,      108, 198,100,20)

	new_lyrbttn("background",  background,    4,   230,100,20)
	new_lyrbttn("foreground",  foreground,    108, 230,100,20)
End Function

Function save_data(tfile$)
	file=WriteFile(tfile)
		For bg:bg_pal=EachIn bg_list
			WriteInt(file,bg.typ)
			WriteInt(file,bg.layer)
		Next
	CloseStream(file)
End Function

</textarea><br><br>Palette Data.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Type bg_pal
	Field x,y
	Field img
	Field frame
	Field typ:Byte
	Field layer:Byte
End Type

Type fg_pal
	Field x,y
	Field img
	Field frame
End Type

Global bgp_list:TList=CreateList()
Global fgp_list:TList=CreateList()

Global mouse=CreateImage(1,1,1,dynamicimage)

Global ms_x#,ms_y#
Global bgsel_x,bgsel_y
Global fgsel_x,fgsel_y
Global tlayer
Global curr_tile, curr_bg, curr_fg
Global curr_typ, curr_layer
Global startup

Function read_current()
If tlayer=0 Then curr_tile=curr_bg
If tlayer=1 Then curr_tile=curr_fg
End Function

Function setup_mouse()
	ms_x=MouseX() ; ms_y=MouseY()
End Function

Function setup_bgpal(tset)
	file=ReadFile("gfx/tilesets/tdata/tset0.tdat")
	For y=0 To 15
	For x=0 To 15
		tcount=tcount+1
			bg:bg_pal=New bg_pal
			bg.x=x*16 ; bg.y=y*16
			bg.img=bgset0
			bg.frame=tcount-1
			bg.typ=ReadByte(file)
			bg.layer=ReadByte(file)
		bgp_list.AddLast bg
	Next
	Next
	CloseStream(file)
End Function

Function render_bgpal(pos_x,pos_y)
	For bg:bg_pal=EachIn bgp_list
		rect(40,40,40,bg.x+pos_x,bg.y+pos_y,16,16)
		SetColor 175,175,175 ; DrawImage bg.img,bg.x+pos_x,bg.y+pos_y,bg.frame
		rect(255,255,255,pos_x,pos_y,256,256)
	Next
	select_bgpal(pos_x,pos_y)
End Function

Function select_bgpal(pos_x,pos_y)
	'startup=True
	'If startup=True Then sel_x=pos_x ; sel_y=pos_y ; startup=False
	For bg:bg_pal=EachIn bgp_list
		If ms_x&gt;pos_x+bg.x-1 And ms_x&lt;pos_x+bg.x+16 And ms_y&gt;pos_y+bg.y-1 And ms_y&lt;pos_y+bg.y+16
			rect(255,0,0,bg.x+pos_x,bg.y+pos_y,16,16)
			If MouseDown(mouse_left) Then bgsel_x=pos_x+bg.x ; bgsel_y=pos_y+bg.y
		End If
		If KeyHit(key_left) Then bgsel_x=bgsel_x-16
		If KeyHit(key_right) Then bgsel_x=bgsel_x+16
		If KeyHit(key_down) Then bgsel_y=bgsel_y+16
		If KeyHit(key_up) Then bgsel_y=bgsel_y-16
		If bgsel_y&lt;pos_y Then bgsel_y=pos_y+240
		If bgsel_y&gt;pos_y+240 Then bgsel_y=pos_y
		If bgsel_x&lt;pos_x Then bgsel_x=pos_x+240
		If bgsel_x&gt;pos_x+240 Then bgsel_x=pos_x
		If bgsel_x=pos_x+bg.x And bgsel_y=pos_y+bg.y Then 
			curr_bg=bg.frame
			curr_typ=bg.typ
			curr_layer=bg.layer
		End If
		If bg.frame=curr_bg Then rect(255,255,255,bg.x+pos_x,bg.y+pos_y,16,16)
	Next
End Function

Function setup_fgpal(tset)
	For y=0 To 15
	For x=0 To 15
		tcount=tcount+1
		fg:fg_pal=New fg_pal
		fg.x=x*16 ; fg.y=y*16
		fg.img=fgset0
		fg.frame=tcount-1
		fgp_list.AddLast fg
	Next
	Next
End Function

Function render_fgpal(pos_x,pos_y)
	For fg:fg_pal=EachIn fgp_list
		rect(40,40,40,fg.x+pos_x,fg.y+pos_y,16,16)
		SetColor 175,175,175 ; DrawImage fg.img,fg.x+pos_x,fg.y+pos_y,fg.frame
		rect(255,255,255,pos_x,pos_y,256,256)
	Next
	select_fgpal(pos_x,pos_y)
End Function

Function select_fgpal(pos_x,pos_y)
	'startup=True
	'If startup=True Then sel_x=pos_x ; sel_y=pos_y ; startup=False
	For fg:fg_pal=EachIn fgp_list
		If ms_x&gt;pos_x+fg.x-1 And ms_x&lt;pos_x+fg.x+16 And ms_y&gt;pos_y+fg.y-1 And ms_y&lt;pos_y+fg.y+16
			rect(255,0,0,fg.x+pos_x,fg.y+pos_y,16,16)
			If MouseDown(mouse_left) Then fgsel_x=pos_x+fg.x ; fgsel_y=pos_y+fg.y
		End If
		If KeyHit(key_left) Then fgsel_x=fgsel_x-16
		If KeyHit(key_right) Then fgsel_x=fgsel_x+16
		If KeyHit(key_down) Then fgsel_y=fgsel_y+16
		If KeyHit(key_up) Then fgsel_y=fgsel_y-16
		If fgsel_y&lt;pos_y Then fgsel_y=pos_y+240
		If fgsel_y&gt;pos_y+240 Then fgsel_y=pos_y
		If fgsel_x&lt;pos_x Then fgsel_x=pos_x+240
		If fgsel_x&gt;pos_x+240 Then fgsel_x=pos_x
		If fgsel_x=pos_x+fg.x And fgsel_y=pos_y+fg.y Then curr_fg=fg.frame
		If fg.frame=curr_fg Then rect(255,255,255,fg.x+pos_x,fg.y+pos_y,16,16)
	Next
End Function

Function setup_palettes(tset)
	setup_bgpal(tset)
	setup_fgpal(tset)
End Function

Function rect(bred,bblue,bgreen,pos_x,pos_y,width,height)
	SetColor bred,bgreen,bblue
		DrawLine pos_x,pos_y,pos_x,pos_y+height
		DrawLine pos_x,pos_y+height,pos_x+width,pos_y+height
		DrawLine pos_x+width,pos_y+height,pos_x+width,pos_y
		DrawLine pos_x+width,pos_y,pos_x,pos_y
	SetColor 255,255,255
End Function 
</textarea><br><br>Tile Data.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Type BGtile
	Field x,y
	Field img
	Field frame
	Field typ
	Field layer
End Type

Type FGtile
	Field x,y
	Field img
	Field frame
End Type

Global bg_list:TList=New TList
Global fg_list:TList=New TList

Const scenery       = 0
Const obstacle      = 1
Const platform      = 2
Const ladder        = 3
Const ladder_top    = 4
Const ladder_bottom = 5
Const g_ladder      = 6
Const g_ladLeft     = 7
Const g_ladRight    = 8
Const g_ladTop      = 9
Const g_ladBottom   = 10
Const g_ladTL       = 11
Const g_ladTR       = 12
Const g_ladBL       = 13
Const g_ladBR       = 14
Const UL_slope      = 15
Const LL_slope      = 16
Const UR_slope      = 17
Const LR_slope      = 18

Const background=0
Const foreground=1

Global bgset0=LoadAnimImage("gfx/tilesets/tset0.png",16,16,0,256)
Global fgset0=LoadAnimImage("gfx/tilesets/tset0.png",16,16,256,256)
</textarea> <br><br></td></tr></table><br>
<a name="1099460"></a>

<a name="1099462"></a>

<a name="1099463"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> this works for me:<br><br><pre class=code>
Global bg_list:TList = CreateList ()

Type bg_pal
	Field typ:Byte
	Field layer:Byte
End Type

Function save_data(tfile$)
	file=WriteFile(tfile)
	If file = Null Print "unable to open "+tfile End
		For bg:bg_pal=EachIn bg_list
			WriteByte(file,bg.typ)
			WriteByte(file,bg.layer)
		Next
	CloseStream(file)
End Function

For loop = 1 To 10
	bg:bg_pal = New bg_pal
	bg.typ = Rand (0, 255)
	bg.layer = Rand (0, 255)
	ListAddLast bg_list, bg
Next

save_data "test.txt"

file = ReadFile("test.txt")
While Not Eof(file)
	Print ReadByte(file)
Wend
CloseStream(file)
</pre><br><br>what operating system are you using?<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1099465"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AvestheFox</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Win XP sp3 <br><br></td></tr></table><br>
<a name="1099487"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> it might be your modules. Try to rebuild them. <br><br></td></tr></table><br>
<a name="1099488"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Zeke</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> you are not using bg_list. only bgp_list. so, bg_list is empty. <br><br></td></tr></table><br>
<a name="1099491"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hmm... re: my previous post.. :-)<br><br>Which was the only logical reason for an empty file to be created... <br><br></td></tr></table><br>
<a name="1099510"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AvestheFox</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> <img src="http://www.ponychan.net/chan/collab/src/131244671296.png"><br><br>dohoho I made a funny.<br><br>and derp<br><br>Thanks guys for the help :) <br><br></td></tr></table><br>
<a name="1099518"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GfK</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just to reiterate, if you'd been using Strict mode your original code would not even have compiled, and the error would have been caught immediately. <br><br></td></tr></table><br>
<a name="1099522"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AvestheFox</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> how do I switch modes?<br><br>I'm still using the BMax trial version if that helps <br><br></td></tr></table><br>
<a name="1099526"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JazzieB</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> By putting Strict or SuperStrict at the beginning of your code.  One is more strict than the other.  I use SuperStrict myself. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
