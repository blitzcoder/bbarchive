<!DOCTYPE html><html lang="en" ><head ><title >Lines and Vectors Intersection</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Lines and Vectors Intersection</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Lines and Vectors Intersection</a><br><br>
<a name="584733"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Haramanai</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I hope I am not writing down something that allready had been  posted, but here I go:<br>I am trying to get the intersection point of two lines but as we know lines in mathimatics starts from negative infinity and gos to positive infinty. I found how to get the intersection point of two lines in the great site MathWorld in the page : <a href="http://mathworld.wolfram.com/Line-LineIntersection.html" target="_blank">http://mathworld.wolfram.com/Line-LineIntersection.html</a><br>So I wroted a litle program to get this point. And interact a litle bit with the lines.<br>Here it is.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Framework brl.Math
Import BRL.StandardIO
Import BRL.Max2d
Import BRL.GLMax2D

Global x1:Int = 10
Global y1:Int = 10
Global x2:Int = 100
Global y2:Int = 160
Global x3:Int = 10
Global y3:Int = 100
Global x4:Int = 100
Global y4:Int = 60



Local xi = ( (x1 * y2 - x2 * y1) * (x3 - x4) - (x3 * y4 - x4 * y3) * (x1 - x2) ) / ( (x1 - x2) * (y3 - y4) - (x3 - x4) * (y1 - y2) )
Local yi = ( (x1 * y2 - x2 * y1) * (y3 - y4) - (x3*y4 - x4 * y3) * (y1 - y2) ) / ( (x1 - x2) * (y3 - y4) - (x3 - x4) * (y1 - y2) )


SetGraphicsDriver GLMax2DDriver()
Graphics 640 , 480 , 0

While Not KeyDown(Key_escape)
	Cls
	SetColor 255 , 0 , 0
	DrawLine x1 , y1 , x2 , y2
	SetColor 0 , 255 , 0
	DrawLine x3 , y3 , x4 , y4
	SetColor 0 , 0 , 255
	DrawLine xi , 0 , xi , 480
	DrawLine 0 , yi , 640 , yi
	keys()
	xi = ( (x1 * y2 - x2 * y1) * (x3 - x4) - (x3 * y4 - x4 * y3) * (x1 - x2) ) / ( (x1 - x2) * (y3 - y4) - (x3 - x4) * (y1 - y2) )
	yi = ( (x1 * y2 - x2 * y1) * (y3 - y4) - (x3*y4 - x4 * y3) * (y1 - y2) ) / ( (x1 - x2) * (y3 - y4) - (x3 - x4) * (y1 - y2) )


	
	Flip
	FlushMem
Wend
End

Function Keys()
	If KeyDown(Key_1)
		x1 = MouseX()
		y1 = MouseY()
	End If
	
	If KeyDown(Key_2)
		x2 = MouseX()
		y2 = MouseY()
	End If
	
	If KeyDown(Key_3)
		x3 = MouseX()
		y3 = MouseY()
	End If
	If KeyDown(Key_4)
		x4 = MouseX()
		y4 = MouseY()
	End If
	
End Function
</textarea><br><br>Evrything its ok but I need to get the point only if there is intersection betwen the two virtual lines as drawn and not as real lines. Or I can say to get the vector vector Intersection. <br><br></td></tr></table><br>
<a name="584860"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tibit</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I tried to break this one. I had some success but I didn't solve it. Something is still wrong.<br><br>Anyhow here is my modification of your code which shows if a point is on the line:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Framework brl.Math
Import BRL.StandardIO
Import BRL.Max2d
Import BRL.GLMax2D

Global x1:Int = 50
Global y1:Int = 200
Global x2:Int = 500
Global y2:Int = 200

Global x3:Int = 10
Global y3:Int = 100
Global x4:Int = 100
Global y4:Int = 60



Local xi = ( (x1 * y2 - x2 * y1) * (x3 - x4) - (x3 * y4 - x4 * y3) * (x1 - x2) ) / ( (x1 - x2) * (y3 - y4) - (x3 - x4) * (y1 - y2) )
Local yi = ( (x1 * y2 - x2 * y1) * (y3 - y4) - (x3*y4 - x4 * y3) * (y1 - y2) ) / ( (x1 - x2) * (y3 - y4) - (x3 - x4) * (y1 - y2) )


SetGraphicsDriver GLMax2DDriver()
Graphics 640 , 480 , 0

While Not KeyDown(Key_escape)
	
	SetColor 255 , 0 , 0 'RED
	DrawLine x1 , y1 , x2 , y2
	
'	SetColor 0 , 255 , 0 'GREEN
'	DrawLine x3 , y3 , x4 , y4
	
	keys()
	
	'If LinesCollide( xi , yi , x1 , y1, x2, y2, x3, y3, x4, y4) Then DrawText "POINT ON LINE!!!!  X= "+xi+"  Y= "+yi,20,30
	LinePointCollide( x1 , y1, x2, y2, MouseX(), MouseY() )
		
	SetColor 0 , 0 , 255 'Blue
'	DrawLine xi , 0 , xi , 480
'	DrawLine 0 , yi , 640 , yi	
	DrawOval xi-5,yi-5,10,10 'Draw a Oval where the lines intersect
	
	Flip;Cls;	FlushMem
Wend
End

'First X,Y is the X,Y of the intersection
'First line is: (x1, y1)  to (x2, y3)
'Second line is:(x3, y3)  to (x4, y4)
Function LinesCollide( xi Var, yi Var, x1 , y1, x2, y2, x3, y3, x4, y4)
	xi = ( (x1 * y2 - x2 * y1) * (x3 - x4) - (x3 * y4 - x4 * y3) * (x1 - x2) ) / ( (x1 - x2) * (y3 - y4) - (x3 - x4) * (y1 - y2) )
	yi = ( (x1 * y2 - x2 * y1) * (y3 - y4) - (x3*y4 - x4 * y3) * (y1 - y2) ) / ( (x1 - x2) * (y3 - y4) - (x3 - x4) * (y1 - y2) )
	If LinePointCollide( x1 , y1, x2, y2, xi, yi) And LinePointCollide( x3 , y3, x4, y4, xi, yi) Return True Else Return False
EndFunction

Function LinePointCollide( x1 , y1, x2, y2, PointX, PointY)

	Local LineX,LineY
	Local LineXR,LineYR
	Local LinePX,LinePY
	Local LinePXR,LinePYR	

	LineX  = x2 - x1 'x1 - x2
	LineY  = y2 - y1 'y1 - y2
	
	LineXR = x1 - x2 'x2 - x1 
	LineYR = y1 - y2 'y2 - y1
		
	LinePX  = PointX - x1
	LinePY  = PointY - y1

	LinePXR = PointX - x2
	LinePYR = PointY - y2
	
	
	
	'Line End to Point
	SetColor 0 , 255 , 0 ' GREEN

	'DrawLine x1,y1,x1 + LineX, y1 + LineY
	DrawLine x1,y1,PointX,PointY

	'Line Start To point
	SetColor 0 , 0 , 255 ' BLUE
	DrawLine x2,y2,PointX,PointY

'	DrawLine x2,y2,x2 + LineXR ,y2 + LineYR
	
	SetColor 255,255,255		
	
	
	
	Local R#,U#

	R# = ( LineX*LinePX + LineY*LinePX ) 'Dot product
	U# = ( Sqr(LineX*LineX + LineY*LineY) )*( Sqr( LinePX*LinePX + LinePY*LinePY ) )
	
	If U = 0 Return
	
	Local Angle# = ACos( (R/U) )

	DrawText "Angle LineStart to Point : "+Angle,20,80


	R# = ( LineXR*LinePXR + LineY*LinePXR ) 'Dot product
	U# = ( Sqr(LineXR*LineXR + LineYR*LineYR) )*( Sqr( LinePXR*LinePXR + LinePYR*LinePYR ) )
	
	If U = 0 Return
	
	Local AngleR# = ACos( (R/U) )
	DrawText "Angle LineEnd   to Point : "+AngleR,20,110

	If Angle = 0 And AngleR = 0 Then Return True Else Return False

EndFunction



Function Keys()
	If KeyDown(Key_1)
		x1 = MouseX()
		y1 = MouseY()
	End If
	
	If KeyDown(Key_2)
		x2 = MouseX()
		y2 = MouseY()
	End If
	
	If KeyDown(Key_3)
		x3 = MouseX()
		y3 = MouseY()
	End If
	If KeyDown(Key_4)
		x4 = MouseX()
		y4 = MouseY()
	End If
	
End Function

</textarea><br><br>I tried to think in the terms of vectors and you should really make your calculations a little more clear :) <br><br>It's impossible to follow your vector math (no offence), but as it works, what can I say.. Nice Work! My point on line might be the solution, yet my point on line doesn't seem to work unless the line is horizontal, which is strange - I mean it is all vector math.. (I need some help here)<br><br>Anyhow my idea for solving this very problem was a little different (I haven't chack that math site yet). My idea was to calculate the normal of the line. And with the help of the normal I could calculate the perpendicular distance to the line - in other words the closest distance to the line from this point. (This is what your function does - it check if we collide with an infinite line)<br><br>If you run the modifyed example you can see that I calculate the angles from each of the end points of the line to the point we want to check. If both angles are 0 then the point is on the line.<br><br>It would be great if we could get this to work, I'm so sick and tired of vector math right now ;) <br><br></td></tr></table><br>
<a name="584878"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warpy</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here you go<br><br><pre class=code>
Strict

Framework brl.Math
Import BRL.StandardIO
Import BRL.Max2d
Import BRL.GLMax2D

Global x1:Int = 10
Global y1:Int = 10
Global x2:Int = 100
Global y2:Int = 160
Global x3:Int = 10
Global y3:Int = 100
Global x4:Int = 100
Global y4:Int = 60



Local xi
Local yi 
Local lambda#				'distance of intersection along first line 
Local mu#					'distance of intersection along second line
Local dx1#,dy1#,dx2#,dy2#	'direction vectors of the lines

SetGraphicsDriver GLMax2DDriver()
Graphics 640 , 480 , 0

While Not KeyDown(Key_escape)
	Cls
	
	
	dx1# = x2-x1
	dy1# = y2-y1
	dx2# = x4-x3
	dy2# = y4-y3
	lambda# = ( y3 - y1 + (dy2/dx2)*( x1 - x3 )) / ( dy1 - dy2*dx1/dx2 )
	If lambda#&gt;=0 And lambda#&lt;=1 'if intersection lies on first line
		mu# = ( x1 + dx1*lambda - x3 ) / dx2 
		If mu#&gt;=0 And mu#&lt;=1 'if intersection also lies on second line
			xi = x1 + lambda*dx1
			yi = y1 + lambda*dy1
			SetColor 0 , 0 , 255
			DrawLine xi , 0 , xi , 480
			DrawLine 0 , yi , 640 , yi
		EndIf
	EndIf

	SetColor 255 , 0 , 0
	DrawLine x1 , y1 , x2 , y2
	SetColor 0 , 255 , 0
	DrawLine x3 , y3 , x4 , y4
	keys()

	
	Flip
	FlushMem
Wend
End

Function Keys()
	If KeyDown(Key_1)
		x1 = MouseX()
		y1 = MouseY()
	End If
	
	If KeyDown(Key_2)
		x2 = MouseX()
		y2 = MouseY()
	End If
	
	If KeyDown(Key_3)
		x3 = MouseX()
		y3 = MouseY()
	End If
	If KeyDown(Key_4)
		x4 = MouseX()
		y4 = MouseY()
	End If
	
End Function
</pre><br><br>(long-winded and confusing explanation follows. Get a real maths teacher to teach you if you need to understand this)<br><br>Vector lines: consider a line as the set of vectors defined by a starting point plus an arbitrary multiple of another vector (the direction vector).<br>So with two lines L1 and L2, starting at A and B with direction vectors r and s, respectively:<br><br>A point on line L1 = A + r*lambda<br>i.e. L1x = Ax + rx*lambda<br>      L1y = Ay + ry*lambda<br>A point on line L2 = B + s*mu<br>i.e. L2x = Bx + sx*lambda<br>      L2y = By + sy*lambda<br><br>When they intersect, L1 = L2, so<br><br>A + r*lambda = B + s*mu<br><br>that is,<br><br>Ax + rx*lambda = Bx + sx*mu [1]<br>and<br>Ay + ry*lambda = By + sy*mu [2]<br><br>With a bit of rearranging of [1]:<br><br>(Ax- Bx + rx*lambda)/sx = mu<br><br>And sticking that back into [2]<br><br>Ay + ry*lambda = By + sy*(Ax - Bx + rx*lambda)/sx<br><br>Collecting all the lambda terms to the left:<br><br>lambda*( ry -sy*rx/sx ) = ( By - Ay +sy*(Ax - Bx)/sx )<br><br>And finally<br><br>lambda = ( By - Ay + sy*(Ax-Bx)/sx ) / ( ry - sy*ry/sx )<br><br>So what does lambda mean? lambda = 0 at the start of the line (Ax,Ay) [=(x1,y1) in your code] and it equals 1 at the end of the line (Ax+rx,Ay+ry) [=(x2,y2) in your code] because the length of the direction vector is the same as the length of the line. So the point of intersection is in the first line when 0&lt;=lambda&lt;=1, and if you solve for mu, the same thing applies for the second line. Some more rearranging gets you the intersection point.<br><br><br>Hope this helps. <br><br></td></tr></table><br>
<a name="584937"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tibit</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Brilliant!<br><br>Thank you sooo much! <br><br>Cleaned up. Function for testing collision between two lines with a simple example.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Framework BRL.Max2d
Import BRL.GLMax2D

'Test Lines
Local x1:Int = 10
Local y1:Int = 10
Local x2:Int = 100
Local y2:Int = 160
Local x3:Int = 10
Local y3:Int = 100
Local x4:Int = 100
Local y4:Int = 60

Local LastMouseX,LastMouseY,MouseDX#,MouseDY#

Local InterSectionX#, InterSectionY#

SetGraphicsDriver GLMax2DDriver()
Graphics 640 , 480 , 0

While Not KeyDown(Key_escape)
	Cls
	
	If KeyDown(Key_1)
		x1 = MouseX()
		y1 = MouseY()
	End If
	
	If KeyDown(Key_2)
		x2 = MouseX()
		y2 = MouseY()
	End If
	
	If KeyDown(Key_3)
		x3 = MouseX()
		y3 = MouseY()
	End If
	If KeyDown(Key_4)
		x4 = MouseX()
		y4 = MouseY()
	End If	

	If LinesCollide(InterSectionX, InterSectionY, x1, y1, x2, y2, x3, y3, x4, y4 )
		SetColor 0 , 0 , 255
		DrawLine InterSectionX, 0 , InterSectionX, 480
		DrawLine 0 , InterSectionY, 640 , InterSectionY		
	EndIf
	
	SetColor 255 , 0 , 0
	DrawLine x1 , y1 , x2 , y2
	SetColor 0 , 255 , 0
	DrawLine x3 , y3 , x4 , y4

	DrawText "DX : "+MouseDX+"  DY: "+MouseDY, 20,100

	LastMouseX = MouseX()
	LastMouseY = MouseY()

	Flip
	FlushMem
Wend
End
</textarea><br><br><br>Here is the Magic Function:<br><pre class=code>
Function LinesCollide( xi# Var , yi# Var, x1#, y1#, x2#, y2#, x3#, y3#, x4#, y4#)

	Local L1d:Float			'distance of intersection along first line 
	Local L2d:Float			'distance of intersection along second line
	Local L1x#,L1y#,L2x#,L2y#	'direction vectors of the lines

	L1x = x2-x1
	L1y = y2-y1
	L2x = x4-x3
	L2y = y4-y3
		
	L1d = ( y3 - y1 + (L2y/L2x)*( x1 - x3 )) / ( L1y - L2y*L1x/L2x )
	
	If L1d &gt;=0 And L1d &lt;=1 'if intersection lies on first line
		L2d  = ( x1 + L1x*L1d - x3 ) / L2x 
		If L2d &gt;=0 And L2d &lt;=1 'if intersection also lies on second line
			xi = x1 + L1d*L1x
			yi = y1 + L1d*L1y
			Return True
		EndIf
	EndIf
	Return False			
	
EndFunction
</pre>USE:<br><br>Check a bullets velocity vector against a another line. This could be a wall or another objects speed vector. Which would mean that no two object would be able to overlap even if they both travel in light speed. Also this is perfect for Beem weapons. It also makes implentation of bounce and slide against line-walls simpler.<br><br>About bouncing:<br><br>r = 2*n*AngleBetweenVectors( n, v ) - v<br><br>r is the resulting bounce.<br>n is the normal of the surface (the line).<br>v is the velocity vector of the object in question.<br><br>Which would be the easiest way to get the normal of the line? In other word a vector perpendicular (in this function) to the line vector?<br><br>Having the normal also means we can see which side of the line we are on, in case that would matter.<br><br>Unanswered questions:<br>What is the best way to handle object that have a size ontop of their velocity vector? For example think a ball against a wall or a rect against a wall. I was thinking about having my maps built up with "collide" lines which you bounce/hit/slide against. This solution allows for slim walls with garanteed collision. <br><br></td></tr></table><br>
<a name="584943"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >sswift</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> There's already a function in the code archive that does this.  I know because I wrote it. :-)  You shoudl really check there when you need a math funciton like this there's lots of such functions. <br><br></td></tr></table><br>
<a name="584949"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tibit</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> That's typical. I hate the code archives because it is a pain to search and browse. Why is there not a simple database which you can quire for all code involving different subjects instead of a few general main areas.<br><br>EDIT-<br>Na didn't find your entry in the code archives..<br>Can you link? <br><br></td></tr></table><br>
<a name="584966"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >sswift</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Click my username for my code archives entries.  Look for lines intersect or ray intersect or line segment.  I forget which I called it.  I think it was ray, because my function can handle lines, rays, and line segments. <br><br></td></tr></table><br>
<a name="585028"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warpy</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> wave - that's a good point. del.icio.us tagging of the code archives would be useful indeed... I might have a play around with that idea<br><br>sswift's line normal code is here - <a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=450" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=450</a><br><br>Your unanswered question:<br>You'll need to keep track of the old position of your object, so before you apply the velocity, set<br>ox = x<br>oy = y<br>Then once you've applied the velocity, check the line position -&gt; position + normal for intersection with each line , where the normal vector is facing outwards. This time you still need 0&lt;=lambda&lt;=0, but if mu &lt;= size, then the object has touched or crossed the line.<br>At the point where the object first touched the line mu = size, so with some rearranging of the stuff in my last post:<br><br>mu = size = (Ax + rx*lambda - Bx) / sx      'Ax = startpoint of line, rx = direction of line, Bx = ox, sx = normal vector of line.<br><br>And rearranging a bit more, you get the point of collision as:<br>xi = Ax + rx*lambda + size*sx<br>yi = Ay + ry*lambda + size*sy<br><br>So there you go :)<br>This is all very similar to what I've been doing lately, you'd better not be making a better game than me! ;) <br><br></td></tr></table><br>
<a name="585044"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tibit</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Warpy! This has help me a lot! <br><br></td></tr></table><br>
<a name="585113"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Haramanai</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks evryone.<br>At last I am unstacked.<br>I gues I must find my old Math books. And start to learn the english traslation of the Math words. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
