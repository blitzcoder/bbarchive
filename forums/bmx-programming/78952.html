<!DOCTYPE html><html lang="en" ><head ><title >Particle Timing</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Particle Timing</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Particle Timing</a><br><br>
<a name="886408"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Schragnasher</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am in need of some help, iv been working this problem for a few weeks now and cant get it right. Iv searched the forum but haven't found much luck on the specifics of timing a particle system to different machine speeds. I want to have a consistent rate of emission on any pc, i know it might slow down the game(like how eschalon gets slow) of a really slow PC but that's ok, I would add a slider for effects. I just want to make sure it looks decent on a fast machine, right now it seems to emit a bunch of particles then stop and emit a few seconds later when the die. I'm using a timing function that i found in the code DB. I just don't know where i need to multiply the AppSpeed() in my particle update. Frankly im not sure if im even doing any of the timing stuff right for it at all. I understand the regular animation timing, and it works, its just an issue with how im doing it in the particle system. Ill post the code, but i cant promise itl make much sense lol. Any help on this is appreciated if im making sense. <br><br>Timing Function below<br><pre class=code>Module mymods.timing

Private

Global AppTime_UPS
Global AppTime_Iterator
Global AppTime_CurrentTime
Global AppTime_PauseStart=0
Global AppTime_Speed:Float=1.0
Global AppTime_DesiredLoopTime#=1000.0/60.0
Global AppTime_LastUpdateTime=0
Global AppTime_LastUPSTime

Public

Rem
bbdoc:
EndRem
Function UpdateAppTime(framerate=60)
	Local time
	Local elapsed
	
	If AppTime_PauseStart Return
	
	AppTime_DesiredLoopTime=1000.0/framerate

	time=MilliSecs()

	If AppTime_LastUpdateTime=0
		AppTime_Speed#=1.0
		AppTime_LastUpdateTime=time
		AppTime_CurrentTime=time
		AppTime_LastUPSTime=time
	Else
		elapsed=time-AppTime_LastUpdateTime
		If Not elapsed
			elapsed=1
			Delay 1
			time:+1
		EndIf
		AppTime_Speed=Float(elapsed)/Float(AppTime_DesiredLoopTime)
		AppTime_CurrentTime=time
		AppTime_LastUpdateTime=time
	EndIf
	
	AppTime_Iterator:+1
	If AppTime_CurrentTime-AppTime_LastUPSTime&gt;=1000
		AppTime_UPS=Float(AppTime_Iterator)/(Float(AppTime_CurrentTime-AppTime_LastUPSTime)/1000.0)
		AppTime_LastUPSTime=AppTime_CurrentTime
		AppTime_Iterator=0
	EndIf
	
EndFunction

Rem
bbdoc:
EndRem
Function AppTime()
	Return AppTime_CurrentTime
EndFunction

Rem
bbdoc:
EndRem
Function AppSpeed#()
	Return AppTime_Speed
EndFunction

Rem
bbdoc:
EndRem
Function UPS:Int()
	Return AppTime_UPS
EndFunction

Rem
bbdoc:
EndRem
Function PauseApp()
	If AppTime_PauseStart Return
	AppTime_PauseStart=MilliSecs()
EndFunction

Rem
bbdoc:
EndRem
Function ResumeApp()
	If Not AppTime_PauseStart Return
	If AppTime_LastUpdateTime
		Local elapsed=MilliSecs()-AppTime_PauseStart
		AppTime_LastUpdateTime:+elapsed
	EndIf
	AppTime_PauseStart=0
EndFunction</pre><br><br>My particle code if it helps.<br><pre class=code>Type tJParticle
	Field x:Float
	Field y:Float
	Field r:Byte
	Field g:Byte
	Field b:Byte
	Field alpha:Float
	Field scale:Float
	Field rot:Float
	Field xspeed:Float
	Field yspeed:Float
	Field age:Float = 0
	Function Create:tJParticle(x:Float, y:Float, initialalpha:Float, initialscale:Float, initialxspeed:Float, initialyspeed:Float)
		Local temp:tJParticle = New tJParticle
		temp.x = x
		temp.y = y
		temp.alpha = initialalpha
		temp.scale = initialscale
		temp.xspeed = initialxspeed
		temp.yspeed = initialyspeed
		Return temp
	End Function
End Type
Type tJEmitter
	Field partimg:tImg = LoadImg("particle.png")
	Field Layer1:TList = CreateList()
	Field pmax:Int
	Field x:Float
	Field y:Float
	Field frame:Float
	Field emitdelay:Float = 1
	Field emitrate:Int = 1
	
	Field blendmode:Int = LIGHTBLEND
	Field pred:Int = 200
	Field pgreen:Int = 100
	Field pblue:Int = 50
	Field xaccel:Float = 0
	Field yaccel:Float =.0001
	Field initialalpha:Float = 1
	Field initialalphajitter:Float = 0
	Field alphadecay:Float =.01
	Field alphadecayjitter:Float = 0
	
	Field initialscale:Float = 0
	Field initialscalejitter:Float = 0
	Field scalerate:Float =.01
	Field scaleratejitter:Float = 0
	
	Field lifetime:Float = 200
	Method Update()
		Local time:Int = MilliSecs()
		For Local t:tJParticle = EachIn layer1
				t.x = t.x + (t.xspeed * AppSpeed())
				t.y = t.y + (t.yspeed * AppSpeed())
				t.alpha = t.alpha - (alphadecay * AppSpeed())
				t.scale = t.scale + (scalerate * AppSpeed())
				If t.alpha &lt;= 0 Or t.age &gt;= lifetime Then
					ListRemove(layer1, t)
				End If
				t.age:+1 * AppSpeed()
		Next
		If layer1.Count() &lt; pmax And frame &gt;= emitdelay Then
			For Local e:Int = 0 To emitrate
				ListAddLast(layer1, tJParticle.Create(x, y, initialalpha, initialscale, Rnd(- 1, 1), - 1))
			Next
			frame = 0
		Else
			frame:+AppSpeed()
		End If
		
	End Method
	Method Draw()
		RenderState.Push()
		SetBlend(blendmode)
		For Local t:tJParticle = EachIn layer1
			SetColor(pred, pgreen, pblue)
			SetAlpha(t.alpha)
			SetScale(t.scale, t.scale)
			DrawImg(partimg, t.x, t.y)
		Next
		RenderState.Pop()
		DrawText(layer1.Count(), x, y - 30)
	End Method
	Function Create:tJEmitter(x:Float, y:Float, pmax:Int)
		Local temp:tJEmitter = New tJEmitter
		temp.pmax = pmax
		temp.x = x
		temp.y = y
		Return temp
	End Function
End Type
Type tJParticleSystem Extends tGameObject
	Field emitters:TList = CreateList()
	Method Draw()
		For Local t:tJEmitter = EachIn emitters
			t.Draw()
		Next
	End Method
	Method Update()
		For Local t:tJEmitter = EachIn emitters
			t.x = x
			t.y = y
			t.Update()
		Next
	End Method
	Method CreateEmitter(pmax:Int)
		ListAddLast(emitters, tJEmitter.Create(x, y, pmax))
	End Method
	Function Create:tJParticleSystem(name:String, x:Float, y:Float)
		Local temp:tJParticleSystem = New tJParticleSystem
		temp.name = name
		temp.x = x
		temp.y = y
		ListAddLast(tGameObject.objectlist, temp)
		Return temp
	End Function
End Type

Function GetParticleSystem:tJParticleSystem(name:String)
	For Local t:tJParticleSystem = EachIn tGameObject.objectlist
		If t.name = name Then Return t
	Next
	Throw "psystem not found: " + name
End Function

Function CreateJParticleSystem:tJParticleSystem(name:String, x:Float, y:Float)
	Return tJParticleSystem.Create(name, x, y)
End Function

Function AddEmitter(system:Object, pmax:Int)
	If tJParticleSystem(system) Then
		tJParticleSystem(system).CreateEmitter(pmax)
	Else If String(system) Then
		GetParticleSystem(String(system)).CreateEmitter(pmax)
	End If
End Function



</pre> <br><br></td></tr></table><br>
<a name="886409"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Muttley</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Have a read of this: <a href="http://www.gaffer.org/game-physics/fix-your-timestep" target="_blank">http://www.gaffer.org/game-physics/fix-your-timestep</a> <br><br></td></tr></table><br>
<a name="886412"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Schragnasher</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> thanks i think that might be what im looking for! <br><br></td></tr></table><br>
<a name="886444"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Muttley</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> No problem.  :)<br><br>I think there's a couple of BlitzMax examples of that method floating around these forums if you get stuck. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
