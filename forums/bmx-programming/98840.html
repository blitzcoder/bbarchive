<!DOCTYPE html><html lang="en" ><head ><title >check if a pixmap is a subrect of another</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >check if a pixmap is a subrect of another</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >check if a pixmap is a subrect of another</a><br><br>
<a name="1157512"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >nitti</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi <br><br>actual question:<br><br>what would be the fastest way of checking if an image/pixmap is also actually a subrect on another image/pixmap ?<br><br><br>context:<br>I am in the process of writing a texture atlas generator, it's working rather well. In my case I'll be having many pre-rendered rotated 'voxel' bodyparts as images. <br><br>because i plan to have bodyparts  that are rolled/yawed/pitched and bones of a few lengths/shapes/dimensions the numbers add up very quickly. <br><br>so i had an idea to drastically cut down the amount of images in the atlas; start with saving bones images with the biggest length, and for each bone that is sort of the same but shorter -&gt; check if the desired image would be an subimage of the bigger length bone image ...<br><br>oh and the actual rectpacking is working and might be usefull for someone:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

SeedRnd(MilliSecs())

Local packer:TRectanglePacker = TSimpleRectanglePacker.Create(768,768)
Local place:TPoint
Local shapeList:TList = CreateList()

prepareShapes(shapelist, packer, place)

Graphics 1024,768

While Not KeyDown(key_escape)
	Cls
	
	
	For Local s:TShape = EachIn shapelist
		SetColor s.r, s.g, s.b
		DrawRect s.x, s.y, s.w, s.h
	Next
	
	
	
	Flip

Wend

Function prepareShapes(shapelist:TList, packer:TRectanglePacker, place:TPoint)
	Local t:Int = MilliSecs()
	
	For Local i:Int = 0 Until 10000
		Local rectWidth:Int =Rand(4,10)
		Local rectHeight:Int =Rand(4,10)
		
		Local shape:TShape = TShape.Create(rectWidth, rectHeight)
		ListAddLast (shapeList, shape)
	Next
	
	
	SortList(shapelist, False, SortonHeight)

	Local count:Int = 0
	For Local s:TShape = EachIn shapelist
		
		If packer.tryPack(s.w,s.h, place)
			s.x = place.x
			s.y = place.y
			count:+1
		Else
			Throw("area full at "+count+" rectangles")
		EndIf
	
	Next
	
	Print (MilliSecs()-t)+" ms for "+count
EndFunction

Function SortOnHeight:Int(o1:Object, o2:Object)
    If TShape(o1).h &lt; TShape(o2).h
        Return -1
    ElseIf TShape(o1).h &gt; TShape(o2).h
        Return 1
    Else
        Return 0
    EndIf
End Function


Type TShape
	Field w:Int,h:Int
	Field x:Int,y:Int
	Field r:Int, g:Int, b:Int
	
	Function Create:TShape(w:Int,h:Int)
		Local ent:TShape = New TShape
		ent.w = w
		ent.h = h 
		ent.r = Rand(255)
		ent.g = Rand(255)
		ent.b = Rand(255)
		Return ent
		
	EndFunction
EndType

Type TPoint
	Global zero:TPoint = TPoint.Create(0,0)
	Field x:Int, y:Int
	
	Function Create:TPoint(x:Int, y:Int)
		Local ent:TPoint = New TPoint
		ent.x = x
		ent.y = y 
		Return ent
	EndFunction
EndType


Type TSimpleRectanglePacker Extends TRectanglePacker
	Field currentLine:Int
	Field lineHeight:Int
	Field column:Int
	
	Function Create:TSimpleRectanglePacker(packingAreaWidth:Int, packingAreaHeight:Int)
		Local ent:TSimpleRectanglePacker = New TSimpleRectanglePacker
		ent.packingAreaWidth = packingAreaWidth
		ent.packingAreaHeight = packingAreaHeight
		Return ent
	EndFunction
	
	Method tryPack:Int(rectangleWidth:Int, rectangleHeight:Int, placement:TPoint Var)
		' // If the rectangle is larger than the packing area in any dimension, it will never fit!
		If rectangleWidth &gt; packingAreaWidth Or rectangleHeight &gt; packingAreaHeight
			placement = TPoint.Zero
			Return False
		EndIf
		
		' // Do we have to start a new line ?
		If (column + rectangleWidth &gt; packingAreaWidth)
			currentLine :+ lineHeight
			lineHeight = 0
			column = 0
		EndIf
		
		' // If it doesn't fit vertically now, the packing area is considered full
		If (currentLine + rectangleHeight &gt; packingAreaHeight)
			placement = TPoint.Zero
			Return False
		EndIf
		
		'  // The rectangle appears to fit at the current location
		placement = TPoint.Create(column, currentLine)
		
		column:+rectangleWidth
		
		If rectangleHeight &gt; lineheight Then lineHeight = rectangleHeight
		
		Return True
		
	EndMethod
	 
	
EndType

Type TRectanglePacker
	Field packingAreaWidth:Int
	Field packingAreaHeight:Int
	
	Method pack:TPoint(rectWidth:Int, rectHeight:Int)
		Local placement:TPoint = New Tpoint
		
		If Not tryPack(rectWidth, rectHeight, placement)
			Throw("rectangle doesn't fit")
		Else
			Return placement
		EndIf
	EndMethod
	
	Method tryPack:Int(rectWidth:Int, rectHeight:Int, placement:TPoint Var)
	EndMethod
EndType
</textarea><br><br>on my system it packs 10000 images in 18 ms, very neatly packed also <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
