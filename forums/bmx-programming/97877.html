<!DOCTYPE html><html lang="en" ><head ><title >chaos.desktopext Fullscreen DesktopPixmap()</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >chaos.desktopext Fullscreen DesktopPixmap()</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >chaos.desktopext Fullscreen DesktopPixmap()</a><br><br>
<a name="1139428"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm making an application that grabs screenshots for you.<br>It runs as its own application and when you press Printscreen it takes a screenshot using DesktopPixmap() from chaos.desktopext.<br>It's all working, except for when you're in fullscreen mode.<br>It just takes a black picture (or transparent sometimes...)<br><br>Anyone know why?<br>Or does anyone have a better way of capturing a screenshot that will work in fullscreen? <br><br></td></tr></table><br>
<a name="1139434"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ima747</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> What platform? <br><br></td></tr></table><br>
<a name="1139435"></a>

<a name="1139437"></a>

<a name="1139438"></a>

<a name="1139440"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Win32.<br>I believe chaos.desktopext only supports Win32 when it comes to DesktopPixmap()<br><br>Here's an example that shows how messed up this is.<pre class=code>SuperStrict
Import chaos.desktopext

Function MainFunction()
	For Local i:Int = 0 To 100
		DrawRect(Rand(0, GraphicsWidth()), Rand(0, GraphicsHeight()), 32, 32)
	Next
	SetColor(255, 0, 0)
	DrawText("Press Escape", 0, 0)
	SetColor(255, 255, 255)
	
	Flip()
	Cls()
End Function

Graphics(1024, 768, 0)
While Not KeyDown(KEY_ESCAPE)
	MainFunction()
Wend
SavePixmapPNG(chaos.desktopext.DesktopPixmap(), "Test_Window.png")
EndGraphics()

Graphics(1024, 768, 32)
While Not KeyDown(KEY_ESCAPE)
	MainFunction()
Wend
SavePixmapPNG(chaos.desktopext.DesktopPixmap(), "Test_Fullscreen.png")
End</pre><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1139515"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hiya,<br><br>Some questions...<br><br>Do you want to capture the dekstop or just your app window thats using graphics?<br>Do you want to just save the capture or manipulate it?<br><br>Because youre saying you want to do a capture during full screen and looking at the fact youre saving the capture to disk in your example above, I assume you want to capture just the app window and save it. Using D3D9 makes this very easy to capture the contents of the window, however to capture the desktop as a whole will require a different method.<br><br>Heres a simple example to capture a D3D9 app windowed and fullscreen ( tested on Win7 x64 )...<br><br><b>CaptureScreenToDisk(FileName$ , FileFormatExt$)</b><br>FileName$ : self explanatory, saves to the same folder as the .exe<br>FileFormatExt$ : Use either "jpg" "bmp" or "png"<br><br>You could easily save to other formats but BMax doesnt support them to load back in as images or pixmaps, unless of course you write a loader too using D3DX :P<br><br>One thing I will say is that this method captures the back buffer. Remember this could actually be different to what you see on the front buffer! So Flip the screen again to see what youre capturing. ( D3D9 does offer the functionality to capture the FrontBuffer directly buts its not always supported by all GPUs for some reason ).<br><br>This example Flips the screen a second time as described to show on the screen what is being captured to disk.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
?Win32
Strict

Global D3DX9Lib
Global D3DXSaveSurfaceToFile(FileName:Byte Ptr,Format,pSurface:Byte Ptr,pSrcPallete:Byte Ptr,pRect:Byte Ptr)"win32"
Global D3D9Graphics:TGraphics
Global CaptureScreenFilename$

InitD3DX9Library()
D3D9Graphics = Graphics(800,600)

CaptureScreenFilename = "CaptureScreen"

While Not KeyDown(KEY_ESCAPE)
	Cls
	
	SetColor(Rnd(0,255),Rnd(0,255),Rnd(0,255))
	
	For Local i:Int = 0 To 100
		DrawRect(Rand(0, GraphicsWidth()), Rand(0, GraphicsHeight()), 32, 32)
	Next

	SetColor(255, 255, 255)
	DrawText "Press SPACE to capture the screen to a file",0,0
	DrawText("Press ESCAPE To Exit", 0, 10)

	If KeyDown(KEY_SPACE)
		If CaptureScreenToFile(CaptureScreenFilename,"png") DebugLog " CaptureScreen Successful"
	EndIf
	Flip
Wend
End

Function CaptureScreenToFile(FileName$,FileFormatExt$)
	Local D3DDev:IDirect3DDevice9 
	Local Surface:IDirect3DSurface9
	Local Desc:D3DSURFACE_DESC
	Local D3DXFileFormat
	
	If Not D3DX9Lib Return False
	
	Flip 'Show the screen that will actually be captured!!!!
	
	D3DDev = TD3D9Graphics(TMax2DGraphics(D3D9Graphics)._graphics).GetDirect3DDevice()
	
	If Not D3DDev Return False
	
	If D3DDev.GetBackBuffer(0,0,0,Surface) &lt; 0 Return False
	
	Select FileFormatExt
		Case "bmp"
			D3DXFileFormat = 0	'BMP
		Case "jpg"
			D3DXFileFormat = 1	'JPG
		Case "png"
			D3DXFileFormat = 3	'PNG
			
		Default Return False
	EndSelect

	If D3DXSaveSurfaceToFile(FileName+"."+FileFormatExt,D3DXFileFormat,Surface,Null,Null) &lt; 0 Return False
	If Surface Surface.Release_
	
	Return True
EndFunction

Function InitD3DX9Library()
	For Local SubVersion = 43 To 24 Step -1
		D3DX9Lib = LoadLibraryA("D3DX9_"+SubVersion+".dll")
		
		If D3DX9Lib D3DXSaveSurfaceToFile = GetProcAddress(D3DX9Lib,"D3DXSaveSurfaceToFileA") ; Exit
	Next
EndFunction

? 'Win32
</textarea> <br><br></td></tr></table><br>
<a name="1139529"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >*</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> why not grab a pixmap and then savepixmappng? <br><br></td></tr></table><br>
<a name="1139553"></a>

<a name="1139554"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I guess the active window is enough to capture.<br>But if it's 3D or software, that's outside of my control.<br><br>I'm making an arcade frontend, it launches different emulators and takes screenshots ingame and saves it to the disk.<br>And the only manipulation I want to do is resize the image down to 320x240.<br><br>So my program just sits in the background and then takes a screenshot of whatever's on display.<br><br>That means I can't just capture my own buffer (my real app is only a MaxGUI app without a graphics canvas or anything, just a hidden window) and I can't do stuff like use OpenGL instead, cause that's outside my control as an emulator might use Direct3D.<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1139576"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I understand.<br><br>Its easy to hack/modify chaos code to suit this and add an extra parameter to the DesktopPixmap() function. I only know how to do it for Windows unforuntately!<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Rem
bbdoc:	Extended commandset for dealing with the desktop.
EndRem
Module chaos.desktopext
ModuleInfo "Version: 1.18"
ModuleInfo "Author: hamZta, Suco-X, d-bug"
ModuleInfo "License: Public Domain"

ModuleInfo "History: 1.18 renamed functions to fix compiling bugs with 1.39 (added suffix 'Multi')"
ModuleInfo "History: 1.17 added DesktopMouseX and DesktopMouseY for OSX"
ModuleInfo "History: 1.16 Linux support skipped / fixed documentation"
ModuleInfo "History: 1.15 added multi-desktop-support for DesktopPixmap"
ModuleInfo "History: 1.14 added DesktopWallpaper"
ModuleInfo "History: 1.13 added DesktopRatio"
ModuleInfo "History: 1.12 fixed 'identifier screen not found' bug on OSX"
ModuleInfo "History: 1.11 fixed multi-desktop-support for Windows"
ModuleInfo "History: 1.10 added multi-desktop-support for Windows"
ModuleInfo "History: 1.09 added pixmap convertion to PF_BGR888 to DesktopPixmap"
ModuleInfo "History: 1.08 added optional flip-mode to DesktopPixmap"
ModuleInfo "History: 1.07 added multi-desktop-support for OSX"
ModuleInfo "History: 1.06 fixed 'TPixmap not found'"
ModuleInfo "History: 1.05 added DesktopMouseX and DesktopMouseY for Windows"
ModuleInfo "History: 1.04 added DesktopPixmap for Windows"
ModuleInfo "History: 1.03 changed documentation to german"
ModuleInfo "History: 1.02 changed module scope to chaos"
ModuleInfo "History: 1.01 fixed missing ReleaseDC"
ModuleInfo "History: 1.00 initial release 2006-05-16"


SuperStrict

Import brl.standardio
Import brl.pixmap
Import brl.linkedlist
Import brl.bank


?Win32 

	Import pub.win32

	Private 

		Const SM_CXVIRTUALSCREEN:Int = 78
		Const SM_CYVIRTUALSCREEN:Int = 79
		Const SM_CMONITORS:Int = 80
		Const SM_SAMEDISPLAYFORMAT:Int = 81
		Const SPI_GETDESKWALLPAPER:Int = $73

		Type TDisplay
			Global List:TList = New TList
			Field id:Int
			Field handle:Int
			Field x:Int
			Field y:Int
			Field width:Int
			Field height:Int
		End Type

		Type TRect
		   Field rLeft:Int
		   Field rTop:Int
		   Field rRight:Int
		   Field rBottom:Int
		End Type
		
		Extern "Win32"
			Function GetTopWindow:Int(hwnd:Int)"Win32" 				'ADDED Col 14.05.2012
			Function GetSystemMetrics:Int (nIndex:Int)"Win32"
			Function EnumDisplayMonitors:Int (hdc:Int, lprcClip:Byte Ptr, lpfnEnum:Byte Ptr, dwData:Byte Ptr)"Win32"
			Function GetCursorPos(lpPoint:Byte Ptr)"Win32" = "GetCursorPos@4"
			Function GetDIBits(hdc:Int, bitmap:Int, Start:Int, Num:Int, bits:Byte Ptr, lpbi:Byte Ptr, usage:Int)"Win32"
			Function GetWindowDC(hwnd:Int)"Win32"
			Function ReleaseDC(hwnd:Int, hdc:Int)"Win32"
			Function SystemParametersInfo (uiAction:Int, uiParam:Int, pvParam:Byte Ptr, fWinIni:Int)"Win32" = "SystemParametersInfoA@16"
		End Extern

		Function MonitorEnumProc:Byte (hMonitor:Int,hdcMonitor:Int,lprcMonitor:Byte Ptr,dwData:Int)
			Local tempRect:TRect = New TRect
			MemCopy(tempRect, lprcMonitor, SizeOf(tempRect))
			Local display:TDisplay = New TDisplay
			TDisplay.List.AddLast(display)
			display.id = TDisplay.List.Count()
			display.handle = hMonitor
			display.x = tempRect.rLeft
			display.y = tempRect.rTop
			display.width = tempRect.rRight - display.x
			display.height = tempRect.rBottom - display.y
			Return True
		End Function
		EnumDisplayMonitors (Null , Null , MonitorEnumProc , Null)
	
	Public 
?MacOS
	Import "macscreen.c"
	Private
		Extern
			Function CGDisplayCurrentMode:Byte Ptr(displayID:Byte Ptr)"MacOS"
			Function CGGetActiveDisplayList:Byte Ptr(kMaxDisplays:Int, display:Byte Ptr, numDisplays:Int Var)"MacOS"
			Function MACOS_GetWidth:Int(mode:Byte Ptr)"C"
			Function MACOS_GetHeight:Int(mode:Byte Ptr)"C"
			Function MACOS_GetBPP:Int(mode:Byte Ptr)"C"
			Function MACOS_GetHertz:Int(mode:Byte Ptr)"C"
			Function MACOS_GetMouseX:Int()"C"
			Function MACOS_GetMouseY:Int()"C"
		End Extern 
	Public
?Linux

?
 
'-------------------------------------------------------------------

Rem
bbdoc:	Get Desktop wallpaper
returns:	Path to the wallpaper as string
EndRem
Function DesktopWallpaper:String ()
	?Linux
		Return ""
	?MacOS
		Return ""
	?Win32
	  Local Bank:TBank = CreateBank(255)
		Local Result:Byte Ptr = BankBuf(Bank)
		SystemParametersInfo(SPI_GETDESKWALLPAPER, 128, Result, 0)
	  Return String.FromCString(Result)
	?
End Function

'-------------------------------------------------------------------

Rem
bbdoc: Get desktop aspect-ratio 
returns: aspect-ratio as string
about: 
&lt;table&gt;
&lt;tr&gt;&lt;th&gt;Flag&lt;/th&gt;&lt;th&gt;Description&lt;/th&gt;&lt;th&gt;Win32&lt;/th&gt;&lt;th&gt;MacOS&lt;/th&gt;&lt;/tr&gt;
&lt;tr&gt;&lt;td&gt;&lt;font class=token&gt;screen&lt;/font&gt;&lt;/td&gt;&lt;td&gt;desktops number (0 = primary)&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;/tr&gt;
&lt;/table&gt;
EndRem
Function DesktopRatio:String (screen:Int=0)

			Function DAR:Int (a:Int, b:Int)
				Local fudge:Int = 0
				Local Result:Int
				If b Mod a &lt;= fudge
					Result = a
				Else
					Result = DAR (b, a Mod b)
				EndIf
				Return Result
			End Function

	Local X:Int = DesktopWidthMulti(screen)
	Local Y:Int = DesktopHeightMulti(screen)
	Local Div:Int = DAR(X,Y)
	Local Result:String = String (X/Div) + ":" + String(Y/Div)
	Select Result
		Case "8:5" ; 	Return "16:10"
		Default ; 		Return Result
	End Select

End Function

'-------------------------------------------------------------------

Rem
bbdoc: Get amount of available desktops 
returns: amount as integer
EndRem
Function DesktopCount:Int () 
	?Win32
		Return GetSystemMetrics (SM_CMONITORS)
	?MacOS
		Local display:Byte Ptr[] = New Byte Ptr[1]
		Local iMode:Byte Ptr = Null
		Local iCount:Int
		CGGetActiveDisplayList 1, display, iCount
		Return iCount
	?Linux
		Return 0
	?
End Function 

'-------------------------------------------------------------------

Rem
bbdoc: Get desktop x-coord
returns: coord as integer
about: 
&lt;table&gt;
&lt;tr&gt;&lt;th&gt;Flag&lt;/th&gt;&lt;th&gt;Description&lt;/th&gt;&lt;th&gt;Win32&lt;/th&gt;&lt;th&gt;MacOS&lt;/th&gt;&lt;/tr&gt;
&lt;tr&gt;&lt;td&gt;&lt;font class=token&gt;screen&lt;/font&gt;&lt;/td&gt;&lt;td&gt;desktops number (0 = primary)&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;td&gt;No&lt;/td&gt;&lt;/tr&gt;
&lt;/table&gt;
EndRem
Function DesktopX:Int (screen:Int = 0) 
	If screen &lt; 0 Then screen = 0
	?Win32
			For Local display:TDisplay = EachIn TDisplay.List
				If display.id = screen+1
					Return display.X
				EndIf
			Next
			Return 0
	?MacOS
		Return 0
	?Linux
		Return 0
	?
End Function 


'-------------------------------------------------------------------

Rem
bbdoc: Get desktop y-coord
returns: coord as integer
about: 
&lt;table&gt;
&lt;tr&gt;&lt;th&gt;Flag&lt;/th&gt;&lt;th&gt;Description&lt;/th&gt;&lt;th&gt;Win32&lt;/th&gt;&lt;th&gt;MacOS&lt;/th&gt;&lt;/tr&gt;
&lt;tr&gt;&lt;td&gt;&lt;font class=token&gt;screen&lt;/font&gt;&lt;/td&gt;&lt;td&gt;desktops number (0 = primary)&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;td&gt;No&lt;/td&gt;&lt;/tr&gt;
&lt;/table&gt;
EndRem
Function DesktopY:Int (screen:Int = 0) 
	If screen &lt; 0 Then screen = 0
	?Win32
			For Local display:TDisplay = EachIn TDisplay.List
				If display.id = screen+1
					Return display.Y
				EndIf
			Next
			Return 0
	?MacOS
		Return 0
	?Linux
		Return 0
	?
End Function 


'-------------------------------------------------------------------

Rem
bbdoc: Get desktop width
returns: width as integer
about: 
&lt;table&gt;
&lt;tr&gt;&lt;th&gt;Flag&lt;/th&gt;&lt;th&gt;Description&lt;/th&gt;&lt;th&gt;Win32&lt;/th&gt;&lt;th&gt;MacOS&lt;/th&gt;&lt;/tr&gt;
&lt;tr&gt;&lt;td&gt;&lt;font class=token&gt;screen&lt;/font&gt;&lt;/td&gt;&lt;td&gt;desktops number (0 = primary)&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;/tr&gt;
&lt;/table&gt;
EndRem
Function DesktopWidthMulti:Int (screen:Int=0) 
	Local width:Int,height:Int,depth:Int,hertz:Int
	GetDesktopMode (width, height, depth, hertz, screen)
	Return width
End Function 


'-------------------------------------------------------------------

Rem
bbdoc: Get desktop height
returns: height as integer
about: 
&lt;table&gt;
&lt;tr&gt;&lt;th&gt;Flag&lt;/th&gt;&lt;th&gt;Description&lt;/th&gt;&lt;th&gt;Win32&lt;/th&gt;&lt;th&gt;MacOS&lt;/th&gt;&lt;/tr&gt;
&lt;tr&gt;&lt;td&gt;&lt;font class=token&gt;screen&lt;/font&gt;&lt;/td&gt;&lt;td&gt;desktops number (0 = primary)&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;/tr&gt;
&lt;/table&gt;
EndRem
Function DesktopHeightMulti:Int (screen:Int=0) 
	Local width:Int,height:Int,depth:Int,hertz:Int
	GetDesktopMode (width, height, depth, hertz, screen)
	Return height
End Function 


'-------------------------------------------------------------------

Rem
bbdoc: Get desktop depth
returns: depth as integer
about: 
&lt;table&gt;
&lt;tr&gt;&lt;th&gt;Flag&lt;/th&gt;&lt;th&gt;Description&lt;/th&gt;&lt;th&gt;Win32&lt;/th&gt;&lt;th&gt;MacOS&lt;/th&gt;&lt;/tr&gt;
&lt;tr&gt;&lt;td&gt;&lt;font class=token&gt;screen&lt;/font&gt;&lt;/td&gt;&lt;td&gt;desktops number (0 = primary)&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;/tr&gt;
&lt;/table&gt;
EndRem
Function DesktopDepthMulti:Int (screen:Int=0) 
	Local width:Int,height:Int,depth:Int,hertz:Int
	GetDesktopMode (width, height, depth, hertz, screen)
	Return depth
End Function 


'-------------------------------------------------------------------

Rem
bbdoc: Get desktop hertz
returns: hertz as integer
about: 
&lt;table&gt;
&lt;tr&gt;&lt;th&gt;Flag&lt;/th&gt;&lt;th&gt;Description&lt;/th&gt;&lt;th&gt;Win32&lt;/th&gt;&lt;th&gt;MacOS&lt;/th&gt;&lt;/tr&gt;
&lt;tr&gt;&lt;td&gt;&lt;font class=token&gt;screen&lt;/font&gt;&lt;/td&gt;&lt;td&gt;desktops number (0 = primary)&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;/tr&gt;
&lt;/table&gt;
EndRem
Function DesktopHertzMulti:Int (screen:Int=0) 
	Local width:Int,height:Int,depth:Int,hertz:Int
	GetDesktopMode (width, height, depth, hertz, screen)
	Return hertz
End Function 


'-------------------------------------------------------------------

Rem
bbdoc: Get desktop width, height, depth, hertz
returns: int var
about: 
&lt;table&gt;
&lt;tr&gt;&lt;th&gt;Flag&lt;/th&gt;&lt;th&gt;Description&lt;/th&gt;&lt;th&gt;Win32&lt;/th&gt;&lt;th&gt;MacOS&lt;/th&gt;&lt;/tr&gt;
&lt;tr&gt;&lt;td&gt;&lt;font class=token&gt;width&lt;/font&gt;&lt;/td&gt;&lt;td&gt;Int Var for desktops width&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;/tr&gt;
&lt;tr&gt;&lt;td&gt;&lt;font class=token&gt;screen&lt;/font&gt;&lt;/td&gt;&lt;td&gt;Int Var for desktops height&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;/tr&gt;
&lt;tr&gt;&lt;td&gt;&lt;font class=token&gt;screen&lt;/font&gt;&lt;/td&gt;&lt;td&gt;Int Var for desktops depth&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;/tr&gt;
&lt;tr&gt;&lt;td&gt;&lt;font class=token&gt;screen&lt;/font&gt;&lt;/td&gt;&lt;td&gt;Int Var for desktops hertz&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;/tr&gt;
&lt;tr&gt;&lt;td&gt;&lt;font class=token&gt;screen&lt;/font&gt;&lt;/td&gt;&lt;td&gt;desktops number (0 = primary)&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;/tr&gt;
&lt;/table&gt;
EndRem
Function GetDesktopMode:Int (width:Int Var, height:Int Var, depth:Int Var, hertz:Int Var, screen:Int=0) 
	?Win32
		Local hwnd:Int = GetDesktopWindow()
		Local hdc:Int = GetDC(hwnd)
		If hdc = Null Then Return -1
		depth  = GetDeviceCaps(hdc, BITSPIXEL)
		hertz  = GetDeviceCaps(hdc, VREFRESH)
		ReleaseDC(hwnd,hdc)
		If screen = -1
			width = GetSystemMetrics (SM_CXVIRTUALSCREEN)
			height = GetSystemMetrics (SM_CYVIRTUALSCREEN)
		Else
			Local valid:Byte = True
			For Local display:TDisplay = EachIn TDisplay.List
				If display.id = screen+1
					width = display.width
					height = display.height
					Exit
				EndIf
			Next
		EndIf	
	?MacOS
		Local display:Byte Ptr[] = New Byte Ptr[screen+1]
		Local iMode:Byte Ptr = Null
		Local iCount:Int
		CGGetActiveDisplayList screen+1, display, iCount
		If (screen &lt; iCount)
			iMode  = CGDisplayCurrentMode(display[screen])
		Else
			Throw " [DesktopExt] Screen '"+screen+"' does not exist!"
		End If
		width  = MACOS_GetWidth(iMode)
		height = MACOS_GetHeight(iMode)
		depth  = MACOS_GetBPP(iMode)
		hertz  = MACOS_GetHertz(iMode)
	?Linux
		width  = 0
		height = 0
		depth  = 0
		hertz  = 0
	?
End Function 



'-------------------------------------------------------------------

Rem
bbdoc: Get desktop screenshot
returns: screenshot as pixmap
about: 
&lt;table&gt;
&lt;tr&gt;&lt;th&gt;Flag&lt;/th&gt;&lt;th&gt;Description&lt;/th&gt;&lt;th&gt;Win32&lt;/th&gt;&lt;th&gt;MacOS&lt;/th&gt;&lt;/tr&gt;
&lt;tr&gt;&lt;td&gt;&lt;font class=token&gt;flag&lt;/font&gt;&lt;/td&gt;&lt;td&gt;True = flip pixmap vertical&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;td&gt;No&lt;/td&gt;&lt;/tr&gt;
&lt;tr&gt;&lt;td&gt;&lt;font class=token&gt;screen&lt;/font&gt;&lt;/td&gt;&lt;td&gt;desktops number (0 = primary)&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;td&gt;No&lt;/td&gt;&lt;/tr&gt;
&lt;tr&gt;&lt;td&gt;&lt;font class=token&gt;fullscreen&lt;/font&gt;&lt;/td&gt;&lt;td&gt;set to true to capture the desktop when an is fullscreen&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;td&gt;No&lt;/td&gt;&lt;/tr&gt;
&lt;/table&gt;
EndRem
Function DesktopPixmap:TPixmap (flag:Byte = True, Screen:Int = -1, FullScreen:Int = 0 ) 'ADDED Fullscreen parameter Col 14.05.2012

	'Original Funktion von Suco-X

	?Linux
		Return Null
	?MacOS
		Return Null
	?Win32
		Local hwnd:Int
		If FullScreen									'ADDED Col 14.05.2012
			hwnd = GetTopWindow(GetDesktopWindow())		'ADDED Col 14.05.2012
		Else											'ADDED Col 14.05.2012
			hwnd = GetDesktopWindow()					'ADDED Col 14.05.2012
		EndIf											'ADDED Col 14.05.2012
		
		If Not hwnd Return Null
		Local hdc:Int = GetWindowDC(hwnd)
		If Not hdc Return Null
		Local hdcmem:Int = CreateCompatibleDC(hdc)
    		If Not hdcmem Return Null
		Local info:BITMAPINFOHEADER = New BITMAPINFOHEADER
		info.biSize = SizeOf(info)
		info.biPlanes = 1
		info.biCompression = 0
		Local hertz:Int,depth:Int
		
		GetDesktopMode:Int (info.biWidth,info.biHeight,depth,hertz,Screen)
		info.biBitCount = depth
		Local bmpmem:Int  = CreateCompatibleBitmap(hdc, info.biWidth, info.biHeight)
 		If (Not bmpmem) Or (Not SelectObject(hdcmem, bmpmem)) Return Null
		If Not BitBlt(hdcmem,0,0,info.biWidth,info.biHeight,hdc,DesktopX(Screen),DesktopY(Screen),ROP_SRCCOPY) Return Null


		Local out:TPixmap = CreatePixmap(info.biWidth, info.biHeight, PF_BGRA8888)
		If Not GetDIBits(hdcmem, bmpmem, 0, info.biHeight, out.PixelPtr(0,0), info, 0) Return Null
		DeleteDC(hdcmem)
		DeleteObject(bmpmem)
		ReleaseDC(hwnd, hdc)
		out = out.Convert(PF_BGR888) 'dumm gelaufen, man kann nicht direkt in dem Format grabben
		If flag = True out = YFlipPixmap(out)
		Return out
	?
End Function


'-------------------------------------------------------------------

Rem
bbdoc: Get desktop mouse x-coord
returns: coord as integer
about: 
&lt;table&gt;
&lt;tr&gt;&lt;th&gt;Flag&lt;/th&gt;&lt;th&gt;Description&lt;/th&gt;&lt;th&gt;Win32&lt;/th&gt;&lt;th&gt;MacOS&lt;/th&gt;&lt;/tr&gt;
&lt;tr&gt;&lt;td&gt;&lt;font class=token&gt;screen&lt;/font&gt;&lt;/td&gt;&lt;td&gt;desktops number (0 = primary)&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;/tr&gt;
&lt;/table&gt;
EndRem
Function DesktopMouseX:Int () 
	?Linux
		Return 0
	?MacOS
		Return MACOS_GetMouseX ()
	?Win32
		Local Coord:Int[2]
		GetCursorPos(Coord)
		Return Coord[0]
	?
End Function 


'-------------------------------------------------------------------

Rem
bbdoc: Get desktop mouse y-coord
returns: coord as integer
about: 
&lt;table&gt;
&lt;tr&gt;&lt;th&gt;Flag&lt;/th&gt;&lt;th&gt;Description&lt;/th&gt;&lt;th&gt;Win32&lt;/th&gt;&lt;th&gt;MacOS&lt;/th&gt;&lt;/tr&gt;
&lt;tr&gt;&lt;td&gt;&lt;font class=token&gt;screen&lt;/font&gt;&lt;/td&gt;&lt;td&gt;desktops number (0 = primary)&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;td&gt;Yes&lt;/td&gt;&lt;/tr&gt;
&lt;/table&gt;
EndRem
Function DesktopMouseY:Int () 
	?Linux
		Return 0
	?MacOS
		Return MACOS_GetMouseY ()
	?Win32
		Local Coord:Int[2]
		GetCursorPos(Coord)
		Return Coord[1]
	?
End Function 
</textarea><br><br>Then modify your code for the new parameter :-<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
Import chaos.desktopext

Function MainFunction()
	For Local i:Int = 0 To 100
		DrawRect(Rand(0, GraphicsWidth()), Rand(0, GraphicsHeight()), 32, 32)
	Next
	SetColor(255, 0, 0)
	DrawText("Press Escape", 0, 0)
	SetColor(255, 255, 255)
	
	Flip()
	Cls()
End Function

Graphics(1024, 768, 0)
While Not KeyDown(KEY_ESCAPE)
	MainFunction()
Wend
SavePixmapPNG(chaos.desktopext.DesktopPixmap(), "Test_Window.png")
EndGraphics()

Graphics(1024, 768, 32)
While Not KeyDown(KEY_ESCAPE)
	MainFunction()
Wend
SavePixmapPNG(chaos.desktopext.DesktopPixmap(,,True), "Test_Fullscreen.png")
End
</textarea> <br><br></td></tr></table><br>
<a name="1139584"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'll give this a test soon Col, thanks a bunch for the help.<br>Is there any way to make it auto detect if the fullscreen parameter is needed, so it doesn't try to capture fullscreen when there's no fullscreen window. <br><br></td></tr></table><br>
<a name="1139588"></a>

<a name="1139591"></a>

<a name="1139592"></a>

<a name="1139594"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> I did think of that myself and thought it would be nice if Max2D had a IsFullScreen() function, but then I realised...<br><br>From what I understand, your MaxGui fires up the emulator(s) as a seperate process? So is there a flag you can set, maybe on the command line, to start the emus in full screen?<br><br>If no, then I don't know how you would tell if that process has a screen mode that has exclusive ownership of the display or not? You'd need some kind of feedback from the emulator.<br><br>[EDIT REMOVED]<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1139593"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah okay... That seems a bit overly complex.<br>I guess I could just assume it's always fullscreen... I mean who plays Emulators in windowmode on an Arcade machine? x_X<br><br>Thanks again Col. :) <br><br></td></tr></table><br>
<a name="1139595"></a>

<a name="1139596"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> I removed the suggestion anyway ;-)<br><br>I looked it up and while its more than likely possible, the vibe I got was its impossible :D<br><br>1 mo...<br>How are you creating the process??<br>As your firing up the process yourself you can get the window handle from the process handle.<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1139601"></a>

<a name="1139602"></a>

<a name="1139603"></a>

<a name="1139610"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah I'm using Freeprocess to create the process.<br><br>Also, I just tested and your fullscreen capture does work very well! :)<br>Though there are some issues with size, because I seem to be getting very big black edges when running fullscreen in a low resolution.<br><br>EDIT: Actually, that only happened once.. it works fine now. :|<br><br>But yeah... being able to automatically get the fullscreen parameter would be good.<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1139659"></a>

<a name="1139669"></a>

<a name="1139670"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hiya,<br><br>Here ya go :-)<br>After trying a hundred different ways...<br><br>[EDIT] Only seems to reliably pick up Dx windows and not so reliable with OpenGL windows!<br><br><b>IsFullScreen()</b> will return if any windows are in a fullscreen state no matter how/who created them. You can use this as the third parameter with the modified chaos.dekstoppixmap([..])<br><br>Heres a version of your code with it incorporated :-<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict
Import chaos.desktopext

Extern"Win32"
	Function EnumWindows(lpEnumProc:Byte Ptr,lParam Var) 'Customised for our purpose :-)
	Function GetSystemMetrics(nIndex)
EndExtern

Function MainFunction()
	For Local i:Int = 0 To 100
		DrawRect(Rand(0, GraphicsWidth()), Rand(0, GraphicsHeight()), 32, 32)
	Next
	SetColor(255, 0, 0)
	DrawText("Press Escape", 0, 0)
	SetColor(255, 255, 255)
	
	Flip()
	Cls()
End Function

Graphics(1024, 768, 0)
While Not KeyDown(KEY_ESCAPE)
	MainFunction()
Wend
SavePixmapPNG(chaos.desktopext.DesktopPixmap(,,IsFullScreen()), "Test_Window.png")
EndGraphics()

Graphics(1024, 768, 32)
While Not KeyDown(KEY_ESCAPE)
	MainFunction()
Wend
SavePixmapPNG(chaos.desktopext.DesktopPixmap(,,IsFullScreen()), "Test_Fullscreen.png")
End


Function IsFullScreen()
	Function IsTopMost(hWnd)
		Local info:WINDOWINFO = New WINDOWINFO
		GetWindowInfo(hWnd,info)
	
		If info.dwExStyle &amp; WS_EX_TOPMOST Return True
	EndFunction
	
	Function IsFullScreenSize(hWnd,cx,cy)
		Local rect[4]
	
		GetWindowRect(hWnd,rect)
	
		Return (rect[2]-rect[0] = cx) And (rect[3]-rect[1] = cy)
	EndFunction
	
	Function IsFullScreenAndMaximized(hWnd)
		If IsTopMost(hWnd)
			Local cx = GetSystemMetrics(0) 'CM_CXSCREEN
			Local cy = GetSystemMetrics(1) 'CM_CYSCREEN
		
			If IsFullScreenSize(hWnd,cx,cy) Return True
		EndIf
	EndFunction

	Function EnumWindowProc(hWnd,lParam Var)"win32"
		If IsFullScreenAndMaximized(hWnd)
			lParam = True
			Return False
		EndIf
	
		Return True
	EndFunction
	
	Local FullScreenWindow
	EnumWindows(EnumWindowProc,FullScreenWindow)
	
	Return FullScreenWindow
EndFunction
</textarea><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1139661"></a>

<a name="1139662"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> I should note:-<br><br>As the window will be created by a different process, be sure that the window is actually created and showing before using the IsFullScreen() test. It takes some time for windows to do its business in creating the process and window, so it's very possible that if you create the process and then immediately run IsFullScreen(), you may find that this function has finished and returned a value before windows has even had a chance to fire up the other process and create/register/show its window.<br><br>You can call IsFullScreen() as many times as you want to, it doesnt matter, just if you call it too early ( as above ) then you may get a false negative. Pretty much if you can see the window then youre ok.<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1139673"></a>

<a name="1139678"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oh my! :o<br><br>I doubt there are any worries about using it too fast though, as the screenshot is supposed to be taken ingame, and it'll take a few second to actually get ingame.<br><br>Thanks again, and I'll give this a spin once the Guild Wars 2 beta is over, heh.<br><br>UPDATE: Wow thanks mate, this works perfectly sofar!<br>I am very very impressed heh. :)<br><br>I have yet to test it in few other emulators, but everything so far works.<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1139707"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thats cool. Glad its working :-) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
