<!DOCTYPE html><html lang="en" ><head ><title >Mysterious type syntax error</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Mysterious type syntax error</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Mysterious type syntax error</a><br><br>
<a name="952033"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pineapple</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I need help on how to fix this.<br><br><br>my code line being highlighted:<br><br><pre class=code>	Field attatch:obj[8],apos%[8,4],anum%=0,atype%[8]=0,astr#[8]=10 'valid joint types would be weld and axis </pre><br><br>I get the error 'Compile Error  Syntax error in user defined type declaration'<br><br>the rest of the type code (excluding methods)<br><br><pre class=code>
Type obj
	Global GRAVITY=0.95,GIBVELOCITY=0.1
	Global list:TList=CreateList()
	Field img:objimage 'image to draw
	Field x#,y#,ang# 'position &amp; orientation
	Field vel#,dir# 'velocity vector
	Field angvel# 'angular velocity - how fast it's spinning
	Field mass#=10 'determines transfer of force factors when colliding with other objects
	Field friction#=0.015 'determines how rapidly velocities decrease
	Field bounce#=0.5 'bounciness factor
	Field health#=100 'remaining hits until gib or destroy
	'handle of objects attached, pos on poth objs of attachment, number of attachments tied to obj, type of joint, str of joint
	Field attatch:obj[8],apos%[8,4],anum%=0,atype%[8]=0,astr#[8]=10 'valid joint types would be weld and axis 
	'(also technical attachments which would only prevent collision)
	Field needsgib:Byte=False 'means that if the velocity is reduced to a low number, object is gibbed
	'parentkind=0 means that the object is not parented to a bot or gun
	Field parentkind:Byte=0,botparent:bot,gunparent:gun,shotparent:shot,vital:Byte=0 'parent info; vital determines if obj break destroys parent
	'collision group - 0 are guns &amp; bullets that collide with everything, and 1 and 2 are teams that do not collide with themselves
	Field group:Byte=0 'attached objects never collide with each other
	'vertexes determine rotation of an object when force is applied
	Field vertnum:Byte=0,vertpos:Short[32,2] 'vertnum is number of vertexes and vertpos is the offset pos of each one
	
	Function Update()
		For Local o:obj=EachIn obj.list
			o.Draw
			o.CheckParent
			o.UpdatePhysics
		Next
	End Function


        [insert a bunch of methods here]
</pre><br><br><br>Please help me! <br><br></td></tr></table><br>
<a name="952035"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is not valid syntax :<br><br>Field atype%[8]=0<br><br>You cannot have "=0" as part of an array definition. <br><br></td></tr></table><br>
<a name="952039"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pineapple</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> thanks for the help <br><br></td></tr></table><br>
<a name="952040"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pineapple</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Now I've got another error that I'm even more confused over on the line:<br><br><pre class=code>plat.frame(0)=LoadImage("Floor.bmp")</pre><br><br>part of<br><pre class=code>Type objimage
	Global list:TList=CreateList()
	Field frame:timage[16],nowframe%,framenum% 'image handles for all frames, current frame of animation, and max number of frames

	Method Free()
		ListRemove objimage.list,Self
	End Method
	Method Draw(x#,y#,rot#)
		SetRotation rot
		DrawImage frame(nowframe),x,y
		If framenum&gt;1 Then
			nowframe=nowframe+1
			If nowframe=&gt;framenum Then nowframe=0
		EndIf
	End Method
End Type


Global plat:objimage=New objimage
plat.framenum=1
plat.nowframe=0
plat.frame(0)=LoadImage("Floor.bmp")</pre><br><br>I get the error 'Compile Error  Expression of type 'brl.max2d.timage Array' cannot be invoked' <br><br></td></tr></table><br>
<a name="952041"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >klepto2</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> it should be frame[0] instead of frame(0) <br><br></td></tr></table><br>
<a name="952129"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pineapple</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I apologize for all the questions, but I wrote a decent amount of code before I had the opportunity to test it.<br><br>how come when with the obj type and I try to run this method:<br><br><pre class=code>	Method Draw()
		SetRotation ang
		DrawImage img.frame[img.nowframe],x,y
		If img.framenum&gt;1 Then
			img.nowframe=img.nowframe+1
			If img.nowframe=&gt;img.framenum Then img.nowframe=0
		EndIf
	End Method</pre><br><br>it gives me 'Unhandled Exception: Attempt to access field or method of Null object'<br>But when it goes over this line of code elsewhere in a different method,<br><br><pre class=code>If attachgrouped=1 And ImagesCollide2(img.frame[img.nowframe],x,y,0,ang,1,1,other.img.frame[other.img.nowframe],other.x,other.y,0,other.ang,1,1) Then
</pre> <br><br>It executes without any errors? <br><br></td></tr></table><br>
<a name="952142"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Litter some <b>Assert</b>'s around your source, specifically around the failing part..<br><pre class=code>
	Method Draw()
		Assert img And img.frame ' &lt;--- like here
		SetRotation ang
		DrawImage img.frame[img.nowframe],x,y
		If img.framenum&gt;1 Then
			img.nowframe=img.nowframe+1
			If img.nowframe=&gt;img.framenum Then img.nowframe=0
		EndIf
	End Method
</pre><br>If that doesnt help, maybe you forgot to call <b>Graphics</b>? ie. DrawImage or SetRotation could be failing. <br><br></td></tr></table><br>
<a name="952143"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mauft</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am guessing that at the moment of executing this Draw method the img variable is not yet set, or the array in it is not set :). <br><br></td></tr></table><br>
<a name="952144"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pineapple</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> The part that makes it most confusing, is the fact that if I tell it to draw the image in my code's main loop, it doesn't complain. Also, if instead of referencing to the type's field img:objimage I put box:objimage (which is a global variable referencing to one of my objimage, it works fine for plat, the other objimage handle, too)<br>Additionally, I am quite certain that img is the same as plat in this instance:<br><pre class=code>Global ground:obj=New obj
ground.img=plat</pre><br><br>And that thing about the other line working - it's just not even making it to that line of code so I doubt it really would work.<br><br>here's my entire source, and if anyone wants to take a crack at eliminating the bugs or even improving the code, please feel free. I actually have some high hopes for this game so you'll definitely get some recognition.<br><br><br>EDIT: Whoops, I forgot to include the code :O (It's WIP obviously. Right now I'm just trying to get some basic physics down for objects)<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

Type bot
	Global list:TList=CreateList()
	Field team:Byte
	Field avgx#,avgy#
	
	Method Remove()
		ListRemove bot.list,Self
	End Method
End Type
Type bottype
	Global list:TList=CreateList()
End Type


Type gun
	Global list:TList=CreateList()
	Field avgx#,avgy#
	Field ref:guntype

	Method Remove()
		ListRemove gun.list,Self
	End Method
End Type
Type guntype
	Global list:TList=CreateList()
End Type


Type shot
	Global list:TList=CreateList()
	Field avgx#,avgy#
	Field shottype:shotype

	Method Remove()
		ListRemove shot.list,Self
	End Method
End Type
Type shotype
	Global list:TList=CreateList()
	Field sharp%=0 'sharper rounds are more likely to gib rather than destroy
End Type



Type obj
	Global GRAVITY#=0.95,GIBVELOCITY#=0.1
	Global list:TList=CreateList()
	Field img:objimage 'image to draw
	Field x#,y#,ang# 'position &amp; orientation
	Field vel#,dir# 'velocity vector
	Field angvel# 'angular velocity - how fast it's spinning
	Field mass#=10 'determines transfer of force factors when colliding with other objects
	Field friction#=0.015 'determines how rapidly velocities decrease
	Field bounce#=0.5 'bounciness factor
	Field health#=100 'remaining hits until gib or destroy
	'handle of objects attached, pos on poth objs of attachment, number of attachments tied to obj, type of joint, str of joint
	Field attach:obj[8],apos%[8,4],anum%=0,atype%[8],astr#[8] 'valid joint types would be weld and axis 
	'(also technical attachments which would only prevent collision)
	Field needsgib:Byte=False 'means that if the velocity is reduced to a low number, object is gibbed
	'parentkind=0 means that the object is not parented to a bot or gun
	Field parentkind:Byte=0,botparent:bot,gunparent:gun,shotparent:shot,vital:Byte=0 'parent info; vital determines if obj break destroys parent
	'collision group - 0 are guns &amp; bullets that collide with everything, and 1 and 2 are teams that do not collide with themselves
	Field group:Byte=0 'attached objects never collide with each other
	'vertexes determine rotation of an object when force is applied
	Field vertnum:Byte=0,vertpos:Short[32,2] 'vertnum is number of vertexes and vertpos is the offset pos of each one
	
	Function Update()
		For Local o:obj=EachIn obj.list
			o.Draw
			o.CheckParent
			o.UpdatePhysics
		Next
	End Function

	Method Remove(gib:Byte=False)
		If vital Then 'the part is vital to parent's function so kill parent but keep other parts
			If parentkind=1 Then botparent.Remove 'parent is a bot
			If parentkind=2 Then gunparent.Remove 'parent is a gun
			If parentkind=3 Then gunparent.Remove 'parent is a shot
		EndIf
		If Not gib Then 
			ListRemove obj.list,Self 
		Else
			needsgib=True
			anum=0 'get rid of all attachments
			group=0 'change group to collide with ex-teammates
		EndIf
	End Method
	
	Method CheckParent() 'give parent info on its coords and unparent the object if the parent no longer exists
		If parentkind=1 Then
			If botparent=Null Then 
				parentkind=0
			Else
				botparent.avgx=(botparent.avgx+x)/2.0
				botparent.avgy=(botparent.avgy+y)/2.0
			EndIf
		ElseIf parentkind=2 Then
			If gunparent=Null Then 
				parentkind=0
			Else
				gunparent.avgx=(gunparent.avgx+x)/2.0
				gunparent.avgy=(gunparent.avgy+y)/2.0
			EndIf
		ElseIf parentkind=3 Then
			If shotparent=Null Then 
				parentkind=0
			Else
				shotparent.avgx=(gunparent.avgx+x)/2.0
				shotparent.avgy=(gunparent.avgy+y)/2.0
			EndIf
		EndIf	
	End Method
	
	Method Draw()
		img.draw(x,y,ang) 'plat.draw and box.draw work just fine, so why won't this??
	End Method
	
	Method UpdatePhysics(acc:Short=2) 'Perform physics on an object - higher accuracy means slower physics
		For Local check:Short=1 To acc
			Local acf#=Float(acc)
			x:+Cos(dir)*vel/acf
			y:+Sin(dir)*vel/acf
			y:+(obj.GRAVITY+(mass/220))/acf
			ang:+angvel/acf
			vel:-friction/acf;angvel:-friction/acf
			For Local other:obj=EachIn obj.list
				'verify correct collision groups
				If (other.group&gt;0 And group&gt;0) And Not(other.group=1 And group=1) And Not(other.group=2 And group=2)
					'ignore collision when objects are attached to each other
					Local attachgrouped:Byte=0,checka:Byte=1
					If anum&gt;0 Then
						For checka:Byte=1 To anum
							If attach[checka-1]=other Then attachgrouped=1;Exit
						Next
					EndIf
					If other.anum&gt;0 Then
						If attachgrouped=0 Then
							For checka:Byte=1 To other.anum
								If other.attach[checka-1]=Self Then attachgrouped=1;Exit
							Next
						EndIf
					EndIf
					If attachgrouped=1 And ImagesCollide2(img.frame[img.nowframe],x,y,0,ang,1,1,other.img.frame[other.img.nowframe],other.x,other.y,0,other.ang,1,1) Then
						'consider newton's 1st law and cause the objects to bounce off each other
							Local addvel#=0,addovel#=0
							'adjust object velocity vectors based on mass
							addvel:+(mass-other.mass)/(1+friction)
							addovel:+(other.mass-mass)/(1+other.friction)
							'Cause objects to change velocity direction
							Local angnow#=mass/other.mass,neg%=1
							If angnow&gt;180 Then angnow=180
							If angnow Mod 2=0 Then neg=-1
							dir=(dir+angnow*neg)/2
							angnow#=mass/other.mass;neg%=1
							If angnow&gt;180 Then angnow=180
							If angnow Mod 2=0 Then neg=-1
							other.dir=(other.dir+angnow*neg)/2
							'consider bounciness
							addvel:*(bounce+other.bounce/4.0)
							addovel:*(bounce/4.0+other.bounce)
							'apply velocity changes
							vel:+addvel
							other.vel:+addovel
						'cause damage to both objects
							Local damage#=0,damageo#=0
							damage=other.mass*other.vel
							damageo=mass*vel
							If other.parentkind=3 Then damage=damage+other.shotparent.shottype.sharp*6
							If parentkind=3 Then damageo=damageo+shotparent.shottype.sharp*6
							If health-damage=&lt;0 Then health=0 Else health:-damage/3
							If other.health-damage=&lt;0 Then other.health=0 Else other.health:-damage/3
							'determine whether to gib or to destroy objects if their health has depleted to 0
							If health=0 Then
								If other.parentkind=3 Then
									If other.shotparent.shottype.sharp&gt;damage Then needsgib=True Else needsgib=False
								Else
									If mass&gt;other.mass Then needsgib=True Else needsgib=False
								EndIf
							EndIf
							If other.health=0 Then
								If parentkind=3 Then
									If shotparent.shottype.sharp&gt;damageo Then other.needsgib=True Else other.needsgib=False
								Else
									If mass&lt;other.mass Then other.needsgib=True Else other.needsgib=False
								EndIf
							EndIf
						'break attachments to other objects if necessary
							Local breakforce#=0,breakas:Byte
							For breakas=1 To anum 'for this object
								If attach[breakas-1] Then
									breakforce=Abs((dir+720)*vel/2.0-(attach[breakas-1].dir+720)*attach[breakas-1].vel/2.0)
									If breakforce&gt;astr[breakas-1] Then attach[breakas-1]=Null
								EndIf
							Next
							For breakas=1 To other.anum 'for other object
								If other.attach[breakas-1] Then
									breakforce=Abs((other.dir+720)*vel/2.0-(other.attach[breakas-1].dir+720)*other.attach[breakas-1].vel/2.0)
									If breakforce&gt;other.astr[breakas-1] Then other.attach[breakas-1]=Null
								EndIf
							Next
							
						'find the closest and the farthest two vertexes to determine rotation
							'dist1 is the shortest distance between 2 vertexes, 2 is longest dist to vert from one of this object's verts,
							'3 is the longest from the other objects vert, chosen keeps track of which verts were determined
							Local dist1#=-1,dist2#=-1,dist3#=-1,chosen:Byte[4]
							For Local vert1:Byte=1 To vertnum
								For Local vert2:Byte=1 To other.vertnum
									Local distnow#=Distance(vertpos[vert1-1,0],vertpos[vert1-1,1],other.vertpos[vert2-1,0],other.vertpos[vert2-1,1])
									If distnow&lt;dist1 Or dist1&lt;0 Then dist1=distnow;chosen[0]=vert1;chosen[1]=vert2
									Local last1:Byte=vert1,last2:Byte=vert2
								Next
							Next
							Local distnow#=-1
							For Local vert1:Byte=1 To vertnum 'farthest vertex on this object
								distnow#=Distance(vertpos[chosen[0],0],vertpos[chosen[0],1],other.vertpos[vert1-1,0],other.vertpos[vert1-1,1])
								If distnow&gt;dist2 Or dist2&lt;0 Then dist2=distnow;chosen[2]=vert1
							Next
							distnow=-1
							For Local vert1:Byte=1 To vertnum 'farthest vertex on other object
								distnow#=Distance(other.vertpos[chosen[0],0],other.vertpos[chosen[0],1],vertpos[vert1-1,0],vertpos[vert1-1,1])
								If distnow&gt;dist3 Or dist3&lt;0 Then dist3=distnow;chosen[3]=vert1
							Next
						'apply angular velocity (is extremely inaccurate ATM)
							If Not Int(dist1)=Int(dist2) Then 'adjust angular velocity due to collision
								If dist1&gt;-1 And dist2&gt;-1 And dist3&gt;-1 Then
									angvel=dist2/Sin(dist1/dist2)*Sin(dist2/dist1)
									other.angvel=dist3/Sin(dist1/dist3)*Sin(dist3/dist1)
								EndIf
							EndIf
						
						Exit
					EndIf
				EndIf
			Next
		Next
	End Method
	
End Type

Type objimage
	Global list:TList=CreateList()
	Field frame:timage[16],nowframe%,framenum% 'image handles for all frames, current frame of animation, and max number of frames
	
	Method Free()
		ListRemove objimage.list,Self
	End Method
	Method Draw(x#,y#,rot#)
		SetRotation rot
		DrawImage frame[nowframe],x,y
		If framenum&gt;1 Then
			nowframe=nowframe+1
			If nowframe=&gt;framenum Then nowframe=0
		EndIf
	End Method
End Type


Function Distance#(x0#,y0#,x1#,y1#)
	Return ((x0-x1)^2+(y0-y1)^2)^0.5
End Function


Graphics 640,480
SetClsColor 80,130,177
AutoMidHandle(1)

SetMaskColor 255,0,255
Global plat:objimage=New objimage
plat.framenum=1
plat.nowframe=0
plat.frame[0]=LoadImage("Floor.bmp")
Global box:objimage=New objimage
box.framenum=1
box.nowframe=0
box.frame[0]=LoadImage("Cube.bmp")
objimage.list.addlast plat
objimage.list.addlast box
Global ground:obj=New obj
ground.img=plat
ground.x=200;ground.y=400
ground.mass=750
ground.vertnum=4
ground.vertpos[0,0]=0
ground.vertpos[0,1]=0
ground.vertpos[1,0]=539
ground.vertpos[1,1]=0
ground.vertpos[2,0]=539
ground.vertpos[2,1]=47
ground.vertpos[3,0]=0
ground.vertpos[3,1]=47
Global cube1:obj=New obj
cube1.img=box
cube1.x=210;cube1.y=300
cube1.mass=40
cube1.vertnum=4
cube1.vertpos[0,0]=0
cube1.vertpos[0,1]=0
cube1.vertpos[1,0]=15
cube1.vertpos[1,1]=0
cube1.vertpos[2,0]=15
cube1.vertpos[2,1]=15
cube1.vertpos[3,0]=0
cube1.vertpos[3,1]=15
Global cube2:obj=New obj
cube1.img=box
cube2.x=260;cube2.y=300
cube2.mass=40
cube2.vertnum=4
cube2.vertpos[0,0]=0
cube2.vertpos[0,1]=0
cube2.vertpos[1,0]=15
cube2.vertpos[1,1]=0
cube2.vertpos[2,0]=15
cube2.vertpos[2,1]=15
cube2.vertpos[3,0]=0
cube2.vertpos[3,1]=15
obj.list.addlast ground
obj.list.addlast cube1
obj.list.addlast cube2

Repeat
	Cls
	
		ground.x=200
		ground.y=400
		
		obj.Update()
	
	Flip
	If KeyDown(27) Then End
Forever
</textarea> <br><br></td></tr></table><br>
<a name="952254"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pineapple</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Buuump I really would appreciate some help to fix this <br><br></td></tr></table><br>
<a name="952257"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> On line 308, you set <b>cube1.img</b> instead of <b>cube2.img</b> ;) <br><br></td></tr></table><br>
<a name="952261"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pineapple</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Dang I can't believe I missed that, thanks <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
