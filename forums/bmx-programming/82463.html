<!DOCTYPE html><html lang="en" ><head ><title >Drawing one image onto another</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Drawing one image onto another</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Drawing one image onto another</a><br><br>
<a name="930100"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GfK</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm currently drawing to backbuffer, then drawing the second image on top, then grabbing it into a pixmap.  Works, but I wonder if there's a better way?  Plus I've heard tales about GrabPixmap not working on some hardware.<br><br>Couple of points to consider:<br><br>1. I need to draw the second image onto the first with alpha.<br>2. I don't need to do it on-the-fly.<br>3. The code needs to draw several images onto another, with a grabPixmap at the end.  Repeat x 108. <br><br></td></tr></table><br>
<a name="930109"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jur</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am using the following function to combine several layers of tiles into one tile.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Function MakeBlendedImage:TImage(Image:TImage[])
	If image.length=Null Then Return
	Local Pixmap:TPixmap=CopyPixmap(image[0].pixmaps[0])
	
	Local R1,G1,B1,A1	'overlaying image  	
	Local R2,G2,B2,A2	'background image 
	Local finalAlpha#	
	For Local i=1 Until image.length
		If image[i]=Null Then Continue
		For Local x=0 Until pixmap.width
			For Local y=0 Until pixmap.height
				GetRGBAfromColorValue(ReadPixel(pixmap,x,y),R2,G2,B2,A2)
				GetRGBAfromColorValue(ReadPixel(image[i].pixmaps[0],x,y),R1,G1,B1,A1)
				finalAlpha=GetFinalAlpha(A1,A2)
				WritePixel(Pixmap,x,y, GetColorValueFromRGBA(GetFinalColor(R1,A1,R2,A2, finalAlpha), GetFinalColor(G1,A1,G2,A2,finalAlpha), GetFinalColor(B1,A1,B2,A2,finalAlpha),finalAlpha))
			Next
		Next
	Next
	
	Function GetFinalColor(color1,alpha1, color2,alpha2,  finalAlpha#)
		Return (alpha1*color1/finalAlpha + alpha2*color2*(255-alpha1)/finalAlpha/255.0)
	EndFunction
	
	Function GetFinalAlpha:Float(alpha1,alpha2)
		Return (255-(255-alpha1)*(255-alpha2)/255.0)
	EndFunction

	Return LoadImage(Pixmap)
EndFunction

</textarea><br><br><br>The first index of image array is the bottom layer. All images must be of the same size. For images with diferent sizes, the read-write pixel loop should be changed.<br><br>Jure <br><br></td></tr></table><br>
<a name="930127"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> in an older library I use:<br><pre class=code>
Function ARGB_Alpha:Int(ARGB:Int)
 Return (argb Shr 24) &amp; $ff
 'Return Int((ARGB &amp; $FF000000:Int) / $1000000:Int) 
End Function

Function ARGB_Red:Int(ARGB:Int)
  Return (argb Shr 16) &amp; $ff
' Return Int((ARGB &amp; $00FF0000:Int) / $10000:Int)
End Function

Function ARGB_Green:Int(ARGB:Int)
  Return (argb Shr 8) &amp; $ff
' Return Int((ARGB &amp; $0000FF00:Int) / $100:Int)
End Function

Function ARGB_Blue:Int(ARGB:Int)
 Return (argb &amp; $ff) 
' Return (ARGB &amp; $000000FF:Int)
End Function

Function ARGB_Color:Int(alpha:Int,red:Int,green:Int,blue:Int)
 Return (Int(alpha * $1000000) + Int(RED * $10000) + Int(green * $100) + Int(blue)) 
End Function

Function DrawOnPixmap(image:TImage, framenr:Int = 0, Pixmap:TPixmap, x:Int, y:Int, alpha:Float = 1.0, light:Float = 1.0, multiply:Int = 0) 
      Local TempPix:TPixmap = Null
	  If image = Null Then Throw "image doesnt exist"
	  If framenr = 0 Then TempPix = LockImage(image) 
      If framenr &gt; 0 Then TempPix = LockImage(image, Framenr) 
	  For Local i:Int = 0 To ImageWidth(image) - 1
	    For Local j:Int = 0 To ImageHeight(image) - 1
		  If x + i &lt; pixmap.width And y + j &lt; pixmap.Height 'And i &gt;= x And j &gt;= y
			Local sourcepixel:Int = ReadPixel(TempPix, i, j) 
			Local destpixel:Int = ReadPixel(pixmap, x+i,y+j)
			Local destA:Float = ARGB_Alpha(destpixel) 
			Local sourceA:Float = ARGB_Alpha(sourcepixel) * alpha
			If sourceA = 255 Then destA = 0
			'remove comment to remove unneeded calculations 
			'but only when light/alpha not used!
'			If sourceA &lt;&gt; 255 And sourceA &lt;&gt; 0
				Local destR:Float = ARGB_Red(destpixel) 
				Local destG:Float = ARGB_Green(destpixel) 
				Local destB:Float = ARGB_Blue(destpixel) 
				Local SourceR:Float = ARGB_Red(Sourcepixel) 
				Local SourceG:Float = ARGB_Green(Sourcepixel) 
				Local SourceB:Float = ARGB_Blue(Sourcepixel) 
					Local AlphaSum:Int = destA + sourceA
					If multiply = 1
						sourceR = (sourceR * light * sourceA / AlphaSum) + destA / AlphaSum * (destR * destA / AlphaSum) 
						sourceG = (sourceG * light * sourceA / AlphaSum) + destA / AlphaSum * (destG * destA / AlphaSum) 
						sourceB = (sourceB * light * sourceA / AlphaSum) + destA / AlphaSum * (destB * destA / AlphaSum) 
					Else
						sourceR = (sourceR * light * sourceA / AlphaSum) + (destR * destA / AlphaSum) 
						sourceG = (sourceG * light * sourceA / AlphaSum) + (destG * destA / AlphaSum) 
						sourceB = (sourceB * light * sourceA / AlphaSum) + (destB * destA / AlphaSum) 
					EndIf
					If AlphaSum &gt; 255 Then AlphaSum = 255
					sourcepixel = ARGB_Color(AlphaSum, SourceR, sourceG, sourceB) 
'			EndIf
			If SourceA &lt;&gt; 0 Then WritePixel(Pixmap, x + i, y + j, sourcepixel) 
		  EndIf
		Next
	  Next
	  If framenr = 0 UnlockImage(image)
	  If framenr &gt; 0 UnlockImage(image, framenr)
End Function
</pre><br><br><br>It draws an image into a pixmap.<br><br><br>bye MB <br><br></td></tr></table><br>
<a name="930138"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GfK</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Isn't the pixel-by-pixel approach incredibly slow? <br><br></td></tr></table><br>
<a name="930140"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I really think somebody should nail the RTT omission.<br>Can you use the pixmap paste method? I never know what allows alpha and what doesn't. Maybe a memcopy would work using the pixelptr value.<br>I wrote some truly horrible code <a href="/codearcs.php?code=1692" target="_blank"> here </a> which might be of use. <br><br></td></tr></table><br>
<a name="930141"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I used one of these two (can't remember which) in Fairway.  The image was pre-calced before the screen was shown, wasn't slow enough to notice:<br><br><pre class=code>' -----------------------------------------------------------------------------
' ccCopyImageRect: Copies part of an image onto another image
' by James Chamblin
' -----------------------------------------------------------------------------
Function ccCopyImageRect(Source:TImage,SX:Int,SY:Int,SWidth:Int,SHeight:Int,Dest:TImage,DX:Int,DY:Int)
	'get the pixmap for the images
	Local SourcePix:TPixmap = LockImage(Source)
	Local DestPix:TPixmap = LockImage(Dest)
	
	'find the dimentions
	Local SourceWidth:Int = PixmapWidth(SourcePix)
	Local SourceHeight:Int = PixmapHeight(SourcePix)
	Local DestWidth:Int = PixmapWidth(DestPix)
	Local DestHeight:Int = PixmapHeight(DestPix)
	
	If SX &lt; SourceWidth And SY &lt; SourceHeight And DX &lt; DestWidth And DY &lt; DestHeight 'make sure rects are on image
		If SX+SWidth &gt; SourceWidth Then SWidth = SourceWidth - SX 'bound the coordinates to the image area
		If SY+SHeight &gt; SourceHeight Then SHeight = SourceHeight - SY
		If DX+SWidth &gt; DestWidth Then SWidth = DestWidth - DX 'Make sure coordinates will fit into the destination
		If DY+SHeight &gt; DestHeight Then SHeight = DestHeight - DY
		
		'find the pitch
		Local SourcePitch:Int = PixmapPitch(SourcePix)
		Local DestPitch:Int = PixmapPitch(DestPix)
	
		'pointers To the first pixel of pixmaps
		Local SourcePtr:Byte Ptr = PixmapPixelPtr(SourcePix) + SY * SourcePitch + SX * 4
		Local DestPtr:Byte Ptr = PixmapPixelPtr(DestPix) + DY * DestPitch + DX * 4
		
		'copy pixels over one line at a time
		For Local i:Int = 1 To SHeight
			MemCopy(DestPtr,SourcePtr,SWidth*4)
			SourcePtr :+ SourcePitch
			DestPtr :+ DestPitch
		Next
	End If
	
	'unlock the buffers
	UnlockImage(Source)
	UnlockImage(Dest)
End Function

' -----------------------------------------------------------------------------
' ccCopyImageToImage: Copies one TImage to another with a variety of modes
' by Dave Munsie
' -----------------------------------------------------------------------------
Function ccCopyImageToImage(Source:TImage,SX:Int,SY:Int,SWidth:Int,SHeight:Int,Dest:TImage,DX:Int,DY:Int,flags:Int=0)
  '
  ' flags: 0 = Normal  1 = Mirror  2 = Flip  3 = Mirror and Flip
  '
  Local SourcePix:TPixmap = LockImage(Source)
  Local DestPix:TPixmap = LockImage(Dest)
  Local SourceWidth:Int = PixmapWidth(SourcePix)
  Local SourceHeight:Int = PixmapHeight(SourcePix)
  Local DestWidth:Int = PixmapWidth(DestPix)
  Local DestHeight:Int = PixmapHeight(DestPix)
  If SX &lt; SourceWidth And SY &lt; SourceHeight And DX &lt; DestWidth And DY &lt; DestHeight 
  If SX+SWidth &gt; SourceWidth Then SWidth = SourceWidth - SX 
  If SY+SHeight &gt; SourceHeight Then SHeight = SourceHeight - SY
  If DX+SWidth &gt; DestWidth Then SWidth = DestWidth - DX 
  If DY+SHeight &gt; DestHeight Then SHeight = DestHeight - DY
  Select flags
   Case 0 ' Normal
    For Local py:Int = 0 To SHeight-1 
     For Local px:Int = 0 To SWidth- 1
      WritePixel(DestPix,DX+px,DY+py,ReadPixel(SourcePix,SX+px,SY+py))
     Next 
    Next
   Case 1 ' Mirror
    For Local py:Int = 0 To SHeight-1 
     For Local px:Int = 0 To SWidth- 1
      WritePixel(DestPix,DX+px,DY+py,ReadPixel(SourcePix,(SX+(SWidth-1))-px,SY+py))
     Next
    Next
   Case 2 ' Flip
    For Local py:Int = 0 To SHeight-1 
     For Local px:Int = 0 To SWidth- 1
      WritePixel(DestPix,DX+px,DY+py,ReadPixel(SourcePix,SX+px,(SY+(SHeight-1))-py))
     Next
    Next
   Case 3 ' Mirror and Flip
    For Local py:Int = 0 To SHeight-1 
     For Local px:Int = 0 To SWidth- 1
      WritePixel(DestPix,DX+px,DY+py,ReadPixel(SourcePix,(SX+(SWidth-1))-px,(SY+(SHeight-1))-py))
     Next
    Next
  End Select
 EndIf
 UnlockImage(Source)
 UnlockImage(Dest)
End Function

</pre> <br><br></td></tr></table><br>
<a name="930145"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GfK</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Can you use the pixmap paste method? <br></div>I *think* I may have tried that.  It's months since I did this stuff and only now am I getting to fixing it up properly. :/<br><br>I'll give it a try.<br><br>[edit] No alpha with tPixmap.Paste(). <br><br></td></tr></table><br>
<a name="930171"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> The code above works with alpha. <br><br></td></tr></table><br>
<a name="930174"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GfK</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> The code above works with alpha.  <br></div><br>Yeah I'll have to try those tomorrow and see how they shape up speedwise.<br><br>[edit] Just tried them now.  Neither of them do what I want (and they're both largely the same anyway).  The zero alpha pixels of the image obliterate anything else that was there before, when those pixels should remain untouched.  That's exactly the same result as tPixmap.paste().<br><br>MichaelB's works, but it isn't fast.  32ms to paste a 128x128 image.  Its saving grace is that it avoids the need for GrabPixmap. <br><br></td></tr></table><br>
<a name="930193"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oh, I just checked and I used ccDrawOnPixmap for Fairway and it seemed fine with a transparent image on another image, but perhaps I missed something. <br><br></td></tr></table><br>
<a name="930233"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> You don't really need to define your own code for copying one pixmap onto another - Blitz already provides pixmap windows/static pixmaps, where you can say that one pixmap is a window within an existing 2nd pixmap, and then using pixmap.paste should I presume clip to the window. Whether you call memcopy to copy a whole row of pixels, or do it in your own loop, probably isn't much different seeming as the bottleneck is memory accesses. <br><br></td></tr></table><br>
<a name="930447"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Like mentioned within my Code...<br>To speed it up you have to remove some comments - it's only for me using the method to precalculate some sprites which get dynamically positioned on some backgroundsprites.<br><br>If you "deactivate" the calculations for multiplying and lightning my code should get a small boost - but then you end up ignoring alpha channels, but since its "pixel by pixel" it will never win the race ;D.<br><br>I don't think there is a proper "faster" way of doing it without touching the whole part of allocated memory, pointers and some magic calculations with no need to read RGBA separately.<br><br><br>So one my test if he/she can calculate the resulting color as ONE integer without separating RGBA - this will speed up things.<br><br><br>But like mentioned, I don't use my code for real-time, just for precalculation. And yes, it's no problem to colorize the sprites giving additional parameters for percentively change RGB-Values (eg creating different colorized playersprites placed on one image for performance issues).<br><br><br>bye MB <br><br></td></tr></table><br>
<a name="930479"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> .. in case you're still looking <a href="/posts.php?topic=51889#579455" target="_blank"> this </a> might help. <br><br></td></tr></table><br>
<a name="930506"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Somewhere in some old thread I posted cost to do an alphablended zooming rotating pixmap onto another using pointer access, it should be pretty quick.<br><br>See here:<br><br><a href="http://www.blitzbasic.com/Community/posts.php?topic=59342#661050" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=59342#661050</a> <br><br></td></tr></table><br>
<a name="939355"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GfK</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just a quick note - decided to use MichaelB's code but with some tiny modifications.  I've added in a scaling parameter, and also modified it to take into account an image's handle.  I don't need rotation (yet) so I didn't add it.<br><br><pre class=code>Function ARGB_Alpha:Int(ARGB:Int)
 Return (argb Shr 24) &amp; $ff
 'Return Int((ARGB &amp; $FF000000:Int) / $1000000:Int) 
End Function

Function ARGB_Red:Int(ARGB:Int)
  Return (argb Shr 16) &amp; $ff
' Return Int((ARGB &amp; $00FF0000:Int) / $10000:Int)
End Function

Function ARGB_Green:Int(ARGB:Int)
  Return (argb Shr 8) &amp; $ff
' Return Int((ARGB &amp; $0000FF00:Int) / $100:Int)
End Function

Function ARGB_Blue:Int(ARGB:Int)
 Return (argb &amp; $ff) 
' Return (ARGB &amp; $000000FF:Int)
End Function

Function ARGB_Color:Int(alpha:Int,red:Int,green:Int,blue:Int)
 Return (Int(alpha * $1000000) + Int(RED * $10000) + Int(green * $100) + Int(blue)) 
End Function

Function DrawOnPixmap(image:TImage, framenr:Int = 0, scale:Float, Pixmap:TPixmap, x:Int, y:Int, alpha:Float = 1.0, light:Float = 1.0) 
      Local TempPix:TPixmap = Null
	  If image = Null Then Throw "image doesnt exist"
	  If framenr = 0 Then TempPix = LockImage(image) 
      If framenr &gt; 0 Then TempPix = LockImage(image, Framenr)
	  If scale &lt;&gt; 1
	  	TempPix = ResizePixmap(TempPix,PixmapWidth(TempPix) * scale,PixmapHeight(TempPix) * scale)
	  EndIf
	  x:-(image.handle_x * scale)
	  y:-(image.handle_y * scale)
	  For Local i:Int = 0 To PixmapWidth(tempPix ) - 1
	    For Local j:Int = 0 To PixmapHeight(tempPix ) - 1
		  If x + i &lt; Pixmap.width And y + j &lt; Pixmap.Height
			Local sourcepixel:Int = ReadPixel(TempPix, i,j)
			Local destpixel:Int = ReadPixel(Pixmap, x+i,y+j)
			Local destA:Float = ARGB_Alpha(destpixel) 
			Local sourceA:Float = ARGB_Alpha(sourcepixel) * alpha
			If sourceA = 255 Then destA = 0
			'remove comment to remove unneeded calculations 
			'but only when light/alpha not used!
'			If sourceA &lt;&gt; 255 And sourceA &lt;&gt; 0
				Local destR:Float = ARGB_Red(destpixel) 
				Local destG:Float = ARGB_Green(destpixel) 
				Local destB:Float = ARGB_Blue(destpixel) 
				Local SourceR:Float = ARGB_Red(Sourcepixel) 
				Local SourceG:Float = ARGB_Green(Sourcepixel) 
				Local SourceB:Float = ARGB_Blue(Sourcepixel) 
					Local AlphaSum:Int = destA + sourceA

					sourceR = (sourceR * light * sourceA / AlphaSum) + destA / AlphaSum * (destR * destA / AlphaSum) 
					sourceG = (sourceG * light * sourceA / AlphaSum) + destA / AlphaSum * (destG * destA / AlphaSum) 
					sourceB = (sourceB * light * sourceA / AlphaSum) + destA / AlphaSum * (destB * destA / AlphaSum) 
					If AlphaSum &gt; 255 Then AlphaSum = 255
					sourcepixel = ARGB_Color(AlphaSum, SourceR, sourceG, sourceB) 
'			EndIf
			If SourceA &lt;&gt; 0 Then WritePixel(Pixmap, x + i, y + j, sourcepixel) 
		  EndIf
		Next
	  Next
	  If framenr = 0 UnlockImage(image)
	  If framenr &gt; 0 UnlockImage(image, framenr)
End Function</pre> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
