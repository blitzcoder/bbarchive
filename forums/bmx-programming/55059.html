<!DOCTYPE html><html lang="en" ><head ><title >Requesting Code Snippets on movement controllers</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Requesting Code Snippets on movement controllers</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Requesting Code Snippets on movement controllers</a><br><br>
<a name="613040"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey guys, I was wondering if you could post a few code snippets for movement paths?<br><br>Right now I am using bezier curves to move my aliens, which looks GREAT but....  Well, there are a few things I would like to do.<br><br>Like.. move down.. spin.. move back up... move to point A, curve around point B, etc.<br><br>Right now its just "follow bezier curve" and that's not working for what I want to do.<br><br>Would be nice if some of you Pro's out there would be willing to post a few code snippets on other methods on controlling enemy objects!!!<br><br>Thanks :) <br><br></td></tr></table><br>
<a name="613213"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >klepto2</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> <textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Type TMove_Event
	
	Method Update:Byte(Child:TMobject)
	End Method
	
End Type

Type TWayPoint Extends TMove_Event
	Field X:Float
	Field Y:Float
	Field Speed:Float
	Field IsInRangeX:Byte
	Field IsInRangeY:Byte
	
	Function create:TWayPoint(X:Float,Y:Float,S:Float)
		Local W:TWayPoint = New TWayPoint
		W.X = X
		W.Y = Y
		W.Speed = S
		Return W
	End Function
	
	Method Update:Byte(Child:TMobject)
		
		Local CX:Float = Child.X
		Local CY:Float = Child.Y
		
		Local Angle=(ATan2(Y-CY,X-CX)+360) Mod 360 
		
		Local vx:Float = Cos(Angle)*Speed
		Local vy:Float = Sin(Angle)*Speed
		
		Child.X:+Vx
		Child.Y:+Vy
		
		IsInrangeX = CheckRange(CX,X,2)
		IsInrangeY = CheckRange(CY,Y,2)
		
		If  IsInRangeX = True And IsInRangeY = True Then 
			Return True
		Else
			Return False
		End If
		
	End Method
	
End Type

Type TRotateAround Extends TMove_Event
	Field Speed:Float	
	Field Times:Int
	Field CurRot:Int = 0
	Field Angle:Float = 0
	
	Function create:TRotateAround(Speed:Float,Times:Int)
		Local R:TRotateAround = New TRotateAround
		R.Speed = Speed
		R.Times = Times
		
		Return R
	End Function
	
	Method Update:Byte(Child:TMObject)
		Self.angle:+Speed
		If Self.Angle &gt; 359 Then
			Self.Angle = 0
			CurRot:+1
		End If
		
		Child.Angle = Self.Angle
		
		If Times = CurRot Then 
			CurRot = 0
			Return True
		Else
			Return False
		End If
	End Method
	
End Type
		
Type TMovement_Manager
	Field Events:TMove_Event[0]
	Field Current_Event:Int = 0
	Field Child:TMObject
	
	
	Function create:TMovement_Manager(Child:TMObject)
		Local M:TMovement_Manager = New TMovement_Manager
		M.Child = Child
		Return M
	End Function
	
	Method AddEVent(Event:TMove_Event)
		Events = Events[..EVents.length+1]
		Events[Events.Length-1] = Event
	End Method
	
	Method Update()
		
		Local W:Byte
	    W = Events[Current_Event].Update(Child)
		
		If W = True  Then
			Current_Event:+1
			If Current_Event &gt; Events.Length-1 Then Current_Event = 0
		EndIf		
		
	End Method
	
	Method GetWayPoint:Int()
		Return Current_WayPoint
	End Method
End Type

Function CheckRange:Byte(P1:Int,P2:Int,Toleranz:Int)
	If P1+Toleranz &gt; P2 And P1-Toleranz &lt; P2 Then Return True
    Return False
End Function

Type TMObject
	Field x:Float
	Field y:Float
	Field angle:Float
End Type


Type Player Extends TMobject

	Function create:Player()
		Return New Player
	End Function
	
	Method Draw()
		SetRotation Angle
		DrawRect x,y,20,20
		SetRotation 0
	End Method
End Type

Graphics 800,600,0,60

Local P:Player = Player.Create()
P.X = 300
P.Y = 400

Local Way1:TWayPoint = TWayPoint.Create(200,200,2.0)
Local Way2:TWayPoint = TWayPoint.Create(100,400,4.0)
Local Way3:TWayPoint = TWayPoint.Create(600,500,1.0)
Local Way4:TWayPoint = TWayPoint.Create(200,200,5.0)
Local Turn1:TRotateAround = TRotateAround.Create(5.0,10)
Local Turn2:TRotateAround = TRotateAround.Create(10.0,20)


Local Manager:TMovement_Manager = TMoveMent_Manager.Create(P)
Manager.AddEvent(Way1)
Manager.AddEvent(Turn1)
Manager.AddEvent(Way2)
Manager.AddEvent(Way3)
Manager.AddEvent(Turn2)
Manager.AddEvent(Way4)

While Not KeyHit(Key_Escape)

P.Draw()
Manager.Update()

DrawText "X : " + P.X,20,20
DrawText "Y : " + P.Y,20,40
DrawText "WayPoint : " + Manager.GetWayPoint(),20,60
'DrawText "IsinX : " + Manager.IsInRangeX,20,80
'DrawText "IsInY : " + Manager.IsInRangeY,20,100

Flip
Cls
Wend
</textarea><br><br>Its late here, so I haven't comment it, if you have questions to the code just ask. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
