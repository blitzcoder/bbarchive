<!DOCTYPE html><html lang="en" ><head ><title >Frame limiting</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Frame limiting</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Frame limiting</a><br><br>
<a name="548127"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tom</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> A search of the B'max forum isn't bringing up much, anyone have some nice frame limiting code? <br><br></td></tr></table><br>
<a name="548134"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I go with delta timing.I posted my code over at codersworkshop. <br><br></td></tr></table><br>
<a name="548142"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tom</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Got a link to it?<br><br>Cheers<br>Tom <br><br></td></tr></table><br>
<a name="548158"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Heh, I'll post it here<br><br><pre class=code>



Type DeltaTime

	Global DeltaTList:TList

	Field OldTime:Double
	Field ElapsedTime:Double
	Field Delta:Double
	Field dFps:Double
	Field dDelta:Double
	Field CurrFps:Double
	Field FPSCounter:Double
	Field CurrTime:Double
	Field CheckTime:Double
	
		Method New ()
            	If DeltaTList = Null Then DeltaTList = New TList
            	DeltaTList.AddLast Self
    	End Method		
		
		Method Destroy ()
            	DeltaTList.Remove Self
        End Method

	
		Function Create:DeltaTime(Fps:Double)
			Local dTime:DeltaTime = New DeltaTime
			dTime.Delta = 1
			dTime.dFps = Fps
			dTime.dDelta = 1000/Fps
			Return dTime
		End Function
		
		Function Update()
			Local dTime:DeltaTime
			For dTime:DeltaTime = EachIn DeltaTList
				dTime.CurrTime = MilliSecs()
				dTime.ElapsedTime = dTime.CurrTime - dTime.OldTime
				dTime.OldTime = dTime.CurrTime
				
				If dTime.CurrTime &gt; dTime.CheckTime
					dTime.CheckTime = dTime.CurrTime + 1000
					dTime.CurrFps = dTime.FPSCounter
					dTime.FPSCounter = 0
				Else
					dTime.FPSCounter:+1
				EndIf
			Next
			
		End Function
		
		Function FPS(dTime:DeltaTime)
			Return dTime.CurrFps
		End Function
		
		Function CurrDelta:Double(dTime:DeltaTime)
			Return dTime.ElapsedTime/dTime.dDelta
		End Function
		
End Type

</pre> <br><br></td></tr></table><br>
<a name="548431"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Any chance to show how it work ? <br><br></td></tr></table><br>
<a name="548437"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Who was John Galt?</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Tom - If I remember correctly, you are using 'ball physics' in one of your games - delta timing doesn't work well at all with these types of games - gives wildly different results on different machines and you will need a logic loop that executes at a fixed rate on all machines to make something reproducable. <br><br></td></tr></table><br>
<a name="548442"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Example:<br><br>Set Your Globals<br><pre class=code>
Global Delta1:DeltaTime = DeltaTime.Create(35.0) ' Normal 35 FPS
Global Delta:Double</pre><br><br>Now Main Game Loop<br><pre class=code>
While Not EXIT_FLAG

	DeltaTime.Update()
	Delta = DeltaTime.CurrDelta(Delta1)
	If Delta &gt; 1 Then Delta = 1 ' Delta should never be bigger than 1.
	
	' -------------------------------GFX FUNCTIONS ---------------------------
	ScreenUpdate = ScreenUpdate + Delta
	
	ScreenX.Update(Delta)
	
	If ScreenUpdate &gt; 0.30 Then
		RenderGFX()
		ScreenUpdate = 0
		Timer.UpdateAll()
	EndIf	
	' ------------------------------------------------------------------------
	
	UpdateGame(delta)
	ActiveChannel.UpdateAll()	
	FlushMem
	Delay(1)
	
Wend</pre><br><br>Multiply all movements and animations etc by Delta. <br><br></td></tr></table><br>
<a name="548446"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Many Thanks <br><br></td></tr></table><br>
<a name="549394"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi try to modify this code but it's strange the blob don't move ?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics 800,600,32

AutoMidHandle True

Type DeltaTime
	Global DeltaTList:TList

	Field OldTime:Double
	Field ElapsedTime:Double
	Field Delta:Double
	Field dFps:Double
	Field dDelta:Double
	Field CurrFps:Double
	Field FPSCounter:Double
	Field CurrTime:Double
	Field CheckTime:Double
	
		Method New ()
            	If DeltaTList = Null Then DeltaTList = New TList
            	DeltaTList.AddLast Self
    	End Method		
		
		Method Destroy ()
            	DeltaTList.Remove Self
        End Method

	
		Function Create:DeltaTime(Fps:Double)
			Local dTime:DeltaTime = New DeltaTime
			dTime.Delta = 1
			dTime.dFps = Fps
			dTime.dDelta = 1000/Fps
			Return dTime
		End Function
		
		Function Update()
			Local dTime:DeltaTime
			For dTime:DeltaTime = EachIn DeltaTList
				dTime.CurrTime = MilliSecs()
				dTime.ElapsedTime = dTime.CurrTime - dTime.OldTime
				dTime.OldTime = dTime.CurrTime
				
				If dTime.CurrTime &gt; dTime.CheckTime
					dTime.CheckTime = dTime.CurrTime + 1000
					dTime.CurrFps = dTime.FPSCounter
					dTime.FPSCounter = 0
				Else
					dTime.FPSCounter:+1
				EndIf
			Next
			
		End Function
		
		Function FPS(dTime:DeltaTime)
			Return dTime.CurrFps
		End Function
		
		Function CurrDelta:Double(dTime:DeltaTime)
			Return dTime.ElapsedTime/dTime.dDelta
		End Function
		
End Type


SetMaskColor(255,0,255)
Global blob:TImage = LoadImage("C:\blitzmax\samples\simonh\fireworks\spark.png")


Global Delta1:DeltaTime = DeltaTime.Create(35.0) ' Normal 35 FPS
Global Delta:Double

Global ScreenUpdate:Float
Global bx:Int=10

While Not KeyHit(KEY_ESCAPE)

	Cls

	
	
	DeltaTime.Update()
	
	Delta = DeltaTime.CurrDelta(Delta1)
	
	If Delta &gt; 1 Then Delta = 1 ' Delta should never be bigger than 1.
	
	' -------------------------------GFX FUNCTIONS ---------------------------
	ScreenUpdate = ScreenUpdate + Delta

	
	If ScreenUpdate &gt; 0.30 Then

	
		RenderGFX()
		ScreenUpdate = 0
	EndIf	
	
	' ------------------------------------------------------------------------
	
	FlushMem
	Flip
Wend


Function RenderGFX()
		DrawImage blob,bx,100

			bx=bx*Delta
	
		If bx&gt;799 Then bx = 0
End Function
</textarea> <br><br></td></tr></table><br>
<a name="549403"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah okay, sorry you need to multiply the amount you want the object to move by Delta. Also you need to make your bx variable a double.<br><br>See below <br><br></td></tr></table><br>
<a name="549407"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> In fact your code should really be this since you are not rendering the gfx every frame, only when screenupdate reaches a certain threshold. You need to pass screenupdate as the delta.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics 800,600,0

AutoMidHandle True

Type DeltaTime
	Global DeltaTList:TList

	Field OldTime:Double
	Field ElapsedTime:Double
	Field Delta:Double
	Field dFps:Double
	Field dDelta:Double
	Field CurrFps:Double
	Field FPSCounter:Double
	Field CurrTime:Double
	Field CheckTime:Double
	
		Method New ()
            	If DeltaTList = Null Then DeltaTList = New TList
            	DeltaTList.AddLast Self
    	End Method		
		
		Method Destroy ()
            	DeltaTList.Remove Self
        End Method

	
		Function Create:DeltaTime(Fps:Double)
			Local dTime:DeltaTime = New DeltaTime
			dTime.Delta = 1
			dTime.dFps = Fps
			dTime.dDelta = 1000/Fps
			Return dTime
		End Function
		
		Function Update()
			Local dTime:DeltaTime
			For dTime:DeltaTime = EachIn DeltaTList
				dTime.CurrTime = MilliSecs()
				dTime.ElapsedTime = dTime.CurrTime - dTime.OldTime
				dTime.OldTime = dTime.CurrTime
				
				If dTime.CurrTime &gt; dTime.CheckTime
					dTime.CheckTime = dTime.CurrTime + 1000
					dTime.CurrFps = dTime.FPSCounter
					dTime.FPSCounter = 0
				Else
					dTime.FPSCounter:+1
				EndIf
			Next
			
		End Function
		
		Function FPS(dTime:DeltaTime)
			Return dTime.CurrFps
		End Function
		
		Function CurrDelta:Double(dTime:DeltaTime)
			Return dTime.ElapsedTime/dTime.dDelta
		End Function
		
End Type


SetMaskColor(255,0,255)
'Global blob:TImage = LoadImage("C:\blitzmax\samples\simonh\fireworks\spark.png")


Global Delta1:DeltaTime = DeltaTime.Create(35.0) ' Normal 35 FPS
Local  Delta:Double

Global ScreenUpdate:Double
Global bx:Double=10

While Not KeyHit(KEY_ESCAPE)

	

	
	
	DeltaTime.Update()
	
	Delta = DeltaTime.CurrDelta(Delta1)
	
	If Delta &gt; 1 Then Delta = 1 ' Delta should never be bigger than 1.
	
	' -------------------------------GFX FUNCTIONS ---------------------------
	ScreenUpdate = ScreenUpdate + Delta

	
	If ScreenUpdate &gt; 0.30 Then
		Cls
	
		RenderGFX(ScreenUpdate)
		ScreenUpdate = 0
		Flip
	EndIf	
	
	' ------------------------------------------------------------------------
	
	FlushMem
	
Wend


Function RenderGFX(Delta:Double=1)
		DrawOval bx,100,10,10

			bx=bx+(Delta * 2)
	
		If bx&gt;799 Then bx = 0
End Function</textarea> <br><br></td></tr></table><br>
<a name="549437"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok !! thanks :) but i have a little question ? this piece of code<br>can be compared with tweening method ?<br><br>Because if i change :<br>Global Delta1:DeltaTime = DeltaTime.Create(35.0) ' Normal 35 FPS<br><br>With <br>Global Delta1:DeltaTime = DeltaTime.Create(10.0) ' Normal 10 FPS<br><br>The circle move more slowly ? it is normal ? If 'im remember<br>the blitz3d tweening method the object move in the same distance/time with more or less FPS ? <br><br></td></tr></table><br>
<a name="549455"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> That command sets the Frame Rate, all delta time will be based off of that rate. Once you have set the rate all object movement will be exactly the same regardless of the speed or the machine it is running on.<br><br>To demonstrate this put a random delay in your loop and see what happens.<br><br><pre class=code>
Delay Rand(1,20)</pre> <br><br></td></tr></table><br>
<a name="549678"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tom</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice code.<br><br>I've since come to realise that at certain FPS rates (when the drawing rate drops low) then things are gonna get juddery and there's nothin you can do about it really.<br><br>i.e, if the monitor refreshrate is 85 and the draw rate drops to 60 fps, then 25 of those 60 frames will be displayed twice during the monitors 85 refreshes. That's the problem I was having but not understanding til now.<br><br>Of course it's gonna be rare that drawing drops that low so it's not too big a deal after all.<br><br>Tom <br><br></td></tr></table><br>
<a name="549768"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Change this following line of code if you think the FPS is going to get lower than you set it. Example : If you set the FPS at 35 then change this to "2" then the FPS can go as low as 17.5 and things will run okay.<br><pre class=code>If Delta &gt; 1 Then Delta = 1 ' Delta should never be bigger than 1.</pre> <br><br></td></tr></table><br>
<a name="577219"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Videogame Takeaway<br><br>I have two questions :<br><br>- I'm obliged to use alway double value ? like this :<br>MySpritePX:Double=10 <br><br>....<br>MySpritePX=MySpritePX+1*Delta<br>....<br><br>It is correct ?<br><br>- I don't understand very well the line :<br>If ScreenUpdate &gt; 0.30 Then<br><br>Can you clarify ?<br><br>I notify a strange stuff : <br>when i want to know the FPS with : DrawTex DeltaTime.FPS(Delta1),10,10<br><br>This return some strange value between 60=&gt;145698=&gt;65=&gt;300 etc ? when i open a screen with the NOSYNC flag,  any idea ?<br>or i must use a constant refresh rate, like 60 ?<br><br>Cheers <br><br></td></tr></table><br>
<a name="577232"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Filax,<br><br>Use floats or doubles, your choice but never Ints.<br><br><div class="quote"> - I don't understand very well the line :<br>If ScreenUpdate &gt; 0.30 Then <br></div><br>Based on the Frame Rate I set earlier in the code this line forces a screen update about every 60th of a second and prevents a screen update occuring every computational frame.<br><br>The FPS reported is actually the speed of each computational frame, remember graphics are only rendered when Screenupdate &gt; 0.30 <br><br>Does this help? probably not, I'm tired and full of beer. Ask me again if you need me to explain.<br><br>PS. Call me Tim. <br><br></td></tr></table><br>
<a name="577235"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Steve Elliott</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>PS. Call me Tim (not Videogame Takeaway).<br> <br></div><br><br>Bring back Indiepath I say - know who you are then.  ;-) <br><br></td></tr></table><br>
<a name="577236"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> Na, that would be boring. <br><br></td></tr></table><br>
<a name="577268"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> lol Tim :)))))))))))<br><br>Drink another beer for me :) i understand now :) thank  !!! <br><br></td></tr></table><br>
<a name="577283"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> Tim <br><br>The last question, it's a fault if use use a NOSYNC graphics screen ? <br><br></td></tr></table><br>
<a name="577407"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> Please use NOSYNC, it allows the logic to update without waiting for the vSync Signal. <br><br>This is particularly good for physics applications where you need a fast logic update but only want to actually draw to screen when needed. <br><br></td></tr></table><br>
<a name="577431"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#23">[#23]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oh, thank for reply :) but when i'm making a screen with <br>NOSYNC parameter the function Function FPS(dTime:DeltaTime)<br>return a strange number between 60=&gt;1235718 ??<br><br>Try this :<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics 800,600,32,NOSYNC

AutoMidHandle True

Type DeltaTime
	Global DeltaTList:TList

	Field OldTime:Double
	Field ElapsedTime:Double
	Field Delta:Double
	Field dFps:Double
	Field dDelta:Double
	Field CurrFps:Double
	Field FPSCounter:Double
	Field CurrTime:Double
	Field CheckTime:Double
	
		Method New ()
            	If DeltaTList = Null Then DeltaTList = New TList
            	DeltaTList.AddLast Self
    	End Method		
		
		Method Destroy ()
            	DeltaTList.Remove Self
        End Method

	
		Function Create:DeltaTime(Fps:Double)
			Local dTime:DeltaTime = New DeltaTime
			dTime.Delta = 1
			dTime.dFps = Fps
			dTime.dDelta = 1000/Fps
			Return dTime
		End Function
		
		Function Update()
			Local dTime:DeltaTime
			For dTime:DeltaTime = EachIn DeltaTList
				dTime.CurrTime = MilliSecs()
				dTime.ElapsedTime = dTime.CurrTime - dTime.OldTime
				dTime.OldTime = dTime.CurrTime
				
				If dTime.CurrTime &gt; dTime.CheckTime
					dTime.CheckTime = dTime.CurrTime + 1000
					dTime.CurrFps = dTime.FPSCounter
					dTime.FPSCounter = 0
				Else
					dTime.FPSCounter:+1
				EndIf
			Next
			
		End Function
		
		Function FPS(dTime:DeltaTime)
			Return dTime.CurrFps
		End Function
		
		Function CurrDelta:Double(dTime:DeltaTime)
			Return dTime.ElapsedTime/dTime.dDelta
		End Function
		
End Type


SetMaskColor(255,0,255)
'Global blob:TImage = LoadImage("C:\blitzmax\samples\simonh\fireworks\spark.png")


Global Delta1:DeltaTime = DeltaTime.Create(35.0) ' Normal 35 FPS
Local  Delta:Double

Global ScreenUpdate:Double
Global bx:Double=10

While Not KeyHit(KEY_ESCAPE)

	

	
	
	DeltaTime.Update()
	
	Delta = DeltaTime.CurrDelta(Delta1)
	
	If Delta &gt; 1 Then Delta = 1 ' Delta should never be bigger than 1.
	
	' -------------------------------GFX FUNCTIONS ---------------------------
	ScreenUpdate = ScreenUpdate + Delta

	
	If ScreenUpdate &gt; 0.30 Then
		Cls
	
		RenderGFX(ScreenUpdate)
		ScreenUpdate = 0
		Flip
	EndIf	
	
	' ------------------------------------------------------------------------
	
	FlushMem
	
Wend


Function RenderGFX(Delta:Double=1)
		DrawOval bx,100,10,10

			bx=bx+(Delta * 2)
	
	DrawText DeltaTime.FPS(Delta1)+" FPS",10,10

		If bx&gt;799 Then bx = 0
End Function
</textarea> <br><br></td></tr></table><br>
<a name="577436"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SillyPutty</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#24">[#24]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is excellant code !! :) awome stuff :)<br><br>can I ask why you use a list for delta time type ?<br><br>Why would one need to add more delta time types into a list ? or is that only on "new"<br><br>Sorry for the noob questions. <br><br></td></tr></table><br>
<a name="577437"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#25">[#25]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes, I get that too. It's reporting the actual speed of the game loop and not the Amount of Renders Per Second. The reason being that we've told the application to only render to screen  when the ScreenUpdate variable reaches a certain threshold, thus we already know what the maximum amount of rendered FPS will be. If the Rendered FPS dips below the ScreenUpdate threshold then it will be reported as Rendered FPS and not number of application loops per second.<br><br>If you take out the ScreenUpdate checks then you will get the REAL ACTUAL Rendered FPS.<br><br>Oh and I recently updated the Delta code, it's a lot more accurate now and faster :D<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics 800,600,32,NOSYNC

AutoMidHandle True

Type DeltaTime

	Global DeltaTList	:TList
	
	Field OldTime		:Int
	Field ElapsedTime	:Int
	Field dFps			:Float
	Field dDelta		:Float
	Field CurrFps		:Int
	Field FPSCounter	:Int
	Field CurrTime		:Int
	Field CheckTime		:Int
	
		Method New ()
            	If DeltaTList = Null Then DeltaTList = New TList
            	DeltaTList.AddLast Self
    		End Method		
		
		Method Destroy ()
            	DeltaTList.Remove Self
       	End Method

		Function Create:DeltaTime(Fps:Float)
			Local dTime:DeltaTime = New DeltaTime
			dTime.dFps = Fps
			dTime.dDelta = 1000/Fps
			Return dTime
		End Function
		
		Method Update()
			Self.CurrTime = MilliSecs()
			Self.ElapsedTime = Self.CurrTime - Self.OldTime
			Self.OldTime = Self.CurrTime
			If Self.CurrTime &gt; Self.CheckTime
				Self.CheckTime = Self.CurrTime + 1000
				Self.CurrFps = Self.FPSCounter
				Self.FPSCounter = 0
			Else
				Self.FPSCounter:+1
			EndIf
		End Method
		
		Method FPS()
			Return Self.CurrFps
		End Method
		
		Method CurrDelta:Float()
			Return Double(Self.ElapsedTime)/Self.dDelta
		End Method
		
		Method SetFPS(NewFPS:Float)
			Self.dFPS = NewFPS
			Self.dDelta = 1000/Self.dFPS
		End Method
		
End Type



SetMaskColor(255,0,255)
'Global blob:TImage = LoadImage("C:\blitzmax\samples\simonh\fireworks\spark.png")


Global Delta1:DeltaTime = DeltaTime.Create(35.0) ' Normal 35 FPS
Local  Delta:Double

Global ScreenUpdate:Double
Global bx:Double=10

While Not KeyHit(KEY_ESCAPE)

	

	
	
	Delta1.Update()
	
	Delta = Delta1.CurrDelta()
	
	If Delta &gt; 1 Then Delta = 1 ' Delta should never be bigger than 1.
	
	' -------------------------------GFX FUNCTIONS ---------------------------
	ScreenUpdate = ScreenUpdate + Delta

	
	If ScreenUpdate &gt; 0.30 Then
		Cls
	
		RenderGFX(ScreenUpdate)
		ScreenUpdate :- 0.3
		Flip
	EndIf	
	
	' ------------------------------------------------------------------------
	
	FlushMem
	
Wend


Function RenderGFX(Delta:Double=1)
		DrawOval bx,100,10,10

			bx=bx+(Delta * 2)
	
	DrawText Delta1.FPS()+" FPS",10,10

		If bx&gt;799 Then bx = 0
End Function</textarea><br><br>@Deux, I use this method as it's good OOP practice, it keeps everything nice and neat. I may also need multiple Delta Timers controlling different things - this code gives me the flexibility. <br><br></td></tr></table><br>
<a name="577443"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#26">[#26]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Tim :)<br><br>Nice piece of code :) but i have another question (no ! don't <br>kill me !! PAN !)<br><br>What this method ?<br><br>	Method SetFPS(NewFPS:Float)<br>		Self.dFPS = NewFPS<br>		Self.dDelta = 1000/Self.dFPS<br>	End Method <br><br></td></tr></table><br>
<a name="577450"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#27">[#27]</a></td></tr></table></td></tr><tr ><td class="posttext"> Arrrgghhhhhh... LOL<br><br>This allows you to dynamically adjust the game timing.<br><br>For example, you initialise the timing with:-<br><pre class=code>Global Delta1:DeltaTime = DeltaTime.Create(35.0)</pre><br>You code your game objects and animations based on that setting (35.0).<br><br>Now lets say you want to dynamically speed up or slow down the whole game. You can affect the whole game speed by setting the new FPS (maybe wrong name for function but who cares). I do this to great effect in GEOM, when you get a SLOWMO powerup the game speed is 50% of normal ie. SetFPS(NormalSpeed/2).<br><br>Cool eh? <br><br></td></tr></table><br>
<a name="577452"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#28">[#28]</a></td></tr></table></td></tr><tr ><td class="posttext"> One more thing....<br><br>You may like to know that I put the IGlass update command in the ScreenUpdate portion of code in my new application and it still works fine. The benefit being that IGLass updates are only done at each screen render and do not affect the game logic speeds at all.<br><br><pre class=code>
	' -------------------------------GFX FUNCTIONS ---------------------------
	ScreenUpdate :+ Delta
	
	If ScreenUpdate &gt; 0.5 Then
		ScreenUpdate :-0.5
		Parse_Render_States()
		IGL_RefreshGui()
	EndIf	
	
	' -------------------------------------------------------------------------
</pre> <br><br></td></tr></table><br>
<a name="577453"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#29">[#29]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok ok !! very interesting !!! Many thanks to reply :) <br><br></td></tr></table><br>
<a name="577460"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#30">[#30]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm trying to make a little template for using simply your <br>delta code : <br><br>The template :<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict 

Include "Inc_Delta.Bmx"
Graphics 800,600,32,NOSYNC

AutoMidHandle True

' ---------------------------
' Init the delta time library
' ---------------------------
DLT_InitDelta(35.0,0.35)


Global bx:Float=10

' ---------
' Main loop
' ---------
While Not KeyHit(KEY_ESCAPE)
	' ------------------------------
	' Refresh the delta time library
	' ------------------------------
	DLT_RefreshDLT()

	' ---------------------------
	' Test for change the app FPS
	' ---------------------------
	If MouseDown(1) Then
		DLT_MyAppDelta.SetNewFPS(65.0)
	Else
		DLT_MyAppDelta.SetNewFPS(35.0)	
	EndIf
		
	' -------------------------------GFX FUNCTIONS ---------------------------
	If DLT_MyScreenUpdate &gt; DLT_MyAppRefresh Then
		Cls
	
		DLT_UpdateScreen(DLT_MyScreenUpdate)
		DLT_MyScreenUpdate=DLT_MyScreenUpdate - DLT_MyAppRefresh
		
		Flip
	EndIf	
	' ------------------------------------------------------------------------
	
	FlushMem
Wend

' -------------------
' Clear the DeltaTime
' -------------------
DLT_ClearDLT()

' -------------------------------------
' Use this function to refresh your app
' -------------------------------------
Function DLT_UpdateScreen(DeltaTime:Float=1)
	' ----------------
	' A fantastic demo 
	' ----------------
	DrawOval bx,100,10,10

	bx=bx+1*DeltaTime
	If bx&gt;799 Then bx = 0
	
	' ----------------------
	' Retrieve the FPS value
	' ----------------------
	Local DLT_GetDeltaFPS=DLT_MyAppDelta.GetDeltaFPS()
	Local DLT_GetRealFPS=DLT_MyAppDelta.GetRealFPS()
	
	DrawText "DELTA FPS : "+DLT_GetDeltaFPS+" / REAL FPS : "+DLT_GetRealFPS,10,10
End Function
</textarea><br><br>Here is the include : Inc_Delta.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Global DLT_DeltaTimeList:TList=CreateList()

Global DLT_MyAppDelta:DLT_DeltaTime
Global DLT_MyAppRefresh:Float
Global DLT_MyDeltaTime:Float
Global DLT_MyScreenUpdate:Float

' ----------------
' DeltaTime Object
' ----------------
Type DLT_DeltaTime
	Field OldTime:Int
	Field ElapsedTime:Int
	Field DeltaFps:Float
	Field DeltaPeriod:Float
	Field CurrentFps:Int
	Field CurrentTiming:Int
	Field FPSCounter:Int
	Field CheckTime:Int

	Field CurrentFrame:Int
	Field RenderTime:Int	
	Field RealFps:Int
	
	' -----------------------------
	' Create the delta time counter
	' -----------------------------
	Function Create:DLT_DeltaTime(Fps:Float)
		Local NewDelta:DLT_DeltaTime = New DLT_DeltaTime
		NewDelta.DeltaFps = Fps
		NewDelta.DeltaPeriod = 1000/Fps
		
		ListAddLast DLT_DeltaTimeList,NewDelta
		
		Return NewDelta
	End Function
	
	' -----------------------------
	' Update the delta time counter
	' -----------------------------
	Method Update()
		Self.CurrentTiming = MilliSecs()
		Self.ElapsedTime = Self.CurrentTiming - Self.OldTime
		Self.OldTime = Self.CurrentTiming
			
		If Self.CurrentTiming &gt; Self.CheckTime
			Self.CheckTime = Self.CurrentTiming + 1000
			Self.CurrentFps = Self.FPSCounter
			Self.FPSCounter = 0
		Else
			Self.FPSCounter=Self.FPSCounter+1
		EndIf
	End Method
	
	' ---------------------------
	' Get the current delta value
	' ---------------------------
	Method GetCurrentDelta:Float()
		Return Float(Self.ElapsedTime)/Self.DeltaPeriod
	End Method

	' -------------------------
	' Get the current delta FPS
	' -------------------------		
	Method GetDeltaFPS()
		Return Self.CurrentFps
	End Method

	' ------------------------
	' Get the current real FPS
	' ------------------------	
	Method GetRealFPS()
		Self.CurrentFrame=Self.CurrentFrame+1
	
		If MilliSecs()-Self.RenderTime=&gt;1000 Then
			Self.RealFps=CurrentFrame
			Self.CurrentFrame=0
	 		Self.RenderTime=MilliSecs()
		EndIf
	
		Return Self.RealFps
	End Method

	' ---------------------------------------------------------------------
	' This method allow the possibility to change dynamicly the current FPS 
	' ---------------------------------------------------------------------	
	Method SetNewFPS(NewFPS:Float)
		Self.DeltaFps = NewFPS
		Self.DeltaPeriod = 1000/Self.DeltaFps
	End Method	
	
	' ---------------------------------
	' This method destroy a delta timer
	' ---------------------------------
	Method Destroy()
		DLT_DeltaTimeList.Remove(Self)
	End Method
End Type

' ------------------
' Init the DeltaTime
' ------------------
Function DLT_InitDelta(DeltaFrameRate:Float=35.0,DeltaRefresh:Float=0.35)
	DLT_MyAppDelta=DLT_DeltaTime.Create(DeltaFramerate) ' Normal 35 FPS
	DLT_MyAppRefresh=DeltaRefresh
End Function

' ---------------------
' Refresh the DeltaTime
' ---------------------
Function DLT_RefreshDLT()
	' -----------------------------
	' Refresh the delta app counter
	' -----------------------------
	DLT_MyAppDelta.Update()
		
	' -----------------------------------
	' Delta should never be bigger than 1
	' -----------------------------------
	DLT_MyDeltaTime= DLT_MyAppDelta.GetCurrentDelta()
	If DLT_MyDeltaTime &gt; 1 Then DLT_MyDeltaTime = 1 
	
	' ----------------------------
	' Update the screen update VAR
	' ----------------------------
	DLT_MyScreenUpdate = DLT_MyScreenUpdate + DLT_MyDeltaTime
End Function

' -------------------
' Clear the DeltaTime
' -------------------
Function DLT_ClearDLT()
	DLT_MyAppDelta.Destroy()
End Function
</textarea><br><br>If you find some error tell me !<br><br>Cheers <br><br></td></tr></table><br>
<a name="577468"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#31">[#31]</a></td></tr></table></td></tr><tr ><td class="posttext"> Looks okay to me BUT I have not tested it. <br><br></td></tr></table><br>
<a name="577469"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#32">[#32]</a></td></tr></table></td></tr><tr ><td class="posttext"> 'Identifier destroy not found'<br>Should it be my_appdelta.destroy()<br>? <br><br></td></tr></table><br>
<a name="577474"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#33">[#33]</a></td></tr></table></td></tr><tr ><td class="posttext"> Tonyg it's ok now :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
