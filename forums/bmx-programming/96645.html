<!DOCTYPE html><html lang="en" ><head ><title >Keyboard Hooks?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Keyboard Hooks?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Keyboard Hooks?</a><br><br>
<a name="1119047"></a>

<a name="1119048"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi guys!<br><br>I need to add a global hotkey to a maxgui app so it can be called and execute a function while I'm in another app.<br><br>Found this code inside the archives but I can't get it to work (BMX 1.45): <a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=2248" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=2248</a><br><br>Example:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Framework brl.eventqueue
Import maxgui.Win32MaxGuiEX 
Import brl.timer
Import BRL.StandardIO
Import "VInput.bmx"
Import "GHotkey.bmx"


Global testhotkey:TGHotkey = CreateGlobalHotkey(KEY_P, False, False, True, OnTestHotkeyPressed)
	CreateGlobalHotkey(KEY_B, True, False, True, HKHandler)
	CreateGlobalHotkey(KEY_Z, True, True, True, HKHandler)
	
Local Tmr_CheckKeys:TTimer = CreateTimer(60)
Local window:TGadget = CreateWindow("TEST", 100, 100, 320, 240, Null)

Repeat
	Select WaitEvent()
		Case EVENT_TIMERTICK
			Select EventSource()
				Case Tmr_CheckKeys
					TGHotkey.CheckHotkeys()	
							
			End Select
			
	End Select

Forever

Function HKHandler(th:TGHotkey)
  Local report:String = Chr(th.key)
	
	If th.m_shift Then report:+ "+Shift"
	If th.m_ctrl Then report:+ "+Ctrl"
	If th.m_alt Then report:+ "+Alt"
	
   Print report + " Global Hotkey Pressed"
	
End Function


Function OnTestHotkeyPressed(th:TGHotkey)
	Print "Test Hotkey Pressed!"
	Print "HELLO KEYBOARD RESPONDING????"
End Function
</textarea><br><br>GHotkey.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import BRL.linkedlist

Extern "Win32"
	Function GetAsyncKeyState:Int(vKey:Int)	
EndExtern

Rem
bbdoc: TGHotkey Type
End Rem
Type TGHotkey
  Global _list:TList = New TList
  
	Rem
	bbdoc: Shift modifier
	End Rem
	Field m_shift:Int
	
	Rem
	bbdoc: Ctrl modifier
	End Rem
	Field m_ctrl:Int
	
	Rem
	bbdoc: Alt modifier
	End Rem
	Field m_alt:Int
	
	Rem
	bbdoc: Key to be checked
	End Rem
	Field key:Int
	
	Rem
	bbdoc: Internal
	End Rem
	Field khit:Int
	
	Rem
	bbdoc: Function to be called when hotkey (&amp; modifiers) pressed
	End Rem
	Field _callback(Obj:TGHotkey) 
	
		Method New() 
		   _list.AddLast(Self) 
			
		End Method
		
		Method remove() 
		   _list.remove(Self) 
			
		End Method
		
		Rem
		bbdoc: CheckHotkeys Function
		about: Checks all global hotkeys, if key pressed executes callback function
		End Rem
		Function CheckHotkeys() 
		  Local th:TGHotkey
		  
			For th = EachIn TGHotkey._list
				
			  Local GKD_CTRL:Int = GKeyDown(VK_CONTROL) 
			  Local GKD_MENU:Int = GKeyDown(VK_MENU) 
			  Local GKD_SHIFT:Int = GKeyDown(VK_SHIFT) 
			
			  Local totkeys:Int = 1
				
				totkeys:+th.m_shift + th.m_ctrl + th.m_alt
				
			  Local Keys:Int = GKeyDown(th.key) 
				If th.m_shift = 1 Then Keys:+GKD_SHIFT
				If th.m_ctrl = 1 Then Keys:+GKD_CTRL
				If th.m_alt = 1 Then Keys:+GKD_MENU
				
				'DebugLog keys
				
				If keys = totkeys
					'DebugLog keys + ", " + th.khit
					If th.khit = 0
					  th.khit = 1
						th._callback(th) 
						
					EndIf
					
				Else
				  th.khit = 0
					
				EndIf
			Next
			
		End Function
		
		Rem
		bbdoc: CreateHotkey Function- Internal, see #CreateGlobalHotkey
		about: @m_shift @m_ctrl and @m_alt are boolean variables to specify the modifiers for the key, @key is any KEY_ variable.
		returns: The new hotkey
		End Rem
		Function Create:TGHotkey(key:Int, m_shift:Int, m_ctrl:Int, m_alt:Int, callback(Obj:TGHotkey)) 
		  Local th:TGHotkey = New TGHotkey
			
			th.key = key
			th.m_shift = m_shift
			th.m_ctrl = m_ctrl
			th.m_alt = m_alt
			th._callback = callback
			
		   Return th
		   
		End Function
		
End Type

Rem
bbdoc: CreateGlobalHotkey Function
about: @m_shift @m_ctrl and @m_alt are boolean variables to specify the modifiers for the key
returns: The new hotkey
End Rem
Function CreateGlobalHotkey:TGHotkey(key:Int, m_shift:Int, m_ctrl:Int, m_alt:Int, callback(Obj:TGHotkey)) 
   Return TGHotkey.Create(key, m_shift, m_ctrl, m_alt, callback) 
	 
End Function

Rem
bbdoc: GKeyDown Function
returns: The state of the virtual key sent to the function.
End Rem
Function GKeyDown:Int(VirtualKey:Int)
 'If (IgnoreFocus = True) And (GetActiveWindow() = 0) Then Return
 
   Return (GetAsyncKeyState:Int(VirtualKey) And %1000000000000000) &gt; 0
	
End Function
</textarea><br><br>VInput.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

'#Region Mouse Variables
Const MOUSEEVENTF_ABSOLUTE:Int   = -32768 ' // Use absolute coords
Const MOUSEEVENTF_MOVE:Int       = 1      ' // Trigger move event

Const MOUSEEVENTF_LEFTDOWN:Int   = 2      ' // LMB Down
Const MOUSEEVENTF_LEFTUP:Int     = 4      ' // LMB Up
Const MOUSEEVENTF_RIGHTDOWN:Int  = 8      ' // RMB Down
Const MOUSEEVENTF_RIGHTUP:Int    = 16     ' // RMB Up
Const MOUSEEVENTF_MIDDLEDOWN:Int = 32     ' // MMB Down
Const MOUSEEVENTF_MIDDLEUP:Int   = 64     ' // MMB Up
Const MOUSEEVENTF_WHEEL:Int      = 128    ' // NT Only: Mouse wheel moved
'#End Region


'#Region Virtual Keys

Const VK_ADD:Byte = $6B
Const VK_ATTN:Byte = $F6
Const VK_BACK:Byte = $8
Const VK_CANCEL:Byte = $3
Const VK_CAPITAL:Byte = $14
Const VK_CLEAR:Byte = $C
Const VK_CONTROL_:Byte = $11
Const VK_CRSEL:Byte = $F7
Const VK_DECIMAL:Byte = $6E
Const VK_DELETE:Byte = $2E
Const VK_DIVIDE:Byte = $6F
Const VK_DOWN:Byte = $28
Const VK_END:Byte = $23
Const VK_EREOF:Byte = $F9
Const VK_ESCAPE:Byte = $1B
Const VK_EXECUTE:Byte = $2B
Const VK_EXSEL:Byte = $F8

Const VK_HELP:Byte = $2F
Const VK_HOME:Byte = $24
Const VK_INSERT:Byte = $2D
Const VK_LBUTTON:Byte = $1
Const VK_LCONTROL:Byte = $A2
Const VK_LEFT:Byte = $25
Const VK_LMENU:Byte = $A4
Const VK_LSHIFT:Byte = $A0
Const VK_MBUTTON:Byte = $4
Const VK_MENU:Byte = $12
Const VK_MULTIPLY:Byte = $6A
Const VK_NEXT:Byte = $22
Const VK_NONAME:Byte = $FC

Const VK_NUMLOCK:Byte = $90
Const VK_NUMPAD0:Byte = $60
Const VK_NUMPAD1:Byte = $61
Const VK_NUMPAD2:Byte = $62
Const VK_NUMPAD3:Byte = $63
Const VK_NUMPAD4:Byte = $64
Const VK_NUMPAD5:Byte = $65
Const VK_NUMPAD6:Byte = $66
Const VK_NUMPAD7:Byte = $67
Const VK_NUMPAD8:Byte = $68
Const VK_NUMPAD9:Byte = $69

Const VK_OEM_CLEAR:Byte = $FE
Const VK_PA1:Byte = $FD
Const VK_PAUSE:Byte = $13
Const VK_PLAY:Byte = $FA
Const VK_PRINT:Byte = $2A
Const VK_PRIOR:Byte = $21
Const VK_PROCESSKEY:Byte = $E5
Const VK_RBUTTON:Byte = $2
Const VK_RCONTROL:Byte = $A3
Const VK_RETURN:Byte = $D
Const VK_RIGHT:Byte = $27
Const VK_RMENU:Byte = $A5
Const VK_RSHIFT:Byte = $A1
Const VK_SCROLL:Byte = $91
Const VK_SELECT:Byte = $29
Const VK_SEPARATOR:Byte = $6C
Const VK_SHIFT:Byte = $10
Const VK_SNAPSHOT:Byte = $2C
Const VK_SPACE:Byte = $20
Const VK_SUBTRACT:Byte = $6D
Const VK_TAB:Byte = $9
Const VK_UP:Byte = $26
Const VK_ZOOM:Byte = $FB

'Const KEYEVENTF_KEYUP:Byte = $00000002
'#End Region

Extern "win32"
	Function GetCursorPos(Point:Byte Ptr)
	Function SetCursorPos(x:Int, y:Int)
	
	Function keybd_event(bVk:Byte, bScan:Byte, dwFlags:Byte, dwExtraInfo:Int)
	Function mouse_event(dwFlags:Int, dx:Int, dy:Int, cButtons:Int, dwExtraInfo:Int) 
	
End Extern

Rem
MouseAction MOUSEEVENTF_LEFTDOWN + MOUSEEVENTF_LEFTUP, 0, 0, 0
MouseAction MOUSEEVENTF_LEFTDOWN + MOUSEEVENTF_LEFTUP, 17, 702, 1
Delay 5000
MouseAction MOUSEEVENTF_LEFTDOWN + MOUSEEVENTF_LEFTUP, 17, 702, 2
EndRem


Rem
'system_ "notepad.exe"
Delay 2000

FKeyStroke Key_A
FKeyStroke Key_B', 2000

SKeyStroke Key_C, 1
Delay 5000
SKeyStroke Key_C, 2
EndRem

'Example: MouseAction MOUSEEVENTF_LEFTDOWN + MOUSEEVENTF_LEFTUP, 0, 0
Rem
bbdoc: Sends a mouse click to the computer.
about: @Flags can be any MOUSEEVENTF_ values or a combination (ie, MOUSEEVENTF_LEFTDOWN + MOUSEEVENTF_LEFTUP)
@X position to send mouse to
@Y position to send mouse to
@mm is a choice variable, 0=do not move mouse, 1=move mouse 2=move mouse, click, then return to previouse mouse position
returns: nothing
EndRem
Function MouseAction(Flags:Int, X:Int, Y:Int, mm:Int = 1)
	
	Select mm
		Case 0
			mouse_event Flags:Int, 0, 0, 0, 0
			
		Case 1 
			SetCursorPos X, Y
			mouse_event Flags:Int, 0, 0, 0, 0
			
		Case 2
		  Local Point:Int[2] 
			
			GetCursorPos Point ; SetCursorPos X, Y
			mouse_event Flags:Int, 0, 0, 0, 0
			SetCursorPos Point[0], Point[1]
			
	End Select
	
End Function


'Example: KeyStroke KEY_A
Rem
bbdoc: Sends a full keystroke to the computer.
about: @VK_KEYV key to be pressed, can be any VK_ variable
returns: nothing
EndRem
Function FKeyStroke(VK_KEYV:Byte) 
	keybd_event(VK_KEYV, 0, 0, 0)
	
	 'Delay dly
	
	keybd_event(VK_KEYV, 0, 2, 0)
	
End Function

'Example: KeyStroke KEY_A, 0 ; KeyStroke KEY_A, 2
Rem
bbdoc: Sends a single keystroke to the computer.
about: @VK_KEYV: key to be pressed, can be any VK_ variable
@updn: 1=down 2=up
returns: nothing
EndRem
Function SKeyStroke(VK_KEYV:Byte, updn:Byte = 0)
	keybd_event(VK_KEYV, 0, updn, 0)
End Function
</textarea><br><br>Is there another framework I could use or am I missing something?<br><br>Thanks<br>Grisu<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1119096"></a>

<a name="1119097"></a>

<a name="1119196"></a>

<a name="1119197"></a>

<a name="1119204"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> This alternative method seems to work -- thanks to grable for the extra window event access code! Code's a bit messy; just got it working and posted...<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Import maxgui.drivers


' EXAMPLE: Win + Alt + P (from anywhere)



' See Case WM_HOTKEY in WindowFunc () for action!


Const MOD_ALT:Int = $0001
Const MOD_CONTROL:Int = $0002
Const MOD_NOREPEAT:Int = $4000
Const MOD_SHIFT:Int = $0004
Const MOD_WIN:Int = $0008

Local RegisterHotkey:Int (hWnd:Int, id:Int, fsModifiers:Int, vk:Int) "win32"

Local user32 = LoadLibraryA ("user32.dll")

If user32
	RegisterHotkey = GetProcAddress (user32, "RegisterHotKey")
EndIf



Local window:TGadget = CreateWindow ("Hotkeys", 320, 200, 320, 200)

Global OldWindowFunc:Int = SetWindowFunc( window, WindowFunc)

' VKEY CODES:
' <a href="http://msdn.microsoft.com/en-us/library/windows/desktop/dd375731(v=VS.85).aspx" target="_blank">http://msdn.microsoft.com/en-us/library/windows/desktop/dd375731(v=VS.85).aspx</a>

' MODIFIER CONSTANTS HERE:
' <a href="http://msdn.microsoft.com/en-us/library/windows/Desktop/ms646309(v=VS.85).aspx" target="_blank">http://msdn.microsoft.com/en-us/library/windows/Desktop/ms646309(v=VS.85).aspx</a>

Print RegisterHotkey (QueryGadget (window, QUERY_HWND), 1, MOD_ALT|MOD_WIN, $50) ' $50 = "P"

Repeat

	Select WaitEvent ()
		Case EVENT_WINDOWCLOSE
			Exit
	End Select
	
Forever

End



' Modified from grable's code here:

' <a href="http://www.blitzbasic.com/Community/posts.php?topic=67031" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=67031</a>

' Copy and paste all code below and modify WM_HOTKEY call to suit...

Function WindowFunc:Int( hwnd:Int, msg:Int, wparam:Int, lparam:Int) "Win32"

	Local hdr:Int Ptr = Int Ptr(lparam)

	Select msg
		Case WM_HOTKEY
			Notify "OK" ' HANDLE HOTKEY HERE!
		Case WM_DESTROY
			SetWindowLong( hwnd, GWL_WNDPROC, OldWindowFunc)
	EndSelect

	Return CallWindowProc( OldWindowFunc, hwnd, msg, wparam, Int(lparam))

EndFunction

'
' setting window function
'
Const GWL_WNDPROC:Int = -4

Extern "Win32"	
	Function SetWindowLong:Int(HWND:Int,nIndex:Int,dwNewLong:Int) = "SetWindowLongA@12"
	Function GetWindowLong:Int(HWND:Int,nIndex:Int) = "GetWindowLongA@8"	
	Function CallWindowProc:Int( prevwndproc:Int, hwnd:Int, msg:Int, wparam:Int, lparam:Int) = "CallWindowProcA@20"
EndExtern

Function SetWindowFunc:Int( window:TGadget, newfunc:Int( hwnd:Int, msg:Int, wparam:Int, lparam:Int))
	Local hwnd:Int = QueryGadget( window, QUERY_HWND)
	Local oldfunc:Int = GetWindowLong( hwnd, GWL_WNDPROC)
	SetWindowLong( hwnd, GWL_WNDPROC, Int(Byte Ptr newfunc))
	Return oldfunc
EndFunction
</textarea><br><br>EDIT: You'll have to refer to the URL given for the key codes you need to use.<br><br>EDIT 2: Took out RemoveHotkey, left over from testing the MaxGUI hotkey commands (which only work within the app's own window).<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1119151"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks a lot for this! Works fine.<br><br>Is it possible to use a hotkey even from within another fullscreen app?<br>I'd like to play Skyrim e.g. and control my radio app while doing so via hotkeys.<br><br>Example app: <a href="http://188.165.211.46/~knot/prp/hot.zip" target="_blank">http://188.165.211.46/~knot/prp/hot.zip</a> <br><br>Hotkeys: Alt &amp; Num+ or Num- to change the volume. <br><br></td></tr></table><br>
<a name="1119198"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I assume so, but I imagine it depends on whether or not the app/game disables keys such as Windows, Alt, etc, which many games do... <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
