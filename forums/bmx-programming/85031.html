<!DOCTYPE html><html lang="en" ><head ><title >Splines or curves</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Splines or curves</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Splines or curves</a><br><br>
<a name="961098"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> You have two points, two normals, and an interpolation value between them.  If the interpolation value is 0, the result is the first point and normal.  If the interpolation value is 1, the result is the second point and normal.<br><br>How do you calculate a curved path between the two points, so that the function can return a value for any interpolation?  For example, if I got the values for interpolations of 0.0, 0.2, 0.4, 0.6, 0.8, and 1.0, and created a line from one to the next, it would form a curved path with 5 segments.<br><br>Function Curve( point0:TVec3, normal0:TVec3, point1:TVec3,  normal1:TVec3, interpolation:Float, resultpoint:TVec3, resultnormal:TVec3 )<br><br>EndFunction <br><br></td></tr></table><br>
<a name="961110"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SSS</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> So in principle I think the problem is pretty easy, but to do it efficiently might not be. Consider the function,<br><br>f(t) = at^3 + bt^2 + ct + d<br><br>where a,b,c,d are 3-vectors. Then essentially the problem that you've posed is to solve the equations,<br><br>f(0) = point0<br>f'(0) = tangent0<br>f(1) = point1<br>f'(1) = tangent1<br><br>where I've used tangent0 and tangent1 as opposed to normal0 and normal1 since these do not generate a unique line. Thus, you must simply solve two cubic equations and two quadratic. The former are going to be (as functions go) quite computationally intensive, but if you're not doing too many it should be OK. <br><br></td></tr></table><br>
<a name="961112"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cubic splines <a href="http://www.tinaja.com/glib/cubemath.pdf" target="_blank">[pdf]</a> are something I've been learning recently, in my quest to deal with network lag prediction, and they're very simple. (Applying them to physical movement/timing is the part that's holding me back.)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

' -----------------------------------------------------------------------------
' Basic cubic spline plotting...
' -----------------------------------------------------------------------------

' From "The Math Behind Bezier Cubic Splines" (http://www.tinaja.com/glib/cubemath.pdf)
' by Don Lancaster (http://www.tinaja.com/gurgrm01.asp)

' IMPORTANT: All variables are of floating point type here!

' -----------------------------------------------------------------------------
' Set up display...
' -----------------------------------------------------------------------------

Graphics 640, 480

' -----------------------------------------------------------------------------
' Start point...
' -----------------------------------------------------------------------------

x0# = 50.0
y0# = 150.0

' -----------------------------------------------------------------------------
' First influence point for curve...
' -----------------------------------------------------------------------------

x1# = 75.0
y1# = 60.0

' -----------------------------------------------------------------------------
' Second influence point for curve...
' -----------------------------------------------------------------------------

x2# = 175.0
y2# = 75.0

' -----------------------------------------------------------------------------
' End point...
' -----------------------------------------------------------------------------

x3# = 200.0
y3# = 150.0

' -----------------------------------------------------------------------------
' Coefficients, ie. the magic maths explained in above PDF...
' -----------------------------------------------------------------------------

' These results are fed into the x/y plotting code for each value of t (time from 0 to 1)...

' x values...

A# = x3 - (3.0 * x2) + (3.0 * x1) - x0
B# = (3.0 * x2) - (6.0 * x1) + (3.0 * x0)
C# = (3.0 * x1) - (3.0 * x0)
D# = x0

' y values...

E# = y3 - (3.0 * y2) + (3.0 * y1) - y0
F# = (3.0 * y2) - (6.0 * y1) + (3.0 * y0)
G# = (3.0 * y1) - (3.0 * y0)
H# = y0

' -----------------------------------------------------------------------------
' SHOW RESULTING SPLINE...
' -----------------------------------------------------------------------------

' The code below draws the points defined at the start and plots the spline...

' -----------------------------------------------------------------------------
' Clear screen...
' -----------------------------------------------------------------------------

Cls

' -----------------------------------------------------------------------------
' Draw specified points...
' -----------------------------------------------------------------------------

CenterRect x0, y0, 4, 255, 255, 0		' START
CenterRect x1, y1, 4, 0, 255, 255		' 	First influence point
CenterRect x2, y2, 4, 0, 255, 255		' 	Second influence point
CenterRect x3, y3, 4, 255, 255, 0		' END

' -----------------------------------------------------------------------------
' Draw spline...
' -----------------------------------------------------------------------------

For t# = 0.0 To 1.0 Step 0.1

	' Change 0.1 above to 0.01, 0.001, etc for more points (slower)...

	x# = (A * (t ^ 3.0)) + (B * (t ^ 2.0)) + (C * t) + D
	y# = (E * (t ^ 3.0)) + (F * (t ^ 2.0)) + (G * t) + H
	
	Plot x, y
	
Next

' -----------------------------------------------------------------------------
' Show result...
' -----------------------------------------------------------------------------

Flip

' -----------------------------------------------------------------------------
' Wait for mouse click and exit...
' -----------------------------------------------------------------------------

WaitMouse
End

' -----------------------------------------------------------------------------
' Draw rectangle centered on given point...
' -----------------------------------------------------------------------------

Function CenterRect (x#, y#, size#, r = 255, g = 255, b = 255)
	SetColor r, g, b
	sz2# = size / 2.0
	DrawRect x - sz2, y - sz2, size, size
End Function
</textarea><br><br>You can just replace the For/Next loop (<i>For t# = 0.0 To 1.0 Step 0.1</i>) with your interpolation value to get any point along the curve (eg. <i>t# = 0.5</i> is halfway).<br><br>Here's an animated visualisation -- use mouse, plus -/+ to select the point to be moved.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

' -----------------------------------------------------------------------------
' Basic cubic spline plotting...
' -----------------------------------------------------------------------------

' From "The Math Behind Bezier Cubic Splines" (http://www.tinaja.com/glib/cubemath.pdf)
' by Don Lancaster (http://www.tinaja.com/gurgrm01.asp)

SuperStrict

Type SplineBase

	' Coefficients of spline based on x/y positions...

	Field A:Float, B:Float, C:Float, D:Float
	Field E:Float, F:Float, G:Float, H:Float

End Type

Function GetSplineBase:SplineBase (x0:Float, y0:Float, x1:Float, y1:Float, x2:Float, y2:Float, x3:Float, y3:Float)

	Local sb:SplineBase = New SplineBase
	
	' x coefficients...
	
	sb.A = x3 - (3.0 * x2) + (3.0 * x1) - x0
	sb.B = (3.0 * x2) - (6.0 * x1) + (3.0 * x0)
	sb.C = (3.0 * x1) - (3.0 * x0)
	sb.D = x0
	
	' y coefficients...
	
	sb.E = y3 - (3.0 * y2) + (3.0 * y1) - y0
	sb.F = (3.0 * y2) - (6.0 * y1) + (3.0 * y0)
	sb.G = (3.0 * y1) - (3.0 * y0)
	sb.H = y0

	Return sb
	
End Function

AppTitle = "Use - and + keys to select current point..."

Graphics 640, 480

Local x:Float [4], y:Float [4]

' Start point...

x [0] = 50.0
y [0] = 150.0

' First influence point for curve...

x [1] = 75.0
y [1] = 60.0

' Second influence point for curve...

x [2] = 275.0
y [2] = 75.0

' End point...

x [3] = 400.0
y [3] = 150.0

MoveMouse GraphicsWidth () / 2, GraphicsHeight () / 2

' Move END point to start with...

Local point:Int = 3

Repeat

	' The - and + keys select current point...
	
	If KeyHit (KEY_MINUS)
		point = point - 1
		If point &lt; 0 Then point = 3
		MoveMouse x [point], y [point]
	EndIf
	
	If KeyHit (KEY_EQUALS)
		point = point + 1
		If point &gt; 3 Then point = 0
		MoveMouse x [point], y [point]
	EndIf

	' Set selected point to mouse position...
	
	x [point] = MouseX ()
	y [point] = MouseY ()
	
	Local test:SplineBase = GetSplineBase (x [0], y [0], x [1], y [1], x [2], y [2], x [3], y [3])

	Cls
	
	' Highlight current selected point...
	
	CenterRect x [point], y [point], 6, 255, 0, 255
	
	CenterRect x [0], y [0], 4, 255, 255, 0, "START"
	CenterRect x [1], y [1], 4, 0, 255, 255, "Influence #1"
	CenterRect x [2], y [2], 4, 0, 255, 255, "Influence #2"
	CenterRect x [3], y [3], 4, 255, 255, 0, "END"

	' Draw spline...
	
	For Local t:Float = 0.0 To 1.0 Step 0.001
	
		Local x:Float = (test.A * (t ^ 3.0)) + (test.B * (t ^ 2.0)) + (test.C * t) + test.D
		Local y:Float = (test.E * (t ^ 3.0)) + (test.F * (t ^ 2.0)) + (test.G * t) + test.H
		
		Plot x, y
		
	Next
	
	Flip

Until KeyHit (KEY_ESCAPE)

End

Function CenterRect (x:Float, y:Float, size:Float, r:Int = 255, g:Int = 255, b:Int = 255, msg:String = "")
	SetColor r, g, b
	Local sz2:Float = size / 2.0
	DrawRect x - sz2, y - sz2, size, size
	DrawText msg, x + sz2, y + sz2
End Function
</textarea><br><br>This <i>Blitz3D code</i> shows how to 'chain' splines to form smooth sequences -- the first two points in the second spline are derived from the last two points in the first.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

; -----------------------------------------------------------------------------
; Basic cubic spline plotting...
; -----------------------------------------------------------------------------

AppTitle "Use - and + keys to select current point..."

Graphics 640, 480, 0, 2
SetBuffer BackBuffer ()

Dim x# (4), y# (4)

; Start point...

x (0) = 50.0
y (0) = 150.0

; First influence point for curve...

x (1) = 75.0
y (1) = 60.0

; Second influence point for curve...

x (2) = 200.0
y (2) = 75.0

; End point...

x (3) = 300.0
y (3) = 150.0

MoveMouse GraphicsWidth () / 2, GraphicsHeight () / 2

; Move END point to start with...

Local point = 3

Repeat

	; The - and + keys select current point...
	
	If KeyHit (12)
		point = point - 1
		If point &lt; 0 Then point = 3
		MoveMouse x (point), y (point)
	EndIf
	
	If KeyHit (13)
		point = point + 1
		If point &gt; 3 Then point = 0
		MoveMouse x (point), y (point)
	EndIf

	; Set selected point to mouse position...
	
	x (point) = MouseX ()
	y (point) = MouseY ()
	
	spline1.SplineBase = GetSplineBase (x (0), y (0), x (1), y (1), x (2), y (2), x (3), y (3))

	; Test joining of a second spline...
	
	spline2.SplineBase = GetSplineBase (x (3), y (3), x (3) + (x (3) - x (2)), y (3) + (y (3) - y (2)), 400, 180, 550, 300)

	Cls
	
	; Highlight current selected point...
	
	CenterRect x (point), y (point), 6, 255, 0, 255
	CenterRect x (0), y (0), 4, 255, 255, 0, "START"
	CenterRect x (1), y (1), 4, 0, 255, 255, "Influence #1"
	CenterRect x (2), y (2), 4, 0, 255, 255, "Influence #2"
	CenterRect x (3), y (3), 4, 255, 255, 0, "END"

	; Draw splines...
	
	Color 127, 127, 255
	DrawSpline (spline1)
	
	Color 255, 127, 127
	DrawSpline (spline2)
	
	Delay 5
	
	Flip

Until KeyHit (1)

End

Type SplineBase

	; Coefficients of spline based on x/y positions...

	Field A#, B#, C#, D#
	Field E#, F#, G#, H#

End Type

Function GetSplineBase.SplineBase (x0#, y0#, x1#, y1#, x2#, y2#, x3#, y3#)

	Local sb.SplineBase = New SplineBase
	
	; x coefficients...
	
	sb\A = x3 - (3.0 * x2) + (3.0 * x1) - x0
	sb\B = (3.0 * x2) - (6.0 * x1) + (3.0 * x0)
	sb\C = (3.0 * x1) - (3.0 * x0)
	sb\D = x0
	
	; y coefficients...
	
	sb\E = y3 - (3.0 * y2) + (3.0 * y1) - y0
	sb\F = (3.0 * y2) - (6.0 * y1) + (3.0 * y0)
	sb\G = (3.0 * y1) - (3.0 * y0)
	sb\H = y0

	Return sb
	
End Function

Function DrawSpline (sp.SplineBase)

	For t# = 0.0 To 1.0 Step 0.001
	
		sx# = (sp\A * (t ^ 3.0)) + (sp\B * (t ^ 2.0)) + (sp\C * t) + sp\D
		sy# = (sp\E * (t ^ 3.0)) + (sp\F * (t ^ 2.0)) + (sp\G * t) + sp\H
		
		Plot sx, sy
		
	Next
	
End Function

Function CenterRect (x#, y#, size#, r = 255, g = 255, b = 255, msg$ = "")
	Color r, g, b
	Local sz2# = size / 2.0
	Rect x - sz2, y - sz2, size, size
	Text x + sz2, y + sz2, msg
End Function
</textarea><br><br>Lastly, some alternative splines that may be of interest:<br><br><a href="http://www.cse.unsw.edu.au/~lambert/splines/" target="_blank">Java demos</a><br><a href="http://www.cse.unsw.edu.au/~lambert/splines/source.html" target="_blank">Java code</a> <br><br></td></tr></table><br>
<a name="961125"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SSS</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah what I said before was totally wrong, you don't need to solve a cubic just a few linear equations. You have,<br><br>d = point0<br>c = tangent 0 (from the first two equations)<br><br>a+b+tangent0+point0 = point1<br>3a+2b+tangent0 = tangent1.<br><br>These two equations can be easily solved to give the equation that you need efficiently. <br><br></td></tr></table><br>
<a name="964940"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks, here is my 3D version:<br><pre class=code>Type TSpline

	Field A:Float, B:Float, C:Float, D:Float
	Field E:Float, F:Float, G:Float, H:Float
	Field I:Float, J:Float, K:Float, L:Float

	Method Init(p0:TVec3,n0:TVec3,p1:TVec3,n1:TVec3,mag:Float=1)
		Local x0:Float, y0:Float, z0:Float, x1:Float, y1:Float, z1:Float, x2:Float, y2:Float, z2:Float, x3:Float, y3:Float, z3:Float
		
		x0=p0.x
		y0=p0.y
		z0=p0.z

		x1=n0.x*mag+p0.x
		y1=n0.y*mag+p0.y
		z1=n0.z*mag+p0.z

		x2=n1.x*mag+p1.x
		y2=n1.y*mag+p1.y
		z2=n1.z*mag+p1.z

		x3=p1.x
		y3=p1.y
		z3=p1.z
				
		' x coefficients...
		A = x3 - (3.0 * x2) + (3.0 * x1) - x0
		B = (3.0 * x2) - (6.0 * x1) + (3.0 * x0)
		C = (3.0 * x1) - (3.0 * x0)
		D = x0
		
		' y coefficients...
		E = y3 - (3.0 * y2) + (3.0 * y1) - y0
		F = (3.0 * y2) - (6.0 * y1) + (3.0 * y0)
		G = (3.0 * y1) - (3.0 * y0)
		H = y0
		
		' z coefficients...
		I = z3 - (3.0 * z2) + (3.0 * z1) - z0
		J = (3.0 * z2) - (6.0 * z1) + (3.0 * z0)
		K = (3.0 * z1) - (3.0 * z0)
		L = z0
	EndMethod

	Method Interpolate(t:Float,v:TVec3)
		v.x = Float((A * (t ^ 3.0)) + (B * (t ^ 2.0)) + (C * t) + D)
		v.y = Float((E * (t ^ 3.0)) + (F * (t ^ 2.0)) + (G * t) + H)
		v.z = Float((I * (t ^ 3.0)) + (J * (t ^ 2.0)) + (K * t) + L)
	EndMethod

EndType</pre> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
