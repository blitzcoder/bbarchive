<!DOCTYPE html><html lang="en" ><head ><title >Leadwerks Abstract File System</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Leadwerks Abstract File System</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Leadwerks Abstract File System</a><br><br>
<a name="775164"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I was coincidentally dealing with package formats when Noel put his PhysFS module up.  I took some ideas from that and wrote a file system and package reader using ZipEngine.<br><br><a href="http://www.leadwerks.com/post/AFS.zip" target="_blank">http://www.leadwerks.com/post/AFS.zip</a><br><br>You start by registering the file path, returning a "Virtual Directory":<br><pre class=code>vd:TVirtualDir=RegisterPath(AppDir)</pre><br><br>You can then go through the files and folders recursively:<br><pre class=code>For vf:TVirtualFile=EachIn vd
	print vf.url
Next
For sd:TVirtualDir=EachIn vd
	'Do something...
Next</pre><br><br>This will treat zip file contents just as if they were in the same directory as the zip file.  It will also handle zip directory structures.  For example, a "materials" folder in the mypak.zip package is considered the same virtual folder as the "materials" folder on the hard drive.<br><br>To read a virtual file:<br><pre class=code>If vf stream:TStream=vf.Read()</pre><br><br>This will either load the real file, or extract and load a temp file from its package file.  You don't care where it comes from, you just want the file.  Files with the same names in different virtual directories are accounted for, of course.<br><br>I think the best aspect of this is you can add new package formats by extending the TPackageReader type, as I did to make the zip reader.  Nem's tools has a HL2 package lib, and it would be nice to see that working. <br><br></td></tr></table><br>
<a name="775166"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Picklesworth</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> This sounds a bit like GnomeVFS (which I've been recently learning about) (if you know what that is).<br><br>Do you have plans for transparent access to files over the Internet and the like? <br><br></td></tr></table><br>
<a name="775167"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hell no. <br><br></td></tr></table><br>
<a name="775170"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >N</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Will take a look at this once I manage to fully wake up.  Should be pretty good though. <br><br></td></tr></table><br>
<a name="775181"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> This will display the contents of the Q3A directory.  Double-click a file to extract and launch it.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">'===============================================================
'Abstract File System by Leadwerks Software
'===============================================================

window:TGadget=CreateWindow("File System Browser",200,200,640,480)
treeview:TGadget=CreateTreeView(0,0,window.clientwidth(),window.clientheight(),window)
SetGadgetLayout treeview,1,1,1,1

'Clean up temp dir
DeleteDir AppDir+"/temp",True

'vd:TVirtualDir=RegisterPath(AppDir)
vd:TVirtualDir=RegisterPath("C:\Program Files\Quake III Arena\baseq3")

do vd,TreeViewRoot(treeview)

ActivateGadget window

While WaitEvent()
	Select EventID()
		Case EVENT_GADGETACTION
			For vf:TVirtualFile=EachIn TVirtualFile.list
				If HandleToObject(vf.userdata)=SelectedTreeViewNode(treeview)
					OpenURL vf.RealURL()
					Exit
				EndIf
			Next
		Case EVENT_WINDOWCLOSE
			End
	End Select
Wend

Function do(vd:TVirtualDir,node:TGadget)
	For sd:TVirtualDir=EachIn vd.kids
		snode:TGadget=AddTreeViewNode(StripDir(sd.url),node)
		do(sd,snode)
	Next
	For vf:TVirtualFile=EachIn vd.files
		snode:TGadget=AddTreeViewNode(StripDir(vf.url),node)
		vf.userdata=HandleFromObject(snode)
	Next
EndFunction

Function RegisterPath:TVirtualDir(url$,parentdir:TVirtualDir=Null)
	url=RealPath(url)
	d=ReadDir(url)
	If Not d Return
	vd:TVirtualDir=TVirtualDir.find(url,parentdir)
	Repeat
		file$=NextFile(d)
		If file="" Exit
		Select FileType(url+"/"+file)
			Case 1
				pr:TPackageReader=TPackageReader.ExtensionSupported(Lower(ExtractExt(file)))
				If pr
					pr.Read(url+"/"+file,vd)
				Else
					TVirtualFile.Register(url+"/"+file,vd)
				EndIf
			Case 2
				If file&lt;&gt;"." And file&lt;&gt;".."
					RegisterPath(url+"/"+file,vd)
				EndIf
		EndSelect
	Forever
	CloseDir d
	Return vd
EndFunction

Type TVirtualFile
	Global list:TList=New TList

	Field url$
	Field lurl$
	Field tempurl$
	Field rpath$=""
	Field source
	Field userdata:Int
	Field package:TPackage

	Method New()
		list.addlast(Self)
	EndMethod
	
	Method Read:TStream()
		If package=Null
			tempurl=url
			Return ReadFile(url)
		Else
			Return package.readfile(Self)
		EndIf
	EndMethod

	Method RealURL$()
		If package=Null
			Return url
		Else
			package.extract(Self)
			Return tempurl
		EndIf
	EndMethod

	Function Find:TVirtualFile(url$)
		url=RealPath(url)
		lurl$=Lower(url)
		For vf:TVirtualFile=EachIn list
			If vf.lurl=lurl Return vf
		Next
	EndFunction
	
	Function Register:TVirtualFile(url$,vd:TVirtualDir,package:TPackage=Null,rpath$="")
		If Find(url) Return Null
		vf:TVirtualFile=New TVirtualFile
		vf.url=RealPath(url)
		vf.lurl$=Lower(url)
		vf.rpath=rpath
		vf.package=package
		vd.files.addlast(vf)
		Return vf
	EndFunction
	
EndType

Type TPackage
	Field url$
	Field files:TList=New TList
	Field vf:TVirtualDir
	Field parent:TPackage
	Field kids:TList=New TList
	Field reader:TPackageReader
	
	Function Load:TPackage(url$,vd:TVirtualDir)
		url=RealPath(url)
		ext$=Lower(ExtractExt(url))
		For pr:TPackageReader=EachIn TPackageReader.list
			If pr.extensions.contains(ext)
				package:TPackage=pr.Read(url,vd)
			EndIf
		Next
		Return package
	EndFunction
	
	Method Extract:Int(vf:TVirtualFile)
		Return reader.extract(vf)
	EndMethod
	
	Method ReadFile:TStream(vf:TVirtualFile)
		Return reader.readfile(vf)
	EndMethod
	
EndType

Type TPackageReader
	Global list:TList=New TList
	
	Field extensions:TList=New TList
	
	Method New()
		list.addfirst(Self)
	EndMethod

	Method Read:TPackage(url$,vd:TVirtualDir) Abstract	
	Method Extract:Int(vf:TVirtualFile) Abstract
	Method ReadFile:TStream(vf:TVirtualFile) Abstract
	
	Function ExtensionSupported:TPackageReader(ext$)
		For pr:TPackageReader=EachIn TPackageReader.list
			If pr.extensions.contains(ext)
				Return pr
			EndIf
		Next
		Return Null
	EndFunction

EndType

Type TZipPackageReader Extends TPackageReader
	Global pr:TPackageReader=New TZipPackageReader
	
	Field zrObject:ZipReader
	
	Method New()
		extensions.addlast("zip")
		extensions.addlast("pk3")
	EndMethod

	Method GenerateExtractPath$(url$)
		Local index=0
		file$=StripAll(url)
		ext$=ExtractExt(url)
		path$=AppDir+"/temp/"+file+"."+ext
		While FileType(path)=1
			path$=AppDir+"/temp/"+file+"_"+index+"."+ext
			index:+1
		Wend
		Return path
	EndMethod
	
	Method Extract:Int(vf:TVirtualFile)
		If Not zrObject.OpenZip(vf.package.url) Return False
		If FileType(AppDir+"/temp")&lt;&gt;2 CreateDir(AppDir+"/temp")
		If FileType(AppDir+"/temp")&lt;&gt;2 Return Null
		vf.tempurl=GenerateExtractPath$(vf.url)
		zrObject.ExtractFileToDisk(vf.rpath,vf.tempurl,False,"")
		zrObject.closezip()
		Return True
	EndMethod
	
	Method ReadFile:TStream(vf:TVirtualFile)
		If Not Extract(vf) Return Null
		stream:TStream=ReadStream(vf.tempurl)
		Return stream
	EndMethod

	Method Read:TPackage(url$,vd:TVirtualDir)
		zrObject=New ZipReader
		If Not zrObject.OpenZip(url) Return Null
		package:TPackage=New TPackage
		package.url=url
		package.reader=Self
		package.vf=vd
		For Local i:Int=0 To zrObject.getFileCount()-1
			file$=zrObject.getFileInfo(i).zipFileName
			ext$=Lower(ExtractExt(file))
			Select ext
				Case ""
					dirname$=ExtractDir(url)+"/"+file
					vfd:TVirtualDir=TVirtualDir.Find(dirname,Null,True)
				Default
					If Instr(file,"/")
						dirname$=ExtractDir(url)+"/"+ExtractDir(file)
						vfd:TVirtualDir=TVirtualDir.Find(dirname,Null,True)
					Else
						vfd=vd
					EndIf
					realfile$=vfd.url+"/"+StripDir(file)
					TVirtualFile.Register realfile,vfd,package,file
			EndSelect
		Next
		zrObject.CloseZip()
		Return package
	EndMethod
	
EndType

Type TVirtualDir
	Global list:TList=New TList
	Field parent:TVirtualDir
	Field kids:TList=New TList
	Field url$,lurl$
	Field files:TList=New TList
	
	Method New()
		list.addlast(Self)
	EndMethod
	
	Function Find:TVirtualDir(url$,parent:TVirtualDir=Null,fix=False)
		url=RealPath(url)
		lurl$=Lower(url)
		For vf:TVirtualDir=EachIn list
			If vf.lurl=lurl Return vf
		Next
		vf=New TVirtualDir
		vf.url=url
		vf.lurl=lurl
		If parent=Null
			If fix
				dirname$=ExtractDir(url)
				parent=Find(dirname)
			EndIf
		EndIf
		If parent parent.kids.addlast(vf)
		Return vf
	EndFunction
	
	Method Display(ws$="",test=False)
		For f:TVirtualFile=EachIn files
			Print ws+"File: "+f.url
			If test
				stream:TStream=f.read()
				If stream
					stream.close()
				Else
					Notify "Error reading virtual file "+Chr(34)+f.url+Chr(34)
				EndIf
			EndIf
		Next
		For d:TVirtualDir=EachIn kids
			Print ws+"Folder: "+d.url
			d.display ws+"	",test
		Next
	EndMethod
	
EndType</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
