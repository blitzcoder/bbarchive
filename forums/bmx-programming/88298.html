<!DOCTYPE html><html lang="en" ><head ><title >Zoom and pan.</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Zoom and pan.</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Zoom and pan.</a><br><br>
<a name="1002548"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sanctus</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey guys. <br>Sorry to post again but I have a intresting problem and I just can't figure it out.<br>I'm trying to make a little map editor (2D) and thus I'd like to use pan and zoom.<br>So I have these variables to hold my zoom and pan<br><pre class=code>
Global Offx:Float = 0
Global Offy:Float = 0
Global Zoom:Float = 1
</pre><br>In my Draw I use this thing<br><pre class=code>
SetScale(TApp.Zoom, TApp.Zoom)
DrawImage (TImage(map.tileset.TileList.ValueAtIndex(grid[I, J].img)), (sx - TApp.Offx) * TApp.Zoom, (sy - TApp.Offy) * TApp.Zoom)
</pre><br><br>sx and sy are the basic tile positions (It's isometric and I have to calculate it)<br><br>Works fine when I pan, but when I zoom in and out I'd like it to zoom around the place where my mouse is so I'm guessing that has to modify my offset but I can't figure out how to do that.<br>Could anyone be so nice to help me? <br><br></td></tr></table><br>
<a name="1002551"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Like you say, your zoom value affects your offsets too.  Something like..<br><br><pre class=code>
SetScale(TApp.Zoom, TApp.Zoom)
DrawImage (TImage(map.tileset.TileList.ValueAtIndex(grid[I, J].img)), sx - (TApp.Offx * TApp.Zoom), sy - (TApp.Offy * TApp.Zoom) )
</pre> <br><br></td></tr></table><br>
<a name="1002554"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sanctus</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I tried this the first time and the result is even worse...<br><img src="http://img109.imageshack.us/img109/8891/badideea.jpg"><br>Look at this. The distance between the tiles stays the same now.<br>sx and sy must really be multiplied with the zoom.<br>There is something with the offx and offy that's not right. <br><br></td></tr></table><br>
<a name="1002559"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> test this:<br><br>write before "DrawImage.....<br><br>Print "before: " + sx - TApp.Offx<br>Print "after: " + (sx - TApp.Offx) * TApp.Zoom<br><br><br>.. to make sure, that the values are as expected <br><br></td></tr></table><br>
<a name="1002561"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Try adjusting the sx &amp; sy according to the zoom value too...<br><br><pre class=code>
SetScale(TApp.Zoom, TApp.Zoom)
DrawImage (TImage(map.tileset.TileList.ValueAtIndex(grid[I, J].img)), (sx * TApp.Zoom) - (TApp.Offx * TApp.Zoom), (sy * TApp.Zoom) - (TApp.Offy * TApp.Zoom) )
</pre><br><br>If possible post more code so we can test what were offering :) <br><br></td></tr></table><br>
<a name="1002563"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_JIM</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well, I gave him a very handy solution but I'll post it here if anyone needs it:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'2D transformation processing class - VERY useful :)
Type TTransform2D
	Field Matrix:Double[2, 2]
	Field Rotation:Float = 0
	Field ScaleX:Float = 1
	Field ScaleY:Float = 1
	
	Method Init(sx:Float = 1, sy:Float = 1, rot:Float = 0)
		Self.ScaleX = sx
		Self.ScaleY = sy
		Self.Rotation = rot
		Self.Update()
	EndMethod
	
	Method Scale(sx:Float, sy:Float)
		ScaleX = sx
		ScaleY = sy
		
		Self.Update()
	EndMethod
	
	Method Update()
		Matrix[0, 0] = Cos(Rotation)
		Matrix[1, 0] = - Sin(Rotation)
		Matrix[0, 1] = Sin(Rotation)
		Matrix[1, 1] = Cos(Rotation)
	End Method
	
	Method Rotate(angle:Float)
		Rotation = angle
		
		Self.Update()
	EndMethod
	
	Method Transform(x:Float var, y:Float var)
		Local xx:Float
		Local yy:Float
		
		x = x * ScaleX
		y = y * ScaleY
		
		xx = x * Matrix[0, 0] + y * Matrix[1, 0]
		yy = x * Matrix[0, 1] + y * Matrix[1, 1]
		
		x = xx
		y = yy
	End Method
	
	Method ITransform(x:Float var, y:Float var)
		Local xx:Float
		Local yy:Float
		
		Self.Rotation = -Self.Rotation
		Self.Update()
		
		xx = x * Matrix[0, 0] + y * Matrix[1, 0]
		yy = x * Matrix[0, 1] + y * Matrix[1, 1]
		
		Self.Rotation = -Self.Rotation
		Self.Update()
		
		xx = xx / ScaleX
		yy = yy / ScaleY
		
		x = xx
		y = yy
	End Method
End Type

Type TEventHandler
	Method New()
		AddHook EmitEventHook, EventHook, Self
	EndMethod
	
	Method Delete()
		RemoveHook EmitEventHook, EventHook, Self
	EndMethod
	
	Method ProcessEvent:TEvent(event:TEvent) Abstract
	
	Function EventHook:Object(id:Int,data:Object,context:Object)
		Local event:TEvent=TEvent(data)
		If event Return TEventHandler(context).ProcessEvent(event) Else Return data
	EndFunction
EndType

Type TZoomPanControl Extends TEventHandler
	Global ControlsList:TList = New TList
	
	Field Gadget:TGadget
	Field isOpen:Int
	
	Field OX:Float
	Field OY:Float
	Field MX:Float
	Field MY:Float
	Field MousePos:Float[2]
	Field MZSpeed:Int
	
	Field MHit:Byte[4]
	Field MDown:Byte[4]
	
	Field KHit:Byte[256]
	Field KDown:Byte[256]
	
	Field OldOX:Float
	Field OldOY:Float
	
	Field ScaleTarget:Float
	Field ScaleFactor:Float
	Field OldScaleFactor:Float
	
	Field Dragging:Int
	
	Method New()
		AddHook EmitEventHook, EventHook, Self
		ControlsList.AddLast(Self)
	End Method
	
	Method Delete()
		RemoveHook EmitEventHook, EventHook, Self
	End Method
	
	Method SetUp()
		If (Gadget &lt;&gt; Null)
			OX = ClientWidth(Gadget) / 2
			OY = ClientHeight(Gadget) / 2
		EndIf
		OldOX = 0
		OldOY = 0
		Self.ScaleTarget = 100
		Self.ScaleFactor = 1
	End Method
	
	Method Update()
		
		Local tf:TTransform2D = New TTransform2D
		
		Local SzX:Float
		Local SzY:Float

		If (Gadget &lt;&gt; Null)
			SzX = ClientWidth(Gadget)
			SzY = ClientHeight(Gadget)
		End If
		
		Local ZoomCenterX:Float = SzX / 2.0
		Local ZoomCenterY:Float = SzY / 2.0
		
		Local OOX:Float = (Self.OX - ZoomCenterX)
		Local OOY:Float = (Self.OY - ZoomCenterY)
		
		tf.Init(Self.ScaleFactor, Self.ScaleFactor)
		tf.ITransform(OOX, OOY)
		
		Self.OldScaleFactor = Self.ScaleFactor
		Self.ScaleFactor = Self.ScaleFactor - (Self.ScaleFactor - (Float(Self.ScaleTarget) / 100.0)) / 20

		tf.Init(Self.ScaleFactor, Self.ScaleFactor)
		tf.Transform(OOX, OOY)
		
		Self.oX = OOX + ZoomCenterX
		Self.oY = OOY + ZoomCenterY
		
		If (Self.MHit[1])
			Self.Dragging = 1
		End If
		
		If (Self.MDown[1] = 0)
			Self.Dragging = 0
		End If
		
		If (Self.MHit[3])
			Self.MX = Self.MousePos[0]
			Self.MY = Self.MousePos[1]
			Self.OldOX = Self.OX
			Self.OldOY = Self.OY
			ActivateGadget(Self.Gadget)
		End If
		
		If (Self.MDown[3])
			Self.OX = (Self.MousePos[0] - Self.MX) + Self.OldOX
			Self.OY = (Self.MousePos[1] - Self.MY) + Self.OldOY
			ActivateGadget(Self.Gadget)
		Else
			Self.OldOX = Self.OX
			Self.OldOY = Self.OY
		End If
		
		If (KHit[KEY_NUMSUBTRACT] Or (Self.MZSpeed &lt; 0))
			Self.ScaleTarget = Max(Self.ScaleTarget - 10, 10)
		EndIf
		If (Self.KHit[KEY_NUMADD] Or (Self.MZSpeed &gt; 0))
			Self.ScaleTarget = Min(Self.ScaleTarget + 10, 400)
		EndIf
		
		If (Self.KHit[KEY_NUMMULTIPLY])
			Self.ScaleTarget = 100
		EndIf
		
		Self.MHit[3] = 0
		Self.MZSpeed = 0
	End Method
	
	Method ProcessEvent:TEvent(event:TEvent)
		If (isOpen = 0) Then Return(event)
		
		Select event.id
			Case EVENT_MOUSEMOVE
				If event.source = Self.Gadget
					Self.MousePos[0] = event.X
					Self.MousePos[1] = event.Y
					Return Null
				EndIf
				
			Case EVENT_MOUSEWHEEL
				If event.source = Self.Gadget
					Self.MZSpeed:+Int(event.data)
					Return Null
				EndIf

			Case EVENT_MOUSEDOWN
				If (event.source = Self.Gadget)
					Self.MHit[Int(event.data)] = 1
					Self.MDown[Int(event.data)] = 1
					Return Null
				EndIf
			Case EVENT_MOUSEUP
				If (event.source = Self.Gadget)
					Self.MDown[Int(event.data)] = 0
					Return Null
				EndIf
			Case EVENT_KEYDOWN
				If (event.source = Self.Gadget)
					KDown[Int(event.data)] = 1
					KHit[Int(event.data)] = 1
					Return Null
				EndIf
			Case EVENT_KEYUP
				If (event.source = Self.Gadget)
					KDown[Int(event.data)] = 0
					Return Null
				EndIf
		End Select
		Return event
	EndMethod
	
	Method AttachTo(g:TGadget)
		If (g = Null) Then Return
		Local t:TZoomPanControl = TZoomPanControl(GadgetExtra(g))
		
		If (t &lt;&gt; Null)
			t.isOpen = 0
		End If
		
		SetGadgetExtra(g, Self)
		Self.Gadget = g
		Self.isOpen = 1
	End Method
	
	Method Follow(targetx:Float, targety:Float)
		Local SzX:Float = GadgetWidth(Self.Gadget)
		Local SzY:Float = GadgetHeight(Self.Gadget)
		Local xdiff:Float = targetx - (-Self.OX + SzX / 2)
		Local ydiff:Float = targety - (-Self.OY + SzY / 2)
		
		Self.OldOX = Self.OX
		Self.OldOY = Self.OY
		
		Self.OX:-xdiff
		Self.OY:-ydiff
	End Method
End Type
</textarea><br><br>In order to use it, you have to basically call 3 methods:<br><br>AttachTo(gadget) - use it when you want the control to handle a specific gadget (used on canvases I presume :) )<br>SetUp() - just after you attached it for the first time. This resets the parameters<br>Update() - call per frame to have the zoom update<br><br>When you draw your images, all you have to do is:<br><br><pre class=code>
DrawImage img, imgx * ZPC.ScaleFactor + ZPC.OX, imgy * ZPC.ScaleFactor + ZPC.OY
</pre><br><br>As always, code is free to use in any way. Credits are welcome :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
