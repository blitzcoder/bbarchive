<!DOCTYPE html><html lang="en" ><head ><title >[Win32+ MAXGUI Required] Cool dissolve effect.</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >[Win32+ MAXGUI Required] Cool dissolve effect.</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >[Win32+ MAXGUI Required] Cool dissolve effect.</a><br><br>
<a name="754119"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Damien Sturdy</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi all,<br><br>I'm getting mixed results with the below (rather messy) code.Errors cropping up on older systems.<br><br>Could you all give this code a try, let me know any findings? :) it works fine on my machine and I can't track the problem. I'm thinking its the clipboard code- (To the person who sent me this code: I lost your email, thanks for the code!)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Framework brl.D3D7Max2D
Import brl.Random
Import brl.Win32MaxGUI


SetGraphicsDriver GLMax2DDriver()

Global width:Int=Desktop().width
Global height:Int=Desktop().height

Global hwnd:Int=QueryGadget(Desktop(),QUERY_HWND)
Global window:TGadget=CreateWindow("Test",000,000,width,height,Null,WINDOW_hidden)',Null,15)
Global canvas:tgadget=CreateCanvas(0,0,width,height,window,0)


SetGraphics CanvasGraphics(canvas)
Global pixmap:TPixmap=getpixmapfromwindow()
If pixmap=Null Then RuntimeError "Grab Desktop Failed. I fart in your general direction. :P."

'Draw pixmap twice - seems to minimise bugs.
DrawPixmap pixmap,0,0
Flip
DrawPixmap pixmap,0,0

pixmap=GrabPixmap(0,0,width,height) 'grab again to clear alpha issues.
Global topixmap:TPixmap=GrabPixmap(0,0,width-1,height-1);ClearPixels(topixmap,$000000)
Global image:Timage=LoadImage(pixmap)


'Draw pixmap twice - seems to minimise bugs.
DrawPixmap pixmap,0,0
Flip
DrawPixmap pixmap,0,0

'Show window. This will be seamless with the desktop!
ShowGadget window
ActivateWindow(window)


'For swipe=0 To 0
Local swipe:Float=Rand(0,16)

	While PollEvent()
		Select EventID()
			
			Case EVENT_GADGETPAINT
			Case EVENT_WINDOWCLOSE
			End
			Case EVENT_APPTERMINATE
			End
			Case EVENT_GADGETACTION
		End Select
	Wend
	'-------------------------------------------------
	'Execute screen transition!
	Swapscreens(pixmap,topixmap,40,40,swipe,10)
	'-------------------------------------------------

	'OK, fade in the desktop again
	SetBlend ALPHABLEND
	For Local alpha#=0 To 1 Step 0.025
		SetRotation 0
		SetAlpha alpha
		DrawImage image,0,0
		Flip
		Delay 30
	Next
	SetBlend 0
'Next

'Hide window. Seamless!
HideGadget window

End

Function Swapscreens(Fromimage:TPixmap,toimage:TPixmap,Xcount:Int,Ycount:Int,style:Int=0,lifemult#=1)
GCSuspend()	'disable GC whilst fading. Seems to bring up the speed a little.

Local chunk:tchunk
	Local oldrot#=GetRotation()
	Type tchunk
		Field image:TImage
		Field pixmap:TPixmap
		Field life#
		Field rot:Float
		Field x#,y#
		Field movex#,movey#=-1
		Field xrotspeed#=Rnd(0,10),yrotspeed#=Rnd(0,10)
		Field xrot#,yrot#
		Field rotspeed#=Rnd(-12,12)
		Method Delete()
		Self.pixmap=Null
		Self.image=Null
		End Method
	End Type
	Local width:Int=fromimage.width
	Local height:Int=fromimage.height

	Local chunklist:TList=New TList
	Local chunksx:Float=Xcount
	Local chunksy:Float=Ycount
	Local chunksizex:Int=Float(width/(chunksx-1))
	Local chunksizey:Int=Float(height/(chunksy-1))
	Local toimage2:timage=LoadImage(toimage)
	Local fromimage2:timage=LoadImage(fromimage)
	
	DrawImage fromimage2,0,0
	Flip
	
	For Local x1#=0 To chunksx
		For Local y1#=0 To chunksy
			Local startx:Int=(x1*chunksizex)
			Local starty:Int=(y1*chunksizey)
			chunk:tchunk=New tchunk
			chunk.x=startx+(chunksizex/2)
			chunk.y=starty+(chunksizey/2)
			
			If startx+chunksizex&lt;=width And starty+chunksizey&lt;=height Then
				'Create image for this chunk!
				chunk.image=CreateImage(chunksizex,chunksizey)
				'Grab from backbuffer into created image.
				GrabImage(chunk.image,startx,starty)
				SetImageHandle(chunk.image,chunksizex/2,chunksizey/2)
				ListAddLast chunklist,chunk

			chunk.life=1
			Local x#=Float(x1/chunksx)*20
			Local y#=Float(y1/chunksy)*20
			If style=0 Then chunk.life=Abs(x-(chunksx/2.0))+Abs(y-(chunksy/2.0))
			If style=1 Then chunk.life=(Abs((chunksx/2.0))+Abs((chunksy/2.0)))-(Abs(x-(chunksx/2.0))+Abs(y-(chunksy/2.0)))
			If style=2 Then chunk.life=(x+y)
			If style=3 Then chunk.life=(chunksx-x)+(chunksy-y)
			If style=4 Then chunk.life=y
			If style=5 Then chunk.life=(chunksy-y)
			If style=6 Then chunk.life=Abs(x-(chunksx/2.0))
			If style=7 Then chunk.life=Abs(y-(chunksy/2.0))
			If style=8 Then chunk.life=chunksx-Abs(x-(chunksx/2.0))
			If style=9 Then chunk.life=chunksy-Abs(y-(chunksy/2.0))
			If style=10 Then chunk.life=(chunksx+chunksy)-Abs((y+x)-(chunksy+chunksx)/2.0)
			If style=11 Then chunk.life=Abs((y+x)-(chunksy+chunksx)/2.0)
			If style=12 Then chunk.life=Abs((Sin(x*10)+Cos(y*20)))*10
			If style=13 Then chunk.life=Abs(((1-Sin(x*10))+(1-Cos(y*20))))*10
			If style=14 Then chunk.life=Abs((Sin(x*10)-Cos(y*20)))*10
			If style=15 Then chunk.life=Abs((-Sin(x*40)+Cos((y*40)+180)))*10
			If style=16 Then chunk.life=20-Abs((Sin(x*40)+Cos((y*40))))*10
			
			'chunk.life=(chunk.life/Float((chunksX+chunksY)/2.0))*10.0
			chunk.life=Int(chunk.life*5)+1'Int((chunk.life*lifemult)+1)
			EndIf

		Next
	Next
	Local count:Int
	Repeat
		count=0
		Cls
		SetRotation 0
		DrawImage toimage2,0,0
		Delay 1
		For chunk:tchunk=EachIn chunklist
			SetRotation chunk.rot
			If chunk.life&lt;0 Then
				chunk.rot=chunk.rot+chunk.rotspeed
			EndIf
			DrawImage chunk.image,chunk.x,chunk.y
			chunk.life=chunk.life-1;If chunk.life=0 Then chunk.movey=Rnd(-3,-5);chunk.movex=Rnd(-5,5)
			If chunk.life&lt;0 Then
				chunk.x=chunk.x+chunk.movex
				chunk.y=chunk.y+chunk.movey
				chunk.movey=chunk.movey+.3
			EndIf
			If chunk.x&gt;GraphicsWidth()+chunksizex Or chunk.x&lt;-chunksizex Or chunk.y&gt;GraphicsHeight()+chunksizey Then ListRemove chunklist,chunk;chunk.image=Null;
			count=count+1
		Next
		Flip True
		If KeyHit(key_escape) Then End
	Until count=0
	SetRotation oldrot
	DebugLog chunklist.count()
	GCResume()

End Function


Function GetScreenCaptureWidth:Int()
	Return Width
End Function

Function GetScreenCaptureHeight:Int()
	Return Height
End Function



Function getpixmapfromwindow:TPixmap(hwnd:Int=-1)
   
   Extern "Win32"
      Function GetDIBits(hdc:Int, bitmap:Int, Start:Int, Num:Int, bits:Byte Ptr, lpbi:Byte Ptr, usage:Int)
      Function CreateCompatibleBitmap(hdc:Int, Width:Int, Height:Int)
      Function CreateDIBSection(hdc:Int, pbmi:Byte Ptr, usage:Int, Bits:Byte Ptr, hSection:Int, Offset:Int)
      Function SelectObject(hdc:Int, obj:Int)
      Function CreateCompatibleDC(hdc:Int)
      Function GetDesktopWindow()
      Function GetWindowDC(hwnd:Int)
      Function GetDeviceCaps(hdc:Int, index:Int)
      Function DeleteDC(hdc:Int)
      Function DeleteObject(obj:Int)
      Function ReleaseDC(hwdn:Int, hdc:Int)
      Function BitBlt(hdc:Int,x:Int,y:Int,w:Int,h:Int,src_dc:Int,src_x:Int,src_y:Int,dwrop:Int)
   End Extern


   ?Win32
   
      Type BITMAPINFO
         Field biSize:Int
         Field biWidth:Int
         Field biHeight:Int
         Field biPlanes:Short
         Field biBitCount:Short
         Field biCompression:Int
         Field biSizeImage:Int
         Field biXPelsPerMeter:Int
         Field biYPelsPerMeter:Int
         Field biClrUsed:Int
         Field biClrImportant:Int
         
         Field R:Byte
         Field G:Byte
         Field B:Byte
         Field Res:Byte
      End Type
      
      
      Const HORZRES:Int = 8
      Const VERTRES:Int = 10
   
      Local HwndDesktop:Int
      Local hdcDesktop:Int
      Local hdcMem:Int
      Local DesktopWidth:Int
      Local DesktopHeight:Int
      Local bmpMem:Int
      Local info:BITMAPINFO
      Local FinalPixmap:TPixmap
   
      If hwnd=-1 Then HwndDesktop = GetDesktopWindow() Else hwnddesktop=hwnd
   
      If Not HwndDesktop
         Return Null
      EndIf
      
      
      hdcDesktop = GetWindowDC(HwndDesktop)
      
      If Not HdcDesktop
         Return Null
      EndIf
      
      
      hdcMem = CreateCompatibleDC(hdcDesktop)
      
      If Not HdcMem
         Return Null
      EndIf
      
      
      DesktopWidth = GetDeviceCaps(hdcDesktop, HORZRES)
      DesktopHeight = GetDeviceCaps(hdcDesktop, VERTRES)
   
      If DesktopWidth = 0 Or DesktopHeight = 0
         Return Null
      EndIf
      
      
      bmpMem  = CreateCompatibleBitmap(hdcDesktop, DesktopWidth, DesktopHeight)
   
      If Not BmpMem
         Return Null
      EndIf
      
      
      If Not SelectObject(HdcMem, bmpMem)
         Return Null
      EndIf
      
      info                = New BITMAPINFO
      info.biSize         = SizeOf(info)
      info.biWidth        = DesktopWidth
      info.biHeight       = DesktopHeight
      info.biPlanes       = 1
      info.biBitCount     = 32
      info.biCompression  = 0
      
      
      If Not BitBlt(hdcMem,0,0,info.biWidth,info.biHeight, hdcDesktop,0,0,ROP_SRCCOPY)   
         Return Null
      EndIf
      
      FinalPixmap = CreatePixmap(info.biWidth, info.biHeight, PF_BGRA8888)
      
      If Not GetDIBits(hdcMem, bmpMem, 0, info.biHeight, FinalPixmap.PixelPtr(0,0), info, 0)
         Return Null
      EndIf
      
      FinalPixmap = YFlipPixmap(FinalPixmap)
      
      DeleteDC(HdcMem)
      DeleteObject(bmpMem)
      ReleaseDC(hwndDesktop, hdcDesktop)
      
      Return FinalPixmap

   ?
   
   ?Linux
      Return Null
   ?
   
   ?MacOs
      Return Null
   ?
End Function
</textarea> <br><br></td></tr></table><br>
<a name="754136"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >WendellM</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Won't even compile here (specs in sig). Is this more than one program stuck together or something?  As currently written, there's an End on line 76 followed by what looks like another program beginning with SuperStrict on line 77 (which the compiler isn't happy about seeing there). <br><br></td></tr></table><br>
<a name="754138"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> It seems he pasted the source twice or something,<br>but other than that it looked nice =) <br><br></td></tr></table><br>
<a name="754235"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Damien Sturdy</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Whoops! Yup, pasted the code twice!!<br><br>Sorry guys. I'd had a bit to drink last night and wasn't checking what i'd posted!<br><br>I fixed my post. :) <br><br></td></tr></table><br>
<a name="754324"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well done. Nice piece of work! <br><br></td></tr></table><br>
<a name="754330"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Doesn't the AnimateWindow API command do the same job? <a href="http://msdn.microsoft.com/library/default.asp?url=/library/en-us/winui/winui/windowsuserinterface/windowing/windows/windowreference/windowfunctions/animatewindow.asp" target="_blank">http://msdn.microsoft.com/library/default.asp?url=/library/en-us/winui/winui/windowsuserinterface/windowing/windows/windowreference/windowfunctions/animatewindow.asp</a> <br><br></td></tr></table><br>
<a name="754356"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Damien Sturdy</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cheers Grisu.<br><br>Indie, did you actually try it? :D this goes a little further than that api func. <br><br></td></tr></table><br>
<a name="754359"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >WendellM</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Your code runs fine (and is quite nice-looking) on my system under XP, Cygnus, but not under 98.  You mentioned older system trouble: I don't get an error in 98; clicking on the exe just doesn't seem to do anything that I can see.<br><br>UPDATE: I tried a debug build (cleverly named "debug") and it crashed non-silently when run in 98, with the following:<br><div class="quote"> DEBUG caused a general protection fault<br>in module &lt;unknown&gt; at 0000:00000003.<br>Registers:<br>EAX=00aa2b90 CS=018f EIP=00000003 EFLGS=00010283<br>EBX=00000000 SS=0197 ESP=00000004 EBP=00000000<br>ECX=00000000 DS=0197 ESI=00000000 FS=0e97<br>EDX=00ab5f30 ES=0197 EDI=00000000 GS=0000<br>Bytes at CS:EIP:<br>00 65 04 70 00 16 00 1f 05 65 04 70 00 65 04 70 <br>Stack dump:<br>00700465 051f0016 00700465 00700465 f000ff54 f0007c68 f000e819 cd000000 051f0028 f000ef6f f000ef6f f000ef6f f000ef6f 051f009a 00700465 cd700007 <br></div> <br><br></td></tr></table><br>
<a name="754377"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >popcade</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> What I can say is "amazing"! <br><br></td></tr></table><br>
<a name="754390"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> No I did not try it :) I don't have MaxGUI. <br><br></td></tr></table><br>
<a name="754473"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SpaceAce</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> The effect looks pretty cool but acts funny on my dual-monitor setup. The secondary screen goes blank like it usually does when an application goes into full-screen mode then the main screen wipes and pieces of the main screen spread out onto the second monitor.<br><br>SpaceAce <br><br></td></tr></table><br>
<a name="754569"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Damien Sturdy</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>The effect looks pretty cool but acts funny on my dual-monitor setup. The secondary screen goes blank like it usually does when an application goes into full-screen mode then the main screen wipes and pieces of the main screen spread out onto the second monitor.<br> <br></div><br><br>Coool!<br><br>I've updated the code at home, it seems more stable now and I fixed a few bugs. I'll repost later.<br><br>Wendel, Thanks. I think the screensnap code is failing on win98.  The effect code however should still work- i'll post that seperately later.<br><br>Indiepath; The non-GUI version will be of use to you, and i'll post a compiled EXE for you to try to see what it's doing. It's the sort of effect that would come in handy in your games :)<br><br>@Yoko. Thanks. I'm glad you like.<br><br>This was meant to be a bug chasing post but it seems OK bar win98. No idea about that though! <br><br></td></tr></table><br>
<a name="754590"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >popcade</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi, Cygnus,<br><br>This is pretty cool to be the starty of a Screen Saver, and will be killer(sinece none was thinking of this yet), maybe it'll be a great start. <br><br></td></tr></table><br>
<a name="754593"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Damien Sturdy</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>This is pretty cool to be the starty of a Screen Saver, and will be killer(sinece none was thinking of this yet), maybe it'll be a great start. <br> <br></div><br><br>I'm way ahead of you. I created a photo-viewer screensaver for my GF last night! :) <br><br></td></tr></table><br>
<a name="754603"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >popcade</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> That's Cool indeed, please keep up updating. <br><br></td></tr></table><br>
<a name="754954"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >WendellM</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cygnus: I installed BMax under 98 to better track it down (I was previously running an .exe compiled under XP in 98), and getpixmapfromwindow() indeed returned null, complete with farting in my general direction &lt;g&gt;.  'Course there's no real <i>need</i> to support 98 these days (I try to on general principle when it's not a big problem, but that's just me). <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
