<!DOCTYPE html><html lang="en" ><head ><title >Loadbank(): Showing progress possible?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Loadbank(): Showing progress possible?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Loadbank(): Showing progress possible?</a><br><br>
<a name="1040195"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi guys!<br><br>I'm using the following function to download a file from the internet. Is it possible to show the current progress of this process? <br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Function HTTPGetFile:Int(url$, filePath$)
	Url$ = url$.Replace("http://", "http::")
	Local bank:TBank=LoadBank(url)
	If bank &lt;&gt; Null Then 
	   SaveBank bank,CurrentDir()+"\"+filePath$
         Return 1
      Else 
         Return -1
      EndIf
End Function
</textarea><br><br>Thanks<br>Grisu <br><br></td></tr></table><br>
<a name="1040209"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I dont' know how to 'hack' LoadBank but this is what I use<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Rem

		lib remote


		ask to a remote server for

		- size of a file 	(1)		FileSizeRemote(name)
		- copy a file		(2)		CopyFileRemote(source,dest,update_callback,buffersize)

		@ based on the works by
		
		(1) KEV
		(2) LEADWERKS

		


End Rem


Import maxgui.drivers

Global win:TGadget=			CreateWindow("",200,200,400,220,Null,WINDOW_TITLEBAR|WINDOW_CENTER)
Global label_main:tgadget = 		CreateLabel("Downloading..." , 10 , 10 , 390,46,win)
Global progbar:TGadget = 		CreateProgBar(10 , 60 , win.ClientWidth() - 20 , 28 , win,1) 

Global label_size:tgadget = 		CreateLabel("Size: " , 10 , win.ClientHeight()-25,80 , 20 , win)
Global label_position:tgadget = 	CreateLabel("Position" , 211 , win.ClientHeight()-25,80 ,20 , win) 
Global label_pos:tgadget = 		CreateLabel("" , 300,win.ClientHeight()-25, 40 , 16 , win) 
Global file_size:Int


Local file$="http://www.blitzbasic.com/file/get.php?file=/Products/demos/BlitzMaxDemo137.exe"
file_size = FileSizeRemote(file) 
SetUpInfo(file,file_size)

If CopyFileRemote(file$,"",DisplayDownloadProgress)
	Notify "Download successful."
Else
	Notify "Download failed."
EndIf

Function SetUpInfo(_name$ , _size:Int) 
	SetGadgetText label_main , "Downloading: " + _name + "..."
	SetGadgetText label_Size , "Size: " + Int(_size / 1024) + " Kb"
End Function

Function DisplayDownloadProgress(progress:Float)
	UpdateProgBar progbar , progress	
	SetGadgetText label_pos,Int(progress*100)+" %"
	If PeekEvent() WaitEvent() 
PollSystem
EndFunction


Function CheckFileRemote:Int(src$) 
	Local temp_src$
	temp_src$=Replace(src,"\","/")
	temp_src=Replace(temp_src,"://","::")
	Local size:Int = FileSizeRemote(src) 
	
	If size &lt; 0 
	'	DebugLog "---Size &lt;0"
		Return False
	End If
	Local stre:TStream = ReadStream(src)
	Local nogood:Int = 1 , line$
	Local trovato:Int
	If stre
		While Not Eof(stre) 
			line = ReadLine(stre)
				If Instr(Lower(line) , "&lt;html&gt;")&gt;0   
				nogood = - 1
			End If
		Wend
		CloseStream stre
	End If

	Return nogood
	
End Function


Function HTTPFindSource:String(url$)
	Local original$=url
	url=url.Replace("http://","")
	Local slashPos:Int = url$.Find("/"), host$, file$
	Local temp_url$
	
	If slashPos &lt;&gt; -1
		host$ = url$[..slashPos]
		file$ = url$[slashPos..]
	Else
		Return -1
	EndIf
'	DebugLog "HOST: "+host
	Local stream:TStream = OpenStream("tcp::" + host$)
	If Not stream 
	'	DebugLog "TCP - No stream"
		Return -1
	End If
	
	stream.WriteLine "HEAD " + file$ + " HTTP/1.0"
	stream.WriteLine "Host: " + host$
	stream.WriteLine ""
	
	While Not Eof(stream)
		Local in$ = stream.ReadLine()
		If in.find("Location:")&lt;&gt;-1
			temp_url=in[in.find(":")+1..].Trim()
			If FileSizeRemote(temp_url)&gt;0 Return temp_url
			temp_url=HTTPFindSource(temp_url)
			If FileSizeRemote(temp_url)&gt;0 Return temp_url
		End If
			
	Wend
	Return original

End Function


Function CopyFileRemote:Int(src$,dst$="",callback:Byte Ptr=Null,event_so:tgadget=Null,bufsize:Int=128,timeout:Int=5000)
	Local url$=src
	If src = "" Return False
	

		
	If filesizeRemote(src) &lt; 0	Return - 1

	src$=Replace(src,"\","/")
	src=Replace(src,"://","::")
	If dst = "" 
		CreateDir "download"
		dst = AppDir$ + "/download/" + StripDir(src)
	End If
	
	Local size:Int , In:TStream , out:TStream , pos:Int
	Local progress:Float
	Local timer:Int=MilliSecs()
	
	If callback = Null
			Return CopyFile(src , dst) 
	Else
	
		'Set up the callback function
		Local DownloadFileUpdateCallback(p#)
		DownloadFileUpdateCallback=callback
		
	
		'Get the file size
		size=FileSizeRemote(url)
		If size=-1 Return False
	
		'Open streams
		out:TStream=WriteFile(dst)
		If Not out Return False
		in:TStream=ReadStream(src)
		If Not in
			out.close()
		'	DebugLog "No stream..."
			Return False 
		EndIf
		
		'Copy file and display progress
		timer=MilliSecs()
		Local oldpos:Int
		Try
			Local buf:Byte[bufSize]
			While Not in.Eof()
				out.WriteBytes buf,in.Read(buf,bufSize)
				pos=pos+bufsize
				progress#=Float(pos)/Float(size)
				DownloadFileUpdateCallback progress
				
				If oldpos&lt;&gt;pos timer=MilliSecs()
		
				oldpos=pos
				PollSystem
					If EventID()=EVENT_GADGETACTION	
						If EventSource()=event_so
						'	DebugLog "CopyFile - OS event"
							EmitEvent CreateEvent( EVENT_GADGETACTION,event_so)
					
							Return False
						End If
					End If
								
				If MilliSecs()&gt;timer+timeout
				'	DebugLog "CopyFile - timeout!"
					Return False
				End If
			Wend
			Catch ex:TStreamWriteException
		EndTry

		'Close the streams
		in.close()
		out.close()
		
		'Verify file size
		If FileSize(dst)&lt;&gt;size
			DeleteFile dst
			Return False
		EndIf
		
		Return True
		
	EndIf
EndFunction

Function Round:Int(val#)
	Local dec#
	dec#=val-Floor(val)
	If dec&lt;0.5 Return Floor(val) Else Return Ceil(val)
EndFunction


Function FileSizeRemote:Int(url$,timeout:Int=5000) 
	url$ = url$.Replace("http://", "")
	Local slashPos:Int = url$.Find("/"), host$, file$
	
	If slashPos &lt;&gt; -1
		host$ = url$[..slashPos]
		file$ = url$[slashPos..]

	Else

		Return -1
	EndIf
	
	Local stream:TStream = OpenStream("tcp::" + host$)
	If Not stream
		Return -1
	End If
	
	stream.WriteLine "HEAD " + file$ + " HTTP/1.0"
	stream.WriteLine "Host: " + host$
	stream.WriteLine ""
	
	Local timer:Int=MilliSecs()
	
	While Not Eof(stream)
		Local in$ = stream.ReadLine()
		If in$.Find("Content-Length:") &lt;&gt; -1
			stream.Close()
			Return Int(in$[in$.Find(":") + 1..].Trim())
		EndIf	
		PollSystem
		If MilliSecs()&gt;timer+timeout
		'	DebugLog "TIMEOUT !!!!"
			Return -1
		End If
	Wend
	
	stream.Close()

	
	Return -1		
End Function

Type mystream Extends TStream
		
	Method ReadLine$() 
		Local timer:Int=MilliSecs()
		Local str$,buf:Byte[1024],p:Int
		Repeat
			Local n:Byte
			If Read( Varptr n,1 )&lt;&gt;1 Exit
			If n=0 Exit
			If n=10 Exit
			If n=13 Continue
			buf[p]=n ; p:+1
			If p&lt;&gt;buf.length Continue
			str:+String.FromBytes(buf,p)
			p = 0
			If MilliSecs()&gt;timer+10000 Exit
		Forever
		If p str:+String.FromBytes(buf,p)
		Return str
	End Method

End Type

</textarea> <br><br></td></tr></table><br>
<a name="1040212"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, I'm hacking something... but I have a problem to retrieve the 'correct' position of the stream, or well, the FULL SIZE of the object to download.<br><br>This is your example with a 'real' file.<br>Below there are some changes, but not fully* working at the moment<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Local file$="http://www.graphio.net/download/BlitzMaxCompanion.zip"

HTTPGetFile(file,"bmx.zip")

Function HTTPGetFile:Int(url$, filePath$)
	Url$ = url$.Replace("http://", "http::")
	Local bank:TBank=LoadBank(url,displayprogress)
	If bank &lt;&gt; Null Then 
	   SaveBank bank,CurrentDir()+"\"+filePath$
         Return 1
      Else 
         Return -1
      EndIf
End Function


Function DisplayProgress(progress:Float)
Print progress
End Function
</textarea><br><br><br>This is in <b>bank.bmx</b><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Function LoadBank:TBank( url:Object ,callback:Byte Ptr=NULL)
	Return TBank.Load( url,callback )
End Function

...


	Function Load:TBank( url:Object,callback:Byte Ptr=Null )
		Local stream:TStream=ReadStream( url )
		If Not stream Return
		Local data:Byte[]=LoadByteArray( stream,callback )
		Local bank:TBank=Create( data.length )
		MemCopy bank.Buf(),data,data.length 
		stream.Close
		Return bank
	End Function
</textarea><br><br>This is in <b>STREAM.bmx</b><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Function LoadByteArray:Byte[]( url:Object,callback:Byte Ptr=Null )
	Local stream:TStream=ReadStream( url )
	If Not stream Throw New TStreamReadException
	Local data:Byte[1024],size
	Global progress:Float
	
	Local DownloadFileUpdateCallback(p#)
	DownloadFileUpdateCallback=callback
	
	While Not stream.Eof()
		If size=data.length data=data[..size*3/2]
		size:+stream.Read( (Byte Ptr data)+size,data.length-size )
		progress#=Float(size)/Float(data.length)
 '&lt;---- I need help here...
		if callback DownloadFileUpdateCallback progress
	Wend
	stream.Close
	Return data[..size]
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1040214"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for your help degac.<br><br>I don't mess around with bmx core functions. <br>Have tried to create a single superstrict example of your code. But get a strange error message... :(<br> <br>[EDIT] deleted source code <br><br></td></tr></table><br>
<a name="1040234"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> You missed a <b>CreateBank</b><br>But still remains the problem about the file size...<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict 

Framework BRL.HTTPStream 
   Import BRL.System
   Import BRL.Threads
   Import BRL.StandardIO


Local file$="http://www.graphio.net/download/BlitzMaxCompanion.zip"

Print "downloading bmx.zip... please wait..."
HTTPGetFile(file,"bmx222.zip")

Function HTTPGetFile:Int(url$, filePath$)
	Url$ = url$.Replace("http://", "http::")
	Local bank:TBank=LoadBank2(url,displayprogress)
	If bank &lt;&gt; Null Then 
	   SaveBank bank,CurrentDir()+"\"+filePath$
         Return 1
      Else 
         Return -1
      EndIf
End Function


Function DisplayProgress(progress:Float)
Print String(progress:Float) 
End Function

Function LoadBank2:TBank( url:Object ,callback:Byte Ptr=Null)
	Return Load2( url,callback )
End Function


Function Load2:TBank( url:Object,callback:Byte Ptr=Null )
		Local stream:TStream=ReadStream( url )
		If Not stream Return Null 
		Local data:Byte[]=LoadByteArray2( stream,callback )
		Local bank:TBank=CreateBank(data.length ) '&lt;--- here
		MemCopy bank.Buf(),data,data.length 
		stream.Close
		Return bank
End Function
	
Function LoadByteArray2:Byte[]( url:Object,callback:Byte Ptr=Null )
Local stream:TStream=ReadStream( url )
If Not stream Throw New TStreamReadException
Local data:Byte[1024],size:Int
Global progress:Float
	
Local DownloadFileUpdateCallback(p#)
DownloadFileUpdateCallback=callback
	
While Not stream.Eof()
		If size=data.length data=data[..size*3/2]
		size:+stream.Read( (Byte Ptr data)+size,data.length-size )
		progress#=Float(size)/Float(data.length) '&lt;---- I need help here...
		If callback DownloadFileUpdateCallback progress
Wend
stream.Close
Return data[..size]
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1040242"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> This one uses a workaround function, perhaps someone finds a simpler solution?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict 

Framework BRL.HTTPStream 
   Import BRL.System
   Import BRL.Threads
   Import BRL.StandardIO


Global file$="http://www.graphio.net/download/BlitzMaxCompanion.zip"
Global file_size:Int = HTTPFileSize:Int(file$)

Print "downloading bmx.zip... "+file_size+"... please wait..."
HTTPGetFile(file,"bmx222.zip")

Function HTTPGetFile:Int(url$, filePath$)
	Url$ = url$.Replace("http://", "http::")
	Local bank:TBank=LoadBank2(url,displayprogress)
	If bank &lt;&gt; Null Then 
	   SaveBank bank,CurrentDir()+"\"+filePath$
         Return 1
      Else 
         Return -1
      EndIf
End Function


Function HTTPFileSize:Int(url$)
	url$ = url$.Replace("http://", "")
	Local slashPos:Int = url$.Find("/"), host$, file$
	
	If slashPos &lt;&gt; -1
		host$ = url$[..slashPos]
		file$ = url$[slashPos..]
	Else
		Return -1
	EndIf
	
	Local stream:TStream=OpenStream("tcp::" + host$, 80)
	If Not stream Then Return -1	
	
	stream.WriteLine "HEAD " + file$ + " HTTP/1.0"
	stream.WriteLine "Host: " + host$
	stream.WriteLine ""
	
	While Not Eof(stream)
		Local in$ = stream.ReadLine()

		If in$.Find("Content-Length:") &lt;&gt; -1
			stream.Close()
			Return Int(in$[in$.Find(":") + 1..].Trim())
		EndIf	
	Wend
	
	stream.Close()
	Return -1		
End Function

Function DisplayProgress(progress:Float)
Print String(progress:Float) 
End Function

Function LoadBank2:TBank( url:Object ,callback:Byte Ptr=Null)
	Return Load2( url,callback )
End Function


Function Load2:TBank( url:Object,callback:Byte Ptr=Null )
		Local stream:TStream=ReadStream( url )
		If Not stream Return Null 
		Local data:Byte[]=LoadByteArray2( stream,callback )
		Local bank:TBank=CreateBank(data.length ) '&lt;--- here
		MemCopy bank.Buf(),data,data.length 
		stream.Close
		Return bank
End Function
	
Function LoadByteArray2:Byte[]( url:Object,callback:Byte Ptr=Null )
Local stream:TStream=ReadStream( url )
If Not stream Throw New TStreamReadException
Local data:Byte[1024],size:Int
Global progress:Float
	
Local DownloadFileUpdateCallback(p#)
DownloadFileUpdateCallback=callback
	
While Not stream.Eof()
		If size=data.length data=data[..size*3/2]
		size:+stream.Read( (Byte Ptr data)+size,data.length-size )
		progress#=Float(size)/Float(file_size) '&lt;---- I need help here...
		If callback DownloadFileUpdateCallback progress
Wend
stream.Close
Return data[..size]
End Function
</textarea><br><br>[EDIT] Streamsize() doesn't apply here.. :( <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
