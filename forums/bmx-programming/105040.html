<!DOCTYPE html><html lang="en" ><head ><title >BlitzMax Long values, Lua, and C...</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >BlitzMax Long values, Lua, and C...</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >BlitzMax Long values, Lua, and C...</a><br><br>
<a name="1276874"></a>

<a name="1276875"></a>

<a name="1276876"></a>

<a name="1276877"></a>

<a name="1276878"></a>

<a name="1276879"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LT</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I apologize if this has been covered somewhere, but I just can't find it!  I've been using LuaJIT's FFI to wrap Blitz functions and have come across a problem with long values that I've been unable to solve thus far.<br><br>If I do this...<br><br>C code:<pre class=code>__declspec(dllexport) int64_t getalong() { return 20000000000L; }</pre><br>Lua code:<pre class=code>ffi.cdef[[
int64_t getalong();
]]

local LV = ffi.C.getalong()
local result = LV + 1LL
print( "Type: " .. type( result ) )
print( "Result: " .. tostring( result ) )
</pre><br>It works - giving me these results...<br><br>Type: cdata<br>Result: 20000000001LL<br><br>But if I do this instead...<br><br>C code:<pre class=code>int64_t bb_getalong();
__declspec(dllexport) int64_t getalong() { return bb_getalong(); }</pre><br>Where the BlitzMax extern looks like this...<br><br>Blitz code:<pre class=code>Extern "C"
  Function getalong:Long()
End Extern

Function getalong:Long()
  Return 20000000000:Long
End Function</pre><br>Using the same Lua code...it crashes and burns!<br><br>Does anyone know what I'm doing wrong here? <br><br></td></tr></table><br>
<a name="1276880"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Shouldn't you declare bb_getalong() in your C code as "extern" ? I'm pretty sure you also don't need "__declspec(dllexport)" in your C...<br>And you don't need to declare the Extern part in Max as that is where your actual function is.<br><br>Anyway, there are issues returning Long in BlitzMax, as far as external code is concerned. I would try passing it back by reference instead..<br><pre class=code>
void bb_getalong(int64_t *);
</pre> <br><br></td></tr></table><br>
<a name="1276881"></a>

<a name="1276882"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LT</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Shouldn't you declare bb_getalong() in your C code as "extern" ? <br></div>I am doing that - just didn't type it out.  Of course, it wouldn't have worked at all without that...  :p<br><div class="quote"> I'm pretty sure you also don't need "__declspec(dllexport)" in your C <br></div>The LuaJIT FFI doesn't work without it - don't know why.<br><br>Thanks for your suggestion, I will try that!  :) <br><br></td></tr></table><br>
<a name="1276883"></a>

<a name="1276884"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is something I have no experience with, but is that really the right syntax to export `getalong` from your Max file? It looks to me like that's <i>importing</i> a C definition and then immediately overriding it with a Max one. 'Cause I've seen code that looks like:<br><pre class=code>Function getalong:Long() "C"
    Return 200000000000:Long
End Function</pre><br>...floating around these forums on DLL-related topics before. <br><br></td></tr></table><br>
<a name="1276885"></a>

<a name="1276886"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LT</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well, I don't know of any other way to get the FFI to "find" it.  The Blitz function definition doesn't work, by itself.<br><br>EDIT: To tell the truth, I've no idea what that "C" does. <br><br></td></tr></table><br>
<a name="1276887"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> This looks like a working example of BlitzMax to C, with 32-bit integers.<br><br><a href="http://www.blitzbasic.com/Community/post.php?topic=83635&amp;post=943458" target="_blank">http://www.blitzbasic.com/Community/post.php?topic=83635&amp;post=943458</a> <br><br></td></tr></table><br>
<a name="1276888"></a>

<a name="1276889"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LT</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks, Floyd, but this is specifically a problem with longs(64-bit integers).  BlitzMax's handling of them is different.<br><br>---<br><br>Brucey, your suggestion worked for the example I presented above, but unfortunately not for my actual use case.  /sigh<br><br>What I'm really trying to do is create C functions that allow LuaJIT access to the various arrays (via FFI).  I have all of the array types working, except Long[].  This is what it looks like, using your suggestion.<br><br>C code:<br><pre class=code>void bb_getindexlong( BBArray *array, int index, int64_t *val );
__declspec(dllexport) int64_t getindexlong( BBArray *array, int index ) {
  int64_t *v;
  bb_getindexlong( array, index, v );
  return *v;
}</pre><br>Blitz code:<br><pre class=code>Function getindexlong( array:Long[], index:Int, val:Long Var )
  val = array[index]
End Function</pre><br>I can only assume that accessing the array is the cause of my problem now.  I'm not sure if this can be done without actually returning a de-referenced value. <br><br></td></tr></table><br>
<a name="1276890"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> How about...<br><pre class=code>
Function getindexlong( array:Long[], index:Int, val:Long Ptr )
  val[0] = array[index]
End Function
</pre> <br><br></td></tr></table><br>
<a name="1276891"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LT</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> How about... <br></div>Gives me an Exception Access Violation.  :/ <br><br></td></tr></table><br>
<a name="1276896"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> The only thing that comes to mind is that "BBArray *array" is not good...<br><br>Where does your BBArray* come from? <br><br></td></tr></table><br>
<a name="1276897"></a>

<a name="1276899"></a>

<a name="1276901"></a>

<a name="1276902"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Your C code is wrong. Pointer `v` is uninitialized. Should be:<br><br><pre class=code>__declspec(dllexport) int64_t getindexlong( BBArray *array, int index ) {
  int64_t v;
  bb_getindexlong( array, index, &amp;v );
  return v;
}</pre><br>One other idea: if your code is BBArray-aware, you're already including blitz.h, so why not solve your specific problem by inlining the functionality you need and dropping the requirement for a Max function at all? i.e.:<br><br><pre class=code>__declspec(dllexport) int64_t getindexlong( BBArray *array, int index ) {
  return ((int64_t *)BBARRAYDATA(array, array-&gt;dims))[index];
}</pre><br>(although I guess that rules out using bounds checks in debug mode) <br><br></td></tr></table><br>
<a name="1276898"></a>

<a name="1276900"></a>

<a name="1276903"></a>

<a name="1276904"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LT</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Where does your BBArray* come from? <br></div>It worked in the other cases and showed up looking correct in the debugger.<br><br><div class="quote"> Pointer 'v' is uninitialized. <br></div>Right you are.  Thanks, that fixed it!  :)<br><br>Thanks for your responses.  Looks like it's sorted out, now.<br><br>EDIT: <div class="quote"> One other idea <br></div>And it's a good one, thanks!  I've been trying to do as much as possible in Blitz, but certain functionality might be better off coming from C functions. <br><br></td></tr></table><br>
<a name="1276906"></a>

<a name="1276907"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LT</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> although I guess that rules out using bounds checks in debug mode <br></div>Not sure that matters because the FFI function is wrapped in a Lua function and that checks bounds.  :) <br><br></td></tr></table><br>
<a name="1277039"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LT</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Come to think of it, it would be nice to simply export Blitz functions without needing the C wrapper.  Is that possible?<br><br>LuaJIT requires __declspec(dllexport) - this comes straight from Mike Pall - but shouldn't that also work for bb_getindexlong?  Or am I just confused? <br><br></td></tr></table><br>
<a name="1277048"></a>

<a name="1277049"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> It won't work for the declaration of a BlitzMax function because it won't be true. If the BlitzMax function was built without the annotation (which it was because it's a completely different language), then the effects of said annotation haven't been applied to it. Adding it to an external declaration after the fact is effectively lying to the compiler about what it should expect to find at that function's code site. It doesn't go back and retroactively change the code to fit the external declaration! <br><br></td></tr></table><br>
<a name="1277050"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> So what does "__declspec(dllexport)" do to a C function on Windows, exactly? Is it an alignment thing or what? <br><br></td></tr></table><br>
<a name="1277052"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> After some reading, it is apparently a Microsoft extension to C: <a href="https://msdn.microsoft.com/en-us/library/dabb5z75(VS.80).aspx" target="_blank">https://msdn.microsoft.com/en-us/library/dabb5z75(VS.80).aspx</a><br><br>The dllexport means it is specifying how symbols are exported. Beyond that I don't know.<br><br>That reminds me, whatever happened with creating DLLs with BlitzMax? That was an experimental feature for a while. <br><br></td></tr></table><br>
<a name="1277055"></a>

<a name="1277056"></a>

<a name="1277057"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> It adds more information to the generated binary so that the function can be looked up by name without needing to use a second mapping file of some kind; instead, the name table is right there in the library. Being a JIT, LuaJIT links to functions at runtime and thus needs a table mapping names (as seen in the .h, <i>which it processes at runtime</i>) to the binary code.<br><br>BlitzMax does build in name information, but I don't know if it's in any way related to the __declspec semi-standard; there's no indication given anywhere that it is. <br><br></td></tr></table><br>
<a name="1277086"></a>

<a name="1278962"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> Its too bad blitzmax doesn export symbols in a standard way...<br><br>But you can build the export table yourself, include it in the execubable and then LuaJIT would find them. Or you could even LoadLibrary that same executable and use GetProcAddress.<br><br>I have a small tool i use for that exact purpose, it extracts marked functions from source code and builds either a .def file or an export table.<br>The export table is just some assembler that you can import in blitz.<br><br>Attached below if anyone needs it.<br><br>makedef.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Rem
	searches through source for functions marked with 'DLLEXPORT
	and stuffs them all in a DEF file or a fasm/pe export table
EndRem
Framework BRL.Blitz
Import BRL.Stream
Import BRL.StandardIO
Import BRL.FileSystem
Import BRL.Map

SuperStrict

Const T_GLOBAL:Int = 1
Const T_FUNCTION:Int = 2
Const T_METHOD:Int = 3
Const T_EXTERN_FUNCTION:Int = 4
Const T_EXTERN_GLOBAL:Int = 5
Const T_TYPE_FUNCTION:Int = 6

Const EXPORT_KEYWORD:String = "'DLLEXPORT"

Global filename:String
Global outfilename:String
Global basename:String
Global output:TStream

Global opt_gen_def:Int
Global opt_gen_table:Int
Global opt_list_syms:Int
Global opt_all_globals:Int
Global opt_all_functions:Int
Global opt_all_methods:Int
Global opt_allow_priv:Int
Global opt_allow_subfuncs:Int
Global opt_skip_includes:Int
Global opt_basename:String

If AppArgs.Length &lt;= 1 Then PrintUsage()
Local i:Int = 1
While i &lt; AppArgs.Length
	Local arg:String = AppArgs[i]
	Select arg
		Case "-h", "--help"
			PrintUsage()
		Case "-d", "--export-dll"
			opt_gen_def = True
		Case "-x", "--export-table"
			opt_gen_table = True
		Case "-l", "--list-symbols"
			opt_list_syms = True			
		Case "-a", "--all-symbols"
			opt_all_globals = True
			opt_all_functions = True
			opt_all_methods = True
		Case "-g", "--all-globals"
			opt_all_globals = True
		Case "-f", "--all-functions"
			opt_all_functions = True
		Case "-m", "--all-methods"
			opt_all_methods = True
		Case "-p", "--allow-private"
			opt_allow_priv = True
		Case "-h", "--allow-sub-functions"
			opt_allow_subfuncs = True
		Case "-s", "--skip-includes"
			opt_skip_includes = True
		Case "-b", "--basename"
			opt_basename = AppArgs[i + 1]
			i :+ 2
			Continue
		Default
			If arg.StartsWith("-") Or filename &lt;&gt; Null Then
				PrintError( "invalid arguments")
			Else
				arg = RealFile(arg)		
				If FileType(arg) &lt;&gt; FILETYPE_FILE Then
					PrintError( "file not found ~q" + arg + "~q")
				EndIf
				filename = arg
				basename = StripExt(StripDir(arg))
			EndIf
	EndSelect
	i :+ 1
Wend

' MAIN
Global remblock:Int
Global externs:Int
Global ispriv:Int
Global inbody:Int
Global outcount:Int = 0
Global exports:TMap = New TMap
Global lexports:TExport[]

If opt_basename Then basename = opt_basename

If Not ParseSource( filename) Then PrintError( "there were errors parsing the source files")
If opt_gen_table Then GEN_export_table()
If opt_gen_def Then GEN_library_def()
If opt_list_syms Then LIST_all_symbols()
' default
If (Not opt_gen_table) And (Not opt_gen_def) And (Not opt_list_syms) Then
	GEN_library_def()
EndIf

End


Type TExport
	Field Tag:Int
	Field Name:String		' name of function
	Field Label:String	' actual label name
	Field EAlias:String	' exported alias
	Field Ordinal:Int
EndType

Function WriteExport( tag:Int, name:String, _alias:String = Null, typename:String = Null)
	If Not _alias Then _alias = name
	Local e:TExport = New TExport	
	e.Tag = tag
	e.Name = name
	e.EAlias = _alias
	e.Ordinal = outcount
	exports.Insert(e.EAlias, e)
	lexports :+ [e]
	outcount :+ 1
	
	Select tag
		Case T_EXTERN_GLOBAL, T_EXTERN_FUNCTION		e.Label = "_" + name
		Case T_GLOBAL, T_FUNCTION							e.Label = "_bb_" + name
		Case T_METHOD, T_TYPE_FUNCTION					e.Label = "__bb_" + typename + "_" + name
		Default
			RuntimeError "invalid export type (" + tag + ")"
	EndSelect
EndFunction

Function GEN_library_def()
	outfilename = StripExt(filename) + ".def"
	output = WriteStream(outfilename)
	If Not output Then PrintError( "unable to create ~q" + outfilename + "~q")
	
	output.WriteLine "LIBRARY ~q" + basename + ".dll~q"
	output.WriteLine "EXPORTS"
	' write symbols
	For Local e:TExport = EachIn exports.Values()
		Local label:String = e.Label[1..] ' strip begining "_" for dlls
		If label = e.EAlias Then
			output.WriteLine "~t" + e.EAlias
		Else
			output.WriteLine "~t" + e.EAlias + "=" + label
		EndIf
	Next
	
	output.Close()
EndFunction

Function GEN_export_table()
	outfilename = StripExt(filename) + ".s"
	output = WriteStream(outfilename)
	If Not output Then PrintError( "unable to create ~q" + outfilename + "~q")

	output.WriteLine "format MS COFF"

	' write externs
	output.WriteLine Null
	For Local e:TExport = EachIn exports.Values()
		output.WriteLine "extrn " + e.Label
	Next
	output.WriteLine Null
	
	' write export header
	output.WriteLine "section '.reloc' data readable discardable ;fixups "
	output.WriteLine "section '.edata' data readable ;export"
	output.WriteLine "~tdd 0,0,0,RVA _lib_name,1"
	output.WriteLine "~tdd "+outcount +","+outcount +",RVA _address_table,RVA _name_table,RVA _ordinal_table"
	
	' write function table
	output.WriteLine "~n~t_address_table:"		
	For Local e:TExport = EachIn exports.Values()
		output.WriteLine "~t~tdd RVA " + e.Label
	Next
	
	' write name table
	output.WriteLine "~n~t_name_table:"
	Local i:Int = 1
	For Local e:TExport = EachIn exports.Values()
		output.WriteLine "~t~tdd RVA _name_" + i
		i :+ 1
	Next
	
	' write ordinal table
	output.WriteLine "~n~t_ordinal_table:"		
	If outcount &gt; 0 Then
		output.WriteString "~t~tdw "
		For i = 0 Until outcount
			If i &gt; 0 Then output.WriteString ", "
			output.WriteString i
		Next
		output.WriteString "~n"		
	EndIf
	
	' write string table
	output.WriteLine "~n~t_lib_name db '" + basename + ".exe',0"			
	i = 1
	For Local e:TExport = EachIn exports.Values()
		output.WriteLine "~t_name_" + i + " db '" + e.EAlias + "',0"
		i :+ 1
	Next

	output.Close()
EndFunction

Function LIST_all_symbols()
	For Local e:TExport = EachIn exports.Values()
		Local t:String
		Select e.Tag
			Case T_EXTERN_GLOBAL, T_GLOBAL
				t = "global"
			Case T_EXTERN_FUNCTION, T_FUNCTION, T_TYPE_FUNCTION
				t = "function"
			Case T_METHOD
				t = "method"
		EndSelect
		Local label:String = e.Label[1..] ' strip begining "_" for dlls
		WriteStdout t + " " + e.EAlias
		If label &lt;&gt; e.EAlias Then WriteStdout " " + label
		WriteStdout "~n"
	Next
EndFunction

Function ParseSource:Int( fn:String)
	Local stream:TStream = ReadStream( fn)
	If Not stream Then
		WriteStderr "** error reading input file: " + fn + "~n"
		Return False
	EndIf

	Local typename:String
	Local _alias:String
	
' read each line from input file
	Local lc:Int = 0
	While Not stream.Eof()
		Local i:Int
		Local ls:String, s:String
		lc :+ 1
		ls = stream.ReadLine().Trim()
		s = ls
		If s.Length &lt;= 0 Then Continue
		If s.StartsWith("'") Then Continue
' handle rem/endrem blocks
		If StartsWith( s, "Rem") Then
			If (s.Length &gt; 3) Then
				If ((s[3] = Asc("'")) Or (s[3] = Asc(" ")) Or (s[3] = Asc("~t"))) Then 
					remblock :+ 1
				EndIf
			Else
				remblock :+ 1
			EndIf
		ElseIf StartsWith( s, "EndRem") Then
			If (s.Length &gt; 6) Then
				If ((s[6] = Asc("'")) Or (s[6] = Asc(" ")) Or (s[6] = Asc("~t"))) Then 
					remblock :- 1
				EndIf
			Else
				remblock :- 1
			EndIf
		Else
			If remblock &lt;&gt; 0 Then Continue
' handle public / private
			If StartsWith( s, "Private") Then
				ispriv = True
			ElseIf StartsWith( s, "Public") Then
				ispriv  = False			
' handle extern blocks
			ElseIf StartsWith( s, "Extern") Then
				externs = True
			ElseIf StartsWith( s, "EndExtern") Or StartsWith( s, "End Extern") Then
				externs = False
			ElseIf StartsWith( s, "Global", True) And (HasDLLEXPORT( s, _alias) Or opt_all_globals) Then
				s = s[7..]
				Local name:String
				If externs Then
					' check for import name
					i = s.FindLast("=")
					If i &gt; s.FindLast(")") Then
						i = s.Find( "~q", i)
						If i &lt;&gt; -1 Then
							Local q:Int = s.Find( "~q", i + 1)
							If q &lt;&gt; -1 Then
								name = s[i+1..q]
							EndIf
						EndIf
					EndIf
				EndIf
				If Not name Then
					' get global name
					i = s.Find(":")				
					If i = -1 Then i = s.Find(" ")
					If i = -1 Then i = s.Find("~t")
					If i = -1 Then i = s.Find("=")
					If i &lt;&gt; -1 Then
						s = s[..i].Trim()
					EndIf
					name = s
				EndIf
				If Not name Then RuntimeError "error parsing global on line " + lc
				If ispriv And Not opt_allow_priv Then Continue
				If externs Then
					WriteExport( T_EXTERN_GLOBAL, name, _alias)
				Else
					WriteExport( T_GLOBAL, name, _alias)
				EndIf
			ElseIf StartsWith( s, "Function", True) Then
				If Not HasDLLEXPORT( s, _alias) And Not opt_all_functions Then
					If Not externs And Not HasKeyword( s, "Abstract") Then
						inbody :+ 1
					EndIf
					Continue
				EndIf 
				s = s[9..]
				Local name:String
				If externs Then
					' check for import name
					i = s.FindLast(")")
					i = s.Find( "=", i)
					If i &lt;&gt; -1 Then
						i = s.Find( "~q", i)
						If i &lt;&gt; -1 Then
							Local q:Int = s.Find( "~q", i + 1)
							If q &lt;&gt; -1 Then
								name = s[i+1..q]
							EndIf
						EndIf
					EndIf
				EndIf
				If Not name Then
					' get function name
					i = s.Find("(")
					If i &lt;&gt; -1 Then
						s = s[..i]
						i = s.Find( ":") 
						If i &lt;&gt; -1 Then s = s[..i]
						name = s
					EndIf
				EndIf
				If Not name Then RuntimeError "error parsing function on line " + lc
				inbody :+ 1
				If ispriv And Not opt_allow_priv Then Continue
				If (inbody &gt; 1) And Not opt_allow_subfuncs Then Continue
				If externs Then
					WriteExport( T_EXTERN_FUNCTION, name, _alias)
				Else
					If typename Then
						WriteExport( T_TYPE_FUNCTION, name, _alias, typename)
					Else
						WriteExport( T_FUNCTION, name, _alias)
					EndIf
				EndIf
			ElseIf StartsWith( s, "EndFunction") Or StartsWith( s, "End Function") Then
				inbody :- 1
			ElseIf StartsWith( s, "Type", True) Then
				s = s[5..].Trim()
				' get type name
				i = s.Find(" ")
				If i = -1 Then i = s.Find("~t")
				If i &lt;&gt; -1 Then s = s[..i].Trim()
				typename = s
			ElseIf StartsWith( s, "EndType") Or StartsWith( s, "End Type") Then
				typename = Null
			ElseIf StartsWith( s, "Method", True) Then
				If Not HasDLLEXPORT( s, _alias) And Not opt_all_methods Then
					If Not externs And Not HasKeyword( s, "Abstract") Then
						inbody :+ 1
					EndIf
					Continue
				EndIf
				s = s[7..]
				' get method name
				i = s.Find("(")
				If i &lt;&gt; -1 Then
					s = s[..i]
					Local i2:Int = s.Find( ":") 					
					If i2 &lt;&gt; -1 And i2 &lt; i Then s = s[..i2]
					inbody :+ 1
					If ispriv And Not opt_allow_priv Then Continue
					If externs Then Continue
					WriteExport( T_METHOD, s, _alias, typename)
				EndIf
			ElseIf StartsWith( s, "EndMethod") Or StartsWith( s, "End Method") Then
				inbody :- 1
			ElseIf StartsWith( s, "Include", True) And (Not opt_skip_includes) Then
				If remblock &lt;&gt; 0 Then Continue
				' parse include file
				Local fn:String = RealFile( s[9..s.Length-1])
				If Not fn Then PrintError( "unable to open include file ~q" + s[9..s.Length-1] + "~q")
				Local rems:Int = remblock
				If Not ParseSource( fn) Then
					stream.Close()
					Return False
				EndIf
				remblock = rems
			EndIf
		EndIf
	Wend
	stream.Close()
	Return True
EndFunction


' case insensitive version of String.StartsWith()
Function StartsWith:Int( s:String, val:String, needsdata:Int = False)
	If s[..val.Length].ToLower() = val.ToLower() Then
		If s.Length = val.Length Then
			Return Not needsdata
		EndIf
		Local ch:Int = s[val.Length]
		If ch = Asc(" ") Or ch = Asc("~t") Then Return True
	EndIf
EndFunction

' checks for ending keywords, like Abstract and Final
Function HasKeyword:Int( s:String, k:String)
	Local i:Int = s.Find("'")
	If i &lt;&gt; -1 Then s = s[..i]
	s = s.ToLower()
	k = k.ToLower()
	i = s.Find(k)
	If i &lt;&gt; -1 And i &gt; 0 Then
		Local ch:Int = s[i-1]
		If ch = Asc(" ") Or ch = Asc("~t") Then
			If i + k.Length &lt; s.Length Then
				ch = s[i + k.Length]
				If ch = Asc(" ") Or ch = Asc("~t") Then Return True
			Else
				Return True
			EndIf
		EndIf
	EndIf
EndFunction

' decodes lines ending with "'DLLEXPORT" or "'DLLEXPORT=name"
Function HasDLLEXPORT:Int( s:String, _alias:String Var)
	_alias = Null
	Local i:Int = s.Find(EXPORT_KEYWORD)
	If i = -1 Then Return False
	If s.Find( "'") &lt;&gt; i Then Return False
	If i + EXPORT_KEYWORD.Length = s.Length Then Return True
	Select s[i + EXPORT_KEYWORD.Length]
		Case Asc(" "), Asc("~t"), Asc("~r"), Asc("~n"), Asc("~0")
			Return True
		Case Asc("=")
			Local start:Int = i + EXPORT_KEYWORD.Length + 1
			Local n:Int = start
			While n &lt; s.Length
				Select s[n]
					Case Asc(" "), Asc("~t"), Asc("~r"), Asc("~n"), Asc("~0")
						Exit
				EndSelect
				n :+ 1
			Wend
			_alias = s[start..n].Trim()
			Return True
	EndSelect
	Return False
EndFunction

Function PrintUsage()
	Local fn:String = StripExt(StripDir(AppArgs[0]))
	Print "syntax: " + fn + " [-d | -x | -l | -a | -g | -f | -m | -p | -s | -b value] [filename.bmx]"
	Print "~t-d    --export-def~tgenerates a def file for dlls (DEFAULT)"
	Print "~t-x    --export-table~tgenerates an export table for ms coff (fasm)"
	Print "~t-l    --list-symbols~tlist all exported symbols on stdout"
	Print "~t-a    --all-symbols~texport all symbols (short for -g -f -m)"
	Print "~t-g    --all-globals~texport all globals"
	Print "~t-f    --all-functions~texport all functions"
	Print "~t-m    --all-methods~texport all methods"
	Print "~t-p    --allow-private~tallow exporting of private symbols (DEFAULT=off)"
	Print "~t-h    --allow-sub-functions~tallow exporting of functions within functions (DEFAULT=off)"
	Print "~t-s    --skip-includes~tdisables recursivly checking of includes"
	Print "~t-b v  --basename~toverride basename with supplied value"
	Print "~ndefault is to export only symbols marked with 'DLLEXPORT[=alias]"
	exit_(1)
EndFunction

Function PrintError( msg:String="unknown error")
	WriteStderr "*err: " + msg + "~n"
	exit_(2)
EndFunction

Function RealFile:String( fn:String)
	If Not fn Then Return Null
	If fn[1] = Asc(":") Or fn[0] = Asc("/") Or fn[0] = Asc("\") Then
		' absolute path
		Return RealPath(fn)
	EndIf
	If fn[0] = Asc(".") And (fn[1] = Asc("/") Or fn[1] = Asc("\")) Then
		' relative path
		Return RealPath(fn)
	EndIf
	' file name
	Local q:String = "/"
	Local t:String
	t = CurrentDir() + q + fn
	If FileType(t) = FILETYPE_FILE Then Return t
	t = LaunchDir + q + fn
	If FileType(t) = FILETYPE_FILE Then Return t
	t = AppDir + q + fn
	If FileType(t) = FILETYPE_FILE Then Return t
	Return RealPath(fn)
EndFunction
</textarea><br>EDIT: updated on 10.10.15 to fix some parsing bugs relating with "type" and handling of sub functions. <br><br></td></tr></table><br>
<a name="1277100"></a>

<a name="1277102"></a>

<a name="1277103"></a>

<a name="1277104"></a>

<a name="1277105"></a>

<a name="1277106"></a>

<a name="1277108"></a>

<a name="1277154"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LT</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hmm, looks interesting, but <strike>I was unable to get it working.  Lua is still not finding the symbols</strike>.<br><br>EDIT: Nevermind, it works.  Thanks!  I was expecting to see a slight speed increase, however, and that didn't happen.  Oh, well... <br><br></td></tr></table><br>
<a name="1277228"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> What advantage are you expecting to see from integrating with FFI ?<br>I thought you couldn't mix FFI with Lua/C ?  In which case how are you taking a BlitzMax-made Object and using it in FFI?<br><br>:o) <br><br></td></tr></table><br>
<a name="1277229"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LT</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> FFI is much faster*.  You can access C structs directly from Lua and the JIT inlines all of it.  Until recently, I've been doing things with the C-API.  I was aware of FFI, but not its full benefits until I did some digging on the LuaJIT mailing list, where I was promptly informed that I was doing it wrong.  :/<br><br>You're right, you can't mix FFI with Lua/C and that's not what I'm doing.  I have an export program that takes a bunch of Blitzmax function definitions and generates glue code - similar to Lugi, but I couldn't use it because I needed a lot more functionality.  My glue code used to generate <i>Function( luastate:Byte Ptr )</i> and now it generates C declarations, among other things.<br><br>A CData can have metatables attached to it, same as userdata.  But unlike userdata, its metatable cannot be altered after it's attached - on account of the JIT compiler.<br><br>*Faster - UNLESS you're running on iOS, which has security precautions that prevent JIT from functioning.  Also, both JIT and FFI are turned off on consoles, but that bridge is a long way off... <br><br></td></tr></table><br>
<a name="1277232"></a>

<a name="1277233"></a>

<a name="1277234"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LT</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#23">[#23]</a></td></tr></table></td></tr><tr ><td class="posttext"> To tell the truth, I really wish that Lua had some kind of static-typed companion language (other than C) that complimented its strengths and also allowed for cross-platform, native compilation to match LuaJIT.  Lua's metatable stuff allows for easy creation of project-specific languages.  You can hand off a custom language to a designer that knows what you mean when you type "local actor1 = RomanSoldier + sword" - how cool is that!? <br><br></td></tr></table><br>
<a name="1277248"></a>

<a name="1277249"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#24">[#24]</a></td></tr></table></td></tr><tr ><td class="posttext"> The problem with Lua is that you can pick two: fast, ahead-of-time, full table+metatable support.<br><br>LuaJIT gets a large part of its performance by making site-specific optimizations, so that table lookups become (after a few runs through a hot path) as fast as a C struct field (at least where they're used that way, anyway) and code handling numbers gets reduced down to native math opcodes. You can't do this ahead-of-time because the whole design of tables means you don't have enough information: offsets can (and do) change all the time, objects of different layouts can pass through the same code block, and objects can have different metatable features or change them or whatever. The same piece of code <i>will</i> change its meaning as the program runs.<br><br>You can compile the functions to C functions, the loops to C loops, the ifs to C ifs, etc., but that will only take you a small part of the way. With very few (trivial) exceptions, you won't get math or table accesses any faster than in the interpreter. <br><br></td></tr></table><br>
<a name="1277251"></a>

<a name="1277254"></a>

<a name="1277328"></a>

<a name="1277329"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LT</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#25">[#25]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> offsets can (and do) change all the time <br></div>Yes, because it's dynamic.<br><br><div class="quote"> fast, ahead-of-time, full table+metatable support <br></div>It occurs to me that some kind of metatable locking mechanism could be useful.  So I'm willing to give up full metatable support for cases where I need to speed up the lookups, if that was an option. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
