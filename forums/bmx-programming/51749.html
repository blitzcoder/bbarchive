<!DOCTYPE html><html lang="en" ><head ><title >Circle drawing</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Circle drawing</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Circle drawing</a><br><br>
<a name="577727"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's some code to draw circles one pixel at a time. It uses entirely integer math, so the radius is also an integer.<br><br><pre class=code>
'Midpoint Circle algorithm

Strict
Graphics 640,480,0

Local xCenter:Int=320
Local yCenter:Int=240
Local radius:Int
Local p,x,y:Int
Repeat
	Cls
	If MouseDown(1)
		xCenter=MouseX()
		yCenter=MouseY()
	EndIf
	radius=Abs(xCenter-MouseX())
	x=0
	y=radius
	Plot xCenter+x,yCenter+y
	Plot xCenter-x,yCenter+y
	Plot xCenter+x,yCenter-y
	Plot xCenter-x,yCenter-y
	Plot xCenter+y,yCenter+x
	Plot xCenter-y,yCenter+x
	Plot xCenter+y,yCenter-x
	Plot xCenter-y,yCenter-x
	p=1-radius
	While x&lt;y
		If p&lt;0
			x:+1
		Else
			x:+1
			y:-1
		EndIf
		If p&lt;0
			p=p+(x Shl 1)+1
		Else
			p=p+((x-y) Shl 1)+1
		EndIf
		Plot xCenter+x,yCenter+y
		Plot xCenter-x,yCenter+y
		Plot xCenter+x,yCenter-y
		Plot xCenter-x,yCenter-y
		Plot xCenter+y,yCenter+x
		Plot xCenter-y,yCenter+x
		Plot xCenter+y,yCenter-x
		Plot xCenter-y,yCenter-x
	Wend
	Flip
Until KeyHit(KEY_ESCAPE)
End
</pre> <br><br></td></tr></table><br>
<a name="577737"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> And here is the routine to draw an ellipse. It's a bit more complicated as you can see. Again it uses only integer math/variables. However, there are two lines that require a floating point calculation and a rounding operation. The ellipse itself, as with the circle, does not use any trigonometry. Currently this routine does not support rotation, but if you are plotting the pixels with Max2D or direct OpenGl/DirectX, maybe you can draw vertices/pixels whose position are influenced by rotation, e.g. rotating the camera or something. As it stands, the routine draws a non-rotated ellipse and if you were to rotate the same coordinates they might not produce an accurate or smooth ellipse when rotated.<br><pre class=code>
'Midpoint ellipse algorithm

Strict
Graphics 640,480,0

Local xCenter:Int=320
Local yCenter:Int=240
Local Rx,Ry:Int
Local p,px,py,x,y:Int
Local Rx2,Ry2,twoRx2,twoRy2:Int
Local pFloat:Float
Repeat
	Cls
	If MouseDown(1)
		xCenter=MouseX()
		yCenter=MouseY()
	EndIf
	Rx=Abs(xCenter-MouseX())
	Ry=Abs(yCenter-MouseY())
	DrawText String(Rx)+" x "+String(Ry),20,20
	Rx2=Rx*Rx
	Ry2=Ry*Ry
	twoRx2=Rx2 Shl 1
	twoRy2=Ry2 Shl 1
	'Region 1
	x=0
	y=Ry
	Plot xCenter+x,yCenter+y
	Plot xCenter-x,yCenter+y
	Plot xCenter+x,yCenter-y
	Plot xCenter-x,yCenter-y
	pFloat=(Ry2-(Rx2*Ry))+(0.25*Rx2)
	p=Int(pFloat)
	If pFloat Mod 1.0&gt;=0.5 Then p:+1
	px=0
	py=twoRx2*y
	While px&lt;py
		x:+1
		px:+twoRy2
		If p&gt;=0
			y:-1
			py:-twoRx2
		EndIf
		If p&lt;0 Then p:+Ry2+px Else p:+Ry2+px-py
		Plot xCenter+x,yCenter+y
		Plot xCenter-x,yCenter+y
		Plot xCenter+x,yCenter-y
		Plot xCenter-x,yCenter-y
	Wend
	'Region 2
	pFloat=(Ry2*(x+0.5)*(x+0.5))+(Rx2*(y-1.0)*(y-1.0))-(Rx2*(Float(Ry2)))
	p=Int(pFloat)
	If pFloat Mod 1.0&gt;=0.5 Then p:+1
	While y&gt;0
		y:-1
		py:-twoRx2
		If p&lt;=0
			x:+1
			px:+twoRy2
		EndIf
		If p&gt;0 Then p:+Rx2-py Else p:+Rx2-py+px
		Plot xCenter+x,yCenter+y
		Plot xCenter-x,yCenter+y
		Plot xCenter+x,yCenter-y
		Plot xCenter-x,yCenter-y
	Wend
	Flip
Until KeyHit(KEY_ESCAPE)
End
</pre> <br><br></td></tr></table><br>
<a name="577739"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> UPDATED:<br><br>Here is the (now updated) circle routine, with a modification to fill the circle, making sure to draw each row only once. In this example it draws a filled circle and also draws a hollow one on top. You can comment out either the DrawRects or the Plots, and you'll see each circle by itself. Because it's drawing both parts simulateneously some pixels appear to go missing.<br><pre class=code>
'Midpoint Circle algorithm

Strict
Graphics 640,480,0

Local xCenter:Int=320
Local yCenter:Int=240
Local radius:Int
Local p,x,y,prevy:Int
Repeat
	Cls
	If MouseDown(1)
		xCenter=MouseX()
		yCenter=MouseY()
	EndIf
	radius=Abs(xCenter-MouseX())
	x=0
	y=radius
	SetColor $FF,$88,$00
	DrawRect xCenter-y,yCenter+x,y Shl 1,1
	SetColor $FF,$FF,$FF
	Plot xCenter+x,yCenter+y
	Plot xCenter-x,yCenter+y
	Plot xCenter+x,yCenter-y
	Plot xCenter-x,yCenter-y
	Plot xCenter+y,yCenter+x
	Plot xCenter-y,yCenter+x
	Plot xCenter+y,yCenter-x
	Plot xCenter-y,yCenter-x
	p=1-radius
	While x&lt;y-1
		prevy=y
		If p&lt;0
			x:+1
		Else
			x:+1
			y:-1
		EndIf
		If p&lt;0
			p=p+(x Shl 1)+1
		Else
			p=p+((x-y) Shl 1)+1
		EndIf
		If y&lt;prevy And x&lt;y
			SetColor $FF,$88,$00
			DrawRect xCenter-x,yCenter+y,x Shl 1,1
			DrawRect xCenter-x,yCenter-y,x Shl 1,1
			SetColor $FF,$FF,$FF
			Plot xCenter+x,yCenter+y
			Plot xCenter-x,yCenter+y
			Plot xCenter+x,yCenter-y
			Plot xCenter-x,yCenter-y
		EndIf
		SetColor $FF,$88,$00
		DrawRect xCenter-y,yCenter+x,y Shl 1,1
		DrawRect xCenter-y,yCenter-x,y Shl 1,1
		SetColor $FF,$FF,$FF
		Plot xCenter+y,yCenter+x
		Plot xCenter-y,yCenter+x
		Plot xCenter+y,yCenter-x
		Plot xCenter-y,yCenter-x
	Wend
	Flip
Until KeyHit(KEY_ESCAPE)
End
</pre> <br><br></td></tr></table><br>
<a name="577740"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> UPDATED:<br><br>Here is the ellipse routine which now draws a filled ellipse (at the same time as a hollow one), making sure to only draw each row once. Again you can either take out the Plots or the DrawRects to see either the filled or unfilled ellipse by itself. Since they are drawn simultaneously, some pixels appear to go missing. Removing the Plots or the DrawRects lets the full filled or unfilled ellipse be drawn in entirity without trashing the other one.<br><br><pre class=code>
'Midpoint ellipse algorithm

Strict
Graphics 640,480,0

Local xCenter:Int=320
Local yCenter:Int=240
Local Rx,Ry:Int
Local p,px,py,x,y,prevy:Int
Local Rx2,Ry2,twoRx2,twoRy2:Int
Local pFloat:Float
Repeat
	Cls
	If MouseDown(1)
		xCenter=MouseX()
		yCenter=MouseY()
	EndIf
	Rx=Abs(xCenter-MouseX())
	Ry=Abs(yCenter-MouseY())
	DrawText String(Rx)+" x "+String(Ry),20,20
	Rx2=Rx*Rx
	Ry2=Ry*Ry
	twoRx2=Rx2 Shl 1
	twoRy2=Ry2 Shl 1
	'Region 1
	x=0
	y=Ry
	SetColor $FF,$88,$00
	DrawRect xCenter-Rx,yCenter,Rx Shl 1,1
	SetColor $FF,$FF,$FF
	Plot xCenter+Rx,yCenter
	Plot xCenter-Rx,yCenter
	Plot xCenter,yCenter-Ry
	Plot xCenter,yCenter+Ry
	pFloat=(Ry2-(Rx2*Ry))+(0.25*Rx2)
	p=Int(pFloat + (Sgn(pFloat)*0.5))
	px=0
	py=twoRx2*y
	While px&lt;py-1
		prevy=y
		x:+1
		px:+twoRy2
		If p&gt;=0
			y:-1
			py:-twoRx2
		EndIf
		If p&lt;0 Then p:+Ry2+px Else p:+Ry2+px-py
		If y&lt;prevy And px&lt;py-1
			SetColor $FF,$88,$00
			DrawRect xCenter-x,yCenter+y,x Shl 1,1
			DrawRect xCenter-x,yCenter-y,x Shl 1,1
			SetColor $FF,$FF,$FF
			Plot xCenter+x,yCenter+y
			Plot xCenter-x,yCenter+y
			Plot xCenter+x,yCenter-y
			Plot xCenter-x,yCenter-y
		EndIf
	Wend
	'Region 2
	pFloat=(Ry2*(x+0.5)*(x+0.5))+(Rx2*(y-1.0)*(y-1.0))-(Rx2*(Float(Ry2)))
	p=Int(pFloat + (Sgn(pFloat)*0.5))
	y:+1
	While y&gt;1
		y:-1
		py:-twoRx2
		If p&lt;=0
			x:+1
			px:+twoRy2
		EndIf
		If p&gt;0 Then p:+Rx2-py Else p:+Rx2-py+px
		SetColor $FF,$88,$00
		DrawRect xCenter-x,yCenter+y,x Shl 1,1
		DrawRect xCenter-x,yCenter-y,x Shl 1,1
		SetColor $FF,$FF,$FF
		Plot xCenter+x,yCenter+y
		Plot xCenter-x,yCenter+y
		Plot xCenter+x,yCenter-y
		Plot xCenter-x,yCenter-y
		Wend
	Flip
Until KeyHit(KEY_ESCAPE)
End
</pre> <br><br></td></tr></table><br>
<a name="577743"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Added all four to the BlitzMax code archives. :-) <br><br></td></tr></table><br>
<a name="577802"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yan</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hoorar for Mr Bresenham! <br><br></td></tr></table><br>
<a name="577895"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Indeed. What I just realized though is that the filled routines are doing some work that doesn't need to be done. The ellipse and circle are drawn first of all from the top and the bottom towards the middle, then the middle section is filled in. The middle part is correct, but because some of the top and bottom quarters have more than one pixel on a horizontal row, drawing rectangles for every one of those pixels is filling the row more times than is needed. What should better happen is the routine should only draw the first rectangle/row for the first pixel that it gets to on a new line, when the Y coord changes, rather than for every movement. Then it would only draw one row for each which would cut out a bunch of graphics operations and speed it up.<br><br>For the circle, those instructions are the last four Plots or the last two DrawRects of each group. For the ellipse, it's anything drawn in `region 1` of the code. <br><br></td></tr></table><br>
<a name="578121"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Since I realized that the fill was drawing a lot of the lines more than once, where there are more than one pixel horizontally on a line (if it were hollow), I have made modifications to the above circle and ellipse programs (and re-uploaded) to now do it correctly.<br><br>Each row to be fiilled is only drawn once. So this now draws correctly when SetBlend LIGHTBLEND or some other mode is used. As a result it is also faster. <br><br></td></tr></table><br>
<a name="1255458"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BLaBZ</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> I needed to use this to write to a pixmap instead of using Max2d - Here's the equivalent<br><br><pre class=code>
'Midpoint ellipse algorithm

SuperStrict
Graphics 640,480,0


Local pix:TPixmap = CreatePixmap(400,400,PF_RGBA8888)
ClearPixels(pix)

Repeat
	Cls
	SetColor(255,0,0)
	DrawOvalPixmap(pix,100,100,200,400)
	DrawPixmap(pix,0,0)
	Flip
	WaitKey()
	Cls
	DrawOval(100,100,200,400)
	Flip
	WaitKey()
	
Until KeyHit(KEY_ESCAPE)
End

Function DrawOvalPixmap(Pixmap:TPixmap,x:Int,y:Int,w:Int,h:Int)
	Local Rx:Int = (w / 2.0)
	Local Ry:Int = (h / 2.0)
	
	Local p:Int,px:Int,py:Int,prevy:Int
	Local Rx2:Int,Ry2:Int,twoRx2:Int,twoRy2:Int
	Local xCenter:Int= x + (w/2.0) '320
	Local yCenter:Int= y + (h/2.0)'240
	Local pFloat:Float
	
	Local r:Int,g:Int,b:Int
	GetColor(r,g,b)

	Rx2=Rx*Rx
	Ry2=Ry*Ry
	twoRx2=Rx2 Shl 1
	twoRy2=Ry2 Shl 1
	'Region 1
	x=0
	y=Ry
	DrawRectPixmap Pixmap,xCenter-Rx,yCenter,Rx Shl 1,1,r,g,b
	pFloat=(Ry2-(Rx2*Ry))+(0.25*Rx2)
	p=Int(pFloat + (Sgn(pFloat)*0.5))
	px=0
	py=twoRx2*y
	While px&lt;py-1
		prevy=y
		x:+1
		px:+twoRy2
		If p&gt;=0
			y:-1
			py:-twoRx2
		EndIf
		If p&lt;0 Then p:+Ry2+px Else p:+Ry2+px-py
		If y&lt;prevy And px&lt;py-1
			DrawRectPixmap Pixmap,xCenter-x,yCenter+y,x Shl 1,1,r,g,b
			DrawRectPixmap Pixmap,xCenter-x,yCenter-y,x Shl 1,1,r,g,b
		EndIf
	Wend
	'Region 2
	pFloat=(Ry2*(x+0.5)*(x+0.5))+(Rx2*(y-1.0)*(y-1.0))-(Rx2*(Float(Ry2)))
	p=Int(pFloat + (Sgn(pFloat)*0.5))
	y:+1
	While y&gt;1
		y:-1
		py:-twoRx2
		If p&lt;=0
			x:+1
			px:+twoRy2
		EndIf
		If p&gt;0 Then p:+Rx2-py Else p:+Rx2-py+px
		DrawRectPixmap Pixmap,xCenter-x,yCenter+y,x Shl 1,1,r,g,b
		DrawRectPixmap Pixmap,xCenter-x,yCenter-y,x Shl 1,1,r,g,b
	Wend
End Function

Function DrawRectPixmap(Pixmap:TPixmap,x:Int,y:Int,w:Int,h:Int,r:Int,g:Int,b:Int)
	For Local x1:Int = x Until x+w
		For Local y1:Int = y Until y+h
			If x1 &lt; Pixmap.width And y1 &lt; Pixmap.height
				WritePixel(Pixmap,x1,y1,GenRGB(r,g,b))
			EndIf 
		Next
	Next
End Function

Function GenRgb:Int(red:Int, green:Int, blue:Int, alpha:Int = 255)
	Local RGB:Int = (Alpha Shl 24) | (Red Shl 16) | (Green Shl 8) | Blue
	Return RGB
End Function
</pre> <br><br></td></tr></table><br>
<a name="1255847"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Scaremonger</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> It is a sad thing that BlitzMax does not have native functions for non-filled circles, rectangles and polygons.<br><br>I did some timings of the MidpointCircle() function against drawOval() and a couple of functions in my library with the following results:<br><br>DrawOval():       FILLED      147 ms<br>DrawCircle():     NON-FILLED  5178 ms<br>DrawPolygon():    NON-FILLED  18668 ms<br>FillPolygon():    FILLED      1452 ms<br>MidpointCircle(): NON-FILLED  14715 ms<br><br>Obviously DrawOval() and FillPolygon() are not an exact test as this is a filled circle rather than an outline, but useful for comparison.<br><br>The test uses 5 iteration of circles drawn with a radius of between 5 and 360 and is listed below. It does not display anything on the screen so you need to watch the output tab.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
Graphics 640,480,0

Const AMOUNT% = 5		'# REPEAT COUNT FOR ALL TESTING

Global timer%, radius%, counter%

'# Draw using Max2D
Print "DrawOval(): FILLED"
timer = MilliSecs()
For counter = 1 To AMOUNT
	For radius = 10 To 360
		DrawOval( 320, 240, radius, radius )
	Next
Next
Print "  " + (MilliSecs() - timer) + " ms"

'# Draw using Floats
Print "DrawCircle(): NON-FILLED"
timer = MilliSecs()
For counter = 1 To AMOUNT
	For radius = 10 To 360
		DrawCircle( 320, 240, radius )
	Next
Next
Print "  " + (MilliSecs() - timer) + " ms"

'# Draw circle using polygon function (360 points)
Print "DrawPolygon(): NON-FILLED"
timer = MilliSecs()
For counter = 1 To AMOUNT
	For radius = 10 To 360
		DrawPolygon( 320, 240, radius, 360 )
	Next
Next
Print "  " + (MilliSecs() - timer) + " ms"

'# Draw circle using filled polygon function (360 points)
Print "FillPolygon(): FILLED"
timer = MilliSecs()
For counter = 1 To AMOUNT
	For radius = 10 To 360
		FillPolygon( 320, 240, radius, 360 )
	Next
Next
Print "  " + (MilliSecs() - timer) + " ms"

'# Draw Using MidpointCircle
Print "MidpointCircle(): NON-FILLED"
timer = MilliSecs()
For counter = 1 To AMOUNT
	For radius = 10 To 360
		MidpointCircle( 320, 240, radius )
	Next
Next
Print "  " + (MilliSecs() - timer) + " ms"

End

'--------------------------------------------------
'# Draw a non-filled circle using points
Function drawCircle( x:Float, y:Float, radius:Float, Steps%=1 )
Local px:Float, py:Float
Local a:Int = 1
	Repeat
		px = x + Sin(a)*radius
		py = y + Cos(a)*radius
		Plot( px,py)
		a :+ steps
	Until a&gt;360
End Function

'--------------------------------------------------
'# Draw a non-filled polygon using lines
Function drawPolygon( x:Float, y:Float, radius:Float, points%=-1 )
Local px:Float, py:Float, lx:Float, ly:Float, a:Float
	'# Calculate number of steps
	If points &lt;= 0 Or points &gt; 360 Then points = 360
	a = 360/Float(points)
	'# Draw the polygon
	For Local p% = 0 To points
		px = x + Sin(p*a)*radius
		py = y + Cos(p*a)*radius
		If p&gt;0 Then	DrawLine( lx, ly, px, py )
		'# Save last position drawn
		lx = px
		ly = py
	Next
End Function

'--------------------------------------------------
'# Draw a filled polygon
Function FillPolygon( x:Float, y:Float, radius:Float, points%=-1 )
Local shape#[], a:Float
	'# Calculate number of steps
	If points &lt;= 0 Or points &gt; 360 Then points = 360
	a = 360/Float(points)
	'# Draw the polygon
	For Local p% = 0 To points
		shape :+ [Float(x + Sin(p*a)*radius)]
		shape :+ [Float(y + Cos(p*a)*radius)]
	Next
	DrawPoly shape	
End Function

'--------------------------------------------------
'Midpoint Circle algorithm
Function MidpointCircle( xCenter%, yCenter%, radius% )
Local p%,x%,y:Int
	x=0
	y=radius
	Plot xCenter+x,yCenter+y
	Plot xCenter-x,yCenter+y
	Plot xCenter+x,yCenter-y
	Plot xCenter-x,yCenter-y
	Plot xCenter+y,yCenter+x
	Plot xCenter-y,yCenter+x
	Plot xCenter+y,yCenter-x
	Plot xCenter-y,yCenter-x
	p=1-radius
	While x&lt;y
		If p&lt;0
			x:+1
		Else
			x:+1
			y:-1
		EndIf
		If p&lt;0
			p=p+(x Shl 1)+1
		Else
			p=p+((x-y) Shl 1)+1
		EndIf
		Plot xCenter+x,yCenter+y
		Plot xCenter-x,yCenter+y
		Plot xCenter+x,yCenter-y
		Plot xCenter-x,yCenter-y
		Plot xCenter+y,yCenter+x
		Plot xCenter-y,yCenter+x
		Plot xCenter+y,yCenter-x
		Plot xCenter-y,yCenter-x
	Wend
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1256404"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> I placed code in the code archives for bmx to do circles/ellipses filled or unfilled. <br><br></td></tr></table><br>
<a name="1256484"></a>

<a name="1256485"></a>

<a name="1256486"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hardcoal</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> tnx for your contribution I.H<br><br>Im gonna make in a blitzmax with mini b3d<br>a Draw oval or any polygone while actualy will create a mesh <br>so its gonna be ultra fast..<br><br>amen <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
