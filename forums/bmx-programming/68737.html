<!DOCTYPE html><html lang="en" ><head ><title >Fix Your Timestep!</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Fix Your Timestep!</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Fix Your Timestep!</a><br><br>
<a name="768245"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have been trying to convert <a href="http://www.gaffer.org/game-physics/fix-your-timestep/" target="_blank"> this </a> article to Bmax because I have never used Fixed Logic timing before.<br>Unfortunately, I'm finding some of the article incoherent so might be missing something. My code doesn't run very smoothly. <br>First, here's what I understand the example code to be doing with my own comments<br><pre class=code>

    float t = 0.0f;    * Guess this is gametime but don't quite know what it's used for.
    const float dt = 0.01f; * Timestep in seconds? 

    float currentTime = 0.0f; * OK.
    float accumulator = 0.0f; * OK.

    State previous; * Guess in Bmax you have to keep the old positions.
    State current; * and this is the new position.

    while (!quit)
    {
         float newTime = time();  *Guess time() is millisecs()
         float deltaTime = newTime - currentTime; * OK with this..
*** Needed some clamp on deltatime here I assume.
         currentTime = newTime; * OK.

         accumulator += deltaTime;  * OK

         while (accumulator&gt;=dt)
         {
              previousState = currentState;   * old_xpos=new_xpos etc?
              integrate(currentState, t, dt); * no idea 
what this really means but assumed you pass 
your update methods/functions the timestep value. 
Not sure what you use 't' for.
              t += dt;  * Keep a count of 't' but not sure why.
              accumulator -= dt;  * OK.
         }

         const float alpha = accumulator / dt;  * OK

         State state = currentState*alpha + previousState*(1.0f-alpha);   * OK.

         render(state);  *OK
    }
</pre><br>Here is the code I came up with :<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
Type ttest1
	Global list:tlist=CreateList()
	Field x:Float = 0.0
	Field y:Float = 100.0
	Field xvel:Float=1.0
	Field previous_x:Float = 0.0
	
	Method New()
		ListAddLast list , Self
	End Method
	
	Function Draw(alpha:Float)
'standard draw without interpolation for comparison.
		For Local all:ttest1 = EachIn list
			DrawOval all.x , all.y , 8 , 8
		Next
	End Function
	
	Function update() 
		For Local all:ttest1=EachIn list
			all.previous_x=all.x	
			all.x=(all.x+all.xvel) Mod GraphicsWidth()
		Next
	End Function
	
End Type

Type ttest2
	Global list:tlist=CreateList()
	Field x:Float = 0.0
	Field y:Float = 200.0
	Field xvel:Float=1.0
	Field previous_x:Float = 0.0
	
	Method New()
		ListAddLast list , Self
	End Method
	
	Function Draw(alpha:Float)
'interpolated draw
		For Local all:ttest2 = EachIn list
			Local xpos:Float = (all.x * alpha) + (all.previous_x * (1.0 - alpha))
			DrawOval xpos , all.y , 8 , 8
		Next
	End Function
	
	Function update(timestep:Float)
		For Local all:ttest2=EachIn list
' saving previous x for interpolation.
			all.previous_x=all.x	
			all.x=(all.x+all.xvel) Mod GraphicsWidth()
		Next
	End Function
	
End Type

Graphics 800 , 600
Local cycletime:Float = 0.0
Const timestep:Float = 10.0   ' run 10ms timesteps?
Local accumulator:Float = 0.0
Local t1:ttest1 = New ttest1
Local t2:ttest2 = New ttest2
Local Current_Time:Float = MilliSecs()

While Not KeyHit(KEY_ESCAPE)
	Local newtime:Float = MilliSecs()
	Local deltatime:Float = newtime - current_time
'clamp deltatime.
	If deltatime &gt; 300.0 deltatime = 300.0
	current_time = newtime
	accumulator:+ deltatime
	While accumulator &gt;= timestep
		update_game(timestep)
		cycletime:+ timestep
		accumulator:- timestep
	Wend
'I have called it cycletime but this is 't' in the article.
'Anybody know what it's for? I zero it to stop it incrementing
'	cycletime=0.0
	Local alpha:Float = accumulator / timestep
	render(alpha)
Wend

Function render(alpha:Float)
	Cls
	ttest1.draw(alpha)
	ttest2.draw(alpha)
	Flip 0
End Function

Function update_game(timestep : Float)
	ttest1.update()
	ttest2.update(timestep)
End Function
</textarea><br>Have I missed something?<br>I realise there are other timing methods available but this is purely to check how fixed rate logic behaves for myself. <br><br></td></tr></table><br>
<a name="768287"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> copypaste and crippled a bit from my code (left out some specific things)<br><br>UpdateMain uses Game.intDeltaTime to calc movement and so on<br><br><br><pre class=code>
'--- Variables for compensated timing
Const dt:Float=1.0/60.0
Global CurrTime:Int=0
Global Kompensator:Float=0.0
Global newTime:Int = 0
Global deltaTime:Float = 0.0
'-------------

Repeat
	If CurrTime = 0 Then CurrTime = MilliSecs()
	newTime   = MilliSecs()
	deltaTime = (newTime - CurrTime) / 1000.0
	CurrTime  = newTime
	Kompensator:+ deltaTime

	While(Kompensator &gt;= dt)
		Game.intDeltaTime = (MilliSecs() - Game.IntTimer) / 1000.0
		Game.IntTimer = MilliSecs()            
		UpdateMain() 
		Kompensator :- dt
	Wend
	DrawMain()
Until AppTerminate() Or ExitGame = 1

</pre><br><br>So "previousState" is missing - no tweening<br><br>bye<br>MB <br><br></td></tr></table><br>
<a name="768352"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gabriel</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm not good at reading other people's code at the best of times, but your understanding of the article seems right to me, and I can't immediately spot anything wrong in your implementation. Have you tried using something more accurate than milliseconds to see if that makes the difference? <br><br></td></tr></table><br>
<a name="768708"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Michael/Gab,<br>I was going to try and use <a href="/codearcs.php?code=1909" target="_blank"> this </a> for a high resolution timer. <br>However, checking Michael's code (which still wasn't too smooth for me although I might have implemented it wrong) led me to <a href="http://dewitters.koonsolo.com/gameloop.html" target="_blank"> this </a><br>For some reason this seems to work much better when (if I have read it right) it should be less 'blended'.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
Type ttest1
	Global list:tlist=CreateList()
	Field x:Float = 0.0
	Field y:Float = 100.0
	Field xvel:Float=8.0
	Field pre_x:Float = 0.0
	Method New()
		ListAddLast list , Self
	End Method
	Function Draw(alpha:Float)
		For Local all:ttest1 = EachIn list
'			DrawOval all.x+(all.xvel*alpha),all.y,8,8
			DrawOval all.x,all.y,8,8
		Next
	End Function
	Function update() 
		For Local all:ttest1=EachIn list
			all.pre_x=all.x	
			all.x=(all.x+all.xvel) Mod GraphicsWidth()
		Next
	End Function
End Type
Type ttest2
	Global list:tlist=CreateList()
	Field x:Float = 0.0
	Field y:Float = 200.0
	Field xvel:Float=8.0
	Field pre_x:Float = 0.0
	Method New()
		ListAddLast list , Self
	End Method
	Function Draw(alpha:Float)
		For Local all:ttest2 = EachIn list
			DrawOval all.x+(all.xvel*alpha),all.y,8,8
		Next
	End Function
	Function update()
		For Local all:ttest2 = EachIn list
			all.pre_x=all.x	
			all.x=(all.x+all.xvel) Mod GraphicsWidth()
		Next
	End Function
End Type
Graphics 800 , 600
Local t1:ttest1 = New ttest1
Local t2:ttest2 = New ttest2
Const TICKS_PER_SECOND:Int = 25
Const SKIP_TICKS:Int = 1000 / TICKS_PER_SECOND
Const MAX_FRAMESKIP:Int = 5
Local NEXT_GAME_TICK:Double = MilliSecs()
Local LOOPS:Int
Local INTERPOLATION:Float
While Not KeyHit(KEY_ESCAPE)
	LOOPS = 0
	While (MilliSecs()&gt;NEXT_GAME_TICK And loops &lt; MAX_FRAMESKIP)
		update_game()
		next_game_tick:+ skip_ticks
		loops:+ 1
	Wend
	interpolation=Float(MilliSecs()+SKIP_TICKS-NEXT_GAME_TICK)/Float(skip_ticks)
	render(INTERPOLATION)
Wend
Function render(alpha:Float)
	Cls
	ttest1.draw(alpha)
	ttest2.draw(alpha)
	Flip 0
End Function
Function update_game()
	ttest1.update()
	ttest2.update()
End Function
</textarea><br>Also found Beakers interval system which looks funky with things done by events although I've yet to find how to get the remainder for an interpolation.<br>If anybody else uses, or has checked, fixed logic timesteps then I'd be interested how your code differs. <br><br></td></tr></table><br>
<a name="768714"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kev</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> tonyg, <br><br>my version is based on the article in your first post. in the example below two of the three rects are moved by fixed logic timesteps. all move using the same amount.<br><br><a href="http://www.winblitz3d.co.uk/FixYourTimestep.zip" target="_blank">http://www.winblitz3d.co.uk/FixYourTimestep.zip</a><br><br>kev <br><br></td></tr></table><br>
<a name="768721"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Kev, does your code run smoothly?<br>Is this something you're using which works OK?<br>How does your code compare to the code I have posted? <br><br></td></tr></table><br>
<a name="768724"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> TonyG: Hmm, yeah that first one isn't so hot but the last one works well on my system.  I used that article to make my framework timer.  It uses the fixed rate logic but without the interpolation.  BUT what it does is run the fixed rate logic a certain number of times with Delta at 1, then the last loop is run with Delta at an appropriate fraction.  Then my game logic used Delta for movements and timers etc and it works really well.  Best of both worlds.  I don't know if you've seen my framework in action but it's pretty smooth ;-) <br><br></td></tr></table><br>
<a name="768739"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> To some degree I think all these methods use the  'remainder'  whether they call it interpolation, blending or delta. I was just expecting more from the first method as it uses the extra information from the last step. <br>GA, I've seen the demos from your framework and it does look impressive. If I was interested in writing games I would probably consider getting it but as I simply mess about it's a bit of a waste to have somebody else do the 'messing' for me.<br>.<br>Anybody seen anything I've done wrong? Anybody explain what 't' is for in the Fix Your Timestep article?<br>Anybody got fully working source they're prepared to share so I can check what I'm missing? <br><br></td></tr></table><br>
<a name="768765"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gabriel</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'd like to share my implementation of this but A) Since I'm still running my game engine in a window and only for the editors at the moment, I've no idea if it even works. and B) It's so tightly integrated within my game engine, it would be hard to rip out and even harder to read. When I get a little further along the line and I actually have the game doing something ( ANYTHING! ) then I'd be more than happy to share the implementation ( assuming it works. ) However, that's not likely to be for a while yet.<br><br>I haven't looked at Kev's so cannot comment on that.<br><br>The problem with Grey's implementation is that his timestep is not fixed, defeating the purpose of the article. In a simple game which doesn't use any physics, this won't matter, but if you do use any physics, it will matter, and it will explode your simulation just as the article describes. I haven't looked at Grey's implementation in any detail, but if you're not going to fix your timestep, I don't see the purpose in doing anything more complicated than delta time anyway. This may simply be shortsightedness on my part, however.<br><br>Since T is implemented in exactly the same way in each type of timing Glenn uses, it won't be having an impact on your results. As far as I can tell, and without digging through all my rather complicated code, I'm pretty sure I assumed that T was the total simulation time, as you have.<br><br>I'll try to find some time on Monday or Tuesday to have a proper look at your version and see if I can compare it to my own. A perfect game timing system is very necessary, so it would be good for all of us if we can all have a good understanding of it. <br><br></td></tr></table><br>
<a name="768822"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> TonyG: Fair enough :-)<br><br>Gabriel: My code runs at a fixed rate of 200 FPS (obvisouly that's modifiable) and just uses Delta time for a bit of extra smoothing (the remainder).  It's based on that article.  Maybe you misunderstood the way it works, or I've misunderstood something ;-) <br><br></td></tr></table><br>
<a name="768831"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> TonyG: Just looked at that link you posted, "The Game Loop", and I agree with why the first 3 are not great, but I also don't like the 4th one (it does have some merits of course).  The 4th one uses render tweening and could result in objects being drawn within other objects before collisions are detected - this seems messy to me.  Also if you are trying to make an object fall with gravity, the change in y velocity will only occur at 25FPS and the interpolation will NOT use gravity (unless you coded it that way I guess) in between - so I'm not sure it would look that good...but maybe it woudn't be that noticeable.<br><br>Probably my 200FPS fixed rate loop is overkill but it does look nice.  I've also built in a threshold so that it won't go crazy on slow machines.  It's probably not ideal either, I'm not sure there is an IDEAL game loop.  Each has pros and cons and maybe it's down to developer preference e.g. do you want it to run on slow PCs, do you want it to look good on normal/fast PCs, how easy is the game to code etc.?<br><br>If a PC get's too slow, well maybe it's not suitable to run your game anyway... <br><br></td></tr></table><br>
<a name="768837"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kev</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> tonyg, <br><br><div class="quote"> <br>does your code run smoothly?<br>Is this something you're using which works OK?<br> <br></div><br><br>this is still to be tested. basicly its a wrapped version from the article, so it should function correctly. <br><br><div class="quote"> <br>How does your code compare to the code I have posted? <br> <br></div><br>it does not, as before mine is a wrapped .cpp and your .bmx<br><br>if your intreasted in the code email me and i will send it your way.<br><br>*EDIT, this example now includes a simple example of firing a buttet that collides with 2 images. im intrested in the smoothness of this example. <br><br><a href="http://www.winblitz3d.co.uk/FixYourTimestep.zip" target="_blank">http://www.winblitz3d.co.uk/FixYourTimestep.zip</a><br><br>kev <br><br></td></tr></table><br>
<a name="768891"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gabriel</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Gabriel: My code runs at a fixed rate of 200 FPS (obvisouly that's modifiable) and just uses Delta time for a bit of extra smoothing (the remainder). It's based on that article. Maybe you misunderstood the way it works, or I've misunderstood something ;-) <br></div><br><br>Well the article is not about fixing your frames per second, it's about fixing your timestep. The purpose is so that your physics updates are always fed the same timestep, because doing anything else results in broken, unpredictable physics. In your description above, you said you do a final timestep which is different from the rest, thus you're not using a fixed timestep at all.<br><br>So I'm not sure which of us has misunderstood either, but I thought you were describing a system of timing involving a variable timestep, because you do a small timestep instead of a tween.<br><br><div class="quote"> The 4th one uses render tweening and could result in objects being drawn within other objects before collisions are detected - this seems messy to me. <br></div><br>No it couldn't. You cannot tween forwards, only backwards. So you're tweening back through a physics update you already did, and you didn't hit a collision in your physics update, then you won't hit one by not doing x% of that update.<br><br><div class="quote"> Also if you are trying to make an object fall with gravity, the change in y velocity will only occur at 25FPS and the interpolation will NOT use gravity (unless you coded it that way I guess) in between - so I'm not sure it would look that good...but maybe it woudn't be that noticeable.<br> <br></div><br>This, however, is quite correct. Your tweening is linear, and will not correspond perfectly with dynamics of whatever kind. I've engaged in long debates on this subject in IRC, and we never did reach complete agreement, but I feel that it won't be noticeable as long as you keep your FPS up. I'm not sure what you mean about only occurring at 25FPS, it will occur as often as you update your physics, but if you were updating your physics at 25FPS, then it might indeed be noticeable that your linear tweening was making things less than smooth. I wouldn't recommend going below 60. <br><br></td></tr></table><br>
<a name="768925"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Kev, that is a lot smoother.<br>@GA, I understood you were using the delta only in the render phase (e.g. as a type of tween) but it you're using it in the update phase then I can see why you Gabe asked why you don't just use Deltatime.<br>Anyway, I replaced millisecs() with <a href="/posts.php?topic=46443#516927" target="_blank"> QueryPerformanceCounter() </a><br>which *might* have made the first code look a bit better (my eyes are fried by looking at sodding ovals move across a screen).<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
Type ttest1
	Global list:tlist=CreateList()
	Field x:Float = 0.0
	Field y:Float = 100.0
	Field xvel:Float=1.0
	Field previous_x:Float = 0.0
	
	Method New()
		ListAddLast list , Self
	End Method
	
	Function Draw(alpha:Float)
'standard draw without interpolation for comparison.
		For Local all:ttest1 = EachIn list
			DrawOval all.x , all.y , 8 , 8
		Next
	End Function
	
	Function update() 
		For Local all:ttest1=EachIn list
			all.previous_x=all.x	
			all.x=(all.x+all.xvel) Mod GraphicsWidth()
		Next
	End Function
	
End Type

Type ttest2
	Global list:tlist=CreateList()
	Field x:Float = 0.0
	Field y:Float = 200.0
	Field xvel:Float = 0.5
	Field previous_x:Float = 0.0
	
	Method New()
		ListAddLast list , Self
	End Method
	
	Function Draw(alpha:Float)
'interpolated draw
		For Local all:ttest2 = EachIn list
			Local xpos:Float = (all.x * alpha) + (all.previous_x * (1.0 - alpha) )
			For Local loopy:Int = 0 To 200 Step 10
				DrawOval xpos , all.y + loopy, 8 , 8
			Next
		Next
	End Function
	
	Function update(timestep:Float)
		For Local all:ttest2=EachIn list
' saving previous x for interpolation.
			all.previous_x=all.x	
			all.x=(all.x+all.xvel) Mod GraphicsWidth()
		Next
	End Function
	
End Type

Graphics 800 , 600
TCodeTimer.Initialise()

Local timer:TCodeTimer = TCodeTimer.Create()


Local cycletime:Float = 0.0
Const timestep:Float = 5.0   ' run 10ms timesteps?
Local accumulator:Float = 0.0
Local t1:ttest1 = New ttest1
Local t2:ttest2 = New ttest2
timer.StartWatch()
Local Current_Time:Double = timer.watchtime()

While Not KeyHit(KEY_ESCAPE)
'	Local newtime:Float = MilliSecs()
	Local newtime:Double = timer.watchtime()
	Local deltatime:Float = newtime - current_time
'clamp deltatime.
	If deltatime &gt; 50.0 deltatime = 50.0
	current_time = newtime
	accumulator:+ deltatime
	While accumulator &gt;= timestep
		update_game(timestep)
		cycletime:+ timestep
		accumulator:- timestep
	Wend
'I have called it cycletime but this is 't' in the article.
'Anybody know what it's for? I zero it to stop it incrementing
'	cycletime=0.0
	Local alpha:Float = accumulator / timestep
	render(alpha)
Wend

Function render(alpha:Float)
	Cls
	ttest1.draw(alpha)
	ttest2.draw(alpha)
'               Flip -1 works nicely
	Flip 0
End Function

Function update_game(timestep : Float)
	ttest1.update()
	ttest2.update(timestep)
End Function
' classes

Extern "win32"
	Function QueryPerformanceCounter(count:Long Var)
	Function QueryPerformanceFrequency(freq:Long Var)
End Extern

Type TCodeTimer
	Field _start:Long
	Field _stop:Long
	Field _iterations:Int
	Field _totaltime:Double
	
	Method ResetTimer()
		_start 		= 0
		_stop 		= 0
		_iterations 	= 0
		_totaltime 	= 0
	End Method	
	
	Method StartTimer()
		QueryPerformanceCounter(_start)
	End Method
	
	
	Method StopTimer()
		QueryPerformanceCounter(_stop) 
		_totaltime :+ (Double(_stop - _start) / Double(_freq)) 
		_iterations :+ 1
	End Method
	
	
	''' Stopwatch functions
	Field _watchstart:Double
	
	Method StartWatch()
		self._watchstart = QueryCounter()
	End Method
	
	''' returns the number of milliseconds since calling StartWatch()
	Method WatchTime:Double()
		Return QueryCounter() - _watchstart
	End Method 
	
	''' Globals
	
	''' Initialize should be called before first use, to determine timer class overhead
	Global _overhead:Double	
	Global _freq:Long
	
	Function Create:TCodeTimer()
		Return New TCodeTimer
	End Function
	
	Function Initialise()
		QueryPerformanceFrequency(_freq)
		_freq :/ 1000
	End Function
	
	Function QueryCounter:Double()
		Local counter:Long
		QueryPerformanceCounter(counter)
		Return Double(counter) / _freq
	End Function 

End Type
</textarea><br><br>Timing code *IS* bizarre. There must be lots of people happy with whatever method they used.<br>If there are pros/cons for different types of game then maybe there should be a 'suite' of timing solutions <br>e.g. set_timing_mode=FIXED_RATE<br>from simple 'waittimers' to those more difficult to implement.<br><br>&lt;edit&gt; Hold on. Other than (probably) a programming glitch causing some extra ovals to be drawn when x reaches graphicswidth() the code I just posted is smooth when I used Flip -1. <br>I was using Flip 0 to flip as quick as possible.<br><br>&lt;edit2&gt; AND it seems to be OK with normal millisecs() counter.<br><br>&lt;edit3&gt; Ignore the dodgy ovals drawn. It's simply when x loops t 0 so (x*alpha)+(prev_x*(1.0-alpha))  is not correct. <br><br></td></tr></table><br>
<a name="769023"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> ...until, of course, I try it on another PC and flip -1 causes terrible tearing. Back to the drawing board. <br><br></td></tr></table><br>
<a name="769063"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Gabriel: Ah yes I see now.  My timestep is ixed for all but the last iteration which is based on delta as you have observed.  The article actually says that the objects may be drawn in other objects but I see what you mean.  As for 25FPS I was just referring to the timing that the article used.<br><br>TonyG: I'm not just using Delta because I wanted a high resolution game logic.  Higher than the FPS, and one that remains constant so that collisions etc are always accurate.  It really is the same method as fix your timestep except I'm not render tweening I'm using a variable Delta for the last game loop interation.  I integrated sswifts sprite and physics code with my framework and that worked fine. <br><br></td></tr></table><br>
<a name="792514"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sonic</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just have to say a big thankyou to Tonyg - I've had my game on hold for ages thinking it would be impossible to plug in some kind of timing code...<br><br>First tried delta timing, but it broke the predictability of my platformer (jumping etc)... However your code worked from the outset with very little work to add it in... Thanks so much!!!!<br><br>One thing I noted was you use:<br><br>all.x=(all.x+all.xvel)<br><br>Wheras I used:<br><br>all.x=all.x+( ( all.x - all.pre_x ) * alpha )<br><br>Wondered if there was a reason for that (I realise it's functionally the same in your code...  But for something more complex?) <br><br></td></tr></table><br>
<a name="792524"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey that's great news you finally got it sorted. <br><br></td></tr></table><br>
<a name="792528"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sonic</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> Got a question though ...<br><br>When I introduce random fluctuations in the draw time (simply using delay() ), the smoothness factor drops quite a bit.  The logic still feels the same, but the interpolation feels like it's not quite where it should be.  In other words, I'm running logic at 60Hz, so sometimes the redraw is occurring only 30 times / second when the delay is introduced, however it should potentially run pretty smooth even in 30fps... So I'm wondering...<br><br>Is there something broken with your implementation Tony?  Or would it just need some kind of averaging-out or 'anti-jitter'?  If so I'm not quite sure where in the code I would implement that. <br><br></td></tr></table><br>
<a name="792531"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sonic</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> Lol Grey, are you ever not in the forums?  So do you have any idea what I'm talking about?  You seem to be really good at this sort of thing... <br><br></td></tr></table><br>
<a name="792548"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> I didn't do any more work on it but <a href="/posts.php?topic=70516#788620" target="_blank"> this </a> might help <br><br></td></tr></table><br>
<a name="792551"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chroma</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> nm <br><br></td></tr></table><br>
<a name="792554"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sonic</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#23">[#23]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hmm... This is what I'm using, based on your second example.  <br><br><pre class=code>
	Const FPS:Int = 60
	Const FRAMELENGTH:Int = 1000 / FPS
	Const MAX_FRAMESKIP:Int = 5
	
	Local nextframetime:Double = MilliSecs()
	Local loops:Int
	Local tween:Float
	
	Repeat

		' THE BIG BIT
		
		loops = 0	
		
		Local ms:Double=MilliSecs()
		
		While (ms&gt;nextframetime And loops &lt; MAX_FRAMESKIP)

			update()
			nextframetime:+ FRAMELENGTH
			loops:+ 1
		Wend
		
		tween=Float(ms+FRAMELENGTH-nextframetime)/Float(FRAMELENGTH)

		Cls
		
		draw(tween)

                flip (1)

       Until 0
</pre> <br><br></td></tr></table><br>
<a name="792559"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sonic</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#24">[#24]</a></td></tr></table></td></tr><tr ><td class="posttext"> So I figure it happily flies when there's more draw() than update() called (the tween definitely works)...<br><br>But even when I force it to 30 fps draw() by using delay 17 (just over the length of one frame) it seems jerky.  I know it's possible to be smooth in 30fps, so I'm wondering what could be causing it.<br><br>Hmm...  The never-ending quest for smoothness continues. <br><br></td></tr></table><br>
<a name="792569"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >deps</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#25">[#25]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> BTW How do I post a codebox? <br></div><br>You use [] with the word CODE in it. You can also use QUOTE like I did here. Finish it off with another [] with /CODE or /QUOTE<br><pre class=code>
[quote ]This is a quote[/quote ]
[code ]Some code.[/code ]
</pre><br><br>I added spaces between the text and the ]<br>otherwise it would be interpreted. <br><br></td></tr></table><br>
<a name="792617"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#26">[#26]</a></td></tr></table></td></tr><tr ><td class="posttext"> can you post some demo code of the jittering problem?  Probably you just need some jitter correction code. Thing is if you start using timing code, it will never lopk as good as when you had none and locked to 60Hz BUT timing code is a neccessity for any pro game.<br><br>And yeah, I probably check the forum too often. I only check a couple of areas of interest tho so it's pretty quick. <br><br></td></tr></table><br>
<a name="792682"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sonic</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#27">[#27]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hehe Jake I was only teasing... I think it's great!  You've helped me out plenty, and you probably learn a lot yourself.<br><br>Anyway, the code I posted above is taken exactly from my game.  The thing I'm not sure about is how Tony came from this:<br><br><pre class=code>
         float newTime = time();  
         float deltaTime = newTime - currentTime; 
         currentTime = newTime; * OK.

         accumulator += deltaTime;  * OK

         while (accumulator&gt;=dt)
         {
              previousState = currentState; 
              integrate(currentState, t, dt); 
              t += dt; 
              accumulator -= dt;  * OK.
         }
</pre><br><br>to this:<br><br><pre class=code>
		loops = 0	
		
		Local ms:Double=MilliSecs()
		
		While (ms&gt;nextframetime And loops &lt; MAX_FRAMESKIP)

			update()
			nextframetime:+ FRAMELENGTH
			loops:+ 1
		Wend

</pre><br><br>The original author seems to be doing something extra in the update() phase - some kind of integration?  Or am I being stupid? <br><br></td></tr></table><br>
<a name="792704"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#28">[#28]</a></td></tr></table></td></tr><tr ><td class="posttext"> the original author was calling their update function Integrate, that's all.  They were passing in more variables because it's a physics sim.<br><br>Does making FRAMELENGTH a float and doing 1000.0/FPS help? <br><br></td></tr></table><br>
<a name="792805"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#29">[#29]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Sonic<br>The second post I made was with code taken from a different article from the first.<br>First code taken from <a href="http://www.gaffer.org/game-physics/fix-your-timestep/" target="_blank"> here </a><br>Second code taken from <a href="http://dewitters.koonsolo.com/gameloop.html" target="_blank"> here </a> <br><br></td></tr></table><br>
<a name="820449"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sonic</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#30">[#30]</a></td></tr></table></td></tr><tr ><td class="posttext"> So with the interest in my game on the forum, I thought I'd revive this thread to see if anyone can help me with my last stumbling block.<br><br>As I mentioned before I'm happily using the frameskip + tweening code at frame rates higher than the 60hz my logic runs at.  However, when I introduce a delay of 17ms to ensure that it runs at under 60hz (and the nearest would be 30hz) it seems to jerk nastily.  This is my code:<br><br><pre class=code>

	Const FPS:Int = 60
	Const FRAMELENGTH:Int = 1000 / FPS
	Const MAX_FRAMESKIP:Int = 5
	
	Local nextframetime:Double = MilliSecs()
	Local loops:Int
	Local tween:Float

	Repeat

		' THE BIG BIT
		
		loops = 0	
		
		Local ms:Double=MilliSecs()
		
		While (ms&gt;nextframetime And loops &lt; MAX_FRAMESKIP)
			all.update()
			nextframetime:+ FRAMELENGTH
			loops:+ 1
		Wend
		
		tween=Float(ms+FRAMELENGTH-nextframetime)/Float(FRAMELENGTH)

		Cls
		
		all.draw(tween)

	Until 0	
</pre> <br><br></td></tr></table><br>
<a name="820451"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#31">[#31]</a></td></tr></table></td></tr><tr ><td class="posttext"> Why do you want to limit it to 30FPS, it will look better at 60...? <br><br></td></tr></table><br>
<a name="820452"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sonic</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#32">[#32]</a></td></tr></table></td></tr><tr ><td class="posttext"> No that's not it Grey - I'd like it to be runnable on machines that can only render at 30fps, hence introducing an artificial delay to test it. <br><br></td></tr></table><br>
<a name="820454"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#33">[#33]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah I see, OK got it.  Hmm I'm not really an expert on this sort of tweening code. Hope you can sort it. <br><br></td></tr></table><br>
<a name="820461"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#34">[#34]</a></td></tr></table></td></tr><tr ><td class="posttext"> Does <a href="/codearcs.php?code=2039" target="_blank"> this </a>code help any? <br><br></td></tr></table><br>
<a name="820600"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MGE</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#35">[#35]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sonic  - Could you explain your main loop please? I've never seen a loop like that before? I can see the update being called as needed, but the tween value you pass to the draw routine confuses me, because the draw routine should just draw objects at their current position unless your draw routine is also moving objects based on the tween value passed? Thanks! <br><br></td></tr></table><br>
<a name="820601"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#36">[#36]</a></td></tr></table></td></tr><tr ><td class="posttext"> That's what tweening does, it's a drawing thing.  It doesn't move them, it interpolates between current and past (or maybe future) positions based on the tween and draws it there.  It complicates drawing routines though which is why I didn't bother with tweening. <br><br></td></tr></table><br>
<a name="820604"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gabriel</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#37">[#37]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> I can see the update being called as needed, but the tween value you pass to the draw routine confuses me, because the draw routine should just draw objects at their current position unless your draw routine is also moving objects based on the tween value passed? <br></div><br>That's right. The tween value is between 0.0 and 1.0 and represents how much of the last update you *should* have done. You only ever update in full steps, so your objects get ahead of themelves. The tween value is used in the draw function to linear interpolate between the position, scale, orientation, color, etc before the last update and the value after the last update. <br><br></td></tr></table><br>
<a name="820647"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dmaz</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#38">[#38]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> It complicates drawing routines though which is why I didn't bother with tweening.  <br></div><br>a little but it's well worth it!  I tweened all my Blitz3d stuff (using Marks code) and you could really tell the difference.  My Invaders from Space looked 10 times better on a larger range of machines after I started tweening it.  Most recommended.  press the function keys in that demo to really see it in it's glory. <br><br>[edit] slowmo is a great tool, it allows you to see the animation defects that need to be fixed like with the little guy... <br><br></td></tr></table><br>
<a name="820729"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#39">[#39]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah but I did logic tweening instead so same difference and I have a slow mo mode too, I use it ALL the time to check out the effects I'm programming. <br><br></td></tr></table><br>
<a name="820760"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dmaz</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#40">[#40]</a></td></tr></table></td></tr><tr ><td class="posttext"> oh I know... and you get slowmo with deltatime too.  I just was mentioning it because I think the tweened slowmo looks smoother than any of the other methods. <br><br></td></tr></table><br>
<a name="820805"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sonic</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#41">[#41]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah actually the beauty is it doesn't complicate drawing at all.  All that's needed is an additional 'draw_x= ( current_x * tween ) + ( new_x * (1.0 - tween) )' etc.  All my graphics derive from a bass class anyway, so it only needed to change one routine, rather than add delta time to all logic.<br><br>Still... something is slightly broken!<br><br>I will have a look at that code, Tony, hopefully I can spot a difference. <br><br></td></tr></table><br>
<a name="820808"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chroma</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#42">[#42]</a></td></tr></table></td></tr><tr ><td class="posttext"> I converted the whole Gaffer post about 4 months ago, including the RK4 (integrator function..etc) and it works silky smooth.  If anyone is interested I can post it.<br><br>Here's part of it (non-RK4).  Supply your own ball.png.<br><br>Press the Space Bar to see the Tween in action.<br><br>Part 1 (main.bmx):<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'FRLRT

SuperStrict

Import "Vec3.bmx"

AppTitle$ = "Fixed Rate Logic with Render Tweening"

Graphics 800,600

Local newTime:Int = MilliSecs()
Local oldtime:Int = newTime
Local dt# = 1.0/100.00	'100 FPS logic
Local deltaTime#, accumulator#, alpha#, t#

Local ball:TBody = CreateBody()
ball.current.vPos.x = 400
ball.current.vPos.y = 500
ball.current.vVel.Set(500,-500,0)

SetBlend ALPHABLEND

Global imgBall:TImage = LoadImage("ball.png")
MidHandleImage imgBall

Global RenderTween:Int = False

While Not KeyHit(KEY_ESCAPE)
Cls

	newTime = MilliSecs()
	deltaTime# = (newTime - oldTime) * 0.001
	oldTime = newTime
	
	accumulator :+ deltaTime

	While accumulator &gt;= dt
	
		'Apply some gravity
		ApplyLinearForce ball, 0, ball.fMass * 981.0, 0

		ball.Logic(dt)
		
		accumulator :- dt
		't :+ dt
	Wend

	alpha = accumulator / dt
	
	ball.Render(alpha)

	If KeyHit(KEY_SPACE) Then RenderTween = 1 - RenderTween
	DrawText "Press the SPACE BAR to turn on render tweening mode.",5,5
	DrawText "RenderTween: "+RenderTween,5,50

Flip 1     'Eliminates Tearing
Wend
End


Type TBody
	Field fMass:Float = 1.0
	Field current:TState = New TState
	Field previous:TState = New TState
	
	Method Logic(dt#)
		
		'Basic Linear Physics
		self.previous = self.current.copy()
		self.current.vAcc = self.current.vForces.DividedByScalar(self.fMass)
		self.current.vVel = self.current.vVel.Plus(self.current.vAcc.TimesScalar(dt))
		self.current.vPos = self.current.vPos.Plus(self.current.vVel.TimesScalar(dt))
		self.current.vForces.Zero()
		
		'Simple Collision
		If self.current.vPos.y &lt;= 0 Or self.current.vPos.y &gt;= 600
			self.current.vVel.y = -self.current.vVel.y
		EndIf
		If self.current.vPos.x &lt;= 0 Or self.current.vPos.x &gt;= 800
			self.current.vVel.x = -self.current.vVel.x
		EndIf

	End Method
	
	Method Render(alpha#)
		Local x#, y#
		If RenderTween
			x# = self.current.vPos.x * alpha + self.previous.vPos.x * (1.0 - alpha)
			y# = self.current.vPos.y * alpha + self.previous.vPos.y * (1.0 - alpha)
		Else
			x = self.current.vPos.x
			y = self.current.vPos.y
		EndIf
		DrawImage imgBall, x, y
	End Method

End Type

Function CreateBody:TBody()
	Return New TBody
End Function

Type TState
	Field vPos:TVec3 = Vec3()
	Field vVel:TVec3 = Vec3()
	Field vAcc:TVec3 = Vec3()
	Field vForces:TVec3 = Vec3()

	Field vAngAcc:TVec3 = Vec3()
	Field vAngVel:TVec3 = Vec3()
	Field vMoments:TVec3 = Vec3()

	Method Copy:TState()
		Local t:TState = New TState
		t.vPos = self.vPos.Copy()
		t.vVel = self.vVel.Copy()
		t.vAcc = self.vAcc.Copy()
		Return t
	End Method
End Type

Function ApplyLinearForce(body:TBody, xf#, yf#, zf#)
	Local vF:TVec3 = Vec3(xf, yf, zf)
	body.current.vForces = body.current.vForces.Plus( vF )
End Function
</textarea><br><br>Part 2 (Vec3.bmx):<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Rem
bbdoc:
EndRem
Type TVec3
	Field x:Float, y:Float, z:Float

	Method Copy:TVec3()
		Return Vec3(self.x, self.y, self.z)
	End Method

	Method CrossProduct:TVec3(v:TVec3)
		Local res:TVec3 = New TVec3
		res.x =  self.y * v.z  -  self.z * v.y
		res.y = -self.x * v.z  +  self.z * v.x
		res.z =  self.x * v.y  -  self.y * v.x
		Return res
	End Method

	Method DividedBy:TVec3(v:TVec3)
		Local res:TVec3 = New TVec3
		res.x = self.x / v.x
		res.y = self.y / v.y
		res.z = self.z / v.z
		Return res
	End Method

	Method DotProduct:Float(v:TVec3)
		Return self.x * v.x + self.y * v.y + self.z * v.z
	End Method

	Method DividedByScalar:TVec3(divisor#)
		Local res:TVec3 = New TVec3
		res.x = self.x / divisor
		res.y = self.y / divisor
		res.z = self.z / divisor
		Return res
	End Method
	
	Method Inverse:TVec3()
		Local v:TVec3 = New TVec3
		v.x = -self.x
		v.y = -self.y
		v.z = -self.z
		Return v
	End Method

	Method Magnitude:Float()
		Return Sqr(self.x * self.x + self.y * self.y + self.z * self.z)
	End Method
	
	Method Minus:TVec3(v:TVec3)
		Local res:TVec3 = New TVec3
		res.x = self.x - v.x
		res.y = self.y - v.y
		res.z = self.z - v.z
		Return res
	End Method
	
	Method Normalize()
		Local m# = self.Magnitude()
		self.x :/ m
		self.y :/ m
		self.z :/ m
	End Method

	Method Plus:TVec3(v:TVec3)
		Local res:TVec3 = New TVec3
		res.x = self.x + v.x
		res.y = self.y + v.y
		res.z = self.z + v.z
		Return res
	End Method

	Method Set(x#, y#, z#)
		self.x = x
		self.y = y
		self.z = z
	End Method
	
	Method Show(cap:String, xpos:Int, ypos:Int, vspc:Int = 12)
		DrawText cap+".x=" + self.x,xpos,ypos
		DrawText cap+".y=" + self.y,xpos,ypos + vspc * 1
		DrawText cap+".z=" + self.z,xpos,ypos + vspc * 2
	End Method

	Method Times:TVec3(v:TVec3)
		Local res:TVec3 = New TVec3
		res.x = self.x * v.x
		res.y = self.y * v.y
		res.z = self.z * v.z
		Return res
	End Method

	Method TimesScalar:TVec3(scalar#)
		Local res:TVec3 = New TVec3
		res.x = self.x * scalar
		res.y = self.y * scalar
		res.z = self.z * scalar
		Return res
	End Method

	Method Zero()
		self.x = 0
		self.y = 0
		self.z = 0
	End Method

End Type

Rem
bbdoc:
EndRem
Function Vec3:TVec3(x#=0, y#=0, z#=0)
	Local v:TVec3 = New TVec3
	v.x = x
	v.y = y
	v.z = z
	Return v
End Function
</textarea> <br><br></td></tr></table><br>
<a name="820900"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sonic</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#43">[#43]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hmmm, well I've double-checked your code Tony and it seems to behave identically to mine... probably because mine was taken from yours in the first place!<br><br>So I still have the problem of jerkiness when the graphics take more than one logic frame to redraw.  I need to solve this to continue, but I'm at a loss.<br><br>Perhaps it's a problem with my introducing a Delay (17) artificially, but I don't have a slow machine to test on, and I can't think of any other way to force the redraw to run at half the logic rate.<br><br>[edit] Oh and Chroma - I can't really follow your code, even though it's well written!  The main stuff seems to be the same, but could you explain the integration stuff to me? <br><br></td></tr></table><br>
<a name="820905"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#44">[#44]</a></td></tr></table></td></tr><tr ><td class="posttext"> There are posts which discuss the jerkiness and GA created a test to even it out. <br><br></td></tr></table><br>
<a name="820926"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MGE</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#45">[#45]</a></td></tr></table></td></tr><tr ><td class="posttext"> Seems like the code tony posted only runs smooth on my machine if it renders at refresh (vsync) speed. If you add delays so it skips vsyncs, you get the normal jerkiness.<br><br>If your game misses a vsync or 2, once in a while, you will get jerkiness, that's 100% unavoidable. The trick is to "do something" to stablize the frame rate or rendering so it's not so obvious when it happens. Averaging the delta over a period of frames can help, but it doesn't 100% get rid of it. What I do is give an option to the end user to run the game at a slower render speed. Sometimes it's better to run at a stable slower frame rate compared to the render loop speed being chaotic. <br><br></td></tr></table><br>
<a name="820927"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#46">[#46]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yep, that was the point of the post as I didn't understand why it wasn't smooth. Since then hrdnutz posted <a href="/posts.php?topic=70516#794074" target="_blank"> this </a> with a <a href="/codearcs.php?code=2039" target="_blank"> code archive </a> and GA posted <a href="/posts.php?topic=70611" target="_blank"> this </a>.<br>@JGOware, did you expand your code from <a href="/posts.php?topic=71030#794227" target="_blank"> this </a>? <br><br></td></tr></table><br>
<a name="820937"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#47">[#47]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> option to the end user to run the game at a slower render speed. <br></div>How did you do that? <br><br></td></tr></table><br>
<a name="820973"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dmaz</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#48">[#48]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> but I don't have a slow machine to test on, and I can't think of any other way to force the redraw to run at half the logic rate. <br></div><br>write a program that will take a lot of cpu time, allow control of that with a press of a button.  now run that in the background when testing your app. <br><br></td></tr></table><br>
<a name="820980"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MGE</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#49">[#49]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Tonyg - Yes. I actually use two different timing options in my game engine. Dellta timing with averging or the more simplistic option you linked to. I choose which timing method I need based on the game type. For games that you pretty much need to run at full vsync all the time, I prefer the more simplistic timing method and if the end user can't run it full vsync, then they can turn some graphics options on/off render in lower resolutions, etc, which should allow it to run at 1 vsync.<br><br>@GA - Nothing techy, just using millisecs() to loop a bit at the end of the render loop until the requested frame rate has passed. The user can attempt to run the game at 20,30,50,60 if desired. Logic still happens at regular speed so you don't get slow down. But ofcourse objects will move greater distances at the slower frame rates. <br><br></td></tr></table><br>
<a name="821016"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sonic</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#50">[#50]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hmm, I'm stumped, running a heavy program in the background produces the same effect as introducing a delay (ie stuttering).  Unless I can figure out how to smooth out logic at 60hz and a frame rate below at, I can't realistically continue. <br><br></td></tr></table><br>
<a name="821059"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#51">[#51]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think you need to post the smallest demo which shows the problem. <br><br></td></tr></table><br>
<a name="821320"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MGE</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#52">[#52]</a></td></tr></table></td></tr><tr ><td class="posttext"> As long as your internal logic is accurate, 99.99% of your end users won't care if the game jitters once in a while. Unless ofcourse your delay introduces some weird error in your logic handling? Otherwise, welcome to the world of PC coding, where the fix for the jitters is an endless quest. At some point you just have to think "that's good enough..." and move on. <br><br></td></tr></table><br>
<a name="821326"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#53">[#53]</a></td></tr></table></td></tr><tr ><td class="posttext"> JGOware: OK thx.<br><br>Yeah post a demo, my PC never stutters on anything so we'll be able to find out if it's your PC or your code.<br>However it could just look bad simply BECAUSE it's at 30 fps.  If the stuttering is regular and looks crap it's just due to the update being less that the refresh rate (like Amiga games running at 25Hz on a 50Hz TV (in UK)).  If it's irregular then it's another problem... <br><br></td></tr></table><br>
<a name="823700"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chroma</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#54">[#54]</a></td></tr></table></td></tr><tr ><td class="posttext"> I just wanted to explain what the alpha(tween) variable is.<br><br><pre class=code>alpha = accumulator / dt</pre><br>Alpha is the amount of interpolation between the previous frame and the current frame.  It is a value between 0 and 1.  0 means that the value is fully set to the previous state.  1 means that the value is fully set to the current state.  So a .5 would set the value halfway between the value of the previous state and the current state.<br><br>previous = 10<br>current = 20<br><br>alpha of 0 means that the value of 10 will be used.<br>alpha of 1 means that the value of 20 will be used.<br>alpha of 0.5 means that the value of 15 would be used.<br><br>A lot may already know this but for those that don't, it helps to be able to envision exactly what it's doing. <br><br></td></tr></table><br>
<a name="1312387"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chapman7</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#55">[#55]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sorry to bump such an old topic but I am having a weird issue.  Chroma's post above which has two bmax files is an excellent port and I used that in my game.<br><br>The issue I am having is just before a Flip, I use WaitTimer if I want to limit the framerate.  The problem is that (with 200 updates per second) once I start using WaitTimer, the updates per second either drop or raise slightly instead of staying at 200.<br><br>I'm having a hard time wrapping my head around why though, any ideas? <br><br></td></tr></table><br>
<a name="1312520"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chapman7</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#56">[#56]</a></td></tr></table></td></tr><tr ><td class="posttext"> Main.bmx (Vec3.bmx is above) <br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

'FRLRT

SuperStrict

Import "Vec3.bmx"

AppTitle$ = "Fixed Rate Logic with Render Tweening"

Graphics 800,600

Local newTime:Int = MilliSecs()
Local oldtime:Int = newTime
Local dt# = 1.0/200.00	'100 FPS logic
Local deltaTime#, accumulator#, alpha#, t#

Local ball:TBody = CreateBody()
ball.Current.vPos.x = 400
ball.Current.vPos.y = 500
ball.Current.vVel.Set(500,-500,0)

SetBlend ALPHABLEND

Global imgBall:TImage = LoadImage("ball.png")
MidHandleImage imgBall

Global RenderTween:Int = False

Global SetTime:Int = MilliSecs()
Global GetFPS:Int
Global SetFPS:Int
Global UPS:Int
Global UPSSet:Int

Local ForcedRefreshRate:TTimer

While Not KeyHit(KEY_ESCAPE)
Cls

	If MilliSecs() - SetTime &gt; 1000
		GetFPS = SetFPS
		SetFPS = 0
		SetTime = MilliSecs()
		UPSSet = UPS
		UPS = 0
	Else
		SetFPS = SetFPS + 1
	EndIf

	newTime = MilliSecs()
	deltaTime# = (newTime - oldTime) * 0.001
	oldTime = newTime
	
	accumulator :+ deltaTime

	While accumulator &gt;= dt
		UPS = UPS + 1
		'Apply some gravity
		ApplyLinearForce ball, 0, ball.fMass * 981.0, 0

		ball.Logic(dt)
		
		accumulator :- dt
		't :+ dt
	Wend

	alpha = accumulator / dt
	
	ball.Render(alpha)

	If KeyHit(KEY_SPACE) Then RenderTween = 1 - RenderTween
	DrawText "Press the SPACE BAR to turn on render tweening mode.",5,5
	DrawText "RenderTween: "+RenderTween,5,50
	
	DrawText(GetFPS, 0, 70)
	DrawText(UPSSet, 0, 80)
	
	Local Suspended:Byte = AppSuspended()
	
		If (Suspended = True) Then
			If ForcedRefreshRate = Null Then ForcedRefreshRate = CreateTimer(30)
			WaitTimer(ForcedRefreshRate)
		Else
			If ForcedRefreshRate &lt;&gt; Null Then StopTimer(ForcedRefreshRate)
		EndIf

Flip 0     'Eliminates Tearing
Wend
End


Type TBody
	Field fMass:Float = 1.0
	Field Current:TState = New TState
	Field previous:TState = New TState
	
	Method Logic(dt#)
		
		'Basic Linear Physics
		Self.previous = Self.Current.copy()
		Self.Current.vAcc = Self.Current.vForces.DividedByScalar(Self.fMass)
		Self.Current.vVel = Self.Current.vVel.Plus(Self.Current.vAcc.TimesScalar(dt))
		Self.Current.vPos = Self.Current.vPos.Plus(Self.Current.vVel.TimesScalar(dt))
		Self.Current.vForces.Zero()
		
		'Simple Collision
		If Self.Current.vPos.y &lt;= 0 Or Self.Current.vPos.y &gt;= 600
			Self.Current.vVel.y = -Self.Current.vVel.y
		EndIf
		If Self.Current.vPos.x &lt;= 0 Or Self.Current.vPos.x &gt;= 800
			Self.Current.vVel.x = -Self.Current.vVel.x
		EndIf

	End Method
	
	Method Render(alpha#)
		Local x#, y#
		If RenderTween
			x# = Self.Current.vPos.x * alpha + Self.previous.vPos.x * (1.0 - alpha)
			y# = Self.Current.vPos.y * alpha + Self.previous.vPos.y * (1.0 - alpha)
		Else
			x = Self.Current.vPos.x
			y = Self.Current.vPos.y
		EndIf
		DrawImage imgBall, x, y
	End Method

End Type

Function CreateBody:TBody()
	Return New TBody
End Function

Type TState
	Field vPos:TVec3 = Vec3()
	Field vVel:TVec3 = Vec3()
	Field vAcc:TVec3 = Vec3()
	Field vForces:TVec3 = Vec3()

	Field vAngAcc:TVec3 = Vec3()
	Field vAngVel:TVec3 = Vec3()
	Field vMoments:TVec3 = Vec3()

	Method Copy:TState()
		Local t:TState = New TState
		t.vPos = Self.vPos.Copy()
		t.vVel = Self.vVel.Copy()
		t.vAcc = Self.vAcc.Copy()
		Return t
	End Method
End Type

Function ApplyLinearForce(body:TBody, xf#, yf#, zf#)
	Local vF:TVec3 = Vec3(xf, yf, zf)
	body.Current.vForces = body.Current.vForces.Plus( vF )
End Function
</textarea><br><br>When I limit the framerate to 30, the Updates Per Second jumps to 205ish instead of 200, when I limit the framerate to 1000, it goes to 211-212... <br><br></td></tr></table><br>
<a name="1312966"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chapman7</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#57">[#57]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'll send whoever can explain clearly why this happens, and how to fix it, $15 to their paypal. <br><br></td></tr></table><br>
<a name="1313018"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#58">[#58]</a></td></tr></table></td></tr><tr ><td class="posttext"> You are working with floating point numbers.<br><br>So 0.05 becomes 0.049999998 or 0.05000012 or ... and then you are creating sums out of it. This might lead to odd scenarios in your while loop.<br><br>so there is a "as long as numberA is bigger than numberB".<br> Imagine numberA being 0.50002 and the subtracted value is 0.24999997. If you subtract, your remainder becomes 0.2500xxx5 - allowing another subtraction (an additional physics/logic call). If the numberA was 0.4999997 it might result in one logic call less.<br>This does not explain why it happens to have 5% off.<br><br>BTW: on my computer I got exact 200 and every 2-3 seconds a hickup to 201 (which fits nicely in my "floating point"-assumption).<br><br><br>Maybe you should log the values in your logic-while-loop (to a predefined array - and then outputting to a log file after finishing the whole app).<br><br><br>Another thing is:<br><pre class=code>
	If MilliSecs() - SetTime &gt; 1000
		GetFPS = SetFPS
		SetFPS = 0
		SetTime = MilliSecs()
		UPSSet = UPS
		UPS = 0
	Else
		SetFPS = SetFPS + 1
	EndIf

	newTime = MilliSecs()
</pre><br><br>so if you happen to have met the first condition ("1 second passed") you do something. This might ... because of some GC kicking in ... be delayed a bit. So when doing "SetTime = Millisecs()" the returned value might be different to the initial Millisecs() in the condition check.<br>So after the condition was met, a bit of time is already run until the "setTime"-assignment. This might lead to the next "fps-calculation" happening a bit earlier (1000ms minus "delay until new assignment"). Seems not to happen here ... so apparently not that problematic.<br><br><br>Back to logging - log what I described and you might see that there are floating point inaccuracies.<br><br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="1313057"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chapman7</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#59">[#59]</a></td></tr></table></td></tr><tr ><td class="posttext"> Derron I was hoping for some code that solves it... I'm having a hard time just wrapping my head around the Timestep code in general.<br><br>When you tried it on your computer, did you click on another window so the app became suspended? Also if you change it to like CreateTimer(1000) I was seeing 210-211 <br><br></td></tr></table><br>
<a name="1313062"></a>

<a name="1313063"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#60">[#60]</a></td></tr></table></td></tr><tr ><td class="posttext"> My way to track it down:<br><br><br>When suspended I see it lowers to 97 of 100... initially blamed the timer and some inaccuracies there.<br><br><br>@ CreateTimer(1000)<br>No issues there.<br><br><br>But the lower, the more time goes by. With "CreateTimer(5)" I got a huge delay:<br><br><pre class=code>
If MilliSecs() - SetTime &gt; 1000
	'added:
	print MilliSecs() - SetTime
</pre><br><br>I see that with "CreateTimer(5)" I get ~1200 output, with higher Timers I get ~1000 (1000-1001).<br><br>A timer of 5 means: every 200ms (5 hertz).<br><br><br>Ok, and with some inaccuracy, it sometimes waits 197ms and sometimes 203ms<br><pre class=code>
begin wait: 106358386
end wait: 106358583

begin wait: 106358585
end wait: 106358783

begin wait: 106358785
end wait: 106358984
</pre><br>Hmm, so in my case it was always a bit less than 200 ("begin" was printed before waiting, "end" was printed after waiting...so they ignore the time until the actual call).<br><br><br>Ok, so what does this mean?<br><br>Your WaitTimer always waits 200ms (in my example).<br>Your "FPS-Counter" sets the current UPS-count as soon as over 1 second was gone. It does not set the counter if "992 ms" were gone (x * 198ms in my case). It then waits until it reaches 1190ms (the next cycle).<br><br>BUT ... while it did not set the current UPS in that cycle, it will run the next round of "While accumulator &gt;= dt" (and the logic-updates). <br><br>So in the time slot "1000 to 1190ms" it will run some updates which are then counted to the "1 second" (which in reality was 1.19s).<br><br><br><br>-----------<br><br>Conclusion:<br><br><pre class=code>
	If MilliSecs() - SetTime &gt;= 1000
		local secondsGone:Float = ((MilliSecs() - SetTime)/1000.0)
		GetFPS = int(SetFPS / secondsGone + 0.5)
		SetFPS = 0
		SetTime = MilliSecs()
		UPSSet = int(UPS / secondsGone + 0.5)
		UPS = 0
	Else
</pre><br><br>"&gt;= 1000" instead of "&gt; 1000"<br>- you want to track a second, not 1.0001 seconds<br><br>secondsGone<br>- to be able to "scale down" the FPS/UPS values in the given time interval (something &gt; 1000ms)<br><br>int(x / srecondsGone + 0.5)<br>- if secondsGone is 1.001 (so minimally over 1 second) it might round wrongly: 100/1.0001 = 99.999 - which is casted to an "int" as 99 (as it just truncates everything after the "." / it removes the decimal part). If we now would use "Ceil(x)" it would always round up, if we used "Floor(x)" we always rounded down. We now add "0.5" so the "int-cast" (truncation of decimal part) leads to the "economical rounding" (&lt;x.5 rounds down, else rounds up).<br><br>0.99 + 0.5 = 1.49 = 1 (as int)<br>0.49 + 0.5 = 0.99 = 0 (as int)<br> <br><br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="1313134"></a>

<a name="1313135"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chapman7</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#61">[#61]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Derron!  Email me your paypal address and Ill send you ze money.<br><br>One more Q if you don't mind... Why does WaitTimer(1000) not produce 1000fps/ticks? it seems to be under for me.<br><br>In the example above it's only a few off but on my main application its about 50 off<br><br>Edit: Nvm I think I got it <br><br></td></tr></table><br>
<a name="1313136"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#62">[#62]</a></td></tr></table></td></tr><tr ><td class="posttext"> @ paypal<br><br>Send it to someone who needs it (some care/red-cross/-organisation - or to "Yue", think he is one of us in the forums who really earns too less) ... or if too time-consuming: buy your mom some flowers.<br><br><br>@ timer and "off"<br>Yes, the problem is what I described as "more than a second" (it measures fps/ups of eg. 1200ms but presents it as if it was done in 1000ms - which is why I added the scale-down).<br><br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
