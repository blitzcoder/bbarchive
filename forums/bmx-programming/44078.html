<!DOCTYPE html><html lang="en" ><head ><title >Using Doxygen with BlitzMax</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Using Doxygen with BlitzMax</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Using Doxygen with BlitzMax</a><br><br>
<a name="491346"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >N</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> If you're interested in using Doxygen with BlitzMax and you don't want to have to work with two different files for documentation and code, this is how you do it:<br><pre class=code>
'/*
Rem
*/

/// @brief Prints "I"
/// @return Pi (3.1415962 and so on)
double Blah();

/// @brief Foo object class
class Foo
{
	public:
	/// @brief Some variable
	int bar;
	
	/// @brief Creates a Foo object
	/// @param i The Foo's bar information
	/// @return A new Foo object
	static Foo Create(int i);
};

/// @brief Bar object class
class Bar : public Foo
{
	public:
	/// @brief Craptasticalnessnessness
	int _foo;
	
	/// @brief Creates a Bar object
	/// @param f The Bar's _foo information
	/// @return A new Bar object
	static Bar Create(Object f);
};
'/*
End Rem

' Doxygen test

Function Blah!()
	Print "I"
	Return Pi
End Function

Type Foo
	Field bar:Int
	
	Function Create:Foo(i:Int)
		Local this:Foo = New Foo
		this.bar = i
		Return this
	End Function
End Type

Type Bar..
	Extends Foo
	
	Field _foo:Object
	
	Function Create:Bar(f:Object)
		Local this:Bar = New Bar
		this._foo = f
		Return this
	End Function
End Type

'*/
</pre><br><br>Ugly, messy, but if you like Doxygen then you probably don't mind that already.  Until my documentation generator is released, this is probably the only way to document non-module code besides writing everything by hand.<br><br>Well, just thought I'd post that- weird experiment, but eh. <br><br></td></tr></table><br>
<a name="491363"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Clyde</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> For those without a clue, Whats Doxygen? <br><br></td></tr></table><br>
<a name="491377"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >N</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> <a href="http://www.stack.nl/~dimitri/doxygen/" target="_blank">http://www.stack.nl/~dimitri/doxygen/</a><br><br>Google would've taken less time -_- <br><br></td></tr></table><br>
<a name="491383"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Booticus</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey thanks Noel! This WILL have to hold us over till your Doc generator is done. Any ETA on its release? <br><br></td></tr></table><br>
<a name="491390"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >N</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think it'll be soon (e.g., week or two), I'm just working on bits and pieces of the output now.  I've been thinking about trying to use Dot with it, but I don't know yet.  That can wait 'til I'm done with the rest of it.<br><br>Basically, <a href="http://s87776868.onlinehome.us/docgen_out.html" target="_blank"><i>this</i></a> is what I've got it outputting at the moment when I run it over my GUI module (test output, in other words this wasn't made by DocGen itself but rather by looping through all of DocGen's acquired data and putting it in a single file so I can look for errors).<br><br>Basically, the way it works at the moment is a three-pass system.  First pass gathers all the objects in the files, be it comments, types, methods, functions, variables, etc., then the second pass gathers all associations between the data, comments, etc. and links them accordingly.  The third pass is generating the HTML based on template files that look something like this (you don't have to write you own as I'm including 'defaults' in it as well, but you'll likely want to write your own because the defaults are gonna be ugly [I don't want to spend a lot of time on them]):<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
@HTMLHEADER
	&lt;html&gt;&lt;head&gt;&lt;link href="docgen.css" rel="stylesheet" type="text/css"/&gt;
	&lt;title&gt;$PROJECTNAME$ Documentation&lt;/title&gt;&lt;/head&gt;&lt;body&gt;
@HTMLFOOTER
	&lt;div class="smallText"&gt;Generated by &lt;img src="docgenr3.png"/&gt; v$DOCGENVERSION$&lt;/div&gt;
	&lt;/body&gt;&lt;/html&gt;
@BRIEFLIST
	&lt;table border="1"&gt;
		$CONTENT$
	&lt;/table&gt;
@FUNCTION
	&lt;TABLE width="100%" align="center"&gt;
		&lt;TR&gt;&lt;TD&gt;
			&lt;TABLE width="100%"&gt;
				&lt;TR&gt;&lt;TD&gt;
					$NAME-BRIEF$
				&lt;/TD&gt;&lt;/TR&gt;
			&lt;/TABLE&gt;
			&lt;BR/&gt;
			&lt;TABLE width="100%"&gt;
				&lt;TR&gt;
					&lt;TD&gt;$ARGS-LONG$&lt;/TD&gt;
					&lt;TD&gt;Description :&lt;br&gt;$DESC$&lt;HR/&gt;Returns :&lt;br&gt;$RETURNS$&lt;/TD&gt;
				&lt;/TR&gt;
			&lt;/TABLE&gt;
		&lt;/TD&gt;&lt;/TR&gt;
	&lt;/TABLE&gt;
	&lt;BR/&gt;
@END
</textarea><br><br>Edit: And for a basic example of how you document code in it:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
''' @brief The class used to update and draw the GUI
Type CGUI Final
	''' @brief The gadget list- only here for windows, other gadgets really/
	''' should not be in this list
	Global GadList:TList
	''' @brief The active window
	Global ActiveWindow:CWindow
	''' @brief Whether or not the GUI class is initialized.  This variable is kindof useless.
	Global Initialized:Int
	''' @brief The active GUI object
	Global GUI:CGUI
	''' @brief The path to the skin the GUI is using
	Global SkinPath:String="./"
	''' @brief The path to the font the GUI is using
	Global FontPath:String=""
	''' @brief The font style of title bar text (like a header)
	Global TitleFontStyle:Int = SMOOTHFONT|BOLDFONT
	''' @brief The normal font style used for pretty much everything
	Global NormalFontStyle:Int = SMOOTHFONT
	''' @brief Normal gadget text color
	Global GadgetR
	''' @brief Normal gadget text color
	Global GadgetG
	''' @brief Normal gadget text color
	Global GadgetB
	''' @brief Disabled gadget text color
	Global DisabledR=92
	''' @brief Disabled gadget text color
	Global DisabledG=92
	''' @brief Disabled gadget text color
	Global DisabledB=92
	''' @brief Title text color
	Global TitleR
	''' @brief Title text color
	Global TitleG
	''' @brief Title text color
	Global TitleB
	''' @brief Selected text color
	Global SelectedR
	''' @brief Selected text color
	Global SelectedG
	''' @brief Selected text color
	Global SelectedB
	''' @brief Current GL context's width
	Global Width:Double
	''' @brief Current GL context's height
	Global Height:Double
	''' @brief
	Global PushData:Int = 1
	
	Method New()
		CGUI.Initialized = 1
		CGUI.GadList = CreateList()
		CGUI.GUI = Self
		SetMaskColor 255,0,255
		Form = LoadImage(CGUI.SkinPath+"form.png",0)
		Assert Form,"Form is Null; this is a required file"
		Font = LoadImageFont(CGUI.FontPath,12,NormalFontStyle)
		If Font = Null Then Font = GetImageFont()
		FontTitle = LoadImageFont(CGUI.FontPath,16,TitleFontStyle)
		If FontTitle = Null Then FontTitle = GetImageFont()
		If Font = Null Then Font = GetImageFont()
		If FontTitle = Null Then FontTitle = GetImageFont()
	End Method
	
	''' @brief Reloads the skin - use this to load a new skin
	Function ReloadSkin()
		SetMaskColor 255,0,255
		Form = LoadImage(CGUI.SkinPath+"form.png",0)
		Assert Form,"Form is Null; this is a required file"
		Font = LoadImageFont(CGUI.FontPath,12,NormalFontStyle)
		FontTitle = LoadImageFont(CGUI.FontPath,16,TitleFontStyle)
		If Font = Null Then Font = GetImageFont()
		If FontTitle = Null Then FontTitle = GetImageFont()
	End Function
	
	''' @brief Updates the GUI and all gadgets
	Function Update()
		If CGUI.Initialized = 0 Then Return
		
		Local cFont:TImageFont = GetImageFont()
		
		MH = 0
		MU = 0
		
		MD = MouseDown(1)
		If MD=1 And OMD=0 Then MH = 1
		If MD=0 And OMD=1 Then MU = 1
		MX = MouseX()
		MY = MouseY()
		
		For Local i:CGadget = EachIn CGUI.GadList
			i.Update()
		Next
		
		SetImageFont(cFont)
		
		OMD = MD
	End Function
	
	''' @brief Draws the GUI and all visible gadgets
	Function Draw()
		Local iPushData = PushData
		Local cFont:TImageFont
		
		If iPushData Then
			glPushClientAttrib(GL_CLIENT_ALL_ATTRIB_BITS)
			glPushAttrib(GL_ALL_ATTRIB_BITS)
			cg_Error_ "Pushing attribs"
			
			glMatrixMode(GL_PROJECTION)
			glPushMatrix()
			glLoadIdentity()
			cg_Error_ "Setting projection matrix"
			glOrtho(0:Double, CGUI.width:Double, CGUI.height:Double, 0:Double,..
				-10:Double, 10:Double)
			cg_Error_ "Setting orthographic projection "+CGUI.Width+" "+cgui.height
			glMatrixMode(GL_MODELVIEW)
			glPushMatrix()
			glLoadIdentity()
			cg_Error_ "Setting modelview matrix"
			
			cFont = GetImageFont()
			
			glEnable GL_TEXTURE_2D
			cg_Error_ "Setting blend modes"
			
			glDisable GL_DEPTH_TEST
		EndIf
		For Local i:CGadget = EachIn CGUI.GadList
			glScissor(0,0,GraphicsWidth(),GraphicsHeight())
			i.Draw()
		Next
		If iPushData Then
			SetImageFont(cFont)
			
			glPopMatrix()
			glMatrixMode GL_PROJECTION
			glPopMatrix()
			glMatrixMode GL_MODELVIEW
			
			glPopAttrib()
			glPopClientAttrib()
			cg_Error_ "Popping attributes and matrices"
		EndIf
	End Function
	
	''' @brief Registers a window - for internal use only
	Function RegisterGadget:TLink(gad:CWindow)
		ActiveWindow = gad
		Return ListAddLast(CGUI.GadList,gad)
	End Function
	
	'' Convenience functions added to make expanding the GUI easier
	
	''' @brief Gets the current skin's image class
	''' @return The current skin's image
	Method GetSkinImage:TImage()
		Return Form
	End Method
	
	''' @brief Gets the current skin's font class
	''' @return The current skin's font
	Method GetGadgetFont:TImageFont()
		Return Font
	End Method
	
	''' @brief Gets the current skin's header font class
	''' @return The current skin's titlebar/header font
	Method GetTitleFont:TImageFont()
		Return FontTitle
	End Method
	
	''' @brief Returns whether the left mouse button was released
	''' @return True if released, False if not
	Method MouseUp()
		Return MU
	End Method
	
	''' @brief Returns whether the left mouse button is being pressed
	''' @return True if pressed, False if not
	Method MouseDown()
		Return MD
	End Method
	
	''' @brief Returns whether the left mouse button was hit
	''' @return True if hit, False if not
	Method MouseHit()
		Return MH
	End Method
End Type
</textarea><br><br>Random blurb over. <br><br></td></tr></table><br>
<a name="491485"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Booticus</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Damn Noel, thats the [poop]! Cant wait! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
