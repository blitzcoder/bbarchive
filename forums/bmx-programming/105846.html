<!DOCTYPE html><html lang="en" ><head ><title >Hyperspace Star Generator (Source Included)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Hyperspace Star Generator (Source Included)</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Hyperspace Star Generator (Source Included)</a><br><br>
<a name="1296126"></a>

<a name="1296127"></a>

<a name="1296128"></a>

<a name="1296129"></a>

<a name="1296130"></a>

<a name="1296131"></a>

<a name="1296183"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> <img src="http://www.writerscafe.org/uploads/rte/a1c042bf254e5824d79bc53a3799bfc6.png"><br><br>I had originally written this code for QBasic, but when I did, it used all manner of complex calculations that today aren't really needed.<br><br><i>X(I) = X(I) - (MX * 5 - X(I)) / (N&amp; / S(I) * 50 / S(I))<br>Y(I) = Y(I) - (MY * 5 - Y(I)) / (N&amp; / S(I) * 50 / S(I))</i><br><br>The description I left for it was:<br><br><b>Imagine if you will, a clear night with no clouds.  The stars twinkle like a thousand little diamonds in the deep sky. Then you close your eyes and feel yourself rising up to soar into the night sky at a phenomenal rate. You pause as there is a sense of wrenching vertigo in passing through the Earth's ionosophere and break free to fly by entire entire galaxies and constellations in a matter of seconds.</b><br><br>Coding in QBasic was certainly a lot of work. With such commands as draw image with scale and rotation in BlitzMAX, however, I can skip all that and focus right on how a single star will appear only affected by these 2 variables.<br><br>The result ? A very nice 3D star tunnel with rapid movement. Hold down SPACE to make a hyperspace effect. Written in 36-lines of code, not counting the remarks.<br><br>Enjoi !<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'     ________________________________________
'    //                                     //
'   // Hyperspace Star Generator           //
'  // Version, "Space Tunnel"             //
' // Written by David W (dw817) 02/19/16 //
'//_____________________________________//
'
' The concept of stars streaming out 3-dimensionally is
' not a new one. The original code I had made use of was in QBASIC
' and a complex formula to be sure:
' X(I)=X(I)-(MX*5-X(I))/(N&amp;/S(I)*50/S(I))
' Today no such coding grittiness is needed, not with BlitzMAX's
' own marvelous image rotation and scaling methods.
'
' The results ? A very nice and fast space tunnel in
' minimal coding and calculation space. :)

Strict
SeedRnd MilliSecs() ' always get random results

SetGraphicsDriver GLMax2DDriver(),0 ' front buffer graphics
Graphics 1024,768
SetBlend lightblend

Local sn=500 ' number of stars
Local sr[sn],ss[sn],i,img:TImage=CreateImage(512,4)

DrawOval 512-8,0,8,4 ' our STAR is a stretchy oval
GrabImage img,0,0 ' grab it as an image

Repeat ' {* MAIN *}

If KeyDown(32)=0 Then Cls ' hold down space for hyperspace effect
For i=0 Until sn
  SetTransform sr[i],ss[i]/10000.0,ss[i]/10000.0 ' set rotation and scale in one command
  DrawImage img,512,384 ' always draw star from the center
  ss[i]:+(ss[i]*ss[i]*.000002) ' scale warps
'  sr[i]:+1 ' Add SWIRL effect
  If ss[i]&gt;=13000 Or ss[i]=0 ' off edge ? choose new place to appear
    ss[i]=Rand(750,10000) ' scale
    sr[i]=fnr(360) ' rotation
  EndIf
Next

Flip 0 ' show our work
Delay 10 ' bit of delay

Until KeyDown(27) ' exit on ESCAPE key
' {* END OF MAIN *}

' &gt;&gt; SIMPLE GET RANDOM NUMBER (0..a-1)
Function fnr(a)
  Return Rand(a)-1
EndFunction
</textarea><br><br><img src="http://www.writerscafe.org/uploads/rte/d3243a2c5c81c3ed5c77e51ce9e4fa91.png"> <br><br></td></tr></table><br>
<a name="1296138"></a>

<a name="1296139"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I appended some color variation to the stars and darkened the outside of the stars (less alpha when doing lightblend).<br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'     ________________________________________
'    //                                     //
'   // Hyperspace Star Generator           //
'  // Version, "Space Tunnel"             //
' // Written by David W (dw817) 02/19/16 //
'//_____________________________________//
'
' The concept of stars streaming out 3-dimensionally is
' not a new one. The original code I had made use of was in QBASIC
' and a complex formula to be sure:
' X(I)=X(I)-(MX*5-X(I))/(N&amp;/S(I)*50/S(I))
' Today no such coding grittiness is needed, not with BlitzMAX's
' own marvelous image rotation and scaling methods.
'
' The results ? A very nice and fast space tunnel in
' minimal coding and calculation space. :)

Strict
SeedRnd MilliSecs() ' always get random results

SetGraphicsDriver GLMax2DDriver(),0 ' front buffer graphics
Graphics 1024,768
SetBlend lightblend

Local sn=500 ' number of stars
Local sr[sn],ss[sn],sc[sn],i,img:TImage=CreateImage(512,4)
SetColor 150,150,150
DrawOval 512-8,0,8,4 ' our STAR is a stretchy oval
SetColor 255,255,255
DrawOval 512-7,1,6,2 ' our STAR is a stretchy oval
GrabImage img,0,0 ' grab it as an image

Repeat ' {* MAIN *}

If KeyDown(32)=0 Then Cls ' hold down space for hyperspace effect
For i=0 Until sn
	SetColor (sc[i] Shr 16) &amp; $ff, (sc[i] Shr 8) &amp; $ff, (sc[i] &amp; $ff)
  SetTransform sr[i],ss[i]/10000.0,ss[i]/10000.0 ' set rotation and scale in one command
  DrawImage img,512,384 ' always draw star from the center
  ss[i]:+(ss[i]*ss[i]*.000002) ' scale warps
'  sr[i]:+1 ' Add SWIRL effect
  If ss[i]&gt;=13000 Or ss[i]=0 ' off edge ? choose new place to appear
	'sc[i] = A + R + G + B
	sc[i] = (Int(255 * $1000000) + Int((Rand(55)+200) * $10000) + Int((Rand(75)+180) * $100) + Int((Rand(55)+200))) 'color
    ss[i]=Rand(750,10000) ' scale
    sr[i]=fnr(360) ' rotation
  EndIf
Next

Flip 0 ' show our work
Delay 10 ' bit of delay

Until KeyDown(27) ' exit on ESCAPE key
' {* END OF MAIN *}

' &gt;&gt; SIMPLE GET RANDOM NUMBER (0..a-1)
Function fnr(a)
  Return Rand(a)-1
EndFunction
</textarea><br><br><br>Stars "pop" into the screen (just look at the outer parts...they just pop in).<br><br>Glowing effect is missing when "warping" (things look pixelated after a while - this is based on the approach you are using)<br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="1296147"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Staring at the centre after a dozen pints is pretty trippy :D <br><br></td></tr></table><br>
<a name="1296154"></a>

<a name="1296156"></a>

<a name="1296157"></a>

<a name="1296160"></a>

<a name="1296164"></a>

<a name="1296165"></a>

<a name="1296166"></a>

<a name="1296167"></a>

<a name="1296171"></a>

<a name="1296186"></a>

<a name="1296187"></a>

<a name="1296188"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Glad you like it, Dave. :D<br><br>I'm seeing the bit of color you have added Derron. Here is the code, minus the array for rotation and giving a "Star Trek The Next Generation" feel to the stars where they are tinged with purple at the tips.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'     ________________________________________
'    //                                     //
'   // Hyperspace Star Generator           //
'  // Version, "Return Of The Tunnel"     //
' // Written by David W (dw817) 02/19/16 //
'//_____________________________________//
'
' The concept of stars streaming out 3-dimensionally is
' not a new one. The original code I had made use of was in QBASIC
' and a complex formula to be sure:
' X(I)=X(I)-(MX*5-X(I))/(N&amp;/S(I)*50/S(I))
' Today no such coding grittiness is needed, not with BlitzMAX's
' own marvelous image rotation and scaling methods.
'
' The results ? A very nice and fast space tunnel in
' minimal coding and calculation space. :)

Strict
SeedRnd MilliSecs() ' always get random results

SetGraphicsDriver GLMax2DDriver(),0 ' front buffer graphics
Graphics 1024,768
SetBlend lightblend ' all images merge

Local ss[360],i,img:TImage=CreateImage(512,4),r,e

SetColor 0,0,255
DrawOval 512-24,0,8,4
SetColor 255,0,255
DrawOval 512-20,0,8,4
SetColor 0,255,0
DrawOval 512-16,0,8,4
SetColor 255,255,0
DrawOval 512-12,0,8,4
SetColor 255,255,255
DrawOval 512-8,0,8,4 ' our STAR is a stretchy oval
GrabImage img,0,0 ' grab it as an image

Repeat ' {* MAIN *}

If KeyDown(32)=0 Then Cls ' hold down space for hyperspace effect
' r:+1 ' rotate stars
For i=0 Until 360
  If ss[i]&gt;=12500 Or ss[i]=0 ' off edge ? choose new place to appear
    ss[i]=Rand(710,4000)
  EndIf
  SetTransform i+r,ss[i]/10000.0,ss[i]/10000.0 ' set rotation and scale in one command
  DrawImage img,512,384 ' always draw star from the center
  ss[i]:+ss[i]^2*.000002 ' tunnel warp
'  ss[i]=ss[i]^1.0015 ' normal warp
Next

Flip 0 ' show our work
Delay 10 ' bit of delay

Until KeyDown(27) ' exit on ESCAPE key
' {* END OF MAIN *}

' &gt;&gt; SIMPLE GET RANDOM NUMBER (0..a-1)
Function fnr(a)
  Return Rand(a)-1
EndFunction

</textarea><br><br><strike>This loses the 'tunnel' effect which might be considered good.</strike><br><br>Nope, I like the tunnel, returned it. <br><br></td></tr></table><br>
<a name="1298322"></a>

<a name="1298325"></a>

<a name="1298326"></a>

<a name="1298327"></a>

<a name="1298328"></a>

<a name="1298329"></a>

<a name="1298333"></a>

<a name="1298334"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm sorry, but I couldn't resist playing with this a little... hope you don't mind?! 8}<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'      ________________________________________
'     //                                     //
'    // Hyperspace Star Generator           //
'   // Version, "Return Of The Tunnel"     //
'  // Written by David W (dw817) 02/19/16 //
' //   ...screwed around with by Taron   //
'//_____________________________________//
'
' The concept of stars streaming out 3-dimensionally is
' not a new one. The original code I had made use of was in QBASIC
' and a complex formula to be sure:
' X(I)=X(I)-(MX*5-X(I))/(N&amp;/S(I)*50/S(I))
' Today no such coding grittiness is needed, not with BlitzMAX's
' own marvelous image rotation and scaling methods.
'
' The results ? A very nice and fast space tunnel in
' minimal coding and calculation space. :)

Strict
SeedRnd MilliSecs() ' always get random results

SetGraphicsDriver GLMax2DDriver(),0 ' front buffer graphics
Graphics 1024,768

Local ss[360],i,img:TImage=CreateImage(512,4)
Local r1:Float = 0.0
Local r2:Float = 0.0

SetColor 0,0,255
DrawOval 512-24,0,8,4
SetColor 255,0,255
DrawOval 512-20,0,8,4
SetColor 0,255,0
DrawOval 512-16,0,8,4
SetColor 255,255,0
DrawOval 512-12,0,8,4
SetColor 255,255,255
DrawOval 512-8,0,8,4 ' our STAR is a stretchy oval
GrabImage img,0,0 ' grab it as an image

Local fader:Float = 0 
Local rotation_vel:Float = 0
Local mx:Float = MouseX()
Local my:float = MouseX()
Local mx_vel:Float = 0
Local my_vel:Float = 0

Repeat ' {* MAIN LOOP*}
	If KeyDown(KEY_SPACE) ' hold down space for hyperspace effect
		fader:+(254-fader)*0.1
	Else ' fade to normal again
		fader:*0.99	
	EndIf 
	' multiply screen to clear or "slowly clear" for hyperspace effect ;o)
	SetTransform(0,1,1)
	SetBlend SHADEBLEND  
	SetColor(fader*0.995,255,255)
	DrawRect(0,0,1024,768)
	SetColor(255,fader*0.997,255)
	DrawRect(0,0,1024,768)
	SetColor(255,255,fader*0.999)
	DrawRect(0,0,1024,768)
	
	Local speed:Float = 0.9
	' use mouse x position to accelerate rotation
	mx_vel:+( MouseX()-mx)*((1.0-speed)^2)
	my_vel:+( MouseY()-my)*((1.0-speed)^2)
	rotation_vel:+((Int(mx_vel)|Int(my_vel))-rotation_vel)*0.01  'strange appraoch to keep x and y equal in influence, but well...
	rotation_vel:*0.99
	r1:+rotation_vel
	r2:+(r1-r2)*0.025
	mx:+mx_vel
	my:+my_vel 
	mx_vel:*speed
	my_vel:*speed
	
	SetBlend LIGHTBLEND ' all images merge
	For i=0 Until 360
		If ss[i]&gt;=12500 Or ss[i]=0 ' off edge ? choose new place to appear
			ss[i]=Rand(710,4000)
		EndIf
		Local scale:Float = ss[i]/10000.0
		Local rotation:Float = i+r1+(r2-r1)*scale
		SetTransform( rotation,scale,scale) ' set rotation and scale in one command
		scale = scale^0.5
		SetColor scale*255,scale*255,scale*255
		
		DrawImage img,512+(512-mx)+(mx-512)*scale^0.5,389+(389-my)+(my-389)*scale^0.5  ' always draw star from the center
		ss[i]:+ss[i]^2*.000002 ' tunnel warp
		'  ss[i]=ss[i]^1.0015 ' normal warp
	Next
	
	Flip 0 ' show our work
	Delay 10 ' bit of delay
Until KeyDown(KEY_ESCAPE) Or AppTerminate() ' exit on ESCAPE key or Window Exit Button
' {* END OF MAIN LOOP*}

' &gt;&gt; SIMPLE GET RANDOM NUMBER (0..a-1)
Function fnr(a)
  Return Rand(a)-1
EndFunction

</textarea><br><br>I keep altering stuff, because it's oddly relaxing and fun. <br><br></td></tr></table><br>
<a name="1298356"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Actually this is some very fine work, Taron. Later I had planned to make a type of modern-version "Star Trek" game and moving up/down/left/right when hunting down enemy ships is definitely part of the design I wanted.<br><br><a href="https://en.wikipedia.org/wiki/Star_Trek_%28text_game%29" target="_blank"><b>STAR TREK GAME</b></a>.<br><br>Here's a challenge for you. Can you make it so acceleration can stop completely and you can move through the stars using the arrow keys w the same effect as this:<br><br><a href="https://www.youtube.com/watch?v=WGJSNH6rwiA" target="_blank"><b>YOUTUBE VIDEO</b></a>.<br><br>Notice how when you hit any of the arrow keys in this game the stars move in a stretchy 3D format, similar to the tunnel method I developed above. <br><br></td></tr></table><br>
<a name="1298364"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> ROFL, Oh man... xD<br>That was fun for a moment, hihihihi... red neck space pilot! xD<br>(The Real StarFighter)<br><br>Anyway, yeah, that's a different thing but no problem either. Fun challenge. I'll take in on a bit later today. Gotta go shopping now, but yeah, GREAT! 8D<br><br>Awww man, I'm so in the mood for this now! Sweet! Thanks, D! <br><br></td></tr></table><br>
<a name="1298405"></a>

<a name="1298406"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, so, I've just given it a quick whirl, so it's far from being clever, but it might just be a start...<br><br>It doesn't rotate, yet, just move around in 3d...<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict 
Framework brl.GLMax2D 
Import brl.random 
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'All is full of stars. .  ..   .  X.. .  . .. x .. .  
'.   .  . ..   .  . ..  x   . .  .  .    ..  .  ...  .
'x.. ..  .   .    .   . .   ... . ..  . .  .  .    ..
'.  .. .  . ... .. .  . .   . ...for David by Taron  .
'.  . ..x. ...X .. . ...  .      .   ..  ..x  .  .  ..
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
AppTitle = "All is full of star  movement with A,S,W,D, Shift=up, Control=down"
SetGraphicsDriver(GLMax2DDriver())
Global w:Int = 1280
Global h:Int = 720
Global hw:Int = 0.5*w 
Global hh:Int = 0.5*h
Graphics w,h

SeedRnd(936)
'----------------------------------------------------
Type star
	Field x:Float
	Field y:Float
	Field z:Float
	Field size:Float 
	
	Method add(_s:star)
		x:+_s.x
		y:+_s.y
		z:+_s.z 
	End Method
	
	Method radd:star(_s:star)
		Return star.Create(x+_s.x,y+_s.y,z+_s.z )
	End Method
	
	Method sub(_s:star)
		x:-_s.x
		y:-_s.y
		z:-_s.z 
	End Method
	
	Method rsub:star(_s:star)
		Return star.Create(x-_s.x,y-_s.y,z-_s.z )
	End Method
	
	Method scale(_f:Float)
		x:*_f
		y:*_f
		z:*_f
	End Method
	
	Method dot:Float(_s:star)
		Return x*_s.x+y*_s.y+z*_s.z
	End Method
	
	Function Create:star(_x:Float=0,_y:Float=0, _z:Float=0, _size:Float=1.0)
		Local s:star= New star
		s.x = _x
		s.y = _y
		s.z = _z
		s.size = _size
		Return s
	End Function
EndType 

Global count:Int = 10000
Global radius:Float = 1000
Global starsystem:star[count]
For Local i:Int = 0 Until count
	starsystem[i] = star.Create(Rnd(-radius,radius),Rnd(-radius,radius),Rnd(-radius,radius),Rnd(0.5,2.0))	
Next

Type camera
	Field pos:star
	Field vel:star 
	
	Method update()
		pos.add(vel)
		vel.scale(0.99)	
	End Method
	
	Function Create:camera()
		Local c:camera = New camera
		c.pos = star.Create()
		c.vel = star.Create()
		Return c
	End Function
EndType 
Global img:TImage=CreateImage(20,20)
SetColor (255,255,255)
DrawOval (0,0,20,20) ' our STAR is a stretchy oval
GrabImage (img,0,0) ' grab it as an image

Global cam:camera = camera.Create()

Repeat
	Cls
	SeedRnd(936)

	For Local i:Int = 0 Until count
		Local s:star = starsystem[i]
		Local size:Float = s.size 
		Local zpos:Float = (s.z-cam.pos.z) 
		If(zpos&gt;1 And zpos&lt;radius)
			Local z:Float = w/zpos 
			z:*z
			Local ds:star = s.rsub(cam.pos)
			Local dist:Float = 1.0- ((ds.dot(ds))/(radius*radius))^0.25
			ds.x = hw+ds.x*z
			ds.y = hh+ds.y*z
			If(ds.x&gt;0 Or ds.x&lt;w) And (ds.y&gt;0 Or ds.y&lt;h)
				Local cd:Float = 128+dist*255
				SetColor(cd,cd,cd)
				size:*dist
				Local hsize:Float = 0.5*size
				SetTransform(0,size,size)
				DrawImage(img,ds.x,ds.y)' ,size, size)
			EndIf
		EndIf 
		Local xpos:Float = (s.x-cam.pos.x) 
		Local ypos:Float = (s.y-cam.pos.y) 
		If cam.vel.z&gt;0
			If zpos&lt;-radius Then s.z=cam.pos.z+Rnd(radius,2*radius)
		Else If zpos&gt;radius 
			s.z=cam.pos.z-Rnd(radius,2*radius)
		EndIf 
		If cam.vel.x&gt;0
			If xpos&lt;-radius Then s.x=cam.pos.x+Rnd(radius,2*radius)
		Else If xpos&gt;radius 
			s.x=cam.pos.x-Rnd(radius,2*radius)
		EndIf 
		If cam.vel.y&gt;0
			If ypos&lt;-radius  Then s.y=cam.pos.y+Rnd(radius,2*radius)
		Else If ypos&gt;radius 
			s.y=cam.pos.y-Rnd(radius,2*radius)
		EndIf 
	Next 
	If KeyDown(KEY_W) Then cam.vel.z:+0.05
	If KeyDown(KEY_S) Then cam.vel.z:-0.05
	If KeyDown(KEY_D) Then cam.vel.x:+0.05
	If KeyDown(KEY_A) Then cam.vel.x:-0.05
	If KeyDown(KEY_LCONTROL) Then cam.vel.y:+0.05
	If KeyDown(KEY_LSHIFT) Then cam.vel.y:-0.05
	cam.update()
	Flip 0
Until KeyDown(KEY_ESCAPE) Or AppTerminate()
End

Function pseudo:Float(pos:star)
	pos.x:-Int(pos.x)
	pos.y:-Int(pos.y)
	pos.z:-Int(pos.z)
	Return (Sin(pos.dot(star.Create(1.9898,78.233,37.34178))) * 43758.5453 Mod 2.0) - 1.0
End Function



</textarea> <br><br></td></tr></table><br>
<a name="1298458"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Neat, but no curious tunnel wrap-star effect like the NES "Star Voyager" has.<br><br>I'm curious to see a BlitzMAX solution to that. Really does make it interesting. If you need, I can point you to the game where you can try it Online to get a feel for the star movement.<br><br><a href="http://www.playretrogames.com/1259-star-voyager" target="_blank"><b>ONLINE PLAY</b></a>.<br><br>Pardon the spam, the game is there. Once it's up, try using the UP DOWN LEFT RIGHT arrow keys to see the unusual and unique behavior of the stars. <br><br></td></tr></table><br>
<a name="1298461"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oh, yeah, I should check that out!<br>I've just added heading to it and some more control. This is really fun!<br><br>Q increases acceleration potential<br>E decreases it<br>Mouse movement only rotates heading right now, but I might add the rest later, too. <br>It's interesting, because it's all just software 3d, while a proper GL implementation should be a heck of a lot easier and more powerful. But this is a fun challenge! <br><br>Anyway, I'll have a look for Star Voyager then! :o)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict 
Framework brl.GLMax2D 
Import brl.random 

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'All is full of stars. .  ..   .  X.. .  . .. x .. .  
'.   .  . ..   .  . ..  x   . .  .  .    ..  .  ...  .
'x.. ..  .   .    .   . .   ... . ..  . .  .  .    ..
'.  .. .  . ... .. .  . .   . ...for David by Taron  .
'.  . ..x. ...X .. . ...  .      .   ..  ..x  .  .  ..
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
AppTitle = "All is full of star  movement with A,S,W,D, Shift=up, Control=down"
SetGraphicsDriver(GLMax2DDriver())
Global w:Int = 1280
Global h:Int = 720
Global hw:Int = 0.5*w 
Global hh:Int = 0.5*h
Graphics w,h

SeedRnd(936)
'----------------------------------------------------
Type star
	Field x:Float
	Field y:Float
	Field z:Float
	Field size:Float 
	
	Method add(_s:star)
		x:+_s.x
		y:+_s.y
		z:+_s.z 
	End Method
	
	Method radd:star(_s:star)
		Return star.Create(x+_s.x,y+_s.y,z+_s.z )
	End Method
	
	Method sub(_s:star)
		x:-_s.x
		y:-_s.y
		z:-_s.z 
	End Method
	
	Method rsub:star(_s:star)
		Return star.Create(x-_s.x,y-_s.y,z-_s.z )
	End Method
	
	Method scale(_f:Float)
		x:*_f
		y:*_f
		z:*_f
	End Method
	
	Method dot:Float(_s:star)
		Return x*_s.x+y*_s.y+z*_s.z
	End Method
	
	Function Create:star(_x:Float=0,_y:Float=0, _z:Float=0, _size:Float=1.0)
		Local s:star= New star
		s.x = _x
		s.y = _y
		s.z = _z
		s.size = _size
		Return s
	End Function
EndType 

Global count:Int = 10000
Global radius:Float = 1000
Global starsystem:star[count]
For Local i:Int = 0 Until count
	starsystem[i] = star.Create(Rnd(-radius,radius),Rnd(-radius,radius),Rnd(-radius,radius),Rnd(0.5,2.0))	
Next

Type camera
	Field pos:star
	Field vel:star 
	Field mov:star 
	Method update()
		mov.y = vel.y*ry.y + vel.z*ry.z
		mov.x = vel.x*rx.x + vel.z*rz.x
		mov.z = vel.x*rx.z + vel.z*rz.z 
		pos.add(mov)
		vel.scale(0.99)	
	End Method
	
	Function Create:camera()
		Local c:camera = New camera
		c.pos = star.Create()
		c.vel = star.Create()
		c.mov = star.Create()
		Return c
	End Function
EndType 
Global img:TImage=CreateImage(20,20)
SetColor (255,255,255)
DrawOval (0,0,20,20) ' our STAR is a stretchy oval
GrabImage (img,0,0) ' grab it as an image

Global cam:camera = camera.Create()
Global relcam:star= star.Create()
Global accel:Float = 0.05

Global heading_vel:Float = 1
Global pitch_vel:Float = 0
Global heading:Float = 0
Global pitch:Float = 0
Global rx:star = star.Create(1.0,0.0,0.0,0.0)
Global ry:star = star.Create(0.0,1.0,0.0,0.0)
Global rz:star = star.Create(0.0,0.0,1.0,0.0)
Global mouse_velx:Float = MouseX()
Repeat
	Cls
	SeedRnd(936)
	heading_vel:+(MouseX()- mouse_velx)*0.01
	If heading_vel&lt;&gt;0
		heading:+heading_vel
		rx.x = Cos(heading)
		rx.z = Sin(heading)
		rz.x = rx.z
		rz.z = -rx.x 
	End If
    MoveMouse(hw,hh)
	mouse_velx = MouseX()
	heading_vel:*0.9
	For Local i:Int = 0 Until count
		
		Local so:star = starsystem[i] 
		Local s:star = star.Create(so.x,so.y,so.z,so.size)
		relcam = s.rsub(cam.pos)
		s.x= cam.pos.x+(rx.x * relcam.x + rz.x * relcam.z)
		s.z= cam.pos.z+(rx.z * relcam.x + rz.z * relcam.z)
		 		
		Local size:Float = s.size 
		relcam = s.rsub(cam.pos)
		If(relcam.z&gt;1 And relcam.z&lt;radius)
			Local z:Float = w/relcam.z 
			z:*z
			Local ds:star = star.Create(relcam.x,relcam.y,relcam.z)
			Local dist:Float = 1.0- ((ds.dot(ds))/(radius*radius))^0.25
			ds.x = hw+ds.x*z
			ds.y = hh+ds.y*z
			If(ds.x&gt;0 Or ds.x&lt;w) And (ds.y&gt;0 Or ds.y&lt;h)
				Local cd:Float = 128+dist*255
				SetColor(cd,cd,cd)
				size:*dist
				SetTransform(0,size,size)
				DrawImage(img,ds.x,ds.y)' ,size, size)
			EndIf
		EndIf 
		relcam = so.rsub(cam.pos)
		If cam.mov.z&gt;0
			If relcam.z&lt;-radius Then so.z=cam.pos.z+Rnd(radius,2*radius)
		Else If relcam.z&gt;radius 
			so.z=cam.pos.z-Rnd(radius,2*radius)
		EndIf 
		If cam.mov.x&gt;0
			If relcam.x&lt;-radius Then so.x=cam.pos.x+Rnd(radius,2*radius)
		Else If relcam.x&gt;radius 
			so.x=cam.pos.x-Rnd(radius,2*radius)
		EndIf 
		If cam.mov.y&gt;0
			If relcam.y&lt;-radius  Then so.y=cam.pos.y+Rnd(radius,2*radius)
		Else If relcam.y&gt;radius 
			so.y=cam.pos.y-Rnd(radius,2*radius)
		EndIf 
	Next 
	If KeyDown(KEY_W) Then cam.vel.z:+accel
	If KeyDown(KEY_S) Then cam.vel.z:-accel
	If KeyDown(KEY_D) Then cam.vel.x:+accel
	If KeyDown(KEY_A) Then cam.vel.x:-accel
	If KeyDown(KEY_LCONTROL) Then cam.vel.y:+accel
	If KeyDown(KEY_LSHIFT) Then cam.vel.y:-accel
	If KeyHit(KEY_Q) Then accel:*2
	If KeyHit(KEY_E) Then accel:/2
	
	cam.update()
	SetColor(200,200,200)
	SetTransform(0,1,1)
	DrawText("X:"+Int(cam.pos.x)+" Y:"+Int(cam.pos.y)+" Z:"+Int(cam.pos.z)+"  Heading:"+Int(heading) Mod 360,10,10)
	Flip 1
Until KeyDown(KEY_ESCAPE) Or AppTerminate()
End

Function pseudo:Float(pos:star)
	pos.x:-Int(pos.x)
	pos.y:-Int(pos.y)
	pos.z:-Int(pos.z)
	Return (Sin(pos.dot(star.Create(1.9898,78.233,37.34178))) * 43758.5453 Mod 2.0) - 1.0
End Function



</textarea> <br><br></td></tr></table><br>
<a name="1298570"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> I can tell you this much... 3d rotation is not that easy, if you haven't done it for a long time or never done it in such a context. It's pretty funny, actually. I feel like a total novice again, hahaha. But I shall figure it out and seek no help... my pride forces me, hahaha. &lt;shame&gt;<br><br>But it already feels incredible...I've made a few changes, especially to thrust, which really give you the space flight feeling! 8D<br>...feeling like an idiot somehow energizes me, too, hahahaha....<br><br>Man, are you a fantastic distraction, David, thank you for getting me into this! 8))))) <br><br></td></tr></table><br>
<a name="1298577"></a>

<a name="1298580"></a>

<a name="1298581"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Alright, I've got it... pffff... not sure, if it's elegant, but it works.<br>I first just rotate x and z, moving the stars into horizontal sight.<br>Then I pitch them for vertical orientation.<br>It's kinda beautiful, I think, and already fun to fly around. For my testing I made a simple little visualization of the coordinate system, orientation of the vessel, so to say.<br><br>I've updated the code to draw the stars sorted by distance, too... might be a bit slow on weaker systems?! I'm sure there is a nicer approach to this, too.<br>Ultimately this is all a bunch of nonsense, of course, because one would really want to do all this with the GPU. But it's a fun challenge anyway. 8}}}<br><br>OH, and I switched to fullscreen now, which is awesome fun for me, for sure! 8D<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict 
Framework brl.GLMax2D 
Import brl.random 

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'All is full of stars. .  ..   .  X.. .  . .. x .. .  
'.   .  . ..   .  . ..  x   . .  .  .    ..  .  ...  .
'x.. ..  .   .    .   . .   ... . ..  . .  .  .    ..
'.  .. .  . ... .. .  . .   . ...for David by Taron  .
'.  . ..x. ...X .. . ...  .      .   ..  ..x  .  .  ..
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
AppTitle = "All is full of star  movement with A,S,W,D, Shift=up, Control=down, Q,E for acceleration power"
SetGraphicsDriver(GLMax2DDriver())

Global w:Int = DesktopWidth()' 1280
Global h:Int = DesktopHeight() '720
Global hw:Int = 0.5*w 
Global hh:Int = 0.5*h
Graphics w,h,32

SeedRnd(936)
'----------------------------------------------------
Global display_list:TList = CreateList()
Type star
	Field x:Float
	Field y:Float
	Field z:Float
	Field size:Float 
	Field distance:Float 
	
	Method add(_s:star)
		x:+_s.x
		y:+_s.y
		z:+_s.z 
	End Method
	
	Method radd:star(_s:star)
		Return star.Create(x+_s.x,y+_s.y,z+_s.z )
	End Method
	
	Method sub(_s:star)
		x:-_s.x
		y:-_s.y
		z:-_s.z 
	End Method
	
	Method rsub:star(_s:star)
		Return star.Create(x-_s.x,y-_s.y,z-_s.z )
	End Method
	
	Method scale(_f:Float)
		x:*_f
		y:*_f
		z:*_f
	End Method
	
	Method dot:Float(_s:star)
		Return x*_s.x+y*_s.y+z*_s.z
	End Method
	
	Function Create:star(_x:Float=0,_y:Float=0, _z:Float=0, _size:Float=1.0)
		Local s:star= New star
		s.x = _x
		s.y = _y
		s.z = _z
		s.size = _size
		Return s
	End Function
	
	Function CompareDist:Int(o1:Object,o2:Object)
		If star(o1).distance&gt;star(o2).distance  Return 1
		Return 0
	End Function		
	
EndType 

Global info:Byte = True

Global count:Int = 10000
Global radius:Float = 1000
Global starsystem:star[count]
For Local i:Int = 0 Until count
	starsystem[i] = star.Create(Rnd(-radius,radius),Rnd(-radius,radius),Rnd(-radius,radius),Rnd(0.5,2.0))	
Next

Type camera
	Field pos:star
	Field vel:star 
	Field mov:star 
	Method update()
		mov.x:+ vel.x*rx.x + vel.y*ry.x + vel.z*rz.x
		mov.y:+ vel.x*rx.y + vel.y*ry.y + vel.z*rz.y
		mov.z:+ vel.x*rx.z + vel.y*ry.z + vel.z*rz.z 
		pos.add(mov)
		mov.scale(0.99)
		vel.scale(0.9)	
	End Method
	
	Function Create:camera()
		Local c:camera = New camera
		c.pos = star.Create()
		c.vel = star.Create()
		c.mov = star.Create()
		Return c
	End Function
EndType 
Global img:TImage=CreateImage(20,20)
SetColor (255,255,255)
DrawOval (0,0,20,20) 
GrabImage (img,0,0) 

Global cam:camera = camera.Create()
Global relcam:star= star.Create()
Global accel:Float = 0.05

Global heading_vel:Float = 1
Global pitch_vel:Float = 0
Global heading:Float = 0
Global pitch:Float = 0
Global ysin:Float = 0
Global rx:star = star.Create(1.0,0.0,0.0,0.0)
Global ry:star = star.Create(0.0,1.0,0.0,0.0)
Global rz:star = star.Create(0.0,0.0,1.0,0.0)
    MoveMouse(hw,hh)
Global mouse_velx:Float = MouseX()
Global mouse_vely:Float = MouseY()
HideMouse() 
Repeat
	Cls
	SeedRnd(936)
	heading_vel:+(MouseX()- mouse_velx)*0.01
	If heading_vel&lt;&gt;0
		heading:+heading_vel
		rx.x = Cos(heading)
		rx.z = Sin(heading)
		rx.y = 0
	End If
	heading_vel:*0.9
	pitch_vel:+(MouseY()- mouse_vely)*0.01
	If pitch_vel&lt;&gt;0
		pitch:+pitch_vel 
		ry.y = Cos(pitch)
		ysin = Sin(pitch)
	End If
	ry.x = ysin*-rx.z
	ry.z = ysin*rx.x
	rz.x = ry.y*rx.z- ry.z*rx.y
	rz.y = ry.z*rx.x- ry.x*rx.z
	rz.z = ry.x*rx.y- ry.y*rx.x
	pitch_vel:*0.9
    MoveMouse(hw,hh)
	mouse_velx = MouseX()
	mouse_vely = MouseY()
	
	SetBlend ALPHABLEND 
	display_list.Clear()
	For Local i:Int = 0 Until count
		
		Local so:star = starsystem[i] 
		Local s:star = star.Create(so.x,so.y,so.z,so.size)
		relcam = s.rsub(cam.pos)
		s.x= cam.pos.x+(rx.x * relcam.x + rx.z * relcam.z)
		s.y= cam.pos.y+relcam.y
		s.z= cam.pos.z+(rx.z * relcam.x - rx.x * relcam.z)
		 		
		relcam = s.rsub(cam.pos)
		s.x= cam.pos.x+ relcam.x
		s.y= cam.pos.y+ry.y * relcam.y- ysin*relcam.z
		s.z= cam.pos.z+ysin * relcam.y+ ry.y*relcam.z
		 		
		Local size:Float = s.size 
		relcam = s.rsub(cam.pos)
		If(relcam.z&gt;1 And relcam.z&lt;radius)
			Local z:Float = w/relcam.z 
			z:*z^0.25
			Local dist:Float = 1.0- ((relcam.dot(relcam))/(radius*radius))^0.25
			relcam.x = hw+relcam.x*z
			relcam.y = hh+relcam.y*z
			If(relcam.x&gt;0 Or relcam.x&lt;w) And (relcam.y&gt;0 Or relcam.y&lt;h) And dist&gt;0.01
				relcam.distance=dist 
				relcam.size = size 
				ListAddLast(display_list,relcam)
			EndIf
		EndIf 
		relcam = so.rsub(cam.pos)
		If cam.mov.z&gt;0
			If relcam.z&lt;-radius Then so.z=cam.pos.z+Rnd(radius,2*radius)
		Else If relcam.z&gt;radius 
			so.z=cam.pos.z-Rnd(radius,2*radius)
		EndIf 
		If cam.mov.x&gt;0
			If relcam.x&lt;-radius Then so.x=cam.pos.x+Rnd(radius,2*radius)
		Else If relcam.x&gt;radius 
			so.x=cam.pos.x-Rnd(radius,2*radius)
		EndIf 
		If cam.mov.y&gt;0
			If relcam.y&lt;-radius  Then so.y=cam.pos.y+Rnd(radius,2*radius)
		Else If relcam.y&gt;radius 
			so.y=cam.pos.y-Rnd(radius,2*radius)
		EndIf 
	Next 
	SortList(display_list,True,star.CompareDist)
	For Local s:star=EachIn display_list
		Local cd:Float = s.distance^0.5*255
		SetColor(cd,cd,cd)
		Local size:Float = s.size*(0.1+s.distance)
		SetTransform(0,size,size)
		DrawImage(img,s.x,s.y)
	Next
	
	SetBlend LIGHTBLEND 
	SetTransform(0,1,1)
	Local disp_center:star = star.Create(hw,hh)
	Local disp_x:star = disp_center.radd(star.Create(40*rx.x,40*rx.z))
	Local disp_y:star = disp_center.radd(star.Create(-40*ry.x,-40*ry.z))
	Local disp_z:star = disp_center.radd(star.Create(40*rz.x,40*rz.z))
	SetColor(0,104,0)
	DrawLine(disp_center.x,disp_center.y,disp_x.x,disp_x.y)
	SetColor(0,0,227)
	DrawLine(disp_center.x,disp_center.y,disp_y.x,disp_y.y)
	SetColor(176,0,0)
	DrawLine(disp_center.x,disp_center.y,disp_z.x,disp_z.y)

	SetTransform(0,.45,.45)
	SetColor(0,104,0)
	DrawImage(img,disp_x.x-4,disp_x.y-4)
	SetColor(0,0,227)
	DrawImage(img,disp_y.x-4,disp_y.y-4)
	SetColor(176,0,0)
	DrawImage(img,disp_z.x-4,disp_z.y-4)
	
	If KeyDown(KEY_W) Then cam.vel.z:+accel
	If KeyDown(KEY_S) Then cam.vel.z:-accel
	If KeyDown(KEY_D) Then cam.vel.x:+accel
	If KeyDown(KEY_A) Then cam.vel.x:-accel
	If KeyDown(KEY_LCONTROL) Then cam.vel.y:+accel
	If KeyDown(KEY_LSHIFT) Then cam.vel.y:-accel
	If KeyHit(KEY_Q) Then accel:*2
	If KeyHit(KEY_E) Then accel:/2
	
	cam.update()
	
	If KeyHit(KEY_TAB) Then info:~1
	
	If(info)
		SetTransform(0,1,1)
		SetColor(200,200,200)
		DrawText("X:"+Int(cam.pos.x)+" Y:"+Int(cam.pos.y)+" Z:"+Int(cam.pos.z)+"  Heading:"+Int(heading)+" Pitch:"+Int(pitch) Mod 360+"  ACCELERATION:"+Int(1000*accel),10,10)
		DrawText("visible stars:"+display_list.count(),10,30)
		EndIf 
	Flip 1
Until KeyDown(KEY_ESCAPE) Or AppTerminate()
End

Function pseudo:Float(pos:star)
	pos.x:-Int(pos.x)
	pos.y:-Int(pos.y)
	pos.z:-Int(pos.z)
	Return (Sin(pos.dot(star.Create(1.9898,78.233,37.34178))) * 43758.5453 Mod 2.0) - 1.0
End Function

</textarea> <br><br></td></tr></table><br>
<a name="1298583"></a>

<a name="1298593"></a>

<a name="1298594"></a>

<a name="1298595"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> another pretty variation...<br>...I keep playing around with this. I've tried out some fun lines between stars, too, which is curious looking. Silly gradient approach is neat in windows mode, but fullscreen the banding just makes it ugly.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict 
SuperStrict 
Framework brl.GLMax2D 
Import brl.random 

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'All is full of stars. .  ..   .  X.. .  . .. x .. .  
'.   .  . ..   .  . ..  x   . .  .  .    ..  .  ...  .
'x.. ..  .   .    .   . .   ... . ..  . .  .  .    ..
'.  .. .  . ... .. .  . .   . ...for David by Taron  .
'.  . ..x. ...X .. . ...  .      .   ..  ..x  .  .  ..
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
AppTitle = "All is full of star  movement with A,S,W,D, Shift=up, Control=down, Q,E for acceleration power"
SetGraphicsDriver(GLMax2DDriver())

Global w:Int = 1280	'	DesktopWidth()' 
Global h:Int = 720 	'	DesktopHeight()'
Global hw:Int = 0.5*w 
Global hh:Int = 0.5*h
Graphics w,h,0

SeedRnd(936)
'----------------------------------------------------
Global display_list:TList = CreateList()
Type star
	Field x:Float
	Field y:Float
	Field z:Float
	Field size:Float 
	Field distance:Float 
	Field ox:Float
	Field oy:Float
	Field oz:Float
	
	Method add(_s:star)
		x:+_s.x
		y:+_s.y
		z:+_s.z 
	End Method
	
	Method radd:star(_s:star)
		Return star.Create(x+_s.x,y+_s.y,z+_s.z )
	End Method
	
	Method sub(_s:star)
		x:-_s.x
		y:-_s.y
		z:-_s.z 
	End Method
	
	Method rsub:star(_s:star)
		Return star.Create(x-_s.x,y-_s.y,z-_s.z )
	End Method
	
	Method scale(_f:Float)
		x:*_f
		y:*_f
		z:*_f
	End Method
	
	Method dot:Float(_s:star)
		Return x*_s.x+y*_s.y+z*_s.z
	End Method
	
	Function Create:star(_x:Float=0,_y:Float=0, _z:Float=0, _size:Float=1.0)
		Local s:star= New star
		s.x = _x
		s.y = _y
		s.z = _z
		s.size = _size
		
		Return s
	End Function
	
	Function CompareDist:Int(o1:Object,o2:Object)
		If star(o1).distance&gt;star(o2).distance  Return 1
		Return 0
	End Function		
	
EndType 

Global info:Byte = True

Global count:Int = 10000
Global radius:Float = 10000
Global starsystem:star[count]
For Local i:Int = 0 Until count
	starsystem[i] = star.Create(Rnd(-radius,radius),Rnd(-radius,radius),Rnd(-radius,radius),Rnd(0.5,2.0))	
Next

Type camera
	Field pos:star
	Field vel:star 
	Field mov:star 
	Method update()
		mov.x:+ vel.x*rx.x + vel.y*ry.x + vel.z*rz.x
		mov.y:+ vel.x*rx.y + vel.y*ry.y + vel.z*rz.y
		mov.z:+ vel.x*rx.z + vel.y*ry.z + vel.z*rz.z 
		pos.add(mov)
		mov.scale(0.99)
		vel.scale(0.9)	
	End Method
	
	Function Create:camera()
		Local c:camera = New camera
		c.pos = star.Create()
		c.vel = star.Create()
		c.mov = star.Create()
		Return c
	End Function
EndType 

Global imgsize:Float = 60 
Global img:TImage=CreateImage(imgsize,imgsize)
Global imghs:Float = 0.5*imgsize 
SetBlend LIGHTBLEND 
Local ofade:Float = 1.0
For Local i:Int = 1 Until imghs 
	Local dia:Float = imghs-i
	SetColor (ofade^4*4,ofade^2*4,ofade^0.25*4)
	DrawOval (dia,dia,i*2,i*2) 
	ofade:*0.99
	ofade = ofade^1.1
Next
SetColor(255,255,255)
DrawOval(imghs-2,imghs-2,5,5)
GrabImage (img,0,0) 
Global gradient:TImage=CreateImage(1,2)
DrawRect(0,1,1,1)
GrabImage (gradient,0,0) 

Global cam:camera = camera.Create()
Global relcam:star= star.Create()
Global accel:Float = 0.05

Global heading_vel:Float = 1
Global pitch_vel:Float = 0
Global heading:Float = 0
Global pitch:Float = 0
Global ysin:Float = 0
Global rx:star = star.Create(1.0,0.0,0.0,0.0)
Global ry:star = star.Create(0.0,1.0,0.0,0.0)
Global rz:star = star.Create(0.0,0.0,1.0,0.0)
    MoveMouse(hw,hh)
Global mouse_velx:Float = MouseX()
Global mouse_vely:Float = MouseY()
HideMouse() 
Repeat
	Cls

	'SetBlend SHADEBLEND 
	'SetColor(100,110,130)
	'DrawRect(0,0,w,h)
	'SetAlpha(255)
	SeedRnd(936)
	heading_vel:+(MouseX()- mouse_velx)*0.01
	If heading_vel&lt;&gt;0
		heading:+heading_vel
		rx.x = Cos(heading)
		rx.z = Sin(heading)
		rx.y = 0
	End If
	heading_vel:*0.9
	pitch_vel:+(MouseY()- mouse_vely)*0.01
	If pitch_vel&lt;&gt;0
		pitch:+pitch_vel 
		ry.y = Cos(pitch)
		ysin = Sin(pitch)
	End If
	ry.x = ysin*-rx.z
	ry.z = ysin*rx.x
	rz.x = ry.y*rx.z- ry.z*rx.y
	rz.y = ry.z*rx.x- ry.x*rx.z
	rz.z = ry.x*rx.y- ry.y*rx.x
	pitch_vel:*0.9
    MoveMouse(hw,hh)
	mouse_velx = MouseX()
	mouse_vely = MouseY()
	SetColor(40,60,180)
	SetTransform(0,w,h*2)
	DrawImage(gradient,0,-rz.y*h)
	SetBlend ALPHABLEND 
	
	display_list.Clear()
	For Local i:Int = 0 Until count
		
		Local so:star = starsystem[i] 
		
		Local s:star = star.Create(so.x,so.y,so.z,so.size)
		relcam = s.rsub(cam.pos)
		s.x= cam.pos.x+(rx.x * relcam.x + rx.z * relcam.z)
		s.y= cam.pos.y+relcam.y
		s.z= cam.pos.z+(rx.z * relcam.x - rx.x * relcam.z)
		 		
		relcam = s.rsub(cam.pos)
		s.x= cam.pos.x+ relcam.x
		s.y= cam.pos.y+ry.y * relcam.y- ysin*relcam.z
		s.z= cam.pos.z+ysin * relcam.y+ ry.y*relcam.z
		Local size:Float = s.size 
		relcam = s.rsub(cam.pos)
		If(relcam.z&gt;1 And relcam.z&lt;radius)
			Local z:Float = (w/relcam.z)
			Local dist:Float = 1.0- ((relcam.dot(relcam))/(radius*radius))^0.25
			relcam.x = hw+relcam.x*z
			relcam.y = hh+relcam.y*z
			If(relcam.x&gt;0 Or relcam.x&lt;w) And (relcam.y&gt;0 Or relcam.y&lt;h) And dist&gt;0.001
				relcam.distance=dist 
				relcam.size = size 
				relcam.ox = so.ox
				relcam.oy = so.oy
				relcam.oz = so.oz
				ListAddLast(display_list,relcam)
				so.oz = 1
			Else
				so.oz = 0 
			EndIf
		Else
			so.oz = 0 
		EndIf 
		so.ox = relcam.x
		so.oy = relcam.y
			
		relcam = so.rsub(cam.pos)
		If cam.mov.z&gt;0
			If relcam.z&lt;-radius Then so.z=cam.pos.z+Rnd(radius,2*radius)
		Else If relcam.z&gt;radius 
			so.z=cam.pos.z-Rnd(radius,2*radius)
		EndIf 
		If cam.mov.x&gt;0
			If relcam.x&lt;-radius Then so.x=cam.pos.x+Rnd(radius,2*radius)
		Else If relcam.x&gt;radius 
			so.x=cam.pos.x-Rnd(radius,2*radius)
		EndIf 
		If cam.mov.y&gt;0
			If relcam.y&lt;-radius  Then so.y=cam.pos.y+Rnd(radius,2*radius)
		Else If relcam.y&gt;radius 
			so.y=cam.pos.y-Rnd(radius,2*radius)
		EndIf 
	Next 
	'SortList(display_list,True,star.CompareDist)
	SetBlend LIGHTBLEND 
	Local lx:Float = hw
	Local ly:Float = hh 
	For Local s:star=EachIn display_list
		Local cd:Float = s.distance^0.5*255
		SetColor(cd,cd,cd)
		Local size:Float = s.size*(0.1+s.distance)
		SetTransform(0,size,size)
		size:*imghs 
		DrawImage(img,s.x-size,s.y-size)
		'SetTransform(0,1,1)
		'SetColor(cd*s.distance^3,cd*s.distance^3,cd*s.distance^2)
		'DrawLine(lx,ly,s.x,s.y)
		'lx = s.x
		'ly = s.y 
		''If(s.oz=1) Then DrawLine(s.ox,s.oy,s.x,s.y)
	Next
	
	SetBlend LIGHTBLEND 
	SetTransform(0,1,1)
	Local disp_center:star = star.Create(hw,hh)
	Local disp_x:star = disp_center.radd(star.Create(40*rx.x,40*rx.z))
	Local disp_y:star = disp_center.radd(star.Create(-40*ry.x,-40*ry.z))
	Local disp_z:star = disp_center.radd(star.Create(40*rz.x,40*rz.z))
	SetColor(0,104,0)
	DrawLine(disp_center.x,disp_center.y,disp_x.x,disp_x.y)
	SetColor(0,0,227)
	DrawLine(disp_center.x,disp_center.y,disp_y.x,disp_y.y)
	SetColor(176,0,0)
	DrawLine(disp_center.x,disp_center.y,disp_z.x,disp_z.y)

	'SetTransform(0,.45,.45)
	'SetColor(0,104,0)
	'DrawImage(img,disp_x.x-4,disp_x.y-4)
	'SetColor(0,0,227)
	'DrawImage(img,disp_y.x-4,disp_y.y-4)
	'SetColor(176,0,0)
	'DrawImage(img,disp_z.x-4,disp_z.y-4)
	
	If KeyDown(KEY_W) Then cam.vel.z:+accel
	If KeyDown(KEY_S) Then cam.vel.z:-accel
	If KeyDown(KEY_D) Then cam.vel.x:+accel
	If KeyDown(KEY_A) Then cam.vel.x:-accel
	If KeyDown(KEY_LCONTROL) Then cam.vel.y:+accel
	If KeyDown(KEY_LSHIFT) Then cam.vel.y:-accel
	If KeyHit(KEY_Q) Then accel:*2
	If KeyHit(KEY_E) Then accel:/2
	
	cam.update()
	
	If KeyHit(KEY_TAB) Then info:~1
	
	If(info)
		SetTransform(0,1,1)
		SetColor(200,200,200)
		DrawText("X:"+Int(cam.pos.x)+" Y:"+Int(cam.pos.y)+" Z:"+Int(cam.pos.z)+"  Heading:"+Int(heading)+" Pitch:"+Int(pitch) Mod 360+"  ACCELERATION:"+Int(1000*accel),10,10)
		DrawText("visible stars:"+display_list.count(),10,30)
		EndIf 
	Flip 1
Until KeyDown(KEY_ESCAPE) Or AppTerminate()
End

Function pseudo:Float(pos:star)
	pos.x:-Int(pos.x)
	pos.y:-Int(pos.y)
	pos.z:-Int(pos.z)
	Return (Sin(pos.dot(star.Create(1.9898,78.233,37.34178))) * 43758.5453 Mod 2.0) - 1.0
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1298609"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Glad you're liking this, Taron. I gave some thought to the 3D stars last night. It seems if when you are scrolling from left to right that you 'accelerate' the stars that are on the edges and 'decelerate' them when they are closer to the center, you will be able to achieve that warped star scrolling effect as seen in Star Voyager.<br><br>Your current star code is really quite interesting. Reminds me of the effects they used in Star Trek TOS (The Original Series) when they show the main screen.<br><br>I'm experimenting with getting that warp effect myself here. <br><br></td></tr></table><br>
<a name="1298610"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sweet, yeah, I mean, if one considers those things stars, then it's all hypermegawarpspeed, hahahaha, but well. <br>I almost thought exploiting that and offering target systems near which you'd have to drop out of warp. But then you'd come by planets and all that jazz. Would be so fun, too.<br><br>But, really, at the latest then I'd want to go GPU instead of this experiment. :o}<br><br>Anyhow, my code is literally 3d stars, while the NES thing most certainly was some funny hack. Clever, though.<br><br>I was thinking about altering my camera routine to create more of a warped look, too, which could be pretty exciting.<br><br>But, yeah, can't wait to see what you'll come up with! <br><br></td></tr></table><br>
<a name="1298624"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Casaber</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Those blue stars are beutiful I could look at those all night long. It feels like you're laying on the roof and are looking around watching up towards the sky. <br><br></td></tr></table><br>
<a name="1298635"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ahaha, thanks, yeah...isn't it enjoyable!? I'm really, really having a blast with all this. The sensation of flying through them and moving around...I can't get enough of it, either.<br>But, really, it gets me giddy to think about using the gpu. Millions of stars, hehehehe... and maybe even some encounters of the third kind. :o) <br><br></td></tr></table><br>
<a name="1298638"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> Maybe not quite so good as you have it. I have the movement correct, but only 2-dimensionally. I need to work the two X &amp; Y together.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' Warp Stars, needs work
Strict
SeedRnd MilliSecs()

SetGraphicsDriver GLMax2DDriver(),0

Global img_star:TImage=CreateImage(64,64)

Global scrnmaxx=924,scrnmaxy=768
Global maxstars=1024

Local mx=scrnmaxx,my=scrnmaxy,ms,sx#,sy#,sz#
Local i,x,y,z,d,dx,dy

SetGraphicsDriver GLMax2DDriver(),0
Graphics mx,my
SetBlend lightblend

Global starx[maxstars],stary[maxstars],starz[maxstars]
Global starax[mx*1000],staray[my*1000]

For i=0 Until mx*500
  starax[mx*500-i]=mx*500-(z/100.0)^1.6
  starax[mx*500+i]=mx*500+(z/100.0)^1.6
  z:+1
Next

z=0
For i=0 Until my*500
  staray[my*500-i]=my*500-(z/100.0)^1.6
  staray[my*500+i]=my*500+(z/100.0)^1.6
  z:+1
Next

For i=1 To 32 Step 2
  colr i*8,i*8,i*8
  DrawOval i,i,64-i*2,64-i*2
Next

GrabImage img_star,0,0
MidHandleImage img_star

For i=0 Until maxstars
  starx[i]=fnr(mx*1000)
  stary[i]=fnr(my*1000)
  starz[i]=Rand(400,1000)
Next

Repeat ' {* MAIN *}

Cls
dx=0 ; dy=0
If KeyDown(key_up) Then dy=-1
If KeyDown(key_left) Then dx=-1
If KeyDown(key_right) Then dx=1
If KeyDown(key_down) Then dy=1

For i=0 Until maxstars
  z=starz[i]
  sz=starz[i]/15000.0
  SetScale sz,sz
  DrawImage img_star,starax[starx[i]]/1000,staray[stary[i]]/1000
  starx[i]:+z*dx
  stary[i]:+z*dy
  starx[i]=wrap(starx[i],0,scrnmaxx*1000-1)
  stary[i]=wrap(stary[i],0,scrnmaxy*1000-1)
Next

Flip 0
Delay 1

Until KeyDown(27) ' {* END OF MAIN *}

Function colr(r,g,b)
  SetColor r,g,b
EndFunction

Function fnr(a)
  Return Rand(a)-1
EndFunction

Function wrap(a,b,c)
Local ok
  Repeat
    ok=1
    If a&lt;b Then a=a+c-b+1 ; ok=0
    If a&gt;c Then a=a-c+b-1 ; ok=0
  Until ok=1
  Return a
EndFunction
</textarea> <br><br></td></tr></table><br>
<a name="1298668"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> Mehehehe. I think, it's a fun effect, David. Your code is puzzling me at first glance, but I'll have a closer look later on. I know what you're after and it might be a lot easier than that! It's a fairly simple warp. You have to translate screen space to create a lens warp effect. I'll have a go later on. I just fell in love with my 3d stars up there, but they, too, could look even more exciting with a nice lens distortion.<br><br>Anyway, keep at it! Maybe you'll beat me to it! 8D<br><br>Good fun, D! <br><br></td></tr></table><br>
<a name="1298673"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> HAAAAHAHA, TRY THIS:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
SetGraphicsDriver GLMax2DDriver(),0

Global w:Int = 1280
Global h:Int = 720 
Global hw:Int = 0.5*w
Global hh:Int = 0.5*h 

Graphics w,h


SetBlend LIGHTBLEND

Global img_star:TImage=CreateImage(5,5)
Cls 
SetColor(15,18,55)
DrawRect(1,1,3,3)
DrawRect(1,2,3,1)
DrawRect(2,1,1,3)
SetColor(255,255,255)
DrawRect(2,2,1,1)
GrabImage(img_star,0,0)

Global px:Float = 100000
Global py:Float = 100000
Global vx:Float = 0
Global vy:Float = 0
While Not KeyDown(KEY_ESCAPE) Or AppTerminate()
	Cls
	SetTransform(0,3,3)
	
	SeedRnd(147)
	For Local x:Int = 0 Until 50
		For Local y:Int = 0 Until 50
			Local layer:Float = Rnd()^4
			Local col:Int = 255*layer
			SetColor(col,col,col)
			layer:+0.5
			Local ppx:Int = (Rand(0,w)+px*layer) Mod w 
			Local ppy:Int = (Rand(0,h)+py*layer) Mod h 
			
			layer:*Rnd(1,3)
			SetTransform(layer,layer,layer)
			
			Local cx:Float = ppx-hw
			Local cy:Float = ppy-hh
			Local dot:Float = cx^2 + cy^2
			If(dot)
				dot = 1.0+dot/(h*h)
				ppx = hw+cx*dot
				ppy = hh+cy*dot
			EndIf 
			DrawImage(img_star,ppx,ppy)
		Next
	Next
	If KeyDown(KEY_UP) 		vy:+0.005
	If KeyDown(KEY_DOWN) 	vy:-0.005
	If KeyDown(KEY_LEFT) 	vx:+0.005
	If KeyDown(KEY_RIGHT) 	vx:-0.005
	px:+vx
	py:+vy
	vx:*0.999
	vy:*0.999
	Flip 1
WEnd

</textarea> <br><br></td></tr></table><br>
<a name="1298693"></a>

<a name="1298695"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's a slightly more elaborate version...<br>PAGEUP/DOWN to adjust warping amount<br>mouse or cursor keys for movement<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
SetGraphicsDriver GLMax2DDriver(),0

Global w:Int = 1960
Global h:Int = 1080
Global hw:Int = 0.5*w
Global hh:Int = 0.5*h 

Graphics w,h

SetBlend LIGHTBLEND

Global img_star:TImage=CreateImage(5,5)
Cls 
SetColor(15,18,55)
DrawRect(1,1,3,3);DrawRect(1,2,3,1);DrawRect(2,1,1,3)
SetColor(255,255,255)
DrawRect(2,2,1,1)
GrabImage(img_star,0,0)

Global px:Float = 100000
Global py:Float = 100000
Global vx:Float, vy:Float = 0
Global mx:Float = hw
Global my:Float = hh
MoveMouse(mx,my)
HideMouse()

Global nx:Int[2500]		'new x positions
Global ny:Int[2500] 	'new y positions 
Global ox:Int[2500] 	'old x positions
Global oy:Int[2500]		'old y positions
Global col:Int[2500]	'colors
Global tf:Float[2500] 	' transforms 

Global warp:Float = 1.0
While Not KeyDown(KEY_ESCAPE) Or AppTerminate()
	Cls
	'SetBlend SHADEBLEND; SetColor(180,180,190);	DrawRect(0,0,w,h) 'cheesy motion blur instead of cls
	
	'-------------------------  FILL ARRAYS positions, colors and transforms
	SeedRnd(147)
	For Local x:Int = 0 Until 50
		For Local y:Int = 0 Until 50
			Local mp:Int = x+y*50
			ox[mp] = nx[mp]
			oy[mp] = ny[mp]
			Local depth:Float = Rnd()^4
			col[mp]= 255*(depth+0.1)
			
			depth:+0.5
			Local ppx:Int = (Rand(0,w)+px*depth) Mod w 
			Local ppy:Int = (Rand(0,h)+py*depth) Mod h 
			
			depth:*Rnd(1,3)
			tf[mp] = depth 
			
			'----- warping 
			Local cx:Float = ppx-hw
			Local cy:Float = ppy-hh
			Local dot:Float = cx^2 + cy^2
			If(dot)
				dot = warp+dot/(h*h*warp)  
				'you can play with dot some more before multiplying! 
				'Like this:
				'	dot = dot^(1+dot)
				'Or this:
				'	dot = dot^(2-2*dot)
				ppx = hw+cx*dot   
				ppy = hh+cy*dot
			EndIf 
			
			nx[mp] = ppx
			ny[mp] = ppy
		Next
	Next
	'-------------------------  DRAW STARS
	SetBlend LIGHTBLEND
	For Local mp:Int = 0 Until 2500
			SetColor(col[mp],col[mp],col[mp])
			SetTransform(tf[mp],tf[mp],tf[mp])
			DrawImage(img_star,nx[mp]-tf[mp]*2.25,ny[mp]-tf[mp]*2.5)
	Next
	'-------------------------  DRAW LINES 
	SetTransform()
	For Local mp:Int = 0 Until 2500
			SetColor(col[mp]*0.5,col[mp]*0.6,col[mp]*0.85)
			If (ox[mp] - nx[mp])^2+(oy[mp] - ny[mp])^2&lt;4000
				DrawLine(nx[mp],ny[mp],ox[mp],oy[mp])
			EndIf 
	Next
	'-------------------------  USER INPUT
	If KeyHit(KEY_PAGEUP)	warp:+0.1
	If KeyHit(KEY_PAGEDOWN)	warp:-0.1
	
	If KeyDown(KEY_UP) 		vy:+0.005
	If KeyDown(KEY_DOWN) 	vy:-0.005
	If KeyDown(KEY_LEFT) 	vx:+0.005
	If KeyDown(KEY_RIGHT) 	vx:-0.005
	vx:-(MouseX()-mx)*0.005
	vy:-(MouseY()-my)*0.005
	MoveMouse(hw,hh)
	mx = MouseX()
	my = MouseY()
	px:+vx
	py:+vy
	vx:*0.999
	vy:*0.999
	Flip 1
WEnd

</textarea> <br><br></td></tr></table><br>
<a name="1298734"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> Even more fancified... 8}}}<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
SetGraphicsDriver GLMax2DDriver(),0

Global w:Int = 1960'DesktopWidth()  '
Global h:Int = 1080'DesktopHeight() '
Global hw:Int = 0.5*w
Global hh:Int = 0.5*h 

Graphics w,h

'--------------- create vignette image
SetBlend SHADEBLEND
Global img_vignette:TImage=CreateImage(255,255)
SetClsColor(255,255,255)
Cls
SetColor(250,250,250)
For Local i:Int = 1 Until 128
	DrawOval(128-i*1.5,128-i*1.5,i*3,i*3)
Next
GrabImage(img_vignette,0,0)

'--------------- create star image
SetBlend LIGHTBLEND
Global img_star:TImage=CreateImage(5,5)
SetClsColor(0,0,0)
Cls 
SetColor(15,18,55)
DrawRect(1,1,3,3);DrawRect(1,2,3,1);DrawRect(2,1,1,3)
SetColor(255,255,255)
DrawRect(2,2,1,1)
GrabImage(img_star,0,0)

'--------------- globals
Global px:Float = 100000
Global py:Float = 100000
Global vx:Float, vy:Float = 0
Global mx:Float = hw
Global my:Float = hh
MoveMouse(mx,my)
HideMouse()

Global nx:Float[2500]		'new x positions
Global ny:float[2500] 	'new y positions 
Global ox:float[2500] 	'old x positions
Global oy:float[2500]		'old y positions
Global col_r:Int[2500]	'colors
Global col_g:Int[2500]	'colors
Global col_b:Int[2500]	'colors
Global tf:Float[2500] 	' transforms 
Global warp:Float = 0.9

Global roll:Float = 0
Global vr:Float = 0
Global vvr:Float = 0
SetClsColor(20,40,70)
While Not KeyDown(KEY_ESCAPE) Or AppTerminate()
	Cls
	SetBlend SHADEBLEND 
	'SetColor(180,180,190);	DrawRect(0,0,w,h) 'cheesy motion blur instead of cls
	'-------------------------  DRAW VIGNETTE
	SetColor(255,255,255)
	SetTransform(0,Float(w)/255,Float(h)/255)
	DrawImage(img_vignette,0,0)	
	'-------------------------  FILL ARRAYS positions, colors and transforms
	SeedRnd(147)
	roll:+(vr-vvr)
	vvr = vr
	vr:+vx
	Local rox:Float = Cos(roll)
	Local roy:Float = Sin(roll)
	roll:*0.995
	vr:*0.9
	For Local x:Int = 0 Until 50
		For Local y:Int = 0 Until 50
			Local mp:Int = x+y*50
			ox[mp] = nx[mp]
			oy[mp] = ny[mp]
			
			Local master_rnd:Float = Rnd()^7
			
			Local depth:Float =master_rnd+0.75
			Local ppx:Float = ((Rand(0,w)+px) Mod w)-hw
			Local ppy:Float = ((Rand(0,w)+py) Mod h)-hh
			
			Local tpx:Float = (ppx*depth) 
			Local tpy:Float = (ppy*depth) 
			ppx = hw+(tpx * rox + tpy *roy)
			ppy = hh+(tpx * roy + tpy *-rox)
			
			col_r[mp]= 255*(master_rnd^1.5+0.1)
			col_g[mp]= 255*(master_rnd^1.2+0.1)
			col_b[mp]= 255*(master_rnd+0.1)
			depth:*Rnd(1,3)
			tf[mp] = depth 
			
			'----- warping 
			Local cx:Float = ppx-hw
			Local cy:Float = ppy-hh
			Local dot:Float = cx^2 + cy^2
			If(dot)
				dot = warp+dot/(h*h*warp)  
				'you can play with dot some more before multiplying! 
				'Like this:
					dot = dot^(1+dot)
				'Or this:
				'	dot = dot^(2-2*dot)
				ppx = hw+cx*dot  
				ppy = hh+cy*dot
			EndIf 
			
			nx[mp] = ppx
			ny[mp] = ppy
		Next
	Next
	'-------------------------  DRAW STARS
	SetBlend LIGHTBLEND
	For Local mp:Int = 0 Until 2500
			SetColor(col_r[mp],col_g[mp],col_b[mp])
			SetTransform(tf[mp],tf[mp],tf[mp])
			DrawImage(img_star,nx[mp]-tf[mp]*2.25,ny[mp]-tf[mp]*2.5)
	Next
	'-------------------------  DRAW LINES 
	SetTransform()
	For Local mp:Int = 0 Until 2500
			SetColor(col_r[mp]*0.5,col_g[mp]*0.6,col_b[mp]*0.85)
			If (ox[mp] - nx[mp])^2+(oy[mp] - ny[mp])^2&lt;4000
				DrawLine(nx[mp],ny[mp],ox[mp],oy[mp])
			EndIf 
	Next

	'-------------------------  USER INPUT
	If KeyHit(KEY_PAGEUP)	warp:+0.1
	If KeyHit(KEY_PAGEDOWN)	warp:-0.1
	
	If KeyDown(KEY_UP) 		vy:+0.005
	If KeyDown(KEY_DOWN) 	vy:-0.005
	If KeyDown(KEY_LEFT) 	vx:+0.005
	If KeyDown(KEY_RIGHT) 	vx:-0.005
	vx:-(MouseX()-mx)*0.005
	vy:-(MouseY()-my)*0.005
	MoveMouse(hw,hh)
	mx = MouseX()
	my = MouseY()
	px:+vx
	py:+vy
	vx:*0.999
	vy:*0.999
	Flip 1
WEnd

</textarea> <br><br></td></tr></table><br>
<a name="1298744"></a>

<a name="1298781"></a>

<a name="1298787"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#23">[#23]</a></td></tr></table></td></tr><tr ><td class="posttext"> By George, I think you've done it, Taron. Well coded !!<br><br>The warp effect of bending on the edges from rotating the ship is spot on. The first time I have ever seen that in code which mimics the unique effect from Star Voyager.<br><br>Also, how do you accelerate ?<br><br>Ready for a further challenge ? Examine the star movement and HUD in "Colony Wars."<br><br><a href="https://youtu.be/KLlwfYwKBm0?t=139" target="_blank">https://youtu.be/KLlwfYwKBm0?t=139</a><br><br>Notice the stars in Colony Wars actually have long streaks when you accelerate. <br><br></td></tr></table><br>
<a name="1298786"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#24">[#24]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm thinking up a way to move forward through those super-fake stars and have something that almost works, but it's a real brain-teaser. However, if you look at my code, you'll see at the bottom a line drawing routine, which already does what it takes to get the fundamentals of such an effect. <br><br>Anyway, I'll have a go at it later, why not! 8)<br><br>I'm almost ready to make a tiny little game with this thing, though.<br><br>OH, and THANKS, of course! Glad you like it! ;o) <br><br></td></tr></table><br>
<a name="1298795"></a>

<a name="1298796"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#25">[#25]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's a warp speed thingy that's real fun. Use PageUp and Down to move forward or backward, engaging warp, really.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
Framework brl.GLMax2D 
Import brl.random 
SetGraphicsDriver GLMax2DDriver(),0

Global w:Float = DesktopWidth()  '1960'
Global h:Float = DesktopHeight() '1080'
Global hw:Float = 0.5*w
Global hh:Float = 0.5*h 

Graphics w,h,32

'--------------- create vignette image
SetBlend SHADEBLEND
Global img_vignette:TImage=CreateImage(255,255)
SetClsColor(255,255,255)
Cls
SetColor(250,250,250)
For Local i:Int = 1 Until 128
	DrawOval(128-i*1.5,128-i*1.5,i*3,i*3)
Next
GrabImage(img_vignette,0,0)

'--------------- create star image
SetBlend LIGHTBLEND
Global img_star:TImage=CreateImage(5,5)
SetClsColor(0,0,0)
Cls 
SetColor(15,18,55)
DrawRect(1,1,3,3);DrawRect(1,2,3,1);DrawRect(2,1,1,3)
SetColor(255,255,255)
DrawRect(2,2,1,1)
GrabImage(img_star,0,0)

'--------------- globals
Global px:Float = 100000
Global py:Float = 100000
Global vx:Float, vy:Float,vz:Float = 0
Global mx:Float = hw
Global my:Float = hh
MoveMouse(mx,my)
HideMouse()

Global sx:Float[2500]	'stored x positions
Global sy:Float[2500] 	'stored y positions 
Global sz:Float[2500] 	'stored z positions 
Global nx:Float[2500]	'new x positions
Global ny:float[2500] 	'new y positions 
Global ox:Float[2500] 	'old x positions
Global oy:Float[2500]	'old y positions
Global col_r:Int[2500]	'colors
Global col_g:Int[2500]	'colors
Global col_b:Int[2500]	'colors
Global tf:Float[2500] 	'transforms 

'--------------- fill star cluster
For Local x:Int = 0 Until 50
	For Local y:Int = 0 Until 50
		Local mp:Int = x+y*50
		
		Local depth:Float =Rnd()*2
		Local ppx:Float = Rand(0,w)
		Local ppy:Float = Rand(0,w)
		
		sx[mp] = ppx
		sy[mp] = ppy
		sz[mp] = depth
	Next
Next


Global pz:Float = 0.0
Global warp:Float = 1.0

Global roll:Float = 0
Global vr:Float = 0
Global vvr:Float = 0
SetClsColor(20,40,70)
While Not KeyDown(KEY_ESCAPE) Or AppTerminate()
	Cls
	SetBlend SHADEBLEND 
	'SetColor(180,180,190);	DrawRect(0,0,w,h) 'cheesy motion blur instead of cls
	'-------------------------  DRAW VIGNETTE
	SetColor(255,255,255)
	SetTransform(0,w/255,h/255)
	DrawImage(img_vignette,0,0)	
	'-------------------------  FILL ARRAYS positions, colors and transforms
	SeedRnd(147)
	roll:+(vr-vvr)
	vvr = vr
	vr:+vx
	Local rox:Float = Cos(roll)
	Local roy:Float = Sin(roll)
	roll:*0.995
	vr:*0.9
	For Local x:Int = 0 Until 50
		For Local y:Int = 0 Until 50
			Local mp:Int = x+y*50
			ox[mp] = nx[mp]
			oy[mp] = ny[mp]
			
			sz[mp]:+vz*(sz[mp]+0.5)
			Local depth:Float =sz[mp]*0.5+1.0
			Local ppx:Float = ((sx[mp]+px) Mod w)-hw
			Local ppy:Float = ((sy[mp]+py) Mod h)-hh
			
			Local tpx:Float = (ppx*depth) 
			Local tpy:Float = (ppy*depth) 
			depth= 1.0+sz[mp]^4
			ppx = hw+(tpx * rox + tpy *roy)*depth
			ppy = hh+(tpx * roy + tpy *-rox)*depth
			depth =sz[mp]
			If(sz[mp]&gt;2.0) sz[mp]:-2.0
			If(sz[mp]&lt;0) sz[mp]:+2.0
			col_r[mp]= 255*(depth^1.5+0.1)
			col_g[mp]= 255*(depth^1.2+0.1)
			col_b[mp]= 255*(depth+0.1)
			depth:*Rnd(1,3)
			tf[mp] = depth 
			
			'----- warping 
			Local cx:Float = ppx-hw
			Local cy:Float = ppy-hh
			Local dot:Float = cx^2 + cy^2
			If(dot)
				dot = warp+dot/(h*h*warp)  
				dot = dot^(1+dot)
				ppx = hw+cx*dot   
				ppy = hh+cy*dot
			EndIf 
			
			nx[mp] = ppx
			ny[mp] = ppy
		Next
	Next
	'-------------------------  DRAW STARS
	SetBlend LIGHTBLEND
	For Local mp:Int = 0 Until 2500
			SetColor(col_r[mp],col_g[mp],col_b[mp])
			SetTransform(tf[mp],tf[mp],tf[mp])
			DrawImage(img_star,nx[mp]-tf[mp]*2.25,ny[mp]-tf[mp]*2.5)
	Next
	'-------------------------  DRAW LINES 
	SetTransform()
	For Local mp:Int = 0 Until 2500
			SetColor(col_r[mp]*0.5,col_g[mp]*0.6,col_b[mp]*0.85)
			If (ox[mp] - nx[mp])^2+(oy[mp] - ny[mp])^2&lt;4000
				DrawLine(nx[mp],ny[mp],ox[mp],oy[mp])
			EndIf 
	Next

	'-------------------------  USER INPUT
	If KeyDown(KEY_LSHIFT) 	Or KeyDown(KEY_PAGEUP)		vz:+0.00005
	If KeyDown(KEY_LCONTROL)Or KeyDown(KEY_PAGEDOWN)	vz:-0.00005
	If KeyDown(KEY_UP) 		Or KeyDown(KEY_W)			vy:+0.05
	If KeyDown(KEY_DOWN)	Or KeyDown(KEY_S) 			vy:-0.05
	If KeyDown(KEY_LEFT) 	Or KeyDown(KEY_A)			vx:+0.05
	If KeyDown(KEY_RIGHT)	Or KeyDown(KEY_D) 			vx:-0.05
	vx:-(MouseX()-mx)*0.005
	vy:-(MouseY()-my)*0.005
	MoveMouse(hw,hh)
	mx = MouseX()
	my = MouseY()
	px:+vx
	py:+vy
	warp = 0.9-vz*vz*10000
	vz:*0.99
	vx:*0.99
	vy:*0.99
	Flip 1
WEnd

</textarea> <br><br></td></tr></table><br>
<a name="1298812"></a>

<a name="1298814"></a>

<a name="1298815"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#26">[#26]</a></td></tr></table></td></tr><tr ><td class="posttext"> Really beautiful, Taron ! Might add lines that appear on the end of your stars to give 'em extra stretch when you're traveling superfast, so it looks sorta like the Star Wars warp.<br><br>If you are traveling forward, I notice some stars just disappear without getting bigger. It would be interesting to be able to 'sneak' up one of the stars and see it majestically fill the screen as you get closer and closer to it.<br><br>Also I noticed when you are in full reverse and release, your stars in the distance take a 'breath.' That is they move slightly forward, not sure if that's the cause of the directional warp which looks GREAT by the way !<br><br>Wow, and you say you are working on a game using this engine of yours now too ? Here is something else to consider. This was one of my favorite games growing up.<br><br>It is called STARSHIP. What makes it different from all other space flight games is - you could actually run into a star if you weren't careful, and it could happen many times during your game.<br><br><a href="https://www.youtube.com/watch?v=EkN6BBqiIA8" target="_blank">https://www.youtube.com/watch?v=EkN6BBqiIA8</a><br><br>The sound effects are pretty neat too. Thus the desired need for being able to play note pitches and white noise on the fly. I think Casaber was fighting this. <br><br></td></tr></table><br>
<a name="1298842"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#27">[#27]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oh, I wrote my own sound engine (real time synthesis) in blitzmax already. Works like a charm! 8)<br><br>I was meaning to put it all together at some point. A cute little game might be a nice opportunity.<br><br>Yeah, I have the Z velocity hooked to the warp, making even negative motion pull the stars. I could be a bit more careful about it, sure. <br><br>As for bringing stars closer to the camera, hmmmm...the trick I'm using here makes this a bit rough. Not sure how well it would do for the effect. I'd rather add other elements that could come close, you know. But I'll have a look. <br><br></td></tr></table><br>
<a name="1298843"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Casaber</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#28">[#28]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Taron Wow any chance to see some sound code? If so I would be interested, I'm trying myself to get some live synthesis working in Bmax but have not had much success just yet. <br><br></td></tr></table><br>
<a name="1298848"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#29">[#29]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oh, it's not nearly as hard as you'd think. I'll make a thread about it!<br><br>But first, I finally remembered some bmax standard graphics code, including setimagehandle, which I totally forgot. So this star version has no lines, but stretched stars. Kinda fun, too...<br>...I've also prepared some targeting system, hehehe<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
Framework brl.GLMax2D 
Import brl.random 
SetGraphicsDriver GLMax2DDriver(),0

Global w:Float = DesktopWidth()  '1960'
Global h:Float = DesktopHeight() '1080'
Global hw:Float = 0.5*w
Global hh:Float = 0.5*h 

Graphics w,h,32

'--------------- create vignette image
SetBlend SHADEBLEND
Global img_vignette:TImage=CreateImage(255,255)
SetClsColor(255,255,255)
Cls
SetColor(250,250,250)
For Local i:Int = 1 Until 128
	DrawOval(128-i*1.5,128-i*1.5,i*3,i*3)
Next
GrabImage(img_vignette,0,0)

'--------------- create star image
SetBlend LIGHTBLEND

Global img_star:TImage=CreateImage(5,5)
SetClsColor(0,0,0)
Cls 
SetColor(15,18,55)
DrawRect(1,1,3,3);DrawRect(1,2,3,1);DrawRect(2,1,1,3)
SetColor(255,255,255)
DrawRect(2,2,1,1)
GrabImage(img_star,0,0)
SetImageHandle(img_star,2.5,2.5)

Global img_aim:TImage=CreateImage(10,10)
Cls
SetColor(255,255,255)
DrawLine(0,0,0,9);DrawLine(9,0,9,9)
DrawLine(0,0,2,0);DrawLine(7,0,9,0)
DrawLine(0,9,2,9);DrawLine(7,9,9,9)
GrabImage(img_aim,0,0)
SetImageHandle(img_aim,5,5)

'--------------- globals
Global px:Float = 100000
Global py:Float = 100000
Global vx:Float, vy:Float,vz:Float = 0
Global mvx:Float = 0
Global mvy:Float = 0
Global mpx:Float = hw
Global mpy:Float = hh
Global tvx:Float = 0
Global tvy:Float = 0
Global tpx:Float = hw
Global tpy:Float = hh
MoveMouse(mpx,mpy)
HideMouse()

Global sx:Float[2500]	'stored x positions
Global sy:Float[2500] 	'stored y positions 
Global sz:Float[2500] 	'stored z positions 
Global nx:Float[2500]	'new x positions
Global ny:float[2500] 	'new y positions 
Global ox:Float[2500] 	'old x positions
Global oy:Float[2500]	'old y positions
Global col_r:Int[2500]	'colors
Global col_g:Int[2500]	'colors
Global col_b:Int[2500]	'colors
Global tf:Float[2500] 	'transforms 

'--------------- fill star cluster
For Local x:Int = 0 Until 50
	For Local y:Int = 0 Until 50
		Local mp:Int = x+y*50
		
		Local depth:Float =Rnd(0.0,2.0)
		Local ppx:Float = Rand(0,w)
		Local ppy:Float = Rand(0,w)
		
		sx[mp] = ppx
		sy[mp] = ppy
		sz[mp] = depth
	Next
Next


Global pz:Float = 0.0
Global warp:Float = 1.0

Global roll:Float = 0
Global vr:Float = 0
Global vvr:Float = 0
SetClsColor(20,40,70)
While Not KeyDown(KEY_ESCAPE) Or AppTerminate()
	Cls
	SetBlend SHADEBLEND 
	'SetColor(180,180,190);	DrawRect(0,0,w,h) 'cheesy motion blur instead of cls
	'-------------------------  DRAW VIGNETTE
	SetColor(255,255,255)
	SetTransform(0,w/255,h/255)
	DrawImage(img_vignette,0,0)	
	'-------------------------  FILL ARRAYS positions, colors and transforms
	SeedRnd(147)
	roll:+(vr-vvr)
	vvr = vr
	vr:+vx
	Local rox:Float = Cos(roll)
	Local roy:Float = Sin(roll)
	roll:*0.995
	vr:*0.9
	For Local x:Int = 0 Until 50
		For Local y:Int = 0 Until 50
			Local mp:Int = x+y*50
			ox[mp] = nx[mp]
			oy[mp] = ny[mp]
			
			sz[mp]:+vz*(sz[mp]+0.5)
			Local depth:Float =sz[mp]*0.5+1.0
			Local ppx:Float = ((sx[mp]+px) Mod w)-hw
			Local ppy:Float = ((sy[mp]+py) Mod h)-hh
			
			Local tpx:Float = (ppx*depth) 
			Local tpy:Float = (ppy*depth) 
			depth= 1.0+sz[mp]^3
			ppx = hw+(tpx * rox + tpy *roy)*depth
			ppy = hh+(tpx * roy + tpy *-rox)*depth
			depth:-0.7
			If(sz[mp]&gt;2.0) sz[mp]:-2.0
			If(sz[mp]&lt;0) sz[mp]:+2.0
			col_r[mp]= 255*(depth^1.5+0.1)
			col_g[mp]= 255*(depth^1.2+0.1)
			col_b[mp]= 255*(depth+0.1)
			depth:*Rnd(1.0,2.0)
			tf[mp] = depth
			
			'----- warping 
			Local cx:Float = ppx-hw
			Local cy:Float = ppy-hh
			Local dot:Float = cx^2 + cy^2
			If(dot)
				dot = warp+dot/(h*h*warp)  
				dot = dot^(1+dot)
				ppx = hw+cx*dot   
				ppy = hh+cy*dot
			EndIf 
			
			nx[mp] = ppx
			ny[mp] = ppy
		Next
	Next
	'-------------------------  DRAW STARS
	SetBlend LIGHTBLEND
	'-------------------------  DRAW LINES 
	For Local mp:Int = 0 Until 2500
			SetColor(col_r[mp],col_g[mp],col_b[mp])
			Local rx:Float = ox[mp] - nx[mp]
			Local ry:Float = oy[mp] - ny[mp]
			Local deg:Float = rx^2+ry^2
			If deg&lt;4000  
				If(deg&gt;0)
					deg = Sqr(deg) 
					Local rot:Float = ASin(rx/deg)
					If(ry&lt;0) rot = 180-rot
					SetTransform(-rot,tf[mp],tf[mp]+deg)
					
					DrawImage(img_star,nx[mp]+0.5*rx,ny[mp]+0.5*ry)
				Else
					SetColor(col_r[mp],col_g[mp],col_b[mp])
					SetTransform(0,tf[mp],tf[mp])
					DrawImage(img_star,nx[mp],ny[mp])
				EndIf 
			EndIf 
	Next

	SetColor(31,5,5)
	SetTransform(0,5,5)
	DrawImage(img_aim,mpx,mpy)
	SetColor(99,10,10)
	SetTransform(0,4,4)
	DrawImage(img_aim,tpx,tpy)
	'-------------------------  USER INPUT
	If KeyDown(KEY_LSHIFT) 	Or KeyDown(KEY_PAGEUP)		vz:+0.00005
	If KeyDown(KEY_LCONTROL)Or KeyDown(KEY_PAGEDOWN)	vz:-0.00005
	If KeyDown(KEY_UP) 		Or KeyDown(KEY_W)			vy:+0.05
	If KeyDown(KEY_DOWN)	Or KeyDown(KEY_S) 			vy:-0.05
	If KeyDown(KEY_LEFT) 	Or KeyDown(KEY_A)			vx:+0.05
	If KeyDown(KEY_RIGHT)	Or KeyDown(KEY_D) 			vx:-0.05
	mvx:+(MouseX()-mpx)*0.05;mpx:+mvx;mvx:*0.5
	mvy:+(MouseY()-mpy)*0.05;mpy:+mvy;mvy:*0.5
	
	tvx:+(MouseX()-tpx)*0.01;tpx:+tvx;tvx:*0.5
	tvy:+(MouseY()-tpy)*0.01;tpy:+tvy;tvy:*0.5
	
	
	'MoveMouse(hw,hh)
	px:+vx
	py:+vy
	If(vz&gt;0) warp = 0.9-vz*vz*10000 Else warp:+(0.9-warp)*0.01
	vz:*0.99
	vx:*0.99
	vy:*0.99
	Flip 1
WEnd

</textarea> <br><br></td></tr></table><br>
<a name="1298874"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#30">[#30]</a></td></tr></table></td></tr><tr ><td class="posttext"> Really lovely, Taron. And yes, I too am quite interested in the ability to generate raw audio in BlitzMAX. <br><br></td></tr></table><br>
<a name="1299052"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#31">[#31]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oh hey, I just had a look and I already wrote a post about it some years ago:<br><a href="http://www.blitzmax.com/Community/posts.php?topic=95529#1102097" target="_blank">http://www.blitzmax.com/Community/posts.php?topic=95529#1102097</a> <br><br></td></tr></table><br>
<a name="1299132"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#32">[#32]</a></td></tr></table></td></tr><tr ><td class="posttext"> Once again your star routine is REALLY sweet ! I noticed when you are full tilt in acceleration, when you let go, suddenly the back stars accelerate slightly forward, not seen any other time.<br><br>You fixed it where they hiccup when you are in reverse.<br><br>Taron, regarding your audio link. Pha-hew ! What a bit of coding you have there !<br><br>Can you isolate the routine that creates and plays the sound in just a few lines of code for a usable function written here ?<br><br>Also if you have the ability of creating white noise and at any pitch or duration. <br><br></td></tr></table><br>
<a name="1299220"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#33">[#33]</a></td></tr></table></td></tr><tr ><td class="posttext"> Study the buffer logic and you're good to go, but I might make a little tutorial about it. I later wrote a little bit nicer routine, I think, but it is years ago, as you can see. No telling how "cool" the routine will be now, hehehehe! 8}<br><br>Hmmmm...might be good fun to write a a tut. <br><br>As for the stars, hm, yeah...I've hooked an enemy into it for a test and it's really not there, yet, as far as I'm concerned. The first fully 3d approach is a lot nicer, I think, because you can strafe as well as rotate and have perfect orientation. This one here, though, would need some taming, I'm afraid. :P<br>THANKS, though! <br><br></td></tr></table><br>
<a name="1299239"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#34">[#34]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice thing how this "progressed" into something new. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
