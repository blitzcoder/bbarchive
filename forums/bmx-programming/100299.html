<!DOCTYPE html><html lang="en" ><head ><title >Textured Poly (again)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Textured Poly (again)</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Textured Poly (again)</a><br><br>
<a name="1183138"></a>

<a name="1183139"></a>

<a name="1183140"></a>

<a name="1183141"></a>

<a name="1183142"></a>

<a name="1183143"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I know this has been asked many many times and answers has been given, but I want a textured polygon, most importantly a triangle textured.<br>But here's the tricky part... I need the texture to NOT move around when the triangle's moved about or stretched, and I haven't found any post on the forum or code archive showing this.<br><br><a href="/post.php?topic=79234&amp;post=889717" target="_blank">CLICK HERE</a> to see a working DrawTexturedPoly function with repeating textures.<br><br>Oh and also, this will only be used in OpenGL, so the entire part of the example about D3D is useless.<br><br>UPDATE: This example will show the "problem"<br>Move your mouse around and watch as the texture stretches.<br>I can't figure out how to set the UV coords to make look good when it's stretched about.<pre class=code>Strict

SetGraphicsDriver GLMax2DDriver()
Graphics 800, 600, 0

Local img:TImage = LoadImage("test.png", FILTEREDIMAGE | MIPMAPPEDIMAGE)
Assert img Else "Oi...Where's yer samples gone?"

SetTextureRepeat(img)

Repeat
	Cls
	
	Local tileScale# = 1
	Local UMax# = 740.0 / (img.pixmaps[0].width * tileScale#)
	Local VMax# = 540.0 / (img.Pixmaps[0].height * tileScale#)
	
	'SetTextureRepeat(img)' Strictly speaking you should set and reset when needed, I spose.
	DrawTexturedPoly([30.0, 30.0, 0.0, 0.0, ..				'TL
										770.0, 30.0, UMax#, 0.0, ..			'TR
										Float(MouseX()), Float(MouseY()), UMax#, VMax# ..	'BR
						  			],  ..		'BL
										img)
	 'SetTextureRepeat(img, false)
	
	Flip
Until KeyHit(KEY_ESCAPE) Or AppTerminate()

End

' Under DX, *ALL* images drawn will have repeating textures. Generally not a problem but be forewarned.
Function SetTextureRepeat(image:TImage, rep=True)
	Assert (image.width = Pow2Size(image.width)) And (image.height = Pow2Size(image.height)),..
					"Image width and height must be a power of two"
	Local D3D7Driver:TD3D7Max2DDriver = TD3D7Max2DDriver(_max2dDriver)
	
	If rep
?Win32 
		If D3D7Driver
			'DX
			D3D7Driver.device.SetTextureStageState 0, D3DTSS_ADDRESS, D3DTADDRESS_WRAP
		Else
?
			'GL		
			glBindTexture GL_TEXTURE_2D, TGLImageFrame(image.Frame(0)).name
			glTexParameteri GL_TEXTURE_2D, GL_TEXTURE_WRAP_S, GL_REPEAT
			glTexParameteri GL_TEXTURE_2D, GL_TEXTURE_WRAP_T, GL_REPEAT
?Win32
		EndIf
?
	Else
?Win32
		If D3D7Driver
			'DX
			D3D7Driver.device.SetTextureStageState 0, D3DTSS_ADDRESS, D3DTADDRESS_CLAMP
		Else
?
			'GL
			glBindTexture GL_TEXTURE_2D, TGLImageFrame(image.Frame(0)).name
			glTexParameteri GL_TEXTURE_2D, GL_TEXTURE_WRAP_S, GL_CLAMP_TO_EDGE
			glTexParameteri GL_TEXTURE_2D, GL_TEXTURE_WRAP_T, GL_CLAMP_TO_EDGE
?Win32
		EndIf
?
	EndIf
End Function

Function DrawTexturedPoly(xyuv#[], image:TImage, frame=0, fan=1)
	Local textureWidthRatio! = Double(image.width) / Pow2Size(image.width)
	Local textureHeightRatio! = Double(image.height) / Pow2Size(image.height)
	Local hx#, hy#, ox#, oy#
	
	GetHandle(hx#, hy#)
	GetOrigin(ox#, oy#)
?Win32
	Local DXDriver:TD3D7Max2DDriver = TD3D7Max2DDriver(_max2dDriver)	
	If DXDriver
		'DX
		Local verts#[(xyuv#.length / 4) * 6]
		Local vCount
		
		For Local c=0 Until xyuv#.length Step 4
			verts#[vCount] = ((xyuv#[c] - hx#) * DXDriver.ix#) + ((xyuv#[c + 1] - hy#) * DXDriver.iy#) + ox#
			verts#[vCount + 1] = ((xyuv#[c] - hx#) * DXDriver.jx#) + ((xyuv#[c + 1] - hy#) * DXDriver.jy#) + oy#
			verts#[vCount + 2] = 0
			(Int Ptr(Float Ptr(verts#)))[vCount + 3] = DXDriver.drawcolor
			verts#[vCount + 4] = xyuv#[c + 2] * textureWidthRatio!
			verts#[vCount + 5] = xyuv#[c + 3] * textureHeightRatio!
			
			vCount :+ 6
		Next

			DXDriver.SetActiveFrame(TD3D7ImageFrame(image.Frame(frame)))
			If fan
				DXDriver.device.DrawPrimitive(D3DPT_TRIANGLEFAN, D3DFVF_XYZ | D3DFVF_DIFFUSE | D3DFVF_TEX1, verts#, xyuv#.length / 4, 0)
			Else
				DXDriver.device.DrawPrimitive(D3DPT_TRIANGLESTRIP, D3DFVF_XYZ | D3DFVF_DIFFUSE | D3DFVF_TEX1, verts#, xyuv#.length / 4, 0)
			EndIf
	Else
?
		'GL
		'Can't get at GLMax2D's transform info, so...
		Local sx#, sy#
		GetScale(sx#, sy#)
		
		glPushMatrix()
	
		glBindTexture(GL_TEXTURE_2D, TGLImageFrame(image.Frame(frame)).name)
		glEnable(GL_TEXTURE_2D)	
		
		glTranslatef(ox#, oy#, 0.0)		
		glRotatef(GetRotation(), 0.0, 0.0, 1.0)
		glScalef(sx#, sy#, 1.0) 
		
		If fan Then glBegin(GL_TRIANGLE_FAN) Else glBegin(GL_TRIANGLE_STRIP)
		
		For Local c=0 Until xyuv#.length Step 4
			glTexCoord2f(xyuv#[c + 2] * textureWidthRatio!, xyuv#[c + 3] * textureHeightRatio!)
			glVertex2f(xyuv#[c] - hx#, xyuv#[c + 1] - hy#)
		Next
		
		glEnd
	
		glDisable(GL_TEXTURE_2D)
		glPopMatrix()
?Win32
	EndIf
?
End Function

Function Pow2Size(n)
	Local t = 1
	
	While t &lt; n
		t :* 2
	Wend
	
	Return t
End Function</pre> <br><br></td></tr></table><br>
<a name="1183144"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> You can work out the uv coords relative to the screen position.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

SetGraphicsDriver GLMax2DDriver()
Graphics 800, 600, 0

Cls 
SetColor(255,255,255)
For Local x:Int = 0 To 32 Step 2
	For Local y:Int = 0 To 32 Step 2
		DrawRect( x, y, 1, 1 )
	Next
Next 
SetColor(255,0,0)
DrawLine( 0,0, 31,0 )
DrawLine( 31, 0, 31, 31 )
DrawLine( 31,31, 0, 31 )
DrawLine( 0, 31, 0 , 0 )

'Flip 

Local img:TImage = CreateImage( 32, 32 )
GrabImage( img, 0, 0 )
'Local img:TImage = LoadImage("test.png", FILTEREDIMAGE | MIPMAPPEDIMAGE)
'Assert img Else "Oi...Where's yer samples gone?"

SetTextureRepeat(img)

Local x:Float = 40
Local y:Float = 40

Repeat
	Cls
	SetColor(255,255,255)
	
	Local tileScale# = 1

		
	
	
	'SetTextureRepeat(img)' Strictly speaking you should set and reset when needed, I spose.
	DrawTexturedPoly([x, y, x / 32, y / 32, ..	'TL
					x + 400.0, y + 30.0, (x + 400.0) / 32, (y + 30.0)/32, ..			'TR
					Float(MouseX()), Float(MouseY()), Float(MouseX())/32, Float(MouseY())/32 ..	'BR
					],  ..		'BL
					img)
	 'SetTextureRepeat(img, false)
	
	Flip
	
	If ( KeyDown( KEY_LEFT ) ) x :- 1
	If ( KeyDown( KEY_RIGHT ) ) x :+ 1
	If ( KeyDown( KEY_UP ) ) y :- 1
	If ( KeyDown( KEY_DOWN ) ) y :+ 1
	
Until KeyHit(KEY_ESCAPE) Or AppTerminate()

End

' Under DX, *ALL* images drawn will have repeating textures. Generally not a problem but be forewarned.
Function SetTextureRepeat(image:TImage, rep=True)
	Assert (image.width = Pow2Size(image.width)) And (image.height = Pow2Size(image.height)),..
					"Image width and height must be a power of two"
	Local D3D7Driver:TD3D7Max2DDriver = TD3D7Max2DDriver(_max2dDriver)
	
	If rep
?Win32 
		If D3D7Driver
			'DX
			D3D7Driver.device.SetTextureStageState 0, D3DTSS_ADDRESS, D3DTADDRESS_WRAP
		Else
?
			'GL		
			glBindTexture GL_TEXTURE_2D, TGLImageFrame(image.Frame(0)).name
			glTexParameteri GL_TEXTURE_2D, GL_TEXTURE_WRAP_S, GL_REPEAT
			glTexParameteri GL_TEXTURE_2D, GL_TEXTURE_WRAP_T, GL_REPEAT
?Win32
		EndIf
?
	Else
?Win32
		If D3D7Driver
			'DX
			D3D7Driver.device.SetTextureStageState 0, D3DTSS_ADDRESS, D3DTADDRESS_CLAMP
		Else
?
			'GL
			glBindTexture GL_TEXTURE_2D, TGLImageFrame(image.Frame(0)).name
			glTexParameteri GL_TEXTURE_2D, GL_TEXTURE_WRAP_S, GL_CLAMP_TO_EDGE
			glTexParameteri GL_TEXTURE_2D, GL_TEXTURE_WRAP_T, GL_CLAMP_TO_EDGE
?Win32
		EndIf
?
	EndIf
End Function

Function DrawTexturedPoly(xyuv#[], image:TImage, frame=0, fan=1)
	Local textureWidthRatio! = Double(image.width) / Pow2Size(image.width)
	Local textureHeightRatio! = Double(image.height) / Pow2Size(image.height)
	Local hx#, hy#, ox#, oy#
	
	GetHandle(hx#, hy#)
	GetOrigin(ox#, oy#)
?Win32
	Local DXDriver:TD3D7Max2DDriver = TD3D7Max2DDriver(_max2dDriver)	
	If DXDriver
		'DX
		Local verts#[(xyuv#.length / 4) * 6]
		Local vCount
		
		For Local c=0 Until xyuv#.length Step 4
			verts#[vCount] = ((xyuv#[c] - hx#) * DXDriver.ix#) + ((xyuv#[c + 1] - hy#) * DXDriver.iy#) + ox#
			verts#[vCount + 1] = ((xyuv#[c] - hx#) * DXDriver.jx#) + ((xyuv#[c + 1] - hy#) * DXDriver.jy#) + oy#
			verts#[vCount + 2] = 0
			(Int Ptr(Float Ptr(verts#)))[vCount + 3] = DXDriver.drawcolor
			verts#[vCount + 4] = xyuv#[c + 2] * textureWidthRatio!
			verts#[vCount + 5] = xyuv#[c + 3] * textureHeightRatio!
			
			vCount :+ 6
		Next

			DXDriver.SetActiveFrame(TD3D7ImageFrame(image.Frame(frame)))
			If fan
				DXDriver.device.DrawPrimitive(D3DPT_TRIANGLEFAN, D3DFVF_XYZ | D3DFVF_DIFFUSE | D3DFVF_TEX1, verts#, xyuv#.length / 4, 0)
			Else
				DXDriver.device.DrawPrimitive(D3DPT_TRIANGLESTRIP, D3DFVF_XYZ | D3DFVF_DIFFUSE | D3DFVF_TEX1, verts#, xyuv#.length / 4, 0)
			EndIf
	Else
?
		'GL
		'Can't get at GLMax2D's transform info, so...
		Local sx#, sy#
		GetScale(sx#, sy#)
		
		glPushMatrix()
	
		glBindTexture(GL_TEXTURE_2D, TGLImageFrame(image.Frame(frame)).name)
		glEnable(GL_TEXTURE_2D)	
		
		glTranslatef(ox#, oy#, 0.0)		
		glRotatef(GetRotation(), 0.0, 0.0, 1.0)
		glScalef(sx#, sy#, 1.0) 
		
		If fan Then glBegin(GL_TRIANGLE_FAN) Else glBegin(GL_TRIANGLE_STRIP)
		
		For Local c=0 Until xyuv#.length Step 4
			glTexCoord2f(xyuv#[c + 2] * textureWidthRatio!, xyuv#[c + 3] * textureHeightRatio!)
			glVertex2f(xyuv#[c] - hx#, xyuv#[c + 1] - hy#)
		Next
		
		glEnd
	
		glDisable(GL_TEXTURE_2D)
		glPopMatrix()
?Win32
	EndIf
?
End Function

Function Pow2Size(n)
	Local t = 1
	
	While t &lt; n
		t :* 2
	Wend
	
	Return t
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1183204"></a>

<a name="1183206"></a>

<a name="1183207"></a>

<a name="1183208"></a>

<a name="1183209"></a>

<a name="1183210"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> -REMOVED (No idea how this happened?)- <br><br></td></tr></table><br>
<a name="1183211"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you matibee, you've saved my day!<br><br>Here's a "cleaned" version with _only_ OpenGL support and that automatically sets the UV coords for you.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

SetGraphicsDriver GLMax2DDriver()
Graphics 800, 600, 0

Local img:TImage = LoadImage("test.png")

SetTextureRepeat(img)

Local x:Float = GraphicsWidth() / 2
Local y:Float = GraphicsHeight() / 2

Local origX:Float
Local origY:Float

While Not KeyDown(KEY_ESCAPE)
	SetOrigin(origX, origY)
	
	If KeyDown(KEY_LEFT) Then x:-1
	If KeyDown(KEY_RIGHT) Then x:+1
	If KeyDown(KEY_UP) Then y:-1
	If KeyDown(KEY_DOWN) Then y:+1
	
	If KeyDown(KEY_W) Then origY:-1
	If KeyDown(KEY_S) Then origY:+1
	If KeyDown(KEY_A) Then origX:-1
	If KeyDown(KEY_D) Then origX:+1
	
	DrawTexturedPoly([x, y, x + Float(Sin(MilliSecs() * 0.1) * 300), y + Float(Cos(MilliSecs() * 0.1) * 200), Float(MouseX() - origX), Float(MouseY() - origY)], img, 0)
	
	Flip()
	Cls()
Wend
End

Function SetTextureRepeat(image:TImage, rep:Int = True)
	If image.width &lt;&gt; Pow2Size(image.width) Or image.height &lt;&gt; Pow2Size(image.height) Then
		Notify("Image width and height must be a power of two")
	EndIf
	If rep
		glBindTexture GL_TEXTURE_2D, TGLImageFrame(image.Frame(0)).name
		glTexParameteri GL_TEXTURE_2D, GL_TEXTURE_WRAP_S, GL_REPEAT
		glTexParameteri GL_TEXTURE_2D, GL_TEXTURE_WRAP_T, GL_REPEAT
	Else
		glBindTexture GL_TEXTURE_2D, TGLImageFrame(image.Frame(0)).name
		glTexParameteri GL_TEXTURE_2D, GL_TEXTURE_WRAP_S, GL_CLAMP_TO_EDGE
		glTexParameteri GL_TEXTURE_2D, GL_TEXTURE_WRAP_T, GL_CLAMP_TO_EDGE
	EndIf
End Function

Function DrawTexturedPoly(xyuv#[], image:TImage, frame:Int = 0, fan:Int = 1)
	Local textureWidthRatio:Double = Double(image.width) / Pow2Size(image.width)
	Local textureHeightRatio:Double = Double(image.height) / Pow2Size(image.height)
	Local hx:Float, hy:Float, ox:Float, oy:Float
	Local sx:Float, sy:Float
	
	GetHandle(hx, hy)
	GetOrigin(ox, oy)
	GetScale(sx, sy)
	
	glPushMatrix()
	
	glBindTexture(GL_TEXTURE_2D, TGLImageFrame(image.Frame(frame)).name)
	glEnable(GL_TEXTURE_2D)	
	
	glTranslatef(ox, oy, 0.0)
	glRotatef(GetRotation(), 0.0, 0.0, 1.0)
	glScalef(sx, sy, 1.0)
	
	If fan Then glBegin(GL_TRIANGLE_FAN) Else glBegin(GL_TRIANGLE_STRIP)
	
	For Local c:Int = 0 Until xyuv.length Step 2
		glTexCoord2f((xyuv[c] / image.width) * textureWidthRatio!, (xyuv[c + 1] / image.height) * textureHeightRatio!)
		glVertex2f(xyuv[c] - hx, xyuv[c + 1] - hy)
	Next
	
	glEnd()
	glDisable(GL_TEXTURE_2D)
	glPopMatrix()
End Function

Function Pow2Size:Int(n:Int)
	Local t:Int = 1
	While t &lt; n
		t:*2
	Wend
	Return(t)
End Function</textarea> <br><br></td></tr></table><br>
<a name="1183213"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice -- seems like it would work as a simple masking/light cone effect. <br><br></td></tr></table><br>
<a name="1183237"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Anyone able to get this working together with gl_polygon_smooth?<br>Or would gl_polygon_smooth not be related at all here? <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
