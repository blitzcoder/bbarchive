<!DOCTYPE html><html lang="en" ><head ><title >Target Selection</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Target Selection</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Target Selection</a><br><br>
<a name="1180279"></a>

<a name="1180280"></a>

<a name="1180281"></a>

<a name="1180282"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cruis.In</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey guys, I am trying to use a mouse click to switch targets.<br><br>I've setup a dummy image at the mouse x/y, I've converted the position to world coordinates using the mouse x/y, since I am checking against world object x/y<br><br>The collision works fine when I click. So if my mouse pointer (which has the dummy alphad image) is over an image of another object and I click, the collision is successful.<br><br>Now based on that I'd like to be able iterate through a list of objects and return the object ID of the object clicked and of course get its x/y... I've tried for eachin loop which does return the coordinates and I am 'locked' onto those coordinates whereever the target object moves to.<br><br>But when I click a different object even if of the same type....it doesn't change to that objects position.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Method SelectTarget()
	
                'dummy image
		DrawImage(testpic, mouseoffsetx, mouseoffsety)
		
		
		'Translate the MouseX and MouseY to world Coordinates
		MouseOffsetx = (Player.x + MouseX()) - screen_width/2
		MouseOffsety = (Player.y + MouseY()) - screen_height/2
		
	for local E:Enemy = eachin list	
		If GetDistance (ship.x, ship.y, X, y) &lt; 2000
			If MouseHit(1)
				If ImagesCollide2(testpic, MouseoffsetX, MouseOffsetY, 0, 0, viewfactor, viewfactor, E.Image, E.X, EY, 0 , E.angle, viewfactor, Viewfactor)
							
						targetx  = E.x
						targety  = E.y						 
				End If
											 
			End If
		End If

     'keep updating your chosen targetx/y no matter where it moves 				
	        TargetX = E.x
		TargetY = E.y

       next

			
		
					
	End Method

</textarea> <br><br></td></tr></table><br>
<a name="1180287"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cruis.In</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> It seems that the collision stops working, once there is more than one enemy object. So if I isntance an enemy object, I can click him and get my target x/y to be his own. But if I instance another one and try clicking it, the collision isn't executing, I guess thats because I am saying for each ship in the list, if collide, and since my mouse pointer with my dummy image cannot be over all the images at the same time, it is not true and therefore no collision and therefore, won't get a new set of target coords. <br><br>Now to work around that.. <br><br></td></tr></table><br>
<a name="1180337"></a>

<a name="1180338"></a>

<a name="1180339"></a>

<a name="1180340"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> there is a problem here:<br><pre class=code>
     'keep updating your chosen targetx/y no matter where it moves 				
	        TargetX = E.x
		TargetY = E.y
</pre><br><br>because its inside the for/next loop targetx and targety will always have the last enemy coordinates.<br><br>why don't you do it like this instead:<br><pre class=code>

	For Local E:Enemy = EachIn list	
		If GetDistance (ship.x, ship.y, X, y) &lt; 2000
			If MouseHit(1)
				If ImagesCollide2(testpic, MouseoffsetX, MouseOffsetY, 0, 0, viewfactor, viewfactor, E.Image, E.X, EY, 0 , E.angle, viewfactor, Viewfactor)
						
						target = E						 
				End If
											 
			End If
		End If


       Next
</pre><br><br>where target is of type enemy<br><br>also are you using strict or super strict?<br> I see an error in the imagescollide2 line. <br><br></td></tr></table><br>
<a name="1180357"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cruis.In</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> hey thanks for the reply, I am using strict, where is the error? SO i can make sure it didn't happen in my adjustment after I pasted it.<br><br>not sure how target = e gets the x/y? <br><br></td></tr></table><br>
<a name="1180359"></a>

<a name="1180361"></a>

<a name="1180365"></a>

<a name="1180371"></a>

<a name="1180372"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> you have:<br><pre class=code>
E.X, EY,
</pre><br>I think it should be:<br><pre class=code>
E.X, E.Y
</pre><br>...<br><div class="quote"> <br>not sure how target = e gets the x/y? <br> <br></div><br>target in this case is a type Enemy and instead of crating a variable for targetx and targety, you just create a variable to hold the address of the type enemy:<br><pre class=code>
field target:Enemy
</pre><br><br>target becomes a temporary place holder for the selected enemy. obviously target in this case will hold the actual enemy targeted. you are not creating new instances, you are just using a temporary variable to hold the selected object in the list.<br><br>[edited]<br><br>also target can be used to determine if an object was selected for targeting<br><br>if target = null no target has been selected<br>if target  &lt;&gt; null target has been chosen<br><br><br>[edit]<br><br>another, once you find the target, there is no need to go through the for/next loop. just use exit to exit the loop. <br><br></td></tr></table><br>
<a name="1180368"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Who was John Galt?</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Also, does the first call to mousehit not clear the hit event? May want to assign mousehit to a variable before checking it for each enemy. I may be barking up the wrong tree. Haven't run the code. <br><br></td></tr></table><br>
<a name="1180415"></a>

<a name="1180418"></a>

<a name="1180419"></a>

<a name="1180420"></a>

<a name="1180421"></a>

<a name="1180422"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cruis.In</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> <textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Field target:tenemyship


'Select Player Target
Method SelectTarget()

SetAlpha 1
DrawImage(testpic, mouseoffsetx, mouseoffsety)
DrawImage(testpic, targetx, targety)

'Translate the MouseX and MouseY to world Coordinates
MouseOffsetx = (ship.x + MouseX()) - screen_width/2
MouseOffsety = (ship.y + MouseY()) - screen_height/2

For Local E:tenemyship = EachIn ShipMasterlist

If GetDistance (x, y, E. X, E.y) &lt; 2000
If MouseHit(1)
If ImagesCollide2(testpic, MouseoffsetX, MouseOffsetY, 0, 0, viewfactor, viewfactor, E.Image, E.X, E.Y, 0 , E.angle, viewfactor, Viewfactor)

target = E

End If

End If
End If


Next


End Method</textarea><br><br>Still only gets the first one. Won't switch either. I'm keeping at it, if I've implemented it wrong inform me :)<br><br>The issue is as I am tracking, the collision won't return 'true' unless I click the first enemy I 'generate' <br><br></td></tr></table><br>
<a name="1180424"></a>

<a name="1180425"></a>

<a name="1180426"></a>

<a name="1180427"></a>

<a name="1180428"></a>

<a name="1180429"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> John Galt made an interesting point that I overlooked.<br><br>every mouse hit is stored in a sort of a stack. Every time you make a call to mousehit an imput is removed from the stack so since you are calling it from with in the for loop(which you don't need to do) if you hit the mouse only one time, the mouse hit will be removed from the stack on the first iteration of the loop. so after the first iteration(first mouse hit call)  is as if no mouse mousehit occurred. <br><br>I made some modifications, try and see if that helps. otherwise, if you don't mind sending me the code, I can help you from there. otherwise you are pretty much on your own.<br><pre class=code>
	Field target:tenemyship


	'Select Player Target
	Method SelectTarget()
	
		SetAlpha 1
		
		DrawImage(testpic, mouseoffsetx, mouseoffsety)
		
		If target
			DrawImage(testpic, target.x, target.y)
		EndIf
		
		'Translate the MouseX and MouseY to world Coordinates
		MouseOffsetx = (ship.x + MouseX()) - screen_width/2
		MouseOffsety = (ship.y + MouseY()) - screen_height/2
		
		If Not Target
			If MouseHit(1)
				For Local E:tenemyship = EachIn ShipMasterlist
				
					If GetDistance (x, y, E. X, E.y) &lt; 2000
						If ImagesCollide2(testpic, MouseoffsetX, MouseOffsetY, 0, 0, viewfactor, viewfactor, E.Image, E.X, E.Y, 0 , E.angle, viewfactor, Viewfactor)
						
							target = E
							Exit
							
						End If
					End If
				Next
			End If
		EndIf
		
		
	End Method</pre> <br><br></td></tr></table><br>
<a name="1180440"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Zeke</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> i have never liked mousehit/keyhit behavior, so i changed polledinput.bmx:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Strict

Rem
bbdoc: User input/Polled input
End Rem
Module BRL.PolledInput

ModuleInfo "Version: 1.01"
ModuleInfo "Author: Mark Sibly, Simon Armstrong"
ModuleInfo "License: zlib/libpng"
ModuleInfo "Copyright: Blitz Research Ltd"
ModuleInfo "Modserver: BRL"

ModuleInfo "History: 1.01 Release"
ModuleInfo "History: Fixed charQueue bug"

Import BRL.System
Import BRL.Graphics 'ADDED

Private

Global enabled
Global autoPoll=True
Global inputSource:Object
Global suspended,terminate

Global keyStates[256],keyHits[256]
Global charGet,charPut,charQueue[256]

Global mouseStates[4],mouseHits[4]
Global mouseLocation[4],lastMouseLocation[4]

'Added
Function ResetHITShook:Object( id:Int,data:Object,context:Object)
	MemClear(keyhits,1024) '256*4
	MemClear(mousehits,16) '4*4
End Function

Function Hook:Object( id,data:Object,context:Object )

	Local ev:TEvent=TEvent(data)
	If Not ev Return data
	
	If inputSource And inputSource&lt;&gt;ev.source Return data
	
	Select ev.id
	Case EVENT_KEYDOWN
		If Not keyStates[ev.data]
			keyStates[ev.data]=1
			keyHits[ev.data]:+1
		EndIf
	Case EVENT_KEYUP
		keyStates[ev.data]=0
	Case EVENT_KEYCHAR
		If charPut-charGet&lt;256
			charQueue[charPut &amp; 255]=ev.data
			charPut:+1
		EndIf
	Case EVENT_MOUSEDOWN
		If Not mouseStates[ev.data]
			mouseStates[ev.data]=1
			mouseHits[ev.data]:+1
		EndIf
	Case EVENT_MOUSEUP
		mouseStates[ev.data]=0
	Case EVENT_MOUSEMOVE
		mouseLocation[0]=ev.x
		mouseLocation[1]=ev.y
	Case EVENT_MOUSEWHEEL
		mouseLocation[2]:+ev.data
	Case EVENT_APPSUSPEND
		FlushKeys
		FlushMouse
		suspended=True
	Case EVENT_APPRESUME
		FlushKeys
		FlushMouse
		suspended=False
	Case EVENT_APPTERMINATE
		terminate=True
	End Select

	Return data

End Function

Public

Rem
Currently only called by Graphics/bglCreateContext.
Private for now, as it really needs a source:object parameter.
End Rem
Function EnablePolledInput( source:Object=Null )
	If enabled Return
	inputSource=source
	FlushKeys
	FlushMouse
	AddHook EmitEventHook,Hook,Null,0
	AddHook FlipHook,ResetHITShook,Null,0 'ADDED
	enabled=True
End Function

Rem
Currently only called by EndGraphics/bglDeleteContext
End Rem
Function DisablePolledInput()
	If Not enabled Return
	RemoveHook EmitEventHook,Hook
	RemoveHook FlipHook, ResetHITShook 'ADDED
	FlushKeys
	FlushMouse
	inputSource=Null
	enabled=False
End Function

Rem
bbdoc: Get app suspended state
returns: True if application is currently suspended.
End Rem
Function AppSuspended()
	If autoPoll PollSystem
	Return suspended
End Function

Rem
bbdoc: Return app terminate state
returns: True if user has requested to terminate application
End Rem
Function AppTerminate()
	If autoPoll PollSystem
	Local n=terminate
	terminate=False
	Return n
End Function

Rem
bbdoc: Check for key hit
returns: Number of times @key has been hit.
about:
The returned value represents the number of the times @key has been hit since the last
call to #KeyHit with the same @key.

See the #{key codes} module for a list of valid key codes.
End Rem
Function KeyHit( key )
	If autoPoll PollSystem
	'Local n=keyHits[key]
	'keyHits[key]=0
	'Return n
	Return keyhits[key]
End Function

Rem
bbdoc: Check for key state
returns: #True if @key is currently down
about:
See the #{key codes} module for a list of valid keycodes.
End Rem
Function KeyDown( key )
	If autoPoll PollSystem
	Return keyStates[key]
End Function

Rem
bbdoc: Get next character
returns: The character code of the next character.
about:
As the user hits keys on the keyboard, BlitzMax records the character codes of these 
keystrokes into an internal 'character queue'.

#GetChar removes the next character code from this queue and returns it the application.

If the character queue is empty, 0 is returned.
End Rem
Function GetChar()
	If autoPoll PollSystem
	If charGet=charPut Return 0
	Local n=charQueue[charGet &amp; 255]
	charGet:+1
	Return n
End Function

Rem
bbdoc: Flush key states and character queue.
about:
#FlushKeys resets the state of all keys to 'off', and resets the character queue
used by #GetChar.
End Rem
Function FlushKeys()
	PollSystem
	charGet=0
	charPut=0
	For Local i=0 Until 256 'memclear keystates,1024 FASTER
		keyStates[i]=0
		keyHits[i]=0
	Next
End Function

Rem
bbdoc: Get mouse x location
returns: Mouse x axis location
about:
The returned value is relative to the left of the screen.
end rem
Function MouseX()
	If autoPoll PollSystem
	Return mouseLocation[0]
End Function

Rem
bbdoc: Get mouse y location
returns: Mouse y axis location
about:
The returned value is relative to the top of the screen.
end rem
Function MouseY()
	If autoPoll PollSystem
	Return mouseLocation[1]
End Function

Rem
bbdoc: Get mouse wheel
returns: Mouse wheel value
about:
The mouse wheel value increments when the mouse wheel is rolled 'away' from the user, and
decrements when the mouse wheel is rolled 'towards' the user.
end rem
Function MouseZ()
	If autoPoll PollSystem
	Return mouseLocation[2]
End Function

Rem
bbdoc: Get mouse x speed
returns: Mouse x speed
End Rem
Function MouseXSpeed()
	If autoPoll PollSystem
	Local d=mouseLocation[0]-lastMouseLocation[0]
	lastMouseLocation[0]=mouseLocation[0]
	Return d
EndFunction

Rem
bbdoc: Get mouse y speed
returns: Mouse y speed
End Rem
Function MouseYSpeed()
	If autoPoll PollSystem
	Local d=mouseLocation[1]-lastMouseLocation[1]
	lastMouseLocation[1]=mouseLocation[1]
	Return d
EndFunction

Rem
bbdoc: Get mouse z speed
returns: Mouse z speed
End Rem
Function MouseZSpeed()
	If autoPoll PollSystem
	Local d=mouseLocation[2]-lastMouseLocation[2]
	lastMouseLocation[2]=mouseLocation[2]
	Return d
EndFunction

Rem
bbdoc: Flush mouse button states
about:
#FlushMouse resets the state of all mouse buttons to 'off'.
End Rem
Function FlushMouse()
	PollSystem
	For Local i=0 Until 4
		mouseStates[i]=0
		mouseHits[i]=0
	Next
	mouseLocation[2]=0
End Function

Rem
bbdoc: Check for mouse button click
returns: Number of times @button has been clicked.
about:
The returned value represents the number of the times @button has been clicked since the
last call to #MouseHit with the same @button.

@button should be 1 for the left mouse button, 2 for the right mouse button or 3 for the
middle mouse button.
End Rem
Function MouseHit( button )
	If autoPoll PollSystem
	'Local n=mouseHits[button]
	'mouseHits[button]=0
	'Return n
	Return mousehits[button]
End Function

Rem
bbdoc: Check for mouse button down state
returns: #True if @button is currently down
about:
@button should be 1 for the left mouse button, 2 for the right mouse button or 3 for the
middle mouse button.
end rem
Function MouseDown( button )
	If autoPoll PollSystem
	Return mouseStates[button]
End Function

Rem
bbdoc: Wait for a key press
returns: The keycode of the pressed key
about:
#WaitKey suspends program execution until a key has been hit. The keycode of this
key is then returned to the application.

See the #{key codes} module for a list of valid keycodes.
End Rem
Function WaitKey()
	FlushKeys
	Repeat
		WaitSystem
		For Local n=1 To 255
			If KeyHit(n) Return n
		Next
	Forever
End Function

Rem
bbdoc: Wait for a key press
returns: The character code of the pressed key
about:
#WaitChar suspends program execution until a character is available from #GetChar. This
character is then returned to the application.
End Rem
Function WaitChar()
	FlushKeys
	Repeat
		WaitSystem
		Local n=GetChar()
		If n Return n
	Forever
End Function

Rem
bbdoc: Wait for mouse button click
returns: The clicked button
about:
#WaitMouse suspends program execution until a mouse button is clicked.

#WaitMouse returns 1 if the left mouse button was clicked, 2 if the right mouse button was
clicked or 3 if the middle mouse button was clicked.
End Rem
Function WaitMouse()
	FlushMouse
	Repeat
		WaitSystem
		For Local n=1 To 3
			If MouseHit(n) Return n
		Next
	Forever
End Function
</textarea><br>this will not reset keyhit/mousehit states until Flip. <br><br></td></tr></table><br>
<a name="1180448"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Zeke.<br>I think the default Behaviour is okay as long as there is no "ResetKey/Button"-Method available. I myself am using a Keywrapper/Mousewrapper lib to get the functions I want.<br><br>It is like saying: hey someone wants to check mouse button state: it will surely care about others wanting that information too. The stack-principle can befound in multiple parts of blitzmax so it is no surprise.<br>Better have a mouse/keywrapper running each "update" to store certain variables. Additional you can store things like "last click" "last click position" etc. <br>Better than suggesting core modifications for beginners.<br><br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="1180461"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cruis.In</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey guys thanks for the help, Jesse, John, Derron, Zeke as usual. You solved it quite early on up...I had a left over piece of code from testing it which was messing up the solution. After hours of trying various stuff etc, and re-trying I hope you can forgive me that it slipped past me. Funny since it was mainly just one or two things.<br><br>Either way can work target = E or targetx targety<br><br>I somehow prefer Target = E because its way cooler :)<br><br>Also I put a check for the mouse hit and a call to the function, instead of running it in the main loop and having the mouse hit in function.<br><br>Also the mousehit thing which John rose was an issue too, remember I said it wasn't returning another collision once I had selected a target? Removing the house hit from the loop squished that.<br><br><br>@Zeke Thanks for that, I had an issue with flushmouse recently which I went into the blitz source code for and altered and it fixed it. But your re-code to not flush until flip is good. So think I'll borrow i'll use it if you don't mind :)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">	Method SelectTarget()
	
		SetAlpha 1
		
		'Translate the MouseX and MouseY to world Coordinates
		MouseOffsetx = (ship.x + MouseX() - screen_width/2) *viewfactor
		MouseOffsety = (ship.y + MouseY() - screen_height/2) *viewfactor
		
		DrawImage(testpic,  mouseoffsetx , mouseoffsety  )
		
		
			For Local E:tenemyship = EachIn ShipMasterlist
			
				If GetDistance (x, y, E. X, E.y) &lt; 2000
					If ImagesCollide2(testpic, MouseoffsetX, MouseOffsetY, 0, 0, viewfactor, viewfactor, E.Image, E.X, E.Y, 0 , E.angle, viewfactor, Viewfactor)
					
						target = E
						Exit
						
					End If
				End If
			Next
	
		
		
		
	End Method	</textarea><br><br>Since I am scaling everything based on the player position for a zooming in and out effect, I now have to adjust the formula for moving the dummy image with the mouse pointer when I scale. Although that's ringing bells in my head since the mouse pointer itself will not be affected by any scale. :)<br><br>Might have to adjust the target images to screen coords instead of the mouse pointer to world coords.<br><br>Thanks again <br><br></td></tr></table><br>
<a name="1180470"></a>

<a name="1180472"></a>

<a name="1180474"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cruis.In</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> here is a test of the issue.<br><br><pre class=code>http://speedy.sh/8JfmJ/test.zip</pre><br><br>contains three files, test.bmx, test dummy image, and two image graphics for targets. Total 33kb.<br><br><a href="http://speedy.sh/8JfmJ/test.zip" target="_blank">http://speedy.sh/8JfmJ/test.zip</a><br><br>once you 'zoom' out or in the mouseoffset x/y is no longer = to the enemy image x/y<br><br>best to just zoom out, and create one BOT. So you can see the numbers on the screen.<br><br>Press 'P' to create a bot.<br><br>thanks :) <br><br></td></tr></table><br>
<a name="1180481"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> you need to compensate for the scale when checking collision and placing objects on the screen.<br>I marked all of the changes I made hopefully you can figure it out.<br>I also modified the flushz part since my version( the newest version) of Blitzmax does not support flushz.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

AutoMidHandle True

Global defiant:		TImage = LoadImage("defiant.png")
Global birdofprey:	TImage = LoadImage("birdofprey.png")
Global testpic:		TImage = LoadImage("test.png")



Const SCREEN_WIDTH  = 800
Const SCREEN_HEIGHT = 600

Graphics SCREEN_WIDTH , SCREEN_HEIGHT

'new player
Global Ship:tShip = New tShip

'default zoom is 1 (standard scale)
Global viewfactor:Double = 1

'Mouse offset
Global mouseoffsetx
Global mouseoffsety


'Player
Type TShip

Global list:TList = CreateList()



Field X:			Double
Field Y:			Double

Field Image:		TImage = defiant
Field Angle = 0

'for drawing enemy for scaling/zooming
Field OffsetX:	Double
Field OffsetY:	Double
Field link:		TLink

'hold Enemy here
Field Target:Tenemy

	'Construct
	Method New()
	
		link = ListAddLast(list, Self)		
		
	End Method
	
	'Draw
	Method Draw()
		SetOrigin SCREEN_WIDTH / 2 - X, SCREEN_HEIGHT / 2 - Y
		
		SetScale viewfactor, viewfactor
		DrawImage(image, x, y)
		
	End Method
	
	
Rem 
the dummy image will be alphad once working, but it is used to check a collision
and based on a collision and mouse input in the real code, it'll select the image 
clicked as its current target. It works, it is just the scaling which throws things
off, which I knew would happen but having trouble compensating for it, and wondering
if it is possible to achieve with the mouse pointer, the dummy image and scaling.
End Rem	
	
	'Selects the target which your dummy image is over, which follows the mouse 
	'pointer
	Method TargetPointer()
	
		
		'Translate the MouseX and MouseY to world Coordinates
		MouseOffsetx = ship.x + ( MouseX() -X - SCREEN_WIDTH / 2 )  
		MouseOffsety = ship.y + ( MouseY() -Y - SCREEN_HEIGHT / 2 ) 
		
		SetRotation 0 
		SetScale 1,1
		DrawText "Mouse OffsetX= "+(mouseoffsetx), ship.x  - 50, ship.y + 60
		DrawText "Mouse OffsetY= "+(mouseoffsety), ship.x  - 50, ship.y + 80

		'test images
		SetScale viewfactor, viewfactor
		DrawImage(TestPic,  MouseOffsetX , MouseOffsetY  )
		
		If Target
			DrawImage(TestPic, Target.X*viewfactor, Target.Y*viewfactor) '******************************
		End If
		
		'Select target which the 'red circle' is over
		If MouseHit(1)
			For Local E:tEnemy = EachIn Tenemy.Elist
				If ImagesCollide2(testpic, MouseoffsetX, MouseOffsetY, 0, 0,viewfactor, viewfactor, E.Image,..
								 E.X*viewfactor,..   '*************************************************
								 E.Y*viewfactor,..   '*************************************************
								 0 , E.angle, viewfactor, Viewfactor)
					target = E
					
					Exit
						
				End If				
			Next
		End If	
		
	End Method	
End Type



'Enemy
Type Tenemy Extends Tship


Global Elist:	TList = CreateList()
Field IMAGE:	TImage = BirdofPrey

	
	Method draw()		
		SetScale viewfactor, viewfactor
		SetRotation 0
		DrawImage(Image, OffsetX, OffsetY)
	End Method
	
	'constructor
	Method New()
		link = ListAddLast(ELIST, Self)		
	End Method	
	
	'random placement of enemy
	Function Create()
		If KeyHit(Key_P)
		
			Local E:Tenemy = New Tenemy
			e.x= Rand(-400, -200)
			e.y= Rand(-200, 400)

		End If 
	End Function
	
	
	
	'draw update...	
	Function DrawAll()
		For Local E:tenemy = EachIn tenemy.Elist
		
			'offset required for zooming
			e.offsetx = ship.x + ((e.x - ship.x) * viewfactor)
			e.offsety = ship.y + ((e.y - ship.y) * viewfactor)
			e.draw()
			
			
			SetScale 1,1
			DrawText "Enemy X= "+Int(E.X), ship.x -50, ship.y +140
			DrawText "Enemy Y= "+Int(E.y), ship.x -50, ship.y +160
		Next
	End Function
End Type






'Zoom function
Function Zooming() 	
	
	Global MinZ:Double = 0.3
	Global MaxZ:Double = 2.0
	Global ZoomSpeed:Double = 0.05 		
	Global TargetScale:Double = 1.0	
	Global oldmz:Double '****************************
	
	TargetScale :+ (Double(MouseZ())-oldmz) * 0.07:Double   '******************************
	TargetScale =  Min( Max(TargetScale,MinZ), MaxZ) 


	Local diff:Double = (TargetScale - viewfactor) * ZoomSpeed
	
	viewfactor :+ diff 
	oldmz = Double(MouseZ()) '**********************************
	End Function



While Not KeyHit(KEY_ESCAPE)

Cls
ResetCollisions

	

	SetRotation 0

	'Control Scale
	Zooming()
	
	'Player
	Ship.Draw()
	
	
	'Enemy
	Tenemy.Create()	
	Tenemy.DrawAll()

	'Dummy Reticule used for collision checking
	Ship.TargetPointer()

	'Draw Coords Info	
	SetScale 1,1
	If ship.target
		DrawText "Target X= "+Int(ship.target.x), ship.x - 50, ship.y + 180
		DrawText "Target Y= "+Int(ship.target.y), ship.x  - 50, ship.y + 200
	End If
		
	DrawText "RealMouseX= "+MouseX(), ship.x - 50, ship.y+ 100
	DrawText "RealMouseY= "+MouseY(), ship.x - 50, ship.y + 120
	
Flip
Wend 
</textarea> <br><br></td></tr></table><br>
<a name="1180484"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cruis.In</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> that is my fault sorry...I modified the source to make a flushmousez() function.<br><br>I see you don't need it anyhow.<br><br>Ah such a mistake I should really be kicking myself, I know I have to test at rotation and scalling, and hence since my application runs on the global viewfactor (everything is scaled by it), I figured that is enough for the imagescollide2 check to satisfy its need for knowing the current scale of all images.<br><br>But because of the way its done, the objects screen coordinates are drawn different than the world coordinates using the offset, and so I need to test at their screen coord and not the world coord.<br><br>Damn probably got the whole forest/trees syndrome, been going for weeks at 4-6 hours a day lol.....<br><br>Not spotting these things.....<br><br>With your pointing out of my error, the other simple change to was<br><br>Check at the actual Offset...since that has the viewfactor included in its calculation already.<br><br>If ImagesCollide2(testpic, MouseoffsetX, MouseOffsetY, 0, 0,viewfactor, viewfactor, E.Image,..<br>								 E.OffsetX,..   '*************************************************<br>								 E.OffsetY,..  <br><br><br>If Target<br>			DrawImage(TestPic, Target.OffsetX, Target.OffsetY) '******************************<br>		End If<br><br><br>Thanks again Jesse. <br><br></td></tr></table><br>
<a name="1180489"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> haha! I am surprised I didn't see that. my defense is that I didn't look at it for more than 10 minutes. <br><br>I am glad you got it all together now. <br><br></td></tr></table><br>
<a name="1180513"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cruis.In</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> You don't need to defend yourself friend....certainly not from me!<br><br>Also the example was easy to follow yes? <br><br></td></tr></table><br>
<a name="1180522"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
