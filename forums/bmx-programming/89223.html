<!DOCTYPE html><html lang="en" ><head ><title >Blitzmax GameNet confusion.</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Blitzmax GameNet confusion.</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Blitzmax GameNet confusion.</a><br><br>
<a name="1013136"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ryan Burnside</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> <b>*** This may need to be moved into Beginner section***</b><br><br>I'm a little confused on how GameNet is used in several areas. Now I do not want to make some pie in the sky MMO game I simply would like to create a game that plays on a LAN for up to maybe 4 or so people. I think I will start by making a game in the style of TANK for the Atari 2600.<br><br>1. My first question deals with the server-client model. From the examples that come with Bmax it looks like every player must create their own "host" object. How then is data shared between computers? Is there a host that processes all clients or does everyone have equal shared processing?<br><br>2. The included examples will run multiple copies of a program on one machine but I'm not totally sure how to set up players using the necessary data needed.<br><br>3. It appears that the GameNet objects contain some field slots but I'm not quite sure how to bind these Gnet objects to my player objects. I would like to stay OOP if possible. The included example does all of the calculations outside the Gnet objects without any OOP style data storage. Everything is just sort of floating loose around the main loop.<br><br>I've not taken any networking courses at the college nor do I have much experance but I would really like to find a way to make suitable small scale games for PC. I've been using Bmax for about 3 years with great success but this online play is sort of an enigma to myself among other people. It seems an area that people just know how to do or they don't.<br><br>This is the source from the included demo. I'm still wondering the general process for synching objects and object to object interaction.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Strict

?Win32
Framework BRL.D3D7Max2D
?MacOS
Framework BRL.GLMax2D
?
Import BRL.GNet
Import BRL.BASIC
Import BRL.PNGLoader

Const GAMEPORT=12345

Const SLOT_TYPE=0
Const SLOT_NAME=1
Const SLOT_CHAT=2		
Const SlOT_SCORE=3
Const SLOT_X=4
Const SLOT_Y=5
Const SLOT_VX=6
Const SLOT_VY=7
Const SLOT_ROT=8
Const SLOT_TIMEOUT=9
Const SLOT_HIT=10

Local GWIDTH=640
Local GHEIGHT=480
Local GDEPTH=0
Local GHERTZ=30

Graphics GWIDTH,GHEIGHT,GDEPTH,GHERTZ

AutoMidHandle True
Local playerImage:TImage=LoadImage( "ship.png" )
Local bulletImage:TImage=LoadImage( "bullet1.png" )
Local warpImage:TImage=LoadImage( "sparkle.png" )

Local host:TGNetHost=CreateGNetHost()

SeedRnd MilliSecs()

Local playerName$="Player"
Local playerChat$=""
Local playerX#=Rnd(GWIDTH-64)+32
Local playerY#=Rnd(GHEIGHT-64)+32
Local playerVx#=0
Local playerVy#=0
Local playerRot#=0
Local playerScore=0
Local playerHit#=0
Local playerShot=0

'create local player
Local localPlayer:TGNetObject=CreateGNetObject( host )

SetGNetString localPlayer,SLOT_TYPE,"player"
SetGNetString localPlayer,SLOT_NAME,playerName
SetGNetString localPlayer,SLOT_CHAT,"Ready"
SetGNetFloat localPlayer,SLOT_X,playerX
SetGNetFloat localPlayer,SLOT_Y,playerY
SetGNetFloat localPlayer,SLOT_ROT,playerRot
SetGNetFloat localPlayer,SLOT_HIT,playerHit
SetGNetInt localPlayer,SLOT_SCORE,playerScore

While Not KeyHit( KEY_ESCAPE )

	Local c=GetChar()
	Select c
	Case 8
		If playerChat playerChat=playerChat[..playerChat.length-1]
	Case 13
		If playerChat
			If playerChat[..1]="/"
				Local cmd$=playerChat[1..]
				Local i=cmd.Find(" "),arg$
				If i&lt;&gt;-1
					arg=cmd[i+1..]
					cmd=cmd[..i]
				EndIf
				Select cmd.ToLower()
				Case "nick"
					If arg
						playerName=arg
						SetGNetString localPlayer,SLOT_NAME,playerName
					EndIf
				Case "listen"
					If Not GNetListen( host,GAMEPORT ) Notify "Listen failed"
				Case "connect"
					If Not arg arg="localhost"
					If Not GNetConnect( host,arg,GAMEPORT ) Notify "Connect failed"
				End Select
			Else
				SetGNetString localPlayer,SLOT_CHAT,playerChat
			EndIf
			playerChat=""
		EndIf
	Default
		If c&gt;31 And c&lt;127 playerChat:+Chr(c)
	End Select
	
	If KeyDown( KEY_LEFT )
		playerRot:-5
		If playerRot&lt;-180 playerRot:+360
		SetGNetFloat localPlayer,SLOT_ROT,playerRot
	Else If KeyDown( KEY_RIGHT )
		playerRot:+5
		If playerRot&gt;=180 playerRot:-360
		SetGNetFloat localPlayer,SLOT_ROT,playerRot
	EndIf
	
	If KeyDown( KEY_UP )
		playerVx:+Cos(playerRot)*.15
		playerVy:+Sin(playerRot)*.15
	Else
		playerVx:*.99
		If Abs(playerVx)&lt;.1 playerVx=0
		playerVy:*.99
		If Abs(playerVy)&lt;.1 playerVy=0
	EndIf
	
	If playerVx
		playerX:+playerVx
		If playerX&lt;-8 playerX:+GWIDTH+16 Else If playerX&gt;=GWIDTH+8 playerX:-GWIDTH+16
		SetGNetFloat localPlayer,SLOT_X,playerX
	EndIf
	
	If playerVy
		playerY:+playerVy
		If playerY&lt;-8 playerY:+GHEIGHT+16 Else If playerY&gt;=GHEIGHT+8 playerY:-GHEIGHT+16
		SetGNetFloat localPlayer,SLOT_Y,playerY
	EndIf
	
	If playerShot playerShot:-1
	
	If KeyHit( KEY_LALT ) And Not playerShot
		Local obj:TGnetObject=CreateGNetObject( host )
		SetGNetString obj,SLOT_TYPE,"bullet"
		SetGNetFloat obj,SLOT_X,playerX
		SetGNetFloat obj,SLOT_Y,playerY
		SetGNetFloat obj,SLOT_VX,playerVx+Cos(playerRot)*10
		SetGNetFloat obj,SLOT_VY,playerVy+Sin(playerRot)*10
		SetGNetInt obj,SLOT_TIMEOUT,60
		playerShot=5
	EndIf
	
	'update bullets
	For Local obj:TGNetObject=EachIn GNetObjects( host )

		If obj.State()=GNET_CLOSED Continue
	
		Local typ$=GetGNetString( obj,SLOT_TYPE )
		If typ&lt;&gt;"bullet" Continue

		Local x#=GetGNetFloat( obj,SLOT_X )
		Local y#=GetGNetFloat( obj,SLOT_Y )
		
		If GNetObjectRemote( obj )
			'remote bullet? Check for collision...
			Local dx#=x-playerX,dy#=y-playerY
			If dx*dx+dy*dy&lt;256'144
				Local msg:TGNetObject=CreateGNetMessage( host )
				If playerHit
					SetGNetString msg,SLOT_TYPE,"gotme"
				Else
					SetGNetString msg,SLOT_TYPE,"hurtme"
					playerHit=1
				EndIf
				SendGNetMessage msg,obj
			EndIf
		Else
			'local bullet? Update...
			Local t=GetGNetInt( obj,SLOT_TIMEOUT )
			
			If Not t
				CloseGNetObject obj
				Continue
			EndIf

			Local vx#=GetGNetFloat( obj,SLOT_VX )
			Local vy#=GetGNetFloat( obj,SLOT_VY )
			
			Local dx#=x-GWIDTH/2
			Local dy#=y-GHEIGHT/2
			Local rot#=ATan2(dy,dx)
			Local accel#=1/(dx*dx+dy*dy)*2000
			vx:-Cos(rot)*accel
			vy:-Sin(rot)*accel
			x:+vx
			y:+vy

			SetGNetFloat obj,SLOT_X,x
			SetGNetFloat obj,SLOT_Y,y
			SetGNetFloat obj,SLOT_VX,vx
			SetGNetFloat obj,SLOT_VY,vy
			SetGNetInt obj,SLOT_TIMEOUT,t-1
		EndIf
	Next
	
	If playerHit
		playerHit:-.05
		If playerHit&lt;0 playerHit=0
		SetGNetFloat localPlayer,SLOT_HIT,playerHit
	EndIf
	
	GNetSync host
	
	For Local msg:TGNetObject=EachIn GNetMessages( host )
		Local typ$=GetGNetString( msg,SLOT_TYPE )
		Select typ
		Case "gotme","hurtme"
			Local obj:TGNetObject=GNetMessageObject(msg)
			If obj.State()&lt;&gt;GNET_CLOSED
				If typ="hurtme" 
					playerScore:+1
					SetGNetInt localPlayer,SLOT_SCORE,playerScore
				EndIf
				CloseGNetObject obj
			EndIf
		End Select
	Next
	
	Cls
	
	Local ty
	For Local obj:TGNetObject=EachIn GNetObjects( host )

		If obj.State()=GNET_CLOSED Continue

		Local typ$=GetGNetString( obj,SLOT_TYPE )
		Local x#=GetGNetFloat( obj,SLOT_X )
		Local y#=GetGNetFloat( obj,SLOT_Y )
		Select typ
		Case "bullet"
			SetBlend LIGHTBLEND
			SetColor 255,255,255
			DrawImage bulletImage,x,y
			SetBlend MASKBLEND
		Case "player"
			Local rot#=GetGNetFloat( obj,SLOT_ROT )
			Local name$=GetGNetString( obj,SLOT_NAME )
			Local chat$=GetGNetString( obj,SLOT_CHAT )
			Local score=GetGNetInt( obj,SLOT_SCORE )
			Local hit#=GetGNetFloat( obj,SLOT_HIT )
			SetRotation rot
			SetColor 255,255,255
			DrawImage playerImage,x,y
			If hit 
				SetAlpha hit
				SetBlend LIGHTBLEND
				DrawImage playerImage,x,y
				SetBlend MASKBLEND
				SetAlpha 1
				SetColor 255,255,255
			EndIf
			SetRotation 0
			DrawText name+":"+score,x,y+16
			If obj=localPlayer SetColor 255,255,255 Else SetColor 0,128,255
			DrawText name+":"+chat,0,ty
			ty:+16
		End Select
	Next
	
	If playerChat
		SetColor 255,255,0
		DrawText "&gt;"+playerChat,0,GHEIGHT-16
		SetColor 0,255,0
		DrawRect TextWidth("&gt;"+playerChat),GHEIGHT-16,8,16
	EndIf
	
	SetColor 255,255,255
	Local txt$="MemAllocd:"+GCMemAlloced()
	DrawText txt,GWIDTH-TextWidth(txt),0
	
	SetBlend LIGHTBLEND
	SetRotation Rnd(360)
	SetScale Rnd(2,2.125),Rnd(2,2.125)
	DrawImage warpImage,GWIDTH/2,GHEIGHT/2
	SetScale 1,1
	SetRotation 0
	SetBlend MASKBLEND
	
	Flip

Wend

CloseGNetHost host
</textarea> <br><br></td></tr></table><br>
<a name="1013186"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dreamora</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> gnet automagically syncs all fields on gnet objects, no control over what and to where.<br><br>thats why its normally avoided by most users, as it generates massive overheads where not even remotely needed <br><br></td></tr></table><br>
<a name="1013274"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ryan Burnside</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, so has anyone written a concise Blitz tutorial aimed at somebody like myself who has no prior online programming experience? I suppose the second library mentioned with sockets would be helpful since you say GameNet is not the best tool. <br><br></td></tr></table><br>
<a name="1013338"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bremer</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Leadwerks made some network code available recently that might be something that could get you going.<br><br><a href="http://blitzmax.com/codearcs/codearcs.php?code=2639" target="_blank">http://blitzmax.com/codearcs/codearcs.php?code=2639</a><br><br>It seems to have the features you are looking for. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
