<!DOCTYPE html><html lang="en" ><head ><title >Screen rotation</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Screen rotation</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Screen rotation</a><br><br>
<a name="807624"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> This might be of use to somebody.<br>Although DX only I'm sure the OGL equivalent is very similar.<br>If anybody wants to clean it up, add to it or comment feel free. <br>I haven't put it in the Code Archives yet as I'm not too sure how much use it is in a real situation. I haven't put too much thought into it either.<br>Thanks to Noel for his Math3D lib in the Code Archives.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import Brl.Math

Strict

Const RAD_TO_DEGREE! = 180!/Pi
Const DEGREE_TO_RAD! = Pi / 180!
Const gw:Float = 800.0
Const gh:Float=600.0
Graphics gw , gh
SetViewport -(gw/2),-(gh/2),gw*2,gh*2
Local depth:Float = 2.0
Local scale :Float = 1.0
Local angle:Float=5.0
Local image1:timage = LoadImage("ground.png")
Local image2:timage=LoadImage("max.png")
Local backup_matrix:matrix=matrix.create(2.0/gw,0.0,0.0,0.0,..
									0.0,( - 2.0 / gh) , 0.0 , 0.0 , ..
									0.0,0.0,(2.0/2.0),0.0,..
		 							 - 1.0 - (1.0 / gw) , 1.0 + (1.0 / gh) , 1.0 , 1.0)
Local world_matrix:matrix=matrix.create(2.0/gw,0.0,0.0,0.0,..
									0.0,( - 2.0 / gh) , 0.0 , 0.0 , ..
									0.0,0.0,(2.0/2.0),0.0,..
		 							 - 1.0 - (1.0 / gw) , 1.0 + (1.0 / gh) , 1.0 , 1.0)

Local transform_z_left:matrix = matrix.create(Cos(angle) , Sin(angle), 0.0 , 0.0 , ..
									-Sin(angle), Cos(angle), 0.0 , 0.0 , ..
									0.0 , 0.0 , 1.0 , 0.0 , .. 
									0.0 , 0.0 , 0.0 , 1.0)
									
Local transform_z_right:matrix = transform_z_left.transpose()
										
setmatrix(world_matrix)		
While Not KeyHit(KEY_ESCAPE)
	Cls
	If KeyDown(KEY_SPACE)
		world_matrix = backup_matrix
		SetScale 1.0,1.0
		setmatrix(world_matrix)
	EndIf
	TileImage image1 , 0 , 0
	DrawImage image2 , mousex(),mousey()
	If KeyDown(KEY_LEFT) 
		world_matrix = world_matrix.transformmat(transform_z_left) 
		setmatrix(world_matrix)
	EndIf
	If KeyDown(KEY_RIGHT)
		world_matrix = world_matrix.transformmat(transform_z_right) 
		setmatrix(world_matrix)
	EndIf
	If KeyDown(KEY_UP)
		SetScale 2.0 , 2.0
	EndIf
	If KeyDown(key_down)
		SetScale 0.5 , 0.5
	EndIf
	Flip
Wend

Function setmatrix(in:matrix)
		Local change_matrix:Float[]
		change_matrix=[in.m00,in.m01,in.m02,in.m03,in.m10,in.m11,in.m12,in.m13,in.m20,in.m21,in.m22,in.m23,in.m30,in.m31,in.m32,in.m33]
		D3D7GraphicsDriver().Direct3DDevice7().SetTransform(D3DTS_PROJECTION , change_matrix)
End Function

Function DegreeToRad:Float( d:Float )
	Return d*DEGREE_TO_RAD
End Function

Function RadToDegree!( r! )
	Return r*RAD_TO_DEGREE
End Function

' 4x4 Matrix
Type Matrix
	Field m00# = 1, m01# = 0, m02# = 0, m03# = 0
	Field m10# = 0, m11# = 1, m12# = 0, m13# = 0
	Field m20# = 0, m21# = 0, m22# = 1, m23# = 0
	Field m30# = 0, m31# = 0, m32# = 0, m33# = 1
	Function Create:matrix(in00# , in01# , in02# , in03# , in10# , in11# , in12# , in13# , in20# , in21# , in22# , in23# , ..
		in30#,in31# , in32# , in33#)
		Local temp_matrix:matrix = New matrix
			temp_matrix.m00=in00
			temp_matrix.m01=in01
			temp_matrix.m02=in02
			temp_matrix.m03=in03
			temp_matrix.m10=in10
			temp_matrix.m11=in11
			temp_matrix.m12=in12
			temp_matrix.m13=in13
			temp_matrix.m20=in20
			temp_matrix.m21=in21
			temp_matrix.m22=in22
			temp_matrix.m23=in23
			temp_matrix.m30=in30
			temp_matrix.m31=in31
			temp_matrix.m32=in32
			temp_matrix.m33 = in33
		Return temp_matrix
	End Function
	
	Method Copy:Matrix( )
		Local i:Matrix = New Matrix
		MemCopy( Varptr i.m00, Varptr m00, 64 )
		Return i
	End Method
	
	Method Translate( x#, y#, z# )
		m03 = x
		m13 = y
		m23 = z
	End Method
	
	Method Scale( x#, y#, z# )
		m00 :* x
		m10 :* x
		m20 :* x
		
		m01 :* y
		m11 :* y
		m21 :* y
		
		m02 :* z
		m12 :* z
		m22 :* z
	End Method
	
	Method TransformMat:Matrix( i:Matrix )
		Local r:Matrix = New Matrix
		
		r.m00 = m00 * i.m00 + m01 * i.m10 + m02 * i.m20 + m03 * i.m30
		r.m01 = m00 * i.m01 + m01 * i.m11 + m02 * i.m21 + m03 * i.m31
		r.m02 = m00 * i.m02 + m01 * i.m12 + m02 * i.m22 + m03 * i.m32
		r.m03 = m00 * i.m03 + m01 * i.m13 + m02 * i.m23 + m03 * i.m33
		
		r.m10 = m10 * i.m00 + m11 * i.m10 + m12 * i.m20 + m13 * i.m30
		r.m11 = m10 * i.m01 + m11 * i.m11 + m12 * i.m21 + m13 * i.m31
		r.m12 = m10 * i.m02 + m11 * i.m12 + m12 * i.m22 + m13 * i.m32
		r.m13 = m10 * i.m03 + m11 * i.m13 + m12 * i.m23 + m13 * i.m33
		
		r.m20 = m20 * i.m00 + m21 * i.m10 + m22 * i.m20 + m23 * i.m30
		r.m21 = m20 * i.m01 + m21 * i.m11 + m22 * i.m21 + m23 * i.m31
		r.m22 = m20 * i.m02 + m21 * i.m12 + m22 * i.m22 + m23 * i.m32
		r.m23 = m20 * i.m03 + m21 * i.m13 + m22 * i.m23 + m23 * i.m33
		
		r.m30 = m30 * i.m00 + m31 * i.m10 + m32 * i.m20 + m33 * i.m30
		r.m31 = m30 * i.m01 + m31 * i.m11 + m32 * i.m21 + m33 * i.m31
		r.m32 = m30 * i.m02 + m31 * i.m12 + m32 * i.m22 + m33 * i.m32
		r.m33 = m30 * i.m03 + m31 * i.m13 + m32 * i.m23 + m33 * i.m33
		
		Return r
	End Method
		
	Method Add:Matrix( i:Matrix )
		Local a:Float Ptr = GetPtr( )
		Local b:Float Ptr = GetPtr( )
		Local r:Matrix = New Matrix
		Local c:Float Ptr = r.GetPtr( )
		For Local n:Int = 0 To 15
			c[n]=a[n]+b[n]
		Next
		Return r
	End Method
	
	Method Subtract:Matrix( i:Matrix )
	 Local a:Float Ptr = GetPtr( )
		Local b:Float Ptr = GetPtr( )
		Local r:Matrix = New Matrix
		Local c:Float Ptr = r.GetPtr( )
		For Local n:Int = 0 To 15
			c[n]=a[n]-b[n]
		Next
		Return r
	End Method
	
	
	Method Transpose:Matrix( )
	 Local x:Int,y:Int
	 Local r:Matrix = New Matrix
	 Local a:Float Ptr = GetPtr( )
	 Local b:Float Ptr = r.GetPtr( )
	 For x = 0 To 3
		 For y = 0 To 3
			 b[x*4+y] = a[y*4+x]
		 Next
	 Next
	 Return r
	End Method
	
	Method GetPtr:Float Ptr( )
		Return Varptr m00
	End Method
	
	Method ToArray:Float[]( )
		Local r:Float[16]
		MemCopy( Varptr r[0], GetPtr( ), 64 )
		Return r
	End Method
	
	Function FromArray:Matrix( arr:Float[] )
		Return FromPtr( Varptr arr[0] )
	End Function
	
	Function FromPtr:Matrix( arr:Float Ptr )
		Local r:Matrix = New Matrix
		Local p:Float Ptr = r.GetPtr( )
		MemCopy( p, arr, 64 )
		Return r
	End Function
End Type

</textarea><br><br>&lt;edit&gt; The viewport and setscale could be slicker plus mouse movement would need adjusting. However, it's the screen rotation I was most interested in doing. <br><br></td></tr></table><br>
<a name="807654"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Wow this is pretty complicated, it's much easier in OpenGL ;-D<br><br>Cool tho. <br><br></td></tr></table><br>
<a name="807696"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Wow this is pretty complicated, it's much easier in OpenGL ;-D<br> <br></div><br>That's good to hear. Can you add the Bmax GL driver version of the same thing to this post so it's in the same place? <br><br></td></tr></table><br>
<a name="807771"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Since I dont have DX/Windows I dont know exactly what your code is doing. Are you just trying to allow the game content to be displayed at like 90 degress so the user has to stand their monitor on its side in order to see a tall view of the game? <br><br></td></tr></table><br>
<a name="807859"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >xlsior</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Wow! *very* nice one!<br><br>Thanks for sharing! <br><br></td></tr></table><br>
<a name="807879"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote">  Since I dont have DX/Windows I dont know exactly what your code is doing <br></div><br>It's rotating the projection matrix so the viewed screen and anything drawn on it is rotated. <br><br></td></tr></table><br>
<a name="807914"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> neat thanks!  My screen must have non-square pixels even though I'm in 1024z768 as I'm sure it warps a bit as it spins. <br><br></td></tr></table><br>
<a name="807924"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> ... I think it does as well unless it's an optical illusion. <br><br></td></tr></table><br>
<a name="808021"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok.<br><br>Yes, if you rotate the general `orientation` of the display then you have to switch around your aspect ratio otherwise it will be out of proportion and you'll get the stretching that Grey Alien is seeing. So instead of doing like width/height you have to do height/width.<br><br>And you also should not rotate the projection matrix if you are using OpenGL - it will create texturing and lighting problems later. The texturing/lighting systems do not take into account the rotation or position of the camera. You should rotate or translate the MODELVIEW matrix instead. Move the game world in front of the camera, do not move the projection itself. The projection matrix is meant to ONLY be used to define the `projection` - the conversion of 3D coordinates to flat 2D coordinates. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
