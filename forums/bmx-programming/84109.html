<!DOCTYPE html><html lang="en" ><head ><title >Superfast blur</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Superfast blur</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Superfast blur</a><br><br>
<a name="949960"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hitori</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I found this box blur argorithm:<br><pre class=code>
// Super Fast Blur v1.1
// by Mario Klingemann &lt;http://incubator.quasimondo.com&gt;
//
// Tip: Multiple invovations of this filter with a small 
// radius will approximate a gaussian blur quite well.
//
BImage a;
BImage b;

void setup()
{

  a=loadImage("dog.jpg");
  size(a.width, a.height);
  b=new BImage(a.width, a.height);
  fill(255);
  noStroke();
  framerate(25);

}

void loop()
{
  System.arraycopy(a.pixels,0,b.pixels,0,a.pixels.length);
  fastblur(b,mouseY/8);
  image(b, 0, 0);
}

void fastblur(BImage img,int radius){

  if (radius&lt;1){
    return;
  }
  int w=img.width;
  int h=img.height;
  int wm=w-1;
  int hm=h-1;
  int wh=w*h;
  int div=radius+radius+1;
  int r[]=new int[wh];
  int g[]=new int[wh];
  int b[]=new int[wh];
  int rsum,gsum,bsum,x,y,i,p,p1,p2,yp,yi,yw;
  int vmin[] = new int[max(w,h)];
  int vmax[] = new int[max(w,h)];
  int[] pix=img.pixels;
  int dv[]=new int[256*div];
  for (i=0;i&lt;256*div;i++){
     dv[i]=(i/div); 
  }
  
  yw=yi=0;
 
  for (y=0;y&lt;h;y++){
    rsum=gsum=bsum=0;
    for(i=-radius;i&lt;=radius;i++){
      p=pix[yi+min(wm,max(i,0))];
      rsum+=(p &amp; 0xff0000)&gt;&gt;16;
      gsum+=(p &amp; 0x00ff00)&gt;&gt;8;
      bsum+= p &amp; 0x0000ff;
   }
    for (x=0;x&lt;w;x++){
    
      r[yi]=dv[rsum];
      g[yi]=dv[gsum];
      b[yi]=dv[bsum];

      if(y==0){
        vmin[x]=min(x+radius+1,wm);
        vmax[x]=max(x-radius,0);
       } 
       p1=pix[yw+vmin[x]];
       p2=pix[yw+vmax[x]];

      rsum+=((p1 &amp; 0xff0000)-(p2 &amp; 0xff0000))&gt;&gt;16;
      gsum+=((p1 &amp; 0x00ff00)-(p2 &amp; 0x00ff00))&gt;&gt;8;
      bsum+= (p1 &amp; 0x0000ff)-(p2 &amp; 0x0000ff);
      yi++;
    }
    yw+=w;
  }
  
  for (x=0;x&lt;w;x++){
    rsum=gsum=bsum=0;
    yp=-radius*w;
    for(i=-radius;i&lt;=radius;i++){
      yi=max(0,yp)+x;
      rsum+=r[yi];
      gsum+=g[yi];
      bsum+=b[yi];
      yp+=w;
    }
    yi=x;
    for (y=0;y&lt;h;y++){
      pix[yi]=0xff000000 | (dv[rsum]&lt;&lt;16) | (dv[gsum]&lt;&lt;8) | dv[bsum];
      if(x==0){
        vmin[y]=min(y+radius+1,hm)*w;
        vmax[y]=max(y-radius,0)*w;
      } 
      p1=x+vmin[y];
      p2=x+vmax[y];

      rsum+=r[p1]-r[p2];
      gsum+=g[p1]-g[p2];
      bsum+=b[p1]-b[p2];

      yi+=w;
    }
  }

}
</pre><br>and i wanted to convert it to blitzmax code,here is what i've done:<br><pre class=code>
SuperStrict
Graphics 800,600
Global a:TPixmap
Global b:TPixmap
a=LoadPixmap("dog.PNG")
b=fastblur(a,3)
DrawImage(LoadImage(b),0,0)
Repeat
Flip
Until KeyDown(key_enter)

Function fastblur:TPixmap(img:TPixmap,radius:Int)
	If radius&lt;1 Return img
	Local blur:TPixmap=CopyPixmap(img)
	Local w:Int=blur.width
	Local h:Int=blur.height
	Local wm:Int=w-1
	Local hm:Int=h-1
	Local wh:Int=w*h
	Local div:Int=radius+radius+1
	Local r:Int[]=New Int[wh]
	Local g:Int[]=New Int[wh]
	Local b:Int[]=New Int[wh]
	Local rsum:Int,gsum:Int,bsum:Int,x:Int,y:Int,y1:Int,y2:Int,i:Int,p:Int,p1:Int,p2:Int,yp:Int,yi:Int,yw:Int
	Local vmin:Int[]=New Int[Max(w,h)]
	Local vmax:Int[]=New Int[Max(w,h)]
	Local pix:Byte Ptr=blur.pixels
	Local dv:Int[]=New Int[256*div]
	For i=0 To 256*div-1
		dv[i]=(i/div)
	Next
	yw=0
	yi=0
	For y=0 To h-1
		rsum=0
		gsum=0
		bsum=0
		For i=-radius To radius
			p=blur.ReadPixel((yi+Min(wm,Max(i,0))) Mod blur.width,Floor((yi+Min(wm,Max(i,0)))/blur.width))
			Print rsum
			Print gsum
			Print bsum
			Print (p And $ff000000) Shr 24
			rsum:+(p And $ff0000) Shr 16
			gsum:+(p And $00ff00) Shr 8
			bsum:+p And $0000ff
		Next
		For x=0 To w-1
			r[yi]=dv[rsum]
			g[yi]=dv[gsum]
			b[yi]=dv[bsum]
			If y=0
				vmin[x]=Min(x+radius+1,wm)
				vmax[x]=Max(x-radius,0)
			EndIf
			p1=blur.ReadPixel((yw+vmin[x]) Mod blur.width,Floor((yw+vmin[x])/blur.width))
			p2=blur.ReadPixel((yw+vmax[x]) Mod blur.width,Floor((yw+vmax[x])/blur.width))
			rsum:+((p1 And $ff0000)-(p2 And $ff0000)) Shr 16
			gsum:+((p1 And $00ff00)-(p2 And $00ff00)) Shr 8
			bsum:+(p1 And $0000ff)-(p2 And $0000ff)
			yi:+1
		Next
		For x=0 To w-1
			rsum=0
			gsum=0
			bsum=0
			yp=-radius*w
			For i=-radius To radius
				yi=Max(0,yp)+x
				rsum:+r[yi]
				gsum:+g[yi]
				bsum:+b[yi]
				yp:+w
			Next
			yi=x
			For y1=0 To h-1
				blur.WritePixel(yi Mod blur.width,Floor(yi/blur.width),$ff000000 + (dv[rsum] Shl 16) + (dv[gsum] Shl 8) + dv[bsum])
				If x=0
					vmin[y1]=Min(y1+radius+1,hm)*w
					vmax[y1]=Max(y1-radius,0)*w
				EndIf
				p1=x+vmin[y1]
				p2=x+vmax[y1]
				rsum:+r[p1]-r[p2]
				gsum:+g[p1]-g[p2]
				gsum:+b[p1]-b[p2]
				yi:+w
			Next
		Next
	Next
	Return blur
EndFunction
</pre><br>But,no matter what i try,it doesn't work.<br>Once it gives me boundary error,other time only fills image blue.<br>Can anyone take a look at this and show me what am i doing wrong ? <br><br></td></tr></table><br>
<a name="949998"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Robert Cummings</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> thats not fast blur, its slow for 3D hardware. fast blur would be to draw 4 copies of the image offset with alpha / 4 <br><br></td></tr></table><br>
<a name="950020"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah you're looking at doing a blur on a pixmap, with the cpu doing all the processing.<br><br>I did not follow the code, but it's not going to be fast, as Rob said. <br><br></td></tr></table><br>
<a name="950051"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> just so you know the "&amp;" and "|" are the same in bmax and C. <br><br></td></tr></table><br>
<a name="1079223"></a>

<a name="1079224"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamRedwoods</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sorry to dig this up, but it's good to have around.<br><br>Couple things wrong with the above code. That first for-next that was encapsulating too much, and the bit math (p1-p2) would get overflow problems, so you have to work the mask in before doing subtraction or addition.<br><br>I created my own port here, with alpha (dont forget to use setblend):<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Function FastBlurPixmap:TPixmap(pix:TPixmap, rad:Int=5)
	''// Super Fast Blur v1.1
	''// by Mario Klingemann &lt;http://incubator.quasimondo.com&gt;
        ''port to bmax by adamredwoods
	
	Local destpix:TPixmap = CreatePixmap(pix.width, pix.height,PF_RGBA8888)
	If Not pix.width Return  destpix
	If rad&lt;1 Then Return destpix

	Local wmax:Int=pix.width-1
	Local hmax:Int=pix.height-1
	Local wh:Int=pix.width*pix.height
	Local div:Int=rad+rad+1
	Local r:Int[]=New Int[wh+1]
	Local g:Int[]=New Int[wh+1]
	Local b:Int[]=New Int[wh+1]
	Local a:Int[]=New Int[wh+1]
	Local rsum:Int,gsum:Int,bsum:Int,x:Int,y:Int,i:Int,p:Int,p1:Int,p2:Int,yp:Int,yi:Int=0,yw:Int=0
	Local asum:Int
	Local vmin:Int[] = New Int[Max(pix.width,pix.height)]
	Local vmax:Int[] = New Int[Max(pix.width,pix.height)]
	Local pixptr:Int Ptr=Int Ptr(pix.pixelptr(0,0) )
	Local destptr:Int Ptr = Int Ptr(destpix.pixelptr(0,0) )
	Local dv:Int[]=New Int[256*div+1]
  	
	'' i/rad^2 division lookup table
	For Local i:Int=0 To 256*div
     	dv[i]=(i/div)
	Next

	'' create lookup tables
	For y = 0 To pix.height-1
		rsum=0
		gsum=0
		bsum=0; asum=0
		For i = -rad To rad
			p=pixptr[Min(wmax,Max(i+yi,0)) ] ''yi+Min(wmax,Max(i,0))
			asum:+ (p Shr 24 ) &amp; $000000ff
			rsum:+ (p Shr 16 ) &amp; $000000ff
			gsum:+ (p Shr 8 ) &amp; $000000ff
			bsum:+ (p ) &amp; $000000ff
		Next
		
		For x = 0 To pix.width-1
			r[yi] = dv[rsum]
			g[yi] = dv[gsum]
			b[yi] = dv[bsum]
			a[yi] = dv[asum]
			If y=0
				vmin[x] = Min(x+rad+1,wmax)
				vmax[x] = Max(x-rad,0)
			EndIf
			p1 = pixptr[yw+vmin[x]]
			p2 = pixptr[yw+vmax[x]]

			Local m:Int = $000000ff
			asum :+ (p1 Shr 24 &amp; m) - (p2 Shr 24 &amp; m)
			rsum :+ (p1 Shr 16 &amp; m) - (p2 Shr 16 &amp; m)
			gsum :+ (p1 Shr 8 &amp; m) - (p2 Shr 8 &amp; m)
			bsum :+ (p1 &amp; m) - (p2 &amp; m)

			yi:+1
		Next
		
		yw :+ pix.width
	Next


	''
	For x =0 To pix.width-1
		rsum=0; gsum=0; bsum=0; asum=0
		yp = -rad*pix.width
		For i=-rad To rad
			yi = Max(0,yp)+x
			asum :+ a[yi]
			rsum :+ r[yi]
			gsum :+ g[yi]
			bsum :+ b[yi]
			yp:+ pix.width
		Next
		yi = x
		For y=0 To pix.height-1
			destptr[yi] = (dv[asum] Shl 24) | (dv[rsum] Shl 16) | (dv[gsum] Shl 8) | dv[bsum]
			If x=0
				vmin[y] = Min(y+rad+1,hmax)*pix.width
				vmax[y] = Max(y-rad,0)*pix.width
			EndIf
			p1 = x+vmin[y]
			p2 = x+vmax[y]
			
			asum :+ a[p1]-a[p2]
			rsum :+ r[p1]-r[p2]
			gsum :+ g[p1]-g[p2]
			bsum :+ b[p1]-b[p2]
			
			yi:+pix.width
		Next
	Next

	Return destpix
EndFunction
</textarea><br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1079281"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> It's good to have around indeed :)<br>Thanks for sharing, Adam. <br><br></td></tr></table><br>
<a name="1079634"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> It's faster to use the GPU to blur. <br><br></td></tr></table><br>
<a name="1079698"></a>

<a name="1079699"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> What language is that?<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
