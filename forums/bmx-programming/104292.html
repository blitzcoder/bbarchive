<!DOCTYPE html><html lang="en" ><head ><title >Arrow to Point to Off Screen Target - Slope Error?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Arrow to Point to Off Screen Target - Slope Error?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Arrow to Point to Off Screen Target - Slope Error?</a><br><br>
<a name="1262560"></a>

<a name="1262561"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi All,<br><br>I'm trying to create an arrow to point to an off screen target, but now and again I get the slope calculation wrong and it calculates 9.99999997e-007. Which of course the displays the arrow in the wrong spot.<br><br>Here is the arrow positional code:<br><br><pre class=code>Method CalcArrow(ax:Float, ay:Float)
	Local centerX:Float = ax - (SCREEN_WIDTH / 2)
	Local centerY:Float = ay - (SCREEN_HEIGHT / 2)

	Local slope:Float = centerY / centerX
		
	Local pad:Int = 200
	Local paddingX:Int = pad
	Local paddingY:Int = pad
		
	Local padWidth:Float = SCREEN_WIDTH - paddingX
	Local padHeight:Float = SCREEN_HEIGHT - paddingY
		
	If centerY &lt; 0
		arrowX = (-padHeight / 2) / slope
		arrowY = -padHeight / 2
	Else
		arrowX = (padHeight / 2) / slope
		arrowY = padHeight / 2
	EndIf
		
	If arrowX &lt; - padWidth / 2
		arrowX  = -padWidth / 2
		arrowY  = slope * -padWidth / 2
	ElseIf arrowX &gt; padWidth / 2
		arrowX = padWidth / 2
		arrowY = slope * padWidth / 2
	EndIf
		
	If id = 1 Then
		DebugLog "slope = "+ slope
	EndIf
		
	arrowAngle = GetAngle2D(SCREEN_WIDTH / 2, SCREEN_HEIGHT/ 2, ax, ay)
	arrowX = arrowX + SCREEN_WIDTH / 2
	arrowY = arrowY + SCREEN_HEIGHT / 2

EndMethod</pre><br><br>And here is some runnable code:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

Const SCREEN_WIDTH:Int = 800
Const SCREEN_HEIGHT:Int = 600

Global game:TGame = New TGame

SeedRnd MilliSecs()

game.Init()
game.Update()

End

Type TGame
	Field mapX:Float, mapY:Float
	Field omx:Float, omy:Float

	Method Init()
		Graphics SCREEN_WIDTH, SCREEN_HEIGHT
		
		For Local i:Int = 1 To 5
			Local t:TTarget = TTarget.Create(Rnd(-SCREEN_WIDTH/2, SCREEN_WIDTH * 1.5), Rnd(-SCREEN_HEIGHT/2, SCREEN_HEIGHT * 1.5), i)
			t.offx = mapX
			t.offy = mapY
			t.r = Rand(100, 255)
			t.g = Rand(100, 255)
			t.b = Rand(100, 255)
		Next
	EndMethod

	Method MoveMap()
		Local sx:Int = MouseXSpeed()
		Local sy:Int = MouseYSpeed()
		
		If MouseDown(1)
			mapX :+ sx
			mapY :+ sy
			
			If Abs(sx) &gt; 0 Then omx = sx / 8
			If Abs(sy) &gt; 0 Then omy = sy / 8
		Else
			If Abs(omx) &gt; 0
				mapX:+omx
				omx:/1.03
			Else
				mapX = Int(mapX)
			EndIf
			If Abs(omy) &gt; 0
				mapY:+omy
				omy:/1.03
			Else
				mapY = Int(mapY)
			EndIf
		EndIf
	
		Local speed:Float = 0.5
		If KeyHit(KEY_DOWN)
			mapY :+ speed
		EndIf
		If KeyHit(KEY_LEFT)
			mapX :- speed
		EndIf
		If KeyHit(KEY_UP)
			mapY :- speed
		EndIf
		If KeyHit(KEY_RIGHT)
			mapX :+ speed
		EndIf		
		

	EndMethod

	Method Update()
		While (Not AppTerminate() And Not KeyHit(KEY_ESCAPE))
			MoveMap()
			TTarget.UpdateAll(mapX, mapY)
			Render()
		Wend
	EndMethod
	
	Method Render()
		Cls		
			TTarget.Render()
		Flip
	EndMethod
EndType

Type TTarget Extends TSprite
	Global targets:TList = New TList
	
	Field id:Int
	Field drawArrow:Int
	Field arrowX:Int, arrowY:Int
	Field arrowAngle:Float
	Field r:Int, g:Int, b:Int
	
	Function Create:TTarget(x:Float, y:Float, id:Int)
		Local t:TTarget = New TTarget 
		targets.AddLast(t)
		t.x = x
		t.y = y
		t.w = 10
		t.h = 10
		t.id = id
		Return t
	EndFunction
	
	Method Draw()
		SetColor r, g, b

		DrawOval (x - w / 2) + offx, (y - h / 2) + offy, w, h
		
		If drawArrow
			SetRotation arrowAngle
			DrawRect(arrowX, arrowY, 20, 5)
			SetRotation 0
			SetColor 255, 255, 255
			DrawText(id, arrowX, arrowY)
		EndIf
		
		SetColor 255, 255, 255
	EndMethod
	
	Function Render()
		For Local t:TTarget= EachIn TTarget.targets
			t.Draw()		
		Next
	EndFunction
	
	Method Update(offx:Float, offy:Float)
		Self.offx = offx
		Self.offy = offy
		If TargetOffScreen()
			drawArrow = True
		Else
			drawArrow = False
		EndIf
	EndMethod
	
	Method CalcArrow(ax:Float, ay:Float)
		Local centerX:Float = ax - (SCREEN_WIDTH / 2)
		Local centerY:Float = ay - (SCREEN_HEIGHT / 2)

		' find slope
		Local slope:Float = centerY / centerX
		
		Local pad:Int = 200
		Local paddingX:Int = pad
		Local paddingY:Int = pad
		
		Local padWidth:Float = SCREEN_WIDTH - paddingX
		Local padHeight:Float = SCREEN_HEIGHT - paddingY
		
		If centerY &lt; 0 'top of the screen
			arrowX = (-padHeight / 2) / slope
			arrowY = -padHeight / 2
		Else ' bottom of the screen
			arrowX = (padHeight / 2) / slope
			arrowY = padHeight / 2
		EndIf
		
		If arrowX &lt; - padWidth / 2 ' left side
			arrowX  = -padWidth / 2
			arrowY  = slope * -padWidth / 2
		ElseIf arrowX &gt; padWidth / 2' right side
			arrowX = padWidth / 2
			arrowY = slope * padWidth / 2
		EndIf
		
		If id = 1 Then
			DebugLog "slope = "+ slope
		EndIf
		
		arrowAngle = GetAngle2D(SCREEN_WIDTH / 2, SCREEN_HEIGHT/ 2, ax, ay)
		arrowX = arrowX + SCREEN_WIDTH / 2
		arrowY = arrowY + SCREEN_HEIGHT / 2

	EndMethod
	
	Method TargetOffScreen:Int()
		Local rv:Int = False
		Local ssx:Float = x + offx
		Local ssy:Float = y + offy
		
		Local border:Int = 75
		If ssx &lt; border Or ssx &gt; SCREEN_WIDTH - border Or ssy &lt; border Or ssy &gt; SCREEN_HEIGHT - border
			rv = True
		EndIf
		
		If rv
			CalcArrow(ssx, ssy)
		EndIf
		
		Return rv
	EndMethod
	
	Function UpdateAll(offx:Float, offy:Float)
		For Local t:TTarget= EachIn TTarget.targets
			t.Update(offx, offy)		
		Next
	EndFunction

EndType

Type TSprite
	Field x:Float, y:Float
	Field w:Float, h:Float
	Field offx:Float, offy:Float
EndType

Function GetAngle2D:Float(x1:Float, y1:Float, x2:Float, y2:Float)
	Local dx:Float = x2 - x1
	Local dy:Float = y2 - y1
	Return ATan2(dy, dx) + 360 Mod 360
EndFunction
</textarea><br><br>Can anyone see what I have done wrong?<br><br>Thanks! <br><br></td></tr></table><br>
<a name="1262564"></a>

<a name="1262565"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Typically, as soon as I post for help I find the issue afterwards...<br><br>The issue is with this bit of the code:<br><pre class=code>Local centerX:Float = ax - (SCREEN_WIDTH / 2)
Local centerY:Float = ay - (SCREEN_HEIGHT / 2)

Local slope:Float = centerY / centerX
</pre><br><br>There are times when ax can be the same as SCREEN_WIDTH / 2 and ay can be the same as SCREEN_HEIGHT / 2. Which makes centerX/centerY close to zero (because of floating point inaccuracies). So I am now using a "close to zero" function to set them to a positive value:<br><br><pre class=code>Local centerX:Float = ax - (SCREEN_WIDTH / 2)
Local centerY:Float = ay - (SCREEN_HEIGHT / 2)

If CloseToZero(cx, 0.5) Then cx = 0.5
If CloseToZero(cy, 0.5) Then cy = 0.5

Local slope:Float = centerY / centerX

...

Function CloseToZero:Int(amount:Double, precision:Double)
	If amount= 0 Return True
	If amount&gt; 0 And amount&lt; precision  Return True
	If amount&lt; 0 And amount&gt; 0 - precision Return True
	Return False
EndFunction
</pre><br><br>Not perfect but good enough (after spending hours looking at the code!!) <br><br></td></tr></table><br>
<a name="1262566"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I don't see how "slope" contributes anything except a possible source of error. ATan2 already gives the necessary angle information. Notice that slope can legitimately be infinite for vertical lines.<br><br>Slope only defines an orientation for a line, with a range of 180 degrees. Since you use the term Arrow I'm guessing you really want a range of 360 degrees, which ATan2 provides.<br><br>One other comment, you use ATan2(dy, dx) + 360 Mod 360, apparently to change the range to 0 to 360 rather than -180 to +180. There is nothing wrong with using -180 to +180 and in fact you already are. <br><br>Mod has the same precedence as division. ATan2(dy, dx) + 360 Mod 360 is treated as ATan2(dy, dx) + ( 360 Mod 360 ), where 360 mod 360 is zero. <br><br></td></tr></table><br>
<a name="1262568"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> The "slope" I am referring to is the Slope–intercept form in Linear equations:<br><br><a href="http://en.wikipedia.org/wiki/Linear_equation#Slope.E2.80.93intercept_form" target="_blank">http://en.wikipedia.org/wiki/Linear_equation#Slope.E2.80.93intercept_form</a><br><br>If we can do this without the linear equations that would be great!<br><br>Execute the runnable example to show you what I am after. <br><br></td></tr></table><br>
<a name="1262572"></a>

<a name="1262573"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> As mentioned you are already using ATan2 to get an orientation angle for the "arrow". The slope of the arrow is just the tangent of this angle, so it provides no new information. In fact it loses something because it only covers 180 degrees. For example it can't distinguish the direction of the positive x-axis from the negative x-axis. They both have the same slope ( zero ) but have ATan2 angles of 0 and 180. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
