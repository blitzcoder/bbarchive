<!DOCTYPE html><html lang="en" ><head ><title >Graphics corruption after using Steam Overlay</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Graphics corruption after using Steam Overlay</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Graphics corruption after using Steam Overlay</a><br><br>
<a name="1271811"></a>

<a name="1271812"></a>

<a name="1271813"></a>

<a name="1271814"></a>

<a name="1271815"></a>

<a name="1271816"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi all, I recently noticed that if I run my game in Steam and any kind of Steam overlay occurs that I get graphics corruption (e.g. the intial Steam popup, or an achievement popup, or if you use Shift+Tab to bring up then hide the Steam Overlay). <br><br><a href="https://www.dropbox.com/s/shm83psz8n7yaca/RSCorruption.jpg?dl=0" target="_blank">https://www.dropbox.com/s/shm83psz8n7yaca/RSCorruption.jpg?dl=0</a> (yes I know it's a jpg but the corruption is still very evident)<br><br>On the left is how it normally looks, and on the right is after a Steam overlay incident. Notice how the fonts look blocky and so do the button rims.  It's as if filteredtexture is being turned off by the Steam overlay!<br><br>Also worth noting is that it ONLY happens if I use the DX9 driver, it doesn't happen with OpenGL.  If I recreate the graphics window it goes away (like by going to windowed mode then full screen again), and also Alt+tabbing fixes it. I'm guessing this is because all the textures get refreshed in VRAM.<br><br>Now I know that this doesn't happen in (most) other games, so it must be Steam+BlitzMax combo. I've also googled this issue and heard about a few other games that suffer from it, and the solution is "turn off Steam overlay", which isn't a great solution really, especially as a player will get a bad first impression with that corruption and may not think to turn off the overlay.<br><br>Perhaps if it was possible to query the texture states and discover they were corrupt (or filtered image was turned off) I could refresh them somehow?<br><br>Any ideas for a fix? Thanks! <br><br></td></tr></table><br>
<a name="1271832"></a>

<a name="1271833"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >H&amp;K</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> The only thing I can think to ask you, what mechanism exits for your program to KNOW that a steam overlay has occurred? (This might be what you are asking already sorry)<br><br>Obviously my solution is gonna be, let it happen them deal with it, where as I expect you want a - don't let it happen solution. <br><br></td></tr></table><br>
<a name="1271849"></a>

<a name="1271851"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> It looks like anti-alias has been turned off...<br><br>Heres a couple of threads I've quickly found:<br><br><a href="http://steamcommunity.com/groups/airforsteam/discussions/1/617330406657138525/" target="_blank">http://steamcommunity.com/groups/airforsteam/discussions/1/617330406657138525/</a><br><br><a href="http://forums.steampowered.com/forums/showthread.php?t=1970609" target="_blank">http://forums.steampowered.com/forums/showthread.php?t=1970609</a><br><br><a href="http://superuser.com/questions/753484/force-direct3d-anti-aliasing-in-a-direct3d-game" target="_blank">http://superuser.com/questions/753484/force-direct3d-anti-aliasing-in-a-direct3d-game</a> <br><br></td></tr></table><br>
<a name="1271857"></a>

<a name="1271858"></a>

<a name="1271859"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> @H&amp;K There's no mechanism via Steam to let me know when that has occurred, but yeah I'm wondering if there's a way in BlitzMax to see if the textures have been corrupted or "dirty". I believe in Blitz3D there used to be something like that.  I know that Mark wrote some code to reupload all the textures to the videocard after an alt+tab (DX surface lost or something) so I might be able to trigger that if I could detect they were corrupt.<br><br>@therevills I don't think so. I haven't turned on anti-aliasing on my video card, don't even know how to do that in code. Remember that older video cards didn't even have FSAA (if that's what you are talking about). The smoothness is simply the fact I'm loading in the textures with FILTEREDIMAGE in the LoadImage flags.  It seems like Steam overlay is changing something in the way things are being rendered, and it looks like FILTEREDIMAGE stops working for some reason.  In fact, here is someone talking about that: <a href="https://github.com/ValveSoftware/Source-1-Games/issues/1646" target="_blank">https://github.com/ValveSoftware/Source-1-Games/issues/1646</a><br><br>Possible workarounds are:<br>1) Once the game has loaded (and the steam popup has appeared and disappeared), recreate the graphics window before the title screen, or main game screen appears.  This will at least clear the issue periodically. But it won't solve the problem if they used Shift+Tab to call up the overlay, or if they get an achievement popup, or I guess if they get a "your friend is playing X" popup (haven't tested that).<br><br>2) Start the game in OpenGL (there are no issues as far as I can tell).  Also provide a game launcher to allow players to change driver and full screen/windowed before they launch the game (in case OpenGL fails as it can be less reliable). However, I should warn them to disable Steam overlay if they select the DX driver.<br><br>Has anyone made a nice game launcher like that (with MaxGUI) btw, and put it in the archives? <br><br></td></tr></table><br>
<a name="1271862"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Looks like there's a function that tells your code when it's running: SteamUtils()-&gt;IsOverlayEnabled()<br><br>So perhaps I could track that switching from True to False and then recreate the graphics window (or better still, just find a way to refresh all the textures in VRAM as that would take less time and wouldn't result in a momentary black screen)<br><br>But Skid doesn't have that in the steamstub.lib.  Not sure how easy it would be to add, besides it would also mean recompiling the the .lib which I'm not set up to do. <br><br></td></tr></table><br>
<a name="1271867"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Also I spoke to someone who said if the filtering is being turned off, it should be safe to reenable it each frame with this: <br><br><pre class=code>
 PDevice-&gt;SetSamplerState(0,D3DSAMP_MINFILTER,D3DTEXF_LINEAR);
 PDevice-&gt;SetSamplerState(0,D3DSAMP_MAGFILTER,D3DTEXF_LINEAR);
</pre><br>Though that would mean writing some C++ code or plugging that into the BlitzMax rendering code somewhere, but I've no idea where to start with that. <br><br></td></tr></table><br>
<a name="1271871"></a>

<a name="1271872"></a>

<a name="1271883"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well, I believe I have fixed it with the help of Cliff Harris who knows his way around DX.<br><br>However, I'd love to hear if anyone thinks this fix is dodgy in any way:<br><br>Basically you need to add this to the bottom of d3d9max2d.bmx ResetDevice() just above _d3dDev.BeginScene<br><br><pre class=code>
'JB
_d3dDev.SetSamplerState(0,D3DSAMP_MINFILTER,D3DTEXF_LINEAR)
_d3dDev.SetSamplerState(0,D3DSAMP_MAGFILTER,D3DTEXF_LINEAR)
_d3dDev.SetSamplerState(0,D3DSAMP_MIPFILTER,D3DTEXF_LINEAR)
'This is for the black line issue
_d3dDev.SetSamplerState(0,D3DSAMP_ADDRESSU,D3DTADDRESS_CLAMP)
_d3dDev.SetSamplerState(0,D3DSAMP_ADDRESSV,D3DTADDRESS_CLAMP)
</pre><br><br>What seems to be happening is the Steam overlay destroys the setting on each texture that specifies it it is a filtered image. It also appear to destroy the clamping.<br><br>However, the code above sets things up correctly via the SamplerState and seems to bypass the incorrect/destroyed setting on each individual texture.<br><br>I did try commenting out the if statement from the code below in TD3D9ImageFrame.Draw() so that the code in the middle gets called every draw, but for some reason that didn't work:<br><br><pre class=code>
		If _texture&lt;&gt;_bound_texture
			_d3dDev.SetTexture 0,_texture
			_d3dDev.SetTextureStageState 0,D3DTSS_MAGFILTER,_magfilter
			_d3dDev.SetTextureStageState 0,D3DTSS_MINFILTER,_minfilter
			_d3dDev.SetTextureStageState 0,D3DTSS_MIPFILTER,_mipfilter
			_bound_texture=_texture
		EndIf
</pre><br><br>So possible side effects are: I guess if you really want to draw a texture without filtering, this code isn't for you, as now everything will be drawn with filtering.<br><br>Also that UV clamping code seem to work well but it could have weird side effects, I don't know.<br><br>WHY THE CLAMPING?<br><br>I forgot to say why I'm using the clamping code, well that's because after the overlay appear in full screen mode, I saw black lines on the edges of some textures (like the top of the options button texture, which has blank space around it) here:<br><br><a href="https://www.dropbox.com/s/nlxvf2tphru7d9h/Blackline.jpg?dl=0" target="_blank">https://www.dropbox.com/s/nlxvf2tphru7d9h/Blackline.jpg?dl=0</a><br><br>This is because in full screen mode, I'm no longer drawing textures at 1:1, I'm scaling everything up to the desktop resolution (via SetVirtualResolution), and normally this is fine. But for some reason the Steam overlay breaks it.  Again it probably breaks the clamping on a per texture level, and the code fix enables clamping at the SamplerState level.<br><br>ALSO be aware that maybe Steam destroys OTHER texture settings that I am not repairing and simply have not discovered yet...<br><br>So this is my kludge. Does it seem sensible or a nightmare? Thx <br><br></td></tr></table><br>
<a name="1271876"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> SetTextureStageState does not have D3DTSS_MAGFILTER, D3DTSS_MINFILTER or D3DTSS_MIPFILTER types, these types don't exist:<br><a href="https://msdn.microsoft.com/en-us/library/windows/desktop/bb172617(v=vs.85).aspx" target="_blank">https://msdn.microsoft.com/en-us/library/windows/desktop/bb172617(v=vs.85).aspx</a><br><br>Those new lines that you added to ResetDevice, add them to the ImageFrame Draw() code.<br>This will ensure those states are set when you draw an image, so a spontaneous Steam overlay won't change them. <br><br></td></tr></table><br>
<a name="1271885"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Weird, because those consts are in this list: <a href="https://msdn.microsoft.com/en-us/library/ms886612.aspx" target="_blank">https://msdn.microsoft.com/en-us/library/ms886612.aspx</a><br><br>They must work in normal BlitzMax operation because it's the only place that textures are set to be filtered or mipmapped, and I know that works (I used filtered textures for most of the graphics in my games)<br><br>It doesn't seem that I need to add those lines from ResetDevice() to draw because somehow setting them there fixes the issue and Steam Overlay doesn't break anything any more. I don't know why though.<br><br>However, maybe it would be better if I could add them to Draw() and ONLY set the ones relevant to the states of _magfilter, _minfilter and _mipfilter? Perhaps that's safer as then if something isn't supposed to be filtered it would not get drawn filtered. <br><br></td></tr></table><br>
<a name="1271886"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Also what is the difference between and SetTextureStageState and SetSamplerState? I don't even know.<br><br>I'm guessing SetTextureStageState is applied to the current texture (specified by _d3dDev.SetTexture 0,_texture) and that SetSamplerState just gets applied to all textures? <br><br></td></tr></table><br>
<a name="1271917"></a>

<a name="1271918"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> That documentation link you posted is for Direct3D 8.<br><br>When you press Shift + Tab to bring the Steam overlay while still inside the game, with that modified ResetDevice(), does it fix the issue?<br>If it does, you can probably leave it like that. No need to modify Draw().<br><br>In case you're interested about the Direct3D 9 API, the MSDN documentation is concise:<br><br>- <a href="https://msdn.microsoft.com/en-us/library/windows/desktop/bb174462(v=vs.85).aspx" target="_blank">https://msdn.microsoft.com/en-us/library/windows/desktop/bb174462(v=vs.85).aspx</a><br>- <a href="https://msdn.microsoft.com/en-us/library/windows/desktop/bb174456(v=vs.85).aspx" target="_blank">https://msdn.microsoft.com/en-us/library/windows/desktop/bb174456(v=vs.85).aspx</a> <br><br></td></tr></table><br>
<a name="1271967"></a>

<a name="1271968"></a>

<a name="1271969"></a>

<a name="1271970"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks @Kryzon: Yep, it does fix it just with the code in ResetDevice() so in theory I could leave it there, but what was bothering me is the original BlitzMax code uses SetTextureStageState within Draw() to set the filter/mipmap on a per texture basis. So in theory you could have a mixture of textures with different filtering required and using SetSamplerState just at the start of a frame will override any individual texture filter/mipmap right?<br><br>Also could calling _d3dDev.SetSamplerState(0,D3DSAMP_MIPFILTER,D3DTEXF_LINEAR) even though I haven't loaded in any textures with the mipmap flag be a problem? It does admittedly seem to work, so I guess it's safe.<br><br>Anyway, I'm wondering if a better/safer solution is to check _magfilter, _minfilter and _mipfilter, and then call the correct SetSamplerState every time I draw, or would that be too slow?<br><br>What do you think?<br><br>I'm also paranoid about what else Steam trashes when it shows the overlay. I know it messes up the uv clamping which I have a fix for above, but maybe it messes other stuff up too?<br><br>For example, ResetDevice() also sets these things:<br><br><pre class=code>
		_d3dDev.SetRenderState D3DRS_ALPHAREF,$80
		_d3dDev.SetRenderState D3DRS_ALPHAFUNC,D3DCMP_GREATEREQUAL

		_d3dDev.SetRenderState D3DRS_ALPHATESTENABLE,False
		_d3dDev.SetRenderState D3DRS_ALPHABLENDENABLE,False
		_active_blend=SOLIDBLEND
		
		_d3dDev.SetRenderState D3DRS_LIGHTING,False
		_d3dDev.SetRenderState D3DRS_CULLMODE,D3DCULL_NONE	
		
		_d3dDev.SetTexture 0,Null
		_bound_texture=Null
		
		_d3dDev.SetFVF D3DFVF_XYZ|D3DFVF_DIFFUSE|D3DFVF_TEX1
		
		_d3dDev.SetTextureStageState 0,D3DTSS_COLORARG1,D3DTA_TEXTURE		
		_d3dDev.SetTextureStageState 0,D3DTSS_COLORARG2,D3DTA_DIFFUSE		
		_d3dDev.SetTextureStageState 0,D3DTSS_COLOROP,D3DTOP_SELECTARG2
		_d3dDev.SetTextureStageState 0,D3DTSS_ALPHAARG1,D3DTA_TEXTURE
		_d3dDev.SetTextureStageState 0,D3DTSS_ALPHAARG2,D3DTA_DIFFUSE
		_d3dDev.SetTextureStageState 0,D3DTSS_ALPHAOP,D3DTOP_SELECTARG2
		_texture_enabled=False
</pre><br><br>Maybe I should set those at the start of each FRAME instead? <br><br></td></tr></table><br>
<a name="1272004"></a>

<a name="1272005"></a>

<a name="1272006"></a>

<a name="1272007"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> It sets the filtering at each Draw because these are persistent states (set on texture unit zero, like a channel, used to draw all Max2D images) and one image may have a different filtering than the previously drawn image. The filtering mode is specified by the programmer at LoadImage.<br><br>If the ResetDevice modification fixes all possible cases of Steam overlay, it's better to leave things like that. <br>Modifying the module further as a preventive measure may introduce other problems.<br><br>Making changes to the texture unit state (like with SetSamplerState etc.) modifies only the texture unit (the "channel" used to draw the textures), it doesn't need to have a texture bound to that unit. <br><br></td></tr></table><br>
<a name="1272025"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah I see, OK thanks.<br><br>Well it may be that BRL want to consider this "fix" for Steam. <br><br></td></tr></table><br>
<a name="1275267"></a>

<a name="1275268"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> We are getting some weird graphical issues with Steam overlay.<br><br>Using GA's code on post #7 fixes the filtering issue, but in OpenGL we get this, when using Aspect Ratio code:<br><br><img src="https://dl.dropboxusercontent.com/u/35103024/ScreenShots/Steam/steam_opengl_error.jpg"><br><br>And with DirectX9 it doesnt clip correctly on the first display, if we change to windows and back to full screen it works:<br><br><img src="https://dl.dropboxusercontent.com/u/35103024/ScreenShots/Steam/steam_dx9_error.jpg"><br><br>Any ideas? <br><br></td></tr></table><br>
<a name="1275277"></a>

<a name="1275278"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> . <br><br></td></tr></table><br>
<a name="1275348"></a>

<a name="1275349"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think this is happening due to my graphics card having triple buffering enabled...<br><br>I've done a work around by overriding the Cls command which will hurt performance a tiny bit:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Function Cls()
	Local steam:Int = True
	If steam
		Local clsr:Int, clsg:Int, clsb:Int
		Local r:Int, g:Int, b:Int
		
		GetClsColor clsr, clsg, clsb
		GetColor r, g, b
		
		SetVirtualResolution GraphicsWidth (), GraphicsHeight ()
		SetViewport 0, 0, GraphicsWidth (), GraphicsHeight ()
		SetOrigin 0, 0
		SetClsColor 0, 0, 0
		brl.max2d.Cls()
		
		SetVirtualResolution VirtualGfx.width, VirtualGfx.height
		SetViewport VirtualGfx.VG.vxoff, VirtualGfx.VG.vyoff, VirtualGfx.VG.vwidth, VirtualGfx.VG.vheight
		SetOrigin VirtualGfx.VG.vxoff, VirtualGfx.VG.vyoff
		SetClsColor clsr, clsg, clsb
		SetColor r, g, b
		brl.max2d.Cls()
	Else
		brl.max2d.Cls()
	EndIf
End Function</textarea><br><br>And you can test with any BMax game, by adding a non-steam game to steam. Here the full test code using James' virtual aspect ratio code:<br><a href="http://www.blitzmax.com/codearcs/codearcs.php?code=2879" target="_blank">http://www.blitzmax.com/codearcs/codearcs.php?code=2879</a><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

' -----------------------------------------------------------------------------
' USAGE: Note that the order is important!
' -----------------------------------------------------------------------------

' 1) Call InitVirtualGraphics before anything else;
' 2) Call Graphics as normal to create display;
' 3) Call SetVirtualGraphics to set virtual display size.

' The optional 'monitor_stretch' parameter of SetVirtualGraphics is there
' because some monitors have the option to stretch non-native ratios to native
' ratios, and you cannot detect this programmatically.

' For instance, my monitor's native resolution is 1920 x 1080, and if I set the
' Graphics mode to 1024, 768, it defaults to stretching that to fill the screen,
' meaning the image is stretched horizontally, so a square will appear non-
' square; however, it also provides an option to scale to the correct aspect
' ratio. Since this is set on the monitor, there's no way to detect or correct
' it other than by offering the option to the user. Leave it off if unsure...

' -----------------------------------------------------------------------------
' Copy this...
' -----------------------------------------------------------------------------

Type VirtualGfx

	Global VG:VirtualGfx
	
	Global DTInitComplete:Int = False
	Global DTW:Int
	Global DTH:Int

	Field vwidth:Int
	Field vheight:Int

	Field vxoff:Float
	Field vyoff:Float

	Field vscale:Float
	
	Global width:Float
	Global height:Float
	
	Function CreateVG:VirtualGfx (width:Int, height:Int)
		VG = New VirtualGfx
		VG.vwidth = width
		VG.vheight = height
	End Function
	
	Function SetVirtualGraphics (vwidth:Int, vheight:Int, monitor_stretch:Int = False)

		' InitVirtualGraphics has been called...
		
		If VirtualGfx.DTInitComplete
		
			' Graphics has been called...
			
			If GraphicsWidth () = 0 Or GraphicsHeight () = 0
				Notify "2 Programmer error! Must call Graphics before SetVirtualGraphics", True; End
			EndIf
			
		Else
			EndGraphics; Notify " 3 Programmer error! Call InitVirtualGraphics before Graphics!", True; End
		EndIf

		' Reset of display needed when re-calculating virtual graphics stuff/clearing borders...
			
		SetVirtualResolution GraphicsWidth (), GraphicsHeight ()
		SetViewport 0, 0, GraphicsWidth (), GraphicsHeight ()
		SetOrigin 0, 0

		' Store current Cls colours...
		
		Local clsr:Int, clsg:Int, clsb:Int
		GetClsColor clsr, clsg, clsb
		
		' Set to black...
		
		SetClsColor 0, 0, 0
		
		' Got to clear both front AND back buffers or it flickers if new display area is smaller...
		
		Cls
		Flip

		Cls
		Flip
		
		Cls
		Flip
		
		' EDIT, 10 Nov 2011: I had a 3rd Cls/Flip here in case triple-buffering was enabled, but have finally
		' tested this and it wasn't needed. (Tested on NVIDIA GTX 260, Windows 7, with driver
		' version 275.33.)

		' EDIT 2, 30 Dec 2011: Reinstated 3rd Cls/Flip for triple-buffering; see first thread comment!

		' Put back Cls colours...

		SetClsColor clsr, clsg, clsb
		
		' Create new (global) virtual display object...
		
		VirtualGfx.CreateVG (vwidth, vheight)
		
		' Real Graphics width/height...
		
		Local gwidth:Int
		Local gheight:Int
		
		' If monitor is correcting aspect ratio IN FULL-SCREEN MODE, use desktop size, otherwise use
		' specified Graphics size. NB. This assumes user's desktop is using native monitor resolution,
		' as most laptops would be by default...
		
		If monitor_stretch And GraphicsDepth ()

			' Pretend real Graphics mode is desktop width/height...

			gwidth = DTW
			gheight = DTH

		Else

			' Use real Graphics width/height...

			gwidth = GraphicsWidth ()
			gheight = GraphicsHeight ()

		EndIf
		
		' Width/height ratios...
		
		Local graphicsratio:Float = Float (gwidth) / Float (gheight)
		Local virtualratio:Float = Float (VirtualGfx.VG.vwidth) / Float (VirtualGfx.VG.vheight)
		
		' Ratio-to-ratio. Don't even know what you'd call this, but hours of trial and error
		' provided the right numbers in the end...
		
		Local gtovratio:Float = graphicsratio / virtualratio
		Local vtogratio:Float = virtualratio / graphicsratio
		
		' Compare ratios...

		If graphicsratio =&gt; virtualratio
			
			' Graphics ratio wider than (or same as) virtual graphics ratio...

			VirtualGfx.VG.vscale = Float (gheight) / Float (VirtualGfx.VG.vheight)
			
			' Now go crazy with trial-and-error... ooh, it works! This tiny bit of code took FOREVER.
			
			Local pixels:Float = Float (VirtualGfx.VG.vwidth) / (1.0 / VirtualGfx.VG.vscale) ' Width after scaling
			Local half_scale:Float = (1.0 / VirtualGfx.VG.vscale) / 2.0
			
			VirtualGfx.width = VirtualGfx.VG.vwidth * gtovratio
			VirtualGfx.height = VirtualGfx.VG.vheight
			
			SetVirtualResolution VirtualGfx.width, VirtualGfx.height

			' Offset into 'real' display area...
			
			VirtualGfx.VG.vxoff = (gwidth - pixels) * half_scale
			VirtualGfx.VG.vyoff = 0
		
			' Set up virtual graphics area...
			
			SetViewport VirtualGfx.VG.vxoff, VirtualGfx.VG.vyoff, VirtualGfx.VG.vwidth, VirtualGfx.VG.vheight
			SetOrigin VirtualGfx.VG.vxoff, VirtualGfx.VG.vyoff
			
		Else
			
			' Graphics ratio narrower...
		
			VirtualGfx.VG.vscale = Float (gwidth) / Float (VirtualGfx.VG.vwidth)
			
			Local pixels:Float = Float (VirtualGfx.VG.vheight) / (1.0 / VirtualGfx.VG.vscale) ' Height after scaling
			Local half_scale:Float = (1.0 / VirtualGfx.VG.vscale) / 2.0
			
			VirtualGfx.width = VirtualGfx.VG.vwidth
			VirtualGfx.height = VirtualGfx.VG.vheight * vtogratio
			
			SetVirtualResolution VirtualGfx.width, VirtualGfx.height
		
			VirtualGfx.VG.vxoff = 0
			VirtualGfx.VG.vyoff = (gheight - pixels) * half_scale
		
			SetViewport VirtualGfx.VG.vxoff, VirtualGfx.VG.vyoff, VirtualGfx.VG.vwidth, VirtualGfx.VG.vheight
			SetOrigin VirtualGfx.VG.vxoff, VirtualGfx.VG.vyoff
			
		EndIf
	
	End Function

	Method VMouseX:Float ()
		Local mx:Float = VirtualMouseX () - vxoff
		If mx &lt; 0 Then mx = 0 Else If mx &gt; vwidth - 1 Then mx = vwidth - 1
		Return mx
	End Method
	
	Method VMouseY:Float ()
		Local my:Float = VirtualMouseY () - vyoff
		If my &lt; 0 Then my = 0 Else If my &gt; vheight - 1 Then my = vheight - 1
		Return my
	End Method
	
	Method VirtualWidth:Int ()
		Return vwidth
	End Method

	Method VirtualHeight:Int ()
		Return vheight
	End Method
	
End Type

' -----------------------------------------------------------------------------
' ... and these helper functions (required)...
' -----------------------------------------------------------------------------

Function InitVirtualGraphics ()

	' There must be a smarter way to check if Graphics has been called...
	
	If GraphicsWidth () &gt; 0 Or GraphicsHeight () &gt; 0 Then EndGraphics; Notify " 1 Programmer error! Call InitVirtualGraphics BEFORE Graphics!", True; End

	VirtualGfx.DTW = DesktopWidth ()
	VirtualGfx.DTH = DesktopHeight ()

	' This only checks once... best to call InitVirtualGraphics again before any further Graphics calls (if you call EndGraphics at all)...
	
	VirtualGfx.DTInitComplete = True

End Function

Function SetVirtualGraphics (vwidth:Int, vheight:Int, monitor_stretch:Int = False)
	VirtualGfx.SetVirtualGraphics (vwidth, vheight, monitor_stretch)
End Function

Function VMouseX:Float ()
	Return VirtualGfx.VG.VMouseX ()
End Function

Function VMouseY:Float ()
	Return VirtualGfx.VG.VMouseY ()
End Function

' Don't need VirtualMouseXSpeed/YSpeed replacements!

Function VirtualWidth:Int ()
	Return VirtualGfx.VG.VirtualWidth ()
End Function

Function VirtualHeight:Int ()
	Return VirtualGfx.VG.VirtualHeight ()
End Function

' -----------------------------------------------------------------------------
' Don't copy after this...
' -----------------------------------------------------------------------------


Function SetDriver:Int(mode:Int)
	'0 = DirectX, 1 = OpenGL
	If mode = 0 Then
		'Don't do anything if Driver is already 0
		If Driver = 0 Then Return 0
		Driver = 0
	Else			
		'Don't do anything if Driver is already 0
		If Driver = 1 Then Return 0
		Driver = 1
	EndIf
	EndGraphics() 'safety
	
' I recommend calling this on the off-chance that the user's desktop size has changed...
		
		InitVirtualGraphics
		
		' Windowed/full-screen...
		
		
		If driver = DIRECTX_DRIVER Then
			SetGraphicsDriver D3D9Max2DDriver()
		Else
			SetGraphicsDriver GLMax2DDriver()
		End If
		
		If FULLSCREEN_TOGGLE
			Graphics fullscreenwidth, fullscreenheight, fullscreendepth
			SetVirtualGraphics 1280, 1024, monitor_adjusting
		Else
			Graphics windowedwidth, windowedheight, windoweddepth
			SetVirtualGraphics 1280, 1024
		EndIf
		
		' These get reset when exiting graphics mode...
		
		SetClsColor 64, 96, 128
		SetMaskColor 255, 0, 255
	Return 1
End Function



' -----------------------------------------------------------------------------
' D E M O . . .
' -----------------------------------------------------------------------------

' REQUIRED! BEFORE Graphics!

InitVirtualGraphics

' Toggle for demo's windowed/full-screen switching...

Global FULLSCREEN_TOGGLE:Int = True

' Change this if your monitor doesn't support it!

Global fullscreenwidth:Int = 1024
Global fullscreenheight:Int = 768
Global fullscreendepth:Int = 32

Global windowedwidth:Int = 640
Global windowedheight:Int = 400
Global windoweddepth:Int = 0
Const DIRECTX_DRIVER:Int = 0
Const OPENGL_DRIVER:Int = 1
Global driver:Int = DIRECTX_DRIVER

If driver = DIRECTX_DRIVER Then
	SetGraphicsDriver D3D9Max2DDriver()
Else
	SetGraphicsDriver GLMax2DDriver()
End If
		
Graphics fullscreenwidth, fullscreenheight, fullscreendepth

' If user's monitor is set to stretch to its native resolution, this needs to be set to True. I
' would recommend making this a selectable option in your game's configuration as there's no way
' to check this from any program...

Global monitor_adjusting:Int = True

' REQUIRED! AFTER InitVirtualGraphics!

SetVirtualGraphics 1280, 1024, monitor_adjusting

SetClsColor 64, 96, 128
SetMaskColor 255, 0, 255

' Some boxes...

Type Box
	Field x:Float, y:Float
	Field xs:Float, ys:Float
	Field size:Int
	Field r:Int, g:Int, b:Int
End Type

Local boxes:TList = CreateList ()

For Local loop:Int = 0 Until 100
	Local b:Box = New Box
	b.size = Rand (32)
	b.x = Rnd (VirtualWidth () - b.size)
	b.y = Rnd (VirtualHeight () - b.size)
	b.xs = Rnd (-4.0, 4.0)
	b.ys = Rnd (-4.0, 4.0)
	b.r = Rand (100, 255)
	b.g = Rand (100, 255)
	b.b = Rand (100, 255)
	ListAddLast boxes, b
Next

Function Cls()
	Local steam:Int = True
	If steam
		Local clsr:Int, clsg:Int, clsb:Int
		Local r:Int, g:Int, b:Int
		
		GetClsColor clsr, clsg, clsb
		GetColor r, g, b
		
		SetVirtualResolution GraphicsWidth (), GraphicsHeight ()
		SetViewport 0, 0, GraphicsWidth (), GraphicsHeight ()
		SetOrigin 0, 0
		SetClsColor 0, 0, 0
		brl.max2d.Cls()
		
		SetVirtualResolution VirtualGfx.width, VirtualGfx.height
		SetViewport VirtualGfx.VG.vxoff, VirtualGfx.VG.vyoff, VirtualGfx.VG.vwidth, VirtualGfx.VG.vheight
		SetOrigin VirtualGfx.VG.vxoff, VirtualGfx.VG.vyoff
		SetClsColor clsr, clsg, clsb
		SetColor r, g, b
		brl.max2d.Cls()
	Else
		brl.max2d.Cls()
	EndIf
End Function

Repeat

	If KeyHit(KEY_G)
		SetDriver(Not driver)
	End If

	If FULLSCREEN_TOGGLE And KeyHit (KEY_RETURN)

		monitor_adjusting = 1 - monitor_adjusting
		SetVirtualGraphics 1280, 1024, monitor_adjusting

	EndIf
	
	If KeyHit (KEY_SPACE)

		FULLSCREEN_TOGGLE = 1 - FULLSCREEN_TOGGLE

		' Demo of how to change graphics mode...

		EndGraphics
		
		' I recommend calling this on the off-chance that the user's desktop size has changed...
		
		InitVirtualGraphics
		
		' Windowed/full-screen...
		
		If FULLSCREEN_TOGGLE
			Graphics fullscreenwidth, fullscreenheight, fullscreendepth
			SetVirtualGraphics 1280, 1024, monitor_adjusting
		Else
			Graphics windowedwidth, windowedheight, windoweddepth
			SetVirtualGraphics 1280, 1024
		EndIf
		
		' These get reset when exiting graphics mode...
		
		SetClsColor 64, 96, 128
		SetMaskColor 255, 0, 255
		
	EndIf
	
	Cls()
	
	SetScale 1, 1
	
			
	For Local b:Box = EachIn boxes
		b.x = b.x + b.xs
		b.y = b.y + b.ys
		If b.x &lt; 0 Or (b.x + b.size) &gt; VirtualWidth () Then b.xs = -b.xs; b.x = b.x + b.xs
		If b.y &lt; 0 Or (b.y + b.size) &gt; VirtualHeight () Then b.ys = -b.ys; b.y = b.y + b.ys
		SetColor b.r, b.g, b.b
		DrawRect b.x, b.y, b.size, b.size
	Next
	
	SetColor 255, 0, 0
	DrawRect 0, 0, 16, 16
	DrawRect 0, VirtualHeight () - 16, 16, 16
	DrawRect VirtualWidth () - 16, 0, 16, 16
	DrawRect VirtualWidth () - 16, VirtualHeight () - 16, 16, 16
	
	SetColor 0, 0, 255
	DrawOval VMouseX () - 32, VMouseY () - 32, 64, 64
	
	SetScale 4, 4
	SetColor 255, 255, 255
	DrawText "SPACE: Windowed/full-screen", 20, 20
	If FULLSCREEN_TOGGLE Then DrawText "ENTER: Monitor stretch correction", 20, 60
	If driver = DIRECTX_DRIVER
		DrawText "G to change Graphics Driver: DIRECTX", 20, 100
	Else
		DrawText "G to change Graphics Driver: OPENGL", 20, 100
	End If
	

	Flip
	
Until KeyHit (KEY_ESCAPE)

End</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
