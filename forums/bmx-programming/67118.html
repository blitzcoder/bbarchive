<!DOCTYPE html><html lang="en" ><head ><title >request for code: simple A*</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >request for code: simple A*</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >request for code: simple A*</a><br><br>
<a name="749497"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'd like to request some A* methods/functions that:<br>1: don't come with 54 terabyte of comments between the code that make it unreadable, and for me, unusable. See, I don't want to know how the code works, I only want to know how the algo should be used.<br>2: are completely local, no globals, global arrays, wasted consts etc.<br>3: are clear about what the input and output formats are.<br>4: come *only* as one type with methods/functions, I don't want to see any examples that mess the whole thing up, also I don't want any external game-depended stuff in the core pathfinding algo. Only its own startX/Y and endX/Y fields. In fact, who's talking games anyway?<br><br>Actually what I'd like to see is a type that looks as follows:<br><br><pre class=code>
Type TPathfinder

	Field status:Int ' 0: no possible path, 1:path found
	Field startX:Int, startY:Int, endX:Int, endY:Int

	Field diagonals:Int=1 ' true: use diagonal routes in pathfinding
	
	Field mapwidth:Int
	Field mapheight:Int
	
	Field map:TBank ' representing a 2d map where 0=walkable and 1=wall
	
	Field route:Int[]
	
	Method Setmapwidth(width:Int)
		' all kinda bounds checks here
		' ..
		' ..
		If mapwidth&lt;1 RuntimeError "mapwidth&lt;1"
		mapwidth=width
		Mapdimensions
	End Method
	
	Method Setstart(x:Int,y:Int)
		Assert x&gt;=0 And x&lt;mapwidth "start X out ot range"
		Assert y&gt;=0 And y&lt;mapheight "start Y out ot range"
		startx=x; starty=y
	End Method

	Method Setend(x:Int,y:Int)
		Assert x&gt;=0 And x&lt;mapwidth "end X out ot range"
		Assert y&gt;=0 And y&lt;mapheight "end Y out ot range"
		endx=x; endy=y
	End Method
	
	Method Mapdimensions()
		mapheight=BankSize(map)/mapwidth
		If mapheight&lt;1 RuntimeError "mapheight&lt;1"	
	End Method
	
	Method Setmap(bank:TBank=Null)
		Assert bank&lt;&gt;Null "bank=null"
		Assert BankSize(bank)&gt;=1 "no dimension in bank"
		map=bank
		Mapdimensions
	End Method
	
	Method Findpath()
	
	
	' resulting path in a 'resliced' route[].
	' as [x0, y0, x1, y1, x2, y2, x3, y3, ..etc. .. xn,yn]
	'     ^ start                                   ^ end
	'
	End Method
	
	
End Type

' to use:
' make some bank
' make a pathfind instance
' add bank to instance
' set width and start/emd coords
' find path
' read out path from array
' yay \o/
</pre><br><br>If it's easy to adjust the A* code in the archives to this format then I'd be grateful to the one who does so. :-) <br><br></td></tr></table><br>
<a name="749544"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Curtastic</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think the problem is that most A* here is blitzbasic code. I'll be happy to code one in blitzmax. <br>Why is the map a bank and not a 2d array though? <br><br></td></tr></table><br>
<a name="749549"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well, dunno. Perhaps because a bank is a bank, and not an int? :P<br><br>If I add a bank to this type instance then map references to this bank, so I won't loose any more mem. Ints/bytes/floats etc. are copied to a new var instance afaik, so I figure the same counts for arrays of those basic vars.<br><br>If you do, please try to fill in those methods/functions in my framework.. ^_^ For I think that's a clean/open way to do all this. <br><br></td></tr></table><br>
<a name="749617"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> <a href="http://www.blitzmax.com/Community/posts.php?topic=62388#697498" target="_blank">http://www.blitzmax.com/Community/posts.php?topic=62388#697498</a> <br><br></td></tr></table><br>
<a name="749619"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Too much noise in it.<br><br>It's what I meant with 'game depending', in that code there's this Tunit stuff everywhere. Unit? What unit? sprite:timage? What image? Speed? Who needs speed? Who says pathfinding is exclusively for games? Tnode, oh really.. so that costs yet another (global) type, and do I actually care, for only 2 int fields? Tilesize? What tiles? As far as I'm concerned, a tile is a visual property, not a functional property. Etc. etc., that whole thing is almost a game already, it's far from neutral and sterile.<br><br>The minimum required for pathfinding is a start location and an end location, and a 2d map with walkable/nonwalkable states. I suggest keeping these kind of solutions as minimal as possible, in order to be as flexible and modular as possible. <br><br></td></tr></table><br>
<a name="749704"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dreamora</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Check the german blitzbasic forum. There is a working and very good A* ressource in the codearchive of the blitzmax categorie (there is one for regular blitz as well if you do not like the BM one) <br><br></td></tr></table><br>
<a name="750450"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Curtastic: so .. you're up to do it (in the way I proposed)? :P <br><br></td></tr></table><br>
<a name="750571"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Curtastic</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> ya!!!111:):):):<br><br>I think I can do it this weekend <br><br></td></tr></table><br>
<a name="751122"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Curtastic</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> <textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'''''''''''''''''''''''''
'A General A* Pathfinder'
'Coded by Curtastic 2007'
'Coded in Lightning IDE.'
'''''''''''''''''''''''''
SuperStrict

Type TPathfinder Abstract
	'1 if diaognal movement is allowed. Cutting corners is allowed.
	'0 otherwise.
	Global Diagonals:Int
	
	'The higher this number, the more the path will randomly differ from what is optimum.
	Global Randomity:Float
	
	'Use SetMap() to set these.
	Global MapWidth:Int
	Global MapHeight:Int
	'Map is a float. The closer to 1 the harder it is to move into this tile.
	'All values 1 or greater are considered walls.
	Global Map:Float[, ]
	
	'The amount of steps in the route. (Read only)
	Global Paths:Int
	
	'The resulting path is a 'resliced' route[].
	' as [x0, y0, x1, y1, x2, y2, x3, y3, ..etc. .. xn,yn]
	' The size of this array is paths*2.
	Global Route:Int[]
	
	'Private
	Global PathMap:TPath[, ]
	Const Root2:Float = 1.4142
	
	
	Function SetMap(Array:Float[, ], Width:Int, Height:Int)
		Map = Array
		MapWidth = Width
		MapHeight = Height
	EndFunction
	
	'Returns 1 if successful and 0 if unseccessful.
	'Fills the route[] array if successful.
	Function FindPath:Int(StartX:Int, StartY:Int, EndX:Int, EndY:Int)
		
		Assert Not(StartX &lt; 0 Or StartY &lt; 0 Or StartX &gt;= MapWidth Or StartY &gt;= MapHeight), ..
		 "Starting point out of bounds: " + StartX + "," + StartY
		Assert Not(EndX &lt; 0 Or EndY &lt; 0 Or EndX &gt;= MapWidth Or EndY &gt;= MapHeight), ..
		 "End point out of bounds: " + EndX + "," + EndY
		Assert Map &lt;&gt; Null, ..
		 "SetMap() must be called before FindPath"
		
		Paths = 0
		
		'already on target
		If StartX = EndX And StartY = EndY Then Return 1
		'target is a wall.
		If Map[EndX, EndY] &gt;= 1 Then Return 0
		
		
		Local P:TPath
		Local P2:TPath
		Local NewP:TPath
		Local NewX:Int
		Local NewY:Int
		Local Dir:Int
		Local DirMax:Int
		Local Done:Int
		Local PHead:TPath
		Local MapHere:Float
		
		PathMap = New TPath[MapWidth, MapHeight]
		
		'make first path node at start
		P = New TPath
		PHead = P
		P.X = StartX
		P.Y = StartY
		PathMap[StartX, StartY] = P
		
		If Diagonals Then
			DirMax = 7
		Else
			DirMax = 3
		EndIf
		
		Repeat
			
 			If KeyDown(key_space) Then DebugStop
			
			For Dir = 0 To DirMax
				
				'move based on direction
				Select Dir
				Case 0; NewX = P.X + 1; NewY = P.Y
				Case 1; NewX = P.X    ; NewY = P.Y + 1
				Case 2; NewX = P.X - 1; NewY = P.Y
				Case 3; NewX = P.X    ; NewY = P.Y - 1
				Case 4; NewX = P.X + 1; NewY = P.Y + 1
				Case 5; NewX = P.X - 1; NewY = P.Y + 1
				Case 6; NewX = P.X - 1; NewY = P.Y - 1
				Case 7; NewX = P.X + 1; NewY = P.Y - 1
				EndSelect
				
				'check if it is ok to make a new path node here.
				If NewX &gt;= 0 And NewY &gt;= 0 And NewX &lt; MapWidth And NewY &lt; MapHeight Then
					MapHere = Map[NewX, NewY]
					If MapHere &lt; 1 Then
						
						If Diagonals = 2 And Dir &gt; 3 Then
							If Map[NewX, P.Y] &gt;= 1 Then Continue
							If Map[P.X, NewY] &gt;= 1 Then Continue
						EndIf
						
						P2 = PathMap[NewX, NewY]
						
						'check if there already is a path here
						If P2 = Null Then
							
							'DrawRect newx*29,newy*29,29,29
							'Flip
							'If KeyHit(key_escape) Then End
							
							'make new node
							NewP = New TPath
							PathMap[NewX, NewY] = NewP
							NewP.Parent = P
							NewP.X = NewX
							NewP.Y = NewY
							
							'cost is slightly more for diagnols
							If Dir &lt; 4 Then
								NewP.Cost = P.Cost + .1 + MapHere + Rnd(0, Randomity)
							Else
								NewP.Cost = P.Cost + (.1 + MapHere + Rnd(0, Randomity)) * Root2
							EndIf
							
							'set distance from end
							If Diagonals Then
								NewP.Dist = ((NewX - EndX) * (NewX - EndX) + (NewY - EndY) * (NewY - EndY)) / 240.0
							Else
								NewP.Dist = (Abs(NewX - EndX) + Abs(NewY - EndY)) / 8.0
							EndIf
							
							'insert node at appropriate spot in list
							P2 = P
							Repeat
								If P2.After = Null Then
									P2.After = NewP
									Exit
								EndIf
								If P2.After.Dist + P2.After.Cost &gt; NewP.Dist + NewP.Cost Then
									NewP.After = P2.After
									P2.After = NewP
									Exit
								EndIf
								P2 = P2.After
							Forever
							
							'check if found end
							If NewX = EndX And NewY = EndY Then
								Done = 1
								Exit
							EndIf
						Else
							'overwrite existing path node if this way costs less.
							If P2.Cost &gt; P.Cost + .1 + MapHere * Root2 + Randomity Then
								P2.Parent = P
								'cost is slightly more for diagnols
								If Dir &lt; 4 Then
									P2.Cost = P.Cost + .1 + MapHere + Rnd(0, Randomity)
								Else
									P2.Cost = P.Cost + (.1 + MapHere + Rnd(0, Randomity)) * Root2
								EndIf
							EndIf
						EndIf
					EndIf
				EndIf
			Next
			
			If Done = 1 Then Exit
			
			P = P.After
			If P = Null Then Exit
			
		Forever
		
		
		If Done Then
			'count how many paths
			P2 = NewP
			Repeat
				Paths:+ 1
				P2 = P2.Parent
				If P2 = Null Then Exit
				If KeyDown(key_space) Then DebugStop

			Forever
			
			'make route from end to start
			Route = New Int[Paths * 2]
			Local i:Int = 0
			P2 = NewP
			Repeat
				Route[i] = P2.X
				i:+ 1
				Route[i] = P2.Y
				i:+ 1
				P2 = P2.Parent
				If P2 = Null Then Exit
			Forever
		EndIf
		
		'nullify pointers so mem will be deallocated.
		P = PHead
		Repeat
			P.Parent = Null
			P = P.After
			If P = Null Then Exit
		Forever
		
		Return Done
	EndFunction
EndType


'Private
Type TPath
	Field X:Int
	Field Y:Int
	Field Parent:TPath
	Field Cost:Float
	Field Dist:Float
	Field After:TPath
EndType

</textarea><br><br><br><br>Example of how to use:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'Click to make walls.
'Rightclick to change starting point.


Strict
Import "pathfinder.bmx"



Graphics 800, 600

Local Grid:Float[20, 20]

TPathfinder.Diagonals = 2
TPathfinder.Randomity = 0
TPathfinder.SetMap(Grid, 20, 20)

Const TS = 29

Local fx, fy
Local my, mx
Local startx = 1, starty = 1
Local i
Local z, g:Float

MoveMouse 111, 111

Repeat
	mx = MouseX() / TS
	my = MouseY() / TS
	mx=Max(mx,0); mx=Min(mx,19)
	my=Max(my,0); my=Min(my,19)

	If MouseDown(1) Then Grid[mx, my] = 1
	
	If MouseHit(2) Then
		startx = mx
		starty = my
	EndIf
	
	
	TPathfinder.FindPath(startx, starty, mx, my)
	
	'use mousewheel to change tile speeds
	g = Grid[mx, my] + (MouseZ() - z) / 10.0
	z = MouseZ()
	If g &lt; 0 Then g = 0
	If g &gt; 1 Then g = 1
	Grid[mx, my] = g
	
	
	'Draw grid tiles
	For fy = 0 To 19
		For fx = 0 To 19
			If Grid[fx, fy] &gt; 0 Then
				SetColor Grid[fx, fy] * 255, 0, 0
				DrawRect fx * TS, fy * TS, TS, TS
			EndIf
		Next
	Next
	'Draw grid lines
	SetColor 255, 255, 255
	For fx = 0 To 20
		DrawRect fx * TS, 0, 1, 20 * TS
		DrawRect 0, fx * TS, 20 * TS, 1
	Next
	'Draw path
	SetColor 0, 255, 0
	For i = 0 To (TPathfinder.Paths - 1) * 2 Step 2
		DrawRect TPathfinder.Route[i] * TS + 5, TPathfinder.Route[i + 1] * TS + 5, 5, 5
	Next
	
	DrawText TPathfinder.Paths, 0, 0
	Flip
	Cls
	If KeyHit(key_escape) Then End
Forever

</textarea> <br><br></td></tr></table><br>
<a name="751132"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> I want your babies! <br><br></td></tr></table><br>
<a name="751137"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Curtastic</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> You're welcome, let me know if any improvements are needed, for the code archives or anything.<br><br>And my seed will conquer the world! <br><br></td></tr></table><br>
<a name="751140"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> ^_^<br>Why the 'abstract' btw? <br><br></td></tr></table><br>
<a name="751151"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Curtastic</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well I don't see the need to make new instances, since all you need is the route. I could easily change it though.<br><br>Right now if you want to keep 2 separate paths you have to do:<br><pre class=code>
local myroute[]
local blahroute[]

tpathfinder.findpath(x1,y1,x2,y2)
myroute=tpathfinder.route

tpathfinder.findpath(blahx,blahy,targetx,targety)
blahroute=tpathfinder.route
</pre> <br><br></td></tr></table><br>
<a name="751403"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Curtastic</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oops there was a big memory leak because of cyclic refrences, I just edited and fixed it. <br><br></td></tr></table><br>
<a name="751476"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's some code I borrowed from somewhere and ported, I've restricted to 4 directions but it's easy to change back.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Rem
bbdoc: 4 Direction Restricted aStar Module
End Rem
Module Indiepath.indie_astar

ModuleInfo "Version: 1.0"
ModuleInfo "Author: Tim Fisher"
ModuleInfo "License: Public Domain"
ModuleInfo "Modserver: BRL"

Import brl.linkedlist
Import brl.basic

Const	NOTFINISHED		= 0
Const	NOTSTARTED		= 0
Const	FOUND			= 1
Const	NONEXISTENT		= 2
Const	WALKABLE		= 0
Const	UNWALKABLE		= 1


' ***********************************************************************

Type aStar

	Global aStarList:TList

	Field MapWidth		: Int
	Field MapHeight		: Int
	Field Walkability	: Int[0,0]
	Field OpenList		: Int[0]
	Field WhichList		: Int[0,0]
	Field OpenX			: Int[0]
	Field OpenY			: Int[0]
	Field ParentX		: Int[0,0]
	Field ParentY		: Int[0,0]
	Field fCost			: Int[0]
	Field gCost			: Int[0,0]
	Field hCost			: Int[0]
	Field onClosedList	: Int
	Field pathStatus	: Int
	Field pathLength	: Int
	Field pathLocation	: Int
	Field pathBank		: TBank
	Field TargetX		: Int
	Field TargetY		: Int
	
		Method New ()
            	If aStarList = Null Then aStarList = New TList
            	aStarList.AddLast Self
	   	End Method	

		'----------------------------------------------------------------
		
		Function KillAll()
				While (Not aStarList.IsEmpty())
					aStarList.RemoveLast()
				Wend
		End Function
			
		'----------------------------------------------------------------
		
		Function Destroy(g:aStar)
				g.pathBank = Null
		     	aStarList.Remove g
				g = Null
        End Function
	
		'----------------------------------------------------------------
		
		Function Intitialise:aStar(width:Int,Height:Int)
			Local a:aStar = New aStar
			a.pathBank = CreateBank(0)
			a.MapWidth 		= width
			a.MapHeight 	= Height
			a.OpenList 		= New Int[a.MapWidth * a.MapHeight + 2]
			a.OpenX			= New Int[a.MapWidth * a.MapHeight + 2]
			a.OpenY			= New Int[a.MapWidth * a.MapHeight + 2]
			a.fCost			= New Int[a.MapWidth * a.MapHeight + 2]
			a.hCost			= New Int[a.MapWidth * a.MapHeight + 2]
			a.Walkability 	= New Int[a.MapWidth + 1 , a.MapHeight + 1]
			a.WhichList		= New Int[a.MapWidth + 1 , a.MapHeight + 1]
			a.ParentX		= New Int[a.MapWidth + 1 , a.MapHeight + 1]
			a.ParentY		= New Int[a.MapWidth + 1 , a.MapHeight + 1]
			a.gCost			= New Int[a.MapWidth + 1 , a.MapHeight + 1]
			Return a
		End Function
	
		'----------------------------------------------------------------
		
		Method FindPath(StartX:Int,StartY:Int,TargetX:Int,TargetY:Int) ' grid based co-ords
				
				Local NumberofOpenListItems:Int
				Local NewOpenListItemID:Int
				Local ParentXval:Int
				Local ParentYval:Int
				Local v:Int, u:Int, temp:Int, a:Int, b:Int, m:Int, x:Int, TempX:Int
				Local Direction:Int
				Local OnOpenList:Int
				Local TempGcost:Int
				Local Path:Int, PathX:Int, PathY:Int
				Local CellPosition:Int
				Local dx1#,dy1#,dx2#,dy2#,cross#,heuristic#
				
				
				self.TargetX = TargetX
				self.TargetY = TargetY
				If StartX = TargetX And StartY = TargetY And self.pathLocation &gt; 0 Then Return FOUND
				If StartX = TargetX And StartY = TargetY And self.pathLocation = 0 Then Return NONEXISTENT
				If self.Walkability[self.TargetX,self.TargetY] = UNWALKABLE Then	Return NONEXISTENT

				If self.onClosedList &gt; 1000000
					self.Whichlist = New Int[self.MapWidth,self.MapHeight]
					self.onClosedList = 10
				EndIf
				self.onClosedList:+2
				onOpenList = self.onClosedList - 1
				self.pathLength 	 = NOTSTARTED
				self.pathLocation 	 = NOTSTARTED
				self.gCost[StartX,StartY] = 0
				NumberofOpenListItems = 1
				self.OpenList[1] = 1
				self.OpenX[1] = StartX
				self.OpenY[1] = StartY
				
				Repeat			' Find Route Loop
				
					If NumberofOpenListItems &lt;&gt; 0 Then
						ParentXval = self.OpenX[self.OpenList[1]]
						ParentYval = self.OpenY[self.OpenList[1]]
						self.WhichList[ParentXval,ParentYval] = self.onClosedList
						self.OpenList[1] = self.OpenList[NumberofOpenListItems]
						NumberOfOpenListItems:-1
						v = 1				
						Repeat
							u = v
							If 2 * U + 1 &lt;= NumberofOpenListItems
								If self.fCost[self.OpenList[u]] &gt;= self.fCost[self.OpenList[2 * u]] Then v = 2 * u
								If self.fCost[self.OpenList[v]] &gt;= self.fCost[self.OpenList[2 * u + 1]] Then v = 2 * u + 1
							Else
								If 2 * u &lt;= NumberofOpenListItems
									If self.fCost[self.openList[u]] &gt;= self.fCost[self.OpenList[2 * u]] Then v = 2 * u
								EndIf
							EndIf
							If u &lt;&gt; v Then
								temp = self.OpenList[u]
								self.OpenList[u] = self.OpenList[v]
								self.OpenList[v] = temp
							Else
								Exit
							EndIf
						Forever
						
						For Direction = 1 To 4
							Select Direction
								Case 3
									a = ParentXval
									b = ParentYval - 1
								Case 4
									a = ParentXval - 1
									b = ParentYval
								Case 1
									a = ParentXval + 1
									b = ParentYval
								Case 2
									a = ParentXval
									b = ParentYval + 1
							End Select
							If a &lt;&gt; - 1 And b &lt;&gt; -1 And a &lt;&gt; Self.MapWidth And b &lt;&gt; Self.MapHeight
								If self.WhichList[a,b] &lt;&gt; self.OnClosedList
									If Self.Walkability[a,b] &lt;&gt; UNWALKABLE
										If self.WhichList[a,b] &lt;&gt; onOpenList
											NewOpenListItemID:+1
											m = NumberofOpenListItems + 1
											self.OpenList[m] = NewOpenListItemID
											self.OpenX[newOpenListItemID] = a
											self.OpenY[newOpenListItemID] = b
											self.gCost[a,b] = self.gCost[ParentXval,ParentYval] + 10
											
											self.hCost[self.OpenList[m]] = 10 * (Abs(a - self.TargetX) + Abs(b - Self.TargetY))
											
										'	dx1 = a - self.TargetX
										'	dy1 = b - self.TargetY
										'	dx2 = startX - self.TargetX
										'	dy2 = startY - self.TargetY
										'	cross = Abs(dx1*dy2 - dx2*dy1)
										'	heuristic :+ cross*0.001
										'	self.hCost[self.OpenList[m]] = 0'heuristic
											
											
											self.fCost[self.OpenList[m]] = self.gCost[a,b] + self.hCost[self.OpenList[m]]
											self.ParentX[a,b] = ParentXval
											self.ParentY[a,b] = ParentYval
											While m &lt;&gt; 1
												If self.fCost[self.OpenList[m]] &lt;= self.fCost[self.OpenList[m/2]] Then
													temp = self.OpenList[m/2]
													self.OpenList[m/2] = self.OpenList[m]
													self.OpenList[m] = temp
													m:/2
												Else
													Exit
												EndIf
											Wend
											NumberOfOpenListItems:+1
											self.WhichList[a,b] = onOpenList
										Else
											tempGcost = self.gCost[ParentXval,ParentYval]+10
											If tempGcost &lt; self.gCost[a,b] Then
												self.ParentX[a,b] = ParentXval
												self.ParentY[a,b] = ParentYval
												self.gCost[a,b] = tempGCost
												For x = 1 To NumberofOpenListItems
													If self.OpenX[self.OpenList[x]] = a And self.OpenY[self.OpenList[x]] = b Then
														self.fCost[self.OpenList[x]] = self.gCost[a,b] + self.hCost[self.OpenList[x]]
														m = x
														While m &lt;&gt; 1
															If self.fCost[self.OpenList[m]] &lt; self.fCost[self.openList[m/2]]
																Temp = self.OpenList[m/2]
																self.OpenList[m/2] = self.OpenList[m]
																self.OpenList[m] = temp
																m:/2
															Else
																Exit
															EndIf
														Wend
														Exit
													EndIf
												Next
											EndIf
										EndIf
									EndIf
								EndIf
							EndIf
						Next
					Else
						path = NONEXISTENT
						Exit
					EndIf
					
					If self.WhichList[self.TargetX,self.TargetY] = onOpenList Then
						Path = FOUND
						Exit
					EndIf					
				Forever
														
				If Path = FOUND
					PathX = Self.TargetX
					PathY = Self.TargetY
					Repeat
						tempx = self.ParentX[pathx,pathy]
						PathY = self.ParentY[pathx,pathy]
						PathX = tempX								
						self.PathLength:+1
					Until PathX = StartX And PathY = StartY
					
					ResizeBank(Self.PathBank,(self.pathLength+1) * 4)
									
					PathX = self.TargetX
					PathY = self.TargetY
					cellPosition = self.Pathlength * 4
					While Not (pathX = StartX And pathY = StartY)
						PokeShort (self.pathBank,cellPosition,PathX)
						PokeShort (self.pathBank,cellPosition+2,PathY)
						cellPosition:-4
						tempx = self.ParentX[pathX,pathY]
						pathY = self.ParentY[pathX,pathY]
						pathX = Tempx
					Wend
					PokeShort (self.pathBank,0,StartX)
					PokeShort (self.pathBank,2,StartY)
				EndIf
				Return Path
		End Method
		
		'----------------------------------------------------------------
		
		Method ReadPathX(PathLocation:Int)
			Return PeekShort(self.pathBank,PathLocation * 4)
		End Method
		
		'----------------------------------------------------------------
		
		Method ReadPathY(PathLocation:Int)
			Return PeekShort(self.pathBank,PathLocation * 4 + 2)
		End Method
		
			
End Type</textarea> <br><br></td></tr></table><br>
<a name="752026"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Diordna</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Awesome Curtastic, thanks! Couple of things:<br>-You misspelled "diagonal."<br>-If I put a value &gt;1 into the map, will it count it as being twice as slow to walk over? If not, what part of the code should I change to make it do this? This is essential for games with varying terrian. (I think it also means that Map[] should be changed to something with decimals, not an int, so I can say, for example, 1.5.) <br><br></td></tr></table><br>
<a name="752101"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Curtastic</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> -You misspelled "diagonal." <br></div><br>wow. How can I even be smart enough to code this? lol.<br>I seriously didnt know how to spell that. fixed.<br><br><div class="quote"> -If I put a value &gt;1 into the map, will it count it as being twice as slow to walk over? If not, what part of the code should I change to make it do this? This is essential for games with varying terrian. (I think it also means that Map[] should be changed to something with decimals, not an int, so I can say, for example, 1.5.) <br> <br></div><br>I could add support for that. Theoretically all you have to do is something like this:<br><pre class=code>
'cost is slightly more for diagnols
If Dir &lt; 4 Then
	NewP.Cost = P.Cost + CostOfTile
Else
	NewP.Cost = P.Cost + CostOfTile*Root2
EndIf
</pre> <br><br></td></tr></table><br>
<a name="752141"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think it needs something like being able to walk in diagonals while not cutting corners when there's a non-walkable thing to cross. Another option for the diagonals field perhaps? <br>0 no diagonals<br>1 diagonals + cutting<br>2 diagonals, no cutting<br><br>I keep seeing Warcraft in front of me where figures are walking in all 8 directions but aren't cutting corners at buildings/trees etc. <br><br></td></tr></table><br>
<a name="752147"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> Actually I think we should expand this pathfinder with all kinda extra functionality, <i>without loosing its neutrality</i> of course.<br><br>Apart from the walk/wall and the said ramping there could also be floors (used in cobination with ramping), meaning that from floor a you can't directly move to floor b, only via a ramp.<br><br>Also, there could be walkable tiles with priorities, high priority is to choose over low priority.<br><br>What about <i>legal</i> random errors in a path to simulate human errors? (otherwise you get straight marching of units). These errors could be simulated by analyzing the found path: for each step in the route check the surrounding tiles, if they are walkable then the route could add this to the route.<br><br>What about another route[] to store enviromental properties for each step? Like how many rings of directly walkable tiles are around the position of the step?<br><br>etc. all these things keep the thing neutral but could greatly enhance the functionality! <br><br></td></tr></table><br>
<a name="752153"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Damien Sturdy</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> Wow, nice bit of code! :) <br><br></td></tr></table><br>
<a name="752485"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Curtastic</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> I think it needs something like being able to walk in diagonals while not cutting corners when there's a non-walkable thing to cross. Another option for the diagonals field perhaps? <br>0 no diagonals<br>1 diagonals + cutting<br>2 diagonals, no cutting<br> <br></div><br>Ya I'll add that. I just didn't know if anyone wanted it.<br><br><div class="quote"> I keep seeing Warcraft in front of me where figures are walking in all 8 directions but aren't cutting corners at buildings/trees etc. <br> <br></div><br>Actually in warcraft (and most games I've seen) you can cut corners. Notice the footman is cutting a corner between farms. Most games have corners rounded, so there is room to cut inbetween.<br><img src="http://www.battle.net/images/battle/war2/pics/wall12.jpg"><br><br><br><div class="quote"> What about legal random errors in a path to simulate human errors? (otherwise you get straight marching of units). These errors could be simulated by analyzing the found path: for each step in the route check the surrounding tiles, if they are walkable then the route could add this to the route.<br> <br></div><br>Done!<br><br><br><div class="quote"> <br>What about another route[] to store enviromental properties for each step? Like how many rings of directly walkable tiles are around the position of the step?<br> <br></div><br>Well once the route is found you can always do that, it shouldn't be be the pathfinder's job.<br><br>Now lets say the object is larger than one tile, the pathfinder will need to find a path that the large object can fit through. That's something that I can add.<br><br><br><div class="quote"> <br>Also, there could be walkable tiles with priorities, high priority is to choose over low priority.<br> <br></div><br>Ya I'll do that except the tiles will store how long it takes to move into it. So a high number is slower and 0 is the fastest. [edit: done]<br><br><div class="quote"> <br>Wow, nice bit of code! :)  <br></div><br>thanks! <br><br></td></tr></table><br>
<a name="752503"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> ^_^ babies, more babies! ^_^<br><br>But one baby needs a new diper: try to move along the edges of the map with this randomness (place a wall next to it orso) and you'll get an assert exception (coords out of bounds).<br><br>[edit] hm.. or maybe not.. oO <br><br></td></tr></table><br>
<a name="752507"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#23">[#23]</a></td></tr></table></td></tr><tr ><td class="posttext"> the example could use this, under the:<br><br>mx = MouseX() / TS<br>my = MouseY() / TS<br><br><pre class=code>
	mx=Max(mx,0); mx=Min(mx,19)
	my=Max(my,0); my=Min(my,19)
</pre> <br><br></td></tr></table><br>
<a name="753198"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Curtastic</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#24">[#24]</a></td></tr></table></td></tr><tr ><td class="posttext"> ok, I added the no cuting corners option. Any more ideas? <br><br></td></tr></table><br>
<a name="753205"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GW</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#25">[#25]</a></td></tr></table></td></tr><tr ><td class="posttext"> nice!<br>perhaps the option to specify a variable width for moving agents as an argument, So the pathfinder could exclude paths that are to narrow for anything with a width greater than 1 cell. For example, a width=3 would represent a unit that occupies a 3x3 area. <br><br></td></tr></table><br>
<a name="753273"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#26">[#26]</a></td></tr></table></td></tr><tr ><td class="posttext"> well, those floors I mentioned perhaps, with ramps 'n stuff (like in Starcraft). Tho I wouldn't know how to include that in one single 2d array. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
