<!DOCTYPE html><html lang="en" ><head ><title >pointing into a circle. help please</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >pointing into a circle. help please</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >pointing into a circle. help please</a><br><br>
<a name="773533"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >peltazoid</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm trying to suss how to do the following for an idea for a shooter.<br><br>The player will be locked to moving in an arc of an ellipse or circle. I need the player to point toward the centre of the circle at all times and bullets fired travel toward the center the out of the edge of the game area.<br><br>I'm having trouble with getting the ship to point inwards and the bullets to fire at the correct angle. (although I suspect the two are linked.)<br><br>the code I have is dead basic and was knocked up for testing the ship rotation.<br><br>The second bit of code is what I have for the bullet path, but it does not take the correct path. I know some correction needs to take place, but I'm not sure how to detect which quadrant the player is in to correct the angle. My maths is quite shakey and does need a in depth refresh.<br><br><pre class=code>
' test rotation, ship should point into the circle.
Graphics 800 , 600

image = LoadImage("D:\Program Files\BlitzMax\samples\digesteroids\graphics\ship.png")
SetImageHandle(image , 10 , 11)

For i = 0 To 360
	
	x = 400 + Sin(i) * 200
	y = 300 + Cos(i) * 200

	
	ang = ATan2(x-400,y-300)
	If ang &lt; 0 Then ang:+360
	SetRotation(ang+180)
	
	DrawImage(image,x,y)
	Print i + "   " + x +"  " + y + "   " + ATan2(x-400,y-300)
	Flip
Next

WaitKey
</pre><br><br><pre class=code>
' test firing code should always fire into the center and beyond
Graphics 800 , 600

While Not KeyDown(KEY_ESCAPE)

	Cls
	
	mx = MouseX()
	px = 400 + Cos(mx) * 300
	py = 300 + Sin(mx) * 200
	
	DrawRect(px, py, 5 ,5)

	If MouseDown(1) And fire = False
		fire = True
		bx = px
		by = py
		
		ang = ATan2(bx-400,by-300)
	End If

	If fire = True
		DrawRect(bx, by, 2 ,2)
		bx :+ Cos(ang) * 10 
		by :+ Sin(ang) * 10
				
	End If
	Flip

Wend

End

</pre><br><br>Thanks<br>Andy <br><br></td></tr></table><br>
<a name="773534"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GfK</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just a quick observation - you have the ATan2 parameters back to front - it should be Y,X. <br><br></td></tr></table><br>
<a name="773553"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CoderLaureate</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's a basic TSprite class that I created for the use in my 2D games.  It has some advanced features.  <br><br><i>For example...</i><br><b>GetDistanceToXY</b> - Returns the distance between the sprite and any X/Y location on the screen.<br><b>TurnToXY</b> - Sets the sprites heading in the direction of any X/Y coordinates.  (The sprite can move toward the coords by setting the <i>StepSize</i> property to a number higher than 0).<br><b>PointATXY</b> - Turns the sprite to point at any X/Y coordinates on the screen.<br><br><pre class=code>
Global SpriteIndex:Int = -1
Global SpriteGroupIndex:Int = -1

Function GetNextSpriteIndex:Int()
	SpriteIndex:+ 1
	Return SpriteIndex
End Function

Function GetNextSpriteGroupIndex:Int()
	SpriteGroupIndex:+ 1
	Return SpriteGroupIndex
End Function

'Basic Sprite Graphics Object
Type TSprite Extends TLink

	'Public Properties
	Field zOrder:Int = GetNextSpriteIndex()
	Field x:Float, y:Float
	Field Rot:Float = 0.0
	Field alpha:Float = 1.0
	Field Blend:Int = GetBlend()
	Field ScaleX:Float = 1.0
	Field ScaleY:Float = 1.0
	Field MaskRed:Int = 255, MaskGreen:Int = 255, MaskBlue:Int = 255
	Field Solid:Int = True
	Field Heading:Float = 0.0
	Field StepSize:Float = 0.0
	
	'Private Properties  ***I Wish***
	Field Img:TImage
	Field tRot:Float, tAlpha:Int, tBlend:Int, tScaleX:Float, tScaleY:Float
	Field tRed:Int, tGreen:Int, tBlue:Int
	Field _CurrentFrame:Int = 0
	
	Method New()
		x = 0.0; y = 0.0
		Img = Null
	End Method
		
	Method Load(FileStream:Object, flags:Int = MASKEDIMAGE)
		Img = LoadImage(FileStream, flags)
	End Method
	
	Method Position(xpos:Float, ypos:Float)
		x = xpos; y = ypos
	End Method
	
	Method GetState()
		tRot = GetRotation()
		tAlpha = GetAlpha()
		tBlend = GetBlend()
		GetScale(tScaleX, tScaleY)
		GetColor(tRed, tGreen, tBlue)
	End Method
	
	Method SetState()
		SetRotation(Rot)
		SetAlpha(alpha)
		SetBlend(Blend)
		SetScale(ScaleX, ScaleY)
		SetColor(MaskRed, MaskGreen, MaskBlue)
	End Method
	
	Method RestoreState()
		SetRotation(tRot)
		SetAlpha(tAlpha)
		SetBlend(tBlend)
		SetScale(tScaleX, tScaleY)
		SetColor(tRed, tGreen, tBlue)
	End Method
	
	Method Render()
		GetState()
		SetState()
		If StepSize &lt;&gt; 0 Then
			Local dx:Float = Sin(Heading) * StepSize
			Local dy:Float = -Cos(Heading) * StepSize
			x:+ dx; y:+ dy
		End If
		DrawImage(Img, x, y, _CurrentFrame) 
		RestoreState()
	End Method
	
	Method Compare(Other:Object)
		spr:TSprite = TSprite(Other)
		If Not spr Then Return -1
		Return zOrder - spr.zOrder
	End Method
	
	Method GetDistanceToXY:Float(x1:Float, y1:Float)
		Local dx:Float = Abs(x - x1)
		Local dy:Float = Abs(y - y1)
		Return Sqr(dx ^ 2 + dy ^ 2)
	End Method

	Method TurnToXY:Float(x1:Float, y1:Float)
		Heading = Float(ATan2(x1 - x, y1 - y)) * -1
	End Method

	Method PointAtXY:Float(x1:Float, y1:Float)
		Rot = Float(ATan2(x1 - x, y1 - y)) * -1
	End Method


End Type

Type TSpriteGroup Extends TList
	
	Field Priority:Int = GetNextSpriteGroupIndex()

	Method Render()
		For Local obj:Object = EachIn Self
			Local sg:TSpriteGroup = TSpriteGroup(obj)
			If sg Then sg.Render()
			Local spr:TSprite = TSprite(obj)
			If spr Then spr.Render()
		Next
		
	End Method

End Type
</pre><br><br>Here's an animated TSprite class (Extends the TSprite class shown above).<br><br><pre class=code>

'A little less basic sprite object.
'Automatically does animation on a Frame by Frame basis.
Type TAnimSprite Extends TSprite

	'Additional Properties
	Field FrameCount:Int, StartFrame:Int, FrameDelay:Int
	Field _NextTick:Int
	Field Animate:Int
	
	Method New()
		X = 0.0; Y = 0.0; Img = Null
		FrameCount = 0; StartFrame = 0; FrameDelay = 0
		_CurrentFrame = 1; Animate = False
		_NextTick = 0
	End Method
	
	Method LoadAnim(FileStream:Object, Width:Int, Height:Int, Cell1:Int, CellCount:Int, FrameDuration:Int, Flags:Int = MASKEDIMAGE)
		Img = LoadAnimImage(FileStream,Width, Height, Cell1, CellCount, Flags)
		FrameCount = CellCount; StartFrame = Cell1
		FrameDelay = FrameDuration
		Animate = False
	End Method
	
	Method Render()
		Super.Render()		
		If Animate Then
			If MilliSecs() &gt; _NextTick Then
				_NextTick = MilliSecs() + FrameDelay
				_CurrentFrame:+ 1
				If _CurrentFrame =&gt; FrameCount Then _CurrentFrame = StartFrame
			End If
		End If
	End Method
	
End Type

</pre> <br><br></td></tr></table><br>
<a name="773720"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >peltazoid</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah those pesky coords :D<br><br>swapped them about and adjust the angle, yay. firing now occours with in the circle and and ship faces inside the circle. :D<br><br>the only thing is, the bullets do not pass though the very center of the circle which is what i am after.<br><br>any suggestions?<br><br>Cheers. <br><br></td></tr></table><br>
<a name="773885"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >bradford6</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> the bullets will not pass through if they are programmed to always 'point' to the center.<br><br>I suggest that you learn about Vectors and put together a Vector Type.<br><br>There are a few of these in the Code Archives to get you started. (Chroma's is very good)<br><br>every moving entity could have a velocity vector. <br><br></td></tr></table><br>
<a name="774048"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >peltazoid</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks. I have suss'd it now anyhow :D<br><br>The reason the shots were off is due to the fact I was not using floats, but ints instead and errors were creeping in :)<br><br>Cheers. <br><br></td></tr></table><br>
<a name="774049"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I was going to say it sounds like you were using integers.<br><br>Of course that's easy to say now, after the fact ;-D<br><br>There was a small game on the Amiga along these lines, where you moved around the circle shooting at stuff that expand outward from the center. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
