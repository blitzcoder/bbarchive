<!DOCTYPE html><html lang="en" ><head ><title >zlib compress/uncompress banks</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >zlib compress/uncompress banks</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >zlib compress/uncompress banks</a><br><br>
<a name="759514"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ghost Dancer</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well, what I thought would be a simple job of writing a data packer is turning into a complete nightmare!<br><br>I am trying to compress/uncompress a bank. This code was posted a while ago:<br><br><pre class=code>
Import Pub.ZLib

Function CompressBank:TBank( bank:TBank )
	Local size=bank.Size()
	Local out_size=size+size/10+32
	Local out:TBank=TBank.Create( out_size )
	compress out.Buf()+4,out_size,bank.Buf(),size
	out.PokeByte 0,size
	out.PokeByte 1,size Shr 8
	out.PokeByte 2,size Shr 16
	out.PokeByte 3,size Shr 24
	out.Resize out_size+4
	Return out
End Function

Function UncompressBank:TBank( bank:TBank )
	Local out_size
	out_size:|bank.PeekByte(0)
	out_size:|bank.PeekByte(1) Shl 8
	out_size:|bank.PeekByte(2) Shl 16
	out_size:|bank.PeekByte(3) Shl 24
	Local out:TBank=TBank.Create( out_size )
	uncompress out.Buf(),out_size,bank.Buf()+4,bank.Size()-4
	Return out
End Function
</pre><br><br>but it does not seem to compress, and the uncompress function errors. Has anyone actually got this (or something similar) working? <br><br></td></tr></table><br>
<a name="759591"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >klepto2</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Try this module, it is a Packer I have written for my personal use. It supports a tree like directory, Passwords, Zip and a simple password based encryption system. Source is available.<br>a comfortable Gui interface unfortunatly not. <br><br>If you have questions or you need an example post it and I will send you an email with the example.<br><br>Link: <a href="http://www.blitzforum.de/upload/file.php?id=1256" target="_blank">http://www.blitzforum.de/upload/file.php?id=1256</a> <br><br></td></tr></table><br>
<a name="759617"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yan</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's some old'n'crusty code...<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Rem
	Compress() example
End Rem

Strict

Local SFile:String = RequestFile("Source File")
Local DFile:String = RequestFile("Destination PAK", "pak", True)

CompressFile(SFile, DFile)

SFile = RequestFile("Source PAK", "pak")
DFile = RequestFile("Destination File", "", True)

DeCompressFile(SFile, DFile)

End


Function CompressFile(SourceFileName:String, DestFileName:String)
	Local SourceBank:TBank, DestBank:TBank
	Local SourceFileStream:TStream, DestFileStream:TStream
	Local SourceFileSize:Int, DestBankSize:Int
	
	If ExtractExt(DestFileName).ToLower() &lt;&gt; "pak" Then DestFileName :+ ".pak"
	
	SourceFileSize = FileSize(SourceFileName)
	SourceBank = CreateBank(SourceFileSize)
	
	SourceFileStream = OpenStream(SourceFileName, True, False)
	If SourceFileStream = Null Then  RuntimeError "Couldn't open source file ~q" + SourceFileName + "~q" 
	
	ReadBank(SourceBank, SourceFileStream, 0, SourceFileSize)
	
	CloseStream SourceFileStream
	
	DestBankSize = Ceil(SourceFileSize * 1.001) + 12
	DestBank = CreateBank(DestBankSize)

	compress(DestBank.Buf(), DestBankSize, SourceBank.Buf(), SourceFileSize)

	'DestBank.Resize(DestBankSize)
	
	DestFileStream = WriteStream(DestFileName)
	If DestFileStream = Null Then  RuntimeError "Couldn't create destination file ~q" + DestFileName + "~q"
	
	WriteInt DestFileStream, SourceFileSize
	WriteBank(DestBank, DestFileStream, 0, DestBankSize)
	
	CloseStream DestFileStream
End Function

Function DeCompressFile(SourceFileName:String, DestFileName:String)
	Local SourceBank:TBank, DestBank:TBank
	Local SourceFileStream:TStream, DestFileStream:TStream
	Local SourceFileSize:Int, DestBankSize:Int
	
	If ExtractExt(SourceFileName).ToLower() &lt;&gt; "pak" Then RuntimeError "Source file is not a PAK file"
	
	SourceFileSize = FileSize(SourceFileName) - 4
	SourceBank = CreateBank(SourceFileSize)
	
	SourceFileStream = OpenStream(SourceFileName, True, False)
	If SourceFileStream = Null Then  RuntimeError "Couldn't open source file ~q" + SourceFileName + "~q" 
	
	DestBankSize = ReadInt(SourceFileStream)
	ReadBank(SourceBank, SourceFileStream, 0, SourceFileSize)
	
	CloseStream SourceFileStream

	DestBank = CreateBank(DestBankSize)

	uncompress(DestBank.Buf(), DestBankSize, SourceBank.Buf(), SourceFileSize)
	
	DestFileStream = WriteStream(DestFileName)
	If DestFileStream = Null Then  RuntimeError "Couldn't create destination file ~q" + DestFileName + "~q"
	
	WriteBank(DestBank, DestFileStream, 0, DestBankSize)
	
	CloseStream DestFileStream
End Function</textarea><br>...and it seems to still work too. Always a bonus! <br><br></td></tr></table><br>
<a name="759778"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ghost Dancer</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you both - I've managed to get my own simple version working:<br><br><pre class=code>
Function compressBank:TBank(sourceBank:TBank, level = 9)
	Local destBankSize = Ceil(sourceBank.Size() * 1.001) + 12
	Local destBank:TBank = CreateBank(destBankSize)
	
	compress2(destBank.Buf(), destBankSize, sourceBank.Buf(), sourceBank.Size(), level)
	
	'resize bank to it's compressed size
	destBank.Resize(destBankSize)
	
	Return destBank
End Function


Function uncompressBank:TBank(sourceBank:TBank, destBankSize)
'destBankSize holds size of the uncompressed data saved previously
	Local destBank:TBank = CreateBank(destBankSize)
	
	uncompress(destBank.Buf(), destBankSize, sourceBank.Buf(), sourceBank.Size())
	
	Return destBank
End Function
</pre> <br><br></td></tr></table><br>
<a name="773959"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tachyon</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey klepto2 - Your packer mod looks nice. Could I have an example or two to get started with (as you so kindly offered in your post above!) =D<br><br>Thank you! <br><br></td></tr></table><br>
<a name="774413"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >klepto2</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here we go it is a converted of the tiledrop.bmx in the sample  folder of Blitzmax (BlitzmaxPath/samples/birdie/games/tiledrop)<br><br>Just save it within the same folder like the original bmx file.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict
Import klepto.klpacker

Local KLPack:TKLPack
'Creating the package if data.klp is not available

If FileType("Data.klp") = 0 Then 
	KLPack = CreatePackage("Data" , 9 , "tiledrop") ' Parameters: Packagename(without ending), CompressionRate,Password
	AddDirtoPack(KLPack , "media") ' Creating a Dir in the PAckage
	AddDirtoPack(KLPack , "media/test") ' Adding a second dir just as a test
	
	'Adding Files like You do with incbin
	AddFiletoPack(KLPACK , "media/back.png" , "media")
	AddFiletoPack(KLPACK , "media/blocks.png" , "media")
	AddFiletoPack(KLPACK , "media/part.png" , "media")
	AddFiletoPack(KLPACK , "media/pointer.png" , "media") 
	AddFiletoPack(KLPACK , "media/shine.png" , "media/test") ' Also just for testing an extra dir
	
	' At the end we save and build the package
	BuildPackage(KLPACK,"") 
Else
	'We load the package URL, and Password
	KLPack = LoadPackage("Data.klp" , "tiledrop")
	KLPAck.GetDirs() ' A small Helper Function to Display the File Hirarchy
EndIf	



Graphics 640 , 480

'Here we load the Files previously saved in the package

Global backIm:TImage = LoadImage(GetFile(KLPack,"media/back.png"))
AutoMidHandle 1
Global blocks:TImage = LoadAnimImage(GetFile(KLPack,"media/blocks.png"),32,32,0,16)
Global partImg:TImage = LoadImage(GetFile(KLPack,"media/part.png"))
Global mousePoint:TImage= LoadImage(GetFile(KLPack,"media/pointer.PNG"))
Global shine_img:TImage = LoadImage(GetFile(KLPack,"media/test/shine.png"))
AutoMidHandle 0
Global Map[8,14]
Global t1x,t1y,t2x,t2y
Global mouse_left_state
Global selection_done
Global rotation# = 0
Global Center_X#
Global Center_Y#
Global mt1,mt2
Global Tile_Rad#
Global THE_Axis
Global FLIPSPEED=8
Global Scn_Flash#=0
Global shine_pos#=0

HideMouse
While Not KeyDown(KEY_ESCAPE)
  Cls
  DrawLayout()
  If selection_done =0
    FillGrid()
  EndIf

  DrawGrid()

  If selection_done
    do_swap_tiles()
  Else
    UpdateSelection()
  EndIf

  SetBlend MASKBLEND
  DrawText mouse_left_state,0,0
  DrawText t1x+","+t1y,0,12
  DrawText t2x+","+t2y,0,23
  DrawImage mousePoint,MouseX(),MouseY()
  Flip
Wend

End
Function UpdateSelection()
  Local x=MouseX()-192
  Local y=MouseY()-24
  Local dx,dy

  If MouseDown(1)
    Select mouse_left_state
      Case 0
        If x&gt;=0 And x&lt;256
          If y&gt;=0 And y&lt;448
            'select tile1
            t1x=Floor(x/32.0)
            t1y=Floor(y/32.0)
          EndIf
        EndIf
        'get tile 1
        mouse_left_state=1
      Case 2
        If x&gt;=0 And x&lt;256
          If y&gt;=0 And y&lt;448
            'select tile1
            t2x=Floor(x/32.0)
            t2y=Floor(y/32.0)
          EndIf
        EndIf
        mouse_left_state=3
    EndSelect
  Else
    Select mouse_left_state
      Case 1
        mouse_left_state=2
      Case 3
        'check that only 1 tile away and not diag
        dx=Abs(t2x-t1x)
        dy=Abs(t2y-t1y)
        If dx=1 And dy=0
'          Switch(t1x,t1y,t2x,t2y)
          selection_done=1
        ElseIf dx=0 And dy=1
'          Switch(t1x,t1y,t2x,t2y)
          selection_done=1
        EndIf
          mouse_left_state=0
      Case 99
        mouse_left_state=0
    EndSelect
  EndIf

EndFunction

'add tiles to array at the top
Function FillGrid()
  Local x,y,tl,tlc
  For x=0 Until 8
    If map[x,0]=0
      map[x,0]=1+Rnd(1)*7'15
    EndIf
  Next
  'Fall
  For y=12 To 0 Step -1
    For x=0 Until 8
      If map[x,y]&gt;0
        If map[x,y+1]=0
          map[x,y+1]=map[x,y]
          map[x,y]=0
        EndIf
      EndIf
    Next
  Next

  For y=0 Until 14
    For x=0 Until 8
      tl=map[x,y]
      If tl&gt;0
        If Counttile(x,y,tl,0,tlc)
          KillTiles(x,y,tlc,0)
        ElseIf Counttile(x,y,tl,1,tlc)
          KillTiles(x,y,tlc,1)
        ElseIf Counttile(x,y,tl,2,tlc)
          KillTiles(x,y,tlc,2)
        ElseIf Counttile(x,y,tl,3,tlc)
          KillTiles(x,y,tlc,3)
        EndIf

      EndIf
    Next
  Next
EndFunction

Function KillTiles(x,y,c,dir)
  Local d
  For d=0 Until c
    Select dir
      Case 0
        map[x,y]=0
        y:-1
      Case 1
        map[x,y]=0
        x:+1
      Case 2
        map[x,y]=0
        y:+1
      Case 3
        map[x,y]=0
        x:-1
    EndSelect
  Next
EndFunction

Function CountTile(x,y,ty,dir, cn Var)
  cn = 0
  Select dir
    Case 0 ' Up
      While y&gt;0
        If map[x,y]=ty
          cn:+1
        Else
          Return cn&gt;2
        EndIf
        y=y-1
      Wend
      Return cn&gt;2
    Case 1 ' Right
      While x&lt;8
        If map[x,y]=ty
          cn:+1
        Else
          Return cn&gt;2
        EndIf
        x:+1
      Wend
      Return cn&gt;2
    Case 2 ' Down
      While y&lt;14
        If map[x,y]=ty
          cn:+1
        Else
          Return cn&gt;2
        EndIf
        y:+1
      Wend
      Return cn&gt;2
    Case 3 ' Left
      While x&gt;0
        If map[x,y]=ty
          cn:+1
        Else
          Return cn&gt;2
        EndIf
        x:-1
      Wend
      Return cn&gt;2
  EndSelect
  cn = 0
  Return 0
EndFunction

Function DrawGrid()
  SetColor 255,255,255
  Local x,y
  For y=0 Until 14
    For x=0 Until 8
      If selection_done
        If map[x,y]&gt;0
          If (x=t1x And y=t1y) Or (x=t2x And y=t2y)
          Else
            SetBlend MASKBLEND
            DrawImage blocks,208+x*32,32+y*32,map[x,y]-1
          EndIf
        EndIf
      Else
        If map[x,y]&gt;0
          DrawImage blocks,208+x*32,32+y*32,map[x,y]-1
        EndIf
      EndIf
    Next
  Next
  SetViewport 0,0,640,480
EndFunction

Function DrawLayout()
  Cls
  If Scn_Flash&lt;0
    Scn_Flash:+0.2
  Else
    Scn_Flash=0
  EndIf
    SetBlend SOLIDBLEND
    SetColor 255,255,255
  TileImage backIm,0,0
  SetColor 128,128,128
  SetViewport 192,24,256,448
  Cls
  TileImage backIm,0,0
  If shine_pos#=0  SetViewport 0,0,640,480
EndFunction

Function CausesPop()
  Local x,y,tl,tlc
  For y=0 Until 14
    For x=0 Until 8
      tl=map[x,y]
      If tl&gt;0
        If Counttile(x,y,tl,0,tlc)
          Return 1
        ElseIf Counttile(x,y,tl,1,tlc)
          Return 1
        ElseIf Counttile(x,y,tl,2,tlc)
          Return 1
        ElseIf Counttile(x,y,tl,3,tlc)
          Return 1
        EndIf
      EndIf
    Next
  Next
  Return 0
EndFunction

Function Switch(x0,y0,x1,y1)
  Local a
  a=map[x0,y0]
  map[x0,y0]=map[x1,y1]
  map[x1,y1]=a
  If CausesPop() Return 1
  a=map[x0,y0]
  map[x0,y0]=map[x1,y1]
  map[x1,y1]=a
  Return 0
EndFunction

Function Do_Swap_Tiles()
  Local x1,x2,y1,y2
  Local size1#,size2#
  Select selection_done
    Case 1
      'setup tile swap
      rotation=180
      selection_done = 2
      'calculate center
      x1 = 208+t1x*32
      x2 = 208+t2x*32
      y1 = 32+t1y*32
      y2 = 32+t2y*32
      Center_X = ((x2-x1)/2)+x1
      Center_Y = ((y2-y1)/2)+y1
      Tile_Rad = 16
      'check side
      If t1y=t2y
        If t1x&gt;t2x
          mt1 = map[t2x,t2y]
          mt2 = map[t1x,t1y]
        Else
          mt1 = map[t1x,t1y]
          mt2 = map[t2x,t2y]
        EndIf
      Else
        If t1y&gt;t2y
          mt1 = map[t2x,t2y]
          mt2 = map[t1x,t1y]
        Else
          mt1 = map[t1x,t1y]
          mt2 = map[t2x,t2y]
        EndIf
      EndIf

      THE_Axis = 1
      If t1y=t2y THE_Axis = 0
    Case 2
      If rotation&lt;0
        If Switch(t1x,t1y,t2x,t2y)
          selection_done = 0
        Else
          selection_done = 3
        EndIf
      Else
        rotation:-FLIPSPEED
        size2# = 0.80+0.20*Sin(rotation)
        size1# = 0.80+0.20*Sin(-rotation)
        Select THE_Axis
          Case 0 'x
            SetScale size1,size1
            DrawImage blocks, Center_X+Tile_Rad*Cos(rotation), Center_Y, mt1-1
            SetScale size2,size2
            DrawImage blocks, Center_X-Tile_Rad*Cos(rotation), Center_Y, mt2-1
            SetScale 1,1
          Case 1 ' y
            SetScale size1,size1
            DrawImage blocks, Center_X, Center_Y+Tile_Rad*Cos(rotation), mt1-1
            SetScale size2,size2
            DrawImage blocks, Center_X, Center_Y-Tile_Rad*Cos(rotation), mt2-1
            SetScale 1,1
        EndSelect
      '  DrawImage blocks,192+t2x*32,24+t2y*32,mt2
      EndIf
    Case 3 '
      If rotation&gt;=180
        selection_done = 0
      Else
        rotation:+FLIPSPEED
        size2# = 0.80+0.20*Sin(rotation)
        size1# = 0.80+0.20*Sin(-rotation)
        Select THE_Axis
          Case 0 'x
            SetScale size1,size1
            DrawImage blocks, Center_X+Tile_Rad*Cos(rotation), Center_Y, mt1-1
            SetScale size2,size2
            DrawImage blocks, Center_X-Tile_Rad*Cos(rotation), Center_Y, mt2-1
            SetScale 1,1
          Case 1 ' y
            SetScale size1,size1
            DrawImage blocks, Center_X, Center_Y+Tile_Rad*Cos(rotation), mt1-1
            SetScale size2,size2
            DrawImage blocks, Center_X, Center_Y-Tile_Rad*Cos(rotation), mt2-1
            SetScale 1,1
        EndSelect
      EndIf
  EndSelect

EndFunction
</textarea> <br><br></td></tr></table><br>
<a name="774425"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tachyon</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you Klepto2!! <br><br></td></tr></table><br>
<a name="844884"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MacSven</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have modified the Compression function to save the Packed data after compressing direct to the disk. The sourcebank is a bank that i created in the mainapplication as;<br><br>global mybank:tbank=createbank(4096)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Function compressBank:TBank(sourceBank:TBank, level = 9)
	Local destBankSize = Ceil(sourceBank.Size() * 1.001) + 12
	Local destBank:TBank = CreateBank(destBankSize)
	
	compress2(destBank.Buf(), destBankSize, sourceBank.Buf(), sourceBank.Size(), level)
	
	'resize bank to it's compressed size
	destBank.Resize(destBankSize)
	
	savebank (destBank, "Bank.pak" )
End Function


Function uncompressBank:TBank(sourceBank:TBank, destBankSize)
'destBankSize holds size of the uncompressed data saved previously
	Local destBank:TBank = CreateBank(destBankSize)
	
	uncompress(destBank.Buf(), destBankSize, sourceBank.Buf(), sourceBank.Size())
	
	Return destBank
End Function

</textarea><br><br>I want now that i load the compressed bank from disk and uncompress it direct to my bank. how can i realize that.<br><br>i create a new bank like:<br><br>local banknew:tbank=loadbank("bank.pak")<br><br>uncompressBank (banknew, 4095) <br><br>i setup the uncompress funtion to this:<br><br>	uncompress(myBankBank.Buf(), destBankSize, sourceBank.Buf(), sourceBank.Size())<br><br>but it does not work! Can someone help me. <br><br></td></tr></table><br>
<a name="1163440"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hardcoal</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> it would be nice if it was possible to read from a pak like from a disk.<br><br>lets say you want to loadimage using xors3d command XloadImage<br>how can you do that with your pak reader?<br><br>if you could do XLoadImage(ReadFile(PKLPacker,"FileName$") that would be nice<br><br>any way how do i use XloadImage or XLoadMesh with your packer? <br><br></td></tr></table><br>
<a name="1163482"></a>

<a name="1163485"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Zeke</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> (yeah, this is quite old topic, but...) you can use this: <a href="http://blitzbasic.com/Community/posts.php?topic=71734" target="_blank">http://blitzbasic.com/Community/posts.php?topic=71734</a><br>or simply use xMountPackFile(...)<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1163581"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> I recommend trying the LZMA module, it's faster and compresses better. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
