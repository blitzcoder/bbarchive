<!DOCTYPE html><html lang="en" ><head ><title >[Windows] Some fun with drawing on the desktop</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >[Windows] Some fun with drawing on the desktop</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >[Windows] Some fun with drawing on the desktop</a><br><br>
<a name="1334634"></a>

<a name="1334635"></a>

<a name="1334647"></a>

<a name="1334828"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> After the thread in General Help by Mainswhorty, about drawing to desktop i found this: <a href="https://www.codeproject.com/articles/856020/draw-behind-desktop-icons-in-windows" target="_blank">https://www.codeproject.com/articles/856020/draw-behind-desktop-icons-in-windows</a><br><br>The code there deals with drawing BEHIND the desktop icons, and was quite fun working out.<br>Im posting it here for future reference and for others to have fun with as well :)<br><br>Drawing using GDI, only works in Windows 8+. Though maybe one can draw directly to the shell window instead of the worker?<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

SuperStrict

SeedRnd MilliSecs()

Local workerw:Int = SpawnDesktopWorkerWindow()
If Not workerw Then Throw "no desktop worker window!"

Local dc:Int = GetDCEx( workerw, 0, DCX_WINDOW | DCX_CACHE | DCX_LOCKWINDOWUPDATE)
Local time:Int
Repeat
	TextOut dc, Rand(DesktopWidth()), Rand(DesktopHeight()), "Hello Desktop!"
	Delay 50
	time :+ 50
Until time &gt;= 5000
ReleaseDC( workerw, dc)
CloseDesktopWorkerWindow(workerw)
End


Function TextOut:Int( dc:Int, x:Int,y:Int, s:String)
	Extern "Win32"
		Function TextOut_:Int( dc:Int, x:Int,y:Int, s$w, sz:Int) = "TextOutW@20"
	EndExtern
	Return TextOut_( dc, x,y, s, s.Length)
EndFunction


Extern "Win32"
	Const SPAWN_DESKTOP_WOKER_WINDOW:Int = $052C
	
	Const SMTO_NORMAL:Int = $0000
	
	Const RDW_INVALIDATE:Int = $0001
	Const RDW_ERASE:Int = $0004
	
	Const RDW_UPDATENOW:Int = $0100
	Const RDW_ERASENOW:Int = $0200
	
	
	Const DCX_WINDOW:Int = $00000001
	Const DCX_CACHE:Int = $00000002
	Const DCX_LOCKWINDOWUPDATE:Int = $00000400

	Function GetShellWindow:Int()
	Function EnumWindows:Int( enumproc:Byte Ptr, lparam:Int)
	Function FindWindowExW:Int( hwndparent:Int, hwndchildafter:Int, classname$w, windowtitle$w)	
	Function SendMessageW:Int( hwnd:Int, msg:Int, wparam:Int, lparam:Int)
	Function SendMessageTimeoutW:Int( hwnd:Int, msg:Int, wparam:Int, lparam:Int, mode:Int, timeout:Int, result:Byte Ptr)
	Function GetDCEx:Int( hwnd:Int, region:Int, flags:Int)
	Function ReleaseDC:Int( hwnd:Int, dc:Int)
EndExtern

Function SpawnDesktopWorkerWindow:Int( timeout:Int = 1000)
	Local shell:Int = GetShellWindow()
	Local result:Int
	SendMessageTimeoutW( shell, SPAWN_DESKTOP_WOKER_WINDOW, 0, 0, SMTO_NORMAL, timeout, Varptr result)
		
	Function enumproc:Int( tophwnd:Int, lparam:Int) "win32"
		Local hwnd:Int = FindWindowExW( tophwnd, 0, "SHELLDLL_DefView", Null)
		If hwnd Then
			hwnd = FindWindowExW( 0, tophwnd, "WorkerW", Null)
			If hwnd Then Int Ptr(lparam)[0] = hwnd
		EndIf
		Return True
	EndFunction
	
	Local hwnd:Int
	EnumWindows( Byte Ptr enumproc, Int Varptr hwnd)
	
	Return hwnd
EndFunction

' doesnt close the window, only clears it. as it functions as a singleton.
Function CloseDesktopWorkerWindow( hwnd:Int, timeout:Int = 1000)
'	RedrawWindow( hwnd, Null, Null, RDW_ERASE | RDW_INVALIDATE | RDW_ERASENOW | RDW_UPDATENOW)
	ShowWindow( hwnd, False)
	ShowWindow( hwnd, True)	
EndFunction
</textarea><br><br>Drawing using Max2D and a seperate window, works in Windows 8+ but check further down for code to make it work on Windows 7:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

SuperStrict

WindowGraphics()
Local hwnd:Int = GetBlitzmaxWindow()

Local workerw:Int = SpawnDesktopWorkerWindow()
If Not workerw Then Throw "no desktop worker window!"
SetParent( hwnd, workerw)

SeedRnd MilliSecs()
Local time:Int
Repeat
	
	SetColor Rand(256),Rand(256),Rand(256)
	DrawRect Rand(DesktopWidth()), Rand(DesktopHeight()), Rand(100), Rand(100)
	
	Flip
'	Cls
	
	time :+ 10
Until time &gt;= 5000
EndGraphics
CloseDesktopWorkerWindow(workerw)
End


Function TextOut:Int( dc:Int, x:Int,y:Int, s:String)
	Extern "Win32"
		Function TextOut_:Int( dc:Int, x:Int,y:Int, s$w, sz:Int) = "TextOutW@20"
	EndExtern
	Return TextOut_( dc, x,y, s, s.Length)
EndFunction


Extern "Win32"
	Const SPAWN_DESKTOP_WOKER_WINDOW:Int = $052C
	
	Const SMTO_NORMAL:Int = $0000
	
	Const RDW_INVALIDATE:Int = $0001
	Const RDW_ERASE:Int = $0004
	
	Const RDW_UPDATENOW:Int = $0100
	Const RDW_ERASENOW:Int = $0200
	
	
	Const DCX_WINDOW:Int = $00000001
	Const DCX_CACHE:Int = $00000002
	Const DCX_LOCKWINDOWUPDATE:Int = $00000400

	Function GetShellWindow:Int()
	Function EnumWindows:Int( enumproc:Byte Ptr, lparam:Int)
	Function FindWindowExW:Int( hwndparent:Int, hwndchildafter:Int, classname$w, windowtitle$w)	
	Function SendMessageW:Int( hwnd:Int, msg:Int, wparam:Int, lparam:Int)
	Function SendMessageTimeoutW:Int( hwnd:Int, msg:Int, wparam:Int, lparam:Int, mode:Int, timeout:Int, result:Byte Ptr)
	Function SetParent:Int( hwnd:Int, hwndparent:Int)
EndExtern

Function SpawnDesktopWorkerWindow:Int( timeout:Int = 1000)
	Local shell:Int = GetShellWindow()
	Local result:Int
	SendMessageTimeoutW( shell, SPAWN_DESKTOP_WOKER_WINDOW, 0, 0, SMTO_NORMAL, timeout, Varptr result)
		
	Function enumproc:Int( tophwnd:Int, lparam:Int) "win32"
		Local hwnd:Int = FindWindowExW( tophwnd, 0, "SHELLDLL_DefView", Null)
		If hwnd Then
			hwnd = FindWindowExW( 0, tophwnd, "WorkerW", Null)
			If hwnd Then Int Ptr(lparam)[0] = hwnd
		EndIf
		Return True
	EndFunction
	
	Local hwnd:Int
	EnumWindows( Byte Ptr enumproc, Int Varptr hwnd)
	
	Return hwnd
EndFunction

' doesnt close the window, only clears it. as it functions as a singleton.
Function CloseDesktopWorkerWindow( hwnd:Int, timeout:Int = 1000)
'	RedrawWindow( hwnd, Null, Null, RDW_ERASE | RDW_INVALIDATE | RDW_ERASENOW | RDW_UPDATENOW)
	ShowWindow( hwnd, False)
	ShowWindow( hwnd, True)	
EndFunction



Function GetBlitzmaxWindow:Int()
?Win32
	Extern "Win32"
		Function FindWindowA:Int( classname$z, windowtitle$z)	
	EndExtern
	Local handle:Int
	handle = FindWindowA( "BBDX9Device Window Class", AppTitle)								' D3D9Max2D
	If Not handle Then handle = FindWindowA( "BlitzMax GLGraphics", AppTitle)			' GLMax2D
	If Not handle Then handle = FindWindowA( "BBDX7Device Window Class", AppTitle)	' D3D7Max2D
	If Not handle Then handle = FindWindowA( "BLITZMAX_WINDOW_CLASS", AppTitle)		' MaxGUI
	Return handle
?
EndFunction

Function WindowGraphics:TGraphics( depth:Int = 0, hz:Int = 60, flags:Int = 0)
	Extern "Win32"
		Const SPI_GETWORKAREA:Int = $30
		
		Function SystemParametersInfoW:Int( action:Int, param1:Int, param2:Byte Ptr, winini:Int)
	EndExtern

	' get size of desktop minus taskbar/toolbars
	Local rect:Int[4]
	SystemParametersInfoW( SPI_GETWORKAREA, 0, rect, 0)
	
	Local g:TGraphics = Graphics( rect[2], rect[3], depth, hz, flags)
	Local hwnd:Int = GetBlitzmaxWindow()
	Assert hwnd Else "unable to find BlitzMax window"

	' remove window frame
	Local wflags:Int = GetWindowLongW( hwnd, GWL_STYLE)	
	SetWindowLongW( hwnd, GWL_STYLE, wflags &amp; ~WS_CAPTION)
	SetWindowPos( hwnd, HWND_TOP, 0,0,0,0, SWP_FRAMECHANGED | SWP_NOZORDER | SWP_NOMOVE | SWP_NOSIZE | SWP_HIDEWINDOW)

	' force window refresh
	Cls
	
	' update window
	SetWindowPos( hwnd, HWND_TOP, rect[0],rect[1], rect[2], rect[3], SWP_NOZORDER | SWP_SHOWWINDOW)
	
	Return g
EndFunction
</textarea> <br><br></td></tr></table><br>
<a name="1334639"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GW</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Doesn't work for me. win7 pro. the exception is thrown. <br><br></td></tr></table><br>
<a name="1334646"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah yes, i forgot to add, this works for Win 8+, same as the article.<br>To get it to work for Windows 7, you would have to change the enumeration proc, i think you can change "SHELLDLL_DefView" to "SysListView32".<br>I dont have Windows 7 so am unable to test myself, but you can read the article and then try something like WinSpy to check the window relations. <br><br></td></tr></table><br>
<a name="1334714"></a>

<a name="1334715"></a>

<a name="1334716"></a>

<a name="1334717"></a>

<a name="1334718"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> There is a comment in that article which mentions how to do it for Windows 7, could work for the second example:<br><div class="quote"> <br>- Create the second WorkerW with the hidden user message as described here.<br>- Hide this new WorkerW that you just created with ShowWindow().<br>- Create your new custom window, that you want to draw behind the desktop icons, as a child of Progman.<br> <br></div><br><br>But again, the relationship between windows is different, so a change to the enumeration proc still has to be made so one can find the WorkerW window.<br><br>EDIT: Progman is the Shell window btw.<br><br>EDIT2: Had a friend with Windows 7 test for me, for the second example:<br>Change<br><pre class=code>SetParent( hwnd, workerw)</pre>to<pre class=code>SetParent( hwnd, GetShellWindow())
ShowWindow( workerw, False)</pre> <br><br></td></tr></table><br>
<a name="1334725"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah, EDIT2 works here on '7, pretty cool. Opens a huge ugly window to start with, but I imagine that could be hidden with a little hackery... <br><br></td></tr></table><br>
<a name="1334739"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah, the reason for that is because its using a Graphics window. And it is created AND shown before the changes are made to it, making it flicker (on slower computers anyway, i dont see it :p)<br><br>Theres no way around this without modifying the Max2D backends im afraid.<br>Using MaxGUI though, you can create the window hidden then show it after the changes, which works. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
