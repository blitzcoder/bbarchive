<!DOCTYPE html><html lang="en" ><head ><title >Digital Clock improvements</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Digital Clock improvements</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Digital Clock improvements</a><br><br>
<a name="1337597"></a>

<a name="1337598"></a>

<a name="1337599"></a>

<a name="1337600"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>(Posted April)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello everyone,<br><br>I've been working on a simple digital clock to work with my little radio app.<br><br><b>Code:</b><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' Digital Clock Example 0.1 by Grisu
' ////////////////////////////////////////////////////////////////////////////////////
SuperStrict

Framework MaxGui.Drivers
 Import BRL.GLMax2D
 Import BRL.StandardIO
 Import BRL.Pixmap
 Import BRL.EventQueue
 Import BRL.Timer
 Import BRL.Retro
 Import BRL.Max2D
 Import Brl.Pngloader
 Import Brl.Jpgloader

Const MAINWINDOW_W:Int = 278 
Const MAINWINDOW_H:Int = 154
Const WIN_MENU_FLAG:Int = 0

'Global SpectrumTimer:TTimer=CreateTimer(24)
Global Clocktimer:TTimer=CreateTimer(1)

' Global vars to hold the current time as string
Global curr_sys_time:String=CurrentTime$()
Global curr_sys_time_old:String=""

' Clock Params
Global dig_clock_Blocksize:Int=10     ' Blocksize in square
Global dig_clock_Blockspace:Int=2 ' Space bettween one sub block
Global dig_clock_Numspace:Int=6   ' Space between each number
Global dig_clock_draw_dots:Int=0 
'Global dig_clock_Img:TImage'=CreatePixmap(274,87,PF_BGR888) ' Holds the clock image itself

' Main Window
Global icol:Int
Global flags:Int=WINDOW_TITLEBAR | WINDOW_CLIENTCOORDS | WINDOW_CENTER | WIN_MENU_FLAG | WINDOW_ACCEPTFILES | WINDOW_RESIZABLE
Global window:TGadget=CreateWindow("Digital Clock Test",0,0,MAINWINDOW_W,MAINWINDOW_H+70,Null, flags)
Global Button2:TGadget=CreateButton("Exit APP",2,MAINWINDOW_H+70-20,MAINWINDOW_W-3,20,window,BUTTON_OK)

Global RadioStationCanvas:TGadget = CreateCanvas(2, 2, 274, 87, window, PANEL_ACTIVE) 

SetGraphics CanvasGraphics( RadioStationCanvas )
DrawbackgroundLogo()
Flip()

' //////////////// Main Loop

SetGraphics CanvasGraphics( RadioStationCanvas )

While WaitEvent()
	Select EventID()
		Case EVENT_WINDOWCLOSE
			End
		Case EVENT_GADGETACTION
   	 	      Select EventSource() 

                 Case BUTTON2
			          End 
                 End Select 

       Case EVENT_TIMERTICK 

  	  Select EventSource()

        Case Clocktimer
           curr_sys_time:String=CurrentTime$() ' only update data each 1 sec
           If curr_sys_time_old &lt;&gt; curr_sys_time Then 
             If dig_clock_draw_dots=0 Then dig_clock_draw_dots=1 Else dig_clock_draw_dots=0

               SetGraphics CanvasGraphics( RadioStationCanvas )
               DrawbackgroundLogo()
   	            Draw_Clock_Digits(curr_sys_time)
    
               Flip()
               If Mid$(curr_sys_time,5,1) &lt;&gt; Mid$(curr_sys_time_old,5,1) Then Print "Clock: "+curr_sys_time
   	           curr_sys_time_old=curr_sys_time ' updated to new time only when modified

           EndIf 

'		Case SpectrumTimer

       End Select 

	End Select
 
Wend 
End 


'////////////// Sub Functions 

'Function Update_ClockRawData()
'hour:Int=Int(Left$(CurrentTime$(),2))
'minute:Int=Int(Mid$(CurrentTime$(),4,2)) 
'second:Int=Int(Right$(CurrentTime$(),2)) 
'End Function

Function Draw_Clock_Digits(curr_sys_time:String)
Local i:Int 
Local dig_clocknum:Int
Local dig_clock_startx:Int
Local dig_clock_starty:Int

SetColor 0,0,0

dig_clock_starty=15

' Draw dots every second!
If dig_clock_draw_dots=1 Then 
     dig_clock_startx=56+dig_clock_Numspace*4+dig_clock_Blocksize*6
     Draw_Dig_Dots(dig_clock_startX:Int,dig_clock_startY:Int)
EndIf 

' go though the time string and get each HH and MM number from left to right

For i=1 To 4 


  Select i ' setting drawing position
   Case 1 'Hx:xx
    dig_clocknum:Int=Int(Mid$(curr_sys_time,1,1))
    dig_clock_startx=56

   Case 2 'xH:xx
    dig_clocknum:Int=Int(Mid$(curr_sys_time,2,1))
    dig_clock_startx=56+dig_clock_Numspace*2+dig_clock_Blocksize*3

   Case 3 'xx:Mx
    dig_clocknum:Int=Int(Mid$(curr_sys_time,4,1))
    dig_clock_startx=56+dig_clock_Numspace*5+dig_clock_Blocksize*7 

   Case 4 'xx:xM
    dig_clocknum:Int=Int(Mid$(curr_sys_time,5,1))
    dig_clock_startx=56+dig_clock_Numspace*7+dig_clock_Blocksize*10

  End Select  

'  Print dig_clocknum

  Select dig_clocknum
    Case 0
      Draw_Dig_0(dig_clock_startx,dig_clock_starty)
    Case 1
      Draw_Dig_1(dig_clock_startx,dig_clock_starty)
    Case 2
      Draw_Dig_2(dig_clock_startx,dig_clock_starty)
    Case 3
      Draw_Dig_3(dig_clock_startx,dig_clock_starty)
    Case 4
      Draw_Dig_4(dig_clock_startx,dig_clock_starty)
    Case 5
      Draw_Dig_5(dig_clock_startx,dig_clock_starty)
    Case 6
      Draw_Dig_6(dig_clock_startx,dig_clock_starty)
    Case 7
      Draw_Dig_7(dig_clock_startx,dig_clock_starty)
    Case 8
      Draw_Dig_8(dig_clock_startx,dig_clock_starty)
    Case 9
      Draw_Dig_9(dig_clock_startx,dig_clock_starty)
  End Select    

Next 

End Function 

Function Draw_Dig_Dots(dig_clock_startX:Int,dig_clock_startY:Int)
 ' . 
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace),dig_clock_Blocksize,dig_clock_Blocksize
 '   .
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*3,dig_clock_Blocksize,dig_clock_Blocksize
End Function

Function Draw_Dig_0(dig_clock_startX:Int,dig_clock_startY:Int)
 ' xxx	
 DrawRect dig_clock_startX,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize+dig_clock_Blockspace,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 ' x x
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace),dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace),dig_clock_Blocksize,dig_clock_Blocksize
 ' x x
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*2,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*2,dig_clock_Blocksize,dig_clock_Blocksize
 ' x x
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*3,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*3,dig_clock_Blocksize,dig_clock_Blocksize
 ' xxx	
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*4,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize+dig_clock_Blockspace,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*4,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*4,dig_clock_Blocksize,dig_clock_Blocksize
End Function

Function Draw_Dig_1(dig_clock_startX:Int,dig_clock_startY:Int)
 '    x	
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 '    x
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace),dig_clock_Blocksize,dig_clock_Blocksize
 '    x
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*2,dig_clock_Blocksize,dig_clock_Blocksize
 '    x
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*3,dig_clock_Blocksize,dig_clock_Blocksize
 '    x	
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*4,dig_clock_Blocksize,dig_clock_Blocksize
End Function

Function Draw_Dig_2(dig_clock_startX:Int,dig_clock_startY:Int)
 ' xxx	
 DrawRect dig_clock_startX,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize+dig_clock_Blockspace,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 '   x
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace),dig_clock_Blocksize,dig_clock_Blocksize
 ' xxx
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*2,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize+dig_clock_Blockspace,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*2,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*2,dig_clock_Blocksize,dig_clock_Blocksize
 ' x 
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*3,dig_clock_Blocksize,dig_clock_Blocksize
 ' xxx	
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*4,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize+dig_clock_Blockspace,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*4,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*4,dig_clock_Blocksize,dig_clock_Blocksize
End Function

Function Draw_Dig_3(dig_clock_startX:Int,dig_clock_startY:Int)
 ' xxx	
 DrawRect dig_clock_startX,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize+dig_clock_Blockspace,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 '   x
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace),dig_clock_Blocksize,dig_clock_Blocksize
 ' xxx
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*2,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize+dig_clock_Blockspace,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*2,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*2,dig_clock_Blocksize,dig_clock_Blocksize
 '   X 
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*3,dig_clock_Blocksize,dig_clock_Blocksize
 ' xxx	
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*4,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize+dig_clock_Blockspace,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*4,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*4,dig_clock_Blocksize,dig_clock_Blocksize
End Function

Function Draw_Dig_4(dig_clock_startX:Int,dig_clock_startY:Int)
 ' x x	
 DrawRect dig_clock_startX,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 ' x x
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace),dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace),dig_clock_Blocksize,dig_clock_Blocksize
 ' xxx
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*2,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize+dig_clock_Blockspace,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*2,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*2,dig_clock_Blocksize,dig_clock_Blocksize
 '   x
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*3,dig_clock_Blocksize,dig_clock_Blocksize
 '   x	
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*4,dig_clock_Blocksize,dig_clock_Blocksize
End Function

Function Draw_Dig_5(dig_clock_startX:Int,dig_clock_startY:Int)
 ' xxx	
 DrawRect dig_clock_startX,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize+dig_clock_Blockspace,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 ' x
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace),dig_clock_Blocksize,dig_clock_Blocksize
 ' xxx
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*2,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize+dig_clock_Blockspace,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*2,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*2,dig_clock_Blocksize,dig_clock_Blocksize
 '   X 
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*3,dig_clock_Blocksize,dig_clock_Blocksize
 ' xxx	
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*4,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize+dig_clock_Blockspace,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*4,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*4,dig_clock_Blocksize,dig_clock_Blocksize
End Function

Function Draw_Dig_6(dig_clock_startX:Int,dig_clock_startY:Int)
 ' xxx	
 DrawRect dig_clock_startX,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize+dig_clock_Blockspace,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 ' x
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace),dig_clock_Blocksize,dig_clock_Blocksize
 ' xxx
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*2,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize+dig_clock_Blockspace,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*2,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*2,dig_clock_Blocksize,dig_clock_Blocksize
 ' X X
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*3,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*3,dig_clock_Blocksize,dig_clock_Blocksize
 ' xxx	
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*4,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize+dig_clock_Blockspace,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*4,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*4,dig_clock_Blocksize,dig_clock_Blocksize
End Function

Function Draw_Dig_7(dig_clock_startX:Int,dig_clock_startY:Int)
 ' xxx	
 DrawRect dig_clock_startX,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize+dig_clock_Blockspace,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 '   x
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace),dig_clock_Blocksize,dig_clock_Blocksize
 '   x
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*2,dig_clock_Blocksize,dig_clock_Blocksize
 '   X
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*3,dig_clock_Blocksize,dig_clock_Blocksize
 '   x	
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*4,dig_clock_Blocksize,dig_clock_Blocksize
End Function

Function Draw_Dig_8(dig_clock_startX:Int,dig_clock_startY:Int)
 ' xxx	
 DrawRect dig_clock_startX,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize+dig_clock_Blockspace,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 ' x x
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace),dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace),dig_clock_Blocksize,dig_clock_Blocksize
 ' xxx
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*2,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize+dig_clock_Blockspace,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*2,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*2,dig_clock_Blocksize,dig_clock_Blocksize
 ' x x
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*3,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*3,dig_clock_Blocksize,dig_clock_Blocksize
 ' xxx	
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*4,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize+dig_clock_Blockspace,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*4,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*4,dig_clock_Blocksize,dig_clock_Blocksize
End Function

Function Draw_Dig_9(dig_clock_startX:Int,dig_clock_startY:Int)
 ' xxx	
 DrawRect dig_clock_startX,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize+dig_clock_Blockspace,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty,dig_clock_Blocksize,dig_clock_Blocksize
 ' x x
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace),dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace),dig_clock_Blocksize,dig_clock_Blocksize
 ' xxx
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*2,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize+dig_clock_Blockspace,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*2,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*2,dig_clock_Blocksize,dig_clock_Blocksize
 '   x
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*3,dig_clock_Blocksize,dig_clock_Blocksize
 ' xxx	
 DrawRect dig_clock_startX,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*4,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize+dig_clock_Blockspace,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*4,dig_clock_Blocksize,dig_clock_Blocksize
 DrawRect dig_clock_startX+dig_clock_Blocksize*2+dig_clock_Blockspace*2,dig_clock_starty+(dig_clock_Blocksize+dig_clock_Blockspace)*4,dig_clock_Blocksize,dig_clock_Blocksize
End Function

Function DrawbackgroundLogo()
 ' Used to "fake" a background image
 'Local station_logo:TImage

'   If station_logo = Null Then 
    'station_logo:TImage=LoadImage("station_ico.png")
 '  Else 
'
'   If station_logo &lt;&gt; Null Then
   '  DrawImage station_logo,0,0
  ' Else
   ' Print "no logo found!"
    SetColor 0,0,0
    DrawRect 0,0,274,87 ' outer border line
    SetColor 255,255,255
    DrawRect 1,1,272,85 ' inner canvas 
'   EndIf 

   SetColor 0,0,0
   DrawRect 1,9,272,69 
   SetColor 170,170,170
   DrawRect 1,10,272,67

End Function
</textarea><br><br>I'm drawing each block of the digits individually, which is highly inefficent. But I want to add some visual effects to each block, if possible.<br><br>Is there a way to create a transparent image of the numbers "on the fly" [HH MM] and copy it onto the background each minute (excluding the middle dots)? This would reduce the CPU overhead and draw calls.  <br><br>I'd also love to add some color changing to each indivdual digit rectangle for instance, but I'm unsure on how to do this effectively and visually pleasing. Perhaps there are some retro demo coders here assist. ;)<br>  <br>All in all I'd like to make the clock display a bit more interesting for the modern user. - Keep in mind, that the background will show different radio station logos. So the clock numbers have to be readable in different situations.<br><br>Any help or ideas are welcome. Thanks!<br>Grisu <br><br></td></tr></table><br>
<a name="1337601"></a>

<a name="1337602"></a>

<a name="1337603"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted April)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi, try using a PANEL and SetGadgetPixmap.<br><br>With a very tiny pixmap (ie: 20x8 pixels) then you can automatically stretch it to the pixmap on a panel.<br><br>edit:<br><br>simple experiment, no bound check on pixmap etc, so need to check it before 'drawing'<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

'BlitzMax program : 02 Apr 2017 18:17:10
'Template         : template_maxgui.bmx

SuperStrict
Import maxgui.drivers


Global window:tgadget=		CreateWindow("Main",100,100,300,300,,WINDOW_TITLEBAR|WINDOW_CENTER)

Global t_timer1:Ttimer=CreateTimer(60)

Global pix:TPixmap=CreatePixmap(16*5,16,PF_RGBA8888)

Global panel:tgadget=CreatePanel(10,10,128,64,window)
SetGadgetColor panel,255,0,0

Global map_digit:Tmap=CreateMap()

Type Tdigit
	Field led:Int[3,5]
End Type

CreateDigit("0","xxx-x x-x x-x x-xxx")
CreateDigit("1","  x-  x-  x-  x-  x")
CreateDigit("2","xxx-  x-xxx-x  -xxx")
CreateDigit("3","xxx-  x-xxx-  x-xxx")
CreateDigit("4","x x-x x-xxx-  x-  x")
CreateDigit("5","xxx-x  -xxx-  x-xxx")
CreateDigit("6","xxx-x  -xxx-x x-xxx")
CreateDigit("7","xxx-  x-  x-  x-  x")
CreateDigit("8","xxx-x x-xxx-x x-xxx")
CreateDigit("9","xxx-x x-xxx-  x-xxx")



Function DrawDigit:Int(p:TPixmap,name$,px:Int,py:Int)
	If p=Null Return 0
	Local cc:Int=$FF00FFFF

	Local digit:Tdigit=Tdigit(MapValueForKey(map_digit,name.tolower()))
	If digit=Null Return 0
	
	For Local nx:Int=0 Until 3
	For Local ny:Int=0 Until 5
			If digit.led[nx,ny]=1 
				PixDrawRect(p,cc,px+nx*2,py+ny*2,1,1)	
			End If
	
	Next
	Next
	
Return 0

End Function

Function CreateDigit:Int(name$,s$)
	s=s.tolower()
	Local t:Tdigit=New Tdigit
	t.led=New Int[3,5]
	Local y:Int
	For Local l$=EachIn s.split("-")

		For Local x:Int=0 Until Len(l)
			t.led[x,y]=0
			If l[x]=Asc("x") 
				t.led[x,y]=1

			End If
		Next
		y:+1	
	Next

	MapInsert map_digit,name.tolower(),t
	
	Return 0
End Function




Repeat
	WaitEvent()
	
	Select EventID()
		Case EVENT_APPTERMINATE, EVENT_WINDOWCLOSE
			End
		Case EVENT_TIMERTICK
			If EventSource()=t_timer1
				UpdateClock()			
			End If
	End Select
Forever

Function UpdateClock:Int()
		pix.ClearPixels($000000ff)
		Local cc:Int=$FF00FFFF
		
		Local ctime$=CurrentTime()
		Local ctc$[]
		ctc=ctc[..Len(ctime)+1]

		For Local e:Int=0 Until Len(ctime)
			ctc[e]=Chr(ctime[e])		
		Next

				
		For Local pd:Int=0 To 8
			DrawDigit(pix,ctc[pd],pd*8+1,1)
		'		Print ctc[pd]
		Next
		
					
		SetGadgetPixmap panel,pix,PANELPIXMAP_FIT
		
		
Return 0
End Function

Function PixDrawRect:Int(p:TPixmap,cc:Int,x:Int,y:Int,w:Int=0,h:Int=0)
	For Local xx:Int=x To x+w
	For Local yy:Int=y To y+h
		WritePixel p,xx,yy,cc	
	Next	
	Next
Return 0

End Function
</textarea> <br><br></td></tr></table><br>
<a name="1337604"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>(Posted April)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Can't use a panel, need this to be a gfx canvas as it's used for much other stuff. <br><br></td></tr></table><br>
<a name="1337605"></a>

<a name="1337607"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted April)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> OK, in this case I think there's no way: you should use your code and write it on the canvas. <br>Scaling a pixmap 'manually' is not so efficient, better draw directly on the image/original pixmap.<br><br>An option could be to use 'block image' instead of DrawBox: this will allow you to have nice gfx effect/change shape and color.<br>Drawing 15 'blocks' for a single digit maybe could be faster than calling 15 times DrawRect... but I never tested it.<br><br>edit:<br>using a Canvas and DrawRect... it should be easy to replace it with DrawImage<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

'BlitzMax program : 02 Apr 2017 19:07:02
'Template         : template_maxgui.bmx

SuperStrict
Import maxgui.drivers

Global map_digit:Tmap=CreateMap()
Global window:tgadget=		CreateWindow("Main",100,100,300,300,,WINDOW_TITLEBAR|WINDOW_CENTER)
Global canvas:Tgadget=		CreateCanvas(10,10,256,64,window)

Global t_timer1:Ttimer=CreateTimer(1) 

CreateDigit("0","xxx-x x-x x-x x-xxx")
CreateDigit("1","  x-  x-  x-  x-  x")
CreateDigit("2","xxx-  x-xxx-x  -xxx")
CreateDigit("3","xxx-  x-xxx-  x-xxx")
CreateDigit("4","x x-x x-xxx-  x-  x")
CreateDigit("5","xxx-x  -xxx-  x-xxx")
CreateDigit("6","xxx-x  -xxx-x x-xxx")
CreateDigit("7","xxx-  x-  x-  x-  x")
CreateDigit("8","xxx-x x-xxx-x x-xxx")
CreateDigit("9","xxx-x x-xxx-  x-xxx")


Repeat
	WaitEvent()
	
	Select EventID()
		Case EVENT_APPTERMINATE, EVENT_WINDOWCLOSE
			End
			
		Case EVENT_TIMERTICK
			If EventSource()=t_timer1 RedrawGadget canvas

		Case EVENT_GADGETPAINT
			SetGraphics CanvasGraphics(canvas)
			Cls
			UpdateClock(6)
			Flip

	End Select
Forever

Type Tdigit
	Field led:Int[3,5]
End Type



Function DrawDigit:Int(name$,px:Int,py:Int,sx:Int=1,sy:Int=1)
	Local digit:Tdigit=Tdigit(MapValueForKey(map_digit,name.tolower()))
	If digit=Null Return 0
	
	Local bw:Int,bh:Int
	
	bw=sx-1
	bh=sy-1
	
	If sx=1 bw=0
	If sy=1 bh=0
	For Local nx:Int=0 Until 3
	For Local ny:Int=0 Until 5
			If digit.led[nx,ny]=1 
			DrawRect px+nx*sx,py+ny*sy,bw,bh
			End If
	Next
	Next
	
Return 0

End Function

Function CreateDigit:Int(name$,s$)
	s=s.tolower()
	Local t:Tdigit=New Tdigit
	t.led=New Int[3,5]
	Local y:Int
	For Local l$=EachIn s.split("-")

		For Local x:Int=0 Until Len(l)
			t.led[x,y]=0
			If l[x]=Asc("x") 
				t.led[x,y]=1

			End If
		Next
		y:+1	
	Next

	MapInsert map_digit,name.tolower(),t
	
	Return 0
End Function

Function UpdateClock:Int(scale:Int=6)
		Local ctime$=CurrentTime()
		Local ctc$[]
		ctc=ctc[..Len(ctime)+1]
		For Local e:Int=0 Until Len(ctime)
			ctc[e]=Chr(ctime[e])		
		Next
		For Local pd:Int=0 To 8
			DrawDigit(ctc[pd],pd*4*scale+1,10,scale,scale)
			'DrawImage ...
		Next
		Return 0
End Function


</textarea> <br><br></td></tr></table><br>
<a name="1337610"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>(Posted April)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Christian. <br><br>Will dig deeper into it over the week. Your code looks much more compact than mine. ;) <br><br>I'm forced to stay within certain code limits. <br><br>P.S.: Do you think a HH MM SS format is better than mine (HH : MM)? <br><br></td></tr></table><br>
<a name="1337614"></a>

<a name="1337615"></a>

<a name="1337616"></a>

<a name="1337617"></a>

<a name="1337618"></a>

<a name="1337730"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>(Posted April)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Uploaded an exe of PRP with the clock code implemented:<br>| link removed |<br><br><img src="https://s11.postimg.org/q2dx6r503/prp_clk4.png"><br><br>P.S.: You need a full version of PRP on your system and just copy the exe over. Select "Digital Clock" from the spectrum dropdown.<br><br>Too simplistic? Works fine though. :) <br><br></td></tr></table><br>
<a name="1337660"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted April)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well I chose HH:MM:SS just to see the result as soon as possible: waiting a minute just to check the change was too much for my taste :D !<br>Of course it could be an option for your app...<br><br>My code is 'compact' just because I re-coded each single number in a 'pattern': of course you could use the same way to code even letters or symbols (like ':') to have a 'scrolling title' ... (another option of course).<br><br>I would like to test with images (just an idea: it's Christmas or Halloween? Just change the color/shape of any single image-block...) <br><br></td></tr></table><br>
<a name="1337674"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>(Posted April)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Switched back to images, think this looks better and gives better performance for sure.<br><br><b>Alpha 2:</b><br><img src="https://s24.postimg.org/z9ouc2h9h/prp_clock.png"><br><br>For the season idea: I might hire an artist to do some new images (272x85 pixels each). So I can replace the default logo file for PRP below according to special events.<br><img src="https://s2.postimg.org/cqzpkx4kp/prp_noico.png"> <br><br></td></tr></table><br>
<a name="1337716"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted April)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> 'liquid crystal' style are very good! <br><br></td></tr></table><br>
<a name="1337731"></a>

<a name="1337734"></a>

<a name="1337735"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>(Posted April)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for the feedback!<br><br><img src="https://s22.postimg.org/vkoy7i335/prp98_test.png"><br><br>I uploaded a temp exe build here for the current version:<br><a href="http://www.mediafire.com/file/2283lrr3lhbvvxp/prp_test.zip" target="_blank">http://www.mediafire.com/file/2283lrr3lhbvvxp/prp_test.zip</a> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
