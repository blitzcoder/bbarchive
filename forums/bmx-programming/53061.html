<!DOCTYPE html><html lang="en" ><head ><title >MaxGUI thingy question</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >MaxGUI thingy question</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >MaxGUI thingy question</a><br><br>
<a name="593085"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kanati</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Since I can't get it til someone sends me my BUID...   &lt;ahem&gt;<br><br>Has it been confirmed one way or another if the gadgets can be used in a canvas (full screen)?<br><br>I'd much rather use standard windows UI stuff in my game if possible. <br><br></td></tr></table><br>
<a name="593208"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kanati</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'll take that as a most likely "no" <br><br></td></tr></table><br>
<a name="593211"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I don't see why not.  You might have to change the desktop size yourself, and create a window that fills the whole screen, but I don't see any reason it shouldn't work. <br><br></td></tr></table><br>
<a name="593258"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >FlameDuck</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote">  I'll take that as a most likely "no"  <br></div>I don't think it's even possible to have Windows GUI elements in fullscreen. You'll notice FileRequest for instance, kills the fullscreen display (and depending on your drivers, the OS as well). <br><br></td></tr></table><br>
<a name="593434"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kanati</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've seen directx games that definitely use regular windows gui elements in them.  But for the life of me I can't remember what games they were.  I think it's becoming some lost mystic art or something.  :) <br><br></td></tr></table><br>
<a name="593508"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >FlameDuck</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> I've seen directx games that definitely use regular windows gui elements in them. <br></div>I've seen full screen games that use components that <b>look like</b> the regular Windows GUI, but never any that actually did, unless running "full screen" in a window. <br><br></td></tr></table><br>
<a name="593841"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >WendellM</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Has it been confirmed one way or another if the gadgets can be used in a canvas (full screen)? <br></div><br>A "Graphics 640,480,32,60"-type fullscreen?  It doesn't seem so.  A "canvas covering the whole desktop"-type 'fullscreen'?  Yes.  Fairly quick-n-dirty code exploring this follows...<br><br>First off, it's not too hard to rescale gadgets to have a consistent look regardless of the window's size:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">'Scaling Hello World

Strict 

Local WindowWidth# = 300, DeltaW#
Local WindowHeight# = 140, DeltaH#
Local FontSize = 10

Local window:TGadget = CreateWindow("Scaling Hello World",40,40,WindowWidth ,WindowHeight ,Desktop(), WINDOW_TITLEBAR | WINDOW_RESIZABLE )

Local panel:TGadget = CreatePanel(0,0,ClientWidth(window),ClientHeight(window),window,PANEL_ACTIVE)
SetGadgetColor panel,200,100,100
SetGadgetLayout panel,EDGE_ALIGNED,EDGE_ALIGNED,EDGE_ALIGNED,EDGE_ALIGNED

Local font:TGuiFont = LoadGuiFont( "Arial",FontSize)

Local textfield:TGadget = CreateTextField(14,14,150,20,panel)
SetGadgetFont textfield,font
SetGadgetLayout textfield,EDGE_RELATIVE,EDGE_RELATIVE,EDGE_RELATIVE,EDGE_RELATIVE

Local button:TGadget = CreateButton("OK",170,14,80,24,panel,BUTTON_OK)
SetGadgetFont button,font
SetGadgetLayout button,EDGE_RELATIVE,EDGE_RELATIVE,EDGE_RELATIVE,EDGE_RELATIVE

While WaitEvent()
	Select EventID()
	Case EVENT_GADGETACTION
		Select EventSource()
			Case textfield
				Print "textfield updated"
			Case button
				SetGadgetText( textfield,"Hello, world!" )
		End Select
	Case EVENT_MOUSEDOWN
		Select EventSource()
			Case panel ' just making sure that a panel can be clicked - for later exploration
				SetGadgetText( textfield,"Da panel!  Da panel!" )
		End Select
	Case EVENT_WINDOWSIZE
		'DebugLog 
		'DebugLog GadgetHeight( window )
		DeltaW = GadgetWidth( window ) / WindowWidth
		DeltaH = GadgetHeight( window ) / WindowHeight
		If DeltaW &lt; DeltaH Then
			font = LoadGuiFont( "Arial",FontSize * DeltaW)
		Else
			font = LoadGuiFont( "Arial",FontSize * DeltaH)
		EndIf
		SetGadgetFont textfield,font
		SetGadgetFont button,font
		RedrawGadget panel 'redrawing panel &amp; textfield helps reduce garbage when shrinking
		RedrawGadget textfield
	Case EVENT_WINDOWCLOSE
		End
	End Select
Wend</textarea><br>So, you can make your windows/gadgets look pretty much the same regardless of the size that the user/player has his desktop set at (rescaling them all to take up the same percentage of screen space).  It <i>might</i> be possible to change the desktop size, then change it back when done, but I imagine that could be a bit messy if your game dies and leaves it at the wrong size (I'm not up on doing this, and I personally prefer avoiding platform-specific tricks).  <br><br>The examples below don't do this dynamic scaling, but they could.  They also aren't too happy on a Mac, but I'd guess(?) that's just a matter of a little fiddling around (I've been reading of some 1.12/MaxGUI Mac bugs).<br><br>Here's the sort of mix of graphical area and control area that I want for my games (the map is a fixed area at left, with a non-movable set of controls on the right):<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict

Local ScreenW = GadgetWidth( Desktop() )
Local ScreenH = GadgetHeight( Desktop() )

'the main window masquerading as "desktop"
Local desk:TGadget = CreateWindow("Fake Desktop", 0 , 0, ScreenW ,ScreenH, Desktop(), Null) '

' a graphics area taking up the left 3/4 of screen
Local canvas:TGadget = CreateCanvas (0,0, ScreenW * 0.75, ScreenH, desk )

' a control area taking up the remaining 1/4
Local window:TGadget = CreateWindow("My Window",ScreenW*0.75, 0, ScreenW*0.25, ScreenH, desk, WINDOW_MENU )

Local button:TGadget[4]
button[0]=CreateButton("Draw Random Rect",10,10,140,24,window,BUTTON_PUSH)
button[1]=CreateButton("Use Oval instead of Rect",10,34,140,24,window,BUTTON_CHECKBOX)
button[2]=CreateButton("Blue",10,58,140,24,window,BUTTON_RADIO)
button[3]=CreateButton("Red",10,58+24,140,24,window,BUTTON_RADIO)
SetButtonState(button[2],True) ' start blue

'Set up a typical menu:

Const MENU_NEW=101
Const MENU_OPEN=102
Const MENU_SAVE=103
Const MENU_CLOSE=104
Const MENU_EXIT=105

Const MENU_CUT=106
Const MENU_COPY=107
Const MENU_PASTE=108

Const MENU_ABOUT=109

Local filemenu:TGadget = CreateMenu("&amp;File",0,WindowMenu(window))
Local FileMenuItem[5]
FileMenuItem[0] = CreateMenu("&amp;New",MENU_NEW,filemenu,KEY_N,MODIFIER_COMMAND)
FileMenuItem[1] = CreateMenu("&amp;Open",MENU_OPEN,filemenu,KEY_O,MODIFIER_COMMAND)
FileMenuItem[2] = CreateMenu("&amp;Close",MENU_CLOSE,filemenu,KEY_W,MODIFIER_COMMAND)
CreateMenu"",0,filemenu
FileMenuItem[3] = CreateMenu("&amp;Save",MENU_SAVE,filemenu,KEY_S,MODIFIER_COMMAND)
CreateMenu"",0,filemenu
FileMenuItem[4] = CreateMenu("E&amp;xit",MENU_EXIT,filemenu,KEY_F4,MODIFIER_COMMAND)

Local EditMenu:TGadget = CreateMenu("&amp;Edit",0,WindowMenu(window))
CreateMenu "Cu&amp;t",MENU_CUT,editmenu,KEY_X,MODIFIER_COMMAND
CreateMenu "&amp;Copy",MENU_COPY,editmenu,KEY_C,MODIFIER_COMMAND
CreateMenu "&amp;Paste",MENU_PASTE,editmenu,KEY_V,MODIFIER_COMMAND

Local HelpMenu:TGadget = CreateMenu("&amp;Help",0,WindowMenu(window))
CreateMenu "&amp;About",MENU_ABOUT,helpmenu

For Local i = 0 To 3
	DisableMenu FileMenuItem[i]
Next
DisableMenu EditMenu

UpdateWindowMenu window

'Prepare canvas for drawing:
SetGraphics CanvasGraphics(canvas)
Local graphW = GraphicsWidth()
Local graphH = GraphicsHeight()
Cls
DrawText "graphics width:" + graphW, 0, 0
DrawText "graphics height:" + graphH, 0, 10
Flip

Local RectX, RectY, RectW, RectH
Local OvalNotRect = False


'Main loop
While True
	WaitEvent 
	Select EventID()
		Case EVENT_WINDOWCLOSE
			End
		Case EVENT_APPRESUME ' user alt-tabbed away, so need to refresh canvas graphics display
			Flip
		Case EVENT_WINDOWMOVE ' clean up after any window moving over canvas
			Flip
		Case EVENT_MENUACTION
			Select EventData()
				Case MENU_EXIT
					End
				Case MENU_ABOUT
					Notify "A 'full screen' GUI" 
					Flip ' Notify messes up canvas if moved so clean up when done
			End Select
		Case EVENT_GADGETACTION
			Select EventSource()
				Case button[0]
					RectW = 10 + Rand( 50 )
					RectH = 10 + Rand( 50 )
					RectX = Rand( GraphW - RectW ) 
					RectY = Rand( GraphH - RectH ) 
					If ButtonState(button[2]) Then SetColor 0,0,255 ' blue
					If ButtonState(button[3]) Then SetColor 255,0,0 ' red
					If ButtonState(button[1]) Then 
						DrawOval RectX, RectY, RectW, RectH
					Else
						DrawRect RectX, RectY, RectW, RectH
					EndIf
					Flip				
				End Select
	End Select
Wend</textarea><br><br>It seems possible to Alt-Tab away from and back to this app safely.<br><br>If you prefer a floating window moving over a full-desktop, that's possible too (I imagine that the "garbage trail" that appears could be reduced/eliminated, but I haven't investigated that since I plan to use the non-moving control area above):<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict

Local ScreenW = GadgetWidth( Desktop() )
Local ScreenH = GadgetHeight( Desktop() )

'the main window masquerading as "desktop"
Local desk:TGadget = CreateWindow("Fake Desktop", 0 , 0, ScreenW ,ScreenH, Desktop(), Null) 

' a graphics area taking up full screen
Local canvas:TGadget = CreateCanvas (0,0, ScreenW , ScreenH, desk )

' floating control area
Local window:TGadget = CreateWindow("My Window",40, 40, 300, 200, desk, WINDOW_TITLEBAR | WINDOW_MENU )

Local button:TGadget[4]
button[0]=CreateButton("Draw Random Rect",10,10,140,24,window,BUTTON_PUSH)
button[1]=CreateButton("Use Oval instead of Rect",10,34,140,24,window,BUTTON_CHECKBOX)
button[2]=CreateButton("Blue",10,58,140,24,window,BUTTON_RADIO)
button[3]=CreateButton("Red",10,58+24,140,24,window,BUTTON_RADIO)
SetButtonState(button[2],True) ' start blue

'Set up a typical menu:

Const MENU_NEW=101
Const MENU_OPEN=102
Const MENU_SAVE=103
Const MENU_CLOSE=104
Const MENU_EXIT=105

Const MENU_CUT=106
Const MENU_COPY=107
Const MENU_PASTE=108

Const MENU_ABOUT=109

Local filemenu:TGadget = CreateMenu("&amp;File",0,WindowMenu(window))
Local FileMenuItem[5]
FileMenuItem[0] = CreateMenu("&amp;New",MENU_NEW,filemenu,KEY_N,MODIFIER_COMMAND)
FileMenuItem[1] = CreateMenu("&amp;Open",MENU_OPEN,filemenu,KEY_O,MODIFIER_COMMAND)
FileMenuItem[2] = CreateMenu("&amp;Close",MENU_CLOSE,filemenu,KEY_W,MODIFIER_COMMAND)
CreateMenu"",0,filemenu
FileMenuItem[3] = CreateMenu("&amp;Save",MENU_SAVE,filemenu,KEY_S,MODIFIER_COMMAND)
CreateMenu"",0,filemenu
FileMenuItem[4] = CreateMenu("E&amp;xit",MENU_EXIT,filemenu,KEY_F4,MODIFIER_COMMAND)

Local EditMenu:TGadget = CreateMenu("&amp;Edit",0,WindowMenu(window))
CreateMenu "Cu&amp;t",MENU_CUT,editmenu,KEY_X,MODIFIER_COMMAND
CreateMenu "&amp;Copy",MENU_COPY,editmenu,KEY_C,MODIFIER_COMMAND
CreateMenu "&amp;Paste",MENU_PASTE,editmenu,KEY_V,MODIFIER_COMMAND

Local HelpMenu:TGadget = CreateMenu("&amp;Help",0,WindowMenu(window))
CreateMenu "&amp;About",MENU_ABOUT,helpmenu

For Local i = 0 To 3
	DisableMenu FileMenuItem[i]
Next
DisableMenu EditMenu

UpdateWindowMenu window

'Prepare canvas for drawing:
SetGraphics CanvasGraphics(canvas)
Local graphW = GraphicsWidth()
Local graphH = GraphicsHeight()
Cls
DrawText "graphics width:" + graphW, 0, 0
DrawText "graphics height:" + graphH, 0, 10
Flip

Local RectX, RectY, RectW, RectH
Local OvalNotRect = False


'Main loop
While True
	WaitEvent 
	Select EventID()
		Case EVENT_WINDOWCLOSE
			End
		Case EVENT_APPRESUME ' user alt-tabbed away, so need to refresh canvas graphics display
			Flip
		Case EVENT_WINDOWMOVE ' clean up after any window moving over canvas
			Flip
		Case EVENT_MENUACTION
			Select EventData()
				Case MENU_EXIT
					End
				Case MENU_ABOUT
					Notify "A 'full screen' GUI" 
					Flip ' Notify messes up canvas if moved so clean up when done
			End Select
		Case EVENT_GADGETACTION
			Select EventSource()
				Case button[0]
					RectW = 10 + Rand( 50 )
					RectH = 10 + Rand( 50 )
					RectX = Rand( GraphW - RectW ) 
					RectY = Rand( GraphH - RectH ) 
					If ButtonState(button[2]) Then SetColor 0,0,255 ' blue
					If ButtonState(button[3]) Then SetColor 255,0,0 ' red
					If ButtonState(button[1]) Then 
						DrawOval RectX, RectY, RectW, RectH
					Else
						DrawRect RectX, RectY, RectW, RectH
					EndIf
					Flip				
				End Select
	End Select
Wend</textarea><br><br>This is all still rather rough, but seems a promising start for one day's exploration. :)  All of this (plus the ability to use panels as "image buttons") has me feeling better about using MaxGUI for doing games as well as apps. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
