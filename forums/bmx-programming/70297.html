<!DOCTYPE html><html lang="en" ><head ><title >Compare Method for z-sorting</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Compare Method for z-sorting</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Compare Method for z-sorting</a><br><br>
<a name="786270"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >verfum</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi, I'm using an overriding Compare Method which someone on here used quite a while ago, I can get it to sort objects created by the type but I cant get it to sort different types, here I'm creating a Player and Enemy Types, the player z-sorts with the other player, but not with the enemy?<br><pre class=code>
Strict

Graphics 640,480,0

Global entity_list:TList = CreateList()  'Create a list for our player and enemy

' Player Class --------------------------------
Type Player
	Field x:Float,y:Float
	Field image:TImage

	Function Create:Player(x,y,anim_file:String)
		Local ent:Player = New Player
		ent.x = x
		ent.y = y
		ent.image = LoadAnimImage(anim_file,128,128,0,1)

		entity_list.AddLast(ent)	'Add the player to the list
		Return ent
	End Function
	
	Method Update()
		
		If KeyDown(KEY_RIGHT)
			x = x +1
		EndIf
		If KeyDown(KEY_LEFT)
			x = x -1
		EndIf
		If KeyDown(KEY_DOWN)
			y = y +1
		EndIf
		If KeyDown(KEY_UP)
			y = y -1
		EndIf
		
			
		DrawImage image,x,y,0
		

	End Method
	
	Method Compare( other:Object )
		
		If Player(other)
			Return Sgn(y - Player(other).y)
		EndIf
		Return -1

	End Method

End Type

' Enemy Class ------------------------------------------
Type Enemy
	Field x:Float,y:Float
	Field image:TImage
	
	Function Create:Enemy(x,y,anim_file:String)
		Local ent:Enemy = New Enemy
		ent.x = x
		ent.y = y
		ent.image = LoadAnimImage(anim_file,256,256,0,1)
		
		entity_list.AddLast(ent)	'Add the enemy to the list
		Return ent
	End Function
		
	Method Update()
		
		DrawImage image,x,y,0
		
	End Method
	
	Method Compare( other:Object )
		
		If Enemy(other)
			Return Sgn(y - Enemy(other).y)
		EndIf
		Return -1

	End Method

	
End Type

'Create our Player instance
Local player1:Player = Player.Create(50,80,"Media/warrior.bmp")
Local player2:Player = Player.Create(100,50,"Media/warrior.bmp")
'Create our Enemy instances
Local forest_monster:Enemy = Enemy.Create(100,100,"Media/forest_monster.bmp")


'MAIN LOOP ----------------
While Not KeyHit(KEY_ESCAPE)
	Cls 
	
	SortList(entity_list)
	
	For Local p:Player = EachIn entity_list  'Update our player
		p.Update()
	Next
	For Local e:Enemy = EachIn entity_list	 'Update our enemy
		e.Update()
	Next

' IGNORE THIS, this is to make one player move slower than the other.
	player1.x :+ (KeyDown(KEY_RIGHT) - KeyDown(KEY_LEFT))*2
	player1.y :+ (KeyDown(KEY_DOWN) - KeyDown(KEY_UP))*2

	Flip
Wend
</pre><br><br>Thanks for any help. <br><br></td></tr></table><br>
<a name="786313"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Suco-X</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi<br>Can't run code but this should help you:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Graphics 640,480,0

Global entity_list:TList = CreateList()  'Create a list for our player and enemy

' Player Class --------------------------------
Type Player
	Field x:Float,y:Float
	Field image:TImage

	Function Create:Player(x,y,anim_file:String)
		Local ent:Player = New Player
		ent.x = x
		ent.y = y
		ent.image = LoadAnimImage(anim_file,128,128,0,1)

		entity_list.AddLast(ent)	'Add the player to the list
		Return ent
	End Function
	
	Method Update()
		
		If KeyDown(KEY_RIGHT)
			x = x +1
		EndIf
		If KeyDown(KEY_LEFT)
			x = x -1
		EndIf
		If KeyDown(KEY_DOWN)
			y = y +1
		EndIf
		If KeyDown(KEY_UP)
			y = y -1
		EndIf
		
			
		DrawImage image,x,y,0
		

	End Method
	
	Method Compare( other:Object )
		
		If Player(other)
			Return Sgn(y - Player(other).y)
		ElseIf Enemy(other)
			Return Sgn(y - Enemy(other).y)
		EndIf
		
		Return -1

	End Method

End Type

' Enemy Class ------------------------------------------
Type Enemy
	Field x:Float,y:Float
	Field image:TImage
	
	Function Create:Enemy(x,y,anim_file:String)
		Local ent:Enemy = New Enemy
		ent.x = x
		ent.y = y
		ent.image = LoadAnimImage(anim_file,256,256,0,1)
		
		entity_list.AddLast(ent)	'Add the enemy to the list
		Return ent
	End Function
		
	Method Update()
		
		DrawImage image,x,y,0
		
	End Method
	
	Method Compare( other:Object )
		
		If Enemy(other)
			Return Sgn(y - Enemy(other).y)
		ElseIf Player(other)
			Return Sgn(y - Player(other).y)
		EndIf
		Return -1

	End Method

	
End Type

'Create our Player instance
Local player1:Player = Player.Create(50,80,"Media/warrior.bmp")
Local player2:Player = Player.Create(100,50,"Media/warrior.bmp")
'Create our Enemy instances
Local forest_monster:Enemy = Enemy.Create(100,100,"Media/forest_monster.bmp")


'MAIN LOOP ----------------
While Not KeyHit(KEY_ESCAPE)
	Cls 
	
	SortList(entity_list)
	
	For Local p:Player = EachIn entity_list  'Update our player
		p.Update()
	Next
	For Local e:Enemy = EachIn entity_list	 'Update our enemy
		e.Update()
	Next

' IGNORE THIS, this is to make one player move slower than the other.
	player1.x :+ (KeyDown(KEY_RIGHT) - KeyDown(KEY_LEFT))*2
	player1.y :+ (KeyDown(KEY_DOWN) - KeyDown(KEY_UP))*2

	Flip
Wend
</textarea><br><br>I think you are on the wrong way. Something like this is more logical:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict 


Type TEntity
	Field Image:TImage
	Field X:Float, Y:Float
	Field Link:TLink
	
	Method Compare(OtherObj:Object)
		Local En:TEntity 
		
		En = TEntity(OtherObj)
		
		If Not En Return
		
		Return Y-En.Y
	End Method
	
	Method AddLast(List:TList)
		Link = List.AddLast(Self)
	End Method
	
	Method Remove()
		If Link
			Link.Remove()
			Link = Null
		EndIf
	End Method
	
	Method Update() Abstract 

End Type



Type TPlayer Extends TEntity
	'Player data
	
	Function Create:TPlayer()
	
	End Function
	
	Method Update()
		'Do something
	End Method
End Type


Type TEnemy Extends TEntity
	'Enemy data
	
	Function Create:TEnemy()
	
	End Function
	
	
	Method Update()
		'Do something
	End Method

End Type




</textarea><br><br>Mfg Suco <br><br></td></tr></table><br>
<a name="786317"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TaskMaster</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Edit: Whoops, looks like I was a day late and a dollar short.  :)<br><br>You probably need to make the types both extend a common base type.<br><br>Type Base<br>End Type<br><br>Type Player Extends Base<br>End Type<br><br>Type Enemy Extends Base<br>End Type<br><br>Then put the common stuff like x,y,image in the base and put the compare function in the base type. <br><br></td></tr></table><br>
<a name="786502"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >verfum</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks guys, it was the base type which was causing issues, I needed to put the method in the Entity and change the updates in the loop to <br>Entity.Update(). <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
