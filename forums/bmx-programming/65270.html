<!DOCTYPE html><html lang="en" ><head ><title >Int to TGadget error..</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Int to TGadget error..</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Int to TGadget error..</a><br><br>
<a name="728408"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >plash</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I get this error when I run the code below:<br>"Compile Error: Unable to convert from 'Int' to 'TGadget'"<br><br>It says this:<br><pre class=code>win=CreateWindow("XMPlayer",100,100,300,190,0,1 | WINDOW_ACCEPTFILES)</pre><br>is the culprit.. Can anyone help me out here?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Framework brl.system
Import brl.win32maxgui
Import brl.bank
Import pub.fmod
Import brl.timer
Import brl.max2d
Import brl.maxgui
Import BRL.D3D7Max2D

Strict

'vars
Local button:TGadget[5]
Local win:TGAdget
Local ofile:String
Local music:Int
Local pause:Byte
Local vol:TGadget
Local mainvol:Int=8
Local volvol:Int=mainvol*255/10
Local panel:TGadget
Local snamepos:Byte Ptr
Local sname:String
Local lab:TGadget[8]
Local gfx:TGadget
Local spec:Byte Ptr
Local pnt:Float
Local bnk:TBank
Local yp:Int

'main window
win=CreateWindow("XMPlayer",100,100,300,190,0,1 | WINDOW_ACCEPTFILES)


If fsound_init(22000,256,0) =  True

Else
	Notify("Can't init sound",True)
EndIf

'gadgets
button[0]=CreateButton("Open File",5,60,60,20,win)
button[1]=CreateButton("Pause/Play",70,60,70,20,win)
vol=CreateSlider(145,60,150,30,win,SLIDER_HORIZONTAL|SLIDER_TRACKBAR)
SetSliderRange(vol,0,10)
SetSliderValue(vol,mainvol)
panel=CreatePanel(5,90,285,60,win,PANEL_GROUP)
panel.settext("Info")
lab[0]=CreateLabel("Name: ",10,105,40,20,win)
lab[1]=CreateLabel("N/A",45,105,160,20,win)
lab[2]=CreateLabel("Pattern: ",10,125,45,20,win)
lab[3]=CreateLabel("",55,125,20,20,win)
lab[4]=CreateLabel("Channels: ",80,125,50,20,win)
lab[5]=CreateLabel("",140,125,15,20,win)
lab[6]=CreateLabel("BMP: ",165,125,30,20,win)
lab[7]=CreateLabel("",200,125,20,20,win)

gfx=CreateCanvas(5,5,285,50,win)

CreateTimer( 60 )

FSOUND_DSP_SetActive(FSOUND_DSP_GetFFTUnit(),True)

'mainloop
Repeat
		
	WaitEvent()

	Select EventID()
		
		Case EVENT_WINDOWACCEPT
			ofile = EventText()
			If ofile &lt;&gt;""
				fmusic_stopsong(music)
				music=fmusic_loadsong(ofile.ToCString())
						
				If music &gt; 0
					fmusic_playsong(music)
					FMUSIC_SetMasterVolume(music,volvol)
					snamepos=FMUSIC_GetName(music)
					lab[1].settext(sname.FromCString(snamepos))
				Else
					Notify("Can't Load Song",True)
				EndIf
				
			EndIf
				
		
		Case EVENT_WINDOWCLOSE
			FSOUND_DSP_SetActive(FSOUND_DSP_GetFFTUnit(),False)
			fsound_close()
			End
		
		Case EVENT_GADGETACTION
			
			Select EventSource()
			
				Case button[0]
					
					ofile=RequestFile("Select Soundfile To Open","SoundFile:xm,mod,s3m,it,mid,rmi,sgt,fsb")
					
					If ofile &lt;&gt;""
						fmusic_stopsong(music)
						music=fmusic_loadsong(ofile.ToCString())
						
						If music &gt; 0
							fmusic_playsong(music)
							FMUSIC_SetMasterVolume(music,volvol)
							snamepos=FMUSIC_GetName(music)
							lab[1].settext(sname.FromCString(snamepos))
						Else
							Notify("Can't Load Song",True)
						EndIf
						
					EndIf
				
				Case button[1]
					
					pause=1-pause
					FMUSIC_SetPaused(music,pause)	
				
				Case vol
					mainvol=SliderValue(vol)
					volvol=mainvol*255/10
					FMUSIC_SetMasterVolume(music,volvol)
					
			EndSelect
		
		Case  EVENT_TIMERTICK
			RedrawGadget gfx
			
			spec = FSOUND_DSP_GetSpectrum()
			
			If music &gt; 0

				lab[3].settext(FMUSIC_GetPattern(music))
				lab[5].settext(FMUSIC_GetNumChannels(music))
				lab[7].settext(FMUSIC_GetBPM(music))
				
						
				
			EndIf
		
		Case EVENT_GADGETPAINT
			Local g=CanvasGraphics(gfx)
			SetGraphics g
			Cls
			If music &gt; 0
				bnk=CreateStaticBank(spec,512*4)
				For Local x:Int=0 To 285
					yp=(PeekFloat(bnk,x*4)*500)
					SetColor (x/8*32,x/8*16,(128)+x/8*2)
					DrawLine x,50-yp,x,50
				Next
			EndIf
			Flip()
		

	End Select
Forever
</textarea> <br><br></td></tr></table><br>
<a name="728410"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> use null not zero for the gadget group parameter <br><br></td></tr></table><br>
<a name="728412"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >plash</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Interesting, thank you. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
