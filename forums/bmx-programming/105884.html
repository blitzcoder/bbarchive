<!DOCTYPE html><html lang="en" ><head ><title >Mid Life Crisis - Midi Thread</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Mid Life Crisis - Midi Thread</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Mid Life Crisis - Midi Thread</a><br><br>
<a name="1297116"></a>

<a name="1297302"></a>

<a name="1297415"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I was looking over the code archives but did not see an example program for BlitzMAX which would play a single MID instrument. For instance, piano, MIDI instrument #1.<br><br>Here is some code I wrote in GFA if it helps. Forgive me if today I don't know how it works or can be applied to BlitzMAX:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
DEFINT "A-Z"
' Midi OUT info written and provided by Engelbert van Loock
' Simplifier and Chopped up into itty bitty bits by David W.  :)

' T to play it repeatedly like for rain &amp; stuff
' R to play it like Wind Chimes, Piano only !
' [ &amp; ] Octave Down/Up (if Sorta Instrument=153 then FX Down/Up by 16)
' Z &amp; X Sorta instrument Down/Up
' , &amp; . Pitch Down/Up (if Sorta Instrument=153 then FX Down/Up)
' [ESC] to exit

OPENW #1,_X \ 2 - 200,_Y \ 2 - 150,400,300,-1

SETTIMER 1,1,50

DLL #1,"mmsystem.dll"
  DECL WORD MIDIOUTOPEN(L,W,L,L,L)
  DECL WORD MIDIOUTCLOSE(W)
  DECL WORD MIDIOUTSHORTMSG(W,L)

  'DECL WORD midiOutLongMsg(w,l,w)
  ' ^ This is not being used yet because I don't know how to use it !

ENDDLL

SETFONT ANSI_FIXED_FONT

~@@MIDIOUTOPEN(*MIDI,-1,0,0,0)

MidiMsg = 0
MidiMsg = &amp;007f0000
'            V OP I
' V=Volume, O=Octave, P=Pitch, I=Instrument Sorta

'I = 153 ' Drums &amp; stuff
I = 154 ' &lt;-- Piano
O = 5
P = 5
RI = 0
' 20,21,23,40,42,43,44,48,49,50,51,52,89
'|_|_| |_|_|_| |_|_| |_|_|_|
'_|_|_|_|_|_|_|_|_|_|_|_|_|_|


'1 3 6 8 10



' O P I
' 2 10 153, 3 6 153, 4 5 153, 4 6 153, 5 2 153
' Nighttime Crickets &lt;- for repeat

' O/P
' 4/7 &amp; 4/8 are birds or frogs
' 4/9 is Rain &lt;- for repeat


'~@@midiOutShortMsg(MIDI,192)
'~@@MIDIOUTSHORTMSG(MIDI,255)
DO
  ~@@MIDIOUTCLOSE(MIDI)
  ~@@MIDIOUTOPEN(*MIDI,-1,0,0,0)
  ~@@MIDIOUTSHORTMSG(MIDI,MidiMsg + RI * 256 + 202)
  ~@@MIDIOUTSHORTMSG(MIDI,MidiMsg + O * 3072 + P * 256 + I)
  ~@@MIDIOUTSHORTMSG(MIDI,Echo &lt;&lt; 8)
  PRINT AT(0,0)"[Q] [W] Octave=";O;"  "
  PRINT AT(0,1)"[A] [S] Pitch=";P;"  "
  PRINT AT(0,2)"[Z] [X] Instrument=";I;"  "
  PRINT AT(0,4)"[E] [R] Real Instr=";RI;"  "
  PRINT AT(0,5)"[D] [F] Echo=";E;"  "

  ' 39 = sustain
  ' RI= 0-15, 32-39, 45-47, 55, 93, 96, 98, 100, 104-108, 112-114
  ' ^ Musical Instruments

  ' Wind Chime instruments:
  ' RI= 4, 8, 14, 46, 98, 112, 113, 114

  ' RI= 115-122, 127
  ' 115= Wood Hit
  ' 116= Bongo
  ' 117= Ride Cymbal
  ' 118= Echo Drum
  ' 119= Power up Cymbal
  ' 120= Squish That Mouse !
  ' 121= Pan Flute
  ' 122= Ocean Surf
  ' 123= Flying Saucer
  ' 127= Gunshot
  ' ^ =TOO= Cool Sound Effects !!
  ' 128= Shaker


  PEEKEVENT
  DO
    PEEKEVENT
    K$ = UCASE$(INKEY$)
  UNTIL K$ &gt; ""
  IF K$ = "T"
    DO
      ~@@MIDIOUTSHORTMSG(MIDI,MidiMsg + O * 4096 + P * 256 + I)
      TT = TIMER
      K$ = ""
      WHILE TIMER - TT &lt; RAND(5) + 5 AND TIMER &gt;= TT AND K$ = ""
        PEEKEVENT
        K$ = UCASE$(INKEY$)
      WEND
    LOOP UNTIL K$ &gt; ""
  ENDIF
  IF K$ = "+" THEN
    K$ = ""
    DO
      SLEEP
      K$ = UCASE$(INKEY$)
      IF (T_Thunder - TIMER &lt; 0 OR T_Thunder - TIMER &gt; 100000) AND Thunder = 0
        PRINT "*"
        T = 0
        FOR I = RAND(16) + 16 TO 0 STEP -1
          PRINT I
          A = RAND(16)
          FOR J = 0 TO 7
            IF RAND(2) = 0 THEN PlayNote 127,RAND(I + A),RAND(127),0,0
            PlayNote 127,0,255,0,0
          NEXT J
          R = RAND(9) + 1
          FOR J = 1 TO RAND(9) + 1
            PAUSE 1
            IF (T + J) MOD 4 = 0 THEN PlayNote 126,RAND(256),40 + RAND(40),0,0
          NEXT J
          T += R
        EXIT IF RAND(I) = 0
        NEXT I
        T_Thunder = 24000 + RAND(24000) + TIMER
      ENDIF
      IF (T_Rain - TIMER &lt; 0 OR T_Rain - TIMER &gt; 100000)
        T_Rain = TIMER + 500
        PRINT "r"
        PlayNote 126,RAND(256),40 + RAND(40),0,0
      ENDIF
    LOOP UNTIL K$ = CHR$(27)
  ENDIF
  IF K$ = "E" THEN RI --
  IF K$ = "R" THEN RI ++
  IF K$ = "Q" THEN O --
  IF K$ = "W" THEN O ++
  IF K$ = "A" THEN P --
  IF K$ = "S" THEN P ++
  IF K$ = "Z" THEN I --
  IF K$ = "X" THEN I ++
  IF K$ = "D" THEN E --
  IF K$ = "F" THEN E ++
  IF RI &lt; 0 THEN RI = 255
  IF RI &gt; 255 THEN RI = 0
  IF I &lt; 0 THEN I = 255
  IF I &gt; 255 THEN I = 0
  IF O &lt; 0 THEN O = 15
  IF O &gt; 15 THEN O = 0
  IF P &lt; 0 THEN P = 15
  IF P &gt; 15 THEN P = 0
LOOP UNTIL K$ = CHR$(27)

'MidiMsg = &amp;4090
'~@@midiOutShortMsg(hMidiOut&amp;,MidiMsg)
' ^ Stops instrument playing
' ^ These aren't needed except for instruments that repeat themselves

~@@MIDIOUTCLOSE(MIDI)
FREEDLL 1

KILLTIMER 1,1
CLOSEW #1

PROCEDURE PlayNote(Instr,Pitch,Volume,Echo,Channel)
  IF Echo
    ~@@MIDIOUTSHORTMSG(MIDI,(255 &lt;&lt; 8) + &amp;00)
  ELSE
    ~@@MIDIOUTSHORTMSG(MIDI,0)
  ENDIF
  ~@@MIDIOUTSHORTMSG(MIDI,(Instr &lt;&lt; 8) + &amp;CA)
  ~@@MIDIOUTSHORTMSG(MIDI,(Volume &lt;&lt; 16) + (Pitch &lt;&lt; 8) + &amp;9A)
  ' Pitch = 0-127

RETURN
</textarea> <br><br></td></tr></table><br>
<a name="1297134"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Porbably best to start with some C and port from there. <br><br><a href="https://msdn.microsoft.com/en-us/library/windows/desktop/dd798495(v=vs.85).aspx" target="_blank">https://msdn.microsoft.com/en-us/library/windows/desktop/dd798495(v=vs.85).aspx</a><br><br>Brucey may already have a portmidi module which would be a lot simpler. <br><br></td></tr></table><br>
<a name="1297169"></a>

<a name="1297170"></a>

<a name="1297171"></a>

<a name="1297181"></a>

<a name="1297293"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Casaber</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Don´t have a mid life crisis now do you hear. <br><br>Here you go.<br><br><pre class=code>

Extern "win32"
Function midiOutOpen(lphMidiOut:Int ,uDeviceID:Int,dwCallback:Int,dwInstance:Int,dwFlags:Int)
Function midiOutClose(hMidiOut:Int)
Function midiOutShortMsg(hMidiOut:Int,dwMsg:Int)
End Extern

Const NoteOn = $9 , NoteOff = $8 , ProgramChange = $C , CC = $B

' Make a more practical version of midioutshortmsg
Function SendMidi(Handle,Channel,Status,Data1=0,Data2=0)
	midiOutShortMsg(Handle,(Data2 Shl 16) | (Data1 Shl 8 ) | (Status Shl 4) | Channel)
End Function

' Open
Local MidiOut:Int , handle:Int Ptr
Handle = Varptr MidiOut ; MidiOutOpen Int(Handle),-1,0,0,0 ' -1 is always MIDI MAPPER

' Set Grand Piano
Channel = 1 ; Instrument = 1 ; SendMidi MidiOut,Channel,ProgramChange,Instrument

Repeat
	Note = Rand(64,127) ; Vel = Rand(64,127)
	SendMidi MidiOut,Channel,NoteOn,Note,Vel ' Note on
	Delay 64 ' Pause some length
	SendMidi MidiOut,Channel,NoteOff,Note ' Note off
Until KeyDown(KEY_ESCAPE)

' Close
midiOutClose MidiOut
</pre> <br><br></td></tr></table><br>
<a name="1297214"></a>

<a name="1297215"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Casaber</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I had to try and see how well Bmax syncs, <br><br>beercuase we all we know.. that's what matters in life.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

' NEEDS MORE COWBELL

Extern "win32"
Function midiOutOpen(lphMidiOut:Int ,uDeviceID:Int,dwCallback:Int,dwInstance:Int,dwFlags:Int)
Function midiOutClose(hMidiOut:Int)
Function midiOutShortMsg(hMidiOut:Int,dwMsg:Int)
End Extern

Graphics 800,600

Const NoteOn = $9 , NoteOff = $8 , ProgramChange = $C , CC = $B

' Make a more practical version of midioutshortmsg
Function SendMidi(Handle,Channel,Status,Data1=0,Data2=0)
	midiOutShortMsg(Handle,((Data2 Shl 16) | (Data1 Shl 8 ) | (Status Shl 4) | Channel))
End Function

' Open
Local MidiOut:Int , handle:Int Ptr
Handle = Varptr MidiOut ; MidiOutOpen Int(Handle),-1,0,0,0 ' -1 is always MIDI MAPPER

' Set Grand Piano
Channel = 1 ; Instrument = 1 ; SendMidi MidiOut,Channel,ProgramChange,Instrument

'Local pattern:Int = %1101000111010101
'Local pattern2:Int = %0001000100001111

Local pattern:Int =  %1010101010101010
Local pattern2:Int = %1000000000000000
Local pattern3:Int = %0000101000110000

Local bit:Int = 1
Local eq:Int[128]
countin=128
Repeat
countin=countin-1
	Cls
	
	'Note = 64 + Rand(64) ; Vel = Rand(64)+64
	'If time Mod 64 = 0 Then  SendMidi MidiOut,Channel,NoteOff,OldNote ; SendMidi MidiOut,Channel,NoteOn,Note,Vel ; OldNote = Note
	'If time Mod 4 = 0 Or time Mod 64 = 0 Then SendMidi MidiOut,Channel,NoteOn,ladder,Rand(90,115) ; ladder = ladder + Rand(2,3) Mod 48
    'If time Mod 64 = 0 Then SendMidi MidiOut,9,NoteOn,64-12-32,127 ; ladder = 48 ' Crazy monkey (cymbal)
	' Forever sustain pedal
	ladder = ladder Mod 128
	
	If countin&lt;0 
	If time Mod 8 = 0 Then If pattern &amp; bit Then SendMidi MidiOut,9,NoteOn,67,127 ; bar = 128 ; ladder = ladder+2 ' 16th note	
	If time Mod 8 = 0 Then If pattern2 &amp; bit Then SendMidi MidiOut,9,NoteOn,68,127 ; bar2 = 128 ; ladder = ladder+4 ' 16th note	
	If time Mod 8 = 0 Then If pattern3 &amp; bit Then SendMidi MidiOut,9,NoteOn,56,127 ; bar3 = 128
	EndIf

	If time Mod 8 = 0 Then bit=bit*2
	time=time+1 ; If time &gt;= 128 Then time = time - 128 ; bit = 1
	x = 200 ; y = 256-Cos(temp2/2)*4

	'SetColor 0,255,128 ; DrawRect x+0+4,y+128-bar,32-4,32-4 ; If bar &gt; 4 Then bar = bar - 8
	'SetColor 0,255,128 ; DrawRect x+32+4,y+128-bar2,32-4,32-4 ; If bar2 &gt; 4 Then bar2 = bar2 - 8
	'SetColor 0,255,128 ; DrawRect x+64+4,y+128-bar2,32-4,32-4 ; If bar3 &gt; 4 Then bar3 = bar3 - 8
	SetColor 128+Sin(temp2)*128,255,128 ; DrawRect x+0+4,y+128-bar,32-4,32-8*Sin(temp2) ; If bar &gt; 0 Then bar = bar - 8
	SetColor 0,128+Sin(temp2)*128,128 ; DrawRect x+32+4,y+128-bar2,32-4,32-8*Sin(temp2) ; If bar2 &gt; 0 Then bar2 = bar2 - 12
	SetColor 0,255,128+Sin(temp2)*128 ; DrawRect x+64+4,y+128-bar3,32-4,32-8*Sin(temp2) ; If bar3 &gt; 0 Then bar3 = bar3 - 16
		
	temp2=temp2+5
	SetAlpha 1.0

Rem
If countin &gt; 0 
	xx = xx + 1
	For temp=0 To 127
	If temp = (ladder Mod xx) = 0 Then eq(temp+xx) = 128
	If eq(temp) &gt; 0 Then eq(temp) = eq(temp) - 8 Else eq(temp)=0
	SetColor 128,0,0 ; DrawRect temp * 4 ,Rand(20,140), 16+Sin(temp*2)*32, eq(temp) * 1
	Next
EndIf
EndRem


	Flip 1 ; Delay 1
Until KeyDown(KEY_ESCAPE)
' Close
midiOutClose MidiOut

</textarea> <br><br></td></tr></table><br>
<a name="1297238"></a>

<a name="1297239"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> here is a (running) code sample for MIDI using Brucey RtMidi.Mod:<br><br><a href="http://www.blitzbasic.com/Account/showuser.php?id=10365" target="_blank">http://www.blitzbasic.com/Account/showuser.php?id=10365</a><br><br>I use this functions very reliable in all my commercial music education software<br><br><br>Here is a tool with source code, that converts MIDI into OGG. It shows also how to call MIDI on low level:<br><br><a href="http://www.blitzforum.de/forum/viewtopic.php?t=37210" target="_blank">http://www.blitzforum.de/forum/viewtopic.php?t=37210</a><br><br>By the way... Your code is written by "Engelbert van Loock". Are you Engelbert? <br><br></td></tr></table><br>
<a name="1297274"></a>

<a name="1297275"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Casaber nailed it. And in very small code too ! Outstanding ! I already have you down for being the first to play, seek, and read MIDI and now here you have the ability to effectively send MIDI notes.<br><br>Nicely done !! I like your beat box too. Catchy rhythm. :)<br><br>I'll take a look it over, make some functions out of it, and then anyone can have real sound effects and musical notes directly in their code without requiring a Mod or DLL.<br>. . .<br><br>Simon Armstrong: Thanks but fortunately I won't have to mess with C.<br><br>Midimaster: Won't need to use it. Thanks though. Interesting bit about MIDI into OGG. Do you mean *.MID to *.OGG, the media file ?<br><br>And no, I am not Engelbert. He was one of many GFA programmers from Germany when I was using the language. I told him I didn't want to use the PC speaker for sound effects for this one game I was working on and was there a way to create sound without using WAV files ?<br><br>He said I could play some of the high percussion instruments by opening MIDI drivers to accomplish this - and then he sent me some code to show playing the piano.<br><br>And yes, I did make use of this. I wrote a quick game Dungeon that used only plotting MIDI sounds throughout and it worked quite well. I should totally rewrite that game sometime.<br><br><img src="http://www.writerscafe.org/uploads/rte/ee2bad08c91dc872ed2c7578d9a901b8.png"> <br><br></td></tr></table><br>
<a name="1297277"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote">  and then anyone can have real sound effects and musical notes directly in their code without requiring a Mod or DLL <br></div><br>Indeed, and for everyone else who doesn't use Windows, or wants more control, there's always the well supported open-source libraries/modules. <br><br></td></tr></table><br>
<a name="1297279"></a>

<a name="1297280"></a>

<a name="1297281"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Casaber</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Dw817 watch out though, WinRT / "Metro" still lacks MIDI completely. I guess that will change. Same with iOS they too had no MIDI support at all before version 3.<br><br>Remember it's only Windows and not ASIO (only MME). RTMIDI uses MME same for Win32 underneath so no changes there, but you could have Apple support if you used it I guess.<br>We could try to do the same as with Windows above but with OSX. It could be fun to see how small Mac version would be, we can talk more about that later if you want.<br><br>I also have few ideas how to search for a softsynth on the device e.g for applying Soundfonts later. <br><br></td></tr></table><br>
<a name="1297296"></a>

<a name="1297297"></a>

<a name="1297298"></a>

<a name="1297299"></a>

<a name="1297301"></a>

<a name="1297306"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Okay, here is a sample game demo to see how playing MIDI notes can be used for sound effects.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' Slightly cleaner code of Casaber's MIDI note player
' alongside a test game example to show interesting audio
' effects w MIDI

Strict
SeedRnd MilliSecs() ' ensure random results each time

Global midiout,midihandle:Int Ptr=Varptr midiout ' required for MIDI sounds
midiinit ' initialize MIDI

Global img_tile:TImage[8],tileno
inittiles "0240000800012000,242,7f3f3f3f3f3f0100,888,18483e0918242200,868,145d497f495d1400,800,7f41554955417f00,444,003e7f00777f7f00,880" ' stock image tiles
' floor, wall, player, critter, pit, treasure

Global scrn:Byte[31,22] ' our virtual screen, plot characters like Commodore
Global k$ ' read all keystrokes into here

Local i,j,r,c,x,y,x2,y2

SetGraphicsDriver GLMax2DDriver(),0 ' zero forces front buffer
Graphics 1024,768
SetBlend alphablend
SetScale 4,4 ' so our 8x8 tiles will appear 32x32 in size

For i=0 Until 21
  For j=0 Until 31
    c=0 ' by default everything is floor
    If i*j=0 Or j=30 Or i=20
      c=1 ' if edge of screen, make it wall
    Else
      r=Rand(0,99) ' decide what goes on inside
      If range(r,0,10) Then c=1 ' interior wall
      If range(r,11,12) Then c=3 ' critter
      If range(r,13,14) Then c=4 ' pit
      If range(r,14,16) Then c=5 ' treasure
    EndIf
    scrn[j,i]=c ' record it
  Next
Next

trx ' clear bottom so it is ready for messages

x=1 ; y=19 ' starting position of player

Repeat ' {* MAIN *}

scrn[x,y]=2 ' plot player
update
scrn[x,y]=0 ' change player's position to floor tile
x2=x ; y2=y ' record position in case we need to go back

If k$="up" Then y:-1 ' move player according to keystroke
If k$="lf" Then x:-1
If k$="rt" Then x:+1
If k$="dn" Then y:+1

If scrn[x,y]=1 ' attempt to walk through wall
  x=x2 ; y=y2 ' recover last position
  trx "CAN'T CROSS WALL"
  midiplay 112,25 ' tink
  midistop ' stop it so it doesn't loop
ElseIf scrn[x,y]=3
  trx "DEFEATED CRITTER!"
  midiplay 96,123 ' squish
ElseIf scrn[x,y]=4
  trx "OUCH! FELL IN PIT!"
  midiplay 48,127 ' badoom
ElseIf scrn[x,y]=5
  trx "FOUND TREASURE!"
  midiplay 96,112 ' crystal
EndIf

Forever ' {* END OF MAIN *}

Function zz() ' if you hit ESCAPE, go here
  midistop ' close up all of MIDI
  midiclose
  End
EndFunction

' &gt;&gt; ENSURE NUMBER IS WITHIN SET RANGE
Function range(a,b,c)
  If a&gt;=b And a&lt;=c Then Return 1
EndFunction

' &gt;&gt; SIMPLE DISPLAY MESSAGE AT BOTTOM OF SCREEN
Function trx(t$="")
Local i,c
  For i=0 Until 31
    c=32
    If i&lt;Len(t$) Then c=t$[i]
    scrn[i,21]=c
  Next
EndFunction

' &gt;&gt; UPDATE FULL SCREEN W MATCHING TILES AND READ KEYBOARD
Function update()
Local i,j,c
  Cls
  For i=0 Until 22
    For j=0 Until 31
      c=scrn[j,i]
      If c&lt;32
        DrawImage img_tile[c],16+j*32,i*32 ' draw image tile
      Else
        DrawText Chr$(c),16+j*32,i*32 ' not a tile ? Draw text then
      EndIf
    Next
  Next
  Repeat
    Flip 0 ' show screen
    Delay 150 ' big delay so player doesn't move too quickly
    k$=""
    If KeyDown(key_up) Then k$="up"
    If KeyDown(key_down) Then k$="dn"
    If KeyDown(key_left) Then k$="lf"
    If KeyDown(key_right) Then k$="rt"
    If KeyDown(key_escape) Then zz
  Until k$&gt;"" ' loop until you hit one of these five keys
EndFunction

' &gt;&gt; CONVERT ONE LONG STRING TO LITTLE 8x8 IMAGES
Function inittiles(t$)
Local i,j,v,n,a,p$,r,g,b,c$
Local pic:TPixmap=CreatePixmap(8,8,pf_rgb888) ' work image
  While t$&gt;""
    p$=yank$(t$) ' image (16-characters, 8-bytes)
    c$=yank$(t$) ' color (3-characters)
    r=c$[0..1].toint()*32 ; g=c$[1..2].toint()*32 ; b=c$[2..3].toint()*32 ' set color
    If r=256 Then r=255 ' make sure it doesn't wrap
    If g=256 Then g=255
    If b=256 Then b=255
    ClearPixels pic,0 ' erase each time
    For i=0 Until 8
      v=("$"+p$[i*2..i*2+2]).toint() ' get full pixels across in one number
      For j=7 To 0 Step -1 ' reverse order to extract highest value bits first
        n=1 Shl j ' extract out bit pixels
        If v&gt;=n
          v:-n
          a=b|g Shl 8|r Shl 16 ' calculate color
          WritePixel pic,j,i,a ' write out single pixel
        EndIf
      Next
    Next
    img_tile[tileno]=LoadImage(MaskPixmap(pic,0,0,0),0) ' record and make it transparent all in one line
    tileno:+1 ' total tile numbers used
  Wend
EndFunction

 ' &gt;&gt; SET COLOR FROM 3-CHARACTER STRING (000-888)
Function colr(c$)
  SetColor c$[0..1].toint()*32,c$[1..2].toint()*32,c$[2..3].toint()*32
EndFunction

' &gt;&gt; RETURN COMMA DELIMITED TEXT FROM A$
Function yank$(a$ Var)
Local b=Instr(a$+",",","),c$=Left$(a$,b-1)
  a$=Mid$(a$,b+1) ' also remove it from input string
  Return c$
EndFunction

' &gt;&gt; INIT MIDI FOR PLAYING
' THANK YOU CASABER !
Function MidiInit()
  MidiOutOpen Int(midiHandle),-1,0,0,0
EndFunction

' &gt;&gt; PLAY INSTRUMENT,PITCH,VOLUME,CHANNEL
' I'm not sure where ECHO comes into play ?
Function midiplay(pitch,inst=0,volume=63,echo=0,chan=0)
  midioutshortmsg midiout,inst Shl 8|12 Shl 4|chan ' program change
  midioutshortmsg midiout,volume Shl 16|pitch Shl 8|9 Shl 4|chan ' play note
EndFunction

' &gt;&gt; STOP ALL TRAILING MIDI NOTES
Function midistop()
  For Local i=0 Until 256
    midioutshortmsg midiout,i Shl 8|8 Shl 4
  Next
EndFunction

' &gt;&gt; CLOSE MIDI
Function midiclose()
  midiOutClose MidiOut
EndFunction

' &gt;&gt; READY SYSTEM LIBRARIES TO DIRECTLY SEND MIDI NOTES
Extern "win32"
Function midiOutOpen(lphMidiOut:Int ,uDeviceID:Int,dwCallback:Int,dwInstance:Int,dwFlags:Int)
Function midiOutClose(hMidiOut:Int)
Function midiOutShortMsg(hMidiOut:Int,dwMsg:Int)
End Extern
</textarea><br><br>Yes ! I was just going to mention SoundFonts, Casaber. My favorite is "<b>Chaos_V20</b>."<br><br>Brucey, as for more control. Nope, I'm good. I just wanted to have the ability to sound out MIDI notes.<br><br>As for system compatibility, yes, I suspect when I finish Carryall 750k it will only run in Windows. <br><br></td></tr></table><br>
<a name="1297308"></a>

<a name="1297309"></a>

<a name="1297310"></a>

<a name="1297311"></a>

<a name="1297312"></a>

<a name="1297313"></a>

<a name="1297321"></a>

<a name="1297323"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Casaber</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> I´m not getting any sounds in that game? I quickly tried debugged what it could be, it so far I think it must be the single playmidievent routine if anything. Everything else is perfect condition. I can't understand why it's quiet.<br><br>So I tried my code again on the same system, and out flows all kinds of notes. So something is different.<br><br>I saw some notes in there by the way, do you mean volume to be velocity? Your thoughts about echo imply that you want to use velocity to fake echo aswell?<br>You could do that with either MIDI channel volume, or with velocity with Note on's. I guess the most "correct way" <br>would be to use MIDI channel volume as velocity might be used for many things, it should be best only used for expression. In my humble opionion.<br><br>With shitty GM it would make no difference though in most cases if you use vel or volume.<br><br>Chaos is a good bank. Very expressive one.<br><br>But David oh David Why do you have have sound and not me? :s Maybe my volume is too low, I´m getting deaf. I experienced very low MIDI earlier<br> today before posting when I tried some ryhthms, I have no idea yet why, I know it has a working MIDI system, well you can play Macgyver.mid on it, that's class.<br>A GM is GM right. I had very odd flutes and low volume, so it might be just my shitty system..<br><br><br>*EDIT SOLVED, how could I miss that, it's MIDIOFF, it should not be called to turn off sounds. Just need a small recode to use noteoff instead.<br>I guess some systems are more forgiving than others.<br><br>EDIT2* no false alarm, you implemented a allnoteoff, but taking it away made me hear sounds at least. We'll it's just some detail then to work out later I guess. I think you just switch off sounds too fast, it just that simple. But I could be wrong. But I don´t think so. Some systems allow percussion especially to cling off, mine doesn't. If you tell it to stop, it stops right away. Or it could be a polyphony problem. <br><br></td></tr></table><br>
<a name="1297325"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Glad you got it working, Casaber ! I would think that the sound I'm using for the player hitting a wall would not be heard it all, but I get a slight metallic tap - so that does work here.<br><br>At one point I couldn't hear any MIDI at all and panicked. Then I clicked the speaker icon in the bottom-right-hand corner of the taskbar, then click MIXER. And see that the program I was running had volume set to zero.<br><br>I never could get MIDI to work in DOS BOX, which is a shame as I now have GFA working in it, I wrote this lovely Wind Chime program that also has a button for generating random music with backup synthesizer chords and everything.<br><br>Just doesn't work in DOS BOX. :/<br><br>And yes, I don't know the difference between velocity or volume - they may be one and the same in MIDI ? <br><br></td></tr></table><br>
<a name="1297326"></a>

<a name="1297327"></a>

<a name="1297328"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Casaber</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> In MIDI Velocity is reserved for the strength of something e.g. the plucked string, the blown sax, or the hit percussion/fx.<br><br>Low velocity are often programed to also sound more mellow and more you know.. everything that you naturally can relate to soft sounds. A good flute for example can become more breathy at low vel. Lower volume is one of those properties of course, but its a package deal in velocity. It might be anything.<br><br>Another thing is that volume can be changed wheneever, velocity is only given at the strike of the sound, at the start. Once you've said a velocity, <br>you can't change it while it's playing, that would be to strike the drum again.<br><br>MIDI volume is pure pure volume and you can change it whenever, and it affects everything on that one channel that you send it to.<br>Velocity happens only once and "per key" and is meant to be a package deal. Vel may filter sounds, it could change EG's it could do alot of things. <br>It's not just volume. Execept in shitty GM's then it's pretty much  just volume and that's part of why we hate it. No expressiveness. <br><br></td></tr></table><br>
<a name="1297329"></a>

<a name="1297330"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Casaber</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Would love to hear those chimes and music generations sometime, maybe it's possible to convert it to Bmax?<br>I remember that it was a real struggle to try get things working sometimes with DOS and sound, haha I guess DOSBOX emulates things TOO WELL  ;) <br><br></td></tr></table><br>
<a name="1297331"></a>

<a name="1297332"></a>

<a name="1297333"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Casaber</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> This technique should suffice for syncing with frames at least, it keeps 2-3msec average on an old XP couple of years ago, and Windows7 and newer improved alot.<br><br>There's no timetagging here in te output, but you can stream with some effort if you want (that would be to setup a buffer upto 64k in size instead of just ONE MIDI event), <br>and then you trigger the whole buffer as you do here all in one go. That would make things "atomic", all events in the buffer would have perfect integrity. <br>But I think that's overkill to stream if you only need framed synced effects.<br><br>With all good tools you could do most things with just careful timing and perhaps using things like threads and <br>learn all kinds of nice mechanisms that makes OS behave nice, which are good also for graphics and everything.<br><br>BMax is powerful enough to do close acccurate sample creation realtime with OpenAL so it could definitley time complex MIDI on its fingertips ;)<br><br>So I say, use the simple route when you can. It's a win win. <br><br></td></tr></table><br>
<a name="1297362"></a>

<a name="1297363"></a>

<a name="1297364"></a>

<a name="1297365"></a>

<a name="1297366"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Casaber</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> I had a look in your original sourcecode about that echo and I think I know what it's meant to do. You set Echo on/off in the <br>source and you do that using SystemEx msg, you're programming the synth to a particular reverb/echo, and instead of using streaming / long midi <br>msg API to send any length MIDI message (which this needs) you're reusing (cleverly) the shortmsg routine. <br><br>So Echo is a flag telling if your're doing that trick or not. Does that ring a bell?<br>I might be wrong because I´m not sure why there's a zero sent when your'e not doing that. It might be a filler for Windows API?<br>If so I don´t think it´s needed.<br><br>PROCEDURE PlayNote(Instr,Pitch,Volume,Echo,Channel)<br>  IF Echo<br>    ~@@MIDIOUTSHORTMSG(MIDI,(255 &lt;&lt; 8) + &amp;00)<br>  ELSE<br>    ~@@MIDIOUTSHORTMSG(MIDI,0)<br>  ENDIF<br>  ~@@MIDIOUTSHORTMSG(MIDI,(Instr &lt;&lt; 8) + &amp;CA) ' This is a hardcoded PROGRAMCHANGE for CHANNEL 10 (DRUM CHANNEL)<br>  ~@@MIDIOUTSHORTMSG(MIDI,(Volume &lt;&lt; 16) + (Pitch &lt;&lt; 8) + &amp;9A) ' This is a HARDCODED NOTE ON for CHANNEL 10 (DRUM CHANNEL)<br>  ' Pitch = 0-127<br><br>This was just an idea I had when I glanced at the code. It's a clever way to send any MIDI msg using the short API if it's true.<br><br>EDIT<br>No I changed my mind it seems to be a MIDI msg template with something else not a Sysex?<br>Will check more closely when I get the time because that zero still aludes me. <br><br></td></tr></table><br>
<a name="1297386"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> @dw817<br>Ah... I know Engelbert as a musician collge since 1980. In 2005 Engelbert worked with me on some projects. Now he is a web developer in south bavaria and I lost the contact....<br><br><br>@all<br>Once sent a PROGRAM CHANCE on a channel the sound (one of 127) remains the same. So there is no need for sending PGC again. Better to use another channel (0,9,11-15) if you need another instrument.<br><br>Channel 10 is always the DRUM CHANNEL.  The message part you called "pitch" is normally called NOTE. It will play 127 chromatic notes from deep C to high C. But in DRUM CHANNEL this results in one of 127 different Drum Sounds. <br><br>There is also a CHANNEL PITCH message 1110nnnn, which manipulates the following NOTE messages.<br><br>My MIDI-TO-OGG converter plays the scale (all 128 notes) of a  sound and saves 128 audio-files, which can be used afterwards in BlitzMax games like normal TSound-samples.<br><br>Also important to know. MIDI uses the sound device of the user to play the events. So you never know, whether the user has only poor (incomplete) GM-Library, good libraries like Roland Sound Canvas or a extern high quality music instrument like the Yamaha Tyros. <br><br></td></tr></table><br>
<a name="1297416"></a>

<a name="1297417"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thread moved to BlitzMax Programming due to quality of content. <br><br></td></tr></table><br>
<a name="1297421"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >angros47</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> BlitzMax is multi-platform; midi programming is easy on windows, but not on linux, because many linux distros by default don't provide a software midi synthesizer, so midi is available only on system with midi hardware (you can install a midi soft synth, but the download is usually about 50 mb or more) <br><br></td></tr></table><br>
<a name="1297422"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Casaber</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Midimaster You could ask the OS and look for known softsynths / drivers.  It's a unusual thing to do but if you have reason to<br>believe that there might be a common  softsynth onboard then it's no problem to direct everything there (and skip midi mapper). <br><br>The absolute standard ones that comes with Windows (Microsoft GS Wavetable)  &amp; Apple OSX (Quicktime synth) both uses <br>Rolands SoundCanvas so I guess they bought the cheapest version, and did something terrible with it.<br><br>Just curious, have you developed alot for Bars&amp;Pipes ? Or I might misstake you for someone else. If you don´t mind me asking. <br><br></td></tr></table><br>
<a name="1297428"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> @angros47 after reading up on <a href="http://www.music.mcgill.ca/~gary/rtmidi/" target="_blank">RTMidi</a> it seems that some midi programming tasks such as virtual soft synths and VJ visualiser apps that both need to provide a midi endpoint for other applications is actually easier for MacOS and Linux. <br><br></td></tr></table><br>
<a name="1297435"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >angros47</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> Midi programming in linux is actually easy, but after you wrote your program, it just won't work on most linux systems, because it won't find a midi port available. <br><br></td></tr></table><br>
<a name="1297437"></a>

<a name="1297438"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> Most linux systems don't have displays let alone speakers :) <br><br></td></tr></table><br>
<a name="1297439"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#23">[#23]</a></td></tr></table></td></tr><tr ><td class="posttext"> My goodness you guys are busy here. I guess I do ask the right questions. :)<br><br>I have looked at the code that generates raw sound (generated AudioSample) in general for BlitzMAX and I can't seem to generate any without a harsh click before it starts and a harsh click as it stops.<br><br>Casaber's MIDI routine however does a nice job. No click before or after a sound, and doesn't require creation of a sound file or anything - and I suspect I could port over my Wind Chimes routine.<br><br>I'm still messing with straight audio though, the one that uses the CreateAudioSample(). Trying to develop a routine where 0 is silent, 1 is very low pitch and 255 is very high pitch. It's the clicking in-between that's the problem. <br><br></td></tr></table><br>
<a name="1297454"></a>

<a name="1297455"></a>

<a name="1297456"></a>

<a name="1297457"></a>

<a name="1297458"></a>

<a name="1297459"></a>

<a name="1297460"></a>

<a name="1297461"></a>

<a name="1297462"></a>

<a name="1297463"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Casaber</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#24">[#24]</a></td></tr></table></td></tr><tr ><td class="posttext"> I looked into what new things Windows has to offer on the MIDI front. When you want more sample like control.<br>DirectMusic being from 1996, MME from 1991 and they've grown old without improving themselves. MIDI itself should have a midlife crisis.<br><br>Wondering if anyone has already have had experience with the new Windows API? I myself havn't had the time too look into it, <br>it seem to cover all new Windows including ARM/"Metro". Which is nice.<br><br>Seemingly it's about MIDI and nothing else, no standard softsynths distributed with Windows, and no way to play for instance a .mid with a single command.<br>Everything is instead pure MIDI and meant to be about hand-made quality connections between apps if I understood things right. <br><br>A very Ipad-like thing to do, I have the feeling. I would like that, so that would be very welcome.<br><br><a href="https://msdn.microsoft.com/en-us/library/windows/apps/windows.devices.midi.aspx" target="_blank">https://msdn.microsoft.com/en-us/library/windows/apps/windows.devices.midi.aspx</a><br><br><a href="http://video.namm.org/general/A3E_Microsoft_Keynote.mp4" target="_blank">http://video.namm.org/general/A3E_Microsoft_Keynote.mp4</a> <br><br></td></tr></table><br>
<a name="1297486"></a>

<a name="1297531"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#25">[#25]</a></td></tr></table></td></tr><tr ><td class="posttext"> Casaber, I know all these things... The portability to Windows and OS and the possibility of selecting the devices is the reason why I use Bruceys RtMidi.Mod. It's not "unusual" to select drivers, but a "must have". My customers always have 3 to 4 differnet midi devices and want to switch the target. The source code of my MIDI-TO-OGG converter shows how to code a complete MIDI implementation in a BlitzMax program. <br><br>The biggest problem is the latency (60-200msec) of the software based wave-tables. So most of my customers use extern midi instrument (10msec) for realtime MIDI. You can feel this in my rhythm-trainer, where it is important to interact with the sounds coming out of the speakers.<br>If you need a real-time music instrument, you need to convert the tones into audio with MIDI-TO-OGG converter. <br><br>@dw817<br>In my former post ( <a href="http://www.blitzbasic.com/Community/post.php?topic=105879&amp;post=1297182" target="_blank">http://www.blitzbasic.com/Community/post.php?topic=105879&amp;post=1297182</a> ) I descriped the 3 reasons for your audio problems. A combination of your code and these three rules should bring the solution. As you can hear in the LISTEN&amp;HEAR-MACHINE complex manipultations on audio are possible.<br><br>@casaber<br>No, I did not work for "Bars  Pipes". I started developing MIDI -Software on Atari in 1985. But always with the target "music education". As my name says I'm "Midimaster" and thats also my company's name www.midimaster.de. We offer 20  programs for music education on Windows and Mac. And there is a cooperation with WIDI-Soft, who hold the still best Audio-To-Note recognition algorithm. So... a big experience in audio related algorithms<br><br><br>MIDI is the standard language for music instruments. No midlife crisis... Musicians need it also to communicate between instruments (without computers) and it is the base for DMX light commands on all stages of the world. Musicians never needed the build in windows MIDI wave table. They always connect a USB device, which is self installing. The USB device has a low latency sound generator or is a driver to connect music instruments or lasers etc.... <br><br></td></tr></table><br>
<a name="1297510"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#26">[#26]</a></td></tr></table></td></tr><tr ><td class="posttext"> MidiMaster, your link shows a profile.<br><br>Casaber, keep up the great work ! Your code submits are incredibly helpful AND useful ! <br><br></td></tr></table><br>
<a name="1297515"></a>

<a name="1297516"></a>

<a name="1297520"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Casaber</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#27">[#27]</a></td></tr></table></td></tr><tr ><td class="posttext"> MidiMaster, sounds like we have alot in common, nice. Me, I started with music back in 1982, <br>and then I begun learning programmming abit later with the Atari (C64 first) not much though but I got the taste for it.<br><br>I´ve treid WIDIsoft many years ago their audio2mid, I used it for Gitarr lead and Cello tracking.  I remember many of those audio2mid at that time did not track volume <br>and I think that one was the only one that did both pitch and volume? Not many ppl care about those details. I loved that they thought of it.<br><br>I should present myself later aswell. Everyone has profiles but me haha but I just need some time I think. I'll get to that. <br><br></td></tr></table><br>
<a name="1297518"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Casaber</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#28">[#28]</a></td></tr></table></td></tr><tr ><td class="posttext"> Dw817 Thanks, and same to you. Your posts engage alot of ppl and thoughts, and makes things very enjoyable :) <br><br></td></tr></table><br>
<a name="1297532"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#29">[#29]</a></td></tr></table></td></tr><tr ><td class="posttext"> @dw817... upps.. corrected <br><br></td></tr></table><br>
<a name="1297555"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#30">[#30]</a></td></tr></table></td></tr><tr ><td class="posttext"> Midimaster, the code you listed does not run by itself. Could you please post it in its entirety so it "runs out of the box ?" Thanks. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
