<!DOCTYPE html><html lang="en" ><head ><title >Bounding box rotation?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Bounding box rotation?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Bounding box rotation?</a><br><br>
<a name="998606"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yahfree</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm trying to create a working OBB(Oriented bounding box) object, fully stocked with collision detection, etc.<br><br>For the collision detection i'm going to try the Seperate Axis Therom.<br><a href="http://gpwiki.org/index.php/Polygon_Collision" target="_blank">http://gpwiki.org/index.php/Polygon_Collision</a><br>But that's later.<br><br>I'm having difficulties trying to get the box to rotate correctly.<br><br>the following code should make the box go left and right with the arrow keys while rotating the box, however it goes off in some random direction.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics 800, 600

'My 2d Vector Object
Type TVec2
	Field x:Float , y:Float
	
	Rem
	bbdoc: Initalizes vector with x/y values
	EndRem
	Method Init:TVec2(_x:Float,_y:Float) 
		x = _x
		y = _y
		Return Self
	End Method
	
	Rem
	bbdoc: Get the vector's X value
	EndRem
	Method GetX:Float() 
		Return x
	End Method
	
	Rem
	bbdoc: Get the vector's Y value
	EndRem
	Method GetY:Float()
		Return y
	End Method
	
	Rem
	bbdoc: Set the vector's X value
	EndRem
	Method SetX(_x:Float) 
		x = _x
	End Method
	
	Rem
	bbdoc: Set the vector's Y value
	EndRem
	Method SetY(_y:Float) 
		y = _y
	End Method
	
	Rem
	bbdoc: Get the vector's angle
	EndRem
	Method GetAngle:Float()
		Return ATan2(y,x)
	End Method
	
	Rem
	bbdoc: Rotate the vector to an angle
	EndRem
	Method Rotate(ang:Float)
		Local xprime:Float=Cos(ang)*x - Sin(ang)*y 
		Local yprime:Float=Sin(ang)*x + Cos(ang)*y
		x=xprime
		y=yprime
	End Method
	
	Rem
	bbdoc: Add values to X and Y
	EndRem
	Method Add(_x:Float,_y:Float)
		x:+_x
		y:+_y
	End Method
	
	Rem
	bbdoc: Add a vector's x/y to this vector
	EndRem
	Method AddVec(Vec:TVec2)
		If Vec=Null Return
		x:+Vec.x
		y:+Vec.y
	End Method
	
	Rem
	bbdoc: Subtract values from X and Y
	EndRem
	Method Subtract(_x:Float,_y:Float)
		x:-_x
		y:-_y
	End Method
	
	Rem
	bbdoc: Subtract a vector's x/y from this vector
	EndRem
	Method SubtractVec(Vec:TVec2)
		If Vec=Null Return
		x:-Vec.x
		y:-Vec.y
	EndMethod
	
	Rem
	bbdoc: Subtract a vector's x/y from this vector, but return a new vector rather than just changing one
	EndRem
	Method SubtractNewVec:TVec2(Vec:TVec2)
		If Vec=Null Return
		Local _x = y - Vec.x
		Local _y = x - Vec.y
		Return New TVec2.Init(_x, _y)
	EndMethod
	
	Rem
	bbdoc: Multiply a vector's x/y by 2 respective factors
	EndRem
	Method Multiply(_x:Float,_y:Float)
		x:*_x
		y:*_y
	EndMethod
	
	Rem
	bbdoc: Multiply this vector by another vector's x/y values
	EndRem
	Method MultiplyVec(Vec:TVec2)
		If Vec=Null Return
		x:*Vec.x
		y:*Vec.y
	EndMethod
	
	Rem
	bbdoc: Divide a vector's x/y by 2 respective factors
	EndRem
	Method Divide(_x:Float,_y:Float)
		If _x = 0 Or _y = 0 Return
		x:/_x
		y:/_y
	EndMethod
	
	Rem
	bbdoc: Divide this vector by another vector's x/y values
	EndRem
	Method DivideVec(Vec:TVec2)
		If Vec=Null Return
		x:/Vec.x
		y:/Vec.y
	EndMethod
	
	Rem
	bbdoc: Get the dot product of this vector and "Vec"
	EndRem
	Method DotProduct:Float(Vec:TVec2)
		Return x*Vec.x+y*Vec.y
	End Method
	
	Rem
	bbdoc: Get the angle difference between this vector and "Vec"
	EndRem
	Method GetAngleDif:Float(Vec:TVec2) 
		If Vec=Null Return 0
		Return Abs(TrueMod(ATan2(y,x)+180-ATan2(Vec.y,Vec.x),360)-180)
	EndMethod
	
	Rem
	bbdoc: Returns a new vector that is the result of this vector reflecting off of "Vec"
	EndRem
	Method Reflected:TVec2(Vec:TVec2) 
		Local VecN:TVec2 = Vec.Normalized()
		Local Vec1:TVec2 = Self.Copy()
		Local VecN_DOT_Vec1:Float = VecN.DotProduct(Vec1) 
		VecN.Multiply(2*VecN_DOT_Vec1, 2*VecN_DOT_Vec1)
		Vec1.SubtractVec(VecN) 
		Return Vec1
	End Method
	
	Rem
	bbdoc: Returns a new vector that is the normalized version of this vector
	EndRem
	Method Normalized:TVec2() 
		Local magn:Float=Self.GetMagnitude()
		Local Vector:TVec2=Self.Copy()
		If magn&lt;&gt;0
			Vector.x=x/magn
			Vector.y=y/magn
		EndIf
		Return Vector
	End Method
	
	Rem
	bbdoc: Get the length (magnitude) of the vector
	EndRem
	Method GetMagnitude:Float() 
		Return Sqr(x*x+y*y)
	End Method
	
	Rem
	bbdoc: Returns a new vector that is an exact copy of this vector.
	EndRem
	Method Copy:TVec2() 
		Return New TVec2.Init(x,y)
	End Method
End Type

'2d Oriented Bounding Box
Type TOBB2
	Field Corner:TVec2[4]
	Field Axis:TVec2[2]
	Field x:Float, y:Float
	Field w:Float, h:Float
	Field angle:Float
	
	Method Create:TOBB2(_x:Float, _y:Float, _w:Float, _h:Float, a:Float)
		x = _x
		y = _y
		w = _w
		h = _h
		angle = a
		
		Corner[0] = New TVec2.Init((x * Cos(angle)) + (y * Sin(angle)), (y * Cos(angle)) - (x * Sin(angle)))'top left
		Corner[1] = New TVec2.Init(((x + w) * Cos(angle)) + (y * Sin(angle)), (y * Cos(angle)) - ((x + w) * Sin(angle)))'top right
		Corner[2] = New TVec2.Init(((x + w) * Cos(angle)) + ((y + h) * Sin(angle)), ((y + h) * Cos(angle)) - ((x + h) * Sin(angle)))'bottom right
		Corner[3] = New TVec2.Init((x * Cos(angle)) + ((y + h) * Sin(angle)), ((y + h) * Cos(angle)) - (x * Sin(angle)))'bottom left
		
		
		Axis[0] = Corner[1].SubtractNewVec(Corner[0])
        Axis[1] = Corner[3].SubtractNewVec(Corner[0])
		
		Return Self
	End Method
	
	Method Move(_x:Float, _y:Float, a:Float)
		x = _x
		y = _y
		angle = a
		
		Corner[0].SetX((x * Cos(angle)) + (y * Sin(angle)))'top left
		Corner[0].SetY((y * Cos(angle)) - (x * Sin(angle)))
		Corner[1].SetX(((x + w) * Cos(angle)) + (y * Sin(angle)))'top right
		Corner[1].SetY((y * Cos(angle)) - ((x + w) * Sin(angle)))
		Corner[2].SetX(((x + w) * Cos(angle)) + ((y + h) * Sin(angle)))'bottom right
		Corner[2].SetY(((y + h) * Cos(angle)) - ((x + h) * Sin(angle)))
		Corner[3].SetX((x * Cos(angle)) + ((y + h) * Sin(angle)))'bottom left
		Corner[3].SetY(((y + h) * Cos(angle)) - (x * Sin(angle)))
	End Method
	
	Method Overlaps:Byte(other:TOBB2)

	End Method
	
	Method ComputeAxes()

	End Method
	
End Type

Local box1:TOBB2 = New TOBB2.Create(50, 50, 20, 20, 0)

While Not KeyHit(KEY_ESCAPE)
	Cls

	If KeyDown(KEY_LEFT)
		box1.Move(box1.x - 1, box1.y, box1.angle + 1)
	End If
	If KeyDown(KEY_RIGHT)
		box1.Move(box1.x + 1, box1.y, box1.angle - 1)
	End If
	
	DrawOBB(box1)
	
	Flip 1
	Delay 5
Wend

Function DrawOBB(box:TOBB2)
	DrawLine(box.Corner[0].GetX(), box.Corner[0].GetY(), box.Corner[1].GetX(), box.Corner[1].GetY())
	DrawLine(box.Corner[1].GetX(), box.Corner[1].GetY(), box.Corner[2].GetX(), box.Corner[2].GetY())
	DrawLine(box.Corner[2].GetX(), box.Corner[2].GetY(), box.Corner[3].GetX(), box.Corner[3].GetY())
	DrawLine(box.Corner[3].GetX(), box.Corner[3].GetY(), box.Corner[0].GetX(), box.Corner[0].GetY())
End Function

Function TrueMod:Float(val:Float, modul:Short)
	val:Mod modul
	If val&lt;0 Then val:+modul
	Return val
EndFunction
</textarea><br><br>You can ignore the TVec2 object and the TrueMod function, those are not the cause of the problem, it's something to do with the math setting the corner points. <br><br></td></tr></table><br>
<a name="998615"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_Skully</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Theres some great polygon code in the archives that includes collisions.. I'm using it in the GUI I'm making for TileMax... works the charm!<br><br>I've wrapped it into a type..<br><pre class=code>
Type Poly
	Function TFormPolyToTFormPoly:Int( p1_xy:Float[], p1_x:Float=0, p1_y:Float=0,..
							p1_rot:Float=0, p1_scale_x:Float=1, p1_scale_y:Float=1,..
							p1_handle_x:Float=0, p1_handle_y:Float=0,..
							p1_origin_x:Float=0, p1_origin_y:Float=0,..
							p2_xy:Float[], p2_x:Float=0, p2_y:Float=0,..
							p2_rot:Float=0, p2_scale_x:Float=1, p2_scale_y:Float=1,..
							p2_handle_x:Float=0, p2_handle_y:Float=0,..
							p2_origin_x:Float=0, p2_origin_y:Float=0)
		
		If p1_xy.length&lt;6 Or (p1_xy.length&amp;1) Return False
		If p2_xy.length&lt;6 Or (p2_xy.length&amp;1) Return False
		
		Local tform1_xy:Float[]=TFormPoly(p1_xy,p1_x,p1_y,p1_rot,p1_scale_x,p1_scale_y,..
							p1_handle_x,p1_handle_y,p1_origin_x,p1_origin_y)
		
		Local tform2_xy:Float[]=TFormPoly(p2_xy,p2_x,p2_y,p2_rot,p2_scale_x,p2_scale_y,..
							p2_handle_x,p2_handle_y,p2_origin_x,p2_origin_y)
		
		If PolyToPoly(tform1_xy,tform2_xy)
			Return True
		Else
			Return False
		EndIf
		
	End Function
	
	Function PolyToTFormPoly:Int( p1_xy:Float[], p2_xy:Float[],..
							p2_x:Float=0, p2_y:Float=0, rot:Float=0,..
							scale_x:Float=1, scale_y:Float=1,..
							handle_x:Float=0, handle_y:Float=0,..
							origin_x:Float=0, origin_y:Float=0 )
		
		If p1_xy.length&lt;6 Or (p1_xy.length&amp;1) Return False
		If p2_xy.length&lt;6 Or (p2_xy.length&amp;1) Return False
		
		Local tform_xy:Float[]=TFormPoly(p2_xy,p2_x,p2_y,rot,scale_x,scale_y,..
							handle_x,handle_y,origin_x,origin_y)
		
		If PolyToPoly(p1_xy,tform_xy)
			Return True
		Else
			Return False
		EndIf
		
	End Function
	
	Function PolyToPoly:Int( p1_xy:Float[], p2_xy:Float[] )
		
		If p1_xy.length&lt;6 Or (p1_xy.length&amp;1) Return False
		If p2_xy.length&lt;6 Or (p2_xy.length&amp;1) Return False
		
		For Local i:Int=0 Until p1_xy.Length Step 2
			If PointInPoly(p1_xy[i],p1_xy[i+1],p2_xy) Then Return True
		Next
		For Local i:Int=0 Until p2_xy.Length Step 2
			If PointInPoly(p2_xy[i],p2_xy[i+1],p1_xy) Then Return True
		Next
		
		Local l1_x1:Float=p1_xy[p1_xy.Length-2]
		Local l1_y1:Float=p1_xy[p1_xy.Length-1]
		For Local i1:Int=0 Until p1_xy.Length Step 2
			Local l1_x2:Float=p1_xy[i1]
			Local l1_y2:Float=p1_xy[i1+1]
			
			Local l2_x1:Float=p2_xy[p2_xy.Length-2]
			Local l2_y1:Float=p2_xy[p2_xy.Length-1]
			For Local i2:Int=0 Until p2_xy.Length Step 2
				Local l2_x2:Float=p2_xy[i2]
				Local l2_y2:Float=p2_xy[i2+1]
				
				If LinesCross(l1_x1,l1_y1,l1_x2,l1_y2,l2_x1,l2_y1,l2_x2,l2_y2)
					Return True
				EndIf
				
				l2_x1=l2_x2
				l2_y1=l2_y2
			Next
			l1_x1=l1_x2
			l1_y1=l1_y2
		Next
		Return False
	End Function
	
	Function CircleToTFormPoly:Int( circle_x:Float, circle_y:Float, radius:Float,..
							xy:Float[], poly_x:Float=0, poly_y:Float=0, rot:Float=0,..
							scale_x:Float=1, scale_y:Float=1,..
							handle_x:Float=0, handle_y:Float=0,..
							origin_x:Float=0, origin_y:Float=0 )
		
		If xy.length&lt;6 Or (xy.length&amp;1) Return False
		
		Local tform_xy:Float[]=TFormPoly(xy,poly_x,poly_y,rot,scale_x,scale_y,..
							handle_x,handle_y,origin_x,origin_y)
		
		Return CircleToPoly(circle_x,circle_y,radius,tform_xy)
	End Function
	
	Function CircleToPoly:Int( circle_x:Float, circle_y:Float, radius:Float, xy:Float[] )
		
		If xy.length&lt;6 Or (xy.length&amp;1) Return False
		
		If PointInPoly(circle_x,circle_y,xy) Then Return True
		
		Local x1:Float=xy[xy.Length-2]
		Local y1:Float=xy[xy.Length-1]
		
		For Local i:Int=0 Until Len xy Step 2
			Local x2:Float=xy[i]
			Local y2:Float=xy[i+1]
			
			If LineToCircle(x1,y1,x2,y2,circle_x,circle_y,radius) Then Return True
			x1=x2
			y1=y2
		Next
		
		Return False
	End Function
	
	Function LineToTFormPoly:Int( line_x1:Float, line_y1:Float, line_x2:Float, line_y2:Float,..
							xy:Float[], poly_x:Float=0, poly_y:Float=0,..
							rot:Float=0, scale_x:Float=1, scale_y:Float=1,..
							handle_x:Float=0, handle_y:Float=0,..
							origin_x:Float=0, origin_y:Float=0 )
		
		If xy.length&lt;6 Or (xy.length&amp;1) Return False
		
		TFormGlobalToLocal(line_x1,line_y1,poly_x,poly_y,rot,scale_x,..
							scale_y,handle_x,handle_y,origin_x,origin_y)
		
		TFormGlobalToLocal(line_x2,line_y2,poly_x,poly_y,rot,scale_x,..
							scale_y,handle_x,handle_y,origin_x,origin_y)
		
		Return LineToPoly(line_x1,line_y1,line_x2,line_y2,xy)
	End Function
	
	Function LineToPoly:Int( line_x1:Float, line_y1:Float, line_x2:Float, line_y2:Float, xy:Float[] )
		
		If xy.length&lt;6 Or (xy.length&amp;1) Return False
		
		If PointInPoly(line_x1,line_y1,xy) Then Return True
		
		Local poly_x1:Float=xy[xy.Length-2]
		Local poly_y1:Float=xy[xy.Length-1]
		
		For Local i:Int=0 Until Len xy Step 2
			Local poly_x2:Float=xy[i]
			Local poly_y2:Float=xy[i+1]
			
			If LinesCross(line_x1,line_y1,line_x2,line_y2,..
							poly_x1,poly_y1,poly_x2,poly_y2) Then Return True
			poly_x1=poly_x2
			poly_y1=poly_y2
		Next
		
		Return False
		
	End Function
	
	Function PointInTFormPoly:Int( point_x:Float, point_y:Float, xy:Float[],..
							poly_x:Float=0, poly_y:Float=0, rot:Float=0,..
							scale_x:Float=1, scale_y:Float=1,..
							handle_x:Float=0, handle_y:Float=0,..
							origin_x:Float=0, origin_y:Float=0 )
		
		If xy.length&lt;6 Or (xy.length&amp;1) Return False
		
		TFormGlobalToLocal(point_x,point_y,poly_x,poly_y,rot,scale_x,..
							scale_y,handle_x,handle_y,origin_x,origin_y)
		
		Return PointInPoly(point_x,point_y,xy)
	End Function
	
	Function PointInPoly:Int( point_x:Float, point_y:Float, xy:Float[] )
		
		If xy.length&lt;6 Or (xy.length&amp;1) Return False
		
		Local x1:Float=xy[xy.Length-2]
		Local y1:Float=xy[xy.Length-1]
		Local cur_quad:Int=GetQuad(point_x,point_y,x1,y1)
		Local next_quad:Int
		Local total:Int
		
		For Local i:Int=0 Until Len xy Step 2
			Local x2:Float=xy[i]
			Local y2:Float=xy[i+1]
			next_quad=GetQuad(point_x,point_y,x2,y2)
			Local diff:Int=next_quad-cur_quad
			
			Select diff
			Case 2,-2
				If ( x2 - ( ((y2 - point_y) * (x1 - x2)) / (y1 - y2) ) )&lt;point_x
					diff=-diff
				EndIf
			Case 3
				diff=-1
			Case -3
				diff=1
			End Select
			
			total:+diff
			cur_quad=next_quad
			x1=x2
			y1=y2
		Next
		
		If Abs(total)=4 Then Return True Else Return False
	End Function
	
	Function TFormPoly:Float[]( xy:Float[], tform_x:Float=0, tform_y:Float=0, rot:Float=0,..
							scale_x:Float=1, scale_y:Float=1,..
							handle_x:Float=0, handle_y:Float=0,..
							origin_x:Float=0, origin_y:Float=0 )
		
		If xy.length&lt;6 Or (xy.length&amp;1) Return Null
		
		Local tform_xy:Float[]=xy[..]
		
		For Local i:Int=0 Until tform_xy.Length Step 2
			TFormLocalToGlobal(tform_xy[i],tform_xy[i+1],tform_x,tform_y,rot,..
							scale_x,scale_y,handle_x,handle_y,origin_x,origin_y)
		Next
		
		Return tform_xy
	End Function
	
	Function TFormGlobalToLocal( point_x:Float Var, point_y:Float Var,..
							tform_x:Float=0, tform_y:Float=0, rot:Float=0,..
							scale_x:Float=1, scale_y:Float=1,..
							handle_x:Float=0, handle_y:Float=0,..
							origin_x:Float=0, origin_y:Float=0 )
		
		point_x:-origin_x
		point_y:-origin_y
		
		point_x:-tform_x
		point_y:-tform_y
		
		Local mag:Float=Sqr(point_x*point_x+point_y*point_y)
		Local ang:Float=ATan2(point_y,point_x)
		point_x=Cos(ang-rot)*mag
		point_y=Sin(ang-rot)*mag
		
		point_x:/scale_x
		point_y:/scale_y
		
		point_x:+handle_x
		point_y:+handle_y
	End Function
	
	Function TFormLocalToGlobal( point_x:Float Var, point_y:Float Var,..
							tform_x:Float=0, tform_y:Float=0, rot:Float=0,..
							scale_x:Float=1, scale_y:Float=1,..
							handle_x:Float=0, handle_y:Float=0,..
							origin_x:Float=0, origin_y:Float=0 )
		
		point_x:-handle_x
		point_y:-handle_y
		
		point_x:*scale_x
		point_y:*scale_y
		
		Local mag:Float=Sqr(point_x*point_x+point_y*point_y)
		Local ang:Float=ATan2(point_y,point_x)
		point_x=Cos(ang+rot)*mag
		point_y=Sin(ang+rot)*mag
		
		point_x:+tform_x
		point_y:+tform_y
		
		point_x:+origin_x
		point_y:+origin_y
	End Function
	
	'Adapted from Fredborg's code
	Function LinesCross:Int( x0:Float, y0:Float , x1:Float, y1:Float,..
							x2:Float ,y2:Float, x3:Float, y3:Float )
		  
		Local n:Float=(y0-y2)*(x3-x2)-(x0-x2)*(y3-y2)
		Local d:Float=(x1-x0)*(y3-y2)-(y1-y0)*(x3-x2)
		
		If Abs(d) &lt; 0.0001 
			' Lines are parallel!
			Return False
		Else
			' Lines might cross!
			Local Sn:Float=(y0-y2)*(x1-x0)-(x0-x2)*(y1-y0)
	
			Local AB:Float=n/d
			If AB&gt;0.0 And AB&lt;1.0
				Local CD:Float=Sn/d
				If CD&gt;0.0 And CD&lt;1.0
					' Intersection Point
					Local X:Float=x0+AB*(x1-x0)
			       	Local Y:Float=y0+AB*(y1-y0)
					Return True
				End If
			End If
		
			' Lines didn't cross, because the intersection was beyond the end points of the lines
		EndIf
	
		' Lines do Not cross!
		Return False
	
	End Function
	
	'Adapted from TomToad's code
	Function LineToCircle:Int( x1:Float, y1:Float, x2:Float, y2:Float, px:Float, py:Float, r:Float )
		
		Local sx:Float = x2-x1
		Local sy:Float = y2-y1
		
		Local q:Float = ((px-x1) * (x2-x1) + (py - y1) * (y2-y1)) / (sx*sx + sy*sy)
		
		If q &lt; 0.0 Then q = 0.0
		If q &gt; 1.0 Then q = 1.0
		
		Local cx:Float=(1-q)*x1+q*x2
		Local cy:Float=(1-q)*y1 + q*y2
		
		
		If PointToPointDist(px,py,cx,cy) &lt; r
			
			Return True
			
		Else
			
			Return False
			
		EndIf
	
		
	End Function 
	
	Function PointToPointDist:Float( x1:Float, y1:Float, x2:Float, y2:Float )
	
		Local dx:Float = x1-x2
		Local dy:Float = y1-y2
		
		Return Sqr(dx*dx + dy*dy)
	End Function
	
	
	Function GetQuad:Int(axis_x:Float,axis_y:Float,vert_x:Float,vert_y:Float)
		If vert_x&lt;axis_x
			If vert_y&lt;axis_y
				Return 1
			Else
				Return 4
			EndIf
		Else
			If vert_y&lt;axis_y
				Return 2
			Else
				Return 3
			EndIf	
		EndIf
	
	End Function
	
	Function DrawPoly( xy:Float[], fill:Int=True, x:Float=0, y:Float=0 )
	Local origin_x:Float
	Local origin_y:Float
	GetOrigin origin_x,origin_y
	Local handle_x:Float
	Local handle_y:Float
	GetHandle handle_x,handle_y
	
	If fill
		_max2dDriver.DrawPoly xy,..
		-handle_x,-handle_y,..
		x+origin_x,y+origin_y
	Else
		Local x1:Float=xy[xy.Length-2]
		Local y1:Float=xy[xy.Length-1]
		For Local i:Int=0 Until Len xy Step 2
			Local x2:Float=xy[i]
			Local y2:Float=xy[i+1]
			_max2dDriver.DrawLine..
			-handle_x+x1,-handle_y+y1,..
			-handle_x+x2,-handle_y+y2,..
			x+origin_x-0.5,y+origin_y-0.5
			x1=x2
			y1=y2
		Next
	EndIf
   End Function
End Type
</pre> <br><br></td></tr></table><br>
<a name="998619"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yahfree</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> But the code I'm specifically interested works regardless of rotation. Do any of the above include this?<br><br>Edit - I can use some of that code to do the actual collision detection, but I still need to figure out how to rotate these four 2d points <br><br></td></tr></table><br>
<a name="998625"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yahfree</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> <textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'2d Oriented Bounding Box
Type TOBB2
	Field Corner:TVec2[4]
	Field Axis:TVec2[2]
	Field x:Float, y:Float
	Field w:Float, h:Float
	Field hw:Float, hh:Float
	Field angle:Float
	
	Method Create:TOBB2(_x:Float, _y:Float, _w:Float, _h:Float, a:Float)
		x = _x
		y = _y
		w = _w
		h = _h
		hw = w / 2
		hh = h / h
		angle = a
		
		Corner[0] = New TVec2.Init((-hw * Cos(angle)) + (-hh * Sin(angle)) + x, (-hh * Cos(angle)) - (-hw * Sin(angle)) + y)'top left
		Corner[1] = New TVec2.Init((hw * Cos(angle)) + (-hh * Sin(angle)) + x, (-hh * Cos(angle)) - (hw * Sin(angle)) + y)'top right
		Corner[2] = New TVec2.Init((hw * Cos(angle)) + (hh * Sin(angle)) + x, (hh * Cos(angle)) - (hw * Sin(angle)) + y)'bottom right
		Corner[3] = New TVec2.Init((-hw * Cos(angle)) + (hh * Sin(angle)) + x, (hh * Cos(angle)) - (-hw * Sin(angle)) + y)'bottom left
		
		
		Axis[0] = Corner[1].SubtractNewVec(Corner[0])
        Axis[1] = Corner[3].SubtractNewVec(Corner[0])
		
		Return Self
	End Method
	
	Method Move(_x:Float, _y:Float, a:Float)
		x = _x
		y = _y
		angle = a
		
		Corner[0].SetX((-hw * Cos(angle)) + (-hh * Sin(angle)) + x)'top left
		Corner[0].SetY((-hh * Cos(angle)) - (-hw * Sin(angle)) + y)
		Corner[1].SetX((hw * Cos(angle)) + (-hh * Sin(angle)) + x)'top right
		Corner[1].SetY((-hh * Cos(angle)) - (hw * Sin(angle)) + y)
		Corner[2].SetX((hw * Cos(angle)) + (hh * Sin(angle)) + x)'bottom right
		Corner[2].SetY((hh * Cos(angle)) - (hw * Sin(angle)) + y)
		Corner[3].SetX((-hw * Cos(angle)) + (hh * Sin(angle)) + x)'bottom left
		Corner[3].SetY((hh * Cos(angle)) - (-hw * Sin(angle)) + y)
		
		
	End Method
	
	Method Overlaps:Byte(other:TOBB2)

	End Method
	
	Method ComputeAxes()

	End Method
	
End Type
</textarea><br><br>tweaked the equations, but it's now showing a squished box, it rotates correctly though :p <br><br></td></tr></table><br>
<a name="998648"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> do you mean something like this:<br><pre class=code>
Graphics 800,600
Local angle:Int = 0
While Not KeyHit(key_escape)
Cls
ROTATING_RECT(200, 150, 80, 30, angle)
angle :+ 1
angle :Mod 360
Flip
Wend 

Function ROTATING_RECT(x, y, w, h, angle)
	s# = Sin(angle)
	c# = Cos(angle)
	sw# = w*s
	cw# = w*c
	sh# = h*s
	ch# = h*c
	ox=-(cw-sh)/2
	oy=-(sw+ch)/2
	DrawLine ox+x, oy+y, ox+x+cw, oy+y+sw
	DrawLine ox+x+cw, oy+y+sw, ox+x+cw-sh, oy+y+sw+ch
	DrawLine ox+x+cw-sh, oy+y+sw+ch, ox+x-sh, oy+y+ch
	DrawLine ox+x-sh, oy+y+ch, ox+x, oy+y
End Function
</pre><br>of course it has to be done through a central point. <br><br></td></tr></table><br>
<a name="998687"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yahfree</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> ohh.. Well that was the problem.<br><br>Thanks Jess. <br><br></td></tr></table><br>
<a name="998973"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yahfree</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Alright, I tried to replicate the Separate Axis Theorem for collision following this:<br><a href="http://www.gamedev.net/reference/programming/features/2dRotatedRectCollision/default.asp" target="_blank">http://www.gamedev.net/reference/programming/features/2dRotatedRectCollision/default.asp</a><br><br>It seems to work, however the collision doesn't seem to be exact. Can anyone spot an error in my code?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics 800, 600

'My 2d Vector Object
Type TVec2
	Field x:Float, y:Float
	
	Rem
	bbdoc: Initalizes vector with x/y values
	EndRem
	Method Init:TVec2(_x:Float,_y:Float) 
		x = _x
		y = _y
		Return Self
	End Method
	
	Rem
	bbdoc: Get the vector's X value
	EndRem
	Method GetX:Float() 
		Return x
	End Method
	
	Rem
	bbdoc: Get the vector's Y value
	EndRem
	Method GetY:Float()
		Return y
	End Method
	
	Rem
	bbdoc: Set the vector's X value
	EndRem
	Method SetX(_x:Float) 
		x = _x
	End Method
	
	Rem
	bbdoc: Set the vector's Y value
	EndRem
	Method SetY(_y:Float) 
		y = _y
	End Method
	
	Rem
	bbdoc: Get the vector's angle
	EndRem
	Method GetAngle:Float()
		Return ATan2(y,x)
	End Method
	
	Rem
	bbdoc: Rotate the vector to an angle
	EndRem
	Method Rotate(ang:Float)
		Local xprime:Float=Cos(ang)*x - Sin(ang)*y 
		Local yprime:Float=Sin(ang)*x + Cos(ang)*y
		x=xprime
		y=yprime
	End Method
	
	Rem
	bbdoc: Add values to X and Y
	EndRem
	Method Add(_x:Float,_y:Float)
		x:+_x
		y:+_y
	End Method
	
	Rem
	bbdoc: Add a vector's x/y to this vector
	EndRem
	Method AddVec(Vec:TVec2)
		If Vec=Null Return
		x:+Vec.x
		y:+Vec.y
	End Method
	
	Rem
	bbdoc: Subtract values from X and Y
	EndRem
	Method Subtract(_x:Float,_y:Float)
		x:-_x
		y:-_y
	End Method
	
	Rem
	bbdoc: Subtract a vector's x/y from this vector
	EndRem
	Method SubtractVec(Vec:TVec2)
		If Vec=Null Return
		x:-Vec.x
		y:-Vec.y
	EndMethod
	
	
	Rem
	bbdoc: Multiply a vector's x/y by 2 respective factors
	EndRem
	Method Multiply(_x:Float,_y:Float)
		x:*_x
		y:*_y
	EndMethod
	
	Rem
	bbdoc: Multiply this vector by another vector's x/y values
	EndRem
	Method MultiplyVec(Vec:TVec2)
		If Vec=Null Return
		x:*Vec.x
		y:*Vec.y
	EndMethod
	
	Rem
	bbdoc: Divide a vector's x/y by 2 respective factors
	EndRem
	Method Divide(_x:Float,_y:Float)
		If _x = 0 Or _y = 0 Return
		x:/_x
		y:/_y
	EndMethod
	
	Rem
	bbdoc: Divide this vector by another vector's x/y values
	EndRem
	Method DivideVec(Vec:TVec2)
		If Vec=Null Return
		x:/Vec.x
		y:/Vec.y
	EndMethod
	
	Rem
	bbdoc: Get the dot product of this vector and "Vec"
	EndRem
	Method DotProduct:Float(Vec:TVec2)
		Return x*Vec.x+y*Vec.y
	End Method
	
	Rem
	bbdoc: Get the angle difference between this vector and "Vec"
	EndRem
	Method GetAngleDif:Float(Vec:TVec2) 
		If Vec=Null Return 0
		Return Abs(TrueMod(ATan2(y,x)+180-ATan2(Vec.y,Vec.x),360)-180)
	EndMethod
	
	Rem
	bbdoc: Returns a new vector that is the result of this vector reflecting off of "Vec"
	EndRem
	Method Reflected:TVec2(Vec:TVec2) 
		Local VecN:TVec2 = Vec.Normalized()
		Local Vec1:TVec2 = Self.Copy()
		Local VecN_DOT_Vec1:Float = VecN.DotProduct(Vec1) 
		VecN.Multiply(2*VecN_DOT_Vec1, 2*VecN_DOT_Vec1)
		Vec1.SubtractVec(VecN) 
		Return Vec1
	End Method
	
	Rem
	bbdoc: Returns a new vector that is the normalized version of this vector
	EndRem
	Method Normalized:TVec2() 
		Local magn:Float=Self.GetMagnitude()
		Local Vector:TVec2=Self.Copy()
		If magn&lt;&gt;0
			Vector.x=x/magn
			Vector.y=y/magn
		EndIf
		Return Vector
	End Method
	
	Method Project:TVec2(vec:TVec2)
		If vec = Null Return
		MultiplyVec(vec)
		Divide(vec.GetMagnitude(), vec.GetMagnitude())
		MultiplyVec(vec)
	End Method
	Rem
	bbdoc: Get the length (magnitude) of the vector
	EndRem
	Method GetMagnitude:Float() 
		Return Sqr(x*x+y*y)
	End Method
	
	Rem
	bbdoc: Returns a new vector that is an exact copy of this vector.
	EndRem
	Method Copy:TVec2() 
		Return New TVec2.Init(x,y)
	End Method
End Type

'2d Oriented Bounding Box
Type TOBB2
	Field Corner:TVec2[4]
	Field Axis:TVec2[2]
	Field x:Float, y:Float
	Field w:Float, h:Float
	Field angle:Float
	Field s:Byte
	
	Method Create:TOBB2(_x:Float, _y:Float, _w:Float, _h:Float, a:Float)
		x = _x
		y = _y
		w = _w
		h = _h
		If w &gt; h
			s = w
		Else
			s = h
		End If
		angle = a
		
		Local s:Float = Sin(angle)
		Local c:Float = Cos(angle)
		Local sw:Float = s * w
		Local sh:Float = s * h
		Local cw:Float = c * w
		Local ch:Float = c * h
		Local ox:Float = -(cw - sh) / 2
		Local oy:Float = -(sw + ch) / 2
		
		Corner[0] = New TVec2.Init(ox + x, oy + y)'top left
		Corner[1] = New TVec2.Init(ox + x + cw, oy + y + sw)'top right
		Corner[2] = New TVec2.Init(ox + x + cw - sh, oy + y + sw + ch)'bottom right
		Corner[3] = New TVec2.Init(ox + x - sh, oy + y + ch)'bottom left
		
		
		Axis[0] = New TVec2.Init(Corner[1].GetX() - Corner[0].GetX(), Corner[1].GetY() - Corner[0].GetY())
        Axis[1] = New TVec2.Init(Corner[1].GetX() - Corner[2].GetX(), Corner[1].GetY() - Corner[2].GetY())
		
		Return Self
	End Method
	
	Method Move(_x:Float, _y:Float, a:Float)
		x = _x
		y = _y
		angle = a
		
		Local s:Float = Sin(angle)
		Local c:Float = Cos(angle)
		Local sw:Float = s * w
		Local sh:Float = s * h
		Local cw:Float = c * w
		Local ch:Float = c * h
		Local ox:Float = -(cw - sh) / 2
		Local oy:Float = -(sw + ch) / 2
		
		Corner[0] = Corner[0].Init(ox + x, oy + y)'top left
		Corner[1] = Corner[1].Init(ox + x + cw, oy + y + sw)'top right
		Corner[2] = Corner[2].Init(ox + x + cw - sh, oy + y + sw + ch)'bottom right
		Corner[3] = Corner[3].Init(ox + x - sh, oy + y + ch)'bottom left
		
		
	End Method

	Method OBBtoOBB:Int(other:TOBB2)
		'circle test - if it's not within the circle, then it's not within the square either.
		If PointsToDistance(x + (w / 2), y + (h / 2), other.x + (w / 2), other.y + (h / 2)) &gt; (s / 2) + (other.s / 2)
				Return False
		End If
		
		
		Local T:Float
		Local Projection:TVec2
		'Polygonal Test, using Seperate Axis therom
		'corner projections onto a perpendicular axis to each face
		'Box one
		Local TMax:Float[4]
		Local TMin:Float[4]
		For Local a:Int = 0 To 3
			For Local c:Int = 0 To 3
				'Onto First Axis
				If a = 0
					Projection:TVec2 = Corner[c].Copy()
					Projection.Project(Axis[0])
					T = Projection.DotProduct(Axis[0])
					If c = 0 TMax[0] = T TMin[0] = T
					If T &gt; TMax[0] TMax[0] = T
					If T &lt; TMin[0] TMin[0] = T
				End If
				'Project onto Second Axis
				If a = 1
					Projection:TVec2 = Corner[c].Copy()
					Projection.Project(Axis[1])
					T = Projection.DotProduct(Axis[1])
					If c = 0 TMax[1] = T TMin[1] = T
					If T &gt; TMax[1] TMax[1] = T
					If T &lt; TMin[1] TMin[1] = T
				End If
				' Project Onto Third Axis
				If a = 0
					Projection:TVec2 = Corner[c].Copy()
					Projection.Project(Axis[0])
					T = Projection.DotProduct(other.Axis[0])
					If c = 0 TMax[2] = T TMin[2] = T
					If T &gt; TMax[2] TMax[2] = T
					If T &lt; TMin[2] TMin[2] = T
				End If
				'Project onto Fourth Axis
				If a = 1
					Projection:TVec2 = Corner[c].Copy()
					Projection.Project(Axis[1])
					T = Projection.DotProduct(other.Axis[1])
					If c = 0 TMax[3] = T TMin[3] = T
					If T &gt; TMax[3] TMax[3] = T
					If T &lt; TMin[3] TMin[3] = T
				End If
			Next
		Next
		'Box two
		Local OMax:Float[4]
		Local OMin:Float[4]
		For a = 0 To 3
			For c = 0 To 3
				'Onto First Axis
				If a = 0
					Projection:TVec2 = other.Corner[c].Copy()
					Projection.Project(Axis[0])
					T = Projection.DotProduct(Axis[0])
					If c = 0 OMax[0] = T OMin[0] = T
					If T &gt; OMax[0] OMax[0] = T
					If T &lt; OMin[0] OMin[0] = T
				End If
				'Project onto Second Axis
				If a = 1
					Projection:TVec2 = other.Corner[c].Copy()
					Projection.Project(Axis[1])
					T = Projection.DotProduct(Axis[1])
					If c = 0 OMax[1] = T OMin[1] = T
					If T &gt; OMax[1] OMax[1] = T
					If T &lt; OMin[1] OMin[1] = T
				End If
				' Project Onto Third Axis
				If a = 0
					Projection:TVec2 = other.Corner[c].Copy()
					Projection.Project(Axis[0])
					T = Projection.DotProduct(other.Axis[0])
					If c = 0 OMax[2] = T OMin[2] = T
					If T &gt; OMax[2] OMax[2] = T
					If T &lt; OMin[2] OMin[2] = T
				End If
				'Project onto Fourth Axis
				If a = 1
					Projection:TVec2 = other.Corner[c].Copy()
					Projection.Project(Axis[1])
					T = Projection.DotProduct(other.Axis[1])
					If c = 0 OMax[3] = T OMin[3] = T
					If T &gt; OMax[3] OMax[3] = T
					If T &lt; OMin[3] OMin[3] = T
				End If
			Next
		Next
		
		
		If OMin[0] &lt;= TMax[0] Or OMax[0] &gt;= TMin[0] 'Test Axis 1 Box 1
			If OMin[1] &lt;= TMax[1] Or OMax[1] &gt;= TMin[1]'Test Axis 2 Box 1
				If OMin[2] &lt;= TMax[2] Or OMax[2] &gt;= TMin[2]'Test Axis 1 Box 2
					If OMin[2] &lt;= TMax[2] Or OMax[2] &gt;= TMin[2]'Test Axis 2 Box 2
						Return True
					Else
						Return False
					End If
				Else
					Return False
				End If
			Else
				Return False
			End If
		Else
			Return False
		End If
	End Method
	
		
End Type

Local box1:TOBB2 = New TOBB2.Create(50, 50, 20, 20, 0)
Local box2:TOBB2 = New TOBB2.Create(500, 50, 20, 20, 0)

While Not KeyHit(KEY_ESCAPE)
	Cls

	If KeyDown(KEY_LEFT)
		box1.Move(box1.x - 1, box1.y, box1.angle + 1)
		box2.Move(box2.x + 1, box2.y, box2.angle - 1)
	End If
	If KeyDown(KEY_RIGHT)
		box1.Move(box1.x + 1, box1.y, box1.angle - 1)
		box2.Move(box2.x - 1, box2.y, box2.angle + 1)
	End If
	
	DrawOBB(box1)
	DrawOBB(box2)
	
	If box1.OBBtoOBB(box2)
		DrawText "collided!", 0, 0
	End If
	
	Flip 1
	Delay 5
Wend

Function DrawOBB(box:TOBB2)
	SetColor 255, 100, 100
	DrawLine(box.Corner[0].GetX(), box.Corner[0].GetY(), box.Corner[1].GetX(), box.Corner[1].GetY())
	SetColor 100, 255, 100
	DrawLine(box.Corner[1].GetX(), box.Corner[1].GetY(), box.Corner[2].GetX(), box.Corner[2].GetY())
	SetColor 100, 100, 255
	DrawLine(box.Corner[2].GetX(), box.Corner[2].GetY(), box.Corner[3].GetX(), box.Corner[3].GetY())
	SetColor 50, 50, 50
	DrawLine(box.Corner[3].GetX(), box.Corner[3].GetY(), box.Corner[0].GetX(), box.Corner[0].GetY())
	SetColor 255, 255, 255
	
End Function

Function PointsToDistance:Float(x1:Float, y1:Float, x2:Float, y2:Float)
	Return Sqr(((x1-x2)*(x1-x2))+((y1-y2)*(y1-y2)))
End Function

Function TrueMod:Float(val:Float, modul:Short)
	val:Mod modul
	If val&lt;0 Then val:+modul
	Return val
EndFunction
</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
