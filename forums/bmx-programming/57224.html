<!DOCTYPE html><html lang="en" ><head ><title >Windows fonts in a MaxGUI Canvas</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Windows fonts in a MaxGUI Canvas</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Windows fonts in a MaxGUI Canvas</a><br><br>
<a name="636073"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >neilo</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I wanted to be able to select a font to use in a multimedia application.  Using MaxGUI, I can RequestFont()... and not be able to use the resulting font (because of the way Windows names and uses fonts).<br><br>So, I needed to write my own font request function:<br><br><pre class=code>
Type WindowsFont
	Field name:String
	Field fullPath:String
	Field size:Int
End Type

Function RequestWindowsFont:WindowsFont(x:Int,y:Int,font:WindowsFont)
	Local win:TGadget
	Local btnOk:TGadget
	Local btnCancel:TGadget
	Local lstFontNames:TGadget
	Local lstFontSizes:TGadget
	Local tbFontSize:TGadget
	Local canSample:TGadget
	Local fontPath:String
	Local fontDirHandle:Int
	Local fileName:String
	Local quit:Int
	Local iFont:TImageFont
	Local i:Int
	Local winFont:WindowsFont
	Local defaultFontSizes:Int[]=[8,10,12,14,18,24,36,48,72]
	
	Function renderSampleText(gr:TGraphics,font:TImageFont)
		Local width:Int,tWidth:Int
		Local height:Int,tHeight:Int
		Local x:Int,y:Int
		
		SetGraphics gr
		width=GraphicsWidth()
		height=GraphicsHeight()
		SetImageFont font
		
		tWidth=TextWidth("Sample")
		tHeight=TextHeight("Sample")
		x=(width-tWidth)/2
		y=(height-tHeight)/2
		Cls
		DrawText "Sample",x,y
		Flip
	End Function
	
	win=CreateWindow("Select Font",x,y,269,234,Null,WINDOW_TITLEBAR|WINDOW_CLIENTCOORDS)
	lstFontNames=CreateListBox(3,3,200,140,win)
	tbFontSize=CreateTextField(GadgetX(lstFontNames)+GadgetWidth(lstFontNames)+3,..
							   GadgetY(lstFontNames),60,22,win)
	lstFontSizes=CreateListBox(GadgetX(tbFontSize),GadgetY(tbFontSize)+26,60,..
							   GadgetHeight(lstFontNames)-25,win)
	canSample=CreateCanvas(3,146,263,60,win)
	btnOk=CreateButton("Ok",206,209,60,22,win)
	btnCancel=CreateButton("Cancel",143,209,60,22,win)
	
	fontPath=getenv_("windir")
	fontPath:+"\Fonts\"
	fontDirHandle=ReadDir(fontPath)
	Repeat
		fileName=NextFile(fontDirHandle)
		Select fileName
			Case ""
			Case "."
			Case ".."
			Default
				If Right(fileName,3)="ttf" Then ..
					AddGadgetItem lstFontNames,fileName
		End Select
	Until fileName=""
	
	For i=0 To defaultFontSizes.length-1
		AddGadgetItem lstFontSizes,defaultFontSizes[i]
	Next
	SetGadgetText tbFontSize,"16"
	
	quit=False
	While Not quit
		WaitEvent
		Select EventID()
			Case EVENT_GADGETACTION
				Select EventSource()
					Case lstFontNames
						i=SelectedGadgetItem(lstFontNames)
						If i&gt;-1
							fileName=fontPath+GadgetItemText(lstFontNames,i)
							iFont=LoadImageFont(fileName,TextFieldText(tbFontSize).ToInt())
							renderSampleText CanvasGraphics(canSample),iFont
						EndIf
					Case lstFontSizes
						i=SelectedGadgetItem(lstFontSizes)
						If i&gt;-1
							SetGadgetText tbFontSize,GadgetItemText(lstFontSizes,i)
							iFont=LoadImageFont(fileName,TextFieldText(tbFontSize).ToInt())
							renderSampleText CanvasGraphics(canSample),iFont
						EndIf
					Case btnOk
						i=SelectedGadgetItem(lstFontNames)
						If i&gt;-1
							winFont=New WindowsFont
							winFont.name=GadgetItemText(lstFontNames,i)
							winFont.fullPath=fontPath+GadgetItemText(lstFontNames,i)
							winFont.size=TextFieldText(tbFontSize).ToInt()
							quit=True
						EndIf
					Case btnCancel
						winFont=Null
						quit=True
				End Select
			Case EVENT_WINDOWCLOSE
				Select EventSource()
					Case win
						quit=True
						winFont=Null
				End Select
			Case EVENT_GADGETPAINT
				Select EventSource()
					Case canSample
						renderSampleText CanvasGraphics(canSample),iFont
				End Select
		End Select	
	Wend
	FreeGadget win
	Return winFont
End Function

RequestWindowsFont 40,40,Null
</pre><br><br>It should be pretty obvious how this works.  It requires MaxGUI (obviously), and it will only run on Windows.<br><br>Feel free to use this any way you see fit.<br><br>Neil <br><br></td></tr></table><br>
<a name="636074"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Very nice!<br><br>You can possibly build a translation map with the same technique to get RequestFont results working? <br><br></td></tr></table><br>
<a name="636092"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >neilo</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Possibly... that would be the nice solution.  I'll work on it (but it won't be cross-platform) <br><br></td></tr></table><br>
<a name="636182"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Beaker</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> You should put this in the Code Archive. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
