<!DOCTYPE html><html lang="en" ><head ><title >Help :)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Help :)</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Help :)</a><br><br>
<a name="1167570"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ingenius</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi<br><br>I'm trying to improve movement in a slot machine. here is what I have done, but does not move naturally.<br><br><a href="http://ubuntuone.com/27fMg1n8D9BGRCekQW5eQa" target="_blank">http://ubuntuone.com/27fMg1n8D9BGRCekQW5eQa</a><br><br>How does the reel of a slot machine.<br><br>1 - Accelerates<br>2 - Run freely<br>3 - Determine the symbol which has to stop.<br>4 - Slows<br>5 - Detention<br><br>This piece of code does exactly that. but lacks things, but what I want is a more natural movement. Anyone have a better idea?!<br><br>Regards<br>I<br><br>P/D My english is not my natural language, sorry if i made any mistake <br><br></td></tr></table><br>
<a name="1167594"></a>

<a name="1167601"></a>

<a name="1167602"></a>

<a name="1167604"></a>

<a name="1167605"></a>

<a name="1167606"></a>

<a name="1167608"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> You did a lot of code, but it is not necessary....<br><br>f.e. the degrees do not serve anything until you do not work with real 3D. So I would suggest to think about a ribbon of elements and only a few are visible.<br><br>This would make the code much shorter.<br><br>Also direction# is not necessary, you could simply change speed# to negative value to get a other direction. And to get speed# negativ you only have to set aceleration# negativ.<br><br>here are some of my ideas:<br><pre class=code>SuperStrict

Local images:TImage[10]
Local counter:Int = 0
Local total:Int = 10

For counter = 0 To total - 1
	Local Nummer$= counter+1
	Nummer=Right("00" + Nummer,2)
	images[counter] = LoadImage("graphics/base/sym_" + Nummer + ".png")
Next

Graphics 640,480

Local ItemHight:Int = ImageHeight(images[0])
Local Position:Float = 1

Local Speed:Float = 0
Const TOP_SPEED:Float = 0.22

Local Aceleration:Float = 0
Const ACCEL:Float=0.001

Local X:Int = 100
Local Y:Int = 100
Local pos:Float

Global  StartView% =50, EndView% =350
Global FPS:TTimer=CreateTimer(60)

While Not KeyHit( KEY_ESCAPE )
	
	Speed = Speed + Aceleration

	If Speed &gt; TOP_SPEED Then
		Speed = TOP_SPEED
	ElseIf Speed &lt; -TOP_SPEED Then
		Speed = -TOP_SPEED
	EndIf

	Position = Position + Speed

	If ( Position &gt; Total) Then
		Position = 0
	ElseIf ( Position &lt; 0 ) Then
		Position = Total
	End If

	Cls
	SetColor 255,255,255
	For counter = 0 To total - 1
		pos=((Position+Counter)*Itemhight) Mod (Total*Itemhight)
		If ( pos &gt;= StartView And pos &lt;= EndView ) Then
			DrawImage images[counter], X+200, pos
			DrawImage images[counter], X+400, pos
		End If
		DrawImage images[counter], X+0, pos
	Next 
	SetColor 221,1,1
	DrawRect 400,0,300,150
	DrawRect 400,300,300,200
	
	Flip 0
	WaitTimer fps
	
	If KeyHit( KEY_1 ) 
		Aceleration= ACCEL
	ElseIf KeyHit( KEY_2 )
		Aceleration= -ACCEL
	End If

Wend </pre><br><br><br>the kernel is the handling of the acceleration. The KEYS only change aceleration#. This will change speed# and this will change position#.<br><br>always do thing like this:<br>check speed# limits immediately after you change it with aceleration#.<br>check position# limits immediately after you change it with speed#.<br><br><br>I did not add your status% things. But let me say: status should never control speed or position but events like KEYS or RND()<br><br><br>here is a sample how to stop it correct. Use KEYs 1 2 to run the slotmachine and 3 to stop it:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

Local images:TImage[10]
Local counter:Int = 0
Local total:Int = 10

Local status:Int 
Const STATUS_STOP%=0, STATUS_START%=1, STATUS_MAX%=2,STATUS_MIN%=3,STATUS_END%=4

For counter = 0 To total - 1
	Local Nummer$= counter+1
	Nummer=Right("00" + Nummer,2)
	images[counter] = LoadImage("graphics/base/sym_" + Nummer + ".png")
Next

Graphics 640,480


Local ItemHight:Int = ImageHeight(images[0])
Local Position:Float = 1

Local Speed:Float = 0
Const TOP_SPEED:Float = 0.22

Local Aceleration:Float = 0
Const ACCEL:Float=0.001

Local X:Int = 100
Local Y:Int = 100

Local initial:Float
Local pos:Float
Local offset:Float
Global  StartView% =50, EndView% =350


Global FPS:TTimer=CreateTimer(60)
While Not KeyHit( KEY_ESCAPE )
	
	Speed = Speed + Aceleration

	If Speed &gt; TOP_SPEED Then
		Status = STATUS_MAX
		Speed = TOP_SPEED
	ElseIf (Abs(Speed) &lt; 0.02) And (Status = STATUS_MAX)Then
		Aceleration=0
		'Speed=-Speed
		Status = STATUS_END
	ElseIf Status = STATUS_END
		If Abs(Position-Int(Position))&lt;0.04
			Position = Int(Position)
			Speed=0
		Else
			Status = STATUS_END
		EndIf
	ElseIf Speed &lt; -TOP_SPEED Then
		Status = STATUS_MAX
		Speed = -TOP_SPEED
	EndIf

	Position = Position + Speed

	If ( Position &gt; Total) Then
		Position = 0
	ElseIf ( Position &lt; 0 ) Then
		Position = Total
	End If



	Cls
	SetColor 255,255,255
	For counter = 0 To total - 1
		pos=Offset + ((Position+Counter)*Itemhight) Mod (Total*Itemhight)
		If ( pos &gt;= StartView And pos &lt;= EndView ) Then
			DrawImage images[counter], X+200, pos
			DrawImage images[counter], X+400, pos
		End If
		DrawImage images[counter], X+0, pos
	Next 
	SetColor 221,1,1
	DrawRect 400,0,300,150
	DrawRect 400,300,300,200
	
	
	Flip 0
	WaitTimer fps
	
	If KeyHit( KEY_1 )
		' start downward
		Status = STATUS_START
		Aceleration= ACCEL
	ElseIf KeyHit( KEY_2 )
		' start upward
		Aceleration= -ACCEL
		Status = STATUS_START
	ElseIf KeyHit( KEY_3 )
		' stop it
		Aceleration= -Aceleration
	End If

Wend 

</textarea><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1167626"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ingenius</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Midi .. these are the days that I think how much i need to learn.. less code and better performance :) let me see <br><br></td></tr></table><br>
<a name="1167730"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ingenius</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Midimaster,<br><br>What do you think about this, and simulates stop effect of wheel. <br><br><pre class=code>
SuperStrict

Const DOWN:Int = 1
Const UP:Int = -1

Const STOPED:Int = 0
Const ROLLING:Int = 1
Const STOPPING:Int = 2

Const SPEED_FACTOR:Float = 200

Local images:TImage[10]
Local counter:Int = 0
Local total:Int = 10

For counter = 0 To total - 1
	Local Nummer$= counter+1
	Nummer=Right("00" + Nummer,2)
	images[counter] = LoadImage("graphics/base/sym_" + Nummer + ".png")
Next

Graphics 640,480

Local ItemHight:Int = ImageHeight(images[0])
Local Position:Float = 1
Local Direction:Int = DOWN
Local start_at:Float = 0
Local offset:Float = 0
Local state:Int = 0
Local stopto:Int = 10
Local loop:Int = 0
Local speed:Float = 0
Local angle:Float = 0
Local ShowStart:Int
Local ShowEnd:Int
Local pos:Int
Local X:Int = 100
Local Y:Int = 100

Global FPS:TTimer=CreateTimer(60)
While Not KeyHit( KEY_ESCAPE )

	If state = ROLLING Then

		If Direction = DOWN And offset &gt;= ItemHight Then
		
			start_at = start_at + 1
			offset = 0
			
			If ( start_at Mod total ) = stopto Then
				loop :+ 1
				If loop = 2 Then
					speed = SPEED_FACTOR * 0.1
					state = STOPPING
					angle = 20
				End If
			End If 
			
		Else If Direction = UP And Offset &lt;= ( ItemHight * -1 ) Then
			
			start_at :+ 1
			offset = 0

			If ( start_at Mod total ) = stopto Then
				loop :+ 1
				If loop = 2 Then
					speed = SPEED_FACTOR = 0.1
					state = STOPPING
					angle = 20
				End If
			End If
			
		End If

	Else If State = STOPPING
		
		speed = SPEED_FACTOR * 0.03 * Sin(angle * 3.14 / 180)
		angle :+ 25
		If angle &gt;= 365 Then
			offset = 0
			speed = 0
			state = STOPED
		End If
			
	End If
	
	If Direction = DOWN Then
		ShowStart = -1
		ShowEnd = 3
	Else
		ShowStart = 0
		ShowEnd = 4
	End If 
	
	Cls
	For counter = ShowStart To ShowEnd
		pos = ( start_at + counter ) Mod Total
		If pos &lt; 0 Then
			pos = pos + Total
		End If
		' Print "POS : " + String(pos)
		DrawImage images[pos], X, Y + ( ItemHight * Counter ) + offset
	Next 
	Flip 0
	
	WaitTimer fps
	
	If state = ROLLING Then
		speed = SPEED_FACTOR;
	Else If state = STOPED
	    speed = 0;
	End If
	
	offset = offset + speed * Float(direction)

	If KeyHit( KEY_1 )
		' start downward
		State = ROLLING
		Loop = 0
		StopTo = Rand(1, Total)
	ElseIf KeyHit( KEY_2 )
		' start upward
	ElseIf KeyHit( KEY_3 )
		' stop it
	End If

Wend
</pre> <br><br></td></tr></table><br>
<a name="1167745"></a>

<a name="1167748"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> if you see some advantage for you.. why not... <br><br>what I see:<br><br>the pictures are now jumping to their next position, but do not move smooth.<br><br>the pictures running incredibel fast and not like a rolling wheel.<br><br>what is the reason you would prefer your code now? what do you try to get finally.<br><br>Simulating the last moment of the movement, this "a little bit back" of the wheel, is not trivial.... I would do it by driving a little bit to far then go back then shake the pictures two three times. <br><br>I my code to can play with the values of ACCEL# and TOP_SPEED# to adjust speed behavior. Perhaps it it necessary to have two different acelerations: RISE_ACCEL# and REDUCE_ACCEL#<br><br>For different direction of three wheels I would use three variables like ..., aceleration_wheel1#, aceleration_wheel2#, aceleration_wheel3#<br>Speed_wheel1#,... etc..<br><br>For the random I would manipulate the time each wheel is running and in the end have a look on which picture got in the center (like in reality)<br><br>If you want to see a slot-machine in one of my software, install this music education game:<br><br><a href="http://www.midimaster.de/download/InterMezzo.exe" target="_blank">http://www.midimaster.de/download/InterMezzo.exe</a><br><br>the slot-machine is a fake, but shows a nice "in-motion unsharpness"<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
