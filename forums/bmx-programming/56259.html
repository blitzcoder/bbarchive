<!DOCTYPE html><html lang="en" ><head ><title >HashTable object</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >HashTable object</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >HashTable object</a><br><br>
<a name="625623"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Defoc8</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, i figured id share this type/class with the community..<br>already sent a copy to noel, but no doubt others would like<br>a basic hashtable system too..<br><br>this is my first bmax class - perhaps not great, but it does <br>the job...anyway enjoy ;)<br><br>feel free to credit me if you use it :]<br><br><pre class=code>

''InsertEntry(name:string,o:object) - add an object to the table, the name is used to
''                                    to generate an index 0-255, selecting the list.
''
''RemoveEntry(name:string) - if you need to remove an object.
''
''GetEntry(name:string) - grab an entry, you must cast is back to its original type,
''                        myobject=myType(GetEntry(name:string))
''
''
''Flush() - removes all objects from the hash table                                    
''
''

''all entries should have unique names - you should first getEntry(), if this returns
''null - its safe to insert the new object, if it returns a valid object, you may want
''to return this object back to the user...


'''''''''''''''''''''''''''
''
''HASHTABLE OBJECT
''
''- M.Laurenson/Defoc8 2006
''''''''''''''''''''''''''''


Type gHashTable

 Field table:TList[]
 
 Method New()
  table=table[..256]
  For Local n:Int=0 To 255
   table[n]=CreateList()
  Next
 EndMethod

 Method genIndex(name$)
  Local val:Int=0
   For Local n:Int=0 To name.length-1
    val=val+(name[n]^2)
   Next
   val=(val&amp;255)  
  Return val
 EndMethod

 Method insertEntry(name:String,obj:Object)
  Local index:Int=genIndex(name$)
  Local entry:gHashEntry=New gHashEntry
    entry.name=name
    entry.obj=obj 
    entry.link=ListAddLast(table[index],entry)
 EndMethod

 Method removeEntry(name:String)
  Local index:Int=genIndex(name)
   For Local entry:gHashEntry=EachIn table[index]
    If(entry.name=name)
     RemoveLink(entry.link)
     Return
    EndIf
   Next
 EndMethod                 

 Method getEntry:Object(name:String)
  Local index:Int=genIndex(name)
   For Local entry:gHashEntry=EachIn table[index]
    If(entry.name=name)
     Return(entry.obj)
    EndIf
   Next
  Return Null
 EndMethod

 Method flush()
  For Local n:Int=0 To 255
   ClearList(table[n])
  Next
 EndMethod 

 Method getEntryCount(index:Int)
   Return CountList(table[index])
 EndMethod
EndType

Type gHashEntry
 Field name:String
 Field obj:Object
 Field link:TLink
EndType



</pre> <br><br></td></tr></table><br>
<a name="625649"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >FlameDuck</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> table=table[..256] <br></div>You should use a prime number as the number of buckets. Like 257. Otherwise it looks like an okay implementation. <br><br></td></tr></table><br>
<a name="625651"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Defoc8</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> hmmm...i dont actually see why the number of buckets <br>should be prime - doesnt this depend on the system used<br>to generate the bucket selection? regardless, i have tested<br>the distrubution on multiple data sets &amp; the results have <br>been good - its a simple system, this doesnt mean its bad.<br><br>Anyway- the code is public, so anyone can modify it.. <br><br></td></tr></table><br>
<a name="625775"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >N</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I figure I may as well share the version I molested...<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Rem
    HASHTABLE OBJECT
    
    - M.Laurenson/Defoc8 2006
EndRem

Rem
    While not explicitly mentioned in the code (most of you probably
    overlooked the actual writing in the post), I can promise
    you Def would appreciate if you credited him if you used this.
    No credit is to go to me.
    
    -Noel
EndRem

Strict

Public

Import "hash.c"

Extern "C"
    Function __c_hash:Int(key$z, length:Int, initval:Int)="Hash"
EndExtern

Global HashKey:Int(name:String) = gHashKey

Function gHashKey%( name:String )
    Return __c_hash( name, name.Length, 0 )
End Function

Type gHashTable
    'private:

    Field table:TList[]
    
    'protected:
    
    Method _rement(key:Int)
        For Local entry:gHashEntry = EachIn table[key Shr 24]
            If entry.key = key
                entry.link.Remove( )
                Return
            EndIf
        Next
    End Method
    
    Method _addent(key:Int,o:Object)
        Local entry:gHashEntry = New gHashEntry
        entry.key = key
        entry.obj = o 
        entry.link = table[key Shr 24].AddLast(entry)
    End Method
    
    Method _getent:Object(key:Int)
        For Local entry:gHashEntry = EachIn table[key Shr 24]
            If entry.key = key
                Return entry.obj
            EndIf
        Next
        Return Null
    End Method
    
    'public:
    
    Method New()
        table=New TList[256]
        For Local n:Int = 0 To 255
            table[n] = New TList
        Next
    End Method
    
    Method SetEntry( name:String, obj:Object )
        Local key:Int = HashKey( name )
        Local o:Object = _getent(key)
        If o And o &lt;&gt; obj Then _rement(key)
        _addent(key,obj)
    End Method
    
    Method InsertEntry(name:String,obj:Object)
        Local key:Int = HashKey( name )
        _addent(key,obj)
    End Method
    
    Method RemoveEntry(name:String)
        Local key:Int = HashKey( name )
        _rement(key)
    End Method
    
    Method GetEntry:Object(name:String)
        Local key:Int = HashKey( name )
        Return _getent(key)
    End Method
    
    Method Flush()
        For Local n:Int = 0 To 255
            table[n].Clear( )
        Next
    End Method 
    
    Method GetEntryCount(index:Int)
        Return table[index].Count( )
    End Method
End Type

Type gHashEntry
    Field key:Int
    Field obj:Object
    Field link:TLink
End Type</textarea><br><br>hash.c<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">/*
    Code by Bob Jenkins, December 1996, Public Domain.  Plus some minor,
    minor tweaks by Noel Cower.  Nothing that affects the implementation.
    You can use this free for any purpose.  It has no warranty.
*/

#define mix(a,b,c) \
{ \
  a -= b; a -= c; a ^= (c&gt;&gt;13); \
  b -= c; b -= a; b ^= (a&lt;&lt;8); \
  c -= a; c -= b; c ^= (b&gt;&gt;13); \
  a -= b; a -= c; a ^= (c&gt;&gt;12);  \
  b -= c; b -= a; b ^= (a&lt;&lt;16); \
  c -= a; c -= b; c ^= (b&gt;&gt;5); \
  a -= b; a -= c; a ^= (c&gt;&gt;3);  \
  b -= c; b -= a; b ^= (a&lt;&lt;10); \
  c -= a; c -= b; c ^= (b&gt;&gt;15); \
}

int Hash( k, length, initval )
register const unsigned char *k;        /* the key */
register int  length;   /* the length of the key */
register int  initval;    /* the previous hash, or an arbitrary value */
{
   register int a,b,c,len;

   /* Set up the internal state */
   len = length;
   a = b = 0x9e3779b9;  /* the golden ratio; an arbitrary value */
   c = initval;           /* the previous hash value */

   /*---------------------------------------- handle most of the key */
   while (len &gt;= 12)
   {
      a += (k[0] +((int)k[1]&lt;&lt;8) +((int)k[2]&lt;&lt;16) +((int)k[3]&lt;&lt;24));
      b += (k[4] +((int)k[5]&lt;&lt;8) +((int)k[6]&lt;&lt;16) +((int)k[7]&lt;&lt;24));
      c += (k[8] +((int)k[9]&lt;&lt;8) +((int)k[10]&lt;&lt;16)+((int)k[11]&lt;&lt;24));
      mix(a,b,c);
      k += 12; len -= 12;
   }

   /*------------------------------------- handle the last 11 bytes */
   c += length;
   switch(len)              /* all the case statements fall through */
   {
   case 11: c+=((int)k[10]&lt;&lt;24);
   case 10: c+=((int)k[9]&lt;&lt;16);
   case 9 : c+=((int)k[8]&lt;&lt;8);
      /* the first byte of c is reserved for the length */
   case 8 : b+=((int)k[7]&lt;&lt;24);
   case 7 : b+=((int)k[6]&lt;&lt;16);
   case 6 : b+=((int)k[5]&lt;&lt;8);
   case 5 : b+=k[4];
   case 4 : a+=((int)k[3]&lt;&lt;24);
   case 3 : a+=((int)k[2]&lt;&lt;16);
   case 2 : a+=((int)k[1]&lt;&lt;8);
   case 1 : a+=k[0];
     /* case 0: nothing left to add */
   }
   mix(a,b,c);
   /*-------------------------------------------- report the result */
   return c;
}
</textarea><br><br>"If it ain't broke, don't fix it" doesn't really apply to Def's code ;)  Then again, I'm rather notorious for fixing that which isn't broken. <br><br></td></tr></table><br>
<a name="625785"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >FlameDuck</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> i dont actually see why the number of buckets<br>should be prime - doesnt this depend on the system used<br>to generate the bucket selection? <br></div>Yes. Which should also use primes to reduce the risk of hash collisions. Perhaps you could also distribute your test code, so others could have a go at testing it? Have you tried adding the canonical location of all files on your root drive, for example? I know that dataset in particular took down early Java hashing algorithms (because they where radix based, and thus would generate identical hashes, for nearly identical keys, where as a solid hashing algorithm generates wildly different hashes from similar keys).<br><br><div class="quote"> its a simple system, this doesnt mean its bad. <br></div>I'm not saying it is. <br><br></td></tr></table><br>
<a name="625893"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Duckstab[o]</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Can someone show a mini demo of the hash tables in use please <br><br></td></tr></table><br>
<a name="625899"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >klepto2</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well, first I have to say : nice thing. but as second I have to ask:<br>Isn't a Hashtable Type already included in BMAX which is called TMap. And what is the advantage of this system in comparison to the TMap type? <br><br></td></tr></table><br>
<a name="626074"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Defoc8</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> hey im new to bmax - i jst thought id share stuff :p<br> - and good stuff noel ;) :] <br><br></td></tr></table><br>
<a name="626089"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >klepto2</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Don't get me wrong,Your stuff is very good. I only was thinking about it. So keep up and share more stuff ;) <br><br></td></tr></table><br>
<a name="626133"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >FlameDuck</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Isn't a Hashtable Type already included in BMAX which is called TMap. <br></div>No. TMap is a Set implemented as a binary search tree.<br><br><div class="quote"> And what is the advantage of this system in comparison to the TMap type? <br></div>'This system' has close to constant look-up times. TMap has Log(n). Also this hashTable implementation doesn't appear to be a Set (that is, the same object can be indexed more than once).<br><br>It's a trade of between speed and space. <br><br></td></tr></table><br>
<a name="626236"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >N</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Duckstab: Here you go.  It's a pseudo-managed resource handler.  You request a resource, it's loaded into memory, and then only unloaded when all the banks created with its buffer are deleted (or when you call UnloadResources( )).<br><br>There are better ways to do this, but this is a rudimentary example.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">'' Use's Defoc8's nice hash table code (modified, of course)

Strict

Import "src/hashtable.bmx"

Private

Global _rtable:gHashTable = New gHashTable

Type IResource
    Field path$
    Field buffer@ Ptr = Null
    Field refs%
    Field size%
    Field key%
    
    Method AsBank:TBank( )
        Open( )
        If buffer = Null Then Return
        Return IResBank.CreateRes( buffer, size, key )
    End Method
    
    Method Open( )
        refs :+ 1
        If refs = 1 Then
            Local s:TStream = OpenStream( path, True, False )
            If s = Null Then
                refs = 0
                Return
            EndIf
            size = s.Size( )
            If size = 0 Then
                refs = 0
                Return
            EndIf
            buffer = MemAlloc( size )
            s.ReadBytes( buffer, size )
            s.Close( ); s = Null
        EndIf
    End Method
    
    Method Close( )
        If refs = 0 Then Return
        
        refs :- 1
        
        If refs = 0 Then
            memset_( buffer, 0, size )
            MemFree( buffer )
            buffer = Null
            size = 0
        EndIf
    End Method
    
    Method Delete( )
        While refs
            Close( )
        Wend
    End Method
End Type

Type IResBank Extends TBank
    Field key:Int
    
    Method PokeByte( o%, v% )
        Assert "Cannot write to resource bank"
    End Method
    Method PokeShort( o%, v% )
        Assert "Cannot write to resource bank"
    End Method
    Method PokeInt( o%, v% )
        Assert "Cannot write to resource bank"
    End Method
    Method PokeLong( o%, v:Long )
        Assert "Cannot write to resource bank"
    End Method
    Method PokeFloat( o%, v# )
        Assert "Cannot write to resource bank"
    End Method
    Method PokeDouble( o%, v! )
        Assert "Cannot write to resource bank"
    End Method
    
    Method Delete( )
        Local rs:IResource = IResource( _rtable._getent(key) )
        If rs Then rs.Close( )
    End Method
    
    Function CreateRes:IResBank( buffer@ Ptr, size%, key% )
        Assert size&gt;=0 Else "Illegal bank size"
		Local bank:IResBank = New IResBank
		bank._buf = buffer
		bank._size = size
		bank.key = key
		bank._capacity = -1
		Return bank
    End Function
End Type

Function RBindResources( dir%, cpath$="/", rpath$="./" )
    Local f$ = NextFile( dir )
    While f &lt;&gt; ""
        If f.Find(".",0) = 0 Then
            f = NextFile( dir )
            Continue
        EndIf
        Try
            If FileType( rpath+f ) = 1 Then
                Local r:IResource = New IResource
                r.path = rpath+f
                r.key = gHashKey( cpath+f )
                _rtable._addent( r.key, r )
            ElseIf FileType( rpath+f ) = 2 Then
                Local underling:Int = ReadDir( rpath+f+"/" )
                RBindResources( underling, cpath+f+"/", rpath+f+"/" )
            EndIf
        Catch o:Object
            DebugLog o.ToString( )
            Continue
        End Try
        f = NextFile( dir )
    Wend
End Function

Public

Function UnloadResources( )
    _rtable.Flush( )
    GCCollect( )
End Function

Function LoadResources( ipath$="./" )
    _rtable.Flush( )
    GCCollect( )
    ipath = ipath.Replace("\","/")
    Local dir:Int = ReadDir( ipath )
    RBindResources( dir, "/", ipath )
    CloseDir( dir )
End Function

Function GetResource:TBank( name$ )
    If name[0] &lt;&gt; "/" Then name = "/"+name
    Local key:Int = gHashKey( name )
    Local rs:IResource = IResource( _rtable._getent( key ) )
    If rs Then Return rs.AsBank( )
    Return Null
End Function</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
