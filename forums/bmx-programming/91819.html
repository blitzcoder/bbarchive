<!DOCTYPE html><html lang="en" ><head ><title >Creating a random Country map</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Creating a random Country map</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Creating a random Country map</a><br><br>
<a name="1045108"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am trying to think of the best way to create a random map of countries.  Sort of like the map on Risk, but I want the countries to be different every game.  Anybody have an idea on how to go about it? <br><br></td></tr></table><br>
<a name="1045119"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hmm, a Voronoi diagram might work.<br><a href="http://en.wikipedia.org/wiki/Voronoi_diagram" target="_blank">http://en.wikipedia.org/wiki/Voronoi_diagram</a><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

Graphics 800,600

Type TPoint
	Field x:Float
	Field y:Float
	Field red:Int
	Field green:Int
	Field blue:Int
End Type

Local List:TList = CreateList()

For Local i:Int = 0 To 30
	Local point:TPoint = New TPoint
	Point.x = Rand(100,700)
	Point.y = Rand(100,500)
	Point.Red = Rand(0,3)*(255.0/3.0)
	Point.Green = Rand(0,3)*(255.0/3.0)
	Point.Blue = Rand(0,3)*(255.0/3.0)
	List.AddLast(Point)
Next
Local Distance:Float = 1000.0
Local ClosestPoint:TPoint
For Local x:Int = 0 To 799
	For Local y:Int = 0 To 599
		For Local Point:TPoint = EachIn List
			Local DX:Float = Point.x - x
			Local DY:Float = Point.y - y
			Local D:Float = Sqr(DX*DX+DY*DY)
			If D &lt; Distance
				Distance = D
				ClosestPoint = Point
			End If
		Next
		SetColor ClosestPoint.Red,ClosestPoint.Green,ClosestPoint.Blue
		Plot x,y
		Distance = 1000.0
	Next
Next
Flip

WaitKey
</textarea><br>Just need to get rid of the cells near the edges and randomize the borders a bit. <br><br></td></tr></table><br>
<a name="1045123"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Experimenting some more, this code will randomize the borders a bit.  Just need to fill the edge cells with water and speed things up a bit and this should work nicely.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict
SeedRnd MilliSecs()

Graphics 800,480

Type TPoint
	Field x:Float
	Field y:Float
	Field red:Int
	Field green:Int
	Field blue:Int
End Type

Global List:TList = CreateList()

For Local i:Int = 0 To 30
	Local point:TPoint = New TPoint
	Point.x = Rand(0,799)
	Point.y = Rand(0,479)
	Point.Red = Rand(0,3)*(255.0/3.0)
	Point.Green = Rand(0,3)*(255.0/3.0)
	Point.Blue = Rand(0,3)*(255.0/3.0)
	List.AddLast(Point)
Next

For Local i:Int = 1 To 4
	Deeper()
Next
Local Distance:Float = 1000.0
Local ClosestPoint:TPoint
For Local x:Int = 0 To 799
	DebugLog x
	For Local y:Int = 0 To 479
		For Local Point:TPoint = EachIn List
			Local DX:Float = Point.x - x
			Local DY:Float = Point.y - y
			Local D:Float = Sqr(DX*DX+DY*DY)
			If D &lt; Distance
				Distance = D
				ClosestPoint = Point
			End If
		Next
		SetColor ClosestPoint.Red,ClosestPoint.Green,ClosestPoint.Blue
		Plot x,y
		Distance = 1000.0
	Next
Next
Flip

WaitKey
				
Function Deeper()
	For Local i:Int = 0 To 30
		Local NewPoint:TPoint = New TPoint
		NewPoint.x = Rand(100,700)
		NewPoint.y = Rand(100,380)
		Local Distance:Float = 1000
		Local Closest:TPoint
		For Local Point:TPoint = EachIn List
			Local DX:Float = Point.x - NewPoint.x
			Local DY:Float = Point.y = NewPoint.y
			Local D:Float = Sqr(DX*DX+DY*DY)
			If D &lt; Distance
				Distance = D
				Closest = Point
			End If
		Next
		NewPoint.Red = Closest.Red
		NewPoint.Green = Closest.Green
		NewPoint.Blue = Closest.Blue
		List.AddLast(NewPoint)
	Next
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1045129"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, my latest version.  Looks pretty good, but I need to figure out what to do about the pointy peninsulas that are created and also still need to make things faster.  Otherwise it looks nice.  Makes little continents and islands.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict
SeedRnd MilliSecs()

Graphics 800,480

Type TPoint
	Field x:Float
	Field y:Float
	Field red:Int
	Field green:Int
	Field blue:Int
End Type

Global List:TList = CreateList()

For Local i:Int = 0 To 30
	Local point:TPoint = New TPoint
	Point.x = Rand(0,799)
	Point.y = Rand(0,479)
	Point.Red = Rand(0,3)*(255.0/3.0)
	Point.Green = Rand(0,3)*(255.0/3.0)
	Point.Blue = Rand(0,3)*(255.0/3.0)
	List.AddLast(Point)
Next

For Local i:Int = 1 To 4
	Deeper()
Next
Local Distance:Float = 1000.0
Local ClosestPoint:TPoint
For Local x:Int = 0 To 799
	DebugLog x
	For Local y:Int = 0 To 479
		For Local Point:TPoint = EachIn List
			Local DX:Float = Point.x - x
			Local DY:Float = Point.y - y
			Local D:Float = Sqr(DX*DX+DY*DY)
			If D &lt; Distance
				Distance = D
				ClosestPoint = Point
			End If
		Next
		SetColor ClosestPoint.Red,ClosestPoint.Green,ClosestPoint.Blue
		Plot x,y
		Distance = 1000.0
	Next
Next
Local Image:TImage = CreateImage(800,480)
GrabImage(Image,0,0)
Local Pixmap:TPixmap = LockImage(Image)
For Local x:Int = 0 To 799
	DebugLog "Filling x = "+x
	Floodfill(Pixmap,x,0)
	floodfill(Pixmap,x,479)
Next
For Local y:Int = 0 To 479
	DebugLog "Filling y = "+y
	Floodfill(Pixmap,0,y)
	Floodfill(Pixmap,799,y)
Next
UnlockImage(Image)
DrawImage Image,0,0
Flip

WaitKey
				
Function Deeper()
	For Local i:Int = 0 To 30
		Local NewPoint:TPoint = New TPoint
		NewPoint.x = Rand(100,700)
		NewPoint.y = Rand(100,380)
		Local Distance:Float = 1000
		Local Closest:TPoint
		For Local Point:TPoint = EachIn List
			Local DX:Float = Point.x - NewPoint.x
			Local DY:Float = Point.y = NewPoint.y
			Local D:Float = Sqr(DX*DX+DY*DY)
			If D &lt; Distance
				Distance = D
				Closest = Point
			End If
		Next
		NewPoint.Red = Closest.Red
		NewPoint.Green = Closest.Green
		NewPoint.Blue = Closest.Blue
		List.AddLast(NewPoint)
	Next
End Function

Function FloodFill(Pixmap:TPixmap,x:Int,y:Int)
	Local FillColor:Int = ReadPixel(Pixmap,x,y) &amp; $FFFFFF
	If FillColor = $FF Then Return
	Local FillList:TList = CreateList()
	Local Point:TPoint = New TPoint
	FillList.AddLast(Point)
	Point.x = x
	point.y = y
	While FillList.Count()
		Point = TPoint(FillList.RemoveFirst())
		Local TestX:Int
		Local TestY:Int
		For Local Direction:Int = 0 To 3
			Select Direction
				Case 0
					TestX = Point.X
					TestY = Point.y - 1
				Case 1
					TestX = Point.X + 1
					TestY = Point.Y
				Case 2
					TestX = Point.x
					TestY = Point.Y+1
				Case 3
					TestX = Point.x - 1
					TestY = Point.y
			End Select
			If TestX &gt; 799 Or TestX &lt; 0 Or TestY &gt; 479 Or TestY &lt; 0 Then Continue
			Local Color:Int = ReadPixel(Pixmap,TestX,TestY) &amp; $FFFFFF
			If Color = $FF Or Color &lt;&gt; FillColor Then Continue
			WritePixel(Pixmap,TestX,TestY,$FF0000FF)
			Local NewPoint:TPoint = New TPoint
			NewPoint.X = TestX
			NewPoint.y = TestY
			FillList.AddLast(NewPoint)
		Next
	Wend
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1045162"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >xlsior</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Perhaps you can use the same principle as used by the 'game of life' algorithm -- those tend to cluster together, with some islands here and there. Possibly run several iterations and blend them together, to enable you to subdivide landmasses into multiple countries dynamically as well. <br><br></td></tr></table><br>
<a name="1045223"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> @xlsior.  Thought I'd give your idea a try.  After some tweeking, came up with some interesting landmasses.  Now to figure out how to subdivide into countries.<br>edit: Pressing space will iterate the creation phase again.  Escape will end the program.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict
SeedRnd MilliSecs()
Graphics 800,480

Const DotOdds:Int = 50
Const NeighborsBorn:Int = 6
Const NeighborsDie:Int = 2
Const LandWidth:Int = 20
Const LandHeight:Int = 12


Local Pixmap:TPixmap = CreatePixmap(LandWidth,LandHeight,PF_RGBA8888)
For Local x:Int = 0 Until LandWidth
	DebugLog "Filling.  X = "+x
	For Local y:Int = 0 Until LandHeight
		If Rand(0,100) &gt; DotOdds Then WritePixel(Pixmap,x,y,$FFFFFFFF)
	Next
Next
Local Key:Int
While Key &lt;&gt; KEY_ESCAPE
	Local TempPixmap:TPixmap = CopyPixmap(Pixmap)
	For Local x:Int = 0 Until LandWidth
		DebugLog "Creating. x = "+x
		For Local y:Int = 0 Until LandHeight
			Local Neighbors:Int = CountNeighbors(TempPixmap,x,y)
			If Neighbors &gt;= NeighborsBorn Then WritePixel(Pixmap,x,y,$FFFFFFFF)
			If Neighbors &lt;= NeighborsDie Then WritePixel(Pixmap,x,y,$FF000000)
		Next
	Next
	Local Image:TImage = LoadImage(Pixmap,0)
	Cls
	SetScale 800.0/LandWidth,480/LandHeight
	DrawImage Image,0,0
	Flip
	Key = WaitKey()
Wend

Function CountNeighbors:Int(Pixmap:TPixmap,x:Int,y:Int)
	Local Count:Int = 0
	Local DX:Int
	Local DY:Int
			For Local direction:Int = 0 To 7
				Select Direction
					Case 0
						If y = 0 Then Continue
						DX = X
						DY = Y-1
					Case 1
						If y = 0 Or x = Pixmap.Width-1 Then Continue
						DX = x + 1
						DY = Y - 1
					Case 2
						If x = Pixmap.Width-1 Then Continue
						DX = X+1
						DY = y
					Case 3
						If x = Pixmap.Width-1 Or y = Pixmap.Height - 1 Then Continue
						DX = x+1
						DY = Y+1
					Case 4
						If y = Pixmap.Height - 1 Then Continue
						DX = x
						DY = y + 1
					Case 5
						If y = Pixmap.Height - 1 Or x = 0 Then Continue
						DX = x - 1
						DY = y + 1
					Case 6
						If X = 0 Then Continue
						DX = x - 1
						DY = y
					Case 7
						If x = 0 Or y = 0 Then Continue
						DX = x - 1
						DY = y - 1
				End Select
				If ReadPixel(Pixmap,DX,DY) &amp; $FFFFFF &gt; 0 Then Count :+ 1
			Next
	Return Count
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1045234"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> the previous one produces an interesting map, the latest one just produces blank and white squares <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
