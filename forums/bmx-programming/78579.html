<!DOCTYPE html><html lang="en" ><head ><title >2d world with zoom ?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >2d world with zoom ?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >2d world with zoom ?</a><br><br>
<a name="881691"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> has anyone got an idea how I would create a playing field like the battle part of star control?<br><br>2d with zoomable point of view,<br><br>I tried using just setscale but the 'camera' is fixed at the top left of the playing area <br><br></td></tr></table><br>
<a name="881705"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warpy</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> From my 'how to do things' folder:<br><br><pre class=code>
Global gwidth#,gheight#

Global panx#=0,pany#=0,zoom#=1,tzoom#=zoom





Function transformpoly#[](poly#[] , px# , py# , an# , scale# = 1) 

	'px, py: translate

	'an: rotate

	'scale: duhhh thicko

	

	Local opoly#[Len(poly)]

	

	can# = Cos(an)

	san#=Sin(an)

	For n=0 To Len(poly)-1 Step 2

		x#=poly[n]*scale

		y#=poly[n+1]*scale

		opoly[n]=x*Can-y*San+px

		opoly[n+1]=x*San+y*Can+py

	Next

	Return opoly

End Function



Function zoompoly#[](poly#[])

	Local opoly#[Len(poly)]

	For n=0 To Len(poly)-1 Step 2

		opoly[n]=zoomx(poly[n])

		opoly[n+1]=zoomy(poly[n+1])

	Next

	Return opoly

End Function



Function drawrotatedline#(x1# , y1# , x2# , y2# , px# , py# , an# , scale# = 1)

	'px,py: translate

	'an: rotate

	'scale: duhhh thicko

	

	can# = Cos(an)

	san# = Sin(an)

	

	nx1#=(x1*can-y1*san)*scale+px

	ny1#=(x1*san+y1*can)*scale+py

	nx2#=(x2*can-y2*san)*scale+px

	ny2#=(x2*san+y2*can)*scale+py

	DrawLine nx1,ny1,nx2,ny2

End Function



Function drawoutline(poly#[],thickness=1)

	'DrawPoly poly

	SetLineWidth thickness

	l = Len(poly)

	For n=0 To l-3 Step 2

		x1#=poly[n]

		y1#=poly[n+1]

		x2#=poly[(n+2) Mod l]

		y2#=poly[(n+3) Mod l]

		DrawZoomLine x1,y1,x2,y2

	Next

	SetLineWidth 1

End Function





Function ZoomX#(x#)

	Return (x - panx) * zoom + gwidth / 2

End Function

Function ZoomY#(y#)

	Return (y - pany) * zoom + gheight / 2

End Function



Function UnzoomX#(x#)

	Return (x - gwidth / 2) / zoom + panx

End Function

Function UnzoomY#(y#)

	Return (y - gheight / 2) / zoom + pany

End Function



Function DrawZoomPoly(poly#[],outline=False)

	poly=poly[..]

	While i &lt; Len(poly)

		poly[i] = zoomx(poly[i])

		poly[i + 1] = zoomy(poly[i + 1]) 

		i:+ 2

	Wend

	If outline

		ox# = poly[0]

		oy# = poly[1]

		i = 2

		While i &lt; Len(poly)

			DrawLine ox , oy , poly[i] , poly[i + 1]

			ox = poly[i]

			oy=poly[i+1]

			i:+ 2

			DrawLine poly[0],poly[1],ox,oy

		Wend

	Else

		DrawPoly poly

	EndIf

End Function



Function DrawZoomLine(ax# , ay# , bx# , by#)

	ax = zoomx(ax)

	ay = zoomy(ay)

	bx = zoomx(bx)

	by = zoomy(by)

	DrawLine ax,ay,bx,by

End Function



Function DrawZoomRect(x# , y# , width# , height#,zoomdimensions=1,filled=1)

	x = zoomx(x)

	y = zoomy(y)

	If zoomdimensions

		width:* zoom

		height:* zoom

	EndIf

	If filled

		DrawRect x , y , width , height

	Else

		DrawLine x , y , x + width , y

		DrawLine x + width , y , x + width , y + height

		DrawLine x , y , x , y + height

		DrawLine x , y + height , x + width , y + height

	EndIf

End Function



Function DrawZoomCircle(x# , y# , radius#)

	x = zoomx(x) 

	y = zoomy(y)

	radius:* zoom

	DrawOval x - radius , y - radius , 2 * radius , 2 * radius

End Function



Function DrawZoomText(txt$ , x# , y#)

	x = ZoomX(x)

	y = ZoomY(y)

	DrawText txt , x , y

End Function



Function DrawZoomImage(image:TImage , x# , y#,width#,heighto=0)

	If heighto

		w# = width / ImageHeight(image)

	Else

		w# = width / ImageWidth(image)

	EndIf

	SetScale w*zoom , w*zoom

	DrawImage image , zoomx(x) , zoomy(y)

	SetScale 1,1

End Function





'graphics init

Function initgfx()

	Graphics gwidth , gheight

End Function
</pre><br><br>Seems I haven't got round to putting this in the code archives yet... I use it all the time, so I'll add it now. <br><br></td></tr></table><br>
<a name="881790"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Digital Anime</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I just posted this small example somewhere else :<br><br><a href="http://server.dedicatedservices.org/upload/zip/retro.zip" target="_blank">http://server.dedicatedservices.org/upload/zip/retro.zip</a><br><br>Use cursor keys to move UFO,<br>and other keys to move stretch and rotate it all.<br><br>Might give an idea on how it works, even tho I think it could be written better. <br><br></td></tr></table><br>
<a name="881795"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> thanks guys ill check em out <br><br></td></tr></table><br>
<a name="881832"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Will</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> This thing is what I use for offsetting, rotating and zooming the view in my games.<br><br><br><br><pre class=code>
'View Orientations

SetGraphicsDriver(GLMax2DDriver()) 'make sure we are in openGL as these only work for OGL


Type ViewOrientation
	Global initialized
	
	Global current:viewOrientation
	
	Field selfInitialized
	
	Field leftEdge#
	Field rightEdge#
	Field bottomEdge#
	Field topEdge#
	
	Field XPos#, YPos#, Zoom#=1, Rotation#=0
	Field vpSavedX#, vpSavedY#
	
	Global realwidth#
	Global realheight#
	
	Global vpX, vpY
	Global currentWidth#
	Global currentHeight#
	
	Global naturalZoom#
	
	Function init()
		If realwidth &lt;&gt; GraphicsWidth() Or realheight &lt;&gt; GraphicsHeight() Or (initialized = False And GraphicsHeight() &lt;&gt; 0) Then
			initialized = True
			naturalZoom = GraphicsHeight() / 768.0
			realwidth = GraphicsWidth()
			realheight = GraphicsHeight()
			currentWidth = realWidth
			currentHeight = realHeight
		End If
	End Function
	
	Function clear:viewOrientation()
		nv:viewOrientation = New vieworientation
		nv.apply()
		Return nv
	End Function
	
	Method New()
		init()
		selfInit()
	End Method
	
	Method selfInit()
		If selfInitialized = True Or GraphicsHeight() = 0 Then Return
		
		selfInitialized = True
		
		leftEdge = 0
		rightEdge = GraphicsWidth()
		topedge = 0
		bottomEdge = GraphicsHeight()
		
		XPos = GraphicsWidth()/2
		YPos = GraphicsHeight()/2
	End Method
	
	Method move(x#, y#, rot#=0)
		setXYZoomRot(XPos + x, Ypos + y, Zoom, Rotation + rot)
	End Method
	
	Method setXY(x#, y#)
		setX(x); setY(y)
	End Method
	
	Method setXYZoomRot(x#, y#, Z#, R#)
		setX(x); setY(y)
		setZoom(Z); SetRotation(R)
	End Method
	
	Method setXYH(x#, y#, h#)
		setZoom(realheight / h)
		setxy(x + (rightEdge-leftEdge)*.5, y + (bottomEdge-topedge)*.5)
	End Method
	
	Method setX(x#)
		XPos = x
		calculate()
	End Method
	
	Method setY(y#)
		YPos = y
		calculate()
	End Method
	
	Method setZoom(Z#)
		Zoom = Z
		calculate()
	End Method
	
	Method SetRotation(rot#)
		Rotataion = rot
		calculate()
	End Method
	
	Method Apply()
		selfInit()
		init()
		
		calculate()
		
		glMatrixMode(GL_PROJECTION)
		glLoadIdentity()
		gluOrtho2D(LeftEdge, rightEdge, bottomEdge, TopEdge)
		glMatrixMode(GL_MODELVIEW)
		glLoadIdentity()
		glTranslateF(xpos,ypos,0)
		glrotateF(Rotation, 0, 0, 1)
		glTranslateF(-xpos,-ypos,0)
		
		current = Self
		
	End Method
	
	Method calculate()
		selfInit()
		
		realZoom# = Zoom
		
		vpSavedX = vpX
		vpSavedY = vpY

		If realZoom = 0 Then Zoom = .00001
		LeftEdge = xpos - ((currentWidth / Zoom) / 2)
		rightEdge = xpos + ((currentWidth / Zoom) / 2)
		topEdge = ypos - ((currentHeight / Zoom) / 2)
		bottomEdge = ypos + ((currentHeight / Zoom) / 2)		
	End Method
	
	Method screenSpaceToWorldSpace(wx# Var, wy# Var)
		wx:-vpSavedX
		wy:-vpSavedY
		wx:/zoom
		wy:/zoom
		wx:+leftEdge
		wy:+topEdge
	End Method
	
	Method worldSpaceToScreenSpace(wx# Var, wy# Var)
		wx:-leftEdge
		wy:-topedge
		wx:*zoom
		wy:*zoom
		wx:+vpSavedX
		wy:+vpSavedY
	End Method
	
	Method onScreen(wx#, wy#, radius#)
		wx:-leftEdge
		wy:-topedge
		wx:*zoom
		wy:*zoom
		radius:*zoom
		
		If wy + radius &lt; 0 Or wy - radius &gt; realheight Then Return 0
		If wx + radius &lt; 0 Or wx - radius &gt; realwidth Then Return 0
		
		Return 1
	End Method
	
	Function SetViewport(x,y,width,height)
		GLViewPort(x, GraphicsHeight() - y - height, width, height)
		
		vpX = x; vpY = y
		currentHeight = height
		currentWidth = width
		
		If current &lt;&gt; Null Then current.apply()
		
	End Function
	
	Function ClearViewport()
		ViewOrientation.SetViewport(0,0,GraphicsWidth(), GraphicsHeight())
		currentWidth = realWidth
		currentHeight = realheight
	End Function
	
End Type
</pre> <br><br></td></tr></table><br>
<a name="881982"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Can't you use SetOrigin to change the center of rotation? <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
