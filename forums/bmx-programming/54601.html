<!DOCTYPE html><html lang="en" ><head ><title >Ini-File functions?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Ini-File functions?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Ini-File functions?</a><br><br>
<a name="608975"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi!<br><br>Has someone created a set of Ini-File functions for bmx already? <br><br>So that one can easily manage strings such as.<br>Fullscreen=True<br>Set_Resolution=800x600<br><br>Could not find them in the archives. <br><br></td></tr></table><br>
<a name="608996"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Who was John Galt?</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Someone has put together a set of XML functions that would be great for this type of thing. They have been spoken about recently so take a look at recent posts. <br><br></td></tr></table><br>
<a name="608998"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> xml? wouldn't that be overkill? <br><br></td></tr></table><br>
<a name="608999"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Paul "Taiphoz"</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Id say yeah overkill lol.<br><br>Best to code it and configure it to your needs, just parse out the commands you after like (ScreenRes=) etc and grap the info you need.<br><br>Taking pre done functions from the community will mean having to use their tag's and that might not fit with what your doing. then again you could just edit the code lol.. <br><br></td></tr></table><br>
<a name="609005"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Diablo</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> heres what i did for my gui:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Strict   

' Test out some functions
Local myTestFile:TStream = ReadFile("testini.ini")

Local bExit:Byte
While bExit = False
	Select Upper(HiReadTag(myTestFile))
		Case "START"
			bExit = False
		Case "SOME VARS"
			Local myInt% = HiReadInt(myTestFile)
			Print myInt
			Local myByte:Byte = HiReadByte(myTestFile)
			Print myByte 
			Local myFloat# = HiReadFloat(myTestFile)
			Print myFloat   
			Local myDouble! = HiReadDouble(myTestFile)
			Print myDouble     
			Local myString$ = HiReadString(myTestFile)
			Print myString   
		Case "SOME ARRAYS"
			Local myIntAr:Int[] 
			myIntAr = HiReadIntArray(myTestFile)
			For Local myInt% = EachIn myIntAr
				Print myInt
			Next 
			Local myByteAr:Byte[] 
			myByteAr = HiReadByteArray(myTestFile)
			For Local myByte:Byte = EachIn myByteAr
				Print myByte
			Next 
			Local myFloatAr#[] 
			myfloatAr = HiReadFloatArray(myTestFile)
			For Local myfloat# = EachIn myfloatAr
				Print myfloat
			Next 
			Local myDoubleAr![] 
			myDoubleAr = HiReadDoubleArray(myTestFile)
			For Local myDouble! = EachIn myDoubleAr
				Print myDouble
			Next
			Local myStringAr$[] 
			myStringAr = HiReadStringArray(myTestFile)
			For Local myString$ = EachIn myStringAr
				Print myString
			Next
		Case "END"
			bExit = True
	End Select
Wend

CloseFile(myTestFile)

' HiReadTag(kFile:TStream)
' =======================
Function HiReadTag:String(kFile:TStream = Null)
	Local sLine:String = ReadLine(kFile)
	Local sTag:String
	Local iPos

	If Mid(sLine, 1, 1) = "[" Then
		For iPos = 2 To Len(sLine) - 1
			sTag:+ Mid(sLine, iPos, 1)
		Next
	EndIf
	Return sTag
End Function

' HiReadIntArray(kFile:TStream)
' ============================
Function HiReadIntArray:Int[](kFile:TStream = Null)
	Local sLine:String = ReadLine(kFile)
	Local iVarSize = 1
	Local sVarSize:String = ""
	Local sVar:String = ""
	
	Local bReadSize:Byte = False
	Local iPos:Int, iVarReadStart:Int
	For iPos = 1 To Len(sLine)
		If bReadSize = True Then
			If Mid(sLine, iPos, 1) &lt;&gt; "]" Then 
				sVarSize:+ Mid(sLine, iPos, 1)
			Else
				iVarReadStart = iPos + 3
				Exit
			EndIf
		End If
		If Mid(sLine, iPos, 1) = "[" Then
			bReadSize = True
		End If
	Next
	iVarSize = Int(sVarSize)
	Local iVar:Int[iVarSize] 
	
	Local iCount:Int
	For iPos = iVarReadStart To Len(sLine)
		If Mid(sLine, iPos, 1) &lt;&gt; "," Then
			sVar:+ Mid(sLine, iPos, 1)
		Else
			iVar[iCount] = Int(sVar)
			sVar = ""
			iCount:+ 1
		End If
	Next
	iVar[iCount] = Int(sVar)
	sVar = ""

	Return iVar
End Function      

' HiReadByteArray(kFile:TStream)
' ============================
Function HiReadByteArray:Byte[](kFile:TStream = Null)
	Local sLine:String = ReadLine(kFile)
	Local iVarSize = 1
	Local sVarSize:String = ""
	Local sVar:String = ""
	
	Local bReadSize:Byte = False
	Local iPos:Int, iVarReadStart:Int
	For iPos = 1 To Len(sLine)
		If bReadSize = True Then
			If Mid(sLine, iPos, 1) &lt;&gt; "]" Then 
				sVarSize:+ Mid(sLine, iPos, 1)
			Else
				iVarReadStart = iPos + 3
				Exit
			EndIf
		End If
		If Mid(sLine, iPos, 1) = "[" Then
			bReadSize = True
		End If
	Next
	iVarSize = Int(sVarSize)
	Local iVar:Byte[iVarSize]
	
	Local iCount:Int
	For iPos = iVarReadStart To Len(sLine)
		If Mid(sLine, iPos, 1) &lt;&gt; "," Then
			sVar:+ Mid(sLine, iPos, 1)
		Else
			iVar[iCount] = Byte(sVar)
			sVar = ""
			iCount:+ 1
		End If
	Next
	iVar[iCount] = Byte(sVar)
	sVar = ""
	
	Return iVar
End Function     

' HiReadFloatArray(kFile:TStream)
' ============================
Function HiReadFloatArray:Float[](kFile:TStream = Null)
	Local sLine:String = ReadLine(kFile)
	Local iVarSize = 1
	Local sVarSize:String = ""
	Local sVar:String = ""
	
	Local bReadSize:Byte = False
	Local iPos:Int, iVarReadStart:Int
	For iPos = 1 To Len(sLine)
		If bReadSize = True Then
			If Mid(sLine, iPos, 1) &lt;&gt; "]" Then 
				sVarSize:+ Mid(sLine, iPos, 1)
			Else
				iVarReadStart = iPos + 3
				Exit
			EndIf
		End If
		If Mid(sLine, iPos, 1) = "[" Then
			bReadSize = True
		End If
	Next
	iVarSize = Int(sVarSize)
	Local iVar:Float[iVarSize]
	
	Local iCount:Int
	For iPos = iVarReadStart To Len(sLine)
		If Mid(sLine, iPos, 1) &lt;&gt; "," Then
			sVar:+ Mid(sLine, iPos, 1)
		Else
			iVar[iCount] = Float(sVar)
			sVar = ""
			iCount:+ 1
		End If
	Next
	iVar[iCount] = Float(sVar)
	sVar = ""
	
	Return iVar
End Function   

' HiReadDoubleArray(kFile:TStream)
' ============================
Function HiReadDoubleArray:Double[](kFile:TStream = Null)
	Local sLine:String = ReadLine(kFile)
	Local iVarSize = 1
	Local sVarSize:String = ""
	Local sVar:String = ""
	
	Local bReadSize:Byte = False
	Local iPos:Int, iVarReadStart:Int
	For iPos = 1 To Len(sLine)
		If bReadSize = True Then
			If Mid(sLine, iPos, 1) &lt;&gt; "]" Then 
				sVarSize:+ Mid(sLine, iPos, 1)
			Else
				iVarReadStart = iPos + 3
				Exit
			EndIf
		End If
		If Mid(sLine, iPos, 1) = "[" Then
			bReadSize = True
		End If
	Next
	iVarSize = Int(sVarSize)
	Local iVar:Double[iVarSize]
	
	Local iCount:Int
	For iPos = iVarReadStart To Len(sLine)
		If Mid(sLine, iPos, 1) &lt;&gt; "," Then
			sVar:+ Mid(sLine, iPos, 1)
		Else
			iVar[iCount] = Double(sVar)
			sVar = ""
			iCount:+ 1
		End If
	Next
	iVar[iCount] = Double(sVar)
	sVar = ""
	
	Return iVar
End Function  

Function HiReadStringArray:String[](kFile:TStream = Null)
	Local sLine:String = ReadLine(kFile)
	Local iVarSize = 1
	Local sVarSize:String = ""
	Local sVar:String = ""
	
	Local bReadSize:Byte = False
	Local iPos:Int, iVarReadStart:Int
	For iPos = 1 To Len(sLine)
		If bReadSize = True Then
			If Mid(sLine, iPos, 1) &lt;&gt; "]" Then 
				sVarSize:+ Mid(sLine, iPos, 1)
			Else
				iVarReadStart = iPos + 3
				Exit
			EndIf
		End If
		If Mid(sLine, iPos, 1) = "[" Then
			bReadSize = True
		End If
	Next
	iVarSize = Int(sVarSize)
	Local iVar:String[iVarSize]
	
	Local iCount:Int
	For iPos = iVarReadStart To Len(sLine)
		If Mid(sLine, iPos, 1) &lt;&gt; "," Then
			sVar:+ Mid(sLine, iPos, 1)
		Else
			iVar[iCount] = String(sVar)
			sVar = ""
			iCount:+ 1
		End If
	Next
	iVar[iCount] = String(sVar)
	sVar = ""
	
	Return iVar
End Function

' HiReadInt(kFile:TStream)
' ============================
Function HiReadInt:Int(kFile:TStream = Null)
	Local sLine:String = ReadLine(kFile)
	Local sVar:String = ""
	
	Local bReadSize:Byte = False
	Local iPos:Int, iVarReadStart:Int
	Local iVar:Int
	For iPos = 1 To Len(sLine)
		If Mid(sLine, iPos, 1) = "=" Then 
			iVarReadStart = iPos + 2
		End If
	Next
	
	For iPos = iVarReadStart To Len(sLine)
		sVar:+ Mid(sLine, iPos, 1)
	Next
	iVar = Int(sVar)
	sVar = ""
	
	Return iVar
End Function   

' HiReadByte(kFile:TStream)
' ============================
Function HiReadByte:Byte(kFile:TStream = Null)
	Local sLine:String = ReadLine(kFile)
	Local sVar:String = ""
	
	Local bReadSize:Byte = False
	Local iPos:Int, iVarReadStart:Int
	Local iVar:Byte
	For iPos = 1 To Len(sLine)
		If Mid(sLine, iPos, 1) = "=" Then 
			iVarReadStart = iPos + 2
		End If
	Next
	
	For iPos = iVarReadStart To Len(sLine)
		sVar:+ Mid(sLine, iPos, 1)
	Next
	iVar = Byte(sVar)
	sVar = ""
	
	Return iVar
End Function 

' HiReadFloat(kFile:TStream)
' ============================
Function HiReadFloat:Float(kFile:TStream = Null)
	Local sLine:String = ReadLine(kFile)
	Local sVar:String = ""
	
	Local bReadSize:Byte = False
	Local iPos:Int, iVarReadStart:Int
	Local iVar:Float
	For iPos = 1 To Len(sLine)
		If Mid(sLine, iPos, 1) = "=" Then 
			iVarReadStart = iPos + 2
		End If
	Next
	
	For iPos = iVarReadStart To Len(sLine)
		sVar:+ Mid(sLine, iPos, 1)
	Next
	iVar = Float(sVar)
	sVar = ""
	
	Return iVar
End Function 

' HiReadDouble(kFile:TStream)
' ============================
Function HiReadDouble:Double(kFile:TStream = Null)
	Local sLine:String = ReadLine(kFile)
	Local sVar:String = ""
	
	Local bReadSize:Byte = False
	Local iPos:Int, iVarReadStart:Int
	Local iVar:Double
	For iPos = 1 To Len(sLine)
		If Mid(sLine, iPos, 1) = "=" Then 
			iVarReadStart = iPos + 2
		End If
	Next
	
	For iPos = iVarReadStart To Len(sLine)
		sVar:+ Mid(sLine, iPos, 1)
	Next
	iVar = Double(sVar)
	sVar = ""
	
	Return iVar
End Function

' HiReadString(kFile:TStream)
' ============================
Function HiReadString:String(kFile:TStream = Null)
	Local sLine:String = ReadLine(kFile)
	Local sVar:String = ""
	
	Local bReadSize:Byte = False
	Local iPos:Int, iVarReadStart:Int
	For iPos = 1 To Len(sLine)
		If Mid(sLine, iPos, 1) = "=" Then 
			iVarReadStart = iPos + 2
		End If
	Next
	
	For iPos = iVarReadStart To Len(sLine)
		sVar:+ Mid(sLine, iPos, 1)
	Next
	
	Return sVar
End Function</textarea><br><br><div class="quote"> testini.ini <br></div><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">[start]
[some vars]
myint= 1024
mybyte= 7
myfloat= 4.7
mydouble= 99.92
mystring= a string in an ini file

[some arrays]
myintarray[10]= 1,2,3,4,5,6,7,8,9,10
mybytearray[10]= 1,2,3,4,5,6,7,8,9,10
myfloatarray[10]= 1.5,2.5,3.5,4.5,5.5,6.5,7.5,8.5,9.5,10.5
mydoublearray[10]= 1.07,2.07,3.07,4.07,5.07,6.07,7.07,8.07,9.07,10.07
mystringarray[10]= a1,b2,c3,d4,e5,f6,g7,h8,i9,j10
[end]</textarea><br><br>i'm have trouble with the arrays at the mo but once thats fixed it'll be done. and bmax doesnt like float/double ethier.<br><br>Edit - Fixed it now. It didn't cause of how i was going through the file. i forgot i made it easy to read the file in one go.<br><br>Edit 2 - you'll notice that you need a start and end tag. I put this in to make easy entry/exit code<br><br>enjoy. <br><br></td></tr></table><br>
<a name="609009"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >d-bug</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> ...and another simple ini module. It can't handle arrays like the one from Diablo but does it's job quite good.<br><br><pre class=code>Module pub.ini

ModuleInfo "Version : 1.00"
ModuleInfo "Author  : d:bug / Future-Coding"
ModuleInfo "License : Public Domain"

ModuleInfo "History : 1.01 Module does not longer need BRL.Retro"
ModuleInfo "History : 1.00 first released version"

Import BRL.FileSystem

Type TIni

	Field path:String
	Field line:String[1]
	Field linecount:Int=0


	Function Create:TIni ()
		Local ini:TIni = New TIni
		Return ini
	End Function
	
	
	Function Read:TIni (inipath:String)
		If FileType (inipath) Then
			Local ini:TIni = New TIni
			Local stream:TStream = ReadFile (inipath)
			While Not stream.EOF()
				ini.linecount :+1
				ini.line = ini.line[..(ini.linecount+1)]
				ini.line[ini.linecount] = stream.ReadLine ()
			Wend
			stream.close
			Return ini
		Else
			DebugLog "Ini.mod / Read : file "+inipath+" not found"
			Return Null
		EndIf
	End Function


	Method Write ()
		Local stream:TStream = WriteFile(path)
		For Local i:Int = 0 To linecount
			stream.WriteLine (line[i])
		Next
		stream.close
	End Method



	Method GetSeek:Int (group:String="")
		If group = "" Then Return -1
		If group.find("[") = -1 Then group = "[" + group
		If group.find("]") = -1 Then group = group + "]"
		For Local i:Int = 0 To linecount
			If line[i].ToLower() = Group.ToLower() Then
				Return i + 1
			EndIf
		Next
		DebugLog "Ini.mod / GetSeek : group &lt;"+group+"&gt; not found"
		Return -1
	End Method
	

	Method GetGroup:String (seek:Int=-1)
		If (seek &gt;= 0) And (seek &lt;= linecount)
			If line[seek][..1] = "[" Return line[i]
		EndIf
		DebugLog "Ini.mod / GetGroup : seek &lt;"+seek+"&gt; not found"
		Return ""			
	End Method

	
	Method SetGroup:Int (group:String)
		If group.find("[") = -1 Then group = "[" + group
		If group.find("]") = -1 Then group = group + "]"
		Local seek:Int = GetSeek (group)
		If seek = -1
			line[linecount] = group
			linecount:+1
			Return linecount
		Else
			line[seek] = group
			Return seek
		EndIf
		DebugLog "Ini.mod / SetGroup : unhandled error building group &lt;"+group+"&gt;"
		Return -1
	End Method


	Method GetValue:String (seek:Int,keyword:String,defaultvalue:String="")
		If seek = -1 Return defaultvalue
		For Local i:Int = seek To linecount
			If line[i][..1] = "[" Then Return defaultvalue
			If (line[i][..1] &lt;&gt; "#") And (line[i][..1] &lt;&gt; "") 
				Local split:Int = line[i].find("=")
				Local key:String = (line[i][..split]).Replace(" ","")
				Local value:String = line[i][split+1..]
				If key.ToLower() = keyword.ToLower()
					While value[..1] = " "
						value = value[1..]
					Wend	
					Return value
				EndIf
			EndIf
		Next
		DebugLog "Ini.mod / GetValue : keyword &lt;"+keyword+"&gt; not found"
		Return defaultvalue
	End Method



	Method SetValue:Byte (seek:Int,keyword:String,value:String)
		If seek = -1 Return defaultvalue
		For Local i:Int = seek To linecount
			If line[i][..1] = "[" Then Return False
			If (line[i][..1] &lt;&gt; "#") And (line[i][..1] &lt;&gt; "") 
				Local split:Int = line[i].find("=")
				Local key:String = (line[i][..split]).Replace(" ","")
				If key.ToLower() = keyword.ToLower()
					line[i] = key + " = " + value
					Return True
				EndIf
			EndIf
		Next
		DebugLog "Ini.mod / SetValue : keyword &lt;"+keyword+"&gt; not found"
		Return True		
	End Method

End Type


'example
Rem
Graphics 640,480,0

local ini:tini = tini.Read ("blub.ini")
local seek:Int = ini.GetSeek ("[TEST]")

Repeat
	Cls
	DrawText (ini.GetValue(seek,"Blub","Muhahahahaha"),10,20)
	DrawText (ini.GetValue(seek,"Bla","Dummes Gelache"),10,40)
	
	If KeyHit (KEY_F1)
		local boing:Byte = ini.SetValue(seek,"Blub","Huhu")
		If boing = True
			ini.Write()
		Else
			Print "moep"
		EndIf
	EndIf
	
	Flip
Until KeyHit(KEY_ESCAPE)

End Rem</pre><br><br>This one reads the Ini into memory. You could manipulate it from there and write it back to disk. Simple stuff without any help. <br><br></td></tr></table><br>
<a name="609065"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >deps</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> http://www.blitzwiki.org/index.php/Category:WB_Modules_%28Module_Collection%29<br><br>Wb.simpleconfig seemes nice. <br><br></td></tr></table><br>
<a name="609099"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GW</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here is one that i use in B3D that I got from Blitzcoder many moon ago. This is the bMax version. <br><br><pre class=code>
Include "bbtype.bmx"
Include "bbvkey.bmx"

Global INI_Type_list:TList=New TList

' Title:	INI file manipulation functions 
' Version:	2.1
' Revised:	September 28 2003
' Author:	Leigh Bowers 

' Email:	leigh.bowers@... 
' Web:		<a href="http://www.curvesoftware.co.uk/blitz" target="_blank">http://www.curvesoftware.co.uk/blitz</a> 

' History: 

' 2.1		Fixed possible problem with locating keys.
' 2.01		Removed superfluous CR LF from file.
' 2.00		Redesigned and rewritten.

' Example usage: 

'Include "Ini2.1.bb"
'
'INI_OpenFile("Archon.ini")
'strValue$ = INI_ReadValue("Config", "Mode", "C64") 
'INI_WriteValue("Config", "Mode", "Atari")
'If Not(INI_CloseFile()) Then
' 	Print "Unable to commit INI file changes to file (" + INI_CurrentFile\strFilename + ")."
'End If

Type bbINI_Type Extends TBBType

	Method New()
		Add(INI_Type_list)
	End Method

	Method After:bbINI_Type()
		Local t:TLink
		t=_link.NextLink()
		If t Return bbINI_Type(t.Value())
	End Method

	Method Before:bbINI_Type()
		Local t:TLink
		t=_link.PrevLink()
		If t Return bbINI_Type(t.Value())
	End Method

	Field strFilename$ = ""
	Field blnModified% = False
	Field strContents$ = ""
End Type
Global INI_CurrentFile:bbINI_Type = New bbINI_Type

' Core functions

Function INI_OpenFile(INI_strFilename$)

' Description:	Open the specified INI file for manipulation (if said file exists).
' Returns:		[] n/a
	Local INI_lngFileHandle:tstream
	
	INI_CurrentFile.strFilename = Trim(INI_strFilename) ; If Right(Upper(INI_strFilename), 4) &lt;&gt; ".INI" Then INI_CurrentFile.strFilename = INI_CurrentFile.strFilename + ".ini"
	INI_CurrentFile.blnModified = False
	INI_CurrentFile.strContents = ""

	INI_lngFileHandle = ReadFile(CurrentDir$() + "\"  + INI_CurrentFile.strFilename)
	If INI_lngFileHandle &lt;&gt; Null Then
		While Not(Eof(INI_lngFileHandle))
			INI_CurrentFile.strContents = INI_CurrentFile.strContents + ReadLine$(INI_lngFileHandle) + Chr$($0D) + Chr$($0A)
		Wend
		CloseStream INI_lngFileHandle
		Return True
	Else
		Return False
	End If

End Function

Function INI_CloseFile%()

' Description:	If INI modifications have been made (via INI_WriteValue), they are saved back to the original INI file.
' Returns:		[boolean] True (success) or False (unable to save modifications back to the INI file, where applicable)

	Local INI_blnResult% = True

	If (INI_CurrentFile.blnModified = True) Then
		' Commit internal changes back to the INI file
		Local INI_lngFileHandle:tstream = WriteFile(CurrentDir$() + "\" + INI_CurrentFile.strFilename)
		If INI_lngFileHandle &lt;&gt; Null Then
			WriteLine INI_lngFileHandle, Left$(INI_CurrentFile.strContents, Len(INI_CurrentFile.strContents) - 2)
			CloseFile INI_lngFileHandle
			INI_CurrentFile.blnModified = False
		Else
			INI_blnResult = False ' Create file failed
		End If
	End If
	
	Return INI_blnResult

End Function

Function INI_ReadValue$(INI_strSection$, INI_strKey$, INI_strDefault$ = "")

' Description:	Retrieve the INI file value for the specified INI Section/Key combination.
' Returns:		[string] INI Value - The optional INI_strDefault value is returned if no SECTION/KEY combination is found

	INI_strSection = "[" + Upper$(Trim$(INI_strSection)) + "]"
	INI_strKey = Upper$(Trim$(INI_strKey))

	Local INI_strUpperContents$ = Upper$(INI_CurrentFile.strContents)

' Locate the SECTION, KEY and VALUE

	Local INI_strValue$ = ""

Local INI_lngSectionPos% = Instr(INI_strUpperContents, INI_strSection)
	If (INI_lngSectionPos &lt;&gt; 0) Then
		Local INI_lngKeyPos% = Instr(INI_strUpperContents, INI_strKey, (INI_lngSectionPos + Len(INI_strSection) + 1))
		If (INI_lngKeyPos &lt;&gt; 0) Then
			Local INI_NextSection% = Instr(INI_strUpperContents, "[", (INI_lngSectionPos + Len(INI_strSection) + 1))
			If (INI_NextSection = 0) Or (INI_lngKeyPos &lt; INI_NextSection) Then
				Local INI_lngStartPos% = Instr(INI_strUpperContents, "=", (INI_lngKeyPos + 1))
				If (INI_lngStartPos &lt;&gt; 0) Then
					Local INI_lngEndPos% = Instr(INI_strUpperContents, Chr$($0D), (INI_lngStartPos + 1))
					If (INI_lngEndPos &lt;&gt; 0) Then
						' We have located the required INI key and it's corresponding value
						INI_strValue = Trim$(Mid$(INI_CurrentFile.strContents, INI_lngStartPos + 1, (INI_lngEndPos - INI_lngStartPos - 1)))
					End If
				End If
			End If
		End If
	End If

' Return the appropriate value

	If (INI_strValue &lt;&gt; "") Then Return INI_strValue Else Return INI_strDefault

End Function

Function INI_WriteValue(INI_strSection$, INI_strKey$, INI_strValue$)

' Description:	Add/update key values within the current INI file (internally).
'				Note: Changes are not committed back To file until you explicitly call INI_CloseFile.
' Returns:		[] n/a

	INI_strSection = "[" + Trim$(INI_strSection) + "]"
	Local INI_strUpperSection$ = Upper$(INI_strSection)
	INI_strKey = Trim$(INI_strKey)
	INI_strValue = Trim$(INI_strValue)

' (Re)Create the INI contents updating/adding the SECTION, KEY and VALUE

	Local INI_blnWrittenKey% = False
	Local INI_blnSectionFound% = False
	Local INI_strCurrentSection$ = ""

	Local INI_strTempContents$ = INI_CurrentFile.strContents
	INI_CurrentFile.strContents = ""

	Local INI_lngOldPos% = 1
	Local INI_lngPos% = Instr(INI_strTempContents, Chr$($0D))
	
	While (INI_lngPos &lt;&gt; 0)

		Local INI_strTemp$ =Trim$(Mid$(INI_strTempContents, INI_lngOldPos, (INI_lngPos - INI_lngOldPos)))
		
		If (INI_strTemp &lt;&gt; "") Then
			If Left$(INI_strTemp, 1) = "[" And Right$(INI_strTemp, 1) = "]" Then
				' Process SECTION
				If (INI_strCurrentSection = INI_strUpperSection) And (INI_blnWrittenKey = False) Then
					INI_blnWrittenKey = INI_CreateKey(INI_strKey, INI_strValue)
				End If
				INI_CreateSection INI_strTemp
				INI_strCurrentSection = Upper$(INI_strTemp)
				If (INI_strCurrentSection = INI_strUpperSection) Then INI_blnSectionFound = True
			Else
				' KEY=VALUE
				Local INI_lngEqualsPos% = Instr(INI_strTemp, "=")
				If (INI_lngEqualsPos &lt;&gt; 0) Then
					If (INI_strCurrentSection = INI_strUpperSection) And (Upper$(Trim$(Left$(INI_strTemp, (INI_lngEqualsPos - 1)))) = Upper$(INI_strKey)) Then
						INI_blnWrittenKey = INI_CreateKey(INI_strKey, INI_strValue)
					Else
						INI_CurrentFile.strContents = INI_CurrentFile.strContents + INI_strTemp + Chr$($0D) + Chr$($0A)
					End If
				End If
			End If
		End If

		' Move through the INI contents...

		INI_lngOldPos = INI_lngPos + 1
		INI_lngPos% = Instr(INI_strTempContents, Chr$($0D), INI_lngOldPos)

	Wend

	' KEY wasn't found in the INI contents - Append a new SECTION if required and create our KEY=VALUE line

	If (INI_strValue &lt;&gt; "") And (INI_blnWrittenKey = False) Then
		If (INI_blnSectionFound = False) Then
			INI_CreateSection INI_strSection
		End If
		INI_CreateKey INI_strKey, INI_strValue
	End If
	
	INI_CurrentFile.blnModified = True

End Function

Function INI_CreateSection(INI_strNewSection$)

	If INI_CurrentFile.strContents &lt;&gt; "" Then
		' Blank line between sections
		INI_CurrentFile.strContents = INI_CurrentFile.strContents + Chr$($0D) + Chr$($0A)
	End If
	INI_CurrentFile.strContents = INI_CurrentFile.strContents + INI_strNewSection + Chr$($0D) + Chr$($0A)

End Function

Function INI_CreateKey%(INI_strKey$, INI_strValue$)

	If (INI_strValue &lt;&gt; "") Then
		INI_CurrentFile.strContents = INI_CurrentFile.strContents + INI_strKey + "=" + INI_strValue + Chr$($0D) + Chr$($0A)
	End If
	
	Return True

End Function

</pre><br><br>Its times like these that i really lament the loss of Blitzcoder.com. So much cool sourcecode lost.. :( <br><br></td></tr></table><br>
<a name="609352"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks everyone! <br><br>I still try to sort out the functions I'll be using... :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
