<!DOCTYPE html><html lang="en" ><head ><title >Is it possible to get Folder Size?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Is it possible to get Folder Size?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Is it possible to get Folder Size?</a><br><br>
<a name="763920"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ima747</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> The FileSize function can be run on folders but it returns somewhat random results it seems. Is there any way to get the size of a folder? For my needs it doesn't need to be very acurate. Preferably cross system friendly... doing a recursive scan of a directory for file sizes can obviously take forever if you try doing it on a directory that's too low (i.e. the root of a drive).<br><br>Any thoughts? my searches have not turned up much of use. <br><br></td></tr></table><br>
<a name="763932"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >FlameDuck</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Do a recursive scan of all the files in the folder and it's subfolders, and add the size of all files to a running tally. <br><br></td></tr></table><br>
<a name="764001"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Space Fractal</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Something like this (wich can of course been better):<br><br><pre class=code>
FolderSize.Examine("Your Path")
Print "Size in KB: "+FolderSize.Size/1024


Type FolderSize
	Global Size
	
	Function Examine(Dir$, Root=1)
		Local files$[]
		Local Handler=ReadDir(Dir$)
		If Root=1 Then Size=0
		If Handler&lt;&gt;Null
			Repeat
				Local File$=NextFile(Handler)
				If File$="" Then Exit
				If FileType(Dir$+"\"+File$)=2
					Examine(Dir$+"\"+File$)
				Else
					Size:+FileSize(DIR$+"\"+File$)
				EndIf
			Forever
		EndIf
	EndFunction
EndType
</pre><br><br>Doing a recursive scan is the only the way to go. You cant do anything other, and I guess FileSIze does the same things? <br><br></td></tr></table><br>
<a name="764006"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ima747</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I tried that before I posted unfortunatetly. It can take a rediculously long time to get a size. I was hoping someone knew of some way to tap into the numbers the OS stores (atleast on a mac) for folder sizes... <br><br></td></tr></table><br>
<a name="764026"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Space Fractal</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes it take time, but even Windows Explorer does that, and it can been clearly seen when right clicking on a folde, and check that size.<br><br>But it does that in a seperate thread, but Blitz dosent support that. <br><br></td></tr></table><br>
<a name="764052"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >marksibly</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>Here's an interesting alternative.<br><br>Of course, you've got to keep 'poking' it, but even a multithreaded solution would required some kind of repetition.<br><br><pre class=code>

Strict

Type TDirSize

	Method Done()
		Return _entry=_entries.length And _dirs.IsEmpty()
	End Method

	Method Update:Long()
		For Local count=1 To 2500
			While _entry=_entries.length
				If _dirs.IsEmpty() Return _size
				_dir=String( _dirs.RemoveLast() )
				_entries=LoadDir( _dir )
				_entry=0
			Wend
			Local path$=_dir+"/"+_entries[_entry]
			_entry:+1
			If FileType( path )=FILETYPE_DIR
				_dirs.AddLast path
			Else
				_size:+FileSize( path )
			EndIf
		Next
		Return _size
	End Method

	Function Create:TDirSize( dir$ )
		Assert FileType( dir )=FILETYPE_DIR
		Local t:TDirSize=New TDirSize
		t._dirs.AddLast dir
		Return t
	End Function
	
	Field _size:Long
	Field _dirs:TList=New TList
	Field _dir$,_entries$[],_entry
	
End Type

Local t:TDirSize=TDirSize.Create( "c:\" )

While Not t.Done()
	Print t.Update()
Wend

Print "Done!"
End
</pre> <br><br></td></tr></table><br>
<a name="764095"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skn3</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Filefunctions.bmx<br><pre class=code>Strict
Import brl.linkedlist
Import BRL.StandardIO
Import BRL.FileSystem

Function DirectorySize:Long(npath:String)
	'fix the path value
	npath = realdir(npath)
	
	'fix forward slashes
	npath = npath.replace("\","/")
	
	'create a list to store folder information
	Local temp_paths:TList = CreateList()
	Local temp_path:String
	Local temp_count:Long
	Local temp_dir:Int
	Local temp_file:String
	
	'add first instance to folder list
	temp_paths.addlast(npath)
	
	'start a scan in this folder
	While temp_paths.isempty() = False
		'grab first path and remove it
		temp_path = String(temp_paths.first())
		temp_paths.removefirst()
		
		Print "scanning: "+temp_path
		
		'scan through the path
		temp_dir = ReadDir(temp_path)
		Repeat
			temp_file = NextFile(temp_dir)
			If temp_file = "" Exit
			If temp_file = "." Or temp_file = ".." Continue

			Select FileType(temp_path + temp_file)
				Case 1
					'add the filesize to the count
					temp_count :+ FileSize(temp_path + temp_file)
				Case 2
					'add the path to list of paths to scan
					temp_paths.addlast(temp_path + temp_file + "/")
			End Select
		Forever
		CloseDir temp_dir
		
	Wend
	
	Return temp_count
End Function



Function ExtractPathSegments:String[](npath:String)
	Local temp_segments:String[]
	Local temp_count:Int
	Local temp_pos:Int
	Local temp_segment:String
	Local temp_subpos:Int
	Local temp_subsegment1:String
	Local temp_subsegment2:String

	'fix dotted paths
	npath = realdir(npath)
	
	'fix forward slashes
	npath = npath.replace("/","\")
	
	temp_pos = npath.find("\")
	While temp_pos &lt;&gt; -1
		'extract the segment
		temp_segment = npath[..temp_pos]
		
		'check to see if segment needs splitting
		temp_subpos = temp_segment.find(":")
		If temp_subpos = -1
			'no split needed
			
			'resize segment array
			temp_segments = temp_segments[..temp_count+1]
			
			'set segment
			temp_segments[temp_count] = temp_segment
			
			'increase segment count
			temp_count :+ 1
		Else
			'needs splitting
			
			temp_subsegment1 = temp_segment[..temp_subpos]
			temp_subsegment2 = temp_segment[temp_subpos+1..]
			
			'check to see for drive name
			If temp_subsegment2.length = 0
				'this is a correctly formed path so extract drive name
				'resize segment array
				temp_segments = temp_segments[..temp_count+1]
				
				'set segment
				temp_segments[temp_count] = temp_subsegment1 + ":"
				
				'increase segment count
				temp_count :+ 1
			Else
				'path missed out backslash after drive name
				'resize segment array
				temp_segments = temp_segments[..temp_count+2]
				
				'set segments
				temp_segments[temp_count] = temp_subsegment1
				temp_segments[temp_count+1] = temp_subsegment2
				
				'increase segment count
				temp_count :+ 2
			End If
		End If
		
		npath = npath[temp_pos+1..]
		temp_pos = npath.find("\")
	Wend
	'add last
	temp_segment = npath
	
	'check to see if segment needs splitting
	temp_subpos = temp_segment.find(":")
	If temp_subpos = -1
		'no split needed
		
		'resize segment array
		temp_segments = temp_segments[..temp_count+1]
		
		'set segment
		temp_segments[temp_count] = temp_segment
		
		'increase segment count
		temp_count :+ 1
	Else
		'needs splitting
		
		temp_subsegment1 = temp_segment[..temp_subpos]
		temp_subsegment2 = temp_segment[temp_subpos+1..]
		
		'check to see for drive name
		If temp_subsegment2.length = 0
			'this is a correctly formed path so extract drive name
			'resize segment array
			temp_segments = temp_segments[..temp_count+1]
			
			'set segment
			temp_segments[temp_count] = temp_subsegment1 + ":"
			
			'increase segment count
			temp_count :+ 1
		Else
			'path missed out backslash after drive name
			'resize segment array
			temp_segments = temp_segments[..temp_count+2]
			
			'set segments
			temp_segments[temp_count] = temp_subsegment1
			temp_segments[temp_count+1] = temp_subsegment2
			
			'increase segment count
			temp_count :+ 2
		End If
	End If
	
	Return temp_segments
End Function

Function EmptyFolder:Int(npath:String)
	'fix the path
	npath = realdir(npath)

	'check to see if folder exists
	If FileType(npath) &lt;&gt; 2
		' folder doesn't exist so return false
		Return False
	Else
		'folder exists
		Local temp_folder:Int = ReadDir(npath)
		Local temp_file:String
		Local temp_path:String
		
		Repeat
			temp_file = NextFile(temp_folder)
			If temp_file = "" Exit
			If temp_file = "." Or temp_file = ".." Continue
			
			temp_path = npath + "/" + temp_file
			Select FileType(temp_path)
				Case 1
					DeleteFile(temp_path)
				Case 2
					DeleteDir(temp_path,True)
			End Select
		Forever
	End If
End Function

Function CreateFolderStructure(npath:String)
	Local temp_i:Int
	Local temp_segment:String
	Local temp_segments:String[]
	Local temp_count:Int
	Local temp_path:String
	
	'get path segments
	temp_segments = ExtractPathSegments(npath)
	
	'go through created segments and create structure
	If temp_segments.length &gt; 0
		For temp_i = 0 Until temp_segments.length
			temp_segment = temp_segments[temp_i]
			
			'check to see what type if segment this is
			If temp_segment[1..2] = ":"
				'drive
				temp_path :+ temp_segment
			Else
				'folder
				'create the folder
				temp_path :+ "\" + temp_segment
				CreateDir(temp_path)
				SetFileMode(temp_path,%111111111)
			End If
		Next
	End If
End Function

Function FindFileInFolder:String(npath:String,nfile:String)
	'fix path
	npath = realdir(npath)

	'this function will find a file in a folder.
	Local temp_folder:Int = ReadDir(npath)
	Local temp_file:String
	Local temp_path:String
	Local temp_i:Int
	Local temp_until:Int
	Local temp_match:Int
	
	'fix case of nfile
	nfile = nfile.tolower()
	
	Repeat
		temp_file = NextFile(temp_folder).tolower()
		If temp_file = "" Exit
		If temp_file = "." Or temp_file = ".." Continue
		
		'check only files
		temp_path = npath + "/" + temp_file
		If FileType(temp_path) = 1
			'check to see for match in file name
			'check to see scanning until which size
			If nfile.length &gt;= temp_file.length
				temp_until = temp_file.length
			Else
				temp_until = nfile.length
			End If
			
			'scan the strings for a match
			temp_match = True
			For temp_i = 0 Until temp_until
				If temp_file[temp_i] &lt;&gt; nfile[temp_i]
					temp_match = False
					Exit
				End If
			Next
			
			If temp_match = True
				Return temp_path
			End If
		End If
	Forever
	
	Return ""
End Function

Function MoveFile:Int(nfrom:String,nto:String)
	'this function will move a file from one place to another
	
	'fix paths
	nfrom = realdir(nfrom)
	nto = realdir(nto)
	
	'make sure from file exists
	If FileType(nfrom) &lt;&gt; 1 Return False
	
	'set nfrom permissions
	SetFileMode(nfrom,%111111111)
	
	'remove to file if it already exists
	If FileType(nto) = 1
		'set to file permissions
		SetFileMode(nto,%111111111)
		DeleteFile(nto)
		'check to see if couldnt delete
		If FileType(nto) = 1 Return False
	End If
	
	'copy nfrom to nto
	CopyFile(nfrom,nto)
	
	'delete nfrom
	DeleteFile(nfrom)
	
	'return that file was copied
	Return True
End Function

Function RealDir:String(npath:String)
	If npath.length &gt; 0
		'this is a temporary fix for the blitzmax bug
		npath = npath.replace("\","/")
		npath = npath.replace("//","/")
		If npath[..1] = "/" npath = npath[2..]
	End If
	
	Return RealPath(npath)
End Function</pre><br><br><br><br>tpathsizereport.bmx<br><pre class=code>
Strict

Import BRL.FileSystem
Import BRL.LinkedList
Import BRL.StandardIO
Import BRL.System

Function CreatePathSizeReport:TList(npath:String,nfilter:Long=0)
	'fix the path value
	npath = RealPath(npath)
	
	'fix forward slashes
	npath = npath.replace("\","/")

	Local temp_list:TList = CreateList()
	RecurseDirectory(npath,temp_list,nfilter)
	Return temp_list
End Function

Function RecurseDirectory:Long(npath:String,nlist:TList Var,nfilter:Long Var)
	'this function will recurse a directory 
	Local temp_dir:Int = ReadDir(npath)
	Local temp_file:String
	Local temp_count:Long
	Local temp_returned:Long
	Local temp_item:tpathsizeitem
	
	temp_dir = ReadDir(npath)
	Repeat
		temp_file = NextFile(temp_dir)
		If temp_file = "" Exit
		If temp_file = "." Or temp_file = ".." Continue

		Select FileType(npath + temp_file)
			Case 1
				
				'add the filesize to the count
				temp_returned = FileSize(npath + temp_file)
				
				If temp_returned &gt;= nfilter
				
					'add file instance to the list
					temp_item = New tpathsizeitem
					temp_item.file = True
					temp_item.path = npath + temp_file
					temp_item.size = temp_returned
					
					'add to list
					nlist.addlast(temp_item)
				End If
				
				'increase count for return
				temp_count :+ temp_returned
			Case 2
				'recurse sub folder
				temp_returned = RecurseDirectory(npath + temp_file + "/",nlist,nfilter)
				
				If temp_returned &gt;= nfilter
					'add folder instance to the list
					temp_item = New tpathsizeitem
					temp_item.file = False
					temp_item.path = npath + temp_file
					temp_item.size = temp_returned
					
					'add to list
					nlist.addlast(temp_item)
				End If
				
				'increase count for return
				temp_count :+ temp_returned
		End Select
		
		Delay 1
	Forever
	CloseDir temp_dir
	
	Return temp_count
End Function

Type tpathsizeitem
	Field file:Byte
	Field path:String
	Field size:Long
End Type
</pre><br><br><br>Enjoy! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
