<!DOCTYPE html><html lang="en" ><head ><title >MaxGUI: strange waitevent() / Paste issue</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >MaxGUI: strange waitevent() / Paste issue</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >MaxGUI: strange waitevent() / Paste issue</a><br><br>
<a name="604370"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Perturbatio</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> The following code creates an app that generates enum types, currently you have to copy the output using ctrl+c, when I do this and switch to the IDE to paste it in, the IDE halts until I switch back to the app (or just mouseover it).<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
Framework BRL.Win32MaxGUI
Import BRL.Retro


Global quit:Int = False

Global dt : TGadget = Desktop()

Global iWidth : Int = GadgetWidth(dt) - (GadgetWidth(dt)*0.4) 'set width to 60%
Global iHeight : Int = GadgetHeight(dt) - (GadgetHeight(dt)*0.4) 'Set height to 60%
Global iXPos : Int = (GadgetWidth(dt)-iWidth)/2
Global iYPos : Int = (GadgetHeight(dt)-iHeight)/2

Global wMainWin : TGadget = CreateWindow("EnumGen", iXPos, iYPos, iWidth, iHeight, Null, WINDOW_TITLEBAR)

Global lblEnumName : TGadget = CreateLabel("Enum Name:",((iWidth-(iWidth*0.8))/2), 0, iWidth*0.2, 12, wMainWin)
Global tfEnumName:TGadget = CreateTextField((iWidth-(iWidth*0.8))/2, 14, iWidth*0.2, 22, wMainWin)

Global lblEnumType : TGadget = CreateLabel("Enum Type:",((iWidth-(iWidth*0.8))/2)+(iWidth*0.2), 0, iWidth*0.2, 12, wMainWin)
Global cbEnumType : TGadget = CreateComboBox(((iWidth-(iWidth*0.8))/2)+(iWidth*0.2), 14, iWidth * 0.2, 34, wMainWin)
	AddGadgetItem(cbEnumType, "Int")
	AddGadgetItem(cbEnumType, "Float")
	AddGadgetItem(cbEnumType, "Bitwise")
	SelectGadgetItem(cbEnumType, 0)

Global lblEnumStep : TGadget = CreateLabel("Step:",((iWidth-(iWidth*0.8))/2)+(iWidth*0.4), 0, iWidth*0.2, 12, wMainWin)
Global tfEnumStep:TGadget = CreateTextField(((iWidth-(iWidth*0.8))/2)+(iWidth*0.4), 14, iWidth*0.2, 22, wMainWin)
	SetGadgetText(tfEnumStep, "1")

Global lblStart : TGadget = CreateLabel("Start Value:",((iWidth-(iWidth*0.8))/2)+(iWidth*0.6), 0, iWidth*0.2, 12, wMainWin)
Global tfStart:TGadget = CreateTextField(((iWidth-(iWidth*0.8))/2)+(iWidth*0.6), 14, iWidth*0.2, 22, wMainWin)


Global lblEnumList:TGadget = CreateLabel("Enum Keyword List:",((iWidth-(iWidth*0.8))/2), 42, iWidth, 12, wMainWin)
Global taEnumList:TGadget = CreateTextArea((iWidth-(iWidth*0.8))/2, 56, iWidth*0.8, (iHeight-56)*0.4, wMainWin)


Global btnProcess:TGadget = CreateButton("&amp;Process", ((iWidth-(iWidth*0.8))/2), ((iHeight-56)*0.4)+58, iWidth*0.1, 22, wMainWin)


Global lblOutput:TGadget = CreateLabel("Output:",((iWidth-(iWidth*0.8))/2), (90+((iHeight-90)*0.4))+4, iWidth, 12, wMainWin)
Global taOutput:TGadget = CreateTextArea((iWidth-(iWidth*0.8))/2, (90+((iHeight-90)*0.4))+18, iWidth*0.8, (iHeight-90)*0.4, wMainWin, TEXTAREA_READONLY)



While Not Quit

	WaitEvent()

	Select EventID()
		Case EVENT_WINDOWCLOSE
			Quit = True
		Case EVENT_GADGETACTION
			Select EventSource()
				Case btnProcess
					If Verify() Then 
						Process() 
					EndIf
			End Select
	End Select

Wend

End


Function Verify:Int()
	'Local function
	Function CheckString:Int(val:String)
		Local disallow:String[] = ["~q","~t","!","£","$","%","^","&amp;","*","(",")","+","-","=","[","]"..
									,"{","}",":","@","~~",";","'","#",",",".","/","&lt;","&gt;","?"]
		
		For Local s:String = EachIn disallow
			If val.find(s)&gt;-1 Then Return False
		Next
		
		Return True
	End Function


	If Len(TextFieldText(tfEnumName))&lt;1 Then 
		Notify("Enum name required")
		Return False
	EndIf
	
	
	If Not CheckString(TextFieldText(tfEnumName)) Then 
		Notify("Invalid character in EnumName!")
		Return False
	EndIf
	
	
	Select SelectedGadgetItem(cbEnumType)
		Case 0
			If Not Int(TextFieldText(tfEnumStep)) Then 
				Notify("Step is not an int value")
				Return False
			EndIf

			If Not Int(TextFieldText(tfStart)) Then 
				Notify("Start is not an int value")
				Return False
			EndIf

		Case 1
			If Not Float(TextFieldText(tfEnumStep)) Then 
				Notify("Step is not an float value")
				Return False
			EndIf
			
			If Not Float(TextFieldText(tfStart)) Then 
				Notify("Start is not an float value")
				Return False
			EndIf

		Case 2
			If Not Int(TextFieldText(tfStart)) Then 
				Notify("Start is not an int value")
				Return False
			EndIf
	End Select

	
	If Not CheckString(Trim(TextAreaText(taEnumList))) Then 
		Notify("Invalid character in Enum List!")
		Return False
	EndIf

	If Len(Trim(TextAreaText(taEnumList))) &lt; 1 Then 
		Notify("No enum variable items specified")
		Return False
	EndIf
	
	Return True
End Function



Function Process()
	Local outText:String
	Local outType:String
	
	Local IntCount:Int = Int(TextFieldText(tfStart))
	Local FloatCount:Float = Float(TextFieldText(tfStart))
	
	Local IntStep:Int = 1
	Local FloatStep:Float = 1.0

	
	
	Select SelectedGadgetItem(cbEnumType)
		Case 0
			outType = "Int"
			IntStep = Int(TextFieldText(tfEnumStep))
			
		Case 1
			outType = "Float"
			FloatStep = TextFieldText(tfEnumStep).ToFloat()
			
		Case 2
			outType = "Int"
			'intCount = 1
	End Select
	outText :+ "'ENUM " + Trim(TextFieldText(tfEnumName)) + "~n"
	outText :+ "Type T" + Trim(TextFieldText(tfEnumName)) + " Final~n"
	Local enumList:TList = SplitString(Trim(TextAreaText(taEnumList)), "~n")
	
	For Local s:String = EachIn enumList
		s = Trim(s) 'remove invisible chars
		outText:+ "~tConst " + s + " : " + outType + " = "
		
		Select SelectedGadgetItem(cbEnumType)
			Case 0 'Int
				outText:+ IntCount
				IntCount:+ IntStep
			Case 1 'Float
				outText:+ FloatCount
				FloatCount:+ FloatStep
			Case 2 'Bitwise
				outText:+ IntCount
				IntCount =  (IntCount*2)
		End Select
		
		outText:+"~n"
		
	Next
	outText:+"End Type~n"
	SetTextAreaText(taOutput, outText)
End Function



Function SplitString:TList(inString:String, Delim:String)
	Local tempList : TList = New TList
	Local currentChar : String = ""
	Local count : Int = 0
	Local TokenStart : Int = 0
	
	If Len(Delim)&lt;&gt;1 Then Return Null
	
	inString = Trim(inString)
	
	For count = 0 Until Len(inString)
		If inString[count..count+1] = delim Then
			tempList.AddLast(inString[TokenStart..Count])
			TokenStart = count + 1
		End If
	Next
	tempList.AddLast(inString[TokenStart..Count])	
	Return tempList
End Function
</textarea><br><br>It appears to be caused by <b>WaitEvent()</b>, if you change it to <b>PollEvent()</b> it works fine. <br><br></td></tr></table><br>
<a name="604477"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Perturbatio</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> could someone at least confirm it happens for them so that I know my system isn't crap? <br><br></td></tr></table><br>
<a name="604487"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >EOF</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Confirmed. Same issue here Perturbatio.<br>Tried it with Notepad. Same again. <br><br></td></tr></table><br>
<a name="604497"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoBoDo</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I see the same thing, weird that it pastes when you mouseover EnumGen.<br><br>Another issue you might want to be aware of -- I use dual monitors, and on a dual monitor setup Desktop() apparently means the entire space of both monitors.  This app starts centered between the two monitors (half on one monitor and half on the other monitor), and since it is not sizeable and takes up 60% of the dual monitor space, it isn't even possible to make it display on one screen.<br><br>Mark. <br><br></td></tr></table><br>
<a name="604498"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Perturbatio</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> yeah, I was just experimenting with dynamic sizing when I did that :) <br><br></td></tr></table><br>
<a name="604504"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >taxlerendiosk</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Is this related to the way that drag and dropped icons don't always seem to register until I move the mouse pointer? Or is that one just me? <br><br></td></tr></table><br>
<a name="604507"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >EOF</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I notice that too with my Framework Assistant. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
