<!DOCTYPE html><html lang="en" ><head ><title >Odd problem with GLMax2DDriver</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Odd problem with GLMax2DDriver</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Odd problem with GLMax2DDriver</a><br><br>
<a name="1108596"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> posted some code in the beginner's forum in the coding challenge thread.  The timing was linked directly with the monitor's vSync.<br><br>Decided I would try implementing fixed rate logic with tweening.  This is where everything gets interesting.  After changing the logic, everything moved a little jerky.  What was really weird was that everything sped up when I moved the mouse.  Thinking I made a mistake in my logic, I looked over my code again, and again.  Redid a few things, improved a few things, but the jerkiness was still there.<br><br>Tried compiling using Flip False so the flip happened as fast as possible.  Everything ran smoothly, but I did get a little tearing (not surprising since the flips are no longer synced to the vertical sync).<br><br>My code was using the GLMax2DDriver.  I decided to comment out the line so that it would use the default D3D9Max2DDriver.  Everything ran smoothly, regardless of what Flip was set to.  Then I realized, the jerkiness was coming from several frames being buffered, then drawn one after another.  When I moved the mouse, several frames were being rendered in rapid succession, hence the speedup.<br><br>Odd thing was, the drawing portion of my program did not change at all, only the logic portion. Everything ran smoothly before making any changes; but the changes I made, which only affected where everything was drawn, not what or when, seemed to have altered openGl behavior.<br><br>Here is the modified code if anyone wants to try.  Just uncomment the SetGraphicsDriver to try it with OpenGL.<br><br>BTW, my video card is an NVidia GeForce 9100<br><br>Image:<br><img src="http://i194.photobucket.com/albums/z157/TomToad_bucket/ChickenLittle.png"><br>Code:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

Incbin "Chicken Little.png"

'SetGraphicsDriver GLMax2DDriver() 'Will be using OpenGL to make it portable to Linux and Mac

Const Gravity:Float = .05 'speed at which objects accelerate due to gravity
Const TerminalVelocity:Float = 20.0 'fastest an object will fall
Const ProgressionTime:Int = 2000 'determines how often the game speeds up
Const MSPF:Int = 10 'millisecs per logic frame


AutoMidHandle True 'midhandle all images

Global Pixmap:TPixmap = LoadPixmap("incbin::Chicken Little.png") 'This pixmap contains all the graphics. LoadAnimImage() will be used to split it to individual components
Local GrassTopImage:TImage = LoadAnimImage(Pixmap,64,64,4,1) 'Grass
Local GrassImage:TImage = LoadAnimImage(Pixmap,64,64,10,1)

Local Timer:Int'This will determine how fast the game progresses.  At every tick, the NextObjectTimer increases in speed
Local NextObjectTimer:Int  'The amount of time between objects falling.  this decreases as the game progresses
Local NextObject:Int 'when the next object actually falls
Local Hit:Int = False 'Flag for when Chicken Little is hit by an object


'Now for the Chicken Little info
Type TChicken
	Field x:Int 'Chicken Little's current position
	Field FaceRight:Int = False 'the direction the chicken is facing
	Field Lives:Int = 3 'Begin with 3 lives
	Field Score:Int 'Chicken Little's Score
	
	Field ChickenImage:TImage
		
	Method New()
		ChickenImage = LoadAnimImage(Pixmap,64,64,0,4)
	End Method
	
	'This method will draw chicken little and will also test for collision, returning true or false
	Method Draw:Int()
		Local TestResult:Int 'We will hold the result of the collision test here.
		
		Local MX:Int = MouseX() 'The current mouse position
		If MX &gt; x Then FaceRight = True
		If MX &lt; x Then FaceRight = False
		Local Frame:Int = MX &amp; 3 ' the frame being drawn for the chicken
		
		If FaceRight Then SetScale -1,1 'reverse the image if Chicken little moves to the right
		x = MX
		
		DrawImage ChickenImage,x,504,Frame 'draw the chicken
		If CollideImage(ChickenImage,x,504,Frame,1,0)
			TestResult = True
		Else
			TestResult = False
		End If
		
		SetScale 1,1 'make sure the scale is reset before returning from the method
		
		Return TestResult
	End Method
		
End Type

Local ChickenLittle:TChicken = New TChicken 'create the chicken type

'This is the type holding info on the sky objects
Type TSkyObject
	Global List:TList = CreateList() 'The list of all the objects falling to earth
	Global LogicTime:Int = 0
	
	Global SkyImage:TImage[5] 'This holds the images that could fall from the sky

	
	Field x:Int, y:Float 'The position of the object
	Field Angle:Float, RotationSpeed:Float, RotationDirection:Float 'the angle, speed and direction the object spins on it's decent
	Field FallSpeed:Float 'the speed the object is falling
	Field Image:Int 'the image to draw
	
	Field yStore:Float 'Used in tweening
	Field AngleStore:Float
	Field FallSpeedStore:Float
	
	Function Init()
		For Local i:Int = 0 To 4
			SkyImage[i] = LoadAnimImage(Pixmap,64,64,5+i,1) 'Here we load in each of the individual sky objects
		Next
	End Function

	'this function will create an object and add it to the list
	Function Create()
		Local SkyObject:TSkyObject = New TSkyObject 'create new object
		SkyObject.x = Rand(0,800) 'A random position in the sky
		SkyObject.yStore = -64 'Begin off the top of the screen
		SkyObject.y = SkyObject.yStore 'update y so there won't be a flash drawn before the first update
		SkyObject.AngleStore = Rnd(0,360) 'The angle of the object
		SkyObject.RotationSpeed = Rnd(0,5) 'how fast it spins
		SkyObject.RotationDirection = Rand(-1,1) 'the direction the object spins
		SkyObject.Image = Rand(0,4) 'The image displayed.
		
		List.AddLast(SkyObject) 'adds the object to the list
	End Function
	
	'this function will update the position of all the objects
	Function Update()
		If LogicTime = 0 'If this is the first time through the loop, then set the timer
			LogicTime = MilliSecs() + MSPF
			Return
		End If
		
		Local Time:Int = MilliSecs()
		
		Local i:Int = 0
		If Time &gt;= LogicTime
			i:Int = (Time - LogicTime)/MSPF+1 'The number of full logic frame iterations
			logicTime :+ i*MSPF 'update LogicTime for the next frame
		End If
		Local Tween:Float = (Time - (LogicTime - MSPF)) / MSPF 'Find the Tween Value
		For Local SkyObject:TSkyObject = EachIn List 'Go through each object in the list
			SkyObject.y = SkyObject.yStore 'reset the values to the last full logic frame values
			SkyObject.Angle = SkyObject.AngleStore
			SkyObject.FallSpeed = SkyObject.FallSpeedStore
			For Local loop:Int = 1 To i
				SkyObject.Move(1) 'move the object by one logic frame
			Next
			SkyObject.yStore = SkyObject.y 'Store the values for the last full logic frame
			SkyObject.AngleStore = SkyObject.Angle
			SkyObject.FallSpeedStore = SkyObject.FallSpeed
			SkyObject.Move(Tween) 'tween the logic

		Next		
		
	End Function
	
	'This function will draw the objects to the screen and add them to the collision layer
	Function Draw()
		ResetCollisions() 'reset the collision layer
		
		For Local SkyObject:TSkyObject = EachIn List
			SetRotation SkyObject.Angle 'set the rotation to the object
			
			DrawImage SkyImage[SkyObject.image],SkyObject.x,SkyObject.y 'draws the image to the screen
			
			CollideImage(SkyImage[SkyObject.Image],SkyObject.X,SkyObject.y,0,0,1) 'Adds to the collision layer
			SetRotation 0 'reset the rotation
		Next
	End Function
	
	Method Move(Tween:Float)
		Angle :+ RotationSpeed * RotationDirection*Tween 'update the rotation
		If Angle &gt;= 360 Then Angle :- 360 'keep the angle within 0-360
		If Angle &lt; 0 Then Angle :+ 360
		
		FallSpeed :+ Gravity*Tween 'Increase the fall speed due to gravity
		If FallSpeed &gt; TerminalVelocity Then FallSpeed = TerminalVelocity 'Fastest it will fall
		
		y :+ FallSpeed * Tween'move the object
		If y &gt; 664 Then List.Remove(Self) 'if it falls below the screen, then remove from the list
	End Method
		
End Type

TSkyObject.Init()

Graphics 800,600,32 'set graphics mode
HideMouse

Repeat
	'we display a countdown to give the palyer time to get ready
	Drawmessage("Get Ready!",3000)
	DrawMessage("3",1000)
	DrawMessage("2",1000)
	DrawMessage("1",1000)
	
	NextObjectTimer = 1000 'set the initial values of the timers
	NextObject = MilliSecs() + NextObjectTimer
	Timer = MilliSecs()+ProgressionTime
	
	TSkyObject.List.Clear() 'Clear the list
	TSkyObject.LogicTime = 0 'reset the logic timer
	ChickenLittle.Score = 0 'Reset the score
	ChickenLittle.Lives = 3 'Reset the lives
	
	
	While Not KeyHit(KEY_ESCAPE) 'The main game loop
		Cls
		For Local i:Int = 0 To 13 'Let's Draw the grass
			DrawImage GrassImage,i*64,568
			DrawImage GrassTopImage,i*64,504
		Next
		
		TSkyObject.Update() 'Update all the objects
		TSkyObject.Draw() 'Draw all the objects
		
		Hit = ChickenLittle.Draw() 'Draw Chicken Little
		
		SetColor 128,128,128 'draw score and lives
		DrawRect 0,568,800,32 'draw the score background
		SetColor 255,255,255
		DrawText "SCORE: "+ChickenLittle.Score,10,578
		DrawText "LIVES: "+ChickenLittle.Lives,400,578
		Flip 1
		
		If Hit 'Was he hit?
			Delay(1000)
			ChickenLittle.Lives :- 1 'decrease the lives counter
			If ChickenLittle.Lives = 0 Then Exit 'If lives drops to 0, game over
			TSkyObject.List.Clear() 'Clear the object list
			NextObjectTimer = 1000 'reset the difficulty
			NextObject = MilliSecs() + NextObjectTimer 'reset the timer
		End If
		
		If MilliSecs() &gt;= Timer 'Time to progress teh game?
			Timer :+ ProgressionTime 'Next time the game gets harder
			NextObjectTimer :- 25 'subtract the delay between objects
			If NextObjectTimer &lt; 1 Then NextObjectTimer = 1 'Make sure the delay isn't less tahn 1 millisecs()
			ChickenLittle.Score :+ 1 'Increase thescore by 1
		End If
		
		If MilliSecs() &gt;= NextObject 'Time for another object?
			TSkyObject.Create() 'Create the new object
			NextObject :+ NextObjectTimer 'Add the time for the next object
		End If
	Wend
	
	'Game over
	Repeat
		DrawMessage("Game Over!~nYour Score Is "+ChickenLittle.Score+"~nPress ESC to Exit~nPress SPACE to try again",0)
		If KeyHit(KEY_ESCAPE) Then End 'end the program if the escape key is hit
	Until KeyHit(KEY_SPACE)
Forever
	

'This function will simply display a message centered on the screen for the specified time.
Function DrawMessage(Message:String,Time:Int)
	Local MessageArray:String[] = Message.Split("~n")
	Local Lines:Int = MessageArray.Length
	Local FontHeight:Int = TextHeight(Message) * 3 + 30
	Local Top:Int = (600-FontHeight*Lines)/2
	Time :+ MilliSecs()
	SetScale 3,3
	
	Repeat
		Cls
		
		For Local i:Int = 0 Until Lines
			DrawText MessageArray[i],(800-TextWidth(MessageArray[i])*3)/2,Top+i*FontHeight
		Next
		Flip
	Until MilliSecs() &gt;= Time
	SetScale 1,1
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1108627"></a>

<a name="1108628"></a>

<a name="1108629"></a>

<a name="1108630"></a>

<a name="1108634"></a>

<a name="1108635"></a>

<a name="1108637"></a>

<a name="1108639"></a>

<a name="1108641"></a>

<a name="1108642"></a>

<a name="1108643"></a>

<a name="1108644"></a>

<a name="1108645"></a>

<a name="1108647"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> <strike>Seems to be smooth here with OGL and Dx. I've tried it with my Dx11 driver and its ok with that too.</strike><br><br>After trying a couple of things its not smooth at all!! Its regular but not smooth.<br><br>Might be talking crap here ( I'm real tired and we all know what that means :D ) but...<br>Using float for 'i' makes it smooth when using syncing, but not smooth with Flip0<br><pre class=code>
Local i:Float = 0
If Time &gt;= LogicTime
	i = Float(Time - LogicTime)/MSPF+1 'The number of full logic frame iterations
	logicTime :+ i*MSPF 'update LogicTime for the next frame
End If
</pre><br>The Tween variable is always 0 because of the brackets around the calculation?<br><pre class=code>
Local Tween:Float = (Time - (LogicTime - MSPF)) / MSPF 'Find the Tween Value

to

Local Tween:Float = (Time - LogicTime - MSPF) / MSPF 'Find the Tween Value
</pre><br><br>Sony Vaio VGN-FW31M - Vista Home 32bit.<br><br>EDIT:- One thing. To get it to work, the media file is spelt different to the file name used in the code. The media file needs to be changed to include a space between the words 'Chicken' and 'Little' or take the space out in the code at Lines 3 and 15 :P<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1108688"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> That won't work right.  As an example, let's say that LogicTime (The next time a full frame tick has occurred) is 100. MSFP (Milliseconds per frame) is 10. Now through the loop, Millisecs(), which is stored in Time in order to keep a consistent value throughout the function, is 115.  So there are 2 full logic frames that must be calculated (1 at 100 and one at 110), and we are halfway to a third frame (which will occur at 120 milliseconds()) so tween needs to be .5.<br>With i an int and brackets in the Tween calculation<br><br>(115-100) = 15<br>15/10 = 1.5 <br>1.5 gets truncated to 1 since we are dealing with ints.<br>1 + 1 = 2<br><br>So far ok, we have the 2 times our function will loop through a full frame<br><br>LogicTime =<br>2 * 10 = 20<br>20 + LogicTime = 120<br><br>Perfect, we updated LogicTime to the next full frame tick<br><br>Now to compute the Tween<br><br>Tween=<br>120 - 10 = 110<br>115 - 110 = 5<br>5 / 10 = .5<br><br>Just what we need for the tween value.<br><br>Now for your version.<br><br>115 - 100 = 15<br>15 / 10 = 1.5 This will not get truncated<br>1.5 + 1 = 2.5 Not the value we want, it will still work at this point, but complications come later<br><br>LogicTime = <br>2.5 * 10 = 25<br>100+25 = 125<br><br>Not the value we want.  The next logic frame will tick off too late.<br><br>Tween = <br>115 - 125 = -10<br>-10 - 10 = -20<br>-20 / 10 = -2.0<br><br>Once again, not the value we want.<br><br>Stepping through this, I have discovered a bug.  Tween is always 0, but not because of the brackets, rather because of the integer division.  I should change that to<br><br>Tween:Float = Float(Time - (LogicTime - MSPF)) / MSPF <br><br></td></tr></table><br>
<a name="1108696"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I did say I was tired ;)<br>Does your fix work ok? I'm at work at the mo so can't test on my system until later. <br><br></td></tr></table><br>
<a name="1108787"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> No, it doesn't.  For 2D games, tweening doesn't really make that much of a noticeable difference, unless you are extremely perceptive. It becomes more apparent when programming 3D games.  I am beginning to think my problem is just with the OpenGL drivers on the cheap imbedded gpu in my system.  Both DX drivers work just fine. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
