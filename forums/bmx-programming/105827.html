<!DOCTYPE html><html lang="en" ><head ><title >Rogue Random Dungeon Map Maker (Source Included)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Rogue Random Dungeon Map Maker (Source Included)</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=102" >BlitzMax Programming</a>/<a href="#bottom" >Rogue Random Dungeon Map Maker (Source Included)</a><br><br>
<a name="1295660"></a>

<a name="1295661"></a>

<a name="1295662"></a>

<a name="1295664"></a>

<a name="1295687"></a>

<a name="1295688"></a>

<a name="1295689"></a>

<a name="1295690"></a>

<a name="1295699"></a>

<a name="1295700"></a>

<a name="1295701"></a>

<a name="1295702"></a>

<a name="1295703"></a>

<a name="1295745"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> While there are many RPG Makers out there, few have the ability to actually create a perfectly random dungeon, instead requiring the Worldbuilder to use a map editor and manually design their own.<br><br>Looking at the computer generated maps from <a href="https://en.wikipedia.org/wiki/Rogue_%28video_game%29" target="_blank"><b>ROGUE</b></a> it is clear that perfectly random dungeons can be created with a bit of thinking and coding.<br><br><img src="https://upload.wikimedia.org/wikipedia/commons/1/17/Rogue_Screen_Shot_CAR.PNG"><br><br>Now while most ROGUE maps have twisty corridors and only 9-rooms per map (3x3), I did not like that so in this map maker - I wrote it so it shortens the length to just a single connecting doorknob between rooms thus giving you many more rooms to explore per staircase level.<br><br><img src="http://www.writerscafe.org/uploads/rte/a9a1d6130537409097ca9c7dd2ba1d9b.png"><br><br>What is especially interesting is unlike other future variations of ROGUE which have more than 9 rooms per level, some of these rooms cannot be reached without teleportation or digging through solid walls. However, my map maker GUARANTEES all rooms can be reached every time no matter what size they are, where they appear, how they are randomly arranged, or even where you randomly appear in them.<br><br>The screen size is 1024x768, full screen size for me anyways, and the map size is 31x23 tiles, a very close match to 32x32 pixels, a good size for walls, critters, and player sprites to engage in.<br><br>And yes, I do plan to make use of this code in my future projects.<br><br><b>Enjoi !</b><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'     ________________________________________
'    //                                     //
'   // Rogue Random Dungeon Map Maker      //
'  // Version, "Roundabout"               //
' // Written by David W (dw817) 02/16/16 //
'//_____________________________________//

' "Rogue" is a popular style of adventure game where no
' two maps are alike - the computer generates the dungeon
' and the player travels through the corridors fighting
' monsters and collecting treasures.

' <a href="https://en.wikipedia.org/wiki/Rogue_%28video_game%29" target="_blank">https://en.wikipedia.org/wiki/Rogue_%28video_game%29</a>

' What's up ??
' Modified code for more intelligent and random door placement
' (not always centered), now all rooms can be reached from
' any other room ! No need to dig through a wall as I was
' thinking earlier.

Strict
SeedRnd MilliSecs() ' always get random results

Global map[31,23] ' size of map, perfect for 32x32 tiles
Local i,j,h,v,x,y,x2,y2,ok,d,m,first=1,n

SetGraphicsDriver GLMax2DDriver(),0 ' front buffer graphics
Graphics 1024,768 ' full screen (at least for me)

Repeat ' {* MAIN *}

Repeat
  h=fnr(31) ; v=fnr(23) ' find a spot where a previous room exists
Until map[h,v]=1 Or first=1 ' also accept if it's our first room
If first=0
  d=fnr(4) ' choose a random direction to try and build
  Repeat
    If d=0 Then v:-1 ' Up
    If d=1 Then h:+1 ' Right
    If d=2 Then v:+1 ' Down
    If d=3 Then h:-1 ' Left
  Until getmap(h,v)&lt;=0 ' loop until we hit a blank
EndIf
x2=h ; y2=v ' mark this place for the door
Repeat
  x=Rand(2,7) ; y=Rand(2,7) ' we have size of new room to add
Until x*y&lt;=25 ' no more than 25-tiles per room
If first=0
  If d=0 ' build room up
    v=v-y
    h=h-Rand(0,x-1)
  ElseIf d=1 ' build room right
    h=h+1
    v=v-Rand(0,y-1)
  ElseIf d=2 ' build room down
    v=v+1
    h=h-Rand(0,x-1)
  ElseIf d=3 ' build room left
    h=h-x
    v=v-Rand(0,y-1)
  EndIf
EndIf
ok=1
For i=v-1 Until v+y+1 ' NOTE we are checking one tile
  For j=h-1 Until h+x+1 ' OUTSIDE the area we will be adding a room
    m=getmap(j,i) ' to ensuring it is not touching another room
    If m=1 Or (m=-1 And Not (i=v-1 Or i=v+y Or j=h-1 Or j=h+x))Then ok=0 ; Exit
  Next ' ^ if on edge of screen make sure it is edge of room additionally
  If ok=0 Then Exit
Next
If ok
  n=0
  For i=v Until v+y
    For j=h Until h+x
      map[j,i]=1 ' this is a valid room, let's fill it in
    Next
  Next
  If first=0
    map[x2,y2]=2 ' add our previously recorded doorknob position
  EndIf
  update
  first=0
EndIf
n:+1 ' keep track of iterations

' {* END OF MAIN *}
Until n&gt;10000 ' exhaustive search, cannot add any more rooms


update
SetScale 3,3
SetColor 64,0,64
DrawText "COMPLETE !",384,354 ' show completion
SetColor 255,255,255
DrawText "COMPLETE !",380,350
Flip 0
WaitKey
End ' ready to exit

' &gt;&gt; UPDATE SCREEN
Function update()
Local i,j,m
  For i=0 Until 23
    For j=0 Until 31
      m=map[j,i]
      If m=0 ' void
        SetColor 64,64,64
        DrawOval j*32+12,i*32+12,7,7
      ElseIf m=1 ' room
        SetColor 128,64,0
        DrawRect j*32,i*32,31,31
        SetColor 255,255,255
        DrawOval j*32+12,i*32+12,7,7
      ElseIf m=2 ' doorknob
        SetColor 128,128,128
        DrawRect j*32,i*32,31,31
        SetColor 0,0,0
        DrawRect j*32+2,i*32+2,27,27
      EndIf
    Next
  Next
  Flip 0
  WaitKey
EndFunction

' &gt;&gt; GET DATA OF MAP EVEN IF OFF BOUNDARIES
Function getmap(x,y)
  If x&gt;=0 And y&gt;=0 And x&lt;=30 And y&lt;=22
    Return map[x,y]
  Else
    Return -1
  EndIf
EndFunction

' &gt;&gt; SIMPLE GET RANDOM NUMBER (0..a-1)
Function fnr(a)
  Return Rand(a)-1
EndFunction
</textarea> <br><br></td></tr></table><br>
<a name="1297063"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've been playing around with random dungeons myself for a while and I too dislike the long twisty corridors (boring to walk through).<br>Glad someone else is on my side heh.<br><br>Anyways, really nice work! <br><br></td></tr></table><br>
<a name="1297086"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks, Hekzore. Make no mistake I really like what I did and am definitely going to apply it to a future RPG Maker or Rogue variant.<br><br>Since someone is showing an interest, I'll post my latest update to this either today or tomorrow. <br><br></td></tr></table><br>
<a name="1297092"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamStrange</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> the only thing you might want to consider is adding a few more connections.<br><br>Currently there is only one way to reach all rooms. The usual complaint with rogue-likes like this is they become less fun than having multiple way of accessing places.<br><br>just a thought :) <br><br></td></tr></table><br>
<a name="1297104"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Actually I was rather pleased w the fact that you might be in one room and have to travel a GOOD long ways just to get to the room next to you. Still ... it's not very believable.<br><br>For instance, if you were making a Silent Hill type of RPG, what Hospital Floor is going to have rooms like these ? Not many. No, it should be quite possible for a center room for instance being able to link to four or more other rooms.<br><br>I will have to give it some thought. <br><br></td></tr></table><br>
<a name="1297140"></a>

<a name="1297141"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> You could add lots of fun options, so that it fits other situations.<br>Like maybe a flag to allow for those twisty long corridors, or length of corridors.<br>Set maximum amount of ways to reach a room.<br>Maybe even irregular shaped rooms and corridors, for caves and such.<br><br>Anyways, yeah I'd love to see an updated version!<br>This as a TDungeon type or something would be perfect. :) <br><br></td></tr></table><br>
<a name="1297337"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm not very good with TYPE style variables, Hekzore.<br><br>As promised, here is the update. Tiles are now flagged not just as floor and doorknob but critter, treasure, trap, and stairs going both up and down.<br><br><img src="http://www.writerscafe.org/uploads/rte/238d585b4c91f15e7049968c68b6b7ea.png"><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'     ________________________________________
'    //                                     //
'   // Rogue Random Dungeon Map Maker      //
'  // Version, "Marking The Tiles"        //
' // Written by David W (dw817) 02/27/16 //
'//_____________________________________//

' "Rogue" is a popular style of adventure game where no
' two maps are alike - the computer generates the dungeon
' and the player travels through the corridors fighting
' monsters and collecting treasures.

' <a href="https://en.wikipedia.org/wiki/Rogue_%28video_game%29" target="_blank">https://en.wikipedia.org/wiki/Rogue_%28video_game%29</a>

' What's up ??
' Modified code for more intelligent and random door placement
' (not always centered), now all rooms can be reached from
' any other room ! No need to dig through a wall as I was
' thinking earlier.
'
' Added definitions to tiles, not just doorknobs or floor.
' Now you have Critters, traps, treasures, and stairs
' leading both up and down.
'
' If you don't want any more levels, stairs down could also
' be flagged to represent the key item to retrieve from
' the bottom level of the dungeon.
'
' Red Circle ..... Critter
' Purple Cross ... Trap
' Yellow Block ... Treasure
' Triple Lines ... (White) Stairs Up
' Triple Lines ... (Black) Stairs Down

Strict
SeedRnd MilliSecs() ' always get random results

Global map[31,23] ' size of map, perfect for 32x32 tiles
Local i,j,h,v,xx,yy,x,y,x2,y2,ok,d,m,first=1,n,r,h2,v2

SetGraphicsDriver GLMax2DDriver(),0 ' front buffer graphics
Graphics 1024,768 ' full screen (at least for me)

Repeat ' {* MAIN *}

Repeat
  ok=1
  Repeat
    h=fnr(31) ; v=fnr(23) ' find a spot where a previous room exists
  Until map[h,v]&gt;0 Or first=1 ' also accept if it's our first room
  If first=0
    d=fnr(4) ' choose a random direction to try and build
    Repeat
      h2=h ; v2=v
      If d=0 Then v:-1 ' Up
      If d=1 Then h:+1 ' Right
      If d=2 Then v:+1 ' Down
      If d=3 Then h:-1 ' Left
    Until getmap(h,v)&lt;=0 ' loop until we hit a blank
    If getmap(h2,v2)&lt;&gt;1 Then ok=0 ' make sure we don't draw a door near an object
  EndIf
Until ok
x2=h ; y2=v ' mark this place for the door
Repeat
  x=Rand(2,7) ; y=Rand(2,7) ' we have size of new room to add
Until x*y&lt;=25 ' no more than 25-tiles per room
If first=0
  If d=0 ' build room up
    v=v-y
    h=h-Rand(0,x-1)
  ElseIf d=1 ' build room right
    h=h+1
    v=v-Rand(0,y-1)
  ElseIf d=2 ' build room down
    v=v+1
    h=h-Rand(0,x-1)
  ElseIf d=3 ' build room left
    h=h-x
    v=v-Rand(0,y-1)
  EndIf
EndIf
ok=1
For i=v-1 Until v+y+1 ' NOTE we are checking one tile
  For j=h-1 Until h+x+1 ' OUTSIDE the area we will be adding a room
    m=getmap(j,i) ' to ensure it is not touching another room
'    If m&lt;&gt;0 And (i=v-1 And j=h-1) Or (i=v-1 And j=h+x) Or (i=v+y And j=h-1) Or (i=v+y And j=h+x) Then m=0
    If m&gt;0 Or (m=-1 And Not (i=v-1 Or i=v+y Or j=h-1 Or j=h+x))Then ok=0 ; Exit
  Next ' ^ if on edge of screen make sure it is edge of room additionally
  If ok=0 Then Exit
Next
If ok
  n=0
  For i=v Until v+y
    For j=h Until h+x
      map[j,i]=1 ' this is a valid room, let's fill it in
    Next
  Next
  For i=0 Until x+y-2 ' always find a space where items can be added
    Repeat
      xx=h+fnr(x)
      yy=v+fnr(y)
    Until map[xx,yy]=1 And Abs(xx-x2)&gt;0 And Abs(yy-y2)&gt;0 ' item position cannot appear near doorknob
    If first
      map[xx,yy]=5 ' this is the first room so record stairs up
      Exit ' do not add anything more to this room
    EndIf
    If fnr(2)=0 ' there is only a 50% change of objects appearing in other rooms
      map[xx,yy]=2+fnr(3) ' tile is now critter, treasure, or trap
    EndIf
  Next
  If first=0
    map[x2,y2]=10 ' add our previously recorded doorknob position
  EndIf
  update
  first=0
EndIf
n:+1 ' keep track of iterations

' {* END OF MAIN *}
Until n&gt;10000 ' exhaustive search, cannot add any more rooms

map[xx,yy]=6 ' plot stairs down on top of the very last object location scanned
' this will also be from the last room added every time

update
SetScale 3,3
SetColor 64,0,64
DrawText "COMPLETE !",384,354 ' show completion
SetColor 255,255,255
DrawText "COMPLETE !",380,350
Flip 0
WaitKey
End ' ready to exit

' &gt;&gt; UPDATE SCREEN
Function update()
Local i,j,m,x,y
  For i=0 Until 23
    For j=0 Until 31
      m=map[j,i]
      x=j*32+16 ; y=i*32
      If m=0 ' void
        SetColor 64,64,64
        DrawOval x+12,y+12,7,7
      ElseIf m&gt;0 And m&lt;10 ' room data
        SetColor 128,64,0
        DrawRect x,y,31,31
        If m=1 ' floor
          SetColor 192,192,192
          DrawOval x+12,y+12,7,7
        ElseIf m=2 ' critter
          SetColor 255,0,0
          DrawOval x+8,y+8,16,16
        ElseIf m=3 ' treasure
          SetColor 255,255,0
          DrawRect x+8,y+8,16,16
        ElseIf m=4 ' trap
          SetColor 255,0,255
          SetLineWidth 5
          DrawLine x+8,y+10,x+22,y+22
          DrawLine x+22,y+10,x+8,y+22
          SetLineWidth 1
        ElseIf m=5 Or m=6 ' stairs up and down
          SetColor 255,255,255
          If m=6 Then SetColor 0,0,0
          SetLineWidth 4
          DrawLine x+4,y+6,x+24,y+6
          DrawLine x+4,y+14,x+24,y+14
          DrawLine x+4,y+22,x+24,y+22
        EndIf
        SetColor 255,255,255
      ElseIf m=10 ' doorknob
        SetColor 128,128,128
        DrawRect x,y,31,31
        SetColor 0,0,0
        DrawRect x+2,y+2,27,27
      EndIf
    Next
  Next
  Flip 0
  WaitChar()
EndFunction

' &gt;&gt; GET DATA OF MAP EVEN IF OFF BOUNDARIES
Function getmap(x,y)
  If x&gt;=0 And y&gt;=0 And x&lt;=30 And y&lt;=22
    Return map[x,y]
  Else
    Return -1
  EndIf
EndFunction

' &gt;&gt; SIMPLE GET RANDOM NUMBER (0..a-1)
Function fnr(a)
  Return Rand(a)-1
EndFunction

' &gt;&gt; ENSURE NUMBER IS WITHIN SET RANGE
Function range(a,b,c)
  If a&gt;=b And a&lt;=c Then Return 1
EndFunction
</textarea><br><br>What I had planned for the critters is they don't move.<br><br>But if you are one square away from them (even diagonally), they attack. So while it might be possible to walk around some in a dungeon, in other rooms where they are positioned near the doorknob by or stairs chance, you won't have a choice but to fight them.<br><br>Also I have noticed new RPGs benefit the player with a bonus when they grab all the treasures or defeat all the critters on a level in a dungeon. That could certainly be applied to this mapper. <br><br></td></tr></table><br>
<a name="1298339"></a>

<a name="1298340"></a>

<a name="1298341"></a>

<a name="1298342"></a>

<a name="1298343"></a>

<a name="1298344"></a>

<a name="1298345"></a>

<a name="1298346"></a>

<a name="1298347"></a>

<a name="1298348"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I turned this into a Type, which should be much easier to include in any game you might want to make.<br>Basically all you have to do now is something like myDungeon = TDungeon.Create(Width,Height)<br>You can also specify the rooms minimum and maximum size, maximum room tiles (or skip that part by setting it to 0) and seed.<br>And once created, you can do myDungeon.Generate() to generate another dungeon.<br>Oh and it's now also SuperStrict.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

SuperStrict

SetGraphicsDriver(GLMax2DDriver())
Graphics(DesktopWidth() * 0.75, DesktopHeight() * 0.75)

Local myDungeon:TDungeon = TDungeon.Create(GraphicsWidth() / 32 - 1, GraphicsHeight() / 32, 3, 3, 7, 7)

While Not KeyHit(KEY_ESCAPE) And Not AppTerminate()
	If KeyHit(KEY_SPACE) Then myDungeon.Generate(GraphicsWidth() / 32 - 1, GraphicsHeight() / 32, 3, 3, 7, 7)
	
	Cls()
	Draw(myDungeon)
	Flip(1)
Wend
End

'RENDER FUNCTION
Function Draw(Dungeon:TDungeon)
	Local i:Int, j:Int, m:Int, x:Int, y:Int
	
	For i = 0 Until Dungeon.Height
	For j = 0 Until Dungeon.Width
		m = Dungeon.Map[j, i]
		x = j * 32 + 16 ; y = i * 32 + 8
		
		Select m
			Case Dungeon.GROUND
				SetColor 128, 64, 0
				DrawRect x, y, 31, 31
				SetColor 192, 192, 192
				DrawOval x + 12, y + 12, 7, 7
				
			Case Dungeon.CRITTER
				SetColor 128, 64, 0
				DrawRect x, y, 31, 31
				SetColor 255, 0, 0
				DrawOval x + 8, y + 8, 16, 16
				
			Case Dungeon.TREASURE
				SetColor 128, 64, 0
				DrawRect x, y, 31, 31
				SetColor 255, 255, 0
				DrawRect x + 8, y + 8, 16, 16
				
			Case Dungeon.TRAP
				SetColor 128, 64, 0
				DrawRect x, y, 31, 31
				SetColor 255, 0, 255
				SetLineWidth 5
				DrawLine x + 8, y + 10, x + 22, y + 22
				DrawLine x + 22, y + 10, x + 8, y + 22
				SetLineWidth 1
				
			Case Dungeon.STAIRS_UP, Dungeon.STAIRS_DOWN
				SetColor 128, 64, 0
				DrawRect x, y, 31, 31
				SetColor 255, 255, 255
				If m = Dungeon.STAIRS_DOWN Then SetColor 0, 0, 0
				SetLineWidth 4
				DrawLine x + 4, y + 6, x + 24, y + 6
				DrawLine x + 4, y + 14, x + 24, y + 14
				DrawLine x + 4, y + 22, x + 24, y + 22
				
			Case Dungeon.DOOR
				SetColor 128, 128, 128
				DrawRect x, y, 31, 31
				SetColor 0, 0, 0
				DrawRect x + 2, y + 2, 27, 27
			
			Default
				SetColor 64, 64, 64
				DrawOval x + 12, y + 12, 7, 7
		EndSelect
		
	Next
	Next
EndFunction

'DUNGEON TYPE
Type TDungeon
	Const GROUND:Int = 1
	Const CRITTER:Int = 2
	Const TREASURE:Int = 3
	Const TRAP:Int = 4
	Const STAIRS_UP:Int = 5
	Const STAIRS_DOWN:Int = 6
	Const DOOR:Int = 10
	
	Field Map:Int[,]
	Field Width:Int
	Field Height:Int
	Field Seed:Int
	
	Function Create:TDungeon(Width:Int, Height:Int, MinRoomWidth:Int = 2, MinRoomHeight:Int = 2, MaxRoomWidth:Int = 7, MaxRoomHeight:Int = 7, MaxRoomTiles:Int = 0, Seed:Int = -1)
		Local nD:TDungeon = New TDungeon
		nD.Generate(Width, Height, MinRoomWidth, MinRoomHeight, MaxRoomWidth, MaxRoomHeight, MaxRoomTiles, Seed)
		Return nD
	EndFunction
	
	Method Generate(Width:Int, Height:Int, MinRoomWidth:Int = 2, MinRoomHeight:Int = 2, MaxRoomWidth:Int = 7, MaxRoomHeight:Int = 7, MaxRoomTiles:Int = 0, Seed:Int = -1)
		Local i:Int, j:Int, h:Int, v:Int, xx:Int, yy:Int, x:Int, y:Int, x2:Int, y2:Int, ok:Int, d:Int, m:Int, first:Int = 1, n:Int, h2:Int, v2:Int
		
		'Store old seed
		Local oldSeed:Int = RndSeed()
		
		'Prepare some map stuff
		Self.Map = New Int[Width, Height]
		Self.Width = Width
		Self.Height = Height
		
		'Apply new seed
		If Seed &gt;= 0 Then Self.Seed = Seed Else Self.Seed = MilliSecs()
		SeedRnd(Self.Seed)
		
		Repeat ' {* MAIN *}
			Repeat
			  ok=1
			  Repeat
			    h = fnr(Width) ; v = fnr(Height) ' find a spot where a previous room exists
			  Until Self.Map[h, v] &gt; 0 Or first = 1 ' also accept if it's our first room
			  
			  If first=0
			    d = fnr(4) ' choose a random direction to try and build
				
			    Repeat
					h2 = h ; v2 = v
					If d=0 Then v:-1 ' Up
					If d=1 Then h:+1 ' Right
					If d=2 Then v:+1 ' Down
					If d=3 Then h:-1 ' Left
			    Until Self.getmap(h, v) &lt;= 0 ' loop until we hit a blank
				
			    If Self.getmap(h2, v2) &lt;&gt; GROUND Then ok = 0 ' make sure we don't draw a door near an object
			  EndIf
			Until ok
			
			x2=h ; y2=v ' mark this place for the door
			Repeat
			  x = Rand(MinRoomWidth, MaxRoomWidth) ; y = Rand(MinRoomHeight, MaxRoomHeight) ' we have size of new room to add
			Until x * y &lt;= MaxRoomTiles Or MaxRoomTiles &lt;= 0 ' no more than maxroomtiles per room
			
			If first=0
			  If d = 0 ' build room up
			    v=v-y
			    h=h-Rand(0,x-1)
			  ElseIf d=1 ' build room right
			    h=h+1
			    v=v-Rand(0,y-1)
			  ElseIf d=2 ' build room down
			    v=v+1
			    h=h-Rand(0,x-1)
			  ElseIf d=3 ' build room left
			    h=h-x
			    v=v-Rand(0,y-1)
			  EndIf
			EndIf
			
			ok=1
			For i=v-1 Until v+y+1 ' NOTE we are checking one tile
			  For j=h-1 Until h+x+1 ' OUTSIDE the area we will be adding a room
			    m = Self.getmap(j, i) ' to ensure it is not touching another room
			'    If m&lt;&gt;0 And (i=v-1 And j=h-1) Or (i=v-1 And j=h+x) Or (i=v+y And j=h-1) Or (i=v+y And j=h+x) Then m=0
			    If m&gt;0 Or (m=-1 And Not (i=v-1 Or i=v+y Or j=h-1 Or j=h+x))Then ok=0 ; Exit
			  Next ' ^ if on edge of screen make sure it is edge of room additionally
			  If ok=0 Then Exit
			Next
			
			If ok
			  n=0
			  For i=v Until v+y
			    For j=h Until h+x
			      Self.map[j, i] = GROUND ' this is a valid room, let's fill it in
			    Next
			  Next
			  
			  For i=0 Until x+y-2 ' always find a space where items can be added
			    Repeat
			      xx=h+fnr(x)
			      yy=v+fnr(y)
			    Until Self.map[xx, yy] = 1 And Abs(xx - x2) &gt; 0 And Abs(yy - y2) &gt; 0 ' item position cannot appear near doorknob
			    If first
			      Self.map[xx, yy] = STAIRS_UP ' this is the first room so record stairs up
			      Exit ' do not add anything more to this room
			    EndIf
			    If fnr(2)=0 ' there is only a 50% change of objects appearing in other rooms
			      Self.map[xx, yy] = CRITTER + fnr(3) ' tile is now critter, treasure, or trap
			    EndIf
			  Next
			  
			  If first=0 Self.map[x2, y2] = DOOR ' add our previously recorded doorknob position
			  first = 0
			EndIf
			n:+1 ' keep track of iterations
		' {* END OF MAIN *}
		Until n&gt;10000 ' exhaustive search, cannot add any more rooms
		
		Self.map[xx, yy] = STAIRS_DOWN ' plot stairs down on top of the very last object location scanned
		' this will also be from the last room added every time
		
		'Restore old seed
		SeedRnd(oldSeed)
	EndMethod
	
	' &gt;&gt; GET DATA OF MAP EVEN IF OFF BOUNDARIES
	Method GetMap:Int(x:Int, y:Int)
	  If x &gt;= 0 And y &gt;= 0 And x &lt;= Self.Width - 1 And y &lt;= Self.Height - 1
	    Return Self.map[x, y]
	  Else
	    Return -1
	  EndIf
	EndMethod
	
	' &gt;&gt; SIMPLE GET RANDOM NUMBER (0..a-1)
	Function fnr:Int(a:Int)
	  Return Rand(a)-1
	EndFunction
	
	' &gt;&gt; ENSURE NUMBER IS WITHIN SET RANGE
	Function range:Int(a:Int, b:Int, c:Int)
	  If a&gt;=b And a&lt;=c Then Return 1
	EndFunction
EndType</textarea> <br><br></td></tr></table><br>
<a name="1298354"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> It looks really good, Hezkore, except that it's not filling in every room. If there is a space remaining that is 3x3, 2x3, or 3x2, a room can be added.<br><br>Wait ... I'm reading more of the code.<br><br>OK, I see, you have made those as part of the dungeon creation arguments ! :D<br><br>Also SEED, so you can recreate the same dungeon each time. Marvelous for returning to a previously saved game.<br><br>I'll have to sit down sometime later and look over your wizardry. Need to run for now.<br><br>Excellent work ! <br><br></td></tr></table><br>
<a name="1298355"></a>

<a name="1298367"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cocopino</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice stuff dw817!<br>Thanks Hezkore for the Type conversion.<br><br>I made an extra Method for Hezkore's TDungeon:<br><pre class=code>
	Method CreateExtraDoor:Int()

		Local doorCreated:Int = 0
		Local triesLeft:Int = 9999

		'Find random tile of "nothing" suitable as door
		While Not doorCreated And triesLeft &gt; 0

			triesLeft:-1
			Local x:Int = Rand(2, width - 2)
			Local y:Int = Rand(2, height - 2)

			'Check tile = nothing, and no neighboring doors exist
			If Map[x, y] &lt;&gt; 0 Continue
			If Map[x - 1, y] = DOOR Or Map[x + 1, y] = DOOR Or Map[x, y - 1] = DOOR Or Map[x, y + 1] = DOOR Continue

			'Check east-west neighbors are ground
			If Map[x - 1, y] = GROUND And Map[x + 1, y] = GROUND doorCreated = 1
			If Map[x, y - 1] = GROUND And Map[x, y + 1] = GROUND doorCreated = 1
			
			If doorCreated
				Map[x, y] = DOOR
			End If
			
		Wend

		Return doorCreated
		
	End Method

</pre><br><br>It's not a very clean way to do this but it will add a new door randomly to an existing dungeon so multiple entrances can be made. To use:<br><pre class=code>
If KeyHit(KEY_D) myDungeon.CreateExtraDoor()
</pre> <br><br></td></tr></table><br>
<a name="1298572"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamStrange</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> mmm, Interesting<br><br>I've got loads of different types of dungeon creators, but after testing they are not quite what I was looking for.<br><br>So here's a different angle on this subject:<br>1. lets assume that a dungeon is a collection of connected rooms (corridors could be just another type of room)<br>2. ignoring all room sizes, types, etc. Let's assume a room can have up to four exits: 1 on each wall. and all rooms are logically connected. E.G. no rooms you can't get to<br><br>Using a simple grid we can map rooms and connections:<br><img src="https://vjointeractive.files.wordpress.com/2016/03/screen-shot-2016-03-06-at-14-20-08.png"><br><br>rooms are numbered so we can put keys, lock doors etc. a key is always found in a lower room number than the locked door!<br><br>Taking this grid, we could then decide room sizes and using a separate 'map' create the true rooms giving more sense to scale, size, etc?<br><br>As a quick cheat<br> - rooms with 2 exits become corridors<br> - rooms with 1 exit become storage or special rooms (treasure)<br> - rooms connected to special rooms could have a monster<br> - sprinkle some other stuff and you have a more logic dungeon to play with <br><br></td></tr></table><br>
<a name="1298573"></a>

<a name="1298575"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> That sounds like a really cool way to do it!<br>I'd love to see a dungeon generated from that method.<br>Is there any chance you'd share the code from the example above? <br><br></td></tr></table><br>
<a name="1298576"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well now if you like that, Hezkore, I did write a flawless maze generator. Flawless in the sense the corridors are not simple nor can you approach the exit except and only by following one path.<br><br>Here is the code:<br><br><a href="http://www.blitzbasic.com/Community/posts.php?topic=105489" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=105489</a><br><br>Using this you can see how to apply it to your 'room' idea above. :)<br><br>Failing that, I'm not certain how to approach building the method you have above for interconnected rooms. <br><br></td></tr></table><br>
<a name="1298649"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamStrange</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's the basic code for how I do it. the code is a bit dirty with no help, but should be pretty readable :)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">


superstrict

Graphics(800, 600)

local map:dMap = new dMap
map.init()

	Cls
	map.show
	Flip

While Not KeyHit(KEY_ESCAPE) And Not AppTerminate()
	If KeyHit(KEY_SPACE) Then
		cls
		map.setup()
		map.show()
		flip
	end if
	delay 10
Wend
End


type dExit
	field occupied:int
	field number:int
	
	field left:int
	field right:int
	field up:int
	field down:int
	
	method reset()
		number = 0
		occupied = false
		
		left = false
		right = false
		up = false
		down = false
	end method		
end type





type dMap
	field exits:dExit[8, 8]	
	field xstart:int
	field yStart:int
	field xEnd:int
	field yEnd:int
	
	field totalRooms:int
	
	method init()
		local k:int
		local l:int
		
		for k = 0 to 7
			for l = 0 to 7
				exits[k, l] = new dExit
			next
		next
		
		setup()
	end method
	
	method setup()
		local k:int
		local l:int
		
		repeat

			for k = 0 to 7
				for l = 0 to 7
					exits[k, l].reset()
				next
			next
	
			xstart = rand(0, 7)
			yStart = rand(5, 7)
			
			totalRooms = 1
	
			exits[xstart, ystart].occupied = true		
			exits[xstart, ystart].number = 1
		
			getExits(xStart, yStart, 20)
		until totalrooms &gt; 20

		exits[xend, yend].occupied = true
	end method
	
	
	method getExits(xst:int, yst:int, roomcount:int)
		if roomcount &lt; 1 then return
		
		local xcurr:int = xst
		local ycurr:int = yst
		local exitCount:int
		
		local ex:int = rand(0,4)
		
		if ex = 0 then
			xend = xcurr
			yend = ycurr
			return
		end if

		exits[xcurr, ycurr].occupied = true
		
		'pick exit count
		for exitCount = 1 to ex
			'pick direction
			select rand(0,3)
				case 0 'left
					if xcurr &gt; 0 then
						if exits[xcurr-1, ycurr].occupied = false then
							exits[xcurr, ycurr].left = true
							exits[xcurr-1, ycurr].right = true
							exits[xcurr-1, ycurr].occupied = true
							totalRooms :+ 1
							exits[xcurr-1, ycurr].number = totalRooms
							roomcount :- 1
							getExits(xcurr-1, ycurr, roomcount)
						end if
					end if
				case 1 ' right
					if xcurr &lt; 6 then
						if exits[xcurr+1, ycurr].occupied = false then
							exits[xcurr, ycurr].right = true
							exits[xcurr+1, ycurr].left = true
							exits[xcurr+1, ycurr].occupied = true
							totalRooms :+ 1
							exits[xcurr+1, ycurr].number = totalRooms
							roomcount :- 1
							getExits(xcurr+1, ycurr, roomcount)
						end if
					end if
				case 2 ' up
					if ycurr &gt; 0 then
						if exits[xcurr, ycurr-1].occupied = false then
							exits[xcurr, ycurr].up = true
							exits[xcurr, ycurr-1].down = true
							exits[xcurr, ycurr-1].occupied = true
							totalRooms :+ 1
							exits[xcurr, ycurr-1].number = totalRooms
							roomcount :- 1
							getExits(xcurr, ycurr-1, roomcount)
						end if
					end if
				case 3 ' down
					if ycurr &lt; 6 then
						if exits[xcurr, ycurr+1].occupied = false
							exits[xcurr, ycurr].down = true
							exits[xcurr, ycurr+1].up = true
							exits[xcurr, ycurr+1].occupied = true
							totalRooms :+ 1
							exits[xcurr, ycurr+1].number = totalRooms
							roomcount :- 1
							getExits(xcurr, ycurr+1, roomcount)
						end if
					end if
			end select
		next		
	end method
	
	
	method show()
		local xdiff:int = 60
		local ydiff:int = 60
		
		local x:int
		local y:int
		
		local xpos:int
		local ypos:int = 10
		
		for y = 0 to 7
			xpos = 10
			for x = 0 to 7
				if exits[x, y].number = 0 then
					setcolor 70,70,70
					drawline xpos, ypos, xpos+xdiff, yPos
					drawline xpos, ypos, xpos, yPos+ydiff

					drawline xpos, ypos+ydiff-1, xpos+xdiff, yPos+ydiff-1
					drawline xpos+xdiff-1, ypos, xpos+xdiff-1, yPos+ydiff
				else
					setcolor 70,70,70
	
					drawrect xpos, ypos, xdiff, ydiff
					setcolor 255,255,255

					drawline xpos, ypos, xpos+xdiff, yPos
					drawline xpos, ypos, xpos, yPos+ydiff

					drawline xpos, ypos+ydiff-1, xpos+xdiff, yPos+ydiff-1
					drawline xpos+xdiff-1, ypos, xpos+xdiff-1, yPos+ydiff

					if exits[x, y].left then drawrect xpos-5, ypos+20, 10, 20
					if exits[x, y].up then drawrect xpos+20, ypos-5, 20, 10
					if exits[x, y].down then drawrect xpos+20, ypos+yDiff-5, 20, 10

					drawtext exits[x, y].number, xpos+16, ypos+16
				end if
				
				xpos :+ xdiff
			next
			ypos :+ ydiff
		next
		
		setcolor 255,255,255
		drawtext "&lt;space&gt; for new map", 0, ypos
		
	end method
end type

</textarea> <br><br></td></tr></table><br>
<a name="1298653"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamStrange</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hezkore here is a sort of map produced from this concept<br><br>First the base connections:<br><img src="https://vjointeractive.files.wordpress.com/2016/03/screen-shot-2016-03-07-at-08-15-30.png"><br><br>and now the map (128 x 128):<br><img src="https://vjointeractive.files.wordpress.com/2016/03/screen-shot-2016-03-07-at-08-15-41.png"><br><br>As you can see I've also used colors to denote different areas:<br>grey = castle<br>light green = grassland<br>dark green = woods<br><br>This map design style does not feature doors, although they could be added <br><br></td></tr></table><br>
<a name="1298654"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamStrange</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> and here is a further refined version:<br><img src="https://vjointeractive.files.wordpress.com/2016/03/screen-shot-2016-03-07-at-08-28-25.png"> <br><br></td></tr></table><br>
<a name="1298875"></a>

<a name="1298876"></a>

<a name="1298877"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> AdamStrange, here is an idea. Is it possible to take your room idea and make it so it generates a floor like you might find in a hospital ?<br><br><a href="http://www.recentlydesigned.com/upload/dlp2gfjvaz867.cloudfront.net/product_photos/8430655/floor_20plan_original.jpg" target="_blank"><b>[IMAGE]</b></a>.<br><br>Thus some rooms have different sizes and the map element is configured to generate smaller rooms with few or no additional exits from initial larger rooms that have and start with many exits. <br><br></td></tr></table><br>
<a name="1298881"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamStrange</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> simple answer is yep. but...<br><br>I would look at hospital design first and come up with a generator that creates the sort of room plan that would say hospital.<br><br>but (again)...<br>If you took the last pic and viewed the grey central area. This does look like some form of community building plan (school, hospital, facility, jail), etc.<br><br>You take the generated (grey) area and then apply a room system on top of this? <br><br></td></tr></table><br>
<a name="1298924"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> I was giving some thought to this today, Adam, and perhaps it could be, the program recognizes one BIG room, the entire building itself, from there it adds smaller 'rooms' and rooms within those rooms.<br><br>That might meet the layout of 'hospital' or 'building.'<br><br>As for your question, yes, I think so, if I am understanding you correctly. Build rooms within rooms, not rooms linking to corridors as Rogue has it. You are making a map that is convenient for people to travel through and meet smaller rooms.<br><br>Here is a hospital map from the slasher/survival game, "Silent Hill."<br><br><a href="http://vignette4.wikia.nocookie.net/silent/images/1/18/Map_of_Alchemilla_General_Hospital.jpg/revision/latest?cb=20130228195939" target="_blank"><b>IMAGE</b></a>. <br><br></td></tr></table><br>
<a name="1299000"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamStrange</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> yep, it's more how you decide the rules and then code from that.<br><br>In this case we already have the two main elements:<br>1. the general shape (the grey building)<br>2. the base rule = single long main corridor with rooms off<br><br>you could further enhance the rules with:<br> - entrance and junctions have large open 'public' spaces<br> - large (grey) 'rooms' have theatres, etc <br><br></td></tr></table><br>
<a name="1299010"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hardcoal</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> I just want to say, That I love random map generators..<br>I wanted to have a 3D game generator,<br>so when you explore a game, its really a new world for you.<br><br>with all do respect to Virtual reality developments<br>I still will love having a 3D monitor with out glass <br>that will truly imitate a window. well.. Just dreaming :) <br><br></td></tr></table><br>
<a name="1299101"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> Glad you're liking it, HC.<br><br>I have yet to find some really small code to do good generation of 3D dungeons. Now years ago I wrote one that simulated 3D. That is, I worked off of a 2D map and displayed a pseudo-3D image.<br><br>Basically it was a series of straight and diagonal lines to give the illusion of distance.<br><br>Try out "Arcana" for the Super Nintendo. This is a great example of 3D graphics using only simple image plotting. <br><br></td></tr></table><br>
<a name="1299117"></a>

<a name="1299120"></a>

<a name="1299121"></a>

<a name="1299122"></a>

<a name="1299125"></a>

<a name="1299126"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#23">[#23]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've made a few 3D dungeon demos and 3D dungeon crawler games back in the day, with various different methods of generating dungeons and towns.<br>(And recently an Android/iOS bike game with some random world elements heh)<br>Here's one that used some Snes games graphics, but the worlds were randomly generated and used a simple array with a byte value for the layout.<br>All the tiles were automatically drawn with the correct edges and borders.<br>Even the houses were basically just a block (or any shape) of 5's in the array and the game made correct roofs and windows and you could seamlessly walk into them.<br>It was also possible to edit the levels from within the game online with your friends. :)<br><br><img src="https://dl.dropboxusercontent.com/u/2842751/Willcaster3_small.jpg"><br><br><br><img src="https://dl.dropboxusercontent.com/u/2842751/WillcasterMenu_small.jpg"><br><br>I have however never managed to get a level generator working to the point where I could just define things something like this:<br>Level.FieldsRooms=4<br>Level.WoodsRooms=6<br>Level.CastleRooms=6<br>Level.LockedCastleRooms=1 'Keys would be created for these rooms somewhere on the level BEFORE this part of the level<br>Level.Generate()<br><br>PlacePlayer(Level.RandomFieldRoom())<br>PlaceNPC(Level.RandomLockedCastleRoom())<br><br>Basically this means the player would start somewhere in the field area, walk through that part and get to the woods, walk through that and get to the castle.<br>And somewhere along the road a key would be found to unlock the locked castle room at the end containing the NPCs to save.<br>All on one map the blends the different room types together based on which room was added first and last.<br>It would be super easy to setup different kinds of levels and also have them fit the quest's story.<br><br>AdamStrange's grid method kinda revived this idea. <br><br></td></tr></table><br>
<a name="1299129"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#24">[#24]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sounds like you're ahead of me, Hezkore. The closest to true random city generation I have seen hearkens from Ultima.<br><br><img src="http://mikesrpgcenter.com/ultima3/maps/royalcity.png"><br><br>At one point I made an "Island Maker" which generated all the proper terrain and land curvature for housing icons of temples, dungeons, castles, cities, and villages. <br><br></td></tr></table><br>
<a name="1299209"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamStrange</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#25">[#25]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's something I was working on last year. It was 3d display out completely 2d and also completely random.<br><br>you can see the actual 2d map on the top right:<br><img src="https://vjointeractive.files.wordpress.com/2016/03/screen-shot-2016-03-10-at-05-51-47.png"><br><br><img src="https://vjointeractive.files.wordpress.com/2016/03/screen-shot-2016-03-10-at-05-53-59.png"><br><br><img src="https://vjointeractive.files.wordpress.com/2016/03/screen-shot-2016-03-10-at-05-52-25.png"><br><br>Everything is procedurally generated, from map layout, to room contents, decoration, etc. when you enter rooms, the outside slowly vanishes, likewise when you are outside the interiors vanish. exterior walls also appress/disappear depending on your location to them <br><br></td></tr></table><br>
<a name="1299853"></a>

<a name="1299854"></a>

<a name="1299855"></a>

<a name="1299856"></a>

<a name="1299858"></a>

<a name="1299859"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#26">[#26]</a></td></tr></table></td></tr><tr ><td class="posttext"> Looks real cool Adam!<br>Though I'd love an isometric orthographic projection instead heh.<br><br>I fooled around with your example Adam, modified it, and I've come up with this.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

SuperStrict

Framework brl.glmax2d
Import brl.retro

Graphics(800, 600)

'Our room layout
Global MyLayout:TRoomLayout = New TRoomLayout

'Setup how we want the layout
MyLayout.AddRoom(TRoomLayout.ROOM_FIELD, 3)			'We want 3 fields
MyLayout.AddRoom(TRoomLayout.ROOM_MINE, 1, True)	'One locked mine room
MyLayout.AddRoom(TRoomLayout.ROOM_MINE, 2)			'2 normal mine rooms
MyLayout.AddRoom(TRoomLayout.ROOM_CASTLE, 1, True)	'A locked castle room
MyLayout.AddRoom(TRoomLayout.ROOM_CASTLE, 2)		'2 normal castle rooms
MyLayout.AddRoom(TRoomLayout.ROOM_CASTLE, 1, True)	'And a final locked castle room
MyLayout.AddExtraRooms(3) 'Randomly add 3 extra rooms somewhere
MyLayout.Generate(1) 'Generate the layout with our seed

Cls()
MyLayout.show()
Flip(1)

While Not KeyHit(KEY_ESCAPE) And Not AppTerminate()
	If KeyHit(KEY_SPACE) Then
		'Do it all again!
		MyLayout.AddRoom(TRoomLayout.ROOM_FIELD, 3)
		MyLayout.AddRoom(TRoomLayout.ROOM_MINE, 1, True)
		MyLayout.AddRoom(TRoomLayout.ROOM_MINE, 2)
		MyLayout.AddRoom(TRoomLayout.ROOM_CASTLE, 1, True)
		MyLayout.AddRoom(TRoomLayout.ROOM_CASTLE, 2)
		MyLayout.AddRoom(TRoomLayout.ROOM_CASTLE, 1, True)
		MyLayout.AddExtraRooms(3)
		MyLayout.Generate()
		
		Cls()
		MyLayout.show()
		Flip(1)
	EndIf
Wend
End

Type TRoom
	Field Number:Int 'Room number (extras at the end)
	Field Occupied:Int 'Room is occupied
	Field Style:Int 'Style of the room (Check TRoomLayout)
	Field Key:Int 'Does this room have a key?
	Field Locked:Int 'Does this room have a locked door?
	Field IsExtra:Int 'Is room an extra room and not main path room?
	Field X:Int 'Rooms X position
	Field Y:Int 'Rooms Y position
	
	Field Left:Int 'Has exit left
	Field Right:Int 'Has exit right
	Field Up:Int 'Has exit up
	Field Down:Int 'Has exit down
	
	Method Reset() 'Reset room falgs
		Number = 0
		Occupied = False
		Style = 0
		Key = 0
		Locked = 0
		IsExtra = 0
		X = 0
		Y = 0
		
		Left = False
		Right = False
		Up = False
		Down = False
	EndMethod
EndType

Type TRoomLayout
	'Define different types of rooms
	Const ROOM_FIELD:Int = 0
	Const ROOM_MINE:Int = 1
	Const ROOM_CASTLE:Int = 2
	
	Field Seed:Int 'Seed for generator
	
	Field Rooms:TRoom[,] 'Out layout array
	Field StartX:Int 'Starting room X
	Field StartY:Int 'Starting room Y
	Field EndX:Int 'Ending room X
	Field EndY:Int 'Ending room Y
	Field CurrentX:Int 'Generators current X
	Field CurrentY:Int 'Generators current Y
	
	Field RoomsToAdd:TList = CreateList() 'List of all the rooms we want (not extra rooms)
	
	Field LastKeyID:Int 'Last key number we added
	Field LastKeyRoom:Int 'Last room that required a key
	Field LastDir:Int 'Last direction of the main path
	Field SameDirLength:Int 'Same direction on main path count
	Field MaxSameDir:Int = 2 'Maximum ammount of same direction on main path
	Field LastRoom:Troom 'Last room that was created (null if failed)
	
	Field TotalRooms:Int 'Total ammount of rooms created (including extras)
	Field ExtraRooms:Int 'Ammount of extra rooms we want to make
	Field WantedRooms:Int 'Ammount of rooms for the main path
	
	Method Resize(width:Int, height:Int) 'Resize the layout array
		Rooms = New TRoom[width, height]
		
		For Local X:Int = 0 Until Rooms.Dimensions()[0]
		For Local Y:Int = 0 Until Rooms.Dimensions()[1]
			Rooms[X, Y] = New TRoom
		Next
		Next
	EndMethod
	
	Method AddRoom(Style:Int, Ammount:Int = 1, Locked:Int = False) 'Add a room to the main path
		For Local i:Int = 0 Until Ammount
			Local nR:TRoom = New TRoom
			nR.Style = Style
			nR.Locked = Locked
			ListAddLast(RoomsToAdd, nR)
			WantedRooms:+1
		Next
	EndMethod
	
	Method AddExtraRooms(Ammount:Int = 1) 'Extra rooms at the end
		ExtraRooms = Ammount
	EndMethod
	
	Method Generate(Seed:Int = -1) 'Generate a new layout
		If WantedRooms &lt;= 0 Then Return
		
		'Apply new seed and store old one
		Local oldSeed:Int = RndSeed()
		If Seed &gt;= 0 Then Self.Seed = Seed Else Self.Seed = MilliSecs()
		SeedRnd(Self.Seed)
		
		'Decide size of layout array
		Local cels:Int = Ceil(WantedRooms / 1.5 + ExtraRooms / 1.5)
		'Apply the new layout size (if needed)
		If Not Rooms Or Rooms.Dimensions()[0] &lt; cels Or Rooms.Dimensions()[1] &lt; cels Then Resize(cels, cels)
		
		'This will be true if a layout was sucessfully made
		Local MadeLayout:Int
		
		'Do this until we've got a layout!
		Repeat
			'Reset all rooms
			For Local X:Int = 0 Until Rooms.Dimensions()[0]
			For Local Y:Int = 0 Until Rooms.Dimensions()[1]
				Rooms[X, Y].Reset()
			Next
			next
			
			'Start around center
			StartX = Floor(Rooms.Dimensions()[0] / 2) + Rand(-1, 1)
			StartY = Floor(Rooms.Dimensions()[1] / 2) + Rand(-1, 1)
			
			'Reset stuff
			TotalRooms = 1
			LastKeyRoom = 2
			LastKeyID = 0
			LastDir = -1
			SameDirLength = 0
			
			'The starting room
			Rooms[StartX, StartY].Occupied = True
			Rooms[StartX, StartY].Number = 1
			Rooms[StartX, StartY].Style = TRoom(RoomsToAdd.ValueAtIndex(TotalRooms - 1)).Style
			Rooms[StartX, StartY].X = StartX
			Rooms[StartX, StartY].y = StartY
			
			LastRoom = Rooms[StartX, StartY]
			
			CurrentX = StartX
			CurrentY = StartY
			
			'Made some rooms!
			Repeat
				If Not GenerateRoom(CurrentX, CurrentY) Then Exit
			Until TotalRooms = WantedRooms 'Continue until we've got the ammount we want
			
			If TotalRooms = WantedRooms Then MadeLayout = True
		Until MadeLayout
		
		'The last room
		EndX = CurrentX
		EndY = CurrentY
		
		'Now we add some extra rooms where keys are (if possible)
		For Local X:Int = 0 Until Rooms.Dimensions()[0]
		For Local Y:Int = 0 Until Rooms.Dimensions()[1]
			'Is this a fitting key room?
			If Rooms[X, Y].Occupied And Rooms[X, Y].Key &gt; 0 And Rooms[X, Y].IsExtra &lt; 2 Then
				GenerateRoom(X, Y, Rooms[X, Y].Style)
				If LastRoom Then 'A room was made!
					LastRoom.Key = Rooms[X, Y].Key 'Place key in new room
					Rooms[X, Y].Key = 0 'Remove old key
					LastRoom.IsExtra = Rooms[X, Y].IsExtra + 1
					ExtraRooms:+1 'So these key rooms aren't counter as extra rooms
				EndIf
			EndIf
		Next
		Next
		
		'And lastly, we add some extra rooms randomly
		Local randomRoom:Int
		Local room:Troom
		While TotalRooms &lt; WantedRooms + ExtraRooms 'Make the ammount of extra rooms specified
			randomRoom = Rand(1, TotalRooms) 'A random room to extend
			For room = EachIn Rooms
				'Is this a fitting room?
				If room.Occupied And room.Number = randomRoom And room.Key &lt;= 0 Then
					GenerateRoom(room.X, room.Y, room.Style)
					If LastRoom Then LastRoom.IsExtra = room.IsExtra + 1 'A room was made!
					Exit 'Repeat
				EndIf
			Next
		Wend
		
		'Reset
		ExtraRooms = 0
		WantedRooms = 0
		RoomsToAdd.Clear()
		
		'Restore old seed
		SeedRnd(oldSeed)
	EndMethod
	
	
	Method GenerateRoom:Int(x:Int, y:Int, forceStyle:Int = -1) 'Generate a new room
		Local MadeRoom:Int 'This will be true if a room was made
		Local Dir:Int 'Direction of the new room
		Local Attempts:Int = 100 'Maximum attempts to create the new room
		
		'Reset last room (so this is null if a room wasn't made)
		LastRoom = Null
		
		'Let's do this!
		Repeat
			'Get a fitting direction for out new room
			'Not too far int he same direction...
			Repeat
				Dir = Rand(0, 3)
			Until SameDirLength &gt;= MaxSameDir And Dir &lt;&gt; LastDir Or SameDirLength &lt; MaxSameDir
			
			'This counts as an attempt!
			Attempts:-1
			
			'Extend in the direction we got
			Select Dir
				Case 0
					'Left
					If x &gt; 0 And Not Rooms[x - 1, y].occupied Then
						Rooms[x, y].Left = True
						Rooms[x - 1, y].Right = True
						x:-1
						MadeRoom = True
					EndIf
					
				Case 1
					'Right
					If x &lt; Rooms.Dimensions()[0] - 1 And Not Rooms[x + 1, y].occupied Then
						Rooms[x, y].Right = True
						Rooms[x + 1, y].Left = True
						x:+1
						MadeRoom = True
					EndIf
					
				Case 2
					'Up
					If y &gt; 0 And Not Rooms[x, y - 1].occupied Then
						Rooms[x, y].Up = True
						Rooms[x, y - 1].Down = True
						y:-1
						MadeRoom = True
					EndIf
					
				Case 3
					'Down
					If y &lt; Rooms.Dimensions()[1] - 1 And Not Rooms[x, y + 1].occupied Then
						Rooms[x, y].Down = True
						Rooms[x, y + 1].Up = True
						y:+1
						MadeRoom = True
					EndIf
					
			EndSelect
		Until MadeRoom Or Attempts &lt;= 0
		
		'A room was made!
		If MadeRoom Then
			'Remember this room for later
			LastRoom = Rooms[x, y]
			
			'Store what dir we used
			If LastDir = Dir Then SameDirLength:+1 Else SameDirLength = 0
			LastDir = Dir
			
			'Make it occupied
			Rooms[x, y].Occupied = True
			
			'Store X and Y
			Rooms[x, y].x = x
			Rooms[x, y].y = y
			
			'Set room number
			TotalRooms:+1
			Rooms[x, y].Number = TotalRooms
			
			'Is there a forced style for this room?
			If forceStyle &gt;= 0 Then
				'Yep!
				Rooms[x, y].Style = forceStyle
			Else
				'Nope, use whatever has been defined at start
				Rooms[x, y].Style = TRoom(RoomsToAdd.ValueAtIndex(TotalRooms - 1)).Style
			EndIf
			
			'This part won't trigger for extra rooms
			If TotalRooms - 1 &lt; RoomsToAdd.Count() Then
				'Is this a locked room?
				If TRoom(RoomsToAdd.ValueAtIndex(TotalRooms - 1)).Locked &gt; 0 Then
					'It is a locked room!
					LastKeyID:+1
					Rooms[x, y].Locked = LastKeyID
					
					'Look for an existing room to place our key in!
					'Look somewhere between rooms we've got so far and the last key room
					Local keyInRoom:Int = Rand(LastKeyRoom, TotalRooms - 1)
					
					'Find the room...
					For Local R:TRoom = EachIn Rooms
						'Is this it?!
						If R.Occupied And R.Number = keyInRoom Then R.Key = LastKeyID 't'is!
					Next
					
					'Remember last key room
					LastKeyRoom = Rooms[x, y].Number + 1
				EndIf
			EndIf
			
			'Update current X and Y coords
			CurrentX = x
			CurrentY = y
			
			'A room was made!
			Return True
		EndIf
		
		'Too many attempts were made to create the room...
		Return False
	EndMethod
	
	
	Method Show()
		Local xdiff:Int = 60
		local ydiff:int = 60
		
		local x:int
		local y:int
		
		Local xpos:Int
		local ypos:int = 10
		
		For y = 0 Until Rooms.Dimensions()[1]
			xpos = 10
			For x = 0 Until Rooms.Dimensions()[0]
				If Rooms[x, y].number = 0 Then
					setcolor 70,70,70
					drawline xpos, ypos, xpos+xdiff, yPos
					drawline xpos, ypos, xpos, yPos+ydiff
					
					DrawLine xpos, ypos + ydiff - 1, xpos + xdiff, ypos + ydiff - 1
					drawline xpos+xdiff-1, ypos, xpos+xdiff-1, yPos+ydiff
				Else
					Select Rooms[x, y].style
						Case ROOM_FIELD SetColor 128, 128, 0
						Case ROOM_MINE SetColor 0, 70, 70
						Default SetColor 70, 70, 70
					EndSelect
					
					drawrect xpos, ypos, xdiff, ydiff
					setcolor 255,255,255
					
					drawline xpos, ypos, xpos+xdiff, yPos
					drawline xpos, ypos, xpos, yPos+ydiff
					
					drawline xpos, ypos+ydiff-1, xpos+xdiff, yPos+ydiff-1
					drawline xpos+xdiff-1, ypos, xpos+xdiff-1, yPos+ydiff
					
					If Rooms[x, y].Left Then DrawRect xpos - 5, ypos + 20, 10, 20
					If Rooms[x, y].up Then DrawRect xpos + 20, ypos - 5, 20, 10
					If Rooms[x, y].down Then DrawRect xpos + 20, ypos + ydiff - 5, 20, 10
					
					DrawText Rooms[x, y].number, xpos + 16, ypos + 16
					If Rooms[x, y].Locked &gt; 0 Then DrawText "Lock" + Rooms[x, y].Locked, xpos + 10, ypos + 16 - 13
					If Rooms[x, y].Key &gt; 0 Then DrawText "Key" + Rooms[x, y].Key, xpos + 10, ypos + 16 + 13 + 12
					If x = StartX And y = StartY Then DrawText "Start", xpos + 10, ypos + 16 + 13
					If X = EndX And Y = EndY Then DrawText "End", xpos + 16, ypos + 16 + 13
				EndIf
				
				xpos :+ xdiff
			Next
			ypos:+ydiff
		Next
		
		SetColor 255, 255, 255
		DrawText "&lt;space&gt; for new map", 0, ypos
	EndMethod
EndType</textarea><br><br>It basically lets you define how you want the layout to be, and then it generates it for you based on a seed.<br>You can specify extra treasure rooms and how many locked doors you want, it'll place keys for you too.<br>It's real easy, just do AddRoom(Type, Ammount, Locked) then set how many extra/treasure rooms you want AddExtraRooms(3)<br>Then just called Generate()<br><br>Here's the first dungeon the example generates:<br><br><img src="http://puu.sh/nGPFH/2ea6fb4091.png"><br><br>As you can see, there are 3 locked doors and keys for each door placed around the map.<br>It tries to not place keys along the main path, but sometimes it kinda has to heh.<br>There are also 3 extra rooms (17, 18, 19) which could contain treasures.<br><br>Does anyone have any tips on how I could "carve" out the rooms in a good way?<br>I want the rooms to be fairly small, like what dw817 has posted above. <br><br></td></tr></table><br>
<a name="1299869"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#27">[#27]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hekzore, you might wanna look at a game called Haunted House for the Atari 2600. They developed rooms in such a way that you had to travel maximum distance in order to find keys to open the locked doors.<br><br>I imagine it is similar to a method called, "Astar."<br><br>https://en.wikipedia.org/wiki/A*_search_algorithm<br><br>One of the seriously good advantages of Astar is the ability to click the mouse anywhere in a maze and the player will work towards that location despite having to travel away from the set destination.<br><br>You could also use Astar to determine maximum travel time to find keys.<br><br>AdamStrange, that's quite a bit you have there. I'm not one for Isolinear graphics so you might provide both this and standard square mapping to cater to people who like one or the other.<br><br>and truly Isolinear is not needed. It was a raster pixel cheat years ago in order to give a psuedo-3-dimensional depth to sprite images.<br><br>Clearly you have perfect 3-dimensional graphics before you. <br><br></td></tr></table><br>
<a name="1299878"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamStrange</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#28">[#28]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hekzore - great mod :)<br><div class="quote"> Does anyone have any tips on how I could "carve" out the rooms in a good way? <br></div><br>Lets assume you are using the routine you showed above.<br><br>Using the data you now have. setup a different larger map (this will be your ground)<br>for each room place it on the new map with different room sizes<br>where there is a door connecting the oldmap, make this a connecting single corridor - straight to begin with, then once it works you can start offsetting adding turns, etc<br><br>Alternatively, take the above map data. and give a size to each room. if a room has only 2 entrances, make it a corridor/angled corridor <br><br></td></tr></table><br>
<a name="1299942"></a>

<a name="1299943"></a>

<a name="1299944"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#29">[#29]</a></td></tr></table></td></tr><tr ><td class="posttext"> @dw817 Yeah I gave A* a go in the past.<br>Planning on using it for enemies (maybe).<br>But I don't want keys to be that hard to find, but if you do you could change the line<pre class=code>Rooms[X, Y].IsExtra &lt; 2</pre>to a higher value which would allow for a longer path to the key.<br><br><br>I've been trying to carve out my level since yesterday, and I'm starting to get something decent going.<br><br><img src="http://puu.sh/nHSvf/3747ee5db5.png"><br><br>The level above starts in the forest. (blue is water)<br>It then goes through a cave and you come out in a swamp. (green is slime)<br>The orange part after that is a "house", which then leads to another swamp.<br>Then there's a castle, and the blue after that is some sort of "ice" cave.<br>At the end there's a "hell" area. (orange is lava)<br><br>Still working on it, and I need to figure out how to best place doors, locked and unlocked ones! <br><br></td></tr></table><br>
<a name="1299949"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#30">[#30]</a></td></tr></table></td></tr><tr ><td class="posttext"> Managed to get doors working.<br>A door is placed at the edge of a room, but only if there's one tile of floor surrounded by walls and no other doors directly next to it.<br>A locked room will have walls placed at the entrance with a locked door in the middle.<br>Some rooms, like fields and swamps will never naturally have doors, but if you decide to lock a field or swamp room, the correct walls will still be placed along with a locked door.<br><br>Doors are the brown tiles.<br>Unlocked doors are black in the middle.<br><br><img src="http://puu.sh/nI055/44fd04df99.png"> <br><br></td></tr></table><br>
<a name="1299968"></a>

<a name="1299970"></a>

<a name="1299971"></a>

<a name="1299974"></a>

<a name="1299975"></a>

<a name="1299982"></a>

<a name="1299984"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#31">[#31]</a></td></tr></table></td></tr><tr ><td class="posttext"> Pretty cool design, Hekzore. The island maker I had in mind blended all of these though, not separate. Hmm ... Guess I need to write it out.<br><br><img src="http://www.writerscafe.org/uploads/rte/6d76fab13478e480e4f8adce3e97a50b.png"><br><br>Here is an island generated in a 255x255 area. More than enough to explore I think if you zoom it to RPG tiles to view only 15x9 squares at any one time like I have sorta like HERE:<br><br><a href="http://www.blitzbasic.com/Community/post.php?topic=105731&amp;post=1293684" target="_blank">http://www.blitzbasic.com/Community/post.php?topic=105731&amp;post=1293684</a><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'     ________________________________________
'    //                                     //
'   // Island Map Maker                    //
'  // Version, "Genesis"                  //
' // Written by David W (dw817) 03/15/16 //
'//_____________________________________//
'
Strict
SeedRnd MilliSecs() ' always get random results

Global map[255,255] ' size of map

Local i,j,xa#,xa2#,z#,r
SetGraphicsDriver GLMax2DDriver(),0 ' front buffer graphics
Graphics 1024,768

Repeat ' {* MAIN *}

For i=0 To 254
  For j=0 To 254
    map[j,i]=0 ' needed since BlitzMAX does not have a FillArray()
  Next
Next

z=fnr(250)+150 ' shape of island
Repeat
  r=fnr(1000)
  xa=Sin(254*z/100+r)*8.0
  xa2=Sin(z/100+r)*8.0
  z:+1
Until xa&gt;0 And xa2&gt;0 ' make sure island isn't pointed

For i=0 To 254
  xa=Sin(i*z/100+r)*12.0+fnr(4)-2 ' sorta scraggly
  xa:+16
  For j=0 To xa
    setmap i,j,1 ' draw the shape of the island
    setmap j,i,1
    setmap 254-i,254-j,1
    setmap 254-j,254-i,1
  Next
Next


maketerrain 7000,512,5000,2 ' drop forest
maketerrain 6000,32,10000,4 ' drop mountains
maketerrain 2000,512,20000,6 ' drop desert
maketerrain 4000,32,100000,5 ' drop streams

For i=0 To 254
  For j=0 To 254
    If fnr(8)=0 And map[j,i]=0 Then map[j,i]=3 ' random scrub
  Next
Next

update ' show screen

Forever ' {* END OF MAIN *}

' &gt;&gt; SHOW OUR WORK AND WAIT A KEYPRESS
Function update()
Local i,j,m
  Cls
  For i=0 To 254
    For j=0 To 254
      colr3 "363" ' grass
      m=map[j,i]
      If m=1 Then colr3 "008" ' water
      If m=2 Then colr3 "141" ' forest
      If m=3 Then colr3 "251" ' scrub
      If m=4 Then colr3 "530" ' mountains
      If m=5 Then colr3 "068" ' streams
      If m=6 Then colr3 "760" ' desert
      DrawRect j*2,i*2,2,2
    Next
  Next
  Flip 0 ' show our work
  Repeat
    Delay 1
    If KeyDown(27) Then End ' END PROGRAM
  Until KeyDown(32) ' continue on SPACEBAR
EndFunction

' &gt;&gt; ROUTINE TO ADD TERRAIN, VALUES EFFECT 'CLINGING' ABILITY
Function maketerrain(a,b,c,d)
Local i,n,x,y
  For i=0 Until a
    n=0
    Repeat
      x=fnr(255) ; y=fnr(255) ' pure random tile
      n:+1
    Until map[x,y]=0 And (i Mod b=0 Or near(x,y,d)=1 Or n=c) ' meets what we are looking for ?
    map[x,y]=d ' change that tile
  Next
EndFunction

' &gt;&gt; SET MAP TILE EVEN IF OFF EDGES
Function setmap(a,b,c)
  If a&gt;=0 And a&lt;=254 And b&gt;=0 And b&lt;=254 Then map[a,b]=c
EndFunction

' &gt;&gt; RETRIEVE MAP TILE EVEN IF OFF EDGES
Function getmap(a,b)
Local r=-1
  If a&gt;=0 And a&lt;=254 And b&gt;=0 And b&lt;=254 Then r=map[a,b]
  Return r
EndFunction

' &gt;&gt; SCAN TO ENSURE THERE IS A MATCHING TILE NEARBY
Function near(a,b,c)
Local i,j
  For i=a-1 To a+1
    For j=b-1 To b+1
      If getmap(j,i)=c And (i=a Or j=b) Then Return 1
    Next
  Next
  Return 0
EndFunction

' &gt;&gt; SIMPLE 3-CHARACTER COLOR 000 (BLACK) TO 888 (WHITE)      
Function colr3(a$)
  SetColor Min((a$[0]-48)*32,255),Min((a$[1]-48)*32,255),Min((a$[2]-48)*32,255)
EndFunction

' &gt;&gt; RETURN RANDOM 0 TO A-1
Function fnr(a)
  Return Rand(a)-1
EndFunction
</textarea><br><br>This was something similar to what I had done back on the Apple ][, but it was a resolution of 40x40 pixels w 16 colors to each, the limit the Apple could do at the time.<br><br>Place dungeons next to mountains, place cities on grass, place castles nearby cities. Place towers and caves in desert or forest.<br><br>I think it's a pretty good way to build an island, maybe one of you guys can help me with the island shape code. For the most part it builds nice looking islands, occasionally though the shape doesn't look right. <br><br></td></tr></table><br>
<a name="1299994"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamStrange</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#32">[#32]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Still working on it, and I need to figure out how to best place doors, locked and unlocked ones!  <br></div><br>here's a dirty way to deal with locked doors and keys:<br>- you now have the block data, map data and doors<br>- you also have a number (the block data number) where you placed the door<br>- you can put the corresponding key (key 3 opens door 3, etc) in any room which has a block number LESS than the loo block number<br><br>Reread that a few times, and it will suddenly make sense ;) <br><br></td></tr></table><br>
<a name="1299999"></a>

<a name="1300005"></a>

<a name="1300006"></a>

<a name="1300007"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Casaber</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#33">[#33]</a></td></tr></table></td></tr><tr ><td class="posttext"> Check out this island generator, I think it looks really good and simple.<br><a href="https://gillesleblanc.wordpress.com/2012/10/16/creating-a-random-2d-game-world-map/" target="_blank">https://gillesleblanc.wordpress.com/2012/10/16/creating-a-random-2d-game-world-map/</a><br><br>EDIT I forgot this but all online QBASIC compilers I found refused to compile it so I dont know how it looks.<br>I don't think it's the codes fault as I could not even compile a PRINT statement on them. Might be good island code.<br>http://www.network54.com/Forum/190883/thread/1199451995/+(View+Thread) <br><br></td></tr></table><br>
<a name="1300056"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#34">[#34]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Casaber.<br><br>Here is the results of the QBasic program.<br><br><img src="http://www.writerscafe.org/uploads/rte/8e8ca2c6b687215af0c995f346b46f42.png"><br><br>I kinna see what they are doing. They are creating a scribble, calling it highest peak of mountains, and then working their way around it to eventual tiles of beach and water. I might be able to do something like this myself. <br><br></td></tr></table><br>
<a name="1300063"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamStrange</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#35">[#35]</a></td></tr></table></td></tr><tr ><td class="posttext"> alternatively:<br>- an island is land that is above sea level (0)<br>- hills/land is anything that is above 0<br>so...<br>1. put a few random heights (in the range 4 to 10) at random positions on the map (these will be your highest points)<br>2. now cycle through each map position. the height being (current height + the height next to it / 2) if this is lower than the current height<br>3. do this a few times or so to spread the heights<br>4. apply some other sculpting if needed<br><br>you should now have a height map<br><br>You can use this to generate trees, water, features, whatever :) <br><br></td></tr></table><br>
<a name="1300065"></a>

<a name="1300066"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Casaber</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#36">[#36]</a></td></tr></table></td></tr><tr ><td class="posttext"> That island came out good I think. But I prefer the other link, with randomising tiles using heightmap, creating heightmap just like AdamStrange said.<br><br>The only thing left to do would be to decide what levels should become what tiles. In their example they have so some beach tiles are missing I noticed, that would be the first thing that I would change. <br><br></td></tr></table><br>
<a name="1300095"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#37">[#37]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi AdamStrange &amp; Casaber:<br><br>The problem I see is, where does SWAMP or DESERT fit in ?<br><br>I can see how you have mountains &gt; forest &gt; grass &gt; beach &gt; water, but how would you introduce these other elements ?<br><br>Sure you can have mountains &gt; desert &gt; swamp &gt; forest &gt; grass &gt; beach &gt; water, but if you did this then it would always tell the player what terrain to expect. They would always know swamp would be near desert and forest would always be near swamp.<br><br>And in the real world, that just doesn't happen always this way.<br><br>Now I know my 1st stage Island Maker is not the best, but it can introduce any number of elements randomly.<br><br>Let me see what code I can come up with this HEIGHT method ... <br><br></td></tr></table><br>
<a name="1300099"></a>

<a name="1300100"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#38">[#38]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here is my attempt at it. I'm not very happy with the results though:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' Island Map Maker - Version "Fractal"

Strict
SeedRnd MilliSecs() ' always get random results

Global map[255,255],sphere[255,255] ' size of map
Local i,j,k,m,n,o,x,y,r

SetGraphicsDriver GLMax2DDriver(),0 ' front buffer graphics
Graphics 1024,768
SetBlend alphablend

Repeat ' {* MAIN *}

For i=0 Until 254
  For j=0 Until 255
    map[j,i]=0 ' needed since BlitzMAX does not have a FillArray()
  Next
Next
x=127 ; y=127 ' Always start in middle
For i=0 To 99999
  m=map[x,y]+16
  If m&gt;255 Then m=255 ' build only so high
  map[x,y]=m ' build up height
  r=fnr(4)
  x=x-(r=0)+(r=1) ; y=y-(r=2)+(r=3) ' travel in random direction
  If x&lt;1 Or y&lt;1 Or x&gt;253 Or y&gt;253 Then x=127 ; y=127 ' reset back to center if out of bounds
Next

For i=0 Until 64 ' only layer 64 times
  For j=2 Until 253
    For k=2 Until 253
      o=map[k,j]
      If o&gt;16 ' make sure there is terrain here
        For m=-1 To 1
          For n=-1 To 1
            If map[k+n,j+m]=0 Then map[k+n,j+m]=-(o-15-fnr(2)) ' filter it out
          Next
        Next
      EndIf
    Next
  Next
  For j=2 Until 253
    For k=2 Until 253
      map[k,j]=Abs(map[k,j]) ' mark for use again
    Next
  Next
Next
update

Forever ' {* END OF MAIN *}

' &gt;&gt; SHOW OUR WORK AND WAIT A KEYPRESS
Function update()
Local i,j,k,m
  Cls
  For i=0 Until 255
    For j=0 Until 255
      m=map[j,i]
      If range(m,221,255) Then colr3 "888" ' impassable mountains
      If range(m,181,220) Then colr3 "530" ' mountains (passable by balloon or airship)
      If range(m,141,180) Then colr3 "760" ' desert (uses up player water)
      If range(m,111,140) Then colr3 "353" ' swamp (hard to cross)
      If range(m,041,110) Then colr3 "141" ' forest (thicker to cross)
      If range(m,021,040) Then colr3 "080" ' grass (easy motion)
      If range(m,011,020) Then colr3 "484" ' scrub (rough terrain)
      If range(m,001,010) Then colr3 "068" ' passable streams
      If range(m,000,000) Then colr3 "008" ' water (req boat)
      DrawRect j*2,i*2,2,2
    Next
  Next
  Flip 0 ' show our work
  k=WaitChar()
  If k=27 Then End ' END PROGRAM
EndFunction

' &gt;&gt; SIMPLE 3-CHARACTER COLOR 000 (BLACK) TO 888 (WHITE)      
Function colr3(a$)
  SetColor Min((a$[0]-48)*32,255),Min((a$[1]-48)*32,255),Min((a$[2]-48)*32,255)
EndFunction

' &gt;&gt; RETURN RANDOM 0 TO A-1
Function fnr(a)
  Return Rand(a)-1
EndFunction

Function range(a,b,c)
  If a&gt;=b And a&lt;=c Then Return 1
EndFunction
</textarea><br><br>I'm also getting weird rectangles to appear. <br><br></td></tr></table><br>
<a name="1300109"></a>

<a name="1300110"></a>

<a name="1300116"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Casaber</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#39">[#39]</a></td></tr></table></td></tr><tr ><td class="posttext"> Why does it look so boxy? Does it use box filtering instead of Gaussian? Shouldn't those be very round and similar to Gaussian?<br><br>About the desert I think.. (or Im sure but I have no algorithm in my head yet) that desert fits in on the other side of the mountains<br> (Water  &lt;mountains&gt; desert).  And then on the other side of mountains, towards the water there's is heavy rain so rivers should be formed outwards there.<br><br>Maybe these rules could do it:<br><br>- Tiles at sea transients (or oceans / rivers) should probably be : grass / sand / gravel / mountains, and river and smaller watersources might have forrests and swamps very next to them though. So Im thinking; the more water that is available - the less likely that green comes right along without transients inbtween?<br><br>- Forrest that are close To water might have swamps depending on how much rock the ground has or some other kind of grounddensity?<br><br>- Desert always needs mountains to separate them from condensation and moist air. <br><br></td></tr></table><br>
<a name="1300120"></a>

<a name="1300121"></a>

<a name="1300122"></a>

<a name="1300123"></a>

<a name="1300124"></a>

<a name="1300125"></a>

<a name="1300126"></a>

<a name="1300127"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#40">[#40]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Casaber:<br><br>It appears blocky because it is. The map is 255x255 in size. I am stretching that so it appears 510x510 pixels, doubling pixel size for appearance.<br><br>In the actual game, it would appear 50% alpha, one pixel per tile, and in the top-right-hand corner and also only show parts of the map the player has explored. The player must discover the rest.<br><br>So here is a version I call "Blossom And Preview." I'm very content w it and will use this for my random island creation needs unless something better arrives. Easy to fix margins too, right now I know it's a bit oversize. It could also use an overall outline to mark the edge of the water to the surf.<br><br><img src="http://www.writerscafe.org/uploads/rte/fc7238a9cd39f458e6321530200d0a75.png"><br><br>Once the map is drawn, use I J K M keys to navigate. Understand that random combat could be initiated about ever 3-4 steps, mapping will animate very slowly, about 2-tiles moved across per second, all there will be MANY structures for the player to find.<br><br>Towns, Castles, Dungeons, Towers, Crypts, Graves, Caves, Villages, Tombs, Temples, Caravans, and Campsites.<br><br>Some cities could also be placed right on the beach near the water so they have boats for lease to travel to other islands when the player is wealthy enough to afford this endeavor.<br><br>Critters could match terrain. Find Sand Crabs, Jellyfish, and Dark Shells near the beach.<br><br>Zombies, Voo Doo Dolls, and Slimes near the swamp.<br><br>Mummies, Evil Scarabs, and Vultures near the desert, etc.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'     ________________________________________
'    //                                     //
'   // Island Map Maker                    //
'  // Version, "Blossom &amp; Preview"        //
' // Written by David W (dw817) 03/16/16 //
'//_____________________________________//
'

Strict
SeedRnd MilliSecs() ' always get random results

Global img_map:TImage=CreateImage(255,255)
Global miniflag,k$,map[255,255] ' size of map
Local i,j,kk,m,n,x,y,z

SetGraphicsDriver GLMax2DDriver(),0 ' front buffer graphics
Graphics 1024,768
SetBlend alphablend

For i=0 Until 255
  For j=0 Until 255
    map[j,i]=0 ' needed since BlitzMAX does not have a FillArray()
  Next
Next
x=64+Rand(127) ; y=64+Rand(127) ' Always start in middle

For i=0 To 777
  map[x,y]=8 ' set for impassable mountain
  map[255-x,255-y]=8 ' mirror it
  x=x+fnr(3)-1 ; y=y+fnr(3)-1 ' move in random direction
  If x&lt;32 Or y&lt;32 Or x&gt;255-32 Or y&gt;255-32 Or i Mod 177=76 Then x=64+Rand(127) ; y=64+Rand(127) ' reset back to center if out of bounds
Next

For z=8 To 1 Step -1 ' start from ice peaks to warm beaches
  For i=0 To 10000-z*1000 ' rare ice peaks, more beaches
    Repeat
      x=1+fnr(253) ; y=1+fnr(253) ' pick a random tile
      m=map[x,y]
    Until m=z ' is it the one we are looking for ?
    For j=-1 To 1
      For kk=-1 To 1
        n=0
        If z=8 Or fnr(56-z*8)=0 Then n=1 ' decrease it by one if meets this query
        If map[x+kk,y+j]=0 Then map[x+kk,y+j]=z-n ' plot it
      Next
    Next
  Next
update ' hit a key to show each stage of creation
Next

x=127 ; y=127

getminimap

Repeat ' {* MAIN *}
  drawmap x,y
  k$=Chr$(WaitChar())
  If k$="i" Then y:-1
  If k$="j" Then x:-1
  If k$="k" Then x:+1
  If k$="m" Then y:+1
  If k$=Chr$(27) Then End
Forever ' {* END OF MAIN *}

Function getminimap()
  miniflag=1
  update
  miniflag=0
EndFunction

Function drawmap(x,y)
Local i,j,m,h,v
  Cls
  For i=-10 To 10
    For j=-10 To 10
      h=j+x ; v=i+y
      colr3 "008"
      If h&gt;=0 And h&lt;=254 And v&gt;=0 And v&lt;=254
        m=map[h,v]
        If m=8 Then colr3 "888" ' impassable mountains
        If m=7 Then colr3 "530" ' mountains (passable by balloon or airship)
        If m=6 Then colr3 "880" ' desert (uses up player water)
        If m=5 Then colr3 "353" ' swamp (hard to cross)
        If m=4 Then colr3 "141" ' forest (thicker to cross)
        If m=3 Then colr3 "080" ' grass (easy motion)
        If m=2 Then colr3 "464" ' scrub (rough terrain)
        If m=1 Then colr3 "876" ' beach
      EndIf
      DrawRect 320+j*32,384-16+i*32,31,31
    Next
  Next
  colr3 "000"
  DrawOval 320+16-12,384-12,24,24
  colr3 "888"
  DrawOval 320+16-10,384-10,20,20
  DrawImage img_map,1024-256,0
  colr3 "000"
  SetAlpha .5
  DrawRect 1023-256+x,y-1,3,3
  SetAlpha 1
  colr3 "888"
  Plot 1024-256+x,y
  Flip 0
EndFunction

' &gt;&gt; SHOW OUR WORK AND WAIT A KEYPRESS
Function update()
Local i,j,m
  Cls
  For i=0 Until 255
    For j=0 Until 255
      m=map[j,i]
      If m=8 Then colr3 "888" ' impassable mountains
      If m=7 Then colr3 "530" ' mountains (passable by balloon or airship)
      If m=6 Then colr3 "760" ' desert (uses up player water)
      If m=5 Then colr3 "353" ' swamp (hard to cross)
      If m=4 Then colr3 "141" ' forest (thicker to cross)
      If m=3 Then colr3 "080" ' grass (easy motion)
      If m=2 Then colr3 "464" ' scrub (rough terrain)
      If m=1 Then colr3 "876" ' beach
      If m=0 Then colr3 "008" ' water (req boat)
      If miniflag
        If m Then Plot j,i
      Else
        DrawRect j*2,i*2,2,2
      EndIf
    Next
  Next
  If miniflag
    GrabImage img_map,0,0
    Return
  EndIf
  Flip 0 ' show our work
  k$=Chr$(WaitChar())
EndFunction

' &gt;&gt; SIMPLE 3-CHARACTER COLOR 000 (BLACK) TO 888 (WHITE)      
Function colr3(a$)
  SetColor Min((a$[0]-48)*32,255),Min((a$[1]-48)*32,255),Min((a$[2]-48)*32,255)
EndFunction

' &gt;&gt; RETURN RANDOM 0 TO A-1
Function fnr(a)
  Return Rand(a)-1
EndFunction
</textarea> <br><br></td></tr></table><br>
<a name="1300128"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Casaber</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#41">[#41]</a></td></tr></table></td></tr><tr ><td class="posttext"> Haha, I was with you all the way until Zoombies and the swamp. It sounded so all natural and up to that point.<br><br>Okay, that example looked really nice, when it grows. Nicely done. <br><br></td></tr></table><br>
<a name="1300152"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamStrange</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#42">[#42]</a></td></tr></table></td></tr><tr ><td class="posttext"> mmm, interesting way of interpreting the concept. definitely not the result I was expecting.<br><br>using the map:int and only 8 values is not really a height map, but the result can be interesting.<br><br>Using your general method here is a true height map (and super strict code):<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

superStrict
SeedRnd MilliSecs() ' always get random results

global heightmap:float[255,255] ' size of map
local k:int

Graphics 1024,768
SetBlend alphablend


clearHeightMap()
addHeightPoints(80)
for k= 1 to 20
	expandHeightMap()
	drawHeightMap()
next

Repeat
	delay(10)
until appterminate()
end

function clearHeightMap()
	local x:int
	local y:int
	For y = 0 Until 254
		For x = 0 Until 254
			heightMap[x, y] = 0 ' needed since BlitzMAX does not have a FillArray()
		Next
	Next
end function

function addHeightPoints(count:int)
	local i:int
	local x:int
	local y:int
	
	For i = 0 To count
		x = Rand(64, 192)
		y = Rand(64, 192) ' Always start somewhere in middle
		heightMap[x,y] = rand(64,200)
	next
end function

function drawHeightMap()
	local x:int
	local y:int
	cls

	for y = 0 to 254
		for x = 0 to 254
			if heightMap[x, y] &gt; 0 then
				setcolor heightMap[x, y],0,0
				Plot x, y
			EndIf
		next
	next

	flip
end function

function expandHeightMap()
	local k:int
	local x:int
	local y:int
	local xx:int
	local yy:int
	
	For k = 0 To 100000

		xx = rand(1, 253)
		yy = rand(1, 253)

		if heightMap[xx, yy] &gt; 0 then
			For y = yy-1 To yy+1
				For x = xx-1 To xx+1
					if heightMap[xx, yy] &gt; heightMap[x, y] then
						heightMap[x, y] = (heightMap[xx, yy]+heightMap[x, y]) * rnd(0.49, 0.5)
					end if
				Next
			Next
		end if
		
	Next
end function
</textarea><br>and the output:<br><img src="https://vjointeractive.files.wordpress.com/2016/03/screen-shot-2016-03-17-at-07-18-46.png"><br><br>There are a few things to note<br>Rand(from:int, to:int)<br>Rnd(float)<br>map[255,255] - the array values go from 0 to 254. 255 is not valid<br>indenting is tabs not spaces<br>using functions makes things clearer<br>using logical variable names with for loops. x and y to reference the map locations is more readable than using i, j as they don't really references anything sensible<br><br>ok, that's my code gripe over ;)<br><br>so how would you turn a (proper) height map into the island?<br>Simples...<br>if height is within the bounds you are interested in, make it what you are interested in. tip. use a second map for the result and the height map as your source <br><br></td></tr></table><br>
<a name="1300211"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#43">[#43]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Casaber:<br><br>Well I remember watching a movie and that's where the zombies came from, the swamp.<br><br>AdamStrange, I've been using I and J for index variables, not just as for X &amp; Y for as long as I can remember. If you check some of David H. Ahl's original games, he used it quite heavily.<br><br>And yes, my map is not 256x256 it is 255x255 so it can have a natural center - as any odd number will.<br><br>I use SPACE instead of TAB because I don't like storing non-ASCII characters in my source, especially if I read it w Notepad later as TAB w it is like 10-spaces.<br><br>What you have above is an effect similar to what I posted earlier HERE as Candy Dreams:<br><br><a href="http://dw817.deviantart.com/art/Candy-Dreams-284150276" target="_blank">http://dw817.deviantart.com/art/Candy-Dreams-284150276</a> <br><br></td></tr></table><br>
<a name="1300320"></a>

<a name="1300321"></a>

<a name="1300323"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Casaber</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#44">[#44]</a></td></tr></table></td></tr><tr ><td class="posttext"> Im curious about one thing, what does that line do? I see how it tries to average to an center height but I've never seen that kind use of randomness?<br><br>If I change <br>If heightMap[xx, yy] &gt; heightMap[x, y] Then heightMap[x, y] = (heightMap[xx, yy]+heightMap[x, y]) * Rnd(0.49, 0.5)<br>into<br>If heightMap[xx, yy] &gt; heightMap[x, y] Then heightMap[x, y] = (heightMap[xx, yy]+heightMap[x, y]) * Rnd(0.5, 0.5)<br><br>then the results is totally different?<br>What actually happens?<br><br><pre class=code>
Function expandHeightMap()
	Local k:Int , x:Int , y:Int , xx:Int , yy:Int	
	For k = 0 To 100000 ; 	xx = Rand(1, 253) ; yy = Rand(1, 253)
		If heightMap[xx, yy] &gt; 0 Then
			For y = yy-1 To yy+1 ; For x = xx-1 To xx+1



					If heightMap[xx, yy] &gt; heightMap[x, y] Then heightMap[x, y] = (heightMap[xx, yy]+heightMap[x, y]) * Rnd(0.49, 0.5)



			Next ; Next
		End If
	Next
End Function
</pre> <br><br></td></tr></table><br>
<a name="1300356"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamStrange</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#45">[#45]</a></td></tr></table></td></tr><tr ><td class="posttext"> it gives an element of randomness to the results. giving different numbers will give different results as you've found.<br><br>because it's floats, we can deal with very small values to give big changes.<br><br>It was also meant as the starting point for mucking around with and also showing height maps - what you do with the results if up to you ;) <br><br></td></tr></table><br>
<a name="1300372"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Casaber</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#46">[#46]</a></td></tr></table></td></tr><tr ><td class="posttext"> I find it interesting how random small changes can make those bubbly even round shapes when you go thru a square around each pixel, great code. <br><br></td></tr></table><br>
<a name="1300394"></a>

<a name="1300395"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#47">[#47]</a></td></tr></table></td></tr><tr ><td class="posttext"> What's even more interesting, Adam &amp; Casaber, if you try to retrieve the value of a random number less than 1 by itself, you get zero.<pre class=code>Strict
Local i,a#
For i=1 To 10
  a=Rand(0.49,0.5)
  Print a
Next</pre> <br><br></td></tr></table><br>
<a name="1300397"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cocopino</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#48">[#48]</a></td></tr></table></td></tr><tr ><td class="posttext"> Rand() returns an integer, you need Rnd() <br><br></td></tr></table><br>
<a name="1300514"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#49">[#49]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah, my bad, Cocopino. Try again.<br><br>RND() ? Trying it out. Hmm ... It's different than standard RND() or RND(1) in that you can have a range. I did not know that ! This could be useful for real arguments. <br><br></td></tr></table><br>
<a name="1300518"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cocopino</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#50">[#50]</a></td></tr></table></td></tr><tr ><td class="posttext"> Some other useful functions from the documentation<br>docs/html/Modules/Math/Random numbers/index.html<br><br><br>Function RndFloat#() <br>Returns A random float in the range 0 (inclusive) to 1 (exclusive)  <br><br>Function RndDouble!() <br>Returns A random double in the range 0 (inclusive) to 1 (exclusive)  <br><br>Function Rnd!( min_value!=1,max_value!=0 ) <br>Returns A random double in the range min (inclusive) to max (exclusive)  <br><br>Function Rand( min_value,max_value=1 ) <br>Returns A random integer in the range min (inclusive) to max (inclusive)  <br><br>Function SeedRnd( seed ) <br>Description Set random number generator seed <br><br>Function RndSeed() <br>Returns The current random number generator seed <br><br></td></tr></table><br>
<a name="1300521"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#51">[#51]</a></td></tr></table></td></tr><tr ><td class="posttext"> Quite a bit, Cocopino ! I'm just now discovering how powerful SeedRnd() is for generating dungeons or random maps that the player may leave at one point and then want to explore later.<br><br>BTW, Rnd() by itself I think does the same thing as you have RndDouble() above ? <br><br></td></tr></table><br>
<a name="1300527"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cocopino</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#52">[#52]</a></td></tr></table></td></tr><tr ><td class="posttext"> Rnd() takes arguments, a min and max value<br>RndDouble() does not take arguments, but returns some random value between 0.0 and 1.0<br><br>So RndDouble() does the same thing as Rnd(0.0,0.999999)<br>What IDE are you using? In most editors you can select your keyword and press F1 to get more information and in many cases a small example. <br><br></td></tr></table><br>
<a name="1300530"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#53">[#53]</a></td></tr></table></td></tr><tr ><td class="posttext"> The IDE is fine, Coco, it's the MIND that's using the code which is flawed (Mine). :)<br><br>I'm used to RND() not having arguments so when I saw you using it up there w 2-arguments, my brain interpreted it to be Rand(). <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
