<!DOCTYPE html><html lang="en" ><head ><title >Anim8or to x plugin</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Anim8or to x plugin</h1><a href="forums.php" >Community Forums</a>/<a href="topics.php?forum=83" >Developer Stations</a>/<a href="#bottom" >Anim8or to x plugin</a><br><br>
<a name="936242"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Can someone please fix this so it exports an object as shiny if it has materials on it?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">/*
* .X Plug-in by Joe Cooning
* Joe Cooning Produx
* 2006
*/

/*
 * Directives to install script as export plugin in Object editor.
 *
 * Required directives:
 *
 *   #plugin("object", "export", &lt;app-name&gt;, &lt;file-extension&gt;);
 *   #file(&lt;file-var&gt;, [ "text" | "binary" ]);
 *   #return(&lt;int-var&gt;);
 *
 * &lt;file-extension&gt; - string, including initial '.'
 * &lt;app-name&gt; - comment for export file open dialog.
 * &lt;file-var&gt; - name of file variable.  Must be declared later.
 * &lt;int-var&gt;  - int variable with result.  1 = success, 0 = failure.
 *
 */

#plugin("object", "export", "Directx", ".x");
#file($output, "text");
#return($result);

file $output;
int $result;

object $curObject;

$curObject = project.curObject;
$output.print("xof 0302txt 0064\nHeader {\n1;\n0;\n1;\n}\n\n");

shape $shape, $shapes[1], $childShapes[1];
tridata $mdata;
int $numPoints, $numFaces, $numMaterials;
int $totalPoints;
int $ii, $jj;
point3 $point, $normal, $tr1, $tr2, $tr3, $tr4;
point2 $uv;
float4x4 $transformMat;
string $matName, $texFile;
material $material;
texture $tex;

/* The shapes in $shapes are processed in reverse order so */
/* reverse the array of values that GeetShapes returns:    */

$curObject.GetShapes($childShapes);
$shapes.size = 0;
while ($childShapes.size &gt; 0)
    $shapes.push($childShapes.pop());

while ($shapes.size &gt; 0) {
    $shape = $shapes.pop();

    /* If $shape is a group push child shapes onto stack: */

    if ($shape.GetKind() == SHAPE_KIND_GROUP) {
        $shape.GetShapes($childShapes);
        $output.print("// Group \"%s\" has %d children:\n",
                      $shape.name, $childShapes.size);
        while ($childShapes.size &gt; 0) {
            $shapes.push($childShapes.pop());
        }
    } else if ($shape.GetKind() == SHAPE_KIND_PATH ||
               $shape.GetKind() == SHAPE_KIND_MODIFIER ||
               $shape.GetKind() == SHAPE_KIND_TEXT)
    {
        /* No 3D mesh to output. */
    } else {

	  $mdata = $shape.GetTriangleData();

	  $output.print("Frame %s {\nFrameTransformMatrix {\n", $shape.name);

	  $transformMat = $shape.GetGlobalTransform();
	  $tr4=$transformMat.Project((0.0,0.0,0.0));
	  $tr1=$transformMat.Project((1.0,0.0,0.0))-$tr4;
	  $tr2=$transformMat.Project((0.0,1.0,0.0))-$tr4;
	  $tr3=$transformMat.Project((0.0,0.0,1.0))-$tr4;
	  $output.print("%.6f, %.6f, %.6f, 0.000000,\n%.6f, %.6f, %.6f, 0.000000,\n%.6f, %.6f, %.6f, 0.000000,\n%.6f, %.6f, %.6f, 1.000000;;\n}\n",$tr1,$tr2,$tr3,$tr4);

        $output.print("Mesh %s {\n", $shape.name);

        $numPoints = $mdata.GetNumPoints();
        $output.print("%d;\n", $numPoints);
        for $ii = 0 to $numPoints-1 do {
            $point = $mdata.GetPoint($ii);
		$point.z =$point.z * -1;
            $output.print("%.6f; %.6f; %.6f;", $point);
		if ($ii&lt;$numPoints-1){
		  $output.print(",\n");
		} else {
		  $output.print(";\n");
		}
        }

        $numFaces = $mdata.GetNumTriangles();
        $output.print("%d;\n", $numFaces);
        for $ii = 0 to $numFaces - 1 do {
		$output.print("3;%d,%d,%d;", $mdata.GetIndex($ii*3 + 2), $mdata.GetIndex($ii*3 + 1), $mdata.GetIndex($ii*3));
		if ($ii&lt;$numFaces-1){
		   $output.print(",\n");
		} else {
		  $output.print(";\n");
		}
        }

	  $output.print("MeshMaterialList {\n");
        $numMaterials = $mdata.GetNumMaterials();
	  $output.print("%d;\n", $numMaterials);
	  $output.print("%d;\n", $numFaces);
	  for $ii = 0 to $numFaces - 1 do {
		$output.print("%d", $mdata.GetMatIndex($ii));
		if ($ii&lt;$numFaces - 1) {
		  $output.print(",\n");
		} else {
		  $output.print(";\n");
		}
	  }
        for $ii = 0 to $numMaterials - 1 do {
            $material = $mdata.GetMaterial($ii);
		$tex = $material.GetTexture(TEXTURE_DIFFUSE);
            $matName = $material.name;
            if ($matName == " -- default --") {
                $matName = "___default___";
            }
            $output.print("Material %s {\n", $matName);
            $output.print("%.6f, %.6f, %.6f, %.6f;;\n", $material.diffuse, $material.alpha);
		$output.print("%.6f;\n", $material.Ks);
            $output.print("%.6f, %.6f, %.6f;;\n", $material.specular); 
            $output.print("%.6f, %.6f, %.6f;;\n", $material.emissive);
		$texFile = $tex.GetFileName();
            if ($texFile != ""){
		    $output.print("TextureFilename {\n\"%s.%s\";\n}\n", $texFile.GetRoot(), $texFile.GetExt());
		}
		$output.print("}\n");
	  }
	  $output.print("}\n\n");

        $output.print("MeshNormals {\n%d;\n", $numPoints);
        for $ii = 0 to $numPoints - 1 do {
          $normal = $mdata.GetNormal($ii);
	    $normal.z = $normal.z * -1;
          $output.print("%.6f; %.6f; %.6f;", $normal);
	    if ($ii&lt;$numPoints-1){
		$output.print(",\n");
	    } else {
		$output.print(";\n");
	    }
        }
        $output.print("%d;\n", $numFaces);
        for $ii = 0 to $numFaces - 1 do {
		$output.print("3; %d,%d,%d;", $mdata.GetIndex($ii*3 + 2), $mdata.GetIndex($ii*3 + 1), $mdata.GetIndex($ii*3));
		if ($ii&lt;$numFaces-1){
		   $output.print(",\n");
		} else {
		  $output.print(";\n");
		}
        }
        $output.print("}\n");

        $output.print("MeshTextureCoords {\n%d;\n", $numPoints);
        for $ii = 0 to $numPoints - 1 do {
            $uv = $mdata.GetTexCoord($ii);
		$uv.y = 1.0-$uv.y;
            $output.print("%.5f; %.5f;", $uv);
		if ($ii&lt;$numPoints - 1){
		  $output.print(",\n");
		} else {
		  $output.print(";\n");
		}
        }
        $output.print("}\n");
	  $output.print("}\n}\n");

    }
}

$result = 1;        /* Assume export will succeed. */</textarea> <br><br></td></tr></table><br>
<a name="937456"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> anyone? <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
