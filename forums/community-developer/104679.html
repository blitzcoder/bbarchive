<!DOCTYPE html><html lang="en" ><head ><title >Blitz3D Render Animation To Frames Tool</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Blitz3D Render Animation To Frames Tool</h1><a href="forums.php" >Community Forums</a>/<a href="topics.php?forum=83" >Developer Stations</a>/<a href="#bottom" >Blitz3D Render Animation To Frames Tool</a><br><br>
<a name="1270154"></a>

<a name="1270155"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matty</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi all,<br><br>As I've mentioned a few times I use blitz3d and blitzplus often to make little tools and so on as 'helpers' in creating my games (regardless of the final product's coding environment).<br><br>This is an example of something I've built which you may find useful as well.<br><br>I may eventually put a 'proper' version in the code archives so that others can use it in a more refined fashion - at the moment it is not entirely user friendly but it should be easy enough for a lot of developers to follow.<br><br><br>.....<br><br>This is an animation tool.  I use it to create frames of animation which then get stitched together into an mp4 (with other software) to use as a background video or similar.<br><br><br>The way it works is that relies on two .bb files.<br><br>The first is the renderer that simply takes the data files you create and draws a little animation. When it executes - if the saverenders flag is set - then the images will render to the folder marked renders.<br><br>The second is a program that generates a data file.  Typically you will make a whole bunch of these and then execute them to generate the data files (in a sub folder marked data).<br><br><br>Enough talk.....here's the code (2)<br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Very simple movie maker
;Matt Lloyd 2015
;
;Expects a folder marked 'data' and a folder marked 'renders' to be in the same folder as the executable/bb file.
;
;It simply parses a bunch of text files in the data folder and plays back the results as an animation which can be 
;exported as bitmap frames into a folder.
;
;
;
;


maxframes = 500 
saverenders = False ;change this flag to true to export the renders to a folder (renders\....)
frate = 30

;recalc/transform frame rate
frate = 1000/(Abs(frate)+1)

If(CommandLine$()="-save") Then saverenders = True

Type anim

Field ent ;particular entity with texture

;data properties

Field meshfile$
Field texfile$

Field alpha#[1000]
Field pitch#[1000],yaw#[1000],roll#[1000]
Field x#[1000],y#[1000],z#[1000]
Field sx#[1000],sy#[1000],sz#[1000]

End Type 


;simply load and play from a file.....
;load the anims in the anim folder....

datadir = ReadDir(CurrentDir()+"data")
If(datadir&lt;&gt;0) Then 
	Repeat
		file$ = NextFile(datadir)
		If(file="") Then Exit 
		If(Left(file,4)="anim" And Right(file,3)="txt") Then 
			;this is us....
			infile = ReadFile(CurrentDir()+"data\"+file)
			If(infile) Then 
				mesh$ = ReadLine(infile)
				tex$ = ReadLine(infile)
				a.anim = New anim
				a\meshfile = mesh
				a\texfile = tex
				ff = 0
				While(Not(Eof(infile)))
					a\alpha[ff] = ReadLine(infile)
					a\pitch[ff] = ReadLine(infile)
					a\yaw[ff] = ReadLine(infile)
					a\roll[ff] = ReadLine(infile)
					a\x[ff] = ReadLine(infile)
					a\y[ff] = ReadLine(infile)
					a\z[ff] = ReadLine(infile)
					a\sx[ff] = ReadLine(infile)
					a\sy[ff] = ReadLine(infile)
					a\sz[ff] = ReadLine(infile)
					ff = ff+1		
					If(ff&gt;1000) Then Exit 
				Wend 		
				CloseFile infile
			EndIf 		
		EndIf

	Forever
	CloseDir datadir
EndIf 

;at this point all our items are ready....now render them out as frames.....


Graphics3D 1280,720,0,6
SetBuffer BackBuffer()
;place our camera...and make it perspective?
camera = CreateCamera()
CameraRange camera,10,10000
MoveEntity camera,0,0,0
AmbientLight 255,255,255
For a.anim = Each anim
	;3 basic primitives allowed....
	primitive = False
	If(a\meshfile="sprite") Then 
		primitive = True
		a\ent = CreateSprite()
		SpriteViewMode a\ent,2
		
	EndIf
	If(a\meshfile="sphere") Then 
		primitive = True
		a\ent = CreateSphere(16)
	EndIf
	If(a\meshfile="cube") Then 
		primitive = True
		a\ent = CreateCube()
	EndIf

	If(primitive = False) Then a\ent = LoadMesh(a\meshfile)
	
	If(a\ent) Then 
		If(Right(a\texfile,3)="png") Then 
			tex = LoadTexture(a\texfile,1+2+16+32)
		Else
			tex = LoadTexture(a\texfile,1+2+4+16+32)
		EndIf 
		If(tex=0) Then 
			FreeEntity a\ent
			Delete a
		Else
			;mask texture with pink mask unless png file
			If(Right(a\texfile,3)="png") Then 
			Else
			For x = 0 To TextureWidth(tex)-1
				For y = 0 To TextureHeight(tex)-1
					rgb = ReadPixel(x,y,TextureBuffer(tex)) And 16777215
					If( ((rgb And 255) = 255) And ((rgb Shr 16) And 255)=255 And ((rgb Shr 8) And 255)=0) Then
						WritePixel(x,y,0,TextureBuffer(tex))
					EndIf 
				Next
				
			Next
			EndIf
			EntityTexture a\ent,tex
			;all good....
		EndIf 
	Else
		Delete a
	EndIf 
Next

;go through all frames....up to ff-1 at 30 fps
font = LoadFont("Arial",16,False,False,False) 

SetFont font
maxf = ff-1

If(maxf&gt;maxframes) Then maxf = maxframes
If(maxf&lt;=0 Or maxf&gt;1000) Then RuntimeError("No Frames!")

Repeat

For ff = 0 To maxf


ftime = MilliSecs()

Cls
For a.anim = Each anim 
	PositionEntity a\ent,a\x[ff],a\y[ff],a\z[ff],True
	RotateEntity a\ent,a\pitch[ff],a\yaw[ff],a\roll[ff],True
	ScaleEntity a\ent,a\sx[ff],a\sy[ff],a\sz[ff],True
	EntityAlpha a\ent,a\alpha[ff]
Next

RenderWorld


;overlay 2d stuff in here......


;end of 2d overlay.....

If(saverenders) Then 
	;save the backbuffer....
	If(ff&lt;10) Then 
		strff$ = "00"+Str(ff)
	Else
		If(ff&lt;100) Then 
			strff$ = "0"+Str(ff) 
		Else
			strff$ = Str(ff)
		EndIf 
	EndIf 
	SaveBuffer BackBuffer(),CurrentDir()+"renders\frame"+strff$+".bmp"	
	Text 0,0,"F:"+ff
Else
	Text 0,0,"FT:"+ftime3+" F:"+ff

EndIf  
Flip False
ftime2 = MilliSecs()
If(ftime2-ftime&lt;frate And ftime2-ftime&gt;=0) Then 
	Delay frate - (ftime2-ftime)
	ftime3 = ftime2 - ftime
EndIf 
 
If(KeyHit(1)) Then End ;jump out
Next
Forever



;these are just some random text bits and pieces that I used....
Function randstr$(mylen=20)
mystr$ = ""
For i = 1 To mylen

u = Rand(0,1)
If(u) Then
mystr$=mystr$+Lower(Chr$(Rand(65,90)))
Else
mystr$=mystr$+Upper(Chr$(Rand(65,90)))
EndIf


Next

Return mystr
End Function 
Function randpct$(num)

num = Rand(-5,5)+num
If(num&lt;5) Then num = 5
If(num&gt;100) Then num = 95

Return num + "%"

End Function 
</textarea><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;;
;Animation File Creator (basic)
;
;

;sample  - you will need to supply your own texture file.....
Graphics3D 800,600,0,6
cube = CreateCube() ;dummy object - just in case you need an actual 3d object...

Global outfile = WriteFile(CurrentDir()+"data\animBARVRind.txt") ;change the file as required....
WriteLine outfile,"sprite" ;valid options are sprite, sphere or cube
WriteLine outfile,CurrentDir()+"pbarthumb.bmp" ;change the texture as required
For i = 0 To 499 ;number of frames to generate
	anim(cube,i)
Next
CloseFile outfile

Global ix#,iy# ;whatever you want to use them for...

Function anim(b,i) ;i = frame, b = 3d entity
;this function is completely user defined....however the output to file
;must match the format here...which simply alpha, pitch, yaw, roll, x,y,z,scale x, scale y, scale z
;
;the code below is really just a sample....ie stuff like yy = .... as well as the numbers are just examples (well actually they're left over from a previous anim)


	aa# = Rnd(0.8,1.10) ;alpha
	If(aa&gt;1) Then aa = 1
	
	WriteLine outfile,aa ;alpha

;rot
	WriteLine outfile,0 ;pitch
	WriteLine outfile,0 ;yaw
	WriteLine outfile,90 ;roll

;pos

    yy# = 100.0*(Sin(Float(i)*3.0)+0.5*Sin(Float(i)*6.0))
	WriteLine outfile,1100
	WriteLine outfile,380+yy
	WriteLine outfile,1250

;scale

	WriteLine outfile,20
	WriteLine outfile,40
	WriteLine outfile,1500




End Function
</textarea><br><br><br>Use however you wish .... (many of you may already have software that does this or that you have written)<br><br>Here are some little samples of animations created with these two programs:<br><br>(note these are just quick samples)<br><br><iframe width="560" height="345" src="http://www.youtube.com/embed/IyVytDb9qak" frameborder="0" allowfullscreen></iframe><br><br><iframe width="560" height="345" src="http://www.youtube.com/embed/jRhPYMezBHM" frameborder="0" allowfullscreen></iframe><br><br><iframe width="560" height="345" src="http://www.youtube.com/embed/sRKnDLtYS34" frameborder="0" allowfullscreen></iframe><br><br><iframe width="560" height="345" src="http://www.youtube.com/embed/Nli9s9rA-qo" frameborder="0" allowfullscreen></iframe><br><br><iframe width="560" height="345" src="http://www.youtube.com/embed/PNiD0q1omk4" frameborder="0" allowfullscreen></iframe><br><br><iframe width="560" height="345" src="http://www.youtube.com/embed/-d3veOG8Niw" frameborder="0" allowfullscreen></iframe><br><br><br><iframe width="560" height="345" src="http://www.youtube.com/embed/yerBPVM5Y7c" frameborder="0" allowfullscreen></iframe> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
