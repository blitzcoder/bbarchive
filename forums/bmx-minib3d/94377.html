<!DOCTYPE html><html lang="en" ><head ><title >More help with rotations...</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >More help with rotations...</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=119" >MiniB3D Module</a>/<a href="#bottom" >More help with rotations...</a><br><br>
<a name="1082628"></a>

<a name="1082629"></a>

<a name="1082630"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jkrankie</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi all,<br><br>I've been trying to simulate the movement physics in mario galaxy, where you can run around the surfaces of planetoids of various shapes and sizes. So far, it's going ok (i've been using the mario galaxy demystified thing as a basic guide), but i'm getting a bit stuck getting the player to face the direction he is supposed to. <br><br>I've posted my code so far below. It needs Warner's version of minib3d, which you can grab from here: <a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=2498" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=2498</a> (zip link towards the bottom of the page).<br><br>What i'm trying to do, is have the player face the direction you are entering on the keyboard/joystick at all times. The movement works in this way, but i don't really know how to make the rotations do the same. You'll notice if you run the program that it starts off rotating correctly, but the more you move around the more the angles get offset. this is most obvious on the cube planetoids is you spin around the corners. <br><br>here's the code:<br><br>controls are arrows/joystick to move, space of joystick button 1 to jump. change planet by clicking on anther one.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

'this requires warners updated minib3d, available here: <a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=2498" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=2498</a>
Import sidesign.minib3d

Const DYNAMIC:Int = 1, STATIC:Int = 2, STATIC2:Int = 3

Graphics3D(800, 600, 32, 1, 60)

'enable collisions
Collisions (DYNAMIC, STATIC, 2, 3)
Collisions (DYNAMIC, STATIC2, 2, 2)

'make some other planetoids to go to

Local ast:TMesh[50]
For Local i:Int = 0 To 49
	Local r:Int = Rand(0, 2)
	If r = 0
		ast[i] = CreateCube()
		EntityColor ast[i], 255, 0, 0
	ElseIf r = 1
		ast[i] = CreateSphere()
		EntityColor ast[i], 255, 255, 0
	Else
		ast[i] = CreateCylinder()
	EndIf
	PositionEntity ast[i], Rand(-250, 250), Rand(-250, 250), Rand(-250, 250)
	RotateEntity ast[i], Rand(0, 359), Rand(0, 359), Rand(0, 359)
	ScaleEntity ast[i], 10, 10, 10
	EntityPickMode ast[i], 2
	EntityType ast[i], STATIC
Next

'make player
Local p1:player = New player
p1.mesh = CreateCone()
PositionEntity p1.mesh, 0, 10, 0
p1.mesh.EntityType (DYNAMIC)
ScaleMesh TMesh(p1.mesh), 0.6, 1, 0.6
RotateMesh TMesh(p1.mesh), 180, 0, 0
p1.mesh.EntityColor(255, 0, 255)
p1.mesh.EntityRadius (1)

'make start planet
Local plan1:planet = New planet
plan1.mesh = CreateSphere(64)
plan1.mesh.EntityType (STATIC)
plan1.mesh.EntityPickMode (2)
PositionEntity plan1.mesh, 0, 0, 0
ScaleEntity plan1.mesh, 9, 9, 9

'set the fisrt planet as the players planet
p1.planetiod = plan1.mesh



Local cam:TCamera = CreateCamera()
CameraZoom cam, 1

Local light:TLight = CreateLight()
PositionEntity light, 16, 12, -30
PointEntity light, plan1.mesh

Local camx:Float = 0, camy:Float = 0, camz:Float = -30

Local playerang:Float = 0
Local lastang:Float = 0

While Not KeyHit(KEY_ESCAPE)


'put player on planet surface
	If EntityCollided(p1.mesh, STATIC)
		LinePick(EntityX(p1.mesh), EntityY(p1.mesh), EntityZ(p1.mesh), EntityX(p1.planetiod) - EntityX(p1.mesh), EntityY(p1.planetiod) - EntityY(p1.mesh), EntityZ(p1.planetiod) - EntityZ(p1.mesh))
		AlignToVector2(p1.mesh, PickedNX(), PickedNY(), PickedNZ(), 3, 1)
		p1.nx = PickedNX()
		p1.ny = PickedNY()
		p1.nz = PickedNZ()
		
		p1.jumptimer = MilliSecs()
		p1.vx = 0
		p1.vy = 0
		p1.vz = 0
		
	Else
		LinePick(EntityX(p1.mesh), EntityY(p1.mesh), EntityZ(p1.mesh), EntityX(p1.planetiod) - EntityX(p1.mesh), EntityY(p1.planetiod) - EntityY(p1.mesh), EntityZ(p1.planetiod) - EntityZ(p1.mesh))
			AlignToVector2(p1.mesh, PickedNX(), PickedNY(), PickedNZ(), 3, 0.1)
			p1.nx = PickedNX()
			p1.ny = PickedNY()
			p1.nz = PickedNZ()
		
			p1.vx:-PickedNX() * 0.05
			p1.vy:-PickedNY() * 0.05
			p1.vz:-PickedNZ() * 0.05
			TranslateEntity(p1.mesh, p1.vx, p1.vy, p1.vz)
	End If

'move and turn player in relation to camera with keyboard
	If KeyDown(KEY_LEFT)
		TFormVector(-0.25, 0, 0, cam, Null)
		TranslateEntity p1.mesh, TFormedX(), TFormedY(), TFormedZ()
		playerang = 270
	ElseIf KeyDown(KEY_RIGHT)
		TFormVector(0.25, 0, 0, cam, Null)
		TranslateEntity p1.mesh, TFormedX(), TFormedY(), TFormedZ()
		playerang = 90
	End If
	
	If KeyDown(KEY_UP)
		TFormVector(0, 0.25, 0, cam, Null)
		TranslateEntity p1.mesh, TFormedX(), TFormedY(), TFormedZ()
		playerang = 0
	ElseIf KeyDown(KEY_DOWN)
		TFormVector(0, -0.25, 0, cam, Null)
		TranslateEntity p1.mesh, TFormedX(), TFormedY(), TFormedZ()
		playerang = 180
	End If
	
	p1.mesh.MQ_Turn(playerang - lastang, 0, 0, 1)
	lastang = playerang

'Move and turn player with joystick	
	If Abs(JoyX(0)) &gt; 0.2
		TFormVector(JoyX() * 0.25, 0, 0, cam, Null)
		TranslateEntity p1.mesh, TFormedX(), TFormedY(), TFormedZ()
	End If
	
	If Abs(JoyY()) &gt; 0.2
		TFormVector(0, -JoyY() * 0.25, 0, cam, Null)
		TranslateEntity p1.mesh, TFormedX(), TFormedY(), TFormedZ()
	End If
	
	'turn stuff with joystick
	If Abs(JoyX()) &gt; 0.2 Or Abs(JoyY()) &gt; 0.2
		playerang = calculateAngle(JoyX(), JoyY(), 0, 0) + 180
		p1.mesh.MQ_Turn(playerang - lastang, 0, 0, 1)
		lastang = playerang
	EndIf
	
'make player jump
	If (KeyDown(KEY_SPACE) Or JoyDown(1)) And p1.jumptimer + 10 &gt; MilliSecs()
		p1.vx = p1.vx + 1.01 * p1.nx
		p1.vy = p1.vy + 1.01 * p1.ny
		p1.vz = p1.vz + 1.01 * p1.nz
	End If


'move camera	
	Local ctx:Float = ((EntityX(p1.mesh) + (p1.nx * 30)) - camx) * 0.075
	Local cty:Float = ((EntityY(p1.mesh) + (p1.ny * 30)) - camy) * 0.075
	Local ctz:Float = ((EntityZ(p1.mesh) + (p1.nz * 30)) - camz) * 0.075
	camx:+ctx
	camy:+cty
	camz:+ctz
	PositionEntity cam, camx, camy, camz'p1.nx * 30, p1.ny * 30, p1.nz * 30

'make camera face player
	Local cdx:Float = EntityX(cam, 1) - EntityX(p1.mesh, 1)
	Local cdy:Float = EntityY(cam, 1) - EntityY(p1.mesh, 1)
	Local cdz:Float = EntityZ(cam, 1) - EntityZ(p1.mesh, 1)
	
	AlignToVector2(cam, -cdx, -cdy, -cdz, 3, 0.5)
	
'switch planetoids with mouse click
	Local Pick:TEntity
	If MouseDown(1)
		Pick:TEntity = CameraPick(cam, MouseX(), MouseY())
		If Pick &lt;&gt; Null
			p1.planetiod = Pick
			p1.Reset()
			PositionEntity p1.mesh, PickedX(), PickedY(), PickedZ()
			ClearCollisions()
		End If
	End If
	
'render stuff
	UpdateWorld()
	RenderWorld
	
	Flip
Wend


Type player
	
	Field mesh:TEntity
	Field planetiod:TEntity
	Field jumptimer:Int
	Field nx:Float, ny:Float, nz:Float
	Field vx:Float, vy:Float, vz:Float
	
	Method Reset()
		nx = 0;ny = 0;nz = 0
		vx = 0;vy = 0;vz = 0
	End Method
	
EndType

Type planet
	Field mesh:TEntity
EndType

Function calculateAngle:Float(x1:Float,y1:Float,x2:Float,y2:Float)
	Local theX:Float=x1-x2
	Local theY:Float=y1-y2
	Local theAngle:Float=-ATan2(theX,theY)
	Return theAngle
End Function

</textarea><br><br>so, what can i do to get the rotation of the player correct? so that i always faces the direction you are pressing regardless of the cameras orientation?<br><br><font class="tiny">Last edited 2011</font><br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1082633"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think, as a first step, try to get it upright allways, ie:<br><pre class=code>
	TFormVector 0, 1, 0, cam, Null
	AlignToVector2 p1.mesh, TFormedX(), TFormedY(), TFormedZ(), 2, 1
</pre><br>Then, maybe attach a child mesh and rotate that. <br><br></td></tr></table><br>
<a name="1082636"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jkrankie</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah, thanks! I'd been trying that, but using the z parameter of tformvector. still haven't quite got my head around this. I'm getting there though.<br><br>Anyway, that seems to work, but there seems to be some strange behaviour when you rotate to around the 180 degree mark. things get wobbly!<br><br>Here's some updated code. The wobbly stuff was not terribly obvious using just the keyboard, so i've added on a value to the angle in MQ_Turn so you can see it. Pressing up will make the player's direction wobble, whereas left, down and right all appear to work as expected. Is this more gimbal lock?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Import warnersidesign.minib3d

Const DYNAMIC:Int = 1, STATIC:Int = 2, STATIC2:Int = 3

Graphics3D(800, 600, 32, 1, 60)

'enable collisions
Collisions (DYNAMIC, STATIC, 2, 3)
Collisions (DYNAMIC, STATIC2, 2, 2)

'make some other planetoids to go to

Local ast:TMesh[50]
For Local i:Int = 0 To 49
	Local r:Int = Rand(0, 2)
	If r = 0
		ast[i] = CreateCube()
		EntityColor ast[i], 255, 0, 0
	ElseIf r = 1
		ast[i] = CreateSphere()
		EntityColor ast[i], 255, 255, 0
	Else
		ast[i] = CreateCylinder()
	EndIf
	PositionEntity ast[i], Rand(-250, 250), Rand(-250, 250), Rand(-250, 250)
	RotateEntity ast[i], Rand(0, 359), Rand(0, 359), Rand(0, 359)
	ScaleEntity ast[i], 10, 10, 10
	EntityPickMode ast[i], 2
	EntityType ast[i], STATIC
Next

'make player
Local p1:player = New player
p1.mesh = CreateCone()
PositionEntity p1.mesh, 0, 10, 0
p1.mesh.EntityType (DYNAMIC)
ScaleMesh TMesh(p1.mesh), 0.6, 1, 0.6
RotateMesh TMesh(p1.mesh), 180, 0, 0
p1.mesh.EntityColor(255, 0, 255)
p1.mesh.EntityRadius (1)

'make start planet
Local plan1:planet = New planet
plan1.mesh = CreateSphere(64)
plan1.mesh.EntityType (STATIC)
plan1.mesh.EntityPickMode (2)
PositionEntity plan1.mesh, 0, 0, 0
ScaleEntity plan1.mesh, 9, 9, 9

'set the fisrt planet as the players planet
p1.planetiod = plan1.mesh



Local cam:TCamera = CreateCamera()
CameraZoom cam, 1

Local light:TLight = CreateLight()
PositionEntity light, 16, 12, -30
PointEntity light, plan1.mesh

Local camx:Float = 0, camy:Float = 0, camz:Float = -30

Local playerang:Float = 0
Local lastang:Float = 0

While Not KeyHit(KEY_ESCAPE)


'put player on planet surface
	If EntityCollided(p1.mesh, STATIC)
		LinePick(EntityX(p1.mesh), EntityY(p1.mesh), EntityZ(p1.mesh), EntityX(p1.planetiod) - EntityX(p1.mesh), EntityY(p1.planetiod) - EntityY(p1.mesh), EntityZ(p1.planetiod) - EntityZ(p1.mesh))
		AlignToVector2(p1.mesh, PickedNX(), PickedNY(), PickedNZ(), 3, 1)
		p1.nx = PickedNX()
		p1.ny = PickedNY()
		p1.nz = PickedNZ()
		
		p1.jumptimer = MilliSecs()
		p1.vx = 0
		p1.vy = 0
		p1.vz = 0
		
	Else
		LinePick(EntityX(p1.mesh), EntityY(p1.mesh), EntityZ(p1.mesh), EntityX(p1.planetiod) - EntityX(p1.mesh), EntityY(p1.planetiod) - EntityY(p1.mesh), EntityZ(p1.planetiod) - EntityZ(p1.mesh))
			AlignToVector2(p1.mesh, PickedNX(), PickedNY(), PickedNZ(), 3, 0.1)
			p1.nx = PickedNX()
			p1.ny = PickedNY()
			p1.nz = PickedNZ()
		
			p1.vx:-PickedNX() * 0.05
			p1.vy:-PickedNY() * 0.05
			p1.vz:-PickedNZ() * 0.05
			TranslateEntity(p1.mesh, p1.vx, p1.vy, p1.vz)
	End If

'move and turn player in relation to camera with keyboard
	If KeyDown(KEY_LEFT)
		TFormVector(-0.25, 0, 0, cam, Null)
		TranslateEntity p1.mesh, TFormedX(), TFormedY(), TFormedZ()
		playerang = 270
	ElseIf KeyDown(KEY_RIGHT)
		TFormVector(0.25, 0, 0, cam, Null)
		TranslateEntity p1.mesh, TFormedX(), TFormedY(), TFormedZ()
		playerang = 90
	End If
	
	If KeyDown(KEY_UP)
		TFormVector(0, 0.25, 0, cam, Null)
		TranslateEntity p1.mesh, TFormedX(), TFormedY(), TFormedZ()
		playerang = 0
	ElseIf KeyDown(KEY_DOWN)
		TFormVector(0, -0.25, 0, cam, Null)
		TranslateEntity p1.mesh, TFormedX(), TFormedY(), TFormedZ()
		playerang = 180
	End If
	
	'rotate the player
	tformvector(0,1,0,cam,Null)
	aligntovector2(p1.mesh,tformedx(),tformedy(),tformedz(),2,1)
	p1.mesh.MQ_Turn(playerang+187, 0, 0, 1)
	

'Move and turn player with joystick	
	If Abs(JoyX(0)) &gt; 0.2
		TFormVector(JoyX() * 0.25, 0, 0, cam, Null)
		TranslateEntity p1.mesh, TFormedX(), TFormedY(), TFormedZ()
	End If
	
	If Abs(JoyY()) &gt; 0.2
		TFormVector(0, -JoyY() * 0.25, 0, cam, Null)
		TranslateEntity p1.mesh, TFormedX(), TFormedY(), TFormedZ()
	End If
	
	'turn stuff with joystick
	If Abs(JoyX()) &gt; 0.2 Or Abs(JoyY()) &gt; 0.2
		playerang = calculateAngle(JoyX(), JoyY(), 0, 0) + 180
		tformvector(0,1,0,cam,Null)
		aligntovector2(p1.mesh,tformedx(),tformedy(),tformedz(),2,1)
		p1.mesh.MQ_Turn(playerang, 0, 0, 1)
	EndIf
	
'make player jump
	If (KeyDown(KEY_SPACE) Or JoyDown(1)) And p1.jumptimer + 10 &gt; MilliSecs()
		p1.vx = p1.vx + 1.01 * p1.nx
		p1.vy = p1.vy + 1.01 * p1.ny
		p1.vz = p1.vz + 1.01 * p1.nz
	End If


'move camera	
	Local ctx:Float = ((EntityX(p1.mesh) + (p1.nx * 30)) - camx) * 0.075
	Local cty:Float = ((EntityY(p1.mesh) + (p1.ny * 30)) - camy) * 0.075
	Local ctz:Float = ((EntityZ(p1.mesh) + (p1.nz * 30)) - camz) * 0.075
	camx:+ctx
	camy:+cty
	camz:+ctz
	PositionEntity cam, camx, camy, camz'p1.nx * 30, p1.ny * 30, p1.nz * 30

'make camera face player
	Local cdx:Float = EntityX(cam, 1) - EntityX(p1.mesh, 1)
	Local cdy:Float = EntityY(cam, 1) - EntityY(p1.mesh, 1)
	Local cdz:Float = EntityZ(cam, 1) - EntityZ(p1.mesh, 1)
	
	AlignToVector2(cam, -cdx, -cdy, -cdz, 3, 0.5)
	
'switch planetoids with mouse click
	Local Pick:TEntity
	If MouseDown(1)
		Pick:TEntity = CameraPick(cam, MouseX(), MouseY())
		If Pick &lt;&gt; Null
			p1.planetiod = Pick
			p1.Reset()
			PositionEntity p1.mesh, PickedX(), PickedY(), PickedZ()
			ClearCollisions()
		End If
	End If
	
'render stuff
	UpdateWorld()
	RenderWorld
	
	Flip
Wend


Type player
	
	Field mesh:TEntity
	Field planetiod:TEntity
	Field jumptimer:Int
	Field nx:Float, ny:Float, nz:Float
	Field vx:Float, vy:Float, vz:Float
	
	Method Reset()
		nx = 0;ny = 0;nz = 0
		vx = 0;vy = 0;vz = 0
	End Method
	
EndType

Type planet
	Field mesh:TEntity
EndType

Function calculateAngle:Float(x1:Float,y1:Float,x2:Float,y2:Float)
	Local theX:Float=x1-x2
	Local theY:Float=y1-y2
	Local theAngle:Float=-ATan2(theX,theY)
	Return theAngle
End Function

</textarea><br><br>Additionally, i haven't added a child mesh as you suggested (i don't really understand what you mean?), but instead just rotated the cone in place. What would the purpose of the child mesh be?<br><br>Cheers<br>Charlie <br><br></td></tr></table><br>
<a name="1082642"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> It seems that the wobbeling is caused by the +187 degrees turn. <br>I think the object's orientation affects the phyics that is used to keep the player aligned to the planetoids. Ie. LinePick being based on the rotation of the object.<br>Change line 122 to:<pre class=code>p1.mesh.MQ_Turn(playerang+180, 0, 0, 1)</pre>And the wobbeling seems to be gone.<br>Either RotateMesh the cone by 7 degrees, or replace the cone by a pivot and attach another cone as a child mesh that is rotated by 7 degrees to it to still achieve this +187 angle. <br><br></td></tr></table><br>
<a name="1082643"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jkrankie</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I put the 187 in to demonstrate the wobbling. It's still there at 180, but not so obvious. If you've got a joystick, you'll notice that any angle between ~160 and 210 are wobbly.<br><br>I'll try the pivot thing and see if that helps.<br><br>Cheers<br>Charlie <br><br></td></tr></table><br>
<a name="1082644"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jkrankie</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Right, got it!<br><br>replaced the p1.mesh with a pivot, added a child cone entity, align the pivot to the camera and MQ_turn the child. This seems to get round the linepick induced wobbliness!<br><br>Thanks for your help! <br><br>Next job: let the player shoot in the direction they are facing!<br><br>Cheers<br>Charlie <br><br></td></tr></table><br>
<a name="1082647"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah, well that will be peanuts now. :) <br><br></td></tr></table><br>
<a name="1082902"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jkrankie</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> It wasn't too hard making something that worked. Actually, i came up with two different methods for letting the player shoot, neither of which i'm too happy with. They are both pretty slow.<br><br>The first uses a line pick, similar to how the player moves. The second uses collisions. Both are super slow when you turn the number of bullets up. i'm getting about ~20-30 fps with only 250 or so entities being rendered. With the line pick/collisions off i get about 150fps.<br><br>Chances are there's a much simpler method for moving the bullets around the various shapes, so any advice for alternatives is much appreciated! What i think i need is to be able to get the normal of the nearest surface of any given mesh, so i don't need the expensive line pick operation, or have to rely on collisions. But i can't work out how to do it :(<br><br>The bullet code is at the top of the while loop. <br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
Import sidesign.minib3d

Const DYNAMIC:Int = 1, STATIC:Int = 2, STATIC2:Int = 3
Const CAM_HUG_NORMALS:Int = 0, CAM_SMOOTH_CHASE:Int = 1
Graphics3D(800, 600, 32, 1, 60)

'enable collisions
Collisions (DYNAMIC, STATIC, 2, 3)
Collisions (DYNAMIC, STATIC2, 2, 2)


'make some other planetoids to go to
	Local ast:planet[50]
	For Local i:Int = 0 To 49
		ast[i] = New planet
		Local r:Int = Rand(0, 2)
		If r = 0
			ast[i].mesh = CreateCube()
			EntityColor ast[i].mesh, 255, 0, 0
			ast[i].camstyle = CAM_HUG_NORMALS
		ElseIf r = 1
			ast[i].mesh = CreateSphere(6)
			EntityColor ast[i].mesh, 255, 255, 0
			ast[i].camstyle = CAM_SMOOTH_CHASE
		Else
			ast[i].mesh = CreateCylinder()
			ast[i].camstyle = CAM_SMOOTH_CHASE
		EndIf
		PositionEntity ast[i].mesh, Rand(-250, 250), Rand(-250, 250), Rand(-250, 250)
		RotateEntity ast[i].mesh, Rand(0, 359), Rand(0, 359), Rand(0, 359)
		ScaleEntity ast[i].mesh, 10, 10, 10
		EntityPickMode ast[i].mesh, 2
		EntityType ast[i].mesh, STATIC
		EntityAlpha ast[i].mesh, 0.6
	Next

'make player
	Local p1:player = New player
	p1.piv = CreatePivot()
	PositionEntity p1.piv, 0, 30, 0
	p1.piv.EntityType (DYNAMIC)
	p1.piv.EntityRadius (1)
	p1.mesh2 = CreateCone(,, p1.piv)
	p1.mesh2.EntityColor(255, 0, 255)

'other bits for player and camera movements
	p1.piv2 = CreatePivot()'p1.mesh)
	Local cb:TMesh = CreateCube(p1.piv2)
	PositionEntity cb, 0, 30, 0
	HideEntity cb
	Local playerang:Float = 0
	Local lastang:Float = 0

'make start planet
	Local plan1:planet = New planet
	plan1.camstyle = CAM_SMOOTH_CHASE
	plan1.mesh = CreateSphere(32)
	plan1.mesh.EntityType (STATIC)
	plan1.mesh.EntityPickMode (2)
	PositionEntity plan1.mesh, 0, 0, 0
	ScaleEntity plan1.mesh, 9, 9, 9

'set the fisrt planet as the players planet
	p1.planetiod = plan1.mesh


'make cameras etc.
	Local cam:TCamera = CreateCamera()
	PositionEntity cam, 0, 30, 0
	CameraZoom cam, 1
	Local camx:Float = 0, camy:Float = 0, camz:Float = -30
	Local cameramovementstyle:Int = plan1.camstyle
	
	Local light:TLight = CreateLight()
	PositionEntity light, 16, 12, -30
	PointEntity light, plan1.mesh

'list for bullets
	Local bulletlist:TList = New TList

'timing stuff
	Local timer:Int = 0
	Local old_ms:Int = MilliSecs()
	Local renders:Int = 0
	Local fps:Int


While Not KeyHit(KEY_ESCAPE)
timer:+1

'	make a bullet
	If timer Mod 1 = 0
		bulletlist.AddLast New bullet
		bullet(bulletlist.Last()).planetoid = p1.planetiod
		bullet(bulletlist.Last()).piv = CreatePivot()
		bullet(bulletlist.Last()).piv.MQ_Position EntityX(p1.piv), EntityY(p1.piv), EntityZ(p1.piv)
		bullet(bulletlist.Last()).piv.RotateEntity(EntityPitch(p1.mesh2, 1), EntityYaw(p1.mesh2, 1), EntityRoll(p1.mesh2, 1) + 90)
		
		bullet(bulletlist.Last()).mesh = CreateCone(3,, bullet(bulletlist.Last()).piv)'CreateSphere(, bullet(bulletlist.Last()).piv)'
		bullet(bulletlist.Last()).mesh.EntityColor(0, 255, 0)
		ScaleEntity bullet(bulletlist.Last()).mesh, 1.2, 0.4, 0.4
		RotateMesh bullet(bulletlist.Last()).mesh, 0, 0, 270
		
		'enable collisions - not needed if using line pick to align bullets
	'	bullet(bulletlist.Last()).piv.EntityType(DYNAMIC, False)
	 '	bullet(bulletlist.Last()).piv.EntityRadius(0.1)
		
	EndIf

'update the bullets
	For Local b:bullet = EachIn bulletlist
		
		'align bullets with planetoid - enable bullet collisions above if not using line pick
		
		'align without line pick
		'Local cdx2:Float = EntityX(b.piv, 1) - EntityX(b.planetoid, 1)
		'Local cdy2:Float = EntityY(b.piv, 1) - EntityY(b.planetoid, 1)
		'Local cdz2:Float = EntityZ(b.piv, 1) - EntityZ(b.planetoid, 1)
		'AlignToVector2(b.piv, cdx2, cdy2, cdz2, 3, 1)
		
		'align with linepick
		LinePick(EntityX(b.piv), EntityY(b.piv), EntityZ(b.piv), EntityX(b.planetoid) - EntityX(b.piv), EntityY(b.planetoid) - EntityY(b.piv), EntityZ(b.planetoid) - EntityZ(b.piv), 0.0)
		AlignToVector2(b.piv, PickedNX(), PickedNY(), PickedNZ(), 3, 1)
	
		'move bullet - z movements represents gravity
		MoveEntity (b.piv, 0.8, 0, -0.03)
		
		'kill bullets and remove from list
		b.life:-1
		If b.life &lt;= 0 'Or EntityVisible(b.mesh, cam) = False
			b.planetoid = Null
			FreeEntity b.piv
			FreeEntity b.mesh
			bulletlist.remove b
		End If
	Next


'put player on planet surface
	If EntityCollided(p1.piv, STATIC)
		LinePick(EntityX(p1.piv), EntityY(p1.piv), EntityZ(p1.piv), EntityX(p1.planetiod) - EntityX(p1.piv), EntityY(p1.planetiod) - EntityY(p1.piv), EntityZ(p1.planetiod) - EntityZ(p1.piv))
		AlignToVector2(p1.piv, PickedNX(), PickedNY(), PickedNZ(), 3, 1)
		
		
		p1.nx = PickedNX()
		p1.ny = PickedNY()
		p1.nz = PickedNZ()
		
		p1.jumptimer = MilliSecs()
		p1.vx = 0
		p1.vy = 0
		p1.vz = 0
		
	Else
		
		LinePick(EntityX(p1.piv), EntityY(p1.piv), EntityZ(p1.piv), EntityX(p1.planetiod) - EntityX(p1.piv), EntityY(p1.planetiod) - EntityY(p1.piv), EntityZ(p1.planetiod) - EntityZ(p1.piv))
			AlignToVector2(p1.piv, PickedNX(), PickedNY(), PickedNZ(), 3, 0.1)
			
		
			p1.nx = PickedNX()
			p1.ny = PickedNY()
			p1.nz = PickedNZ()
		
			p1.vx:-PickedNX() * 0.05
			p1.vy:-PickedNY() * 0.05
			p1.vz:-PickedNZ() * 0.05
			TranslateEntity(p1.piv, p1.vx, p1.vy, p1.vz)
	End If

'move and turn player in relation to camera with keyboard
	If KeyDown(KEY_LEFT)
		TFormVector(-0.25, 0, 0, cam, Null)
		TranslateEntity p1.piv, TFormedX(), TFormedY(), TFormedZ()
		
		playerang = 270
	ElseIf KeyDown(KEY_RIGHT)
		TFormVector(0.25, 0, 0, cam, Null)
		TranslateEntity p1.piv, TFormedX(), TFormedY(), TFormedZ()
		
		playerang = 90
	End If
	
	If KeyDown(KEY_UP)
		TFormVector(0, 0.25, 0, cam, Null)
		TranslateEntity p1.piv, TFormedX(), TFormedY(), TFormedZ()
	
		playerang = 0
	ElseIf KeyDown(KEY_DOWN)
		TFormVector(0, -0.25, 0, cam, Null)
		TranslateEntity p1.piv, TFormedX(), TFormedY(), TFormedZ()
		
		playerang = 180
	End If
	
	'rotate player to target angle
		TFormVector (0, 1, 0, cam, Null)
		AlignToVector2 (p1.piv, TFormedX(), TFormedY(), TFormedZ(), 2, 1)
		p1.mesh2.MQ_Turn(playerang - lastang, 0, 0, 1)
		lastang = playerang

'Move and turn player with joystick	
	If Abs(JoyX(0)) &gt; 0.2
		TFormVector(JoyX() * 0.25, 0, 0, cam, Null)
		TranslateEntity p1.piv, TFormedX(), TFormedY(), TFormedZ()
	
	End If
	
	If Abs(JoyY()) &gt; 0.2
		TFormVector(0, -JoyY() * 0.25, 0, cam, Null)
		TranslateEntity p1.piv, TFormedX(), TFormedY(), TFormedZ()
		
	End If
	
	'turn stuff with joystick
	If Abs(JoyX()) &gt; 0.2 Or Abs(JoyY()) &gt; 0.2
		TFormVector (0, 1, 0, cam, Null)
		AlignToVector2 (p1.piv, TFormedX(), TFormedY(), TFormedZ(), 2, 1)
		playerang = calculateAngle(JoyX(), JoyY(), 0, 0) + 180
		p1.mesh2.MQ_Turn(playerang - lastang, 0, 0, 1)
		lastang = playerang
	EndIf
	
'make player jump
	If (KeyDown(KEY_SPACE) Or JoyDown(1)) And p1.jumptimer + 10 &gt; MilliSecs()
		p1.vx = p1.vx + 1.01 * p1.nx
		p1.vy = p1.vy + 1.01 * p1.ny
		p1.vz = p1.vz + 1.01 * p1.nz
	
	End If

	
'move camera	
	
	Local cdx2:Float = EntityX(p1.piv2, 1) - EntityX(p1.piv, 1)
	Local cdy2:Float = EntityY(p1.piv2, 1) - EntityY(p1.piv, 1)
	Local cdz2:Float = EntityZ(p1.piv2, 1) - EntityZ(p1.piv, 1)
	AlignToVector2(p1.piv2, -cdx2, -cdy2, -cdz2, 2, 0.5)


	If cameramovementstyle = CAM_HUG_NORMALS
		'normal hugging cam
		Local ctx:Float = ((EntityX(p1.piv) + p1.nx * 30) - camx) * 0.075
		Local cty:Float = ((EntityY(p1.piv) + p1.ny * 30) - camy) * 0.075
		Local ctz:Float = ((EntityZ(p1.piv) + p1.nz * 30) - camz) * 0.075
		camx:+ctx
		camy:+cty
		camz:+ctz
		PositionEntity cam, camx, camy, camz'p1.nx * 30, p1.ny * 30, p1.nz * 30
	ElseIf cameramovementstyle = CAM_SMOOTH_CHASE
		'smoothspinning cam
		Local ctx:Float = ((EntityX(cb, 1)) - camx) * 0.2
		Local cty:Float = ((EntityY(cb, 1)) - camy) * 0.2
		Local ctz:Float = ((EntityZ(cb, 1)) - camz) * 0.2
		camx:+ctx
		camy:+cty
		camz:+ctz
		PositionEntity cam, camx, camy, camz'p1.nx * 30, p1.ny * 30, p1.nz * 30
	EndIf
	
'make camera face player
	Local cdx:Float = EntityX(cam, 1) - EntityX(p1.piv, 1)
	Local cdy:Float = EntityY(cam, 1) - EntityY(p1.piv, 1)
	Local cdz:Float = EntityZ(cam, 1) - EntityZ(p1.piv, 1)
	
	AlignToVector2(cam, -cdx, -cdy, -cdz, 3, 0.5)
	
'switch planetoids with mouse click
	Local Pick:TEntity
	If MouseDown(1)
		Pick:TEntity = CameraPick(cam, MouseX(), MouseY())
		If Pick &lt;&gt; Null
			p1.planetiod = Pick
			p1.Reset()
			For Local i:Int = 0 To 49
				If p1.planetiod = ast[i].mesh
					cameramovementstyle = ast[i].camstyle
				EndIf
			Next
			If Pick = plan1.mesh
				cameramovementstyle = CAM_SMOOTH_CHASE
			End If
			PositionEntity p1.piv2, EntityX(Pick), EntityY(Pick), EntityZ(Pick)
			
			PositionEntity p1.piv, PickedX(), PickedY(), PickedZ()
			ClearCollisions()
		End If
	End If
	
	
'render stuff
'Wireframe True
	UpdateWorld()
	RenderWorld
	
	renders = renders + 1


	If MilliSecs()-old_ms&gt;=1000
		old_ms=MilliSecs()
		fps=renders
		renders=0
	EndIf
	
	BeginMax2D()
	DrawText bulletlist.count(), 10, 10
	DrawText "fps: " + fps, 10, 20
	EndMax2D()
	
	Flip 0
Wend


Type player
	Field mesh2:TMesh
	Field mesh:TEntity
	Field piv:TPivot
	Field piv2:TPivot
	Field planetiod:TEntity
	Field jumptimer:Int
	Field nx:Float, ny:Float, nz:Float
	Field vx:Float, vy:Float, vz:Float
	
	Method Reset()
		nx = 0;ny = 0;nz = 0
		vx = 0;vy = 0;vz = 0
	End Method
	
EndType

Type bullet
	Field mesh:TMesh
	Field piv:TPivot
	Field planetoid:TEntity
	Field life:Int = 180
EndType

Type planet
	Field mesh:TEntity
	Field camstyle:Int
EndType

Function calculateAngle:Float(x1:Float,y1:Float,x2:Float,y2:Float)
	Local theX:Float=x1-x2
	Local theY:Float=y1-y2
	Local theAngle:Float=-ATan2(theX,theY)
	Return theAngle
End Function

</textarea><br><br>Cheers<br>Charlie <br><br></td></tr></table><br>
<a name="1082977"></a>

<a name="1082978"></a>

<a name="1083026"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well, you are now using EntityPickmode 2, which is slower than EntityPickmode 1 or 3. Maybe you can use them instead?<br>Line 33/34:<pre class=code>		EntityPickMode ast[i].mesh, 1
		EntityRadius ast[i].mesh, 1</pre><br>Alternatively, you could investigate if you can keep an object on a planet by using maths only.<br>To elaborate a bit on that idea, the following projects lat/long coords onto a sphere. There might be much more suitable solutions for this, but unf. it is as far as my math skills go:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Import sidesign.minib3d

Graphics3D 800, 600, 0, 2

CreateLight()

cam = CreateCamera()
MoveEntity cam, 0, 0, -5

planet = CreateSphere()
EntityColor planet, 0, 0, 255

px# = 0
py# = 0

player = CreateCone()
ScaleMesh player, 0.1, 0.25, 0.1
EntityColor player, 255, 0, 255


Repeat

	If KeyDown(37) px = px - 0.5
	If KeyDown(39) px = px + 0.5
	If KeyDown(38) py = py - 0.5
	If KeyDown(40) py = py + 0.5

	ProjectSphere( player, px, py )
	RenderWorld
	Flip

Until KeyHit(27)


Function ProjectSphere( ent:TEntity, lat#, lon# )

	px# = Cos(lat) * Cos(lon)
	py# = Sin(lat)
	pz# = Cos(lat) * Sin(lon)
	
	PositionEntity ent, px*1.2, py*1.2, pz*1.2 '1.2 is radius

	AlignToVector2 ent, px, py, pz, 2, 1 'since sphere is centered around 0,0,0  normal=same as xyz
	
End Function</textarea><br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1083048"></a>

<a name="1083049"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jkrankie</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm familiar with the spherical coordinate calculation you've put here (i've used it lots in the past, most recently to make and animate a massive jellyfish!), and you're right, it's fine for spheres. However, the planets are not exclusively spherical, if you click your mouse on one of the cubes or cylinders in the background and fly to that planet, the bullets will need to track round the cube, so it's not always a spherical motion. In fact, you can, in theory, load any mesh you like and use that as a planet. Try it!<br><br>What i'm trying to avoid is to have to come up with different methods for moving things round each planetoid shape. The linepick method i have works exactly how i want it to (i.e, it works on every shape i've tried, and a few different meshes i've loaded in), but it's super slow, and even when i've played around with the different pick modes the speed up is minimal/unnoticeable.<br><br>If i stay with aligning bullets to the normals of a mesh, i'll need to find a faster way of determining the closest surface to the bullet is. I know you can get the surface data from a mesh object, but it's working out the fastest way to get the right surface, and then the normal from it...<br><br>Cheers<br>Charlie<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1083069"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> And are you enabling/disabling pick modes for objects that don't need to be tested? Because I believe 'picking' loops through every pickable object.<br>You might benefit from customizing the picking routines. It uses a List to emulate the Blitz3D behaviour, but it doesn't need to. You could also modify it to check on only a single object instead.<br>If your meshes are built in a certain predefined way, it might be easier to optimize the routine further. Say, if you convert your players xyz coords to lon/lat coords, you could do a 2d rectsoverlap or point-in-triangle test. <br>First you could divide your mesh into 'areas' (I mean quad shaped groups of triangles), determine in which 'area' the player is and then perform a search to find the closest triangle within that certain group.<br>I wish I could say something more genius, but nope .. Hopefully someone else has this marvel suggestion. <br><br></td></tr></table><br>
<a name="1085024"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Paul "Taiphoz"</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Stupid question but iv downloaded the latest minib3d and tried to compile the above code but I get an unkown on the function aligntovector2.<br><br>am I missing some module or something ? <br><br></td></tr></table><br>
<a name="1085039"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ima747</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> aligntovector2 is not part of standard minib3d, the above code is using the variant of minib3d linked in the first post, developed by Warner. <br><br></td></tr></table><br>
<a name="1085045"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Paul "Taiphoz"</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> oh.. hmm wondering now which I should use why was there a need to have another version ? <br><br></td></tr></table><br>
<a name="1085048"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jkrankie</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Warner's version uses quaternion rotations, so it doesn't suffer from gimbal lock like the standard Minib3d does. I understand it's slightly slower though, on account of quaternion maths being more CPU intensive. <br><br>Cheers<br>Charlie <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
