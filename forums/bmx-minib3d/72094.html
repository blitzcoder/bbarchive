<!DOCTYPE html><html lang="en" ><head ><title >Minib3d and Maxgui (3d in a canvas)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Minib3d and Maxgui (3d in a canvas)</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=119" >MiniB3D Module</a>/<a href="#bottom" >Minib3d and Maxgui (3d in a canvas)</a><br><br>
<a name="805785"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >plash</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Whenever I make the window bigger the canvas doesn't update or something.<br><br>M3DCanvas.bmx<br><pre class=code>
SuperStrict

Framework BRL.GLGraphics

Import BRL.Win32MaxGUI
Import BRL.Timer
Import BRL.EventQueue
Import sidesign.minib3d

SetGraphicsDriver GLGraphicsDriver(), GRAPHICS_BACKBUFFER | GRAPHICS_DEPTHBUFFER

Global Wnd_Main:TGadget = CreateWindow("3d Canvas Test", 0, 0, 800, 600, Desktop(), WINDOW_TITLEBAR | WINDOW_RESIZABLE)
'MaximizeWindow Wnd_Main

Global Cnv_G3D:TGadget = CreateCanvas(0, 0, ClientWidth(Wnd_Main), ClientHeight(Wnd_Main), Wnd_Main)
SetGadgetLayout Cnv_G3D, 1, 1, 1, 1

TGlobal.width = ClientWidth(Cnv_G3D)
TGlobal.height = ClientHeight(Cnv_G3D)

TGlobal.depth = 32
TGlobal.mode = 0
TGlobal.rate = 60

SetGraphics CanvasGraphics(Cnv_G3D)

TGlobal.GraphicsInit()

Global cam:TCamera = CreateCamera()
PositionEntity cam, 0, 0, -15

Global light:TLight = CreateLight()
RotateEntity light, 45, 0, 0

Global marker:TMesh = CreateSphere()
ScaleEntity marker, 0.15, 0.15, 0.15
EntityColor marker, 255, 0, 0

Local sphere:TMesh = CreateSphere()
EntityRadius sphere, 1
EntityPickMode sphere, 1
PositionEntity sphere, -10, 0, 0

Local box:TMesh = CreateCube()
FitMesh box, -2, -1, -1, 4, 2, 2
EntityBox box, -2, -1, -1, 4, 2, 2
EntityPickMode box, 3
PositionEntity box, 10, 0, 0

;' used by camera code
Global mxs#=0
Global mys#=0
Global move#=0.5

;' used by fps code
Global old_ms:Int = MilliSecs()
Global renders:Int
Global fps:Int

Global mx:Int = 0, my:Int = 0

Global MST_LEFT:Int = 0, MST_RIGHT:Int = 0
Global KST_A:Int = 0, KST_D:Int = 0, KST_W:Int = 0, KST_S:Int = 0, KST_Q:Int = 0, KST_SPACE:Int = 0

CreateTimer(60)
While Not KeyDown(key_escape)	
	Select WaitEvent()
		Case EVENT_TIMERTICK
			Cnv_G3D_Input
			
			RedrawGadget Cnv_G3D
			
		Case EVENT_MOUSEMOVE
			mx = EventX()
			my = EventY()
			
		Case EVENT_KEYDOWN
			Select EventData()
				Case KEY_ESCAPE End
				Case KEY_W KST_W = 1
				Case KEY_S KST_S = 1
				Case KEY_A KST_A = 1
				Case KEY_D KST_D = 1
				Case KEY_Q KST_Q = 1
				Case KEY_SPACE KST_SPACE = 1
				
			End Select
			
		Case EVENT_KEYUP
			Select EventData()
				Case KEY_W KST_W = 0
				Case KEY_S KST_S = 0
				Case KEY_A KST_A = 0
				Case KEY_D KST_D = 0
				Case KEY_Q KST_Q = 0
				Case KEY_SPACE KST_SPACE = 0
				
			End Select
				
		Case EVENT_MOUSEDOWN
			Select EventData()
				Case 1
					EntityColor box, 255, 255, 255
					EntityColor sphere, 255, 255, 255
					
					Local pick:TEntity = CameraPick(cam, mx, my)
					'Local pe:TEntity = PickedEntity()
					'Local ps:TSurface = PickedSurface()
					
					If pick
						DebugLog "Picked!"
						'DebugLog "PickedX(): " + PickedX()
						'DebugLog "PickedY(): " + PickedY()
						'DebugLog "PickedZ(): " + PickedZ()
						'DebugLog "PickedNX(): " + PickedNX()
						'DebugLog "PickedNY(): " + PickedNY()
						'DebugLog "PickedNZ(): " + PickedNZ()
						'DebugLog "PickedTime(): " + PickedTime()
						'DebugLog "PickedEntity(): " + PickedEntity()
						'DebugLog "PickedSurface(): " +  PickedSurface()
						'DebugLog "PickedTriangle(): " + PickedTriangle()
						EntityColor PickedEntity(), 255, 255, 0
						PositionEntity marker, PickedX(), PickedY(), PickedZ()
						
					Else
						DebugLog "Not Picked"
						
					EndIf
					
				Case 2
					MST_LEFT = 1
					
			End Select
			
		Case EVENT_MOUSEUP
			Select EventData()
				Case 2
					MST_LEFT = 0
					
			End Select
			
		Case EVENT_WINDOWSIZE
			'SetGadgetShape Cnv_G3D, 0, 0, ClientWidth(Wnd_Main), ClientHeight(Wnd_Main)
			TGlobal.width = ClientWidth(Cnv_G3D)
			TGlobal.height = ClientHeight(Cnv_G3D)
			
		Case EVENT_WINDOWCLOSE
			End
			
		Case EVENT_GADGETPAINT
			
			'SetGraphics CanvasGraphics(Cnv_G3D)
			
			MouseXSpeed2()
			MouseYSpeed2()
			
			RenderWorld
			renders = renders + 1
			
			If MilliSecs() - old_ms &gt;= 1000
				old_ms = MilliSecs()
				fps = renders
				renders = 0
			EndIf
	
			Text 0, 0, "FPS: " + fps
			
			Flip
			
	End Select
Wend
End

Function Cnv_G3D_Input()
	If KST_W MoveEntity cam, 0, 0, move#
	If KST_S MoveEntity cam, 0, 0, -move#
	If KST_A MoveEntity cam, -move#, 0, 0
	If KST_D MoveEntity cam, move#, 0, 0
	
	If KST_SPACE PositionEntity cam, 0, 0, -15
	
	If MST_LEFT
	  mxs# :+ MouseXSpeed2() / 5.0
	   mys# :+ MouseYSpeed2() / 5.0
		If KST_Q
			MoveEntity cam, mxs# / 50.0, -mys# / 50.0, 0
			
		Else
			RotateEntity cam, mys#, -mxs#, 0
			
		EndIf
		
	EndIf
	
End Function

Function MouseXSpeed2:Int()
	Global oldmx:Int
	
	Local mxs:Int = mx - oldmx
	oldmx = mx
	
	Return mxs
	
End Function

Function MouseYSpeed2:Int()
	Global oldmy:Int
	
	Local mys:Int = my - oldmy
	oldmy = my
	
	Return mys
	
End Function
</pre> <br><br></td></tr></table><br>
<a name="805807"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >klepto2</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>
Case EVENT_WINDOWSIZE
			'SetGadgetShape Cnv_G3D, 0, 0, ClientWidth(Wnd_Main), ClientHeight(Wnd_Main)
			TGlobal.width = ClientWidth(Cnv_G3D)
			TGlobal.height = ClientHeight(Cnv_G3D)
                        CameraViewport(Cam,0,0,TGlobal.Width,TGlobal.Height)
</pre><br><br>You have to manually update the CameraViewport to the new size. <br><br></td></tr></table><br>
<a name="805875"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >plash</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
