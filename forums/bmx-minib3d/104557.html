<!DOCTYPE html><html lang="en" ><head ><title >Mouse coordinates wrong when resizing minib3d canv</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Mouse coordinates wrong when resizing minib3d canv</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=119" >MiniB3D Module</a>/<a href="#bottom" >Mouse coordinates wrong when resizing minib3d canv</a><br><br>
<a name="1267971"></a>

<a name="1268006"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Created a minib3d canvas in maxgui like shown in the sample.  Using mouse coordiantes for Camerapick() works fine until I resize the window, then the mouse is off by a few pixels.  The more I resize, the more the pointer is off.  Tried SetVirtualResolution, but get an error.<br><br>edit: after further experiment I've discovered it is not the mouse that is wrong, but CameraPick() is selecting the wrong triangle after a resize.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict
Import maxgui.drivers
Import sidesign.minib3d



'******************************************************
'*
'*   GUI Elements are defined here
'*
'******************************************************
GLShareContexts()
SetGraphicsDriver GLGraphicsDriver(),GRAPHICS_BACKBUFFER|GRAPHICS_DEPTHBUFFER


Global MainWindow:TGadget = CreateWindow("BlockAid",0,0,800,600,Null,WINDOW_DEFAULT|WINDOW_TOOL|WINDOW_CENTER)
Global OpenTool:TGadget = CreatePanel(0,0,400,32,MainWindow)
Global _3dPanel:TGadget = CreatePanel(0,32,ClientWidth(MainWindow),ClientHeight(MainWindow)-32,MainWindow,PANEL_SUNKEN)
SetGadgetLayout _3dPanel,EDGE_ALIGNED,EDGE_ALIGNED,EDGE_ALIGNED,EDGE_ALIGNED

Global _3dCanvas:TGadget = CreateCanvas(0,0,ClientWidth(_3dPanel),ClientHeight(_3dPanel),_3dPanel)
SetGadgetLayout _3dCanvas,EDGE_ALIGNED,EDGE_ALIGNED,EDGE_ALIGNED,EDGE_ALIGNED

Global CX:Int,CY:Int, CZ:Int
Global Piece:TEntity[6,6,6]


TGlobal.width=ClientWidth(_3dCanvas)
TGlobal.height=ClientHeight(_3dCanvas)

TGlobal.depth=32
TGlobal.mode=0
TGlobal.rate=60

SetGraphics CanvasGraphics(_3dCanvas)

TGlobal.GraphicsInit()
Global Pivot:TPivot = CreatePivot()
Global Camera:TCamera = CreateCamera(Pivot)
Global Light:TLight = CreateLight()
PositionEntity Camera,0,0,-10
PointEntity Camera,Pivot
RotateEntity Pivot,30,0,0
CameraZoom Camera,1.9

Global v0:Int
Global v1:Int
Global v2:Int
Global v3:Int

'Create the 3d plane where we will build the model
Global Matt:TMesh = CreateMesh()
Local Surface:TSurface = CreateSurface(Matt)
For Local x:Int = -3 To 2
	For Local y:Int = -3 To 2
		v0:Int = AddVertex(Surface,x,0,y,0,0)
		VertexNormal(Surface,v0,0,1,0)
		v1:Int = AddVertex(Surface,x+1,0,y,1,0)
		VertexNormal(Surface,v1,0,1,0)
		v2:Int = AddVertex(Surface,x+1,0,y+1,1,1)
		VertexNormal(Surface,v2,0,1,0)
		v3:Int = AddVertex(Surface,x,0,y+1,0,1)
		VertexNormal(Surface,v3,0,1,0)
		AddTriangle(Surface,v0,v2,v1)
		AddTriangle(Surface,v0,v3,v2)
	Next
Next
Global Texture:TTexture = LoadTexture("Textured-Marble-Floor-Tiles-P60115-.jpg")
EntityTexture Matt,Texture
EntityPickMode Matt,2

'This is the "pointer" which will show us where a new block will be created, or existing block destroyed
Global Cursor:TMesh = CreateMesh()
Surface = CreateSurface(Cursor)
v0 = AddVertex(Surface,0,0,0,0,0)
v1 = AddVertex(Surface,1,0,0,1,0)
v2 = AddVertex(Surface,1,-1,0,1,1)
v3 = AddVertex(Surface,0,-1,0,0,1)
AddTriangle Surface,v0,v2,v1
AddTriangle Surface,v0,v3,v2
EntityColor Cursor,255,128,128
EntityAlpha Cursor,.5
UpdateNormals Cursor


RedrawGadget _3dCanvas

CreateTimer(60)

Global KeyLeft:Int = False
Global KeyRight:Int = False
Global MX:Int, MY:Int

'******************************************
'*
'*  Event Loop
'*
'*****************************************
AddHook EmitEventHook, MainHook
Repeat
	WaitEvent()
Forever

Function MainHook:Object(id:Int, data:Object, context:Object)
	Local event:TEvent = TEvent(data)
	
	Select event.id
		Case EVENT_WINDOWCLOSE, EVENT_APPTERMINATE
			End
		Case EVENT_KEYDOWN
			Select event.data
				Case KEY_ESCAPE
					End
				Case KEY_LEFT
					KeyLeft = True
				Case KEY_RIGHT
					KeyRight = True
				Case KEY_SPACE
					HideEntity Matt
			End Select
		Case EVENT_KEYUP
			Select event.data
				Case KEY_LEFT
					KeyLeft = False
				Case KEY_RIGHT
					KeyRight = False
				Case KEY_SPACE
					ShowEntity Matt
			End Select
		Case EVENT_MOUSEMOVE
			MX = event.x
			MY = event.y
		Case EVENT_MOUSEDOWN
			Select event.data
				Case 1
					CX = EntityX(Cursor)+3
					CZ = EntityZ(Cursor)+3
					CY = EntityY(Cursor)
					DebugLog(CX+","+CY+","+CZ)
				Case 2
					DebugLog("right")
				Case 3
					DebugLog("center")
					
			End Select
			
		Case EVENT_WINDOWSIZE
			TGlobal.Width = ClientWidth(_3dPanel)
			TGlobal.Height = ClientHeight(_3dPanel)-32
			Camera.CameraViewport(0,32,TGlobal.Width,TGlobal.Height)
		Case EVENT_TIMERTICK
			If KeyLeft Then TurnEntity Pivot,0,-.4,0
			If KeyRight Then TurnEntity Pivot,0,.4,0
			Local Entity:TEntity = CameraPick(Camera,MX,MY) 
			If Entity Then
				Local Surface:TSurface = PickedSurface()
				Local Triangle:Int = PickedTriangle()
				Local Vertex:Int = TriangleVertex(Surface,Triangle,0)
				
				PositionEntity Cursor,VertexX(Surface,Vertex),VertexY(Surface,Vertex),VertexZ(Surface,Vertex)
				Local vx:Float = VertexNX(Surface,Vertex)
				Local vy:Float = VertexNY(Surface,Vertex)
				Local vz:Float = VertexNZ(Surface,Vertex)
				RotateEntity Cursor,VectorPitch(vx,vy,vz),VectorYaw(vx,vy,vz),0
				MoveEntity Cursor,0,0,.001
			End If
			RedrawGadget _3dCanvas
 
		Case EVENT_GADGETPAINT
			If Event.Source = _3dCanvas Then
				SetGraphics CanvasGraphics(_3dCanvas)
				RenderWorld
				Flip
			End If
	End Select
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1268135"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> NM, figured this one out on my own.  Had subtracted 32 from the top of the window to make room for a custom icon bar I am going to create.  Decided to go with a panel instead and forgot to remove the -32 from the EVENT_WINDOWSIZE routine. <br><br></td></tr></table><br>
<a name="1268138"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oh good. I hadn't got as far as testing this but was just going to suggest using EndGraphics/Graphics and reloading everything. Glad to know this isn't a bug/feature. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
