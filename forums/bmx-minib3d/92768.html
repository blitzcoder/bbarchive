<!DOCTYPE html><html lang="en" ><head ><title >SMesh - Static Mesh exporter for 3D Max</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >SMesh - Static Mesh exporter for 3D Max</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=119" >MiniB3D Module</a>/<a href="#bottom" >SMesh - Static Mesh exporter for 3D Max</a><br><br>
<a name="1058632"></a>

<a name="1058648"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm not very happy about b3dpipeline not being updated for the latest 3D MAX for quite some time (b3dpipeline is great, but having no source to it, and no updates since 2008 is bad); so I decided to do something about it.<br><br>I looked yesterday and and today the basics of MAXScript - and just made a small "static mesh" (.smesh) exporter. It's fairly basic, it will export mesh xyz, normals and uv data - no texture or materials, no bones or animations.<br><br>One of the best things about it being a script, is that it will work on pratically any version of MAX - no need to recompile anything. <br><br>The script will export all EDITABLE MESHES on the scene - not anything else. <br><br>Please test it - so far I just did some basic testing, and it's working: see screenshot below for that ;)<br><br>This is the MaxScript:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
-- SMesh Exporter version 0.01
-- export static meshes with uv and normal coords from max to file
-- (c) Ikone Soft Programas de Computador ltda
-- Selling or distributing this code, in part or full, without prior written consent is expressly forbidden.
-- If you use this with commercial purposes, please make a donation at
-- <a href="https://www.paypal.com/cgi-bin/webscr?cmd=_s-xclick&amp;hosted_button_id=1314394" target="_blank">https://www.paypal.com/cgi-bin/webscr?cmd=_s-xclick&amp;hosted_button_id=1314394</a>
--
-- use: open in maxscript, select this whole piece of code and eval it
-- it will export every EDITABLE MESH on the scene to a .smesh file

fname = getSaveFileName caption:"Export to file:" filename:"test.smesh"
deletefile fname
f = fopen fname "wb"

    for mesh in objects do
    (
		if superclassof mesh == GeometryClass and ClassOf mesh == Editable_mesh then
  		(
	        -- object name
			writestring f mesh.name 
			
			if mesh.numVerts &gt; 0 then	
			(
				-- vertex and normal data
				writelong f mesh.numVerts
				for i = 1 to mesh.numVerts do
				(
					vert = ((GetVert mesh i))
					writefloat f vert.x
					writefloat f vert.y
					writefloat f vert.z
					
					normal = GetNormal mesh i
					writefloat f normal.x
					writefloat f normal.y
					writefloat f normal.z
				)
			)

			if mesh.numTVerts &gt; 0 then 	
			(
				-- UV tex coords
				writelong f mesh.numTVerts
				for i = 1 to mesh.numTVerts do
				(
					uvw = GetTVert mesh i
					writefloat f uvw.x
					writefloat f uvw.y
				)
			)

			if mesh.NumFaces &gt; 0 then
			(
				-- faces (1 face = 2 triangles)
				writelong f mesh.numFaces
				for i = 1 to mesh.numFaces do
				(
					poly = GetFace mesh i
					tvert = GetTVFace mesh i
					-- write face index
					writelong f (poly.x as integer -1)
					writelong f (poly.y as integer -1)
					writelong f (poly.z as integer -1)
					-- write face uv index
					writelong f (tvert.x as integer -1) 
					writelong f (tvert.y as integer -1) 
					writelong f (tvert.z as integer -1)					
				)
			)
			
		)
    )
fclose f
</textarea><br><br>This is the loading code:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Type TUV
	Field u#, v#
End Type

Type TMeshFace
	Field i1%, i2%, i3%
	Field uv1%, uv2%, uv3%
End Type

Function LoadSMesh:TEntity(fname:String)
Local numVerts%
Local numNormal%
Local numFaces%
Local numUV%

Local file:TStream = OpenStream("littleendian::"+fname,True,False)
Local name$
Local b:Byte, f:Int

	Local MasterPivot:TPivot=CreatePivot()
	NameEntity MasterPivot, "Scene Root"
	
	While Not Eof(file)
		' read mesh name	
		name$=""
		Repeat
			b=ReadByte(file)
			If b&lt;&gt;0 Then  name$=name$+Chr$(b)
		Until b=0
		
		DebugLog "mesh name:" + name$
		numVerts=ReadInt(file)
		DebugLog "number of vertex:" + numVerts
		
		Local verts:TVector[numVerts+1]
		Local norms:TVector[numVerts+1]
	
		' read vertex pos and normals	
		For f=0 Until numVerts
			' read x,y,z
			verts[f] = New TVector
			verts[f].x = ReadFloat(file)
			verts[f].z = ReadFloat(file)
			verts[f].y = ReadFloat(file)
			
			' read vertex normal
			norms[f] = New TVector
			norms[f].x = -ReadFloat(file)
			norms[f].y = -ReadFloat(file)
			norms[f].z = -ReadFloat(file)
			
			'DebugLog "verts " + f + " xyz:" + verts[f].x + "," + verts[f].y + "," + verts[f].z
			'DebugLog "norms " + f + " xyz:" + norms[f].x + "," + norms[f].y + "," + norms[f].z
		Next
		
		' read number of UV coords
		numUV = ReadInt(file)
		DebugLog "number of UV coords:" + numUV
		Local uvw:TUV[numUV]
		' check if it has UV coordinates...
		If numUV&gt;0 Then
			' read texture UV coordinates
			For f=0 Until numUV
				uvw[f] = New TUV
				uvw[f].u = ReadFloat(file)
				uvw[f].v = -ReadFloat(file)
				
				'DebugLog "uv coords " + f + ": " + uvw[f].u + "," + uvw[f].v
			Next
		End If
		
		' read number of faces
		numFaces = ReadInt(file)
		DebugLog "number of faces:" + numFaces
		Local faces:TMeshFace[numFaces]
		
		For f=0 Until numFaces
		    faces[f] = New TMeshFace
		
			' this face vertex index
			faces[f].i1 = ReadInt(file)
			faces[f].i2 = ReadInt(file)
			faces[f].i3 = ReadInt(file)
	
			'DebugLog "faces " + f + " index:" + faces[f].i1 + "," + faces[f].i2 + "," + faces[f].i3
			
			' this face vertex uvw index
			faces[f].uv1 = ReadInt(file)
			faces[f].uv2 = ReadInt(file)
			faces[f].uv3 = ReadInt(file)
		Next
		
		' create the actual mesh
		Local mesh:TMesh = CreateMesh()
		Local surf:TSurface=CreateSurface(mesh)
		
		' loop through all faces
		For f=0 Until numFaces
			Local v1% = surf.AddVertex(verts[faces[f].i1].x, verts[faces[f].i1].y, verts[faces[f].i1].z, uvw[faces[f].uv1].u, uvw[faces[f].uv1].v, 0)	
			Local v2% = surf.AddVertex(verts[faces[f].i2].x, verts[faces[f].i2].y, verts[faces[f].i2].z, uvw[faces[f].uv2].u, uvw[faces[f].uv2].v, 0)
			Local v3% = surf.AddVertex(verts[faces[f].i3].x, verts[faces[f].i3].y, verts[faces[f].i3].z, uvw[faces[f].uv3].u, uvw[faces[f].uv3].v, 0)

			surf.VertexNormal v1, norms[faces[f].i1].x, norms[faces[f].i1].y, norms[faces[f].i1].z
			surf.VertexNormal v2, norms[faces[f].i2].x, norms[faces[f].i2].y, norms[faces[f].i2].z
			surf.VertexNormal v3, norms[faces[f].i3].x, norms[faces[f].i3].y, norms[faces[f].i3].z
			
			surf.AddTriangle v1,v3,v2
		Next	
		
		EntityParent mesh, MasterPivot
		NameEntity mesh, name$
	Wend
	
	CloseStream file
	
	Return MasterPivot
End Function
</textarea><br><br>Just call LoadSmesh(filename) in your program, and it will return an entity (actually a pivot named 'Scene Root') so you can manipulate the model (position, scale, etc)<br><br>Here's my first real test with this: <br><img src="http://img534.imageshack.us/img534/2748/smesh.jpg"><br><br><font class="tiny">Last edited 2010</font> <br><br></td></tr></table><br>
<a name="1058650"></a>

<a name="1058651"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> hmmm... apparently there's some problem with too many vertex. I tried exporting a mesh with 50k+ polys and it rendered completely wrong; optimised the mesh to less than 20k and it's working... is this some restriction on miniB3D part?<br><br>Edit: hmmm, no - exporting the full mesh to B3D works.... that's strange, to say the least :P<br><br><font class="tiny">Last edited 2010</font> <br><br></td></tr></table><br>
<a name="1058671"></a>

<a name="1058672"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jhocking</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> fopen fname "wb" <br></div><br><br>It's great that MaxScript can write binary, so why don't you write b3d files? Lately I've been working on writing b3d files from Python and it's not very complicated to do static meshes:<br><a href="http://www.blitzmax.com/Community/posts.php?topic=92467" target="_blank">http://www.blitzmax.com/Community/posts.php?topic=92467</a><br><br>I tried to do this once years ago but MEL (Maya's scripting language) didn't support writing binary; now that Maya has Python I may be able to write a b3d exporter.<br><br><font class="tiny">Last edited 2010</font> <br><br></td></tr></table><br>
<a name="1058709"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah, I want to in the end generate models in B3D format - but first I would like to get the basics working - and this 'too many vertex problem' is puzzling me right now :( <br><br></td></tr></table><br>
<a name="1058714"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jhocking</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Must say, if it were me I wouldn't bother to investigate this and just switch to writing b3d files. In your second post you said the problem went away went you exported the mesh to b3d. I would guess the problem has to do with limitations on generating the mesh in code, as opposed to minib3d generating the mesh during loading. <br><br></td></tr></table><br>
<a name="1058720"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> The problem went away when I used b3d pipeline to export the mesh - not my exporter. <br><br>I just tested that way to see if it wasn't a internal problem on miniB3D - I really don't know if it's a problem exporting the data (which apparently is written correct, at least the number of faces/vertex/etc), or a problem rendering the data. <br><br></td></tr></table><br>
<a name="1058727"></a>

<a name="1058728"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just for testing, the same loader for Blitz3D:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 640,480,0,2

camera=CreateCamera()
MoveEntity camera, 0,50, -100
AmbientLight 127,127,127

thing = LoadSmesh("buda.smesh")

light=CreateLight()
MoveEntity light, 0,1000,0

While Not KeyHit(1)
	Cls
	UpdateWorld
	RenderWorld
	Flip
Wend

End

;------------------------------------------------------
;------------------------------------------------------
;------------------------------------------------------
Type TUV
	Field u#, v#
End Type

Type TMeshFace
	Field i1, i2, i3
	Field uv1, uv2, uv3
End Type

Type TVector
	Field x#,y#,z#
End Type

Function LoadSMesh(fname$)
Local numVerts%
Local numNormal%
Local numFaces%
Local numUV%

Local file = OpenFile(fname)
Local name$
Local b%, f%

	Local MasterPivot=CreatePivot()
	NameEntity MasterPivot, "Scene Root"
	
	While Not Eof(file)
		; Read mesh name	
		name$=""
		Repeat
			b=ReadByte(file)
			If b&lt;&gt;0 Then  name$=name$+Chr$(b)
		Until b=0
		
		DebugLog "mesh name:" + name$
		numVerts=ReadInt(file)
		DebugLog "number of vertex:" + numVerts

		Local verts.TVector[65535]
		Local norms.TVector[65535]
		Local uvw.TUV[65535]
			
		; Read vertex pos And normals	
		For f=0 To numVerts-1
			;' Read x,y,z
			verts[f] = New TVector
			verts[f]\x = ReadFloat(file)
			verts[f]\z = ReadFloat(file)
			verts[f]\y = ReadFloat(file)
			
			; Read vertex normal
			norms[f] = New TVector
			norms[f]\x = ReadFloat(file)
			norms[f]\z = ReadFloat(file)
			norms[f]\y = ReadFloat(file) 
			
		Next
		
		;' Read number of UV coords
		numUV = ReadInt(file)
		DebugLog "number of UV coords:" + numUV
		;' check If it has UV coordinates...
		If numUV&gt;0 Then
			;' Read texture UV coordinates
			For f=0 To numUV-1
				uvw[f] = New TUV
				uvw[f]\u = ReadFloat(file)
				uvw[f]\v = -ReadFloat(file)
			Next
		End If
		
		;' Read number of faces
		numFaces = ReadInt(file)
		DebugLog "number of faces:" + numFaces
		Local faces.TMeshFace[65535]
		
		For f=0 To numFaces-1
		    faces[f] = New TMeshFace
		
			;' this face vertex index
			faces[f]\i1 = ReadInt(file)
			faces[f]\i2 = ReadInt(file)
			faces[f]\i3 = ReadInt(file)
	
			;' this face vertex uvw index
			faces[f]\uv1 = ReadInt(file)
			faces[f]\uv2 = ReadInt(file)
			faces[f]\uv3 = ReadInt(file)
		Next
		
		;' create the actual mesh
		Local mesh = CreateMesh(MasterPivot)
		Local surf = CreateSurface(mesh)
		
		;' loop through all faces
		For f=0 To numFaces-1
			Local v1% = AddVertex(surf, verts[faces[f]\i1]\x, verts[faces[f]\i1]\y, verts[faces[f]\i1]\z, uvw[faces[f]\uv1]\u, uvw[faces[f]\uv1]\v, 0)	
			Local v2% = AddVertex(surf, verts[faces[f]\i2]\x, verts[faces[f]\i2]\y, verts[faces[f]\i2]\z, uvw[faces[f]\uv2]\u, uvw[faces[f]\uv2]\v, 0)
			Local v3% = AddVertex(surf, verts[faces[f]\i3]\x, verts[faces[f]\i3]\y, verts[faces[f]\i3]\z, uvw[faces[f]\uv3]\u, uvw[faces[f]\uv3]\v, 0)

			VertexNormal surf, v1, norms[faces[f]\i1]\x, norms[faces[f]\i1]\y, norms[faces[f]\i1]\z
			VertexNormal surf, v2, norms[faces[f]\i2]\x, norms[faces[f]\i2]\y, norms[faces[f]\i2]\z
			VertexNormal surf, v3, norms[faces[f]\i3]\x, norms[faces[f]\i3]\y, norms[faces[f]\i3]\z
			
			;'surf.AddTriangle v2,v1,v3
			AddTriangle surf,v1,v3,v2
		Next	
		
		;'EntityParent mesh, MasterPivot
		NameEntity mesh, name$
	Wend
	
	CloseFile file
	
	Return MasterPivot
End Function
</textarea><br><br>On large meshes, it gives me a mav on renderworld... =(<br><br><font class="tiny">Last edited 2010</font> <br><br></td></tr></table><br>
<a name="1058794"></a>

<a name="1058795"></a>

<a name="1058796"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> You're in for a pickle, I'll tell you that. <br><br>@ your source: <br>Since when is one face 2 triangles? in MaxScript, when dealing with a Mesh value from a Max node, a single face means a single triangle.<br><br><font class="tiny">Last edited 2010</font> <br><br></td></tr></table><br>
<a name="1059189"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Is there any limit to how many faces or vertex a surface can have in miniB3D? <br><br>The exporter is working - if I sub-divide the mesh in smaller chunks and add the faces to different surfaces the mesh is rendered correctly. <br><br></td></tr></table><br>
<a name="1059197"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just by changing the loader code to this, I can load the high poly mesh:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
		' loop through all faces
		For f=0 Until numFaces
			If f Mod 21000 = 0 Then surf = CreateSurface(mesh)
			
			Local v1% = surf.AddVertex(verts[faces[f].i1].x, verts[faces[f].i1].y, verts[faces[f].i1].z, uvw[faces[f].uv1].u, uvw[faces[f].uv1].v, 0)	
			Local v2% = surf.AddVertex(verts[faces[f].i2].x, verts[faces[f].i2].y, verts[faces[f].i2].z, uvw[faces[f].uv2].u, uvw[faces[f].uv2].v, 0)
			Local v3% = surf.AddVertex(verts[faces[f].i3].x, verts[faces[f].i3].y, verts[faces[f].i3].z, uvw[faces[f].uv3].u, uvw[faces[f].uv3].v, 0)

			surf.VertexNormal v1, norms[faces[f].i1].x, norms[faces[f].i1].y, norms[faces[f].i1].z
			surf.VertexNormal v2, norms[faces[f].i2].x, norms[faces[f].i2].y, norms[faces[f].i2].z
			surf.VertexNormal v3, norms[faces[f].i3].x, norms[faces[f].i3].y, norms[faces[f].i3].z
			
			'surf.AddTriangle v2,v1,v3
			surf.AddTriangle v1,v3,v2
		Next
</textarea><br><br>Does anyone know if there are really a limit (apparently 21k faces) to miniB3D TSurface? <br><br></td></tr></table><br>
<a name="1059314"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Moving on, I already figure out how to read the material diffuse color, alpha level and glossiness (which I believe is the same as "EntityShininess" on blitz3D) - now just left to read the maps applied to the standard material, get the texture and it's done :)<br><br>(I know, it would be nice to have a second UV channel, more than 1 texture level too... but for a start this is good enough!) <br><br></td></tr></table><br>
<a name="1059321"></a>

<a name="1059323"></a>

<a name="1059324"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> It's working now!<br><br><img src="http://img232.imageshack.us/img232/5039/smeshtexture.jpg"><br><br>It will export editable mesh or editable poly - it will convert all editable poly on scene to editable mesh :P<br><br>I can now set the mesh as faceted, as double sided, change it's glossiness, the material color and even if I set another texture on another map than "diffuse", the program should be able to read it and apply it on the next texture level.<br><br>Textures are saved without any path, so they should be on the same folder as the mesh itself to load. I'm thinking of including a extra parameter on the loader function, to specify a path - so I can have the same mesh loaded over and over, and just change the texturepath, so it would apply a different texture automatically... anyone has some other crazy idea? <br><br>This is fun :)<br><br><font class="tiny">Last edited 2010</font> <br><br></td></tr></table><br>
<a name="1060536"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hujiklo</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well done - keep going. You'll become a MiniB3D hero with your own statue in the Blitz Hall of fame! <br><br></td></tr></table><br>
<a name="1066044"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gun Ecstasy</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is some excellent work!<br><br>Hujiklo said it best. Keep up the good work! <br><br></td></tr></table><br>
<a name="1073351"></a>

<a name="1073352"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Corum</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Is there any limit to how many faces or vertex a surface can have in miniB3D?<br><br>The exporter is working - if I sub-divide the mesh in smaller chunks and add the faces to different surfaces the mesh is rendered correctly.  <br></div><br>Yes, the limit exists: <a href="/posts.php?topic=91364" target="_blank">read this</a>.<br><br>Anyway, you did a great job with this exporter!<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
