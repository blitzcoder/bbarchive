<!DOCTYPE html><html lang="en" ><head ><title >AlignToVector, with slerp</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >AlignToVector, with slerp</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=119" >MiniB3D Module</a>/<a href="#bottom" >AlignToVector, with slerp</a><br><br>
<a name="1132635"></a>

<a name="1132636"></a>

<a name="1132637"></a>

<a name="1132638"></a>

<a name="1132639"></a>

<a name="1132640"></a>

<a name="1132645"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamRedwoods</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> This *almost* works, but I could use a little help for it to work properly and perhaps optimize it.<br>Dividing out the scale is a bit ugly, but is necessary. Is there a better way? Perhaps it would just be better to go with eulers? Or would euler lerping get gimbal lock?<br><br>Thank you all.<br><br>Test code: (slerp_test.bmx) You'll also need TQuaternion.bmx updated below.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import "../minib3d.bmx"

Strict

Local width=640,height=480,depth=16,mode=2

Graphics3D width,height,depth,mode,30

Local cam:TCamera=CreateCamera()
PositionEntity cam,0,5,-15

Local light:TLight=CreateLight()

Local ent:TMesh=LoadAnimMesh("media/zombie.b3d")

rem
ent.RotateEntity(45,45,10)

PrintMatrix(ent.mat)
Local quat:TQuaternion = New TQuaternion
quat.MatrixToQuat(ent.mat)
quat.QuatToMatrix(quat.x,quat.y,quat.z,quat.w,ent.mat)
Print ""
PrintMatrix(ent.mat)
endrem

ent.ScaleEntity(0.5,0.5,0.5)

Local cube:TMesh = CreateCube()
cube.PositionEntity(2,3,2)


Local anim_time#=0, axis%=2

' used by fps code
Local old_ms=MilliSecs()
Local renders=0
Local fps=0
Local keypause=0

While Not KeyDown(KEY_ESCAPE)		

	If KeyHit(KEY_ENTER) Then DebugStop

	' control camera
	MoveEntity cube,KeyDown(KEY_D)-KeyDown(KEY_A),0,KeyDown(KEY_W)-KeyDown(KEY_S)
	TurnEntity cam,KeyDown(KEY_DOWN)-KeyDown(KEY_UP),KeyDown(KEY_LEFT)-KeyDown(KEY_RIGHT),0
	
	If KeyDown(KEY_SPACE) And (MilliSecs()-keypause &gt; 100)
		cube.PositionEntity( cube.EntityX(), cube.EntityY()*-1, cube.EntityZ() )
		keypause = MilliSecs()
	EndIf
	
	If KeyDown(KEY_1) Then axis=1
	If KeyDown(KEY_2) Then axis=2
	If KeyDown(KEY_3) Then axis=3
	If KeyDown(KEY_MINUS) Then anim_time#=anim_time#-0.1
	If KeyDown(KEY_EQUALS) Then anim_time#=anim_time#+0.1
	
	SetAnimTime(ent,anim_time#)
	
	AlignToVector(ent, cube.EntityX() - ent.EntityX(), cube.EntityY()-ent.EntityY(), cube.EntityZ() - ent.EntityZ() , axis, 0.4)
	
	RenderWorld
	renders=renders+1
	
	' calculate fps
	If MilliSecs()-old_ms&gt;=1000
		old_ms=MilliSecs()
		fps=renders
		renders=0
	EndIf
	
	Text 0,0,"FPS: "+fps

	Flip
	
Wend
End


Function AlignToVector(ent:TEntity, vx:Float,vy:Float,vz:Float, axi:Int=1, rate:Float=1.0)
		
	Local dvec:TVector = TVector.Create(vx,vy,vz)
		
	Local dd# = dvec.Length()
	If dd &lt; 0.0001 Then Return
	dd=1.0/dd
	dvec = TVector.Create(vx * dd, vy * dd, vz * dd)
	'dvec = dvec.Normalize()
		
	'get axis to get our angle from (b/c rotations start at axis)	
	Local ovec:TVector
	If (axi=1) Then ovec = TVector.Create(1.0,0.0,0.0)
	If (axi=2) Then ovec = TVector.Create(0.0,1.0,0.0)
	If (axi=3) Then ovec = TVector.Create(0.0,0.0,1.0)
		
		
	''use axis-angle 
	Local angle:Float = ACos(ovec.Dot(dvec))
	'If angle &lt; 0.00001 Then Return
	Local axis:TVector = ovec.Cross(dvec)
	'axis.Normalize()
		
	Local invs:Float
	Local cs:Float = Cos(angle*0.5)
		
	invs = Sin(angle*0.5)		
	Local quat:TQuaternion = New TQuaternion

	quat.x = axis.x * invs
	quat.y = axis.y * invs
	quat.z = axis.z * invs
	quat.w = cs 'si * 0.5
		
		
	If angle &gt; 179.999
		quat.x = 0.9999
		quat.y = 0.0001
		quat.z = 0.0001
		quat.w = 0.0
	Else
		quat.Normalize()
	EndIf
		
	''axis angle to quat to matrix
	Local new_mat:TMatrix = New TMatrix
	Local quato:TQuaternion = New TQuaternion

	ent.mat.Scale(1.0/ent.sx,1.0/ent.sy,1.0/ent.sz) '' hmmm...
	quato.MatrixToQuat( ent.mat )
	quato.Normalize()
		

		
	quat.Slerp(quato.x,quato.y,quato.z,quato.w,  quat.x,quat.y,quat.z,quat.w, quat.x,quat.y,quat.z,quat.w,rate)
	quat.QuatToMatrix(quat.x,quat.y,quat.z,quat.w, new_mat)
		
'DebugLog quat.x,quat.y,quat.z,quat.w
'PrintMatrix new_mat
		
	new_mat.grid[3,0] = ent.px
	new_mat.grid[3,1] = ent.py
	new_mat.grid[3,2] = ent.pz
		
		
	'mat.LoadIdentity()
	If ent.parent&lt;&gt;Null
		ent.mat.Overwrite(ent.parent.mat)
		new_mat.Multiply(ent.mat) ''double check???????
	EndIf
		
	new_mat.Scale(ent.sx,ent.sy,ent.sz)
	ent.mat.Overwrite(new_mat)
		
	ent.rx = ent.mat.GetPitch()
	ent.ry = ent.mat.GetYaw()
	ent.rz = ent.mat.GetRoll()
	
	ent.UpdateChildren(ent)
		
EndFunction
</textarea><br><br>TQuaternion.bmx: (note: my functions are x,y,z,w)<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Type TQuaternion

	Field w#,x#,y#,z#

	Method New()
	
		If LOG_NEW
			DebugLog "New TQuaternion"
		EndIf
	
	End Method
	
	Method Delete()
	
		If LOG_DEL
			DebugLog "Del TQuaternion"
		EndIf
	
	End Method

	Function QuatToMat(w#,x#,y#,z#,mat:TMatrix Var)
	
		Local q:Float[4]
		q[0]=w
		q[1]=x
		q[2]=y
		q[3]=z
		
		Local xx#=q[1]*q[1]
		Local yy#=q[2]*q[2]
		Local zz#=q[3]*q[3]
		Local xy#=q[1]*q[2]
		Local xz#=q[1]*q[3]
		Local yz#=q[2]*q[3]
		Local wx#=q[0]*q[1]
		Local wy#=q[0]*q[2]
		Local wz#=q[0]*q[3]
	
		mat.grid[0,0]=1-2*(yy+zz)
		mat.grid[0,1]=  2*(xy-wz)
		mat.grid[0,2]=  2*(xz+wy)
		mat.grid[1,0]=  2*(xy+wz)
		mat.grid[1,1]=1-2*(xx+zz)
		mat.grid[1,2]=  2*(yz-wx)
		mat.grid[2,0]=  2*(xz-wy)
		mat.grid[2,1]=  2*(yz+wx)
		mat.grid[2,2]=1-2*(xx+yy)
		mat.grid[3,3]=1
	
		For Local iy=0 To 3
			For Local ix=0 To 3
				xx#=mat.grid[ix,iy]
				If xx#&lt;0.0001 And xx#&gt;-0.0001 Then xx#=0
				mat.grid[ix,iy]=xx#
			Next
		Next
	
	End Function
	
	Function QuatToEuler(w#,x#,y#,z#,pitch# Var,yaw# Var,roll# Var)
	
		Local q:Float[4]
		q[0]=w
		q[1]=x
		q[2]=y
		q[3]=z
		
		Local xx#=q[1]*q[1]
		Local yy#=q[2]*q[2]
		Local zz#=q[3]*q[3]
		Local xy#=q[1]*q[2]
		Local xz#=q[1]*q[3]
		Local yz#=q[2]*q[3]
		Local wx#=q[0]*q[1]
		Local wy#=q[0]*q[2]
		Local wz#=q[0]*q[3]
	
		Local mat:TMatrix=New TMatrix
		
		mat.grid[0,0]=1-2*(yy+zz)
		mat.grid[0,1]=  2*(xy-wz)
		mat.grid[0,2]=  2*(xz+wy)
		mat.grid[1,0]=  2*(xy+wz)
		mat.grid[1,1]=1-2*(xx+zz)
		mat.grid[1,2]=  2*(yz-wx)
		mat.grid[2,0]=  2*(xz-wy)
		mat.grid[2,1]=  2*(yz+wx)
		mat.grid[2,2]=1-2*(xx+yy)
		mat.grid[3,3]=1
	
		For Local iy=0 To 3
			For Local ix=0 To 3
				xx#=mat.grid[ix,iy]
				If xx#&lt;0.0001 And xx#&gt;-0.0001 Then xx#=0
				mat.grid[ix,iy]=xx#
			Next
		Next
	
		pitch#=ATan2( mat.grid[2,1],Sqr( mat.grid[2,0]*mat.grid[2,0]+mat.grid[2,2]*mat.grid[2,2] ) )
		yaw#=ATan2(mat.grid[2,0],mat.grid[2,2])
		roll#=ATan2(mat.grid[0,1],mat.grid[1,1])
				
		'If pitch#=nan# Then pitch#=0
		'If yaw#  =nan# Then yaw#  =0
		'If roll# =nan# Then roll# =0
	
	End Function
			
	Function Slerp(Ax#,Ay#,Az#,Aw#,Bx#,By#,Bz#,Bw#, cx# Var, cy# Var, cz# Var, cw# Var, t#)
		
		If Abs(ax-bx)&lt;0.001 And Abs(ay-by)&lt;0.001 And Abs(az-bz)&lt;0.001 And Abs(aw-bw)&lt;0.001
			cx#=ax
			cy#=ay
			cz#=az
			cw#=aw
			Return 
		EndIf
		
		Local cosineom#=Ax#*Bx#+Ay#*By#+Az#*Bz#+Aw#*Bw#
		Local scaler_w#
		Local scaler_x#
		Local scaler_y#
		Local scaler_z#
		
		If cosineom# &lt;= 0.0
			cosineom#=-cosineom#
			scaler_w#=-Bw#
			scaler_x#=-Bx#
			scaler_y#=-By#
			scaler_z#=-Bz#
		Else
			scaler_w#=Bw#
			scaler_x#=Bx#
			scaler_y#=By#
			scaler_z#=Bz#
		EndIf
		
		Local scale0#
		Local scale1#
		
		If (1.0 - cosineom#)&gt;0.0001
			Local omega#=ACos(cosineom#)
			Local sineom#=Sin(omega#)
			scale0#=Sin((1.0-t#)*omega#)/sineom#
			scale1#=Sin(t#*omega#)/sineom#
		Else
			scale0#=1.0-t#
			scale1#=t#
		EndIf
			
		cw#=scale0#*Aw#+scale1#*scaler_w#
		cx#=scale0#*Ax#+scale1#*scaler_x#
		cy#=scale0#*Ay#+scale1#*scaler_y#
		cz#=scale0#*Az#+scale1#*scaler_z#
		
		Return
	End Function
	
	Method MatrixToQuat:TQuaternion(mat:TMatrix)
		'' only good for orthogonal
		Local wt:Float, w4:Float
		
		
		
		w4 = mat.grid[0,0] + mat.grid[1,1] + mat.grid[2,2]

		If (w4 &gt; 0)  
		  w4 = 1.0/(Sqr(w4+1.0) * 2) '' S=4*qw 
		  w = 0.25 / w4
		  x = (mat.grid[2,1] - mat.grid[1,2]) *w4
		  y = (mat.grid[0,2] - mat.grid[2,0]) *w4 
		  z = (mat.grid[1,0] - mat.grid[0,1]) *w4 
		ElseIf ((mat.grid[0,0] &gt; mat.grid[1,1]) And (mat.grid[0,0] &gt; mat.grid[2,2]))
		  w4 = 1.0/(Sqr(1.0 + mat.grid[0,0] - mat.grid[1,1] - mat.grid[2,2]) * 2) '' S=4*qx 
		  w = (mat.grid[2,1] - mat.grid[1,2]) *w4
		  x = 0.25 / w4
		  y = (mat.grid[0,1] + mat.grid[1,0]) *w4
		  z = (mat.grid[0,2] + mat.grid[2,0]) *w4
		ElseIf (mat.grid[1,1] &gt; mat.grid[2,2])
		  w4 = 1.0/(Sqr(1.0 + mat.grid[1,1] - mat.grid[0,0] - mat.grid[2,2]) * 2) '' S=4*qy
		  w = (mat.grid[0,2] - mat.grid[2,0]) *w4
		  x = (mat.grid[0,1] + mat.grid[1,0]) *w4 
		  y = 0.25 / w4
		  z = (mat.grid[1,2] + mat.grid[2,1]) *w4 
		Else 
		  w4 = 1.0/(Sqr(1.0 + mat.grid[2,2] - mat.grid[0,0] - mat.grid[1,1]) * 2) '' S=4*qz
		  w = (mat.grid[1,0] - mat.grid[0,1]) *w4
		  x = (mat.grid[0,2] + mat.grid[2,0]) *w4
		  y = (mat.grid[1,2] + mat.grid[2,1]) *w4
		  z = 0.25 / w4
		EndIf
		
		Return Self
	EndMethod
	
	
	Function QuatToMatrix(x#,y#,z#,w#,mat:TMatrix)
		Local x2#, xx2#, xy2#, xz2#, xw2#, y2#, yy2#, yz2#, yw2#, z2#, zz2#, zw2#
		
		x2  = x+ x
		xx2 = x2   * x
		xy2 = x2   * y
		xz2 = x2   * z
		xw2 = x2   * w
	
		y2  = y+ y
		yy2 = y2   * y
		yz2 = y2   * z
		yw2 = y2   * w
		
		z2  = z+ z
		zz2 = z2   * z
		zw2 = z2   * w
		
		mat.grid[0,0] = 1.0 - yy2 - zz2
		mat.grid[0,1] = xy2  - zw2
		mat.grid[0,2] = xz2  + yw2
	
		mat.grid[1,0] = xy2  + zw2
		mat.grid[1,1] = 1.0 - xx2 - zz2
		mat.grid[1,2] = yz2  - xw2
	
		mat.grid[2,0] = xz2  - yw2
		mat.grid[2,1] = yz2  + xw2
		mat.grid[2,2] = 1.0 - xx2 - yy2
	
		'matrix.f14 = matrix.f24 = matrix.f34 = matrix.f14 = matrix.f42 = matrix.f43 = 0.0
		mat.grid[3,0] = 0.0; mat.grid[3,1] = 0.0; mat.grid[3,2] = 0.0
		mat.grid[0,3] = 0.0; mat.grid[1,3] = 0.0; mat.grid[2,3] = 0.0
		mat.grid[3,3] = 1.0
		
		
	
	EndFunction

	Method Normalize()

		Local lengthSq:Float = x * x + y * y + z * z + w * w
	
		If (lengthSq = 0.0 ) Return
		If (lengthSq &lt;&gt; 1.0 )
			Local scale:Float = ( 1.0 / Sqr( lengthSq ) )
			x :* scale
			y :* scale
			z :* scale
			w :* scale
		EndIf
		
	EndMethod

	Method Update(xx:Float, yy:Float, zz:Float, ww:Float)
		
		x=xx; y=xx; z=zz; w=ww
		
	EndMethod
	
End Type
</textarea><br><br>PrintMatrix() to help with unit testing:<br><pre class=code>
Function PrintMatrix(mat:TMatrix)
	
	Print mat.grid[0,0]+":"+mat.grid[1,0]+":"+mat.grid[2,0]+":"+mat.grid[3,0]
	Print mat.grid[0,1]+":"+mat.grid[1,1]+":"+mat.grid[2,1]+":"+mat.grid[3,1]
	Print mat.grid[0,2]+":"+mat.grid[1,2]+":"+mat.grid[2,2]+":"+mat.grid[3,2]	
	Print mat.grid[0,3]+":"+mat.grid[1,3]+":"+mat.grid[2,3]+":"+mat.grid[3,3]
	
EndFunction
</pre><br><br><font class="tiny">Last edited 2012</font><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1132717"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jkrankie</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Warner's version does rotations properly and has a working aligntovector function, perhaps you could look at his source code?<br><br>Cheers<br>Charlie <br><br></td></tr></table><br>
<a name="1132718"></a>

<a name="1132719"></a>

<a name="1132722"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jkrankie</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's a link to that version of minib3d: <a href="http://www.hi-toro.com/blitz/sidesign.zip" target="_blank">http://www.hi-toro.com/blitz/sidesign.zip</a><br><br>Cheers<br>Charlie<br><br><font class="tiny">Last edited 2012</font><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1132732"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamRedwoods</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for that, I took quite a bit from his code already, but unfortunately his routines have inaccuracies. This check:<br><pre class=code>
PrintMatrix(ent.mat)
TMatrix.ToQuat(quat.w,quat.x,quat.y,quat.z)
Print ""
TQuaternion.QuatToMat(quat.x,quat.y,quat.z,quat.w,ent.mat)
PrintMatrix(ent.mat)
</pre><br>does not give accurate (within float pt accuracy) results due to the discrepancies of column-major vs row-major matricies. <br><br></td></tr></table><br>
<a name="1132751"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I can't get your example running here so I can't see the problem. However, in the code archives is a small version of <a href="/codearcs.php?code=2782" target="_blank">AligntoVector from Warner</a>, I tried to combine it with your example on the fly (but it has no axis/speed):<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict

Import sidesign.minib3d

Graphics3D 800, 600, 0, 2

CreateLight()

'camera
Local cam:TCamera = CreateCamera()
MoveEntity cam, 0, 5, -15

'cone 
Local cube:TMesh = CreateCone()
RotateMesh cube, 90, 0, 0
PositionMesh cube, 0, 0, 1
Local surf:TSurface = GetSurface(cube, 1)
VertexColor surf, 0, 255, 0, 0
VertexColor surf, 1, 255, 0, 0
VertexColor surf, 2, 255, 0, 0
EntityFX cube, 2

'sphere
Local sph:TMesh = CreateSphere()
ScaleMesh sph, 5, 5, 5
EntityColor sph, 0, 255, 0
EntityPickMode sph, 2
surf = GetSurface(sph, 1)
For Local i% = 0 To CountVertices(surf) - 1
	VertexColor surf, i, 0, Rand(128, 255), 0
Next
EntityFX sph, 2

PointEntity cam, cube

Repeat

	'turn sphere using keys
	TurnEntity sph, 0, KeyDown(39)-KeyDown(37), KeyDown(40)-KeyDown(38)
	
	'click on sphere to place cone
	If MouseDown(1)
		CameraPick cam, MouseX(), MouseY()
		PositionEntity cube, PickedX(), PickedY(), PickedZ()
		
		AlignToVector cube, PickedNX(), PickedNY(), PickedNZ()
	End If
		
	RenderWorld
			
	Flip

'esc=exit
Until KeyHit(27)

End

'--------------------------------------------------------------------------------------------------------------------------
'											AlignToVector
'--------------------------------------------------------------------------------------------------------------------------
Function AlignToVector(e:TEntity, x#, y#, z#)

	'order=yaw-pitch-roll
	
	Local yaw#,pitch#,roll#
	Local x1#,y1#,z1#
	Local x2#,y2#,z2#
	Local x3#,y3#,z3#
	
	yaw# = -ATan2(x, z)
	
	x1# = z*Sin(yaw) + x*Cos(yaw)
	y1# = y	
	z1# = z*Cos(yaw) - x*Sin(yaw)
	
	pitch# = -ATan2(y1, z1)
	x2# = x1
	y2# = y1*Cos(pitch) - z1*Sin(pitch)
	z2# = y1*Sin(pitch) + z1*Cos(pitch)
	
	roll# = -ATan2(x2, y2)
	x3# = x2*Cos(roll) - y2*Sin(roll)
	y3# = x2*Sin(roll) + y2*Cos(roll)
	z3# = z2

	'FIX - might turn out it should be If y &lt;= 0 .. haven't tested it thouroughly enough
	If y &lt; 0 roll :+ 180
	
	RotateEntity e, pitch, yaw, roll
	
End Function</textarea> <br><br></td></tr></table><br>
<a name="1132838"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jkrankie</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've been playing around with writing my own rendering thing lately, and i've been using Warner's code as a reference, but i've not been using his/simon's (don't know who wrote the matrix/quat code for mini b3d, maybe both???) matrix etc. commands. I've been referring to this: <a href="https://github.com/toji/gl-matrix/blob/master/gl-matrix.js" target="_blank">https://github.com/toji/gl-matrix/blob/master/gl-matrix.js</a> instead. <br><br>Cheers<br>Charlie <br><br></td></tr></table><br>
<a name="1132888"></a>

<a name="1132889"></a>

<a name="1132890"></a>

<a name="1132904"></a>

<a name="1132905"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamRedwoods</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> well thanks for the help, but i think i'm going to call this one quits.<br><br>I went through with a bunch of unit tests (see code below) and piece by piece created the correct equations for TQuaternion&lt;-&gt;TMatrix. It works with the tests now and also keeps backward compatibility with B3D animation quaternions.<br><br>The example below works for axis=2 (Y axis) but not as well with the other two.<br>All the axises calculations break down when the cube is close to the pivot. I can't make heads or tails of it....<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import "../minib3d.bmx"

Strict

Global testx#, testy#, testz#
Local width=640,height=480,depth=16,mode=2

Graphics3D width,height,depth,mode,30

Local r1#,r2#, r3#

Local cam:TCamera=CreateCamera()
PositionEntity cam,0,5,-15

Local light:TLight=CreateLight()
Local dot:TMesh = CreateSphere()
dot.ScaleEntity(0.4,0.4,0.4)
dot.EntityColor(255,0,0)

Local ent:TMesh=LoadAnimMesh("media/zombie.b3d")

''tests
ent.RotateEntity(0,0,90)
'Print ent.rx+" "+ent.ry+" "+ent.rz

PrintMatrix(ent.mat)
Print ""
Local quat:TQuaternion = New TQuaternion
Local mat3:TMatrix = New TMatrix
quat.MatrixToQuat(ent.mat)
Print "q= "+quat.x+" "+quat.y+" "+quat.z+" "+quat.w
quat.QuatToEuler(quat.w,quat.x,quat.y,quat.z, r1,r2,r3)
Print "e= "+r1+" "+r2+" "+r3

quat.QuatToMat(quat.w,quat.x,quat.y,quat.z,mat3)
Print ""
PrintMatrix(mat3)
Print ""

ent.RotateEntity(0,90,0)
'Print ent.rx+" "+ent.ry+" "+ent.rz

PrintMatrix(ent.mat)
Print ""


quat.MatrixToQuat(ent.mat)
Print "q= "+quat.x+" "+quat.y+" "+quat.z+" "+quat.w
quat.QuatToEuler(quat.w,quat.x,quat.y,quat.z, r1,r2,r3)
Print "e= "+r1+" "+r2+" "+r3

quat.QuatToMat(quat.w,quat.x,quat.y,quat.z,mat3)
Print ""
PrintMatrix(mat3)
Print ""

ent.RotateEntity(-90,0,0)
'Print ent.rx+" "+ent.ry+" "+ent.rz

PrintMatrix(ent.mat)
Print ""

quat.MatrixToQuat(ent.mat)
Print "q= "+quat.x+" "+quat.y+" "+quat.z+" "+quat.w
quat.QuatToEuler(quat.w,quat.x,quat.y,quat.z, r1,r2,r3)
Print "e= "+r1+" "+r2+" "+r3

quat.QuatToMat(quat.w,quat.x,quat.y,quat.z,mat3)
Print ""
PrintMatrix(mat3)
Print ""

ent.RotateEntity(90,0,0)
'Print ent.rx+" "+ent.ry+" "+ent.rz

PrintMatrix(ent.mat)
Print ""

quat.MatrixToQuat(ent.mat)
Print "q= "+quat.x+" "+quat.y+" "+quat.z+" "+quat.w
quat.QuatToEuler(quat.w,quat.x,quat.y,quat.z, r1,r2,r3)
Print "e= "+r1+" "+r2+" "+r3

quat.QuatToMat(quat.w,quat.x,quat.y,quat.z,mat3)
Print ""
PrintMatrix(mat3)
Print ""
''

'ent.ScaleEntity(0.5,0.5,0.5)

Local cube:TMesh = CreateCube()
cube.PositionEntity(2,3,0)
cube.ScaleEntity(0.3,0.3,0.3)

Local anim_time#=15, axis%=2

' used by fps code
Local old_ms=MilliSecs()
Local renders=0
Local fps=0
Local keypause=0

While Not KeyDown(KEY_ESCAPE)		

	If KeyHit(KEY_ENTER) Then DebugStop

	' control camera
	MoveEntity cube,KeyDown(KEY_D)-KeyDown(KEY_A),0,KeyDown(KEY_W)-KeyDown(KEY_S)
	TurnEntity cam,KeyDown(KEY_DOWN)-KeyDown(KEY_UP),KeyDown(KEY_LEFT)-KeyDown(KEY_RIGHT),0
	
	If KeyDown(KEY_SPACE) And (MilliSecs()-keypause &gt; 100)
		cube.PositionEntity( cube.EntityX(), cube.EntityY()*-1, cube.EntityZ() )
		keypause = MilliSecs()
	EndIf
	
	If KeyDown(KEY_1) Then axis=1
	If KeyDown(KEY_2) Then axis=2
	If KeyDown(KEY_3) Then axis=3
	
	If KeyDown(KEY_MINUS) Then anim_time#=anim_time#-1.0
	If KeyDown(KEY_EQUALS) Then anim_time#=anim_time#+1.0
	
	SetAnimTime(ent,anim_time#)
	

	AlignToVector(ent, cube.EntityX(1) - ent.EntityX(1), cube.EntityY(1)-ent.EntityY(1), cube.EntityZ(1) - ent.EntityZ(1) , axis, 0.4)


dot.PositionEntity(testx,testy,testz)
	
	RenderWorld
	renders=renders+1
	
	' calculate fps
	If MilliSecs()-old_ms&gt;=1000
		old_ms=MilliSecs()
		fps=renders
		renders=0
	EndIf
	
	Text 0,0,"FPS: "+fps

	Flip
	
Wend
End



Function AlignToVector(ent:TEntity, vx:Float,vy:Float,vz:Float, axi:Int=1, rate:Float=1.0)
		
		Local dvec:TVector = TVector.Create(vx,vy,vz)
		
		Local dd# = dvec.Length()
		If dd &lt; 0.0001 Then Return
		dd=1.0/dd
		dvec = TVector.Create(vx * dd, vy * dd, vz * dd)
		
		'get axis to get our angle from (b/c rotations start at axis)	
		Local ovec:TVector
		If (axi=1) Then ovec = TVector.Create(1.0,0.0,0.0)
		If (axi=2) Then ovec = TVector.Create(0.0,1.0,0.0)
		If (axi=3) Then ovec = TVector.Create(0.0,0.0,1.0)

		
		''use axis-angle quat for slerp and convert to matrix,euler
		Local angle:Float = ACos(ovec.Dot(dvec))
		Local axis:TVector = ovec.Cross(dvec)
		'axis.Normalize()
		
		Local quat:TQuaternion = New TQuaternion
		Local cs:Float = Cos(angle*0.5)
		Local invs:Float = Sin(angle*0.5) 
		
		quat.x = axis.x * invs
		quat.y = axis.y * invs
		quat.z = axis.z * invs
		quat.w = cs 
		
		
		If angle &gt; 179.9999
			quat.x = ovec.y '0.99999
			quat.y = ovec.x '0.00000
			quat.z = 0.00000
			quat.w = 0.0
		Else
			'quat.Normalize()
		EndIf
		
		
		
		''axis angle to quat to matrix
		Local new_mat:TMatrix = New TMatrix

		Local quato:TQuaternion = New TQuaternion

		ent.mat.Scale(1.0/ent.sx,1.0/ent.sy,1.0/ent.sz) '' hmmm...
		
		quato.MatrixToQuat( ent.mat )
		quato.Normalize()
	
		Local dotq:Float = (quato.x*quat.x + quato.y*quat.y + quato.z*quat.z + quato.w*quat.w)
		Print dotq
		If dotq &lt; 0
			quat.x = -quat.x
			quat.y = -quat.y
			quat.z = -quat.z
			quat.w = -quat.w
		EndIf

		quat.Slerp(quato.x,quato.y,quato.z,quato.w,  quat.x,quat.y,quat.z,quat.w, quat.x,quat.y,quat.z,quat.w, rate)
		quat.QuatToMat(quat.w,quat.x,quat.y,quat.z, new_mat)

'PrintMatrix new_mat
		
		new_mat.grid[3,0] = ent.px
		new_mat.grid[3,1] = ent.py
		new_mat.grid[3,2] = ent.pz
		
		If ent.parent&lt;&gt;Null
			ent.mat.Overwrite(ent.parent.mat)
			new_mat.Multiply(ent.mat) ''double check???????
		EndIf
		
		new_mat.Scale(ent.sx,ent.sy,ent.sz)
		ent.mat.Overwrite(new_mat)
		
		ent.rx = ent.mat.GetPitch()
		ent.ry = ent.mat.GetYaw()
		ent.rz = ent.mat.GetRoll()
	
		ent.UpdateChildren(ent)
		
EndFunction


Function PrintMatrix(mat:TMatrix)
	
	Print mat.grid[0,0]+":"+mat.grid[1,0]+":"+mat.grid[2,0]+":"+mat.grid[3,0]
	Print mat.grid[0,1]+":"+mat.grid[1,1]+":"+mat.grid[2,1]+":"+mat.grid[3,1]
	Print mat.grid[0,2]+":"+mat.grid[1,2]+":"+mat.grid[2,2]+":"+mat.grid[3,2]	
	Print mat.grid[0,3]+":"+mat.grid[1,3]+":"+mat.grid[2,3]+":"+mat.grid[3,3]
	
EndFunction
</textarea><br><br>TQuaternion (tested with unit tests above):<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Type TQuaternion

	Field w#,x#,y#,z#

	Method New()
	
		If LOG_NEW
			DebugLog "New TQuaternion"
		EndIf
	
	End Method
	
	Method Delete()
	
		If LOG_DEL
			DebugLog "Del TQuaternion"
		EndIf
	
	End Method

	Rem
	Function QuatToMat2(w#,x#,y#,z#,mat:TMatrix Var)
	
		Local q:Float[4]
		q[0]=w
		q[1]=x
		q[2]=y
		q[3]=z
		
		Local xx#=q[1]*q[1]
		Local yy#=q[2]*q[2]
		Local zz#=q[3]*q[3]
		Local xy#=q[1]*q[2]
		Local xz#=q[1]*q[3]
		Local yz#=q[2]*q[3]
		Local wx#=q[0]*q[1]
		Local wy#=q[0]*q[2]
		Local wz#=q[0]*q[3]
	
		mat.grid[0,0]=1-2*(yy+zz)
		mat.grid[0,1]=  2*(xy-wz)
		mat.grid[0,2]=  2*(xz+wy)
		mat.grid[1,0]=  2*(xy+wz)
		mat.grid[1,1]=1-2*(xx+zz)
		mat.grid[1,2]=  2*(yz-wx)
		mat.grid[2,0]=  2*(xz-wy)
		mat.grid[2,1]=  2*(yz+wx)
		mat.grid[2,2]=1-2*(xx+yy)
		mat.grid[3,3]=1
	
		For Local iy=0 To 3
			For Local ix=0 To 3
				xx#=mat.grid[ix,iy]
				If xx#&lt;0.0001 And xx#&gt;-0.0001 Then xx#=0
				mat.grid[ix,iy]=xx#
			Next
		Next
	
	End Function
	EndRem
	
	Function QuatToEuler(w#,x#,y#,z#,pitch# Var,yaw# Var,roll# Var)
	
		Local q:Float[4]
		q[0]=w
		q[1]=x
		q[2]=y
		q[3]=z
		
		Local xx#=q[1]*q[1]
		Local yy#=q[2]*q[2]
		Local zz#=q[3]*q[3]
		Local xy#=q[1]*q[2]
		Local xz#=q[1]*q[3]
		Local yz#=q[2]*q[3]
		Local wx#=q[0]*q[1]
		Local wy#=q[0]*q[2]
		Local wz#=q[0]*q[3]
	
		Local mat:TMatrix=New TMatrix
		
		'mat.grid[0,0]=1-2*(yy+zz)
		mat.grid[0,1]=  -2*(xy-wz)
		'mat.grid[0,2]=  -2*(xz+wy)
		'mat.grid[1,0]=  2*(xy+wz)
		mat.grid[1,1]=-(1-2*(xx+zz))
		mat.grid[1,2]=  2*(yz-wx)
		mat.grid[2,0]=  -2*(xz-wy)
		mat.grid[2,1]=  2*(yz+wx)
		mat.grid[2,2]=1-2*(xx+yy)
		'mat.grid[3,3]=1
	
		'For Local iy=0 To 3
			'For Local ix=0 To 3
				'xx#=mat.grid[ix,iy]
				'If xx#&lt;0.0001 And xx#&gt;-0.0001 Then xx#=0
				'mat.grid[ix,iy]=xx#
			'Next
		'Next
	
		pitch#=ATan2( mat.grid[2,1],Sqr( mat.grid[2,0]*mat.grid[2,0]+mat.grid[2,2]*mat.grid[2,2] ) )
		yaw#=ATan2(mat.grid[2,0],mat.grid[2,2])
		roll#=ATan2(mat.grid[0,1],mat.grid[1,1])
				
		'If pitch#=nan# Then pitch#=0
		'If yaw#  =nan# Then yaw#  =0
		'If roll# =nan# Then roll# =0
	
	End Function
			
	Function Slerp(Ax#,Ay#,Az#,Aw#,Bx#,By#,Bz#,Bw#, cx# Var, cy# Var, cz# Var, cw# Var, t#)
		
		If Abs(ax-bx)&lt;0.001 And Abs(ay-by)&lt;0.001 And Abs(az-bz)&lt;0.001 And Abs(aw-bw)&lt;0.001
			cx#=ax
			cy#=ay
			cz#=az
			cw#=aw
			Return 
		EndIf
		
		Local cosineom#=Ax#*Bx#+Ay#*By#+Az#*Bz#+Aw#*Bw#
		Local scaler_w#
		Local scaler_x#
		Local scaler_y#
		Local scaler_z#
		
		If cosineom# &lt;= 0.0
			cosineom#=-cosineom#
			scaler_w#=-Bw#
			scaler_x#=-Bx#
			scaler_y#=-By#
			scaler_z#=-Bz#
		Else
			scaler_w#=Bw#
			scaler_x#=Bx#
			scaler_y#=By#
			scaler_z#=Bz#
		EndIf
		
		Local scale0#
		Local scale1#
		
		If (1.0 - cosineom#)&gt;0.0001
			Local omega#=ACos(cosineom#)
			Local sineom#=Sin(omega#)
			scale0#=Sin((1.0-t#)*omega#)/sineom#
			scale1#=Sin(t#*omega#)/sineom#
		Else
			scale0#=1.0-t#
			scale1#=t#
		EndIf
			
		cw#=scale0#*Aw#+scale1#*scaler_w#
		cx#=scale0#*Ax#+scale1#*scaler_x#
		cy#=scale0#*Ay#+scale1#*scaler_y#
		cz#=scale0#*Az#+scale1#*scaler_z#
		
		Return
	End Function
	
	Method MatrixToQuat:TQuaternion(mat:TMatrix)
		'' only good for orthogonal
		Local wt:Float, w4:Float
		
			
		w4 = mat.grid[0,0] + mat.grid[1,1] + mat.grid[2,2] +1.0
				
		If (w4 &gt; 0.0001)  
		  w4 = 0.5/(Sqr(w4) ) '' S=4*qw 
		  w = 0.25 / w4
		  x = (mat.grid[2,1] - mat.grid[1,2]) *w4
		  y = -(mat.grid[0,2] - mat.grid[2,0]) *w4 
		  z = -(mat.grid[1,0] - mat.grid[0,1]) *w4 
		Else
			If ((mat.grid[0,0] &gt; mat.grid[1,1]) And (mat.grid[0,0] &gt; mat.grid[2,2]))
			  w4 = 1.0/(Sqr(1.0 + mat.grid[0,0] - mat.grid[1,1] - mat.grid[2,2]) * 2.0) '' S=4*qx 
			  w = (mat.grid[2,1] - mat.grid[1,2]) *w4
			  x = 0.25 / w4
			  y = -(mat.grid[0,1] + mat.grid[1,0]) *w4
			  z = -(mat.grid[0,2] + mat.grid[2,0]) *w4

			ElseIf (mat.grid[1,1] &gt; mat.grid[2,2])
			  w4 = 1.0/(Sqr(1.0 + mat.grid[1,1] - mat.grid[0,0] - mat.grid[2,2]) * 2.0) '' S=4*qy
			  w = (mat.grid[0,2] - mat.grid[2,0]) *w4
			  x = (mat.grid[0,1] + mat.grid[1,0]) *w4 
			  y = -0.25 / w4
			  z = -(mat.grid[1,2] + mat.grid[2,1]) *w4 

			Else 
			  w4 = 1.0/(Sqr(1.0 + mat.grid[2,2] - mat.grid[0,0] - mat.grid[1,1]) * 2.0) '' S=4*qz
			  w = (mat.grid[1,0] - mat.grid[0,1]) *w4
			  x = (mat.grid[0,2] + mat.grid[2,0]) *w4
			  y = -(mat.grid[1,2] + mat.grid[2,1]) *w4
			  z = -0.25 / w4

			EndIf
		EndIf
		
		Return Self
	EndMethod
	
	
	Function QuatToMat( w#,x#,y#,z#,mat:TMatrix Var)
		Local x2#, xx2#, xy2#, xz2#, xw2#, y2#, yy2#, yz2#, yw2#, z2#, zz2#, zw2#
		
		x2  = x+ x
		xx2 = x2   * x
		xy2 = x2   * y
		xz2 = x2   * z
		xw2 = x2   * w
	
		y2  = y+ y
		yy2 = y2   * y
		yz2 = y2   * z
		yw2 = y2   * w
		
		z2  = z+ z
		zz2 = z2   * z
		zw2 = z2   * w
		 
		mat.grid[0,0] =  1.0 - yy2 - zz2  
		mat.grid[0,1] =  -(xy2  - zw2)
		mat.grid[0,2] =  -(xz2  + yw2)
	
		mat.grid[1,0] =  -(xy2  + zw2)
		mat.grid[1,1] =  1.0 - xx2 - zz2
		mat.grid[1,2] =  yz2  - xw2
	
		mat.grid[2,0] =  -(xz2 - yw2)
		mat.grid[2,1] =  yz2  + xw2
		mat.grid[2,2] =  1.0 - xx2 - yy2
	
	
		'matrix.f14 = matrix.f24 = matrix.f34 = matrix.f14 = matrix.f42 = matrix.f43 = 0.0
		mat.grid[3,0] = 0.0; mat.grid[3,1] = 0.0; mat.grid[3,2] = 0.0
		mat.grid[0,3] = 0.0; mat.grid[1,3] = 0.0; mat.grid[2,3] = 0.0
		mat.grid[3,3] = 1.0
		
		
	
	EndFunction

	Method Normalize()

		Local lengthSq:Float = x * x + y * y + z * z + w * w
	
		If (lengthSq = 0.0 ) Return
		If (lengthSq &lt;&gt; 1.0 )
			Local scale:Float = ( 1.0 / Sqr( lengthSq ) )
			x :* scale
			y :* scale
			z :* scale
			w :* scale
		EndIf
		
	EndMethod

	Method Update(xx:Float, yy:Float, zz:Float, ww:Float)
		
		x=xx; y=xx; z=zz; w=ww
		
	End Method
	
	Method RotateVector:TVector(vec:TVector)

		Local ax#,ay#,az#,aw#, vec2:TVector
	
		ax = (-(x * vec.x) - (y * vec.y) - (z * vec.z))
		ay = ( (w * vec.x) + (y * vec.z) - (z * vec.y))
		az = ( (w * vec.y) - (x * vec.z) + (z * vec.x))
		aw = ( (w * vec.z) + (x * vec.y) - (y * vec.x))
	
		vec2.x = ((az * vec.y) - (aw * vec.x) - (ay * vec.z))
		vec2.y = ((ax * vec.z) - (aw * vec.y) - (az * vec.x))
		vec2.z = ((ay * vec.x) - (aw * vec.z) - (ax * vec.y))
		
		Return vec2
		
	End Method
	
	Method Multiply:TQuaternion(q:TQuaternion)
		Local r:TQuaternion = New TQuaternion
		
		r.w = (w*q.w - x*q.x - y*q.y - z*q.z)
		r.x = (w*q.x + x*q.w + y*q.z - z*q.y)
		r.y = (w*q.y - x*q.z + y*q.w + z*q.x)
		r.z = (w*q.z + x*q.y - y*q.x + z*q.w)
		
		Return r
	End Method
	
	Method AxisAngleToQuat:TQuaternion(ax:Float,ay:Float,az:Float,an:Float)
		'' only good for orthogonal
		Local s:Float

		s = Sin(an*0.5)
		x = ax * s
		y = ay * s
		z = az * s
		w = Cos(an*0.5)
		
		Return Self
		
	End Method
	
End Type

</textarea><br><br>I will probably just use werner's euler AlignToVector and implement a nlerp (or slerp) for it since the calculations seem less and I won't have as many conversions going on.<br><br>jkrankie, that library looks great, the matrix is a sequential array and the code is quite complete. The only reason I hesitate using code is that sometimes the matrix orders are different or the signs are different in the quaternion code, so it'd be once again a bunch of unit testing to figure out if things are working correctly.<br><br>If i have time in the future I may revisit this. thanks again.<br><br><font class="tiny">Last edited 2012</font><br><br>P.S. i'm pretty sure the problem is that the rotations are stored in the matrix and not in quats, so I wonder if information is lost in the conversion. I would try normalizing the matrix to prevent any calculation drift.<br><br><font class="tiny">Last edited 2012</font><br>P.S.S code updated with negative angel check, see below.<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1132903"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamRedwoods</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> i take that back. putting this in:<br><pre class=code>Local dotq:Float = (quato.x*quat.x + quato.y*quat.y + quato.z*quat.z + quato.w*quat.w)
		Print dotq
		If dotq &lt; 0
			quat.x = -quat.x
			quat.y = -quat.y
			quat.z = -quat.z
			quat.w = -quat.w
		EndIf</pre><br>makes the calcs more stable.<br><br>and after playing with the euler-align-to-vector, it's not bad.<br>just need to fix the other axises. <br><br></td></tr></table><br>
<a name="1133347"></a>

<a name="1133348"></a>

<a name="1133349"></a>

<a name="1133361"></a>

<a name="1133362"></a>

<a name="1133363"></a>

<a name="1133535"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamRedwoods</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> GOT IT!<br>Now works beautifully, but I decided to go with axis-angle instead of quaternions.<br>So the code does not require TQuaternion anymore, just TVector.<br><br>Yes, it does flip as angles near 180, but my main concern was for planes only. So just set one of the axis to 0 and it will restrain to plane perfectly. (axis = 3 and y axis =0 is for xz plane). If you need all axises, you'll need an extra pivot as a parent and set your object and pivot to their respective planes (ie. xz and yz planes).<br><br>The rate actually goes from 0.0-10.0 or higher, so to get instant change, just set it to a negative rate.<br><br><pre class=code>
Function AlignToVector (ent:TEntity, vx:Float,vy:Float,vz:Float, axi:Int=1, rate:Float=1.0)
		
		Local dvec:TVector = TVector.Create(vx,vy,-vz)
		Local ovec:TVector = New TVector
		Local avec:TVector = New TVector
		Local p1#, p2#, p3#
		
		Local dd# = dvec.Length()
		If dd &lt; 0.0001 Then Return
		dd=1.0/dd
		dvec.Update(dvec.x*dd, dvec.y*dd, dvec.z*dd )
		'dvec = dvec.Normalize()
		
		
		''slerp or lerp between the dvec and the current matrix forward, up, or left axis
		Local cvec:TVector
		
		If (axi=1) Then cvec = TVector.Create(ent.mat.grid[0,0],ent.mat.grid[0,1],ent.mat.grid[0,2])
		If (axi=2) Then cvec = TVector.Create(ent.mat.grid[1,0],ent.mat.grid[1,1],ent.mat.grid[1,2])
		If (axi=3) Then cvec = TVector.Create(ent.mat.grid[2,0],ent.mat.grid[2,1],ent.mat.grid[2,2])
		
		cvec = cvec.Normalize() ''if entity is scaled
		
		''lerp is inaccurate, but only on large distances
		If rate&gt;=0.0
			dvec = TVector.Create( (cvec.x+dvec.x)*rate+cvec.x, (cvec.y+dvec.y)*rate+cvec.y, (cvec.z+dvec.z)*rate+cvec.z )
			dvec.Normalize()
		EndIf
			
			
		'get axis to get our angle from (b/c rotations start at axis)	

		If (axi=1) Then avec = TVector.Create(1.0,0.0,0.0)
		If (axi=2) Then avec = TVector.Create(0.0,1.0,0.0)
		If (axi=3) Then avec = TVector.Create(0.0,0.0,1.0)
		
		Local new_mat:TMatrix = ent.mat.Copy()'.Inverse() ''do the inverse just below
		
		''use axis-angle quat for slerp and convert to matrix,euler
		Local angle:Float = ACos( dvec.Dot(avec) )
		Local axis:TVector = dvec.Cross(avec)

		'Print angle
		If angle &lt; 0.00001
			ent.mat.LoadIdentity()
			ent.mat.Scale(ent.sx,ent.sy,ent.sz)
			Return
		ElseIf angle &gt; 179.9999
			''flip
			'ent.mat.LoadIdentity()
			axis = TVector.Create(0.0,-1.0,0.0)
			angle=179.9
		EndIf

		axis.Normalize()
		
		Local c:Float = Cos(angle)
		Local s:Float = Sin(angle)
		Local t:Float = 1.0 - c
		''  axis is normalised
		
		new_mat.grid[0,0] = (c + axis.x*axis.x*t) *ent.sx
		new_mat.grid[1,1] = (c + axis.y*axis.y*t) *ent.sy
		new_mat.grid[2,2] = (c + axis.z*axis.z*t) *ent.sz
				
		Local tmp1:Float = axis.x*axis.y*t
		Local tmp2:Float = axis.z*s
		new_mat.grid[1,0] = (tmp1 + tmp2) *ent.sy
		new_mat.grid[0,1] = (tmp1 - tmp2) *ent.sx
		
		tmp1 = axis.x*axis.z*t
		tmp2 = axis.y*s
		new_mat.grid[2,0] = (tmp1 - tmp2) *ent.sz
		new_mat.grid[0,2] = (tmp1 + tmp2) *ent.sx
		
		tmp1 = axis.y*axis.z*t
		tmp2 = axis.x*s
		new_mat.grid[2,1] = (tmp1 + tmp2) *ent.sz
		new_mat.grid[1,2] = (tmp1 - tmp2) *ent.sy
		
		new_mat.grid[3,0] = ent.px
		new_mat.grid[3,1] = ent.py
		new_mat.grid[3,2] = ent.pz
		new_mat.grid[3,3] = 1.0
		
		If ent.parent&lt;&gt;Null
			ent.mat.Overwrite(ent.parent.mat)
			ent.mat.Multiply(new_mat)
		Else
			ent.mat.Overwrite(new_mat )
		EndIf
		
		ent.rx = ent.mat.GetPitch()
		ent.ry = ent.mat.GetYaw()
		ent.rz = ent.mat.GetRoll()
	
		ent.UpdateChildren(ent)
		
EndFunction
</pre><br><br>*updated for entity scaling<br><br><font class="tiny">Last edited 2012</font><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1133440"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ian Thompson</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Congrats! That was not an easy task! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
