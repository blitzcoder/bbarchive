<!DOCTYPE html><html lang="en" ><head ><title >Look around using mouse like in most PC FPS games</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Look around using mouse like in most PC FPS games</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=119" >MiniB3D Module</a>/<a href="#bottom" >Look around using mouse like in most PC FPS games</a><br><br>
<a name="900160"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pineapple</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Gfx provided me with some very helpful MouseXSpeed and MouseYSpeed substitutes, but I cannot figure out why the following code refuses to budge the camera when I move the mouse:<br><br><pre class=code>TurnEntity camera,-mouse.YSpeed()*0.9,mouse.XSpeed()*0.9,mouse.XSpeed()/10.000</pre><br><br><br>Could I please get some help? <br><br></td></tr></table><br>
<a name="900170"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Beaker</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just a guess, but try:<br>TurnEntity camera,-Float(mouse.YSpeed())*0.9,Float(mouse.XSpeed())*0.9,Float(mouse.XSpeed())/10.000 <br><br></td></tr></table><br>
<a name="900174"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pineapple</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> No, that's not the issue.<br><br>For some reason, when I'm calling the function for drawing text, everything goes smoothly ( text 50,50,"MouseYSpeed: "+mouse.YSpeed();text 50,60,"MouseXSpeed: "+mouse.XSpeed() ) but when I assign it to a variable or do anything other than text for that matter, it usually returns 0 but occasionaly returns *something* just to throw me off.<br><br>Main program (revised to use RotateEntity rather than TurnEntity because TurnEntity doesn't seem to support roll whereas RotateEntity does)<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Import sidesign.minib3d

Global gw=640,gh=480

Graphics3D gw,gh


Include "TMouse.bmx"
Include "TPlayer.bmx"





Global light=CreateLight()

Global player:TPlayer=New TPlayer
player.Create()
PositionEntity player.entity,0,0,0
Global camera=CreateCamera()
PositionEntity camera,0,0.46,-7
RotateEntity camera,-20,0,0
Global cpivot=CreatePivot(player.entity)
'EntityParent(camera,cpivot)



Global p=CreateCube();HideEntity p
For x=1 To 500
	x2=CopyEntity(p)
	PositionEntity x2,Rand(-800,800),Rand(-800,800),Rand(-800,800)
	ScaleEntity x2,Rnd(0.4,8),Rnd(0.4,8),Rnd(0.4,8)
	EntityColor x2,Rand(80,250),Rand(80,250),Rand(80,250)
Next




Global fpstimer=CreateTimer(60)



Repeat


	Local mys=mouse.YSpeed()
	Local mxs=mouse.XSpeed()	
	Print mys+" , "+mxs

	If KeyDown(KEY_W) player.spd:+0.9
	If KeyDown(KEY_S) player.spd:-0.9
	If KeyDown(KEY_A) player.y:-0.2
	If KeyDown(KEY_D) player.y:+0.2
	player.p:-Float(mouse.YSpeed())
	player.y:+Float(mouse.XSpeed())
	player.r:+Float(mouse.XSpeed())
	player.update()
	MoveMouse gw/2,gh/2
	If MouseHit(1)
		
	EndIf

	UpdateWorld
	RenderWorld
	

	text 50,50,"MouseYSpeed: "+mouse.YSpeed()
	text 50,60,"MouseXSpeed: "+mouse.XSpeed()

	Flip
	WaitTimer(fpstimer)
	If KeyDown(KEY_ESCAPE) End
	mouse.update()
	
Forever</textarea><br><br><br>TMouse.bmx (thanks to Gfx):<br><pre class=code>Global mouse:tMouse = New tMouse

Type tMouse
	Field lastX:Int
	Field lastY:Int
	Method XSpeed:Int()
		Return MouseX() - lastX
	End Method
	Method YSpeed:Int()
		Return MouseY() - lastY
	End Method
	Method update()
		lastx = MouseX()
		lasty = MouseY()
	End Method
End Type
</pre><br><br>And TPlayer.bmx:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Global PlayerMesh=CreateCone(4)
ScaleEntity PlayerMesh,5,2,0.6
TurnEntity PlayerMesh,90,0,0
HideEntity PlayerMesh

Global players=0

Type TPlayer
	Global list:TList=CreateList() 'in the case of more than 1 player (online play would be awesome)
	
	Global decellerate#=0.001
	
	Field spd#,maxspd#,p#,y#,r#
	
	Field entity
	
	Field hits=100
	
	Method Create(x#=0,y#=0)
		spd#=0
		maxspd=40
		p#=0;y#=0;r#=0
		entity=CopyEntity(PlayerMesh)
		list.AddLast(Self)
		players:+1
	End Method
	
	Method Kill()
		FreeEntity entity
		players:-1
		list.remove(Self)
	End Method
	
	Method Update()
		If spd&gt;0 spd:-decellerate ElseIf spd&lt;0 spd:+decellerate
		If Abs(spd)=&lt;decellerate spd=0
		If spd&gt;maxspd spd=maxspd
		If spd&lt;-maxspd spd=-maxspd
		MoveEntity entity,0,spd/40,0
		RotateEntity entity,p,y,r
	End Method
	
End Type
</textarea> <br><br></td></tr></table><br>
<a name="900182"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >impixi</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'd probably encapsulate the mouselook capability within the mouse class itself, to make things a little tidier.<br><br>Your code structure is somewhat unkind to my way of thinking, so I can't help you directly, but here's a simple example that hopefully will help:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Import sidesign.minib3d

Local ScreenWidth:Int = 800
Local ScreenHeight:Int = 600
Local ScreenWidthC:Int = (ScreenWidth / 2)
Local ScreenHeightC:Int = (ScreenHeight / 2)

Graphics3D 800, 600

AmbientLight 255, 255, 255

Local Camera:TCamera = CreateCamera()
PositionEntity Camera, 0.0, 0.0, -10.0

Local Cube:TMesh = CreateCube()
PositionEntity Cube, 0.0, 0.0, 0.0

PointEntity Camera, Cube

Local Mouse:TMouseSystem = TMouseSystem.Create()

Local MoveSpeed:Float = 0.5

HideMouse

While Not KeyHit(KEY_ESCAPE)
	
	If KeyDown(KEY_W) Then MoveEntity Camera, 0.0, 0.0, MoveSpeed
	If KeyDown(KEY_S) Then MoveEntity Camera, 0.0, 0.0, -MoveSpeed
	If KeyDown(KEY_A) Then MoveEntity Camera, -MoveSpeed, 0.0, 0.0
	If KeyDown(KEY_D) Then MoveEntity Camera, MoveSpeed, 0.0, 0.0
	
	Mouse.Look Camera, ScreenWidthC, ScreenHeightC
	
	Mouse.Update
	
	RenderWorld
	
	Flip True

Wend

End


'************* MOUSE CLASS *************


Type TMouseSystem


	Function Create:TMouseSystem(pointer_url:String = "")
	
		Local msys:TMouseSystem = New TMouseSystem
		
		msys.Pointer = LoadImage(pointer_url)
		
		Return msys
	
	EndFunction


	Field MX:Int, MY:Int, OldMX:Int, OldMY:Int
	Field MXS:Int, MYS:Int
	Field Pointer:TImage

	
	Method MovePointer(x:Int, y:Int)
	
		MoveMouse x, y
		
		MX = x
		MY = y
	
	EndMethod


	Method Look(cam:TEntity, x:Int, y:Int, quickness:Float = 3.0)
	
		MXS :+ (XSpeed() / quickness)
		MYS :+ (YSpeed() / quickness)

		If cam Then RotateEntity cam, MYS, -MXS, 0.0

		MovePointer x, y
		
		XSpeed()
		YSpeed()
	
	EndMethod 
	
	
	Method Update()

		MX = MouseX()
		MY = MouseY()
		
		If Pointer Then DrawImage Pointer, MX, MY
	
	EndMethod
		

	Method XSpeed:Int()
	
		Local mxs:Int = MX - OldMX
		
		OldMX = MX
		
		Return mxs
		
	EndMethod
	
	
	Method YSpeed:Int()
	
		Local mys:Int = MY - OldMY
		
		OldMY = MY
		
		Return mys
		
	EndMethod


EndType

</textarea> <br><br></td></tr></table><br>
<a name="900297"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pineapple</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Your code was a bit.. jumpy.. for me, but this code works wonderfully:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Type T_MouseLook
	Field resetX%
	Field resetY%
	Field speedX%  'not needed externally but may be useful for other things
	Field speedY%  
        Field pitch:Float  'smoother inverse value for rotation
        Field yaw:Float

	Method Init(rX% , rY%)
		resetX = rx
		resetY = ry
	End Method
	
	Method Pollmouse()
		speedX = resetX - MouseX()
		speedY = resetY - MouseY()
              	yaw :- Float(-speedX) / 15
         	pitch :+ Float(-speedY) / 15
		MoveMouse(resetX,resetY)	
	End Method
	
	Method flush()
		speedX = 0
		speedY = 0
		MoveMouse(resetX,resetY)
	End Method	
End Type

'Initialise mouselook
Local mlook:T_MouseLook=New t_mouselook
mlook.init(512,350) ' or width/height*.5 etc
mlook.flush() 'kills any initial movements

While Not KeyDown(KEY_ESCAPE)		

	'Poll mouse And adjust rotation values.
	mlook.pollmouse()
	RotateEntity cam, mlook.pitch, mlook.yaw, 0
     '   (......rest of main loop)
Wend</textarea><br><br><br><br>Can't remeber where I found it XD I thought I copied your code but apparently this remained on the clipboard. Thanks all for the help. <br><br></td></tr></table><br>
<a name="900515"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LT</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have unfortunately learned that making mouse move code that works well on a lot of different computers is tough in OpenGL.  The results with the various drivers are pretty inconsistent.  At least, that has been my experience.  :( <br><br></td></tr></table><br>
<a name="900783"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >impixi</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> I have unfortunately learned that making mouse move code that works well on a lot of different computers is tough in OpenGL.  <br></div><br><br>I have also noticed this. I've yet to find an acceptable solution in Mac OS 10.5. It is annoying the hell out of me... <br><br></td></tr></table><br>
<a name="901012"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >impixi</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's a partial framework that functions reasonably well on MacOS 10.5, but still isn't entirely smooth:<br><br>EDIT: Removed code. Works crap on Windows... Back to the drawing board... <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
