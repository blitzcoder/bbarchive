<!DOCTYPE html><html lang="en" ><head ><title >Convert Sprite to Mesh</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Convert Sprite to Mesh</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=119" >MiniB3D Module</a>/<a href="#bottom" >Convert Sprite to Mesh</a><br><br>
<a name="1127054"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kittomer</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just a quick question: Is it somehow possible to convert a TSprite into a TMesh?<br><br>I tried Addmesh and casting (haha), but while it produced no errors nothing seemed to happen, either.<br><br>I'm asking this since right now I'm creating grass in my game by the way of sprites and it would be dapper if I could merge them all into a single mesh so that I only have to expend one drawcall on it.<br><br>I could swear to remember that MiniB3D does some singlesurface stuff with sprites, but considering how much framerate suffers when I have a moderate amount of grass sprites in my scene, I tend not to believe that.<br><br>If there is no easy way to do that, I guess I will have to put the mesh together manually, not like I don't know how that works. I would prefer the easy way, though. <br><br></td></tr></table><br>
<a name="1127057"></a>

<a name="1127058"></a>

<a name="1127059"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamRedwoods</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Tell you what...<br><br>Here's my TBatchSprite for monkey+minib3d, but you'll have to convert it to BlitzMax yourself. If you do, please repost it so others can benefit. I was going to do this afterwards, as I have several methods of speeding up BMax minib3d.<br><br>Conversion Notes:<br><br>- has no Copy() method, instead use CreateSprite()<br><br>- you'll have to convert over monkey-specific classes like IntStack<br><br>- make sure the batchorigin is not the came as the camera (0,0,0). i may have to add a check in here for this.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

'' TBatchSprites
''
'' these batchsprites are one mesh
''

Class TBatchSpriteMesh Extends TMesh

	Field surf:TSurface 
	Field free_stack:IntStack ''list of available vertex
	Field num_sprites =0
	Field sprite_list:List&lt;TBatchSprite&gt;
	
	
	
	Function Create:TBatchSpriteMesh(parent_ent:TEntity=Null)
	
		Local mesh:TBatchSpriteMesh = New TBatchSpriteMesh
		
		mesh.AddParent(parent_ent)
		entity_list.EntityListAdd(mesh)

		' update matrix
		If mesh.parent&lt;&gt;Null
			mesh.mat.Overwrite(mesh.parent.mat)
			mesh.UpdateMat()
		Else
			mesh.UpdateMat(True)
		Endif
		
		mesh.surf = mesh.CreateSurface()
		mesh.num_sprites = 0
		mesh.free_stack = New IntStack
				'mainlist
				
		mesh.EntityFX 1+2'+1'+32 '' full bright+ use vertex colors + alpha
		mesh.brush.shine=0.0
		'mesh.EntityBlend 3
		'mesh.EntityColor(0,0,0)
		'mesh.EntityAlpha 0.001
		
				
		mesh.classname = "BatchSpriteMesh"
		mesh.is_sprite = True ''may not need this
		mesh.cull_radius = -999999.0
		
		mesh.sprite_list = New List&lt;TBatchSprite&gt;
	
		Return mesh
	End
	
	Method Update(cam:TCamera)
	
		mat_sp = mat
		


		TBatchSprite.min_x=999999999.0
		TBatchSprite.max_x=-999999999.0
		TBatchSprite.min_y=999999999.0
		TBatchSprite.max_y=-999999999.0
		TBatchSprite.min_z=999999999.0
		TBatchSprite.max_z=-999999999.0
		
		For Local ent:TEntity = Eachin sprite_list
			
			ent.Update(cam)
			
		Next
		
		''do our own bounds
		
		If num_sprites&gt;0
		
			Local width#=TBatchSprite.max_x-TBatchSprite.min_x
			Local height#=TBatchSprite.max_y-TBatchSprite.min_y
			Local depth#=TBatchSprite.max_z-TBatchSprite.min_z
	
			' get bounding sphere (cull_radius#) from AABB
			' only get cull radius (auto cull), if cull radius hasn't been set to a negative no. by TEntity.MeshCullRadius (manual cull)
	
			If width&gt;=height And width&gt;=depth
				cull_radius=width
			Else
				If height&gt;=width And height&gt;=depth
					cull_radius=height
				Else
					cull_radius=depth
				Endif
			Endif
			cull_radius=cull_radius * 0.5
			Local crs#=cull_radius*cull_radius
			cull_radius= Sqrt(crs+crs+crs)
		
		
			min_x = TBatchSprite.min_x
			min_y = TBatchSprite.min_y
			min_z = TBatchSprite.min_z
			max_x = TBatchSprite.max_x
			max_y = TBatchSprite.max_y
			max_z = TBatchSprite.max_z

			'mat.grid[3][0] = 1.0'width *0.5
			'mat.grid[3][1] = 0'height *0.5
			'mat.grid[3][2] = -10.0'-depth *0.5
			
			'PositionEntity(width *0.5,height *0.5,depth *0.5)
		Endif
	End
End


Class TBatchSprite Extends TSprite
		
		Field batch_id:Int ''ids start at 1
		Field vertex_id:Int
		Field sprite_link:list.Node&lt;TBatchSprite&gt;
		
		Global min_x:Float, min_y:Float, max_x:Float, max_y:Float, min_z:Float, max_z:Float
		
		Global mainsprite:TBatchSpriteMesh[] = New TBatchSpriteMesh[10]	
		Global total_batch:Int =0
		
		Private
	
		Global temp_mat:Matrix = New Matrix
	
		Public
		
		Method New()
			
			''new batch sprite
						
		End
		
		Method FreeEntity()
'Print"spritebatch remove"			
			mainsprite[batch_id].surf.VertexColor(vertex_id+0, 0,0,0,0)
			mainsprite[batch_id].surf.VertexColor(vertex_id+1, 0,0,0,0)
			mainsprite[batch_id].surf.VertexColor(vertex_id+2, 0,0,0,0)
			mainsprite[batch_id].surf.VertexColor(vertex_id+3, 0,0,0,0)
			
			'mainsprite[batch_id].surf.reset_vbo = mainsprite[batch_id].surf.reset_vbo|8
			
			mainsprite[batch_id].free_stack.Push(vertex_id)
			
			sprite_link.Remove()
			
			If parent
				parent_link.Remove()
				parent=Null
			Endif
			mat=Null
			brush=Null
			link=Null
			
		End
		
		
		Method Copy()
			
			''use CreateSprite(), since they should all be the same
			
		End
		
		
		
		''
		''add a parent to the entire batch mesh
		''
		Function BatchSpriteParent(id:Int=0, ent:TEntity,glob:Int=True)
			
			If id=0 Then id = total_batch
			If id = 0 Then Return
			
			mainsprite[id].EntityParent(ent, glob)
		
		End
		
		''
		''return the sprite batch main mesh entity
		''
		Method BatchSpriteEntity:TEntity()
			
			Return mainsprite[batch_id]
		
		End
		
		''
		''move the batch sprite origin for depth sorting
		''
		Method BatchSpriteOrigin(x:Float,y:Float,z:Float)
			
			mainsprite[batch_id].PositionEntity(x,y,z)
			
		End
		
		Function CreateBatchMesh:TBatchSpriteMesh( batchid:Int )

			While total_batch &lt; batchid Or total_batch =0
			
				total_batch +=1
				If total_batch &gt;= mainsprite.Length() Then mainsprite = mainsprite.Resize(total_batch+5)

	'Print "TBatchSPrite createmesh:"+total_batch
	'Print mainsprite.Length()		
				mainsprite[total_batch] = TBatchSpriteMesh.Create()
				
				
			Wend
			
			Return mainsprite[total_batch]
		End

		Function CreateSprite:TBatchSprite(idx:Int=0)
			''add sprite to batch
				
			Local sprite:TBatchSprite=New TBatchSprite
			sprite.classname="BatchSprite"
	
			' update matrix
			If sprite.parent&lt;&gt;Null
				sprite.mat.Overwrite(sprite.parent.mat)
				sprite.UpdateMat()
			Else
				sprite.UpdateMat(True)
			Endif
			
			If idx = 0 Then sprite.batch_id = total_batch Else sprite.batch_id = idx
			If sprite.batch_id = 0 Then sprite.batch_id = 1
			Local id:Int = sprite.batch_id
			
			''create main mesh
			Local mesh:TBatchSpriteMesh
	
			If id &gt; total_batch
			
				mesh = CreateBatchMesh(id)
				If id=0 Then id = 1
				
			Else
			
				mesh = mainsprite[id]
				
			Endif

			''get vertex id
			Local v:Int
			
			If mesh.free_stack.IsEmpty()
				
				mesh.num_sprites +=1
				v = (mesh.num_sprites-1) * 4 ''4 vertex per quad
				'Local p:Int = v+1
				
				mesh.surf.AddVertex(-1,-1,0, 0, 1)
				mesh.surf.AddVertex(-1, 1,0, 0, 0)
				mesh.surf.AddVertex( 1, 1,0, 1, 0)
				mesh.surf.AddVertex( 1,-1,0, 1, 1)
				mesh.surf.AddTriangle(0+v,1+v,2+v)
				mesh.surf.AddTriangle(0+v,2+v,3+v)

'Print "TSPrite verts:"+mesh.surf.no_verts+":"
			Else
			
				v = mesh.free_stack.Pop()
				
			Endif
			
			mesh.reset_bounds = False ''we control our own bounds
			
			sprite.vertex_id = v
			'sprite.EntityParent(mainsprite[batchid],0) ''add to child list
			'sprite.parent = Null ''but dont use the parent matrix
			sprite.sprite_link = mesh.sprite_list.AddLast(sprite)

			Return sprite
	
		End 
		
		
		Function LoadBatchTexture(tex_file$,tex_flag:Int=1,id:Int=0)
			''
			''does not create sprite, just loads texture
			''
			
			If id&lt;=0 Or id&gt;total_batch Then id=total_batch
			If id=0 Then id=1
			
			CreateBatchMesh(id)
	
			Local tex:TTexture=TTexture.LoadTexture(tex_file,tex_flag)
			mainsprite[id].EntityTexture(tex)

			' additive blend if sprite doesn't have alpha or masking flags set
			If tex_flag&amp;2=0 And tex_flag&amp;4=0
			
				mainsprite[id].EntityBlend 3
				'tex.TextureBlend 3
				
			Endif

	
		End
		
		
		Method Update(cam:TCamera )


			If view_mode&lt;&gt;2
				
				'' add in mainsprite position offset
				
				Local x#=mat.grid[3][0] - mainsprite[batch_id].mat.grid[3][0]
				Local y#=mat.grid[3][1] - mainsprite[batch_id].mat.grid[3][1]
				Local z#=mat.grid[3][2] - mainsprite[batch_id].mat.grid[3][2]
			
				temp_mat.Overwrite(cam.mat)
				temp_mat.grid[3][0]=x
				temp_mat.grid[3][1]=y
				temp_mat.grid[3][2]=z
				mat_sp.Overwrite(temp_mat)

				
				If angle&lt;&gt;0.0
					mat_sp.RotateRoll(angle)
				Endif
				
				If scale_x&lt;&gt;1.0 Or scale_y&lt;&gt;1.0
					mat_sp.Scale(scale_x,scale_y,1.0)
				Endif
				
				If handle_x&lt;&gt;0.0 Or handle_y&lt;&gt;0.0
					mat_sp.Translate(-handle_x,-handle_y,0.0)
				Endif
				
			Else
				
				mat_sp.Overwrite(mat)
			
				If scale_x&lt;&gt;1.0 Or scale_y&lt;&gt;1.0
					mat_sp.Scale(scale_x,scale_y,1.0)
				Endif
				
			Endif
					
			''update main mesh
			
			'' rotate each point corner offset to face the camera with cam_mat
			'' use the mat.x.y.z for position and offset from that
			Local p0:Float[], p1:Float[], p2:Float[], p3:Float[]
			'Local temp_mat:Matrix = mat_sp.Copy() 'Inverse()
			Local o:Float[] = [mat_sp.grid[3][0],mat_sp.grid[3][1],mat_sp.grid[3][2]]
			
			Local m00:Float = mat_sp.grid[0][0]
			Local m01:Float = mat_sp.grid[0][1]
			Local m10:Float = mat_sp.grid[1][0]
			Local m11:Float = mat_sp.grid[1][1]
			Local m02:Float = mat_sp.grid[0][2]
			Local m12:Float = mat_sp.grid[1][2]
			
			'p0 = mat_sp.TransformPoint(-1.0,-1.0,0.0)
			p0 = [-m00 + -m10 + o[0] , -m01 + -m11 + o[1], m02 + m12 - o[2]]		
			'p1 = mat_sp.TransformPoint(-1.0,1.0,0.0)		
			p1 = [-m00 + m10 + o[0] , -m01 + m11 + o[1], m02 - m12 - o[2]]	
			'p2 = mat_sp.TransformPoint(1.0,1.0,0.0)
			p2 = [m00 + m10 + o[0] , m01 + m11 + o[1], -m02 - m12 - o[2]]			
			'p3 = mat_sp.TransformPoint(1.0,-1.0,0.0)
			p3 = [m00 - m10 + o[0] , m01 - m11 + o[1], -m02 + m12 - o[2]]
			
			
			mainsprite[batch_id].surf.VertexCoords(vertex_id+0,p0[0],p0[1],p0[2])
			mainsprite[batch_id].surf.VertexCoords(vertex_id+1,p1[0],p1[1],p1[2])
			mainsprite[batch_id].surf.VertexCoords(vertex_id+2,p2[0],p2[1],p2[2])
			mainsprite[batch_id].surf.VertexCoords(vertex_id+3,p3[0],p3[1],p3[2])
			
			Local r# = brush.red'*brush.alpha
			Local g# = brush.green'*brush.alpha
			Local b# = brush.blue'*brush.alpha
			Local a# = brush.alpha '*0.5
		
			mainsprite[batch_id].surf.VertexColorFloat(vertex_id+0, r,g,b,a)
			mainsprite[batch_id].surf.VertexColorFloat(vertex_id+1, r,g,b,a)
			mainsprite[batch_id].surf.VertexColorFloat(vertex_id+2, r,g,b,a)
			mainsprite[batch_id].surf.VertexColorFloat(vertex_id+3, r,g,b,a)
			
			
			''determine our own bounds
			
			min_x = Min5(p0[0],p1[0],p2[0],p3[0],min_x )
			min_y = Min5(p0[1],p1[1],p2[1],p3[1],min_y )
			min_z = Min5(p0[2],p1[2],p2[2],p3[2],min_z )
			
			max_x = Max5(p0[0],p1[0],p2[0],p3[0],max_x )
			max_y = Max5(p0[1],p1[1],p2[1],p3[1],max_y )
			max_z = Max5(p0[2],p1[2],p2[2],p3[2],max_z )
			
		End
		
End

Function Min5:Float(a:Float, b:Float, c:Float, d:Float, e:Float)
	
	Local r:Float = a
	Local t:Float = c
	
	If b&lt;r Then r=b
	If d&lt;t Then t=d
	If t&lt;r Then r=t
	
	If r &lt; e Then Return r Else Return e
	
End

Function Max5:Float(a:Float, b:Float, c:Float, d:Float, e:Float)
	
	Local r:Float = a
	Local t:Float = c
	
	If b&gt;r Then r=b
	If d&gt;t Then t=d
	If t&gt;r Then r=t
	
	If r &gt; e Then Return r Else Return e
	
End
</textarea><br><br>Other thoughts re: Grass sprites<br>.... so when rendering sprites as one mesh, you lose alpha depth sorting (order independent depth sorting anyone? bleh).<br><br>Therefore, you would have to use Alpha testing mask (but no alpha), which means you'll get fringe artifacts on your transparent pngs.<br>Or you could just render the mesh first (set origin far away from camera) and handle cases where grass overlaps transparent objects <br>(ie. glass cylinder in open fields, will render over the grass if the grass batch sprite is transparent)<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1127145"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kittomer</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks, I'll look into it!<br>Although I'm not sure I need something quite that comprehensive.<br>Also I know nothing about monkey.<br><br>Being on the subject, how likely is it that my code will just... work if I'd just copy it into monkey, if I don't use anything except standard blitzmax and minib3d code? <br><br></td></tr></table><br>
<a name="1127167"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamRedwoods</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> re: copying into monkey<br><br>from blitz to monkey, you might do ok, but i would also hesitate and say no, monkey has too many 'gotchas' that will require some thinking if portability is a requirement. <br><br></td></tr></table><br>
<a name="1127504"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kittomer</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Converting that code was a bit above my head, so I ended up adapting my chunk meshing code to build a single grass mesh. Is a lot faster now. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
