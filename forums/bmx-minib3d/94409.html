<!DOCTYPE html><html lang="en" ><head ><title >Save mesh</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Save mesh</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=119" >MiniB3D Module</a>/<a href="#bottom" >Save mesh</a><br><br>
<a name="1083084"></a>

<a name="1083092"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Does anyone have any code for saving meshes loaded in MiniB3d? :o<br>I'd like to load a bunch of meshes, merge them into one mesh and finally save that one mesh as a single B3D file. (or some other format)<br>It's important that the textures are there though...<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1083108"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> You could try this archive entry:<br><a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=866" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=866</a><br>It can be autoconverted by using File-&gt;Import bb file.<br>Then it needs a few tweaks to get it running:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import sidesign.minib3d

Global b3d_stack[100+1]
Global b3d_file,b3d_tos



' ID: 866
' Author: jfk EO-11110
' Date: 2003-12-24 01:23:50
' Title: SaveB3D
' Description: Save multisurface textured Mesh as .B3D

Graphics3D 640,480,32,2

meshname$="testsurf.b3d" ' Mesh To save... (one that is using textures)

' BTW: you should run this from inside the folder where the Mesh resides, unless you edit the
' Part labeled by "&lt;&lt;&lt;&lt;&lt;&lt;&lt;&lt;&lt;&lt;&lt;&lt;"

Include "b3dfile.bmx"
' this file can be found here: <a href="http://www.blitzbasic.com/sdkspecs/sdkspecs/b3dfile_utils.zip" target="_blank">http://www.blitzbasic.com/sdkspecs/sdkspecs/b3dfile_utils.zip</a>
cam=CreateCamera()
TranslateEntity cam,0,0,-10
If  FileType(meshname$) &lt;&gt; 1 Then
	RuntimeError "File not found: " + meshname$
End If

mesh=LoadMesh(meshname$)
Global c_surfs=CountSurfaces(mesh)

Print "Mesh "+meshname$+" has "+ c_surfs+" Surfaces, using the following textures:"

Global c_surf[c_surfs+1]
Global c_brush[c_surfs+1]
Global c_tex[c_surfs+1]
Global c_tex_name$[c_surfs+1]

' track down used textures (thanks Mark!)
For i=1 To c_surfs
 c_surf(i)= GetSurface(mesh,i)
 c_brush(i)=GetSurfaceBrush( c_surf(i) )
 c_tex(i)=GetBrushTexture( c_brush(i) )
 If c_tex(i) &lt;&gt; Null
	 c_tex_name$(i)=Lower$(TextureName$( c_tex(i))) ' Full (!) Texture Path
	 curdir$=Lower$(CurrentDir$()) 
	 c_tex_name$(i)= Replace$(c_tex_name$(i),curdir$,"") '&lt;&lt;&lt;&lt;&lt;&lt;&lt;&lt;&lt;&lt;&lt;&lt;&lt;&lt;&lt;&lt;&lt;&lt;&lt;
 End If
 Print c_tex_name$(i)
 If c_tex_name$(i)="" Then Print "Error: Surface No."+i+" has no Texture"
 If FileType(c_tex_name$(i))&lt;&gt;1 Then Print "Warning: Surface No."+i+" uses nonexistant Texture ("+c_tex_name$(i)+")."
Next

Print "Press any key to save this Mesh as TEMP.B3D"


WaitKey()

' End

WriteBB3D( "temp.b3d",mesh )

For i=1 To c_surfs
 FreeBrush c_brush(i)' Release memory
 If c_tex(i) &lt;&gt; Null FreeTexture c_tex(i)
Next


' test If it worked...
FreeEntity mesh
mesh2=LoadMesh("temp.b3d")
While Not KeyDown(27)
 TurnEntity mesh2,1,2,3
 RenderWorld()
 Text 0,0,"TEMP.B3D"
 Flip
Wend
End


Function WriteBB3D( f_name$,mesh )

	file=WriteFile( f_name$ )

	b3dSetFile( file )
	
	b3dBeginChunk( "BB3D" )
		b3dWriteInt( 1 )	'version

		b3dBeginChunk( "TEXS" ) ' list all textures used by the mesh
		For i=1 To c_surfs
			b3dWriteString( c_tex_name$(i) ) 	'texture file
			b3dWriteInt( 1 )					'flags
			b3dWriteInt( 2 )					'blend
			b3dWriteFloat( 0 )					'x in tex 0 (hu?)
			b3dWriteFloat( 0 )					'y in tex 0
			b3dWriteFloat( 1 )					'x scale 1
			b3dWriteFloat( 1 )					'y scale 1
			b3dWriteFloat( 0 )					'rotation 0
			
		Next
		b3dEndChunk()	'End of TEXS chunk

		
		For i=1 To c_surfs
			b3dBeginChunk( "BRUS" ) ' describe all brushes used by the mesh
		
			b3dWriteInt( 1 )					'number of textures per brush ; (eg 2 with lightmap)
			b3dWriteString( "brush"+(i-1) )		'brushname
			b3dWriteFloat( 1 )					'red
			b3dWriteFloat( 1 )					'green
			b3dWriteFloat( 1 )					'blue
			b3dWriteFloat( 1 )					'alpha
			b3dWriteFloat( 0 )					'shininess
			b3dWriteInt( 1 )					'blendmode
			b3dWriteInt( 0 )					'FX
			b3dWriteInt( i-1 )					'used texture index 
'			b3dWriteInt( ? )					;additional texture index (eg lightmap), but here we only use 1 (see above)

			b3dEndChunk()	'End of BRUS chunk
		Next
		
		b3dBeginChunk( "NODE" )
			b3dWriteString( "entity_name_here!" )
			b3dWriteFloat( 0 )	'x_pos
			b3dWriteFloat( 0 )	'y_pos
			b3dWriteFloat( 0 )	'z_pos
			b3dWriteFloat( 1 )	'x_scale
			b3dWriteFloat( 1 )	'y_scale
			b3dWriteFloat( 1 )	'z_scale
			b3dWriteFloat( 1 )	'rot_w
			b3dWriteFloat( 0 )	'rot_x
			b3dWriteFloat( 0 )	'rot_y
			b3dWriteFloat( 0 )	'rot_z
			WriteMESH( mesh )
		b3dEndChunk()	'End of NODE chunk
		
	b3dEndChunk()	'End of BB3D chunk
	
	CloseFile file
End Function

Function WriteMESH( mesh )

	n_surfs=CountSurfaces( mesh )
	
	b3dBeginChunk( "MESH" )
		b3dWriteInt( -1 )				'no 'entity' brush -1
		
		b3dBeginChunk( "VRTS" )
			b3dWriteInt( 0 )			'flags - 0=no normal/color
			b3dWriteInt( 1 )			'number of tex_coord sets (eg: 2 with lightmap)
			b3dWriteInt( 2 )			'coords per set (u,v,w?) 2 with uv, 3 with uvw
			
			For k=1 To n_surfs
				surf=GetSurface( mesh,k )
				n_verts=CountVertices( surf )-1
				
				For j=0 To n_verts
					b3dWriteFloat( VertexX( surf,j ) )
					b3dWriteFloat( VertexY( surf,j ) )
					b3dWriteFloat( VertexZ( surf,j ) )
					b3dWriteFloat( VertexU#( surf,j,0 ) )
					b3dWriteFloat( VertexV#( surf,j,0 ) )
'					b3dWriteFloat( VertexW#( surf,j,0 ) )
';					b3dWriteFloat( VertexU#( surf,j,1 ) ) ; lightmap uv
';					b3dWriteFloat( VertexV#( surf,j,1 ) ) ; lightmap uv
'					b3dWriteFloat( VertexW#( surf,j,1 ) )
				Next
			Next
		b3dEndChunk()	'End of VRTS chunk
		
		first_vert=0
		For k=1 To n_surfs
			surf=GetSurface( mesh,k )
			n_tris=CountTriangles( surf )-1
			
			b3dBeginChunk( "TRIS" )
				b3dWriteInt( k-1 )		'brush For these triangles (surf -1 !!!)
				
				For j=0 To n_tris
					b3dWriteInt( first_vert+TriangleVertex( surf,j,0 ) )
					b3dWriteInt( first_vert+TriangleVertex( surf,j,1 ) )
					b3dWriteInt( first_vert+TriangleVertex( surf,j,2 ) )
				Next
				
			b3dEndChunk()	'End of TRIS chunk
			
			first_vert=first_vert+CountVertices( surf )
			
		Next
		
	b3dEndChunk()	'End of MESH chunk
	
End Function


'-------------------------------------------------------------------------------------------------




'
'b3d file utils to be included
'


Function b3dSetFile( file )
	b3d_tos=0
	b3d_file=file
End Function

'***** functions for reading from B3D files *****

Function b3dReadByte()
	Return ReadByte( b3d_file )
End Function

Function b3dReadInt()
	Return ReadInt( b3d_file )
End Function

Function b3dReadFloat#()
	Return ReadFloat( b3d_file )
End Function

Function b3dReadString$()
	Repeat
		ch=b3dReadByte()
		If ch=0 Return t$
		t$=t$+Chr$(ch)
	Forever
End Function

Function b3dReadChunk$()
	For k=1 To 4
		tag$=tag$+Chr$(b3dReadByte())
	Next
	sz=ReadInt( b3d_file )
	b3d_tos=b3d_tos+1
	b3d_stack(b3d_tos)=StreamPos( b3d_file )+sz
	Return tag$
End Function

Function b3dExitChunk()
	SeekStream b3d_file,b3d_stack(b3d_tos)
	b3d_tos=b3d_tos-1
End Function

Function b3dChunkSize()
	Return b3d_stack(b3d_tos)-StreamPos( b3d_file )
End Function

'***** Functions for writing to B3D files *****

Function b3dWriteByte( n )
	WriteByte( b3d_file,n )
End Function

Function b3dWriteInt( n )
	WriteInt( b3d_file,n )
End Function

Function b3dWriteFloat( n# )
	WriteFloat( b3d_file,n )
End Function

Function b3dWriteString( t$ )
	For k=1 To Len( t$ )
		ch=Asc(Mid$(t$,k,1))
		b3dWriteByte(ch)
		If ch=0 Return
	Next
	b3dWriteByte( 0 )
End Function

Function b3dBeginChunk( tag$ )
	b3d_tos=b3d_tos+1
	For k=1 To 4
		b3dWriteByte(Asc(Mid$( tag$,k,1 )))
	Next
	b3dWriteInt( 0 )
	b3d_stack(b3d_tos)=StreamPos( b3d_file )
End Function

Function b3dEndChunk()
	n=StreamPos( b3d_file )
	SeekStream b3d_file,b3d_stack(b3d_tos)-4
	b3dWriteInt( n-b3d_stack(b3d_tos) )
	SeekStream b3d_file,n
	b3d_tos=b3d_tos-1
End Function
</textarea><br>The program loads a file, then saves it. If you run it, it will give a 'file not found' error because it can't find "testsurf.b3d". <br><br></td></tr></table><br>
<a name="1083167"></a>

<a name="1083168"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah but Warner... you seem pretty high on this 3D stuff...<br>What I want to do is merge a bunch of meshes (over 300) and they all have one texture of their own.<br>I'd like to merge those 300 into one file, so I was thinking about merging the meshes using the merge mesh function and then resaving the mesh as something.<br>But the problem is that when I do it (tried like all of the code archive export codes in Blitz3D) they just save one of the textures and it's very important that it saves all of the textures.<br>Do you know what I mean?<br>I guess I should make an example of this...<br>I'll upload it soon!<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1083174"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> I'd like to merge those 300 into one file,  <br></div>O_o really? That sounds like quite erm .. much.<br>You might use your own mesh format, maybe. If it is a single mesh, without hierarchy, use something like this:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Import sidesign.minib3d

Graphics3D 800, 600, 0, 2

CreateLight()

Local cam:TCamera = CreateCamera()
MoveEntity cam, 0, 0, -15

'build a mesh
Local tex1:TBrush = LoadBrush("..\data\4922.png")
Local tex2:TBrush = LoadBrush("..\data\checker.png")

Local mesh:TMesh = CreateCube()
Local mesh2:TMesh = CreateSphere()

PaintSurface GetSurface(mesh, 1), tex1
PaintSurface GetSurface(mesh2, 1), tex2

PositionMesh mesh2, 0, 2, 0
AddMesh mesh2, mesh
FreeEntity mesh2

'save it
SaveMesh(mesh, "mesh.dat")

'free it
FreeEntity mesh

'load it back
Local test:TMesh = ReadMesh("mesh.dat")

Repeat

	TurnEntity test, 1, 2, 3
	RenderWorld
	Flip

Until KeyHit(27)

End


Function SaveMesh(mesh:TMesh, fname$)

	Local ff:TStream = WriteFile(fname$)
	WriteInt ff, CountSurfaces(mesh)
	For Local i% = 1 To CountSurfaces(mesh)
	
		Local surf:TSurface = GetSurface(mesh, i)
			
		Local brush:TBrush = GetSurfaceBrush(surf)
		For Local d% = 0 To 7
			Local tex:TTexture = GetBrushTexture(brush, d)
			If tex &lt;&gt; Null Then WriteLine ff, TextureName(tex) Else WriteLine ff, ""
		Next
	
		WriteInt ff, CountVertices(surf) 
		For Local v% = 0 To CountVertices(surf) - 1
			WriteFloat ff, VertexX(surf, v)
			WriteFloat ff, VertexY(surf, v)
			WriteFloat ff, VertexZ(surf, v)				
			WriteFloat ff, VertexU(surf, v, 0)
			WriteFloat ff, VertexV(surf, v, 0)
			WriteFloat ff, VertexU(surf, v, 1)
			WriteFloat ff, VertexV(surf, v, 1)
			WriteFloat ff, VertexNX(surf, v)
			WriteFloat ff, VertexNY(surf, v)
			WriteFloat ff, VertexNZ(surf, v)				
		Next
		
		WriteInt ff, CountTriangles(surf)
		For Local t% = 0 To CountTriangles(surf) - 1
			WriteInt ff, TriangleVertex(surf, t, 0)
			WriteInt ff, TriangleVertex(surf, t, 1)
			WriteInt ff, TriangleVertex(surf, t, 2)
		Next
		
	Next
	CloseFile ff

End Function

Function ReadMesh:TMesh(fname$)

	If FileType(fname$) &lt;&gt; 1 Return Null
	Local ff:TStream = ReadFile(fname$)
	
	Local mesh:TMesh = CreateMesh()
		
	Local countsurf% = ReadInt(ff)
	For Local i% = 1 To countsurf
	
		Local surf:TSurface = CreateSurface(mesh)

		Local brush:TBrush = CreateBrush(255, 255, 255)
		
		For Local d% = 0 To 7
			Local texname$ = ReadLine(ff)
			If texname$ &lt;&gt; Null Then 
				Local tex:TTexture = LoadTexture(texname$)
				If tex &lt;&gt; Null Then BrushTexture brush, tex, 0, d
			End If
		Next
		PaintSurface surf, brush
		
		Local countv% = ReadInt(ff)
		For Local v% = 0 To countv - 1
			Local vx# = ReadFloat(ff)
			Local vy# = ReadFloat(ff)
			Local vz# = ReadFloat(ff)
			Local vu# = ReadFloat(ff)
			Local vv# = ReadFloat(ff)
			Local vertex% = AddVertex (surf, vx,vy,vz,vu,vv)
			Local vu2# = ReadFloat(ff)
			Local vv2# = ReadFloat(ff)			
			VertexTexCoords surf, vertex, vu2, vv2, 0.0, 1
			Local nx# = ReadFloat(ff)
			Local ny# = ReadFloat(ff)
			Local nz# = ReadFloat(ff)
			VertexNormal surf, vertex, nx, ny, nz
		Next

		Local countt% = ReadInt(ff)
		For Local t% = 0 To countt - 1
			Local a% = ReadInt(ff)
			Local b% = ReadInt(ff)
			Local c% = ReadInt(ff)
			AddTriangle surf, a, b, c
		Next
	Next

	CloseFile ff
	
	Return mesh
	
End Function</textarea> <br><br></td></tr></table><br>
<a name="1083177"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah heh it's quite much, but there are a LOOOOOT of objects and the problem is that I don't really control it, cause the editor I'm using (and am forced to use) is not made by me.<br>And that's the reason I'd like to merge them into one single file.<br><br>When I did AddMesh() in Blitz3D... well let's just say it had some odd results when it came to using GetSurfaceBrush()<br><br>But yeah, thanks a lot, I'll give this a try! :) <br><br></td></tr></table><br>
<a name="1083199"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamRedwoods</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> AddMesh() compares brushes and if they're the same then it combines the surfaces.<br><br>Sometimes what I do is add a switch to disable odd behaviors that miniB3D does:<br>AddMesh(mesh, true) '' to skip compare brushes routine, and add in your own if/then to check, etc...<br><br>That way the original code works, but adds your own special routines. <br><br></td></tr></table><br>
<a name="1083212"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for the tip AdamRedwoods!<br>And thanks Warner for all the help.<br>Everything's been solved! :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
