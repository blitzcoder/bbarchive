<!DOCTYPE html><html lang="en" ><head ><title >Brush-Problems</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Brush-Problems</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=119" >MiniB3D Module</a>/<a href="#bottom" >Brush-Problems</a><br><br>
<a name="1032017"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have a problem with CreateSurface() under MiniB3D.<br><br>The skybox should show 6 different Textures, but it always shows the bottom texture. This happens on all graphics card on all Windows-versions. <br><br>My original B3D-Code runs perfect under Win XP, The MINIB3D-Code runs also perfect on Mac OsX. No problems no graphic errors.<br><br>Because I have heavy performance problems with B3D under WIN7, I tried to compile the MiniB3D-Version on Windows. Only the "Peter-Scheutz"-version was able to start, but produces this problem.<br><br><pre class=code>SuperStrict
'Import sidesign.minib3d
Import "minib3dsf/minib3d.bmx"

Graphics3d 1024,768,0,2


Global t:TMesh=CreateCube()
Global C:TCamera=CreateCamera()
ambientlight 255,255,55
moveentity c,0,2,-5
Global sky:TMesh = LoadSkyBox()
	


Repeat
	moveentity c,0.01,0,0.01
	Renderworld()
	Flip 0
Until KeyHit(key_escape)



Function LoadSkyBox:TMesh()
	Local m:TMesh = CreateMesh(), S:TSurface, B:TBrush
	'front face:
	'... for demonstration of the problem one face is enough:
	'....
	
	'top face:
	'b=LoadBrush( "SKY_UP.jpg",49 )
	b=CreateBrush(0,255,255) ' BLUE
			s=CreateSurface( m,b )
			AddVertex s,-1,+1,+1,0,1
			AddVertex s,+1,+1,+1,0,0
			AddVertex s,+1,+1,-1,1,0
			AddVertex s,-1,+1,-1,1,1
			AddTriangle s,0,1,2
			AddTriangle s,0,2,3
			FreeBrush b
	
	'bottom face:	
	'b=LoadBrush( "SKY_DN.jpg",49 )
      b=CreateBrush(0,255,0) ' GREEN
			s=CreateSurface( m,b )
			AddVertex s,-1,-1,-1,1,0
			AddVertex s,+1,-1,-1,1,1
			AddVertex s,+1,-1,+1,0,1
			AddVertex s,-1,-1,+1,0,0
			AddTriangle s,0,1,2
			AddTriangle s,0,2,3
			FreeBrush b
	
	ScaleMesh m,400,60,400
	FlipMesh m
	EntityFX m,1
	Return m
End Function</pre> <br><br></td></tr></table><br>
<a name="1032229"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I don't know what's going on, but if you create the faces in reverse order (the bottom first) they both appeared here.<br><br>Changing the LoadSkybox to this, also works:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Function LoadSkyBox:TMesh()
	Local m:TMesh = CreateMesh(), S:TSurface, B:TBrush, b2:TBrush, s2:TSurface
	'front face:
	'... for demonstration of the problem one face is enough:
	'....

	'top face:
	'b=LoadBrush( "SKY_UP.jpg",49 )
	b=CreateBrush(0,255,255) ' BLUE
			s=CreateSurface(m)
			AddVertex s,-1,+1,+1,0,1
			AddVertex s,+1,+1,+1,0,0
			AddVertex s,+1,+1,-1,1,0
			AddVertex s,-1,+1,-1,1,1
			AddTriangle s,0,1,2
			AddTriangle s,0,2,3
	 	 	s.PaintSurface b
			FreeBrush b

	'bottom face:	
	'b=LoadBrush( "SKY_DN.jpg",49 )
      b2=CreateBrush(0,255,0) ' GREEN
			s2=CreateSurface(m)
			AddVertex s2,-1,-1,-1,1,0
			AddVertex s2,+1,-1,-1,1,1
			AddVertex s2,+1,-1,+1,0,1
			AddVertex s2,-1,-1,+1,0,0
			AddTriangle s2,0,1,2
			AddTriangle s2,0,2,3
			s2.PaintSurface b2
			FreeBrush b2
	
			
	ScaleMesh m,400,60,400
	FlipMesh m
	EntityFX m,1
	Return m
End Function

</textarea> <br><br></td></tr></table><br>
<a name="1032231"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Found the problem... this one was easy! :)<br><br>on tMesh.bmx you have:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
	Method CreateSurface:TSurface(bru:TBrush=Null)
	
		Local surf:TSurface=New TSurface
		
		no_surfs=no_surfs+1
		
		If bru&lt;&gt;Null
			surf.brush=bru.Copy()
			brush = bru.Copy()
		EndIf

		' new mesh surface - update reset flags
		reset_bounds=True
		col_tree.reset_col_tree=True

		ListAddLast surf_list,surf
		Return surf
		
	End Method
</textarea><br><br>Look at brush = bru.copy() - this actually overwrites the mesh brush! just comment it out (or remove it, like below), and suddenly the example works :)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
	Method CreateSurface:TSurface(bru:TBrush=Null)
	
		Local surf:TSurface=New TSurface
		
		no_surfs=no_surfs+1
		
		If bru&lt;&gt;Null
			surf.brush=bru.Copy()
		EndIf

		' new mesh surface - update reset flags
		reset_bounds=True
		col_tree.reset_col_tree=True

		ListAddLast surf_list,surf
		Return surf
		
	End Method
</textarea> <br><br></td></tr></table><br>
<a name="1032359"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> @ SLotman<br>Thanks a lot for this help. It really works now. I would never have the heart to change something inside modules...<br><br>Is this an error in the module or could there be situations where this module code is correct?<br><br><br><b>Next Problem: TEXT command error</b><br>If you use the TEXT command of MINIB3D you will have graphic problems with the other object in the 2D-Section. They are all black<br><br><br><pre class=code>
SuperStrict
'Import sidesign.minib3d
Import "minib3dsf/minib3d.bmx"

Graphics3d 1024,768,0,2


Global t:TMesh=CreateCube()
Global C:TCamera=CreateCamera()
ambientlight 255,255,55
moveentity c,0,2,-5
	

Global	font:Timagefont=LoadImageFont(  "Arial.ttf",32,True )

Repeat
	moveentity c,0.01,0,0.01
 	RenderWorld()


	BeginMax2D
		SetColor 00,100,200
		DrawRect 400,400,200,200
		SetImageFont font
 		DrawText "Hallo" ,0,0
'		Text 0,0,"Hallo" 
	Endmax2d

	Flip 0
Until KeyHit(key_escape)


</pre><br>You can see a blue Rect, but if you use the TEXT command instead of the DRAWTEXT command the box will be black. A possible solution would be to use the DRAWTEXT command. Is this another error in the MINIB3D?<br><br><br><br><b>Next Problem: Performance Problems</b><br><br>I have heavy performance problems with the FLIP command. Already in this simple sample (first post) the FLIP need 15 msec and in the WIN-MINIB3D version of my "Catching Scale Game" (http://www.midimaster.de/download/tonleiter_demo.exe) the combination of RenderWorld(), some 2D Graphics in Front of the 3D-world and the FLIP eats more than 45 msec. Is this normal? The WinXP-B3D version or the OsX-MiniB3D dont need such a lot of time!<br><br>to test this behavior, replace the code:<br><pre class=code>SuperStrict
'Import sidesign.minib3d
Import "minib3dsf/minib3d.bmx"

Graphics3d 1024,768,0,2


Global t:TMesh=CreateCube()
Global C:TCamera=CreateCamera()
ambientlight 255,255,55
moveentity c,0,2,-5
Global sky:TMesh = LoadSkyBox()
	

Global TimeStamp%, V1%,v2%,v3%

Repeat
	Local TimeStamp%=MilliSecs()
	moveentity c,0.01,0,0.01
	v1= (MilliSecs()-TimeStamp)
	Renderworld()
	v2= (MilliSecs()-TimeStamp)
	Flip 0
	v3= (MilliSecs()-TimeStamp)
	Print v1+ " " + v2 + " " + v3
Until KeyHit(key_escape)



Function LoadSkyBox:TMesh()
	Local m:TMesh = CreateMesh(), S:TSurface, B:TBrush
	'front face:
        .....
        .....
</pre> <br><br></td></tr></table><br>
<a name="1032494"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> The text problem, just don't put the Text inside "BeginMax2D" and "EndMax2D" and it works - just like regular B3D. Begin/End Max2D is only needed when using plaing Max2D code.<br><br>As for performance 'problems', I tried your code, with my fps count function:<br><br><pre class=code>
Global fps:Float, fpst:Float=MilliSecs(),fpsc:Float

Function CountFPS:Float()
	If MilliSecs()-fpst&gt;=1000 Then
	   fps=fpsc+1
	   fpsc=0
	   fpst=MilliSecs()
	Else
	   fpsc = fpsc + 1
	End If

	Return fps
End Function
</pre><br><br>Just add a Text 0,0,CountFPS() before the flip... and here it was running at 130+ fps, on a laptop radeon X1300, so no performance problems here for me. <br><br></td></tr></table><br>
<a name="1032542"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you SLotman,<br><br>this was very helpful I did not know this. On my MacOsX the TEXT command seems to work also within the MAX2D-area.<br><br>Now I replaced it with DRAWTEXT and it works.<br><br>The performance problems can be a result of my old graphic card. In avarage I also have 100+fps, but I can observe moments where one RenderWorld() needs 185 msec.  I will test it on several computers and call back....<br><br>I had to separate all movings into a timer-based function call to make it independent from the Render performance problems. That seems to be a acceptable compromise.<br><br><br><b>Transparency problem</b><br><br>I still have a problem with a BMP-picture  of a shoot, which has a black (transparent) area around the ball. It looks transparent on B3D and MINIB3D-OSX, but not under WIN-MINIB3D. Here I can see a white square. But no problem: After converting it to PNG it works!<br><br><br><br><b>CameraFog Problem</b><br>The CameraFog seems to bring different results on OSX-MINIB3D and WIN-MINIB3D. But also no problem. After adjusting it, it looks good.<br><br><br>Now the game works and I can test it on a WIN7-64-System.<br><br><br>[EDIT ]<br>It seem to work on WIN7-64-Home Premium too! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
