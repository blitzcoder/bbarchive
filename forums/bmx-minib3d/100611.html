<!DOCTYPE html><html lang="en" ><head ><title >face target</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >face target</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=119" >MiniB3D Module</a>/<a href="#bottom" >face target</a><br><br>
<a name="1189286"></a>

<a name="1189288"></a>

<a name="1189289"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >redmoth</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi<br><br>I'm trying to learn minib3d (inside Monkey) by trying to make a very basic on rails shooter.  As you see in the photo below, I have a target (it will become the cross-hair, to where the player aims and shoots) and a ship:<br><br><img src="http://i.imgur.com/zODKflo.png"><br><br>The ship and the target move forward at a steady rate and I can move the target.<br><pre class=code>
'move ship mesh entity (mShip) and target entity (mTarget)
mShip.z+=Speed
mTarget.z+=Speed

'move the target left or right, but keep it in the window view
If KeyDown(KEY_LEFT) Or DirectionInput = 1 Then 
	If targetX&gt;= -40 Then targetX -=1
	
	
Else If KeyDown(KEY_RIGHT)Or DirectionInput = 2 Then
	If targetX&lt;= 40 Then targetX +=1

End
</pre><br><br>But what I'm having problems with, is making the ship face the target.  I've had a look in the blitz3D docs, but don't understand correctly what I should be doing.  <br><br><pre class=code>
dx = targetX - x
dy = targetY - y
dz = targetZ - z
AlignToVector( mShip, dx, dy, dz, 3, 0.5)
mShip.TurnEntity(0,dy,dz)
</pre><br><br><br>Any help would be appreciated. <br><br></td></tr></table><br>
<a name="1189296"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >redmoth</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, I've been looking through the blitz3D forums and I've tried this: <br>mShip.TurnEntity(0,(DeltaYaw(mShip,mTarget)*2),0)<br><br>but, the ship is facing backwards.  :-( <br><br></td></tr></table><br>
<a name="1189304"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> 1.<br>better you move the objects by using<br><br><pre class=code>objectxxx.MoveEntity x,y,z
or 
objectxxx.TurnEntity x,y,z</pre><br><br>instead of direct acces to the coordinates.<br><br><br>2.<br>all object hav a "inner" coordinate system, where z is the always the "ahead" direction, so ...<br><pre class=code>mShip.mMoveEntity 0,0,1</pre> <br>...always moves the ship "forward".<br><br>If in such a condition your ship does look like not moving forward, your 3D modell has a wrong orientation. You should turn it in the 3D construction tool. <br><br></td></tr></table><br>
<a name="1189319"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >redmoth</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you.<br><br>I've updated my code to use MoveEntity instead of the coordinates.<br><br>And with a little tinkering with DeltaYaw and DeltaPitch, the ship doesn't look to bad how it moves to look at the target box. <br><br></td></tr></table><br>
<a name="1189776"></a>

<a name="1189777"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >redmoth</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> The code works. <br>but I have the problem that if I mode the target to the side,s the ship goes too far.  This means that the ship is closer to the side of the screen than the target, so it's always pointing inwards, so I can't shoot towards the sides.<br>So I'm trying to get the ship to look at the target without moving it, so I can move it independently and that the ship to always be closer to the center of the screen than the target.<br><pre class=code>
mTarget1.MoveEntity(0,0,moveSpeed)
mTarget1.MoveEntity(0,0,moveSpeed)
PointEntity(mShip,mTarget2)

If KeyDown(KEY_LEFT) '
		If EntityX(mTarget1)&gt;= -30 Then  'keep the target in window
		mTarget1.MoveEntity(-3,0,0)
Else If KeyDown(KEY_LEFT)
...
</pre><br><br>I've tried limiting the ships movement in the KeyDown functions, but to no avail.<br><br>The problem is shown in the screen below, where I've moved the target to the left, but now the ship is futher left than the target.<br><img src="http://i.imgur.com/D9gY6jr.png"><br><br>Any help greatly appreciated <br><br></td></tr></table><br>
<a name="1189779"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ima747</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Are you sure the ship is actually further left than the target? I'm not sure exactly what you're trying to accomplish but those 3 points (2 targets and ship nose) are quite possibly in line, perspective is hard to gauge and the targets appear to be billboard sprites which makes it even harder to tell... Again, not sure exactly what you're trying to accomplish, it seems like you might want to adjust your camera's field of view to be wider, OR pull your camera further back... <br><br></td></tr></table><br>
<a name="1189794"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think, what you try to reach is, that the camera shall be connected to the ship. Shall the camera be always behind the ship?<br><br>In this case it would look like the ship is exactly on the line from camera to target.<br><br>A.)<br>Try to create the ship as a child of the camera:<br><pre class=code>
Ship=LoadMesh("blabla.b3d",camera)
MoveEntity Ship 0,0,5
</pre><br>Now you have to turn or move the camera towards the target. The ship will always be between camera and target.<br><br><br><br>B.)<br>Or the camera as a child of the ship:<br><pre class=code>
Ship=LoadMesh("blabla.b3d")
Camera=CreateCamera(Ship)
MoveEntity Camera 0,0,-5
</pre><br>Here you can still move and turn the ship. The camera will always be behind the ship<br><br><br>C.)<br>Or with a pivot:<br><pre class=code>
Mother=CreatePivot()
Camera=CreateCamera(Mother)
Ship=LoadMesh("blabla.b3d",Mother)
MoveEntity Ship 0,0,2
MoveEntity Camera 0,0,-2
</pre><br>Turn the ship, but move the Mother. This will look like target can get away to the sides of the screen. <br><br></td></tr></table><br>
<a name="1189805"></a>

<a name="1189807"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >redmoth</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for your suggestions and your time!<br><br>Well, I'm trying to create is a basic 'on rails' shooter (like star fox) so <br><br>1.the camera never moves along the x or y axis, and just goes forward in a straight line down the z axis. (works fine)<br><br>2. The target and ship can only move in the confines of the screen, which is this part ( the buttons control the screen, and the ship moves as it's always pointing in the direction of the taget):<br><br><pre class=code>
If KeyDown(KEY_LEFT) '
	If EntityX(mTarget1)&gt;= -30 Then  'keep the target in window
	mTarget1.MoveEntity(-3,0,0)
</pre><br><br>@ima747<br>yes they are in line perfectly, but that's the problem,  If I move the target to one of the sides, the ship goes off screen and so it lines itself up to the target and the lasers go towards the center of the screen.<br> I think Midimaster's 'c' suggestion will give me something to play around with, and I'll post my results for anyone else to see. <br><br></td></tr></table><br>
<a name="1189819"></a>

<a name="1189820"></a>

<a name="1189821"></a>

<a name="1189822"></a>

<a name="1189823"></a>

<a name="1189824"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi. What a cool subject. Star Fox 64 uses a complex navigation system, with a lot of smooth corrections.<br><br>First, let's see some reference (put it on 3:00):<br><br><iframe width="560" height="345" src="http://www.youtube.com/embed/ihQcCWXOVvs" frameborder="0" allowfullscreen></iframe><br><br>There are two navigation modes in the game. On-rail (navigating through a fixed path) and all-range (you fly wherever you want). Since you want to reproduce the on-rail, we'll deal with that.<br><br>Before writing this, I played the game. From my interpretation there are four elements responsible for the on-rail navigation, with the following order of importance:<br>1) An invisible 3D path.<br>2) A pivot moving along the path.<br>3) A camera.<br>4) The player ship.<br><br>How do these elements relate?<br>- The player ship roams "freely" in a 3D rectangular zone. If you keep moving your ship in a certain direction, it will eventually reach one of the sides of this rectangular zone and not move further. <br>This rectangular area and its orientation are derived from the main pivot traveling the path. You can use TFormPoint to check if the ship's location in relation to the main pivot has crossed the borders of this rectangular area that you define.<br>The ship's pivot-point (or mesh center) is located at its rear. When you move the ship, it tilts and starts going in the direction you want. If you let go, the ship smoothly <i>returns to alignment with the path</i>. This means, if you let go of the control, the ship will slowly turn parallel to the current path direction.<br>The target sights displayed on screen are derived from the ship's position and orientation based on a TForm and CameraProject. They're calculated <i>after</i> the ship moves, not the other way around.<br><br>- The camera does the exact same movement and orientation behavior as the ship described above, but in a <b>partial</b> manner.<br>This means if the ship moves sideways the distance 10, the camera will move someting like 5.<br>If the ship turns right 30 degrees, the camera turns right 15.<br>The camera <i>does not</i> point to the ship, otherwise the ship would always be centered on the screen. When the ship reaches the rectangular limit area described previously, the camera also stops moving (it doesn't "reach" to the ship, even from the ship being unable to move further from the limit).<br>The fact that the camera turns in the same direction as the ship is turning <b>drives</b> the view toward the obstacles the ship will encounter. This is an intuitive way to let the player see not only his ship but also the obstacles and enemies coming ahead.<br> <br>- Both the camera and ship are parented to the main pivot. This means the ship or camera don't move forward, only sideways and vertically - what drives them forward is the main pivot.<br>The main pivot keeps traveling and pointing along the path. If the path changes direction, the ship and camera will simply be smoothly displaced from the pivot turning to this new direction (they'll be dragged along because they're parented to the pivot).<br><br>This is all very complex because it's all very well designed (Nintendo!).<br>If you need help with code I can write some; It handles some pesky TForm commands to put it all in order. <br><br></td></tr></table><br>
<a name="1189827"></a>

<a name="1189828"></a>

<a name="1189829"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >redmoth</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks, I hadn't thought about it like that.  I've started from scratch trying to think about your advice.  A good excuse to play Star Fox 64, I have also played it, and have noticed how it slowly resets.<br><br>I think I can ignore '1' as I just want 1 path, and will obviously not attempt to make something as complex as Star Fox.  <br><br>I've been looking at TForm in the docs, but am finding it difficult getting my head around it, as before I was using a number such as 30 on each side to limit the movement, but obviously using TForm sounds much better. I've pasted my restarted Ship class below, but would appreciate help or even an explanation on how to implement the TForm.  <br><br><pre class=code>
Class Ship
	Field originX:Float = 0
	Field originY:Float = 70 'a decent amount above the ground
	Field originZ:Float =0
	Field mTarget1:TMesh  'the target sights
	
	Field mShip:TMesh	
	Field mainPivot:TPivot 'the main pivot as described
	
	Method New()
		mainPivot = CreatePivot()
		mainPivot.MoveEntity(0,originY,0)
		
		mTarget1 = LoadSprite("target.png",2,mainPivot)
		mTarget1.MoveEntity(0,0,+40)
		
		mShip=LoadMesh("ship1.b3d",mainPivot)
		mShip.MoveEntity(0,0,+10)
	End
	
	Method Update()
		mainPivot.MoveEntity(0,0,moveSpeed) 'move the main pivot along
	End
	
	Method Control(clicked:Bool,mX:Float,mY:Float)
		If KeyDown(KEY_LEFT) 
			mShip.MoveEntity(-1,0,0)
			mShip.TurnEntity(0,2,0)
		Else If KeyDown(KEY_RIGHT)
			mShip.MoveEntity(1,0,0)
			mShip.TurnEntity(0,-2,0)
		Else
		  'Here will be the reset code to move the ship
		  'slowly back to the center	
		End
	End

End


</pre> <br><br></td></tr></table><br>
<a name="1189830"></a>

<a name="1189831"></a>

<a name="1189833"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >redmoth</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think I missed adding a ship Pivot there, anyway I'll keep on trying.   I've been reading and searching these forums all day.  But worth it.  Even, though the game will probably turn out to be very basic, it will be a good learning experience. <br><br></td></tr></table><br>
<a name="1189834"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ima747</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Haven't played star fox in years but from memory the camera did move along X and Y, just not as far as the ship (as noted above). I think I recall that the camera also rotated to face roughly (but not exactly) towards the ship, allowing for yet more X/Y movement of the ship, while keeping the camera motion minimal. The ship (and by extension the camera since it was following the ships motions) was confined to an invisible "tunnel" along the rail flight path.<br><br>While the term on rails is appropriate for star fox like motion, it typically refers to game play where the player's character is total out of their control, with the exception of aiming (which is usually limited). Think any light gun game (anything in an arcade with a gun controller), that's a super limited rail shooter. Any game where you're in a turret and can't control your craft is also a rail shooter sequence... Just throwing that out there as it can give the wrong impression of what you're trying to accomplish.<br><br>A thought that might give you near what you want with relative ease: Player controls position of the ship, but the camera is actually fixed to point at the actual target reticle, rather than the ship itself. You may need to point at the mid reticle rather than the end reticle to keep the ship in view, or maybe even an invisible pivot along the line between the ship and the target, but fixing the camera to the flight path, and just pivoting it to look at an arbitrary point like that would be very easy (see PointEntity <a href="http://www.blitzbasic.com/b3ddocs/command.php?name=PointEntity&amp;ref=3d_cat)" target="_blank">http://www.blitzbasic.com/b3ddocs/command.php?name=PointEntity&amp;ref=3d_cat)</a> and probably give you a good enough experience to keep moving forward. Important to not get bogged down with getting something perfect when the game is super rough since if you ever finish it you *will* change literally everything anyway :) <br><br></td></tr></table><br>
<a name="1189861"></a>

<a name="1189862"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >redmoth</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks, I do agree that I'm starting to get bogged down in this, but I really want to have something basic that works first.<br><br>As shown in my code above (Post #5) I was using PointEntity, and that's where I was having the problems, such as the ship moving too far to the left.<br><br>The following code sort of works, although, I'm not sure about the best way to implement the 'tunnel', ie to limit the movement of the target and the ship, using the TForm commands.<br><br>And also I have another problem in that with every left/right/up/down movement, the ship starts to move forwards and get away from the camera, I've made a video of it to explain it.  I know that it's to do with the ships pivot, but am having problems working out the best solution, I've tried to limit the targets z movement, but that didn't work too well.<br><br><iframe width="560" height="345" src="http://www.youtube.com/embed/0N7-ViuhjGw" frameborder="0" allowfullscreen></iframe><br><br><br><pre class=code>
'in the main class code,
playerShip = New Ship()
Camera = CreateCamera(playerShip.mainPivot)
</pre><br><pre class=code>
Class Ship
	Field originX:Float = 0
	Field originY:Float = 70 'a decent amount above the ground
	Field originZ:Float =0
	Field mTarget1:TMesh
	Field mTarget2:TMesh
	
	Field mShip:TMesh	
	Field mainPivot:TPivot 'the pivot which moves the camera and everything along the z axis
	
	Method New()
		originY = 70
		
		mainPivot = CreatePivot()
		mainPivot.MoveEntity(0,originY,0)
		
		'create the target cross hair
		mTarget1 = LoadSprite("target.png",2,mainPivot)
		mTarget1.MoveEntity(0,0,+40)
		
		mShip=LoadMesh("ship1_b3d_base64.txt",mainPivot)
		mShip.MoveEntity(0,0,+10)
		
	
	End
	
	Method Update()
	
		mainPivot.MoveEntity(0,0,moveSpeed)
		'mTarget1.TFormPoint(0,0,-20,mTarget1,mShip)
		'mShip.PositionEntity(TFormedX(),TFormedY(),TFormedZ())
		PointEntity(mShip,mTarget1)
		
	End
	
	Method Control(clicked:Bool,mX:Float,mY:Float)
		If KeyDown(KEY_LEFT) 
			mTarget1.MoveEntity(-4,0,0)
			mShip.MoveEntity(-1,0,0)
			'mShip.
		Else If KeyDown(KEY_RIGHT)
			mTarget1.MoveEntity(4,0,0)
			mShip.MoveEntity(1,0,0)
		Else
		End
		
		If KeyDown(KEY_UP) 
			mTarget1.MoveEntity(0,4,0)
			mShip.MoveEntity(0,1,0)
			'mShip.
		Else If KeyDown(KEY_DOWN)
			mTarget1.MoveEntity(0,-4,0)
			mShip.MoveEntity(0,-1,0)
		Else
			
		End
	End
End
</pre><br><br><br>PS @ima747<br>Thanks for your suggestions. <br>I do admit that maybe I should of added the fact that the camera in my game only moves down the z axis and doesn't turn or move along the x or y axis.  I do know what 'on rails' games are.  Star fox is considered a 'rail shooter' and since what I'm trying to achieve here is even more restricted I think it's fine.  We could continue to be pedantic about this, however it really doesn't matter. <br><br></td></tr></table><br>
<a name="1189928"></a>

<a name="1189929"></a>

<a name="1189930"></a>

<a name="1189931"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi, I did some code. I used Blitz3D though.<br><br>1) If you don't have Blitz3D, download the demo <a href="/file/get.php?file=/Products/demos/Blitz3DDemo183.exe" target="_blank">from here</a>.<br><br>2) Open <a href="http://www.youtube.com/watch?v=6gUe99u11bw" target="_blank">this youtube video</a> and let it running in the background for some tunes.<br><br>3) Paste and run the B3D code below.<br>You can also download the compiled executable from here (<a href="http://www.mediafire.com/?jg3akhm678zvm3c" target="_blank">MediaFire</a>). This way you don't need Blitz3D to run it.<br><br><br><img src="http://i39.tinypic.com/fcismh.png"><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">; Star Fox 64-like Navigation &amp; Steering.
;
; By Kryzon, 2013.
;


;CONSTANTS

;Level.
Const LEVEL_Z_SPEED#	=	0.3		;How fast the level progresses.
Const GROUND_Y_POS#		=	-17		;Vertical position of the ground.

;Player.
;The priority with the following values is making the game feel natural and look its best.
;They were all figured from testing and tweaking.
Const PLAYER_Z_OFFSET#		=	9.5		;How far (in depth) the player is away from the camera. Important for gameplay.
Const PLAYER_MOVE_SPEED#	=	0.75	;The top velocity of the player. Important for gameplay.

Const PLAYER_TURN_FACTOR#	=	0.05	;How fast the player reaches top velocity.
Const PLAYER_MAX_TURN#		=	30.0	;The total amount of degrees the player is allowed to rotate horizontally or vertically. 
Const PLAYER_ROLL_FACTOR#	=	1.5		;Cosmetic, just to give a certain amount of "banking" (rolling when changing direction).

Const SHOT_SPEED#	=	4.0				;Speed of each shot. Important for gameplay.
Const PLAYER_SIGHT_DISTANCE_1#	=	20	;Distance from the tip of the ship, in 3D units, that the first sight will be positioned.
Const PLAYER_SIGHT_DISTANCE_2#	=	70	;Distance from the tip of the ship, in 3D units, that the second sight will be positioned.

;Camera.
Const CAMERA_MOVEMENT_FACTOR#	=	0.5		;The percentage of player movement that the camera will perform. If it's 1.0, the camera will move just as much as the player.
Const CAMERA_ROTATION_FACTOR#	=	0.35	;The percentage of player rotation that the camera will perform. Try with 1.0 and see how exaggerated it is.

;The 3D rectangular limits should preferably follow the screen's ratio. 
;In this Case, 800x600 = 4:3. But the priority is making it look good and feel natural
Const PLAYER_X_RANGE#	=	23	;Horizontal range.
Const PLAYER_Y_RANGE#	=	15	;Vertical range.


;GRAPHICS

AppTitle "StarFox 64 style Navigation &amp; Steering"
Graphics3D 800,600,0,2
SeedRnd MilliSecs()
Global fpsTimer = CreateTimer( 60 )
TFormFilter False ;Important for the sights.


;MAIN ENTITIES

Global mainPivot	=	CreatePivot()
;Main pivot, the one that drives the camera and the ship forward. EDIT: this concept has now been scratched.
;If you actually moved with the main pivot, you'd eventually go so far from the world's origin as to make the coordinates wobble
;and become imprecise, from reaching the floating point precision limits. It's better having the entire game coming toward you 
;(that is, the environment, enemies etc.) with the player staying in the same spot. The look of it is absolutely the same.
;The pivot still remains as a practical way to transform the ship and camera to a new orientation, should the path change direction.

;Game camera, the one that displays the game. It's parented to 'mainPivot'.
Global gameCamera	=	CreateCamera( mainPivot )
						CameraRange( gameCamera, 1, 200 )
						CameraClsColor( gameCamera, 30, 50, 100 )
						CameraFogMode( gameCamera, 1 )
						CameraFogRange( gameCamera, 15, 60 )
						CameraFogColor( gameCamera, 30, 50, 100 )

;Player ship. It's parented to 'mainPivot'.
Global playerShip	=	CreateCone( 10, True, mainPivot )
						RotateMesh( playerShip, 90, 0, 0 )
						ScaleMesh( playerShip, 1, 0.5, 2 )
						PositionMesh( playerShip, 0, 0, 2 )
						EntityColor( playerShip, 210, 10, 70 )
Global playerPitch#		=	0
Global playerYaw#		=	0
Global playerRoll#		=	0

Global playerX#			=	0	;The player is handled by absolute coordinates.
Global playerY#			=	0

Global gamePadX#		=	1.0	;These Game Pad variables are here to exemplify how you'd implement game pads with analog the movement
Global gamePadY#		=	1.0	;sticks in the movement math. They would allow for much smoother and precise control than a keyboard.

Local wings	=	CreateCube( playerShip )
				PositionMesh( wings, 0, 0, 1.01 )
				ScaleMesh( wings, 2.2, 0.15, 0.4 )
				EntityColor( wings, 170, 170, 170 )
Local shadow	=	CreateCylinder()
					ScaleMesh( shadow, 3, 0.1, 3 )
					PositionMesh( shadow, 0, 0, 0.2 )
					EntityColor( shadow, 160, 160, 160 )
					EntityBlend( shadow, 2 )
					EntityFX( shadow, 1 + 8 )
Local headLights	=	CreateLight( 2, playerShip )
						LightRange( headLights, 3 )
						PositionEntity( headLights, 0, 0, 5, False ) 					
Local sight1		=	CreateTargetSight( 1.5 )	;The sights are images. You can use pixel-perfect sprites (like Max2D or Mojo images)
Local sight2		=	CreateTargetSight( 1.0 )	;to get smooth alpha transparency and fractional sizes.

Type Shot
	Field mesh
	Field vecX#, vecY#, vecZ#
End Type


;LEVEL

Type Block
	Field mesh 
	Field x#, y#, z#
	Field halfMeshDepth#
End Type

Type LevelSettings
	Field amount
	Field xMaxScale#
	Field yMaxScale#
	Field zMaxScale
	Field zOffset
	
	Field groundTexture	;The ground isn't moving. Rather its texture is sliding and giving the illusion.
	Field vPos#
End Type

Global levelSets.LevelSettings = New LevelSettings
levelSets\amount	=	30
levelSets\xMaxScale	=	10
levelSets\yMaxScale	=	3
levelSets\zMaxScale	=	20
levelSets\zOffset	=	60

CreateLevel()

AmbientLight 40,60,90 
Local sunLight	=	CreateLight( 1 )
					TurnEntity( sunLight, 30, 50, 0 )


;MAIN LOOP

Local kLeft, kRight, kUp, kDown, kSpace
Local pX#, pY#, pZ#
SetBuffer BackBuffer()

While Not ( KeyHit( 1 ) Or ( KeyDown(184) Or KeyDown( 56 ) And KeyDown( 62 ) ) )
	WaitTimer( fpsTimer )
	
	;INPUT
	
	kLeft	=	KeyDown( 30 ) Or KeyDown( 203 )
	kRight	=	KeyDown( 32 ) Or KeyDown( 205 )
	kUp		=	KeyDown( 17 ) Or KeyDown( 200 )
	kDown	=	KeyDown( 31 ) Or KeyDown( 208 )
	kSpace	=	KeyHit( 57 ) Or KeyHit( 29 ) Or KeyHit( 157 ) Or KeyHit( 42 ) Or KeyHit( 54 ) Or KeyHit( 28 )
	
	
	;ORIENTATION
	
	;The smooth factor is achieved by multiplying a difference by a scaling factor -&gt; (DestValue - SrcValue)*factor
	;This is what causes the smooth response.
	
	;Horizontal orientation.
	If kLeft And ( Not kRight ) Then playerYaw = playerYaw + ( PLAYER_MAX_TURN - playerYaw )*PLAYER_TURN_FACTOR * GamePadX
	If kRight And ( Not kLeft ) Then playerYaw = playerYaw - ( PLAYER_MAX_TURN + playerYaw )*PLAYER_TURN_FACTOR * GamePadX
	
	If ( kLeft + kRight ) &lt;&gt; 1 Then playerYaw = playerYaw - ( playerYaw*PLAYER_TURN_FACTOR )
	
	;Vertical orientation.
	If kUp And ( Not kDown ) Then playerPitch = playerPitch + ( PLAYER_MAX_TURN - playerPitch )*PLAYER_TURN_FACTOR * GamePadY
	If kDown And ( Not kUp ) Then playerPitch = playerPitch - ( PLAYER_MAX_TURN + playerPitch )*PLAYER_TURN_FACTOR * GamePadY
	
	If ( kUp + kDown ) &lt;&gt; 1 Then playerPitch = playerPitch - ( playerPitch*PLAYER_TURN_FACTOR )
		
		
	;POSITION
	
	;Project the 'forward vector' from the player ship's space to the space of its parent pivot.
	;Then the X component of the resulting vector is the horizontal amount that the player ship has to be translated by,
	;and the Y component is the vertical amount.
	TFormNormal( 0, 0, 1, playerShip, mainPivot ) 
		
	;Use the X component of the normal, scaled by the desired top speed, to move the ship horizontally.
	playerX = playerX + ( TFormedX() * PLAYER_MOVE_SPEED )
	If playerX &gt; PLAYER_X_RANGE Then playerX = PLAYER_X_RANGE ;Impose the horizontal limits of the 3D rectangle.
	If playerX &lt; -PLAYER_X_RANGE Then playerX = -PLAYER_X_RANGE
	
	;Use the Y component of the normal, scaled by the desired speed, to move the ship vertically. 
	playerY = playerY + ( TFormedY() * PLAYER_MOVE_SPEED )
	If playerY &gt; PLAYER_Y_RANGE Then playerY = PLAYER_Y_RANGE ;Impose the vertical limits of the 3D rectangle.
	If playerY &lt; -PLAYER_Y_RANGE Then playerY = -PLAYER_Y_RANGE
	
	
	;APPLYING TRANSFORMATIONS
	;Note the 'False' argument at the end of the function calls. This means these transformations are relative
	;to the entities' parent, which is 'mainPivot'. This means 'mainPivot' is considered as the origin instead of the
	;world origin, and thus the pivot can move around, rotate etc. and the ship and camera will be dragged along while
	;still behaving the same.
	;You can see how simple it is to position the camera. It's the ship's position and rotation values 'scaled' by a percentage.
	
	;Updating rotation.
	;The roll (to simulate "banking") is performed by applying the 'playerYaw' value to the ship's roll axis.
	RotateEntity( playerShip, playerPitch, playerYaw, playerYaw*PLAYER_ROLL_FACTOR, False )
	RotateEntity( gameCamera, playerPitch*CAMERA_ROTATION_FACTOR, playerYaw*CAMERA_ROTATION_FACTOR, 0, False ) ;No roll on the camera, else it's nauseating.	
	
	;Updating position.
	;The camera is positioned at 'zero' Z so it's in the same depth as 'mainPivot'.
	PositionEntity( playerShip, playerX, playerY, PLAYER_Z_OFFSET, False )
	PositionEntity( gameCamera, playerX*CAMERA_MOVEMENT_FACTOR, playerY*CAMERA_MOVEMENT_FACTOR, 0, False )
	
	;Having absolute coordinates for the player makes it easy to align such things as a shadow.
	PositionEntity( shadow, playerX, GROUND_Y_POS, PLAYER_Z_OFFSET, False ) ;Purely cosmetic, but it looks nice when you're close to the ground.	
	
	
	;3D RENDER
	
	UpdateLevel()
	UpdateShots()
	UpdateWorld()
	RenderWorld()
	
	
	;2D RENDER
	
	Color 255,255,255
	Text 10,10,"Use the ARROW KEYS or A,W,S,D to steer. Press SPACE or CTRL to fire."
	
	TFormVector( 0, 0, 1, playerShip, 0 ) ;Get the direction the player ship is pointing in world-space.
	pX	=	EntityX( playerShip, True )
	pY	=	EntityY( playerShip, True )
	pZ	=	EntityZ( playerShip, True )
	
	CameraProject( gameCamera, pX + TFormedX()*PLAYER_SIGHT_DISTANCE_1, pY + TFormedY()*PLAYER_SIGHT_DISTANCE_1, pZ + TFormedZ()*PLAYER_SIGHT_DISTANCE_1 )
	DrawImage( sight1, ProjectedX(), ProjectedY() )
	CameraProject( gameCamera, pX + TFormedX()*PLAYER_SIGHT_DISTANCE_2, pY + TFormedY()*PLAYER_SIGHT_DISTANCE_2, pZ + TFormedZ()*PLAYER_SIGHT_DISTANCE_2 )
	DrawImage( sight2, ProjectedX(), ProjectedY() )
	
	
	;SHOTS
	If kSpace Then CreateShot( pX, pY, pZ )

	Flip	
Wend 


;Populates the world with with objects.
Function CreateLevel()

	;Blocks.
	Local tempAmount = levelSets\amount
	
	While ( tempAmount &gt; 0 )
		b.Block = New Block
		b\mesh	=	CreateCube()
					EntityColor( b\mesh, 220, 170, 70 )
					ScaleMesh( b\mesh, Rand( levelSets\xMaxScale ), Rand( levelSets\yMaxScale ), Rand( levelSets\zMaxScale ) )	
					PositionEntity( b\mesh, Rand( -levelSets\xMaxScale, levelSets\xMaxScale ), Rand( -levelSets\yMaxScale, levelSets\yMaxScale ), tempAmount * MeshDepth( b\mesh )*1.5 + levelSets\zOffset )
		b\x = EntityX( b\mesh, True )
		b\y = EntityY( b\mesh, True )
		b\z = EntityZ( b\mesh, True )
		b\halfMeshDepth = MeshDepth( b\mesh ) / 2
					
		tempAmount = tempAmount - 1
	Wend
	
	;Ground.
	Local ground	=	CreatePlane( 1 )
						PositionEntity( ground, 0, GROUND_Y_POS, 0 )
						EntityFX( ground, 1 )
						EntityColor( ground, 180, 200, 170 )
	Local tex	=	CreateTexture( 16, 16, 1 )
					SetBuffer TextureBuffer( tex )
						ClsColor( 180, 180, 180 )
						Cls
						Color( 255, 255, 255 )
						Rect( 2, 2, 14, 14, True )						
					SetBuffer BackBuffer()
					ScaleTexture( tex, 10, 10 )
	EntityTexture( ground, tex	)
	levelSets\groundTexture = tex
	  
End Function


;Moves the environment towards the player.
Function UpdateLevel()
	
	;Slide ground texture.
	levelSets\vPos = levelSets\vPos - ( LEVEL_Z_SPEED / 10.0 )
	PositionTexture( levelSets\groundTexture, 0, levelSets\vPos )
	
	;Update blocks.
	Local makeNew = False
	
	For b.Block = Each Block
		b\z = b\z - LEVEL_Z_SPEED
		PositionEntity( b\mesh, b\x, b\y, b\z, True )
		
		;If a block has passed by the player and won't be seen again, release it and create a new one.
		If b\z &lt; -b\halfMeshDepth Then 
			FreeEntity b\mesh
			Delete b
			makeNew = True	
		EndIf 
	Next
	
	If makeNew Then	
		b.Block = New Block
		b\mesh	=	CreateCube()
					EntityColor( b\mesh, 185, 150, 70 )
					ScaleMesh( b\mesh, Rand( levelSets\xMaxScale ), Rand( levelSets\yMaxScale ), Rand( levelSets\zMaxScale ) )	
					PositionEntity( b\mesh, Rand( -levelSets\xMaxScale, levelSets\xMaxScale ), Rand( -levelSets\yMaxScale, levelSets\yMaxScale ), levelSets\amount * MeshDepth( b\mesh )*1.5 + levelSets\zOffset )
		b\x = EntityX( b\mesh, True )
		b\y = EntityY( b\mesh, True )
		b\Z = EntityZ( b\mesh, True )
		b\halfMeshDepth = MeshDepth( b\mesh ) / 2
	EndIf 
End Function


Function CreateTargetSight( size#=1.0 )

	Local img
	Local pixel
	img	=	CreateImage( 32, 32 )
			SetBuffer ImageBuffer( img )
				ClsColor( 0, 0, 0 )
				Cls
				Color( 0, 255, 0 )
				Oval( 1, 1, 30, 30, True )
				Color( 0, 0, 0 )
				Oval( 4, 4, 24, 24, True )
				Rect( 0, 9, 32, 14, True )
			SetBuffer BackBuffer()
			MaskImage( img, 0, 0, 0 )
			MidHandle( img )
			If size &lt;&gt; 1.0 Then ScaleImage img, size, size
	Return img
End Function


Function CreateShot( x#, y#, z# ) ;This function assumes the shot's orientation vector is stored in TFormedX\Y\Z.
	Local zOffset# = 3.0
	Local s.Shot = New Shot
	s\mesh	=	CreateCone()
				RotateMesh( s\mesh, 90, 0, 0 )
				ScaleMesh( s\mesh, 0.2, 0.2, 3 )
				EntityColor( s\mesh, 255, 255, 0 )
				PositionEntity( s\mesh, x + TFormedX()*zOffset, y + TFormedY()*zOffset, z + TFormedZ()*zOffset, True ) 
				EntityFX( s\mesh, 1 + 8 )
				AlignToVector( s\mesh, TFormedX(), TFormedY(), TFormedZ(), 3 )
End Function


Function UpdateShots()
	For s.Shot = Each Shot
		MoveEntity( s\mesh, 0, 0, SHOT_SPEED# )
	
		If EntityDistance( s\mesh, gameCamera ) &gt; 100 Then
			FreeEntity s\mesh
			Delete s
		EndIf 
	Next
End Function
	

End</textarea><br><br>If any part is not clear, feel free to ask. It seems you have difficulty with TForm, so I'll explain how it works later on. <br>See ya. <br><br></td></tr></table><br>
<a name="1190074"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >redmoth</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> thanks, I've been converting the code to fit in with minib3d, which has helped me understand it more. I've also been playing around with it.   <br><br>Just a question, If I move all the enemies and world objects instead of just the player, won't that slow things down? <br><br></td></tr></table><br>
<a name="1190111"></a>

<a name="1190112"></a>

<a name="1190115"></a>

<a name="1190116"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> It would add just a little to the cost. It shouldn't be something you have to worry about.<br>Most of the time you'd be moving things such as enemy ships etc. so they're performing coreography (getting in formation, flying towards the player etc.) so you'd be calling transformation methods on them anyway - so you can sneak in the -Z offset in these calls.<br>The actual slow down will come from world objects that are "static" and wouldn't have needed these calls upon them if the player was moving.<br><br>To keep things as economical as possible you should do it like the code above: handle positions and rotation angles only by absolute numerical values. Then, before doing an UpdateWorld() or RenderWorld(), you can update each object with the following:<br><br>Ship.PositionEntity( Ship.X, Ship.Y, Ship.Z, False )<br>Ship.RotateEntity( Ship.Pitch, Ship.Yaw, Ship.Roll, False )<br><br>I just checked the mB3D source, and the 'False' arguments at the end of these functions ensures the quickest code is run.  <br><br>- - - - -<br>The benefits of moving the universe instead of the player in a game like StarFox 64 are far greater than their cost, in my opinion. <br>A game like this should be more easily handled if you're using this origin-centric architecture because you get the following:<br>- No problems with floating point precision.<br>- If an enemy or object passes through the origin (plus its mesh size), you can delete it.<br>- If you need an object to "syncronize" with the player (that is, travel at the same speed as), all you have to do is not move it. Since the player isn't moving and neither is the object, they'll appear to be moving together, at the same speed. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
