<!DOCTYPE html><html lang="en" ><head ><title >Simple pixel shaders</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Simple pixel shaders</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=119" >MiniB3D Module</a>/<a href="#bottom" >Simple pixel shaders</a><br><br>
<a name="1312848"></a>

<a name="1312849"></a>

<a name="1312850"></a>

<a name="1312851"></a>

<a name="1312968"></a>

<a name="1312972"></a>

<a name="1312973"></a>

<a name="1312974"></a>

<a name="1312975"></a>

<a name="1312976"></a>

<a name="1312985"></a>

<a name="1312992"></a>

<a name="1314060"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi all,<br><br>EDIT: I'm using this BMX-NG setup on Windows 7 64-bit, which includes OpenB3D/Wrapper: <a href="https://drive.google.com/folderview?id=0B_i4iys72wl6enNxQ3N4ZFBlVlk&amp;usp=sharing" target="_blank">BlitzMaxNG_077.7z on Google Drive [86.4 MB]</a>. In theory, you can extract anywhere and use it. In practice, however... ?<br><br><img src="http://www.hi-toro.com/blitz/images/greytoggle.jpg"><br><br>Just posting this in case it helps anyone else who wants to play with a simple pixel shader. I realise that some parts of the shader programs need future-proofing (help would be appreciated here!), but wanted to post a basic example to kick things off, since I've <i>finally</i> got it all working...<br><br><i>example.bmx</i><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

SuperStrict

Import Openb3d.B3dglgraphics

Graphics3D 1024, 768, 0, 2

	ClearTextureFilters

Local campivot:TPivot = CreatePivot ()

	' Pivot for cameras/full-screen sprite -- can't parent one camera to another!

Local cam:TCamera = CreateCamera (campivot)

	CameraClsColor cam, 64.0, 127.0, 255.0
	CameraRange cam, 0.5, 1000.0

	' Short camera range to pick up sprite located at z = 1.0
	
Local spritecam:TCamera = CreateCamera (campivot)

	CameraClsColor spritecam, 64.0, 127.0, 255.0
	CameraRange spritecam, 0.5, 1000.0
	
	' Short camera range to pick up sprite located at z = 1.0
	
Local sprite:TSprite = CreateSprite (campivot)

	' Full-screen sprite at z = 1.0...
	
	ScaleSprite sprite, 1.0, Float (GraphicsHeight ()) / Float (GraphicsWidth ())
	MoveEntity sprite, 0.0, 0.0, 1.0
	EntityOrder sprite, -1

	' Rendered in front of everything else via EntityOrder of -1
	
Local shadertex:TTexture = CreateTexture (GraphicsWidth (), GraphicsHeight (), 1 | 256)

	' Screen-size texture on which shader will operate...
	
	' From DOF example: "in GL 2.0 render textures need attached before other textures (EntityTexture)"
	
	ScaleTexture shadertex, 1.0, -1.0 ' Invert y for shader!
	EntityTexture sprite, shadertex

Local shader:TShader = LoadShader ("", "basic.vert.glsl", "basic.frag.glsl")

	' Used in vertex shader:
	
	UseMatrix shader, "matrix_projection", 2
	UseMatrix shader, "matrix_modelview", 3
	
	ShaderTexture shader, shadertex, "shadertex", 0 ' Last param not needed?
	ShadeEntity sprite, shader

Local msg:String = "Use A/Z and cursors to move; SPACE to toggle greyscale..."

' Toggle used in shader...

Local toggle:Float = 0.0

	UseFloat shader, "toggle", toggle

' Display size used in shader...

Local display_width:Float = Float (GraphicsWidth ())
Local display_height:Float = Float (GraphicsHeight ())

	UseFloat2 shader, "display", display_width, display_height

' Set up light/world centre pivot...

InitLight

' Buncha spinning cubes...
	
Global CubeList:TList = CreateCubes ()

Repeat

	If KeyHit (KEY_SPACE)
		toggle = 1.0 - toggle
		UseFloat shader, "toggle", toggle
	EndIf

	If KeyDown (KEY_A)
		MoveEntity campivot, 0.0, 0.0, 1.0
	Else
		If KeyDown (KEY_Z)
			MoveEntity campivot, 0.0, 0.0, -1.0
		EndIf
	EndIf
	
	If KeyDown (KEY_LEFT)
		TurnEntity campivot, 0.0, 1.0, 0.0, True
	Else
		If KeyDown (KEY_RIGHT)
			TurnEntity campivot, 0.0, -1.0, 0.0, True
		EndIf
	EndIf

	If KeyDown (KEY_UP)
		TurnEntity campivot, 1.0, 0.0, 0.0
	Else
		If KeyDown (KEY_DOWN)
			TurnEntity campivot, -1.0, 0.0, 0.0
		EndIf
	EndIf
	
	Cls

	' Render pass 1:
	
	HideEntity spritecam				' Hide the sprite camera
	HideEntity sprite					' Hide the sprite
	ShowEntity cam						' Use main camera to film scene
	
	SpinCubes
	
	CameraToTex shadertex, cam			' Render to shader texture

	' Render pass 2:
	
	HideEntity cam						' Hide main camera
	ShowEntity sprite					' Show sprite
	ShowEntity spritecam				' Use sprite camera to film sprite

	RenderWorld							' Render scene showing shaded sprite only

	BeginMax2D
		SetColor 0, 0, 0
		DrawText msg, 20.0, 20.0
		SetColor 255, 255, 255
		DrawText msg, 21.0, 21.0
	EndMax2D
	
	Flip
	
Until KeyHit (KEY_ESCAPE)

End



' --
' Light/pivot only...

Function InitLight:Int ()

	Local pivot:TPivot = CreatePivot ()		' World centre
	Local light:TLight = CreateLight (1)
	
	MoveEntity light, -5.0, 5.0, -2.0
	PointEntity light, pivot

End Function

' --
' Cube stuff only...

Type Cube
	Field entity:TEntity
	Field pitch:Float
	Field yaw:Float
	Field roll:Float
End Type

Function CreateCubes:TList ()

	SeedRnd MilliSecs ()
	
	Local cubes:TList = CreateList ()
	
	Local protocube:TEntity = CreateCube ()
	
	For Local loop:Int = 0 Until 1000
	
		Local temp:Cube = New Cube
		
		temp.entity = CopyEntity (protocube)
		
		Local scale:Float = Rnd (0.25, 2.0)
		ScaleEntity temp.entity, scale, scale, scale
		
		EntityColor temp.entity, Float (Rnd (128.0, 255.0)), Float (Rnd (128.0, 255.0)), Float (Rnd (128.0, 255.0))
		
		PositionEntity temp.entity, Float (Rnd (-100.0, 100.0)), Float (Rnd (-100.0, 100.0)), Float (Rnd (4.0, 100.0))
		
		temp.pitch = Float (Rnd (-3.0, 3.0))
		temp.yaw = Float (Rnd (-3.0, 3.0))
		temp.roll = Float (Rnd (-3.0, 3.0))
		
		ListAddLast cubes, temp
	
	Next
	
	FreeEntity protocube

	Return cubes
	
End Function

Function SpinCubes:Int ()

	For Local temp:Cube = EachIn CubeList
		TurnEntity temp.entity, temp.pitch, temp.yaw, temp.roll
	Next

End Function

</textarea><br><br>Vertex shader: <i>basic.vert.glsl</i><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

uniform mat4 matrix_projection;
uniform mat4 matrix_modelview;

void main()
{
	gl_Position = matrix_projection * matrix_modelview * gl_Vertex;
}

</textarea><br><br>Fragment (pixel) shader: <i>basic.frag.glsl</i><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

uniform sampler2D shadertex;
uniform float toggle;
uniform vec2 display;

void main()
{
	float x = gl_FragCoord.x / display.x;
	float y = gl_FragCoord.y / display.y;

	vec4 pixel = texture2D(shadertex, vec2(x, y));

	if (toggle)
	{
		pixel.r = pixel.r;
		pixel.g = pixel.r; // Crap greyscale
		pixel.b = pixel.r;
	}
	else
	{
		pixel.r = pixel.r;
		pixel.g = pixel.g;
		pixel.b = pixel.b;
	}
	
	pixel.a = 1.0;
	
	gl_FragColor = pixel;
}

</textarea><br><br>Save all three files in the same folder and run the example. To play with the pixel colours, simply alter the values of pixel.r, pixel.g and pixel.b (each value ranging from 0.0 - 1.0 instead of 0 - 255) in basic.frag.glsl, as in...<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

uniform sampler2D shadertex;
uniform float toggle;
uniform vec2 display;

void main()
{
	float x = gl_FragCoord.x / display.x;
	float y = gl_FragCoord.y / display.y;

	vec4 pixel = texture2D(shadertex, vec2(x, y));

	// Lower red/green values to quarter/half respectively...

	pixel.r = pixel.r * 0.25;
	pixel.g = pixel.g * 0.5;
	pixel.b = pixel.b * 1.0;
	
	pixel.a = 1.0;
	
	gl_FragColor = pixel;
}

</textarea><br><br>Onwards to ZX Spectrum shaders, DOF and all the other toys I wanted to play with! OpenB3D and Wrapper are awesome! <br><br></td></tr></table><br>
<a name="1312969"></a>

<a name="1312980"></a>

<a name="1312988"></a>

<a name="1312989"></a>

<a name="1312990"></a>

<a name="1314061"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's a set of 'proper' greyscale/grayscale algorithms (normal RGB, lightness, average, luminosity) that you can switch between via the keyboard -- the above version just used the red value for r, g and b:<br><br><img src="http://www.hi-toro.com/blitz/images/greys.jpg"><br><br><i>example.bmx</i>:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

SuperStrict

Import Openb3d.B3dglgraphics

Graphics3D 1024, 768, 0, 2

	ClearTextureFilters

Const CHECK_SHADER:Int = False

	' Enable for shader compilation output:
	
	If CHECK_SHADER
	
		Delay 500 ' Aesthetics, man!
		EndGraphics
		
		' Test shader compilation (see Output tab in MaxIDE):
		
		Local testshader:TGLShader = New TGLShader
		testshader.InitShaders ("grey.vert.glsl", "grey.frag.glsl")
		End
	
	EndIf

Local campivot:TPivot = CreatePivot ()

	' Pivot for cameras/full-screen sprite -- can't parent one camera to another!

Local cam:TCamera = CreateCamera (campivot)

	CameraClsColor cam, 64.0, 127.0, 255.0
	CameraRange cam, 0.5, 1000.0

	' Short camera range to pick up sprite located at z = 1.0
	
Local spritecam:TCamera = CreateCamera (campivot)

	CameraClsColor spritecam, 64.0, 127.0, 255.0
	CameraRange spritecam, 0.5, 1000.0
	
	' Short camera range to pick up sprite located at z = 1.0
	
Local sprite:TSprite = CreateSprite (campivot)

	' Full-screen sprite at z = 1.0...
	
	ScaleSprite sprite, 1.0, Float (GraphicsHeight ()) / Float (GraphicsWidth ())
	MoveEntity sprite, 0.0, 0.0, 1.0
	EntityOrder sprite, -1

	' Rendered in front of everything else via EntityOrder of -1
	
Local shadertex:TTexture = CreateTexture (GraphicsWidth (), GraphicsHeight (), 1 | 256)

	' Screen-size texture on which shader will operate...
	
	' From DOF example: "in GL 2.0 render textures need attached before other textures (EntityTexture)"
	
	ScaleTexture shadertex, 1.0, -1.0 ' Invert y for shader!
	EntityTexture sprite, shadertex

Local shader:TShader = LoadShader ("", "grey.vert.glsl", "grey.frag.glsl")

	' Used in vertex shader:
	
	UseMatrix shader, "matrix_projection", 2
	UseMatrix shader, "matrix_modelview", 3
	
	ShaderTexture shader, shadertex, "shadertex", 0 ' Last param not needed?
	ShadeEntity sprite, shader

Local msg:String = "Use A/Z and cursors to move; -/+ to change mode"

Local modename:String [] = ["Normal", "Lightness", "Average", "Luminosity"]

' Mode used in shader...

Local mode:Float = 0.0

	UseFloat shader, "mode", mode

' Display size used in shader...

Local display_width:Float = Float (GraphicsWidth ())
Local display_height:Float = Float (GraphicsHeight ())

	UseFloat2 shader, "display", display_width, display_height

' Set up light/world centre pivot...

InitLight

' Buncha spinning cubes...
	
Global CubeList:TList = CreateCubes ()

Repeat

	If KeyHit (KEY_MINUS) Or KeyHit (KEY_NUMSUBTRACT)
		mode = mode - 1.0; If mode &lt; 0.0 Then mode = 3.0
		UseFloat shader, "mode", mode
	EndIf

	If KeyHit (KEY_EQUALS) Or KeyHit (KEY_NUMADD)
		mode = mode + 1.0; If mode &gt; 3.0 Then mode = 0.0
		UseFloat shader, "mode", mode
	EndIf

	If KeyDown (KEY_A)
		MoveEntity campivot, 0.0, 0.0, 1.0
	Else
		If KeyDown (KEY_Z)
			MoveEntity campivot, 0.0, 0.0, -1.0
		EndIf
	EndIf
	
	If KeyDown (KEY_LEFT)
		TurnEntity campivot, 0.0, 1.0, 0.0, True
	Else
		If KeyDown (KEY_RIGHT)
			TurnEntity campivot, 0.0, -1.0, 0.0, True
		EndIf
	EndIf

	If KeyDown (KEY_UP)
		TurnEntity campivot, 1.0, 0.0, 0.0
	Else
		If KeyDown (KEY_DOWN)
			TurnEntity campivot, -1.0, 0.0, 0.0
		EndIf
	EndIf
	
	Cls

	' Render pass 1:
	
	HideEntity spritecam				' Hide the sprite camera
	HideEntity sprite					' Hide the sprite
	ShowEntity cam						' Use main camera to film scene
	
	SpinCubes
	
	CameraToTex shadertex, cam			' Render to shader texture

	' Render pass 2:
	
	HideEntity cam						' Hide main camera
	ShowEntity sprite					' Show sprite
	ShowEntity spritecam				' Use sprite camera to film sprite

	RenderWorld							' Render scene showing shaded sprite only

	BeginMax2D
	
		SetColor 0, 0, 0
		DrawText msg, 20.0, 20.0
		DrawText "Current mode: " + modename[Int (mode)], 20.0, 40.0
		
		SetColor 255, 255, 255
		DrawText msg, 21.0, 21.0
		DrawText "Current mode: " + modename[Int (mode)], 21.0, 41.0
				
	EndMax2D
	
	Flip
	
Until KeyHit (KEY_ESCAPE)

End



' --
' Light/pivot only...

Function InitLight:Int ()

	Local pivot:TPivot = CreatePivot ()		' World centre
	Local light:TLight = CreateLight (1)
	
	MoveEntity light, -5.0, 5.0, -2.0
	PointEntity light, pivot

End Function

' --
' Cube stuff only...

Type Cube
	Field entity:TEntity
	Field pitch:Float
	Field yaw:Float
	Field roll:Float
End Type

Function CreateCubes:TList ()

	SeedRnd MilliSecs ()
	
	Local cubes:TList = CreateList ()
	
	Local protocube:TEntity = CreateCube ()
	
	For Local loop:Int = 0 Until 1000
	
		Local temp:Cube = New Cube
		
		temp.entity = CopyEntity (protocube)
		
		Local scale:Float = Rnd (0.25, 2.0)
		ScaleEntity temp.entity, scale, scale, scale
		
		EntityColor temp.entity, Float (Rnd (128.0, 255.0)), Float (Rnd (128.0, 255.0)), Float (Rnd (128.0, 255.0))
		
		PositionEntity temp.entity, Float (Rnd (-100.0, 100.0)), Float (Rnd (-100.0, 100.0)), Float (Rnd (4.0, 100.0))
		
		temp.pitch = Float (Rnd (-3.0, 3.0))
		temp.yaw = Float (Rnd (-3.0, 3.0))
		temp.roll = Float (Rnd (-3.0, 3.0))
		
		ListAddLast cubes, temp
	
	Next
	
	FreeEntity protocube

	Return cubes
	
End Function

Function SpinCubes:Int ()

	For Local temp:Cube = EachIn CubeList
		TurnEntity temp.entity, temp.pitch, temp.yaw, temp.roll
	Next

End Function

</textarea><br><br><i>grey.vert.glsl</i>:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

uniform mat4 matrix_projection;
uniform mat4 matrix_modelview;

void main()
{
	gl_Position = matrix_projection * matrix_modelview * gl_Vertex;
}

</textarea><br><br><i>grey.frag.glsl</i>:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

uniform sampler2D shadertex;
uniform float mode;
uniform vec2 display;

// Algorithms from <a href="http://www.johndcook.com/blog/2009/08/24/algorithms-convert-color-grayscale/" target="_blank">http://www.johndcook.com/blog/2009/08/24/algorithms-convert-color-grayscale/</a>

void main()
{
	float x = gl_FragCoord.x / display.x;
	float y = gl_FragCoord.y / display.y;

	vec4 pixel = texture2D(shadertex, vec2(x, y));

	float rgb;
	
	switch (int(mode))
	{
		case 0:
			
			// Default RGB
			
			break;
			
		case 1:
		
			// Lightness
			
			rgb = (max(max(pixel.r, pixel.g), pixel.b) + min(min(pixel.r, pixel.g), pixel.b)) / 2.0;
			pixel.r = rgb;
			pixel.g = rgb;
			pixel.b = rgb;
			break;
			
		case 2:
		
			// Average
			
			rgb = (pixel.r + pixel.g + pixel.b) / 3.0;
			pixel.r = rgb;
			pixel.g = rgb;
			pixel.b = rgb;
			break;
			
		case 3:
		
			// Luminosity
			
			rgb = (pixel.r * 0.21) + (pixel.g * 0.72) + (pixel.b * 0.07);
			pixel.r = rgb;
			pixel.g = rgb;
			pixel.b = rgb;
			break;
	}
	
	pixel.a = 1.0;
	
	gl_FragColor = pixel;
}

</textarea> <br><br></td></tr></table><br>
<a name="1312978"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Woo-hoo, step 1 of my semi-rewritten Speccy Shader (basic colour conversion), rendered via glorious 256 x 192 texture/camera viewport!<br><br><img src="http://www.hi-toro.com/blitz/images/newspeccyshader.jpg"> <br><br></td></tr></table><br>
<a name="1312979"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RustyKristi</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> that looks nice James <br><br></td></tr></table><br>
<a name="1312991"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks, RK! Enough for tonight, though... <br><br></td></tr></table><br>
<a name="1313976"></a>

<a name="1313977"></a>

<a name="1314062"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> After many failed attempts and finally getting back to it...<br><br><img src="http://www.hi-toro.com/blitz/images/newerspeccyshader.jpg"><br><br>Realtime ZX Spectrum shader (with <a href="https://en.wikipedia.org/wiki/Colour_clash" target="_blank">attribute clash</a>)! Turns arbitrary resolution display into 256 x 192, 8 colours (plus 8 dark variants), 1 background and one foreground colour for every 8 x 8 block, on the fly. (Doesn't take aspect ratio into account, though... yet.)<br><br>Code/media plus runnable executable (<a href="https://www.virustotal.com/en/file/b3515f9ac09537a7dc824fa340d28a942a28104603999cd978480b0716f8f1f9/analysis/1469576544/" target="_blank">VirusTotal scan</a>) -- no idea if/how it will run elsewhere:<br><br><a href="http://www.hi-toro.com/blitz/speccyshader.zip" target="_blank">http://www.hi-toro.com/blitz/speccyshader.zip</a><br><br><i>example.bmx</i> (uses <a href="http://www.hi-toro.com/blitz/images/boing.png" target="_blank">boing.png</a>):<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

SuperStrict

Import Openb3d.B3dglgraphics

' Name of shader ("shader_prefix.vert.glsl" and "shader_prefix.frag.glsl")...

Local shader_prefix:String = "speccy"

Graphics3D 1024, 768, 0, 2

	ClearTextureFilters

Const CHECK_SHADER:Int = 0

	' Enable for shader compilation output:
	
	If CHECK_SHADER
	
		Delay 500 ' Aesthetics, man!
		EndGraphics
		
		' Test shader compilation (see Output tab in MaxIDE):
		
		Local testshader:TGLShader = New TGLShader
		testshader.InitShaders (shader_prefix + ".vert.glsl", shader_prefix + ".frag.glsl")
		End
	
	EndIf

Local campivot:TPivot = CreatePivot ()

	' Pivot for cameras/full-screen sprite -- can't parent one camera to another!

Local cam:TCamera = CreateCamera (campivot)

	CameraClsColor cam, 64.0, 127.0, 255.0
	CameraRange cam, 0.1, 1000.0
	CameraViewport cam, 0, 0, 256, 192
	
	' Short camera range to pick up sprite located at z = 1.0
	
Local spritecam:TCamera = CreateCamera (campivot)

	CameraClsColor spritecam, 64.0, 127.0, 255.0
	CameraRange spritecam, 0.5, 1000.0

Local boing:TSprite = LoadSprite ("boing.png", 1 | 4)

	MoveEntity boing, 0.0, 0.0, 10.0
	ScaleSprite boing, 2.0, 2.0
	
Local boing_ang:Float = 1.0

Local sprite:TSprite = CreateSprite (campivot)

	' Full-screen sprite at z = 1.0...
	
	ScaleSprite sprite, 1.0, Float (GraphicsHeight ()) / Float (GraphicsWidth ())
	MoveEntity sprite, 0.0, 0.0, 1.0
	EntityOrder sprite, -1

	' Rendered in front of everything else via EntityOrder of -1
	
Local shadertex:TTexture = CreateTexture (256, 192, 1 | 256)

	' Screen-size texture on which shader will operate...
	
	' From DOF example: "in GL 2.0 render textures need attached before other textures (EntityTexture)"
	
	ScaleTexture shadertex, 1.0, -1.0 ' Invert y for shader!
	EntityTexture sprite, shadertex

Local shader:TShader = LoadShader ("", shader_prefix + ".vert.glsl", shader_prefix + ".frag.glsl")

	' Used in vertex shader:
	
	UseMatrix shader, "matrix_projection", 2
	UseMatrix shader, "matrix_modelview", 3
	
	ShaderTexture shader, shadertex, "shadertex", 0 ' Last param not needed?
	ShadeEntity sprite, shader

' Display size used in shader...

Local display_width:Float = Float (GraphicsWidth ())
Local display_height:Float = Float (GraphicsHeight ())

	UseFloat2 shader, "display", display_width, display_height

Local msg:String = "Use A/Z and cursors to move, SPACE to toggle Speccy quad..."

' Shader quad on/off...

Local quadtoggle:Int = 0
Local quadstate:String

' Set up light/world centre pivot...

InitLight

' Buncha spinning cubes...

Local cubepivot:TPivot = CreatePivot ()
Global CubeList:TList = CreateCubes (cubepivot)

Repeat

	If KeyHit (KEY_SPACE)
		quadtoggle = 1 - quadtoggle
	EndIf
	
	If KeyDown (KEY_A)
		MoveEntity campivot, 0.0, 0.0, 1.0
	Else
		If KeyDown (KEY_Z)
			MoveEntity campivot, 0.0, 0.0, -1.0
		EndIf
	EndIf
	
	If KeyDown (KEY_LEFT)
		TurnEntity campivot, 0.0, 1.0, 0.0, True
	Else
		If KeyDown (KEY_RIGHT)
			TurnEntity campivot, 0.0, -1.0, 0.0, True
		EndIf
	EndIf

	If KeyDown (KEY_UP)
		TurnEntity campivot, 1.0, 0.0, 0.0
	Else
		If KeyDown (KEY_DOWN)
			TurnEntity campivot, -1.0, 0.0, 0.0
		EndIf
	EndIf
	
	Cls

	' ----------------------------------------------------------------------------
	' Render pass 1:
	' ----------------------------------------------------------------------------
	
	HideEntity spritecam				' Hide the sprite camera
	HideEntity sprite					' Hide the sprite
	ShowEntity cam						' Use main camera to film scene
	
	' TurnEntity cubepivot, 0.1, 0.2, 0.4 ' Bleurgh!
	SpinCubes
	
	RotateSprite boing, boing_ang
	boing_ang = boing_ang + 1.0
	If boing_ang &gt; 360.0 Then boing_ang = boing_ang - 360.0
	
	CameraToTex shadertex, cam			' Render to shader texture
	
	' ----------------------------------------------------------------------------
	' Render pass 2:
	' ----------------------------------------------------------------------------
	
	HideEntity cam						' Hide main camera

	If quadtoggle
		ShowEntity sprite				' Show sprite
		quadstate = "visible"
	Else
		quadstate = "hidden"
	EndIf
	
	ShowEntity spritecam				' Use sprite camera to film sprite

	RenderWorld							' Render scene showing shaded sprite only

	BeginMax2D
	
		ShadowText msg, 20.0, 20.0
		ShadowText "Speccy quad: " + quadstate, 20.0, 40.0
		
	EndMax2D
	
	Flip
	
Until KeyHit (KEY_ESCAPE)

End


Function ShadowText:Int (msg:String, x:Float, y:Float)

	Local red:Int, green:Int, blue:Int
	
	GetColor red, green, blue
	
	SetColor 0, 0, 0
	DrawText msg, x + 1, y + 1

	SetColor 255, 255, 255
	DrawText msg, x, y

	SetColor red, green, blue

End Function


' --
' Light/pivot only...

Function InitLight:Int ()

	Local pivot:TPivot = CreatePivot ()		' World centre
	Local light:TLight = CreateLight (1)
	
	MoveEntity light, -5.0, 5.0, -2.0
	PointEntity light, pivot

End Function

' --
' Cube stuff only...

Type Cube
	Field entity:TEntity
	Field pitch:Float
	Field yaw:Float
	Field roll:Float
End Type

Function CreateCubes:TList (pivot:TPivot)

	SeedRnd MilliSecs ()
	
	Local cubes:TList = CreateList ()
	
	Local protocube:TMesh = CreateCube ()
	
	For Local loop:Int = 0 Until 1000
	
		Local temp:Cube = New Cube
		
		temp.entity = CopyEntity (protocube, pivot)
		
		Local scale:Float = Rnd (0.5, 2.0)
		ScaleEntity temp.entity, scale, scale, scale
		
		EntityColor temp.entity, Float (Rnd (128.0, 255.0)), Float (Rnd (128.0, 255.0)), Float (Rnd (128.0, 255.0))
		
		PositionEntity temp.entity, Float (Rnd (-100.0, 100.0)), Float (Rnd (-100.0, 100.0)), Float (Rnd (4.0, 100.0))
		
		temp.pitch	= Float (Rnd (-3.0, 3.0))
		temp.yaw	= Float (Rnd (-3.0, 3.0))
		temp.roll	= Float (Rnd (-3.0, 3.0))
		
		ListAddLast cubes, temp
	
	Next
	
	FreeEntity protocube

	Return cubes
	
End Function

Function SpinCubes:Int ()

	For Local temp:Cube = EachIn CubeList
		TurnEntity temp.entity, temp.pitch, temp.yaw, temp.roll
	Next

End Function

</textarea><br><br><i>speccy.vert.glsl</i>:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

uniform mat4 matrix_projection;
uniform mat4 matrix_modelview;

varying vec2 texCoord;

void main()
{
	gl_Position = ftransform();
	
	// Flip "y"-component!
	
	texCoord = vec2(gl_MultiTexCoord0.s, -gl_MultiTexCoord0.t);
}

</textarea><br><br><i>speccy.frag.glsl</i> (may still be some Stupid in here):<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

// ------------------------------------------------------------------------
// Core shader params...
// ------------------------------------------------------------------------

varying vec2		texCoord;
uniform sampler2D	shadertex;
uniform vec2		display;

// ------------------------------------------------------------------------
// Standard colour index...
// ------------------------------------------------------------------------

const int COLORS		= 16;

const int BLACK			= 0;
const int BLUE			= 1;
const int RED			= 2;
const int MAGENTA		= 3;
const int GREEN			= 4;
const int CYAN			= 5;
const int YELLOW		= 6;
const int WHITE			= 7;

const int DARKBLACK		= 8;
const int DARKBLUE		= 9;
const int DARKRED		= 10;
const int DARKMAGENTA	= 11;
const int DARKGREEN		= 12;
const int DARKCYAN		= 13;
const int DARKYELLOW	= 14;
const int DARKWHITE		= 15;

void main ()
{ 
	// ------------------------------------------------------------------------
	// Define Spectrum colours...
	// ------------------------------------------------------------------------

	int color_count [COLORS];

//	int zero;
	
//	for (zero = 0; zero &lt; COLORS; zero++)
//	{
//		color_count[zero] = 0;
//	}
	
	vec4 color [COLORS];

	// ------------------------------------------------------------------------
	// Light:
	// ------------------------------------------------------------------------
	
	color [BLACK]		= vec4 (0.0, 0.0, 0.0, 1.0);
	color [BLUE]		= vec4 (0.0, 0.0, 1.0, 1.0);
	color [RED]			= vec4 (1.0, 0.0, 0.0, 1.0);
	color [MAGENTA]		= vec4 (1.0, 0.0, 1.0, 1.0);
	color [GREEN]		= vec4 (0.0, 1.0, 0.0, 1.0);
	color [CYAN]		= vec4 (0.0, 1.0, 1.0, 1.0);
	color [YELLOW]		= vec4 (1.0, 1.0, 0.0, 1.0);
	color [WHITE]		= vec4 (1.0, 1.0, 1.0, 1.0);

	color [DARKBLACK]	= vec4 (0.0, 0.0, 0.0, 1.0);
	color [DARKBLUE]	= vec4 (0.0, 0.0, 0.75, 1.0);
	color [DARKRED]		= vec4 (0.75, 0.0, 0.0, 1.0);
	color [DARKMAGENTA]	= vec4 (0.75, 0.0, 0.75, 1.0);
	color [DARKGREEN]	= vec4 (0.0, 0.75, 0.0, 1.0);
	color [DARKCYAN]	= vec4 (0.0, 0.75, 0.75, 1.0);
	color [DARKYELLOW]	= vec4 (0.75, 0.75, 0.0, 1.0);
	color [DARKWHITE]	= vec4 (0.75, 0.75, 0.75, 1.0);

	vec4 texel = texture2D (shadertex, texCoord);

	// 0.0 - 1.0:
	
	float texel_x = gl_FragCoord.x / display.x;
	float texel_y = gl_FragCoord.y / display.y;

	// Convert to Spectrum colours:
	
	float brightness_cutoff = 0.5;
	
	float brightness = (texel.r + texel.g + texel.b) * 0.333;
	
	// Pixel bright or non-bright? NB. 0.75 hard-coded, as this
	// is what real Speccy does to create dark colours, at least approx...
	
	if (brightness &lt; 0.75)
	{
		brightness = 0.75;
	}
	else
	{
		brightness = 1.0;
	}
	
	if (texel.r &gt; brightness_cutoff)
	{
		texel.r = 1.0 * brightness;
	}
	else
	{
		texel.r = 0.0;
	}
	
	if (texel.g &gt; brightness_cutoff)
	{
		texel.g = 1.0 * brightness;
	}
	else
	{
		texel.g = 0.0;
	}
	
	if (texel.b &gt; brightness_cutoff)
	{
		texel.b = 1.0 * brightness;
	}
	else
	{
		texel.b = 0.0;
	}
	
	// Scan 8 x 8 pixels and determine two most common colours,
	// for background (most) and foreground (next-most):
	
	// Texel position in Speccy pixels:
	
	float speccy_x = texel_x * 256.0;
	float speccy_y = texel_y * 192.0;
	
	// Use to get pixel's 0.0 to 7.0 position in 8 x 8 grid:
	
	float x8 = floor (mod (speccy_x, 8.0));
	float y8 = floor (mod (speccy_y, 8.0));
	
	// Start position in Speccy space:
	
	float start_x = max (speccy_x - x8, 0.0);
	float start_y = max (speccy_y - y8, 0.0);

	float end_x = start_x + 8.0;
	float end_y = start_y + 8.0;
	
	float scan_x;
	float scan_y;
	
	float color_index;
	
	// "Hhhuuuuup!" Deep breath before bone-headed 8 x 8 scan...
	
	scan_y = start_y;
	
	while (scan_y &lt; end_y)
	{
		float y_scan = scan_y / 192.0;
		
		scan_x = start_x;
		
		while (scan_x &lt; end_x)
		{
			float x_scan = scan_x / 256.0;
			
			vec2 scan_pos = vec2 (x_scan, y_scan);
			
			vec4 scanned_texel = texture2D (shadertex, scan_pos);

			float scanned_brightness = (scanned_texel.r + scanned_texel.g + scanned_texel.b) * 0.333;
			
			if (scanned_brightness &lt; 0.75)
			{
				scanned_brightness = 0.75;
			}
			else
			{
				scanned_brightness = 1.0;
			}
			
			if (scanned_texel.r &gt; brightness_cutoff)
			{
				scanned_texel.r = 1.0 * scanned_brightness;
			}
			else
			{
				scanned_texel.r = 0.0;
			}
			
			if (scanned_texel.g &gt; brightness_cutoff)
			{
				scanned_texel.g = 1.0 * scanned_brightness;
			}
			else
			{
				scanned_texel.g = 0.0;
			}
			
			if (scanned_texel.b &gt; brightness_cutoff)
			{
				scanned_texel.b = 1.0 * scanned_brightness;
			}
			else
			{
				scanned_texel.b = 0.0;
			}

			color_index = 0;
			
			while (color_index &lt; COLORS)
			{
				if (scanned_texel == color[color_index])
				{
					color_count[color_index]++;
				}
				color_index++;
			}
			scan_x++;
		}
		scan_y++;
	}
	
	int bgcount = 0; // Background colour count
	int fgcount = 0; // Foreground colour count

	vec4 background; // Background colour
	vec4 foreground; // Foreground colour

	color_index = 0;
	
	while (color_index &lt; COLORS)
	{
		if (color_count [color_index] &gt; bgcount)
		{
			background = color [color_index];
			bgcount = color_count [color_index];
		}
		else
		{
			if (color_count [color_index] &gt; fgcount)
			{
				foreground = color [color_index];
				fgcount = color_count [color_index];
			}
		}
		color_index++;
	}

	if (texel != background)
	{
		texel = foreground;
	}
	
	//	Compiling?!
	//	texel = 1.0;

	// Show every 8th pixel:
	//	if (x8 == 0 &amp;&amp; y8 == 0)
	//	{
	//		texel = 1.0;
	//	}

	gl_FragColor = texel;
}

</textarea><br><br>For <i>every pixel</i>, the shader has to scan every 8 x 8 block of 256 x 192 texture to determine count of each of 16 colours, using highest two for background and foreground.<br><br>Still need to play about with brightness/contrast boosting of source render, but good enough for now. <br><br></td></tr></table><br>
<a name="1314012"></a>

<a name="1314013"></a>

<a name="1314014"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Looks interesting.<br><br>I'm sure that pixel shader can be improved somewhat though :D<br>Its using a serial approach that you'd use for a cpu but gpus are parallel processors.<br><br>That would be a good exercise :O)<br><br>edit:<br>I can't get any of this to run on win7 x64 ng, or legacy max.<br>It either won't compile due to std lib errors (legacy) or I get a grey screen with 'app has stopped working' in ng ( apparently there's a heap corruption :/ )<br><br>Oh the fun :D <br><br></td></tr></table><br>
<a name="1314021"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Wasn't sure how else to manage the reading of 8 x 8 pixels to count colours -- the problem as I see it is that for every pixel, I *must* read the 8 x 8 block it sits within, since I can't share info between each pixel's instance of the shader.<br><br>Shame it won't run -- maybe try the NG archive at the top of this thread. Does the exe fail too? <br><br></td></tr></table><br>
<a name="1314027"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> The archive at the top, which I would have thought to be self-contained does open the editor but when I hit compile I get 'Cannot find a valid GCC compiler' message. Shaders are shaders so I can probably get something up and running in legacy max gl, just to play with.<br><br>Off the top of my head, I was thinking of using a deferred approach... and hopefully let the rasterizer do the 8x8 colour blocks for you at the expense of rendering a quad and sampling from 2 textures.<br><br>I'll set about having a play this evening :p <br><br></td></tr></table><br>
<a name="1314031"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> I did vaguely think of using &gt; 1 texture, though wasn't sure how exactly. The NG archive 'should' be self-contained, but... !<br><br>Be cool if you come up with a faster approach, though, as I suspect mine would be very slow on many cards... if it ran... er... would like to see a smarter way to handle it, anyway. <br><br></td></tr></table><br>
<a name="1314035"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi<br>tested the .exe - but when I press SPACE I see just a 'black' window.<br>Windows10 x64 AMD HD5700 <br><br></td></tr></table><br>
<a name="1314045"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice examples + setup, James. Perhaps I should start playing around with GLSL, too. And you should use codeboxes for your snippets, it's easier to copy'n'paste :-D <br><br></td></tr></table><br>
<a name="1314059"></a>

<a name="1314064"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> I was going to use codeboxes, but thought that since it's not in the middle of someone else's thread, and they're meant as examples, it was better to post in full. Sort of changing my mind now, especially if you think it's easier to copy (I thought full size was easier)...<br><br>Oh, and definitely worth playing around with! Think my next job will be a depth of field shader. (I've done all this stuff in Max3D before, but none of it works now, and I've found some really dumb stuff in my old examples.)<br><br>@degac: Sorry, don't know why! Probably some sort of GLSL version thing, but with only my main PC to really test on I'm kind of stuck! <br><br></td></tr></table><br>
<a name="1314063"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah, codeboxes definitely better. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
