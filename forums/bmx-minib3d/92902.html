<!DOCTYPE html><html lang="en" ><head ><title >BlitzSky - help!</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >BlitzSky - help!</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=119" >MiniB3D Module</a>/<a href="#bottom" >BlitzSky - help!</a><br><br>
<a name="1060511"></a>

<a name="1060512"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I need help! I've tried to convert <a href="/posts.php?topic=87026" target="_blank">BlitzSky</a> to miniB3D, and I <b>almost</b> succeed - I've got everything working - except the sun and moon, which are being rotated wrongly, and I can't figure out why.<br><br>Here's a image to show what's happening:<br><br><img src="http://img444.imageshack.us/img444/3637/blitzsky.jpg"><br><br>That bright thing should be the sun :P<br><br>Here's the converted (and buggy) code:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Global objFPS_list:TList=New TList
Global startwinkle_list:TList=New TList

Rem
' -----------------------------------------------------------------------------------------------
' BlitzSky - a derivat of BlitzTiles
' -----------------------------------------------------------------------------------------------
'
' Blitz3D version (C) 2009 Christian Hart. All rights reserved.
' Please check the "License.txt" provided with this package for terms and conditions.
'
-----------------------------------------------------------------------------------------------
End Rem

' -----------------------------------------------------------------------------------------------
' Engine Variables
' -----------------------------------------------------------------------------------------------

Global BS_GameSpeed%			= 60
Global BS_LoadMediaToVRAM%		= 1
Global BS_TexturePath$			= "textures\"
Global BS_FontPath$				= "fonts\"
Global BS_ShowHelp%				= 0
Global BS_ShowInformation%		= 0
Global BS_OceanPatchResolution%	= 32
Global BS_ShorePatchResolution%	= 32
Global BS_GroundPatchResolution%= 32
Global BS_FPSfrequency%			= 500
Global BS_FPSfont$				= "quartz.png"
Global BS_ShowFPS%				= 1

' -----------------------------------------------------------------------------------------------
' Sky Variables
' -----------------------------------------------------------------------------------------------

Global BS_Hazesegments%			= 16
Global BS_SkyObjectsMulti#		= 32.0
Global BS_BrightnessMulti#		= 128.0
Global BS_MoonTextureSize%		= 512
Global BS_StarSizeMin#			= 0.02
Global BS_StarSizeMax#			= 0.03
Global BS_FixStarsProportion#	= 0.95
Global BS_StarMinBrightness%	= 64
Global BS_StarMaxBrightness%	= 255
Global BS_Stars%				= 1000
Global BS_SunInclination#		= 45.0
Global BS_MoonPhase#			= 0.0
Global BS_MoonInclination#		= 60.0
Global BS_CloudSpeed#			= 8.0
Global BS_CloudScale#			= 4.0
Global BS_SunAngle#				= 90.0
Global BS_MoonAngle#			= 90.0
Global BS_HazeHeight#			= 0.5
Global BS_ShowHaze%				= 1
Global BS_ShowSky%				= 1
Global BS_ShowStarTwinkle%		= 1
Global BS_ShowBrightness%		= 1
Global BS_Minheight#			= -10


' -----------------------------------------------------------------------------------------------
' Scene Variables
' -----------------------------------------------------------------------------------------------

Global BS_IsIsland%				= 1
Global BS_WaterSpeed#			= 8.0
Global BS_ShoreSpeed#			= 4.0
Global BS_WaterAlpha#			= 0.5
Global BS_WaterLevel#			= 0.0
Global BS_ShoreMove%			= 0
Global BS_ShowGround%			= 1
Global BS_ShowOcean%			= 1


' -----------------------------------------------------------------------------------------------
' Lights Variables
' -----------------------------------------------------------------------------------------------

Global BS_SunLightIntensity#	= 10.0
Global BS_MoonLightIntensity#	= 5.0
Global BS_ShowLights%			= 1
Global BS_SunLightType%			= 1
Global BS_MoonLightType%		= 1


' -----------------------------------------------------------------------------------------------
' Camera Variables
' -----------------------------------------------------------------------------------------------

Global BS_CameraMinRange#		= 0.5
Global BS_CameraMaxRangeMulti#	= 1.5
Global BS_FogMinRange#			= 0.0
Global BS_FogMaxRangeMulti#		= 1.5
Global BS_CameraCollisionRadius#= 0.5
Global BS_CameraFOV#			= 90.0
Global BS_ShowWireframe%		= 0
Global BS_ShowFog%				= 1


' -----------------------------------------------------------------------------------------------
' Player Variables
' -----------------------------------------------------------------------------------------------

Global BS_PlayerSpeed#			= 1.00
Global BS_PlayerStartX#			= -256
Global BS_PlayerStartY#			= 128
Global BS_PlayerStartZ#			= 0
Global BS_PlayerStartR#			= 90


' -----------------------------------------------------------------------------------------------
' Scene objects
' -----------------------------------------------------------------------------------------------
Global BS_Camera:TCamera

' Pivots
Global BS_SunPivot:TPivot,BS_MoonPivot:TPivot,BS_Skypivot:TPivot
Global BS_SunLightPivot:TPivot,BS_MoonLightPivot:TPivot
Global BS_PlacesPivot:TPivot

' Lights
Global BS_SunLight:TLight,BS_MoonLight:TLight

' Quads
Global BS_Sun1:TMesh,BS_Sun2:TMesh,BS_Sun3:TMesh,BS_SunBright1:TMesh,BS_SunBright2:TMesh,BS_SunGlow:TMesh
Global BS_Moon1:TMesh,BS_Moon2:TMesh,BS_MoonBright1:TMesh,BS_MoonBright2:TMesh

' Patches
Global BS_Ocean:TMesh,BS_Shore:TMesh,BS_Ground:TMesh,BS_Sky:TMesh,BS_Stratosphere:TMesh

' Custom Meshes
Global BS_Player:TPivot,BS_Haze:TMesh,BS_Starsphere:TMesh


' -----------------------------------------------------------------------------------------------
' Player Data
' -----------------------------------------------------------------------------------------------
Global BS_SpeedMultiplicator%
Global BS_CamX#,BS_CamY#,BS_CamZ#
Global BS_PlayerX#,BS_PlayerY#,BS_PlayerZ#


' -----------------------------------------------------------------------------------------------
' Engine Data
' -----------------------------------------------------------------------------------------------
Global BS_OldSunAngle#=-1,BS_ManualUpdate%,BS_DeltaTimeOld%
Global BS_FramePeriod%=1000/BS_GameSpeed
Global BS_FrameTime%=MilliSecs()-BS_FramePeriod
Global FPS%,FPS_Timer%,FPS_TimerValue%
Global BS_ShoreMove1#,BS_ShoreMove2#,BS_WaveMove1#,BS_WaveMove2#,BS_CloudMove#
Global BS_SaveScreenshot%


' -----------------------------------------------------------------------------------------------
' Media globals
' -----------------------------------------------------------------------------------------------
Global BS_Texture_MoonSphere:TTexture,BS_Texture_MoonQuad1:TTexture,BS_Texture_MoonQuad2:TTexture
Global BS_Texture_Water1:TTexture,BS_Texture_Water2:TTexture
Global BS_Texture_Clouds1:TTexture,BS_Texture_Clouds2:TTexture,BS_Texture_Clouds3:TTexture
Global BS_Texture_Sun1:TTexture,BS_Texture_Sun2:TTexture,BS_Texture_Sunglow1:TTexture,BS_Texture_Sunglow2:TTexture
Global BS_Texture_Moonglow:TTexture,BS_Texture_SingleStar:TTexture
Global BS_Texture_HazeGradient:TTexture,BS_Texture_HorizonMask:TTexture
Global BS_DomeBulge:TTexture,BS_Numbers:TTexture


' -----------------------------------------------------------------------------------------------
' Arrays
' -----------------------------------------------------------------------------------------------
Global BS_VertexBuffer[33+1,33+1]
Global BS_GradientPercent#[0+1],BS_GradientRed%[0+1],BS_GradientGreen%[0+1],BS_GradientBlue%[0+1]


' -----------------------------------------------------------------------------------------------
' Blitzarrays (Gradients)
' -----------------------------------------------------------------------------------------------
Const BS_ColorSteps%=3600
Global AmbientR%[BS_ColorSteps+1],AmbientG%[BS_ColorSteps+1],AmbientB%[BS_ColorSteps+1]
Global CLScolorR%[BS_ColorSteps+1],CLScolorG%[BS_ColorSteps+1],CLScolorB%[BS_ColorSteps+1]
Global HazeColR%[BS_ColorSteps+1],HazeColG%[BS_ColorSteps+1],HazeColB%[BS_ColorSteps+1]
Global LightR%[BS_ColorSteps+1],LightG%[BS_ColorSteps+1],LightB%[BS_ColorSteps+1]
Global SunLightR%[BS_ColorSteps+1],SunLightG%[BS_ColorSteps+1],SunLightB%[BS_ColorSteps+1]
Global OceanR%[BS_ColorSteps+1],OceanG%[BS_ColorSteps+1],OceanB%[BS_ColorSteps+1]
Global BrightnessR%[BS_ColorSteps+1],BrightnessG%[BS_ColorSteps+1],BrightnessB%[BS_ColorSteps+1]
Global SunAspectX%[BS_ColorSteps+1],SunAspectY%[BS_ColorSteps+1],SunAspectZ%[BS_ColorSteps+1]
Global Sunsizes%[BS_ColorSteps+1]
Global StarAlpha%[BS_ColorSteps+1]
Global MoonAlpha%[BS_ColorSteps+1]
Global SunLightRange%[BS_ColorSteps+1]
Global MoonLightRange%[BS_ColorSteps+1]


' -----------------------------------------------------------------------------------------------
' Types
' -----------------------------------------------------------------------------------------------

Type bbstartwinkle Extends TBBType

	Method New()
		Add(startwinkle_list)
	End Method

	Method After:bbstartwinkle()
		Local t:TLink
		t=_link.NextLink()
		If t Return bbstartwinkle(t.Value())
	End Method

	Method Before:bbstartwinkle()
		Local t:TLink
		t=_link.PrevLink()
		If t Return bbstartwinkle(t.Value())
	End Method

	
	Field id%
	Field brightness%
	Field sourcecolor%
	Field frequency#
	
End Type

Type bbobjFPS Extends TBBType

	Method New()
		Add(objFPS_list)
	End Method

	Method After:bbobjFPS()
		Local t:TLink
		t=_link.NextLink()
		If t Return bbobjFPS(t.Value())
	End Method

	Method Before:bbobjFPS()
		Local t:TLink
		t=_link.PrevLink()
		If t Return bbobjFPS(t.Value())
	End Method

	Field Array[129]
	Field Total
	Field Index
	Field LastTime
	Field LastIndex
End Type


' -----------------------------------------------------------------------------------------------
' Outputs FPS
' -----------------------------------------------------------------------------------------------
Function BS_UpdateFPS()
	
	Local ms%=MilliSecs()
	
	' use FPS Timer
	If ms&gt;FPS_Timer+BS_FPSfrequency Then
		FPS_TimerValue=FPS
		FPS_Timer=ms
	EndIf
	
	' draw FPS value
	'If BS_ShowFPS Then BS_DrawDigits(BS_ScreenWidth+1,-1,BS_Numbers,18,FPS_TimerValue,True)
	
End Function

' -----------------------------------------------------------------------------------------------
' Average FPS calculation
' -----------------------------------------------------------------------------------------------
Function BS_GetFPS()
	
	Local This:bbobjFPS, ThisTime#,ms%


	If objFPS_list.isEmpty() Then
		This=New bbobjFPS 
		This.Index=0
		This.LastIndex=1
	Else
		This=bbobjFPS(objFPS_list.First())
	End If
	
	' fix negative Millisecs values
	ms%=MilliSecs()
	If ms&lt;0 Then ms=ms &amp; $7fffffff
	
	This.Index=(This.Index Shr 0 &amp; 127 Shl 0)+1
	This.LastIndex=(This.LastIndex Shr 0 &amp; 127 Shl 0)+1
	This.Array[This.Index]=ms-This.LastTime 
	This.LastTime=ms
	This.Total=This.Total+This.Array[This.Index]-This.Array[This.LastIndex]
	ThisTime#=Float(This.Total)/128.0
	
	If ThisTime = 0 Then
		
		Return(1000)
		
	Else
		
		Return(1000.0 / ThisTime)
		
	EndIf
	
End Function


' -----------------------------------------------------------------------------------------------
' Draws digits
' -----------------------------------------------------------------------------------------------
Function BS_DrawDigits(sx%,sy%,font:TImage,size%,value%,rt%=False)
Rem	
	Local v$=String(value),i%
	
	' align right if set
	If rt Then sx=sx-(Len(v)*size)
	
	' output number
	For i=0 To Len(v)-1
		
		DrawImageRect font,sx+(size*i),sy,(Asc(Mid(v,i+1,1))-48)*size,0,size,size
		
	Next
End Rem	
End Function


' -----------------------------------------------------------------------------------------------
' Enhanced Loadtexture function
' -----------------------------------------------------------------------------------------------
Function BS_LoadTexture:TTExture(filename$,flags%,uscale#=1.0,vscale#=1.0,blendmode%=False)
	
	Local tex:TTexture=LoadTexture(BS_TexturePath+filename$,flags)
	ScaleTexture tex,uscale,vscale
	If blendmode Then TextureBlend tex,blendmode
	
	Return tex
	
End Function


' -----------------------------------------------------------------------------------------------
' Loads the textures and images
' -----------------------------------------------------------------------------------------------
Function BS_LoadMedia()
	
	Local s#=BS_CloudScale
	
	If BS_LoadMediaToVRAM Then Local vram%=256
	vram=0
	
	' Create Moonphase
	BS_Texture_MoonSphere	= BS_LoadTexture("moon.jpg"			,0+vram)
	'BS_Texture_MoonQuad1	= BS_LoadTexture("moon1.png"		,2+vram)
	'BS_Texture_MoonQuad2	= BS_LoadTexture("moon2.png"		,2+vram)	
	
	
	' Loading Textures
	BS_Texture_Water1		= BS_LoadTexture("mywaves.png"		,2+8+vram,1.0/16	,1.0/16	,2)
	BS_Texture_Water2		= BS_LoadTexture("mywaves.png"		,2+8+vram,1.0/32	,1.0/32	,5)
	BS_Texture_Clouds1		= BS_LoadTexture("clouds1.png"		,2+8+vram,1.0/2		,1.0/2	,2)
	BS_Texture_Clouds2		= BS_LoadTexture("clouds2.png"		,2+8+vram,1.0/s		,1.0/s	,2)
	BS_Texture_Clouds3		= BS_LoadTexture("iceclouds.png"	,2+8+vram,1.0/s		,1.0/s	,2)
	BS_Texture_Sun1			= BS_LoadTexture("sun1.png"			,2+8+vram)
	BS_Texture_Sun2			= BS_LoadTexture("sun2.png"			,2+8+vram)
	BS_Texture_HorizonMask	= BS_LoadTexture("mask.png"			,2+8+vram)
	BS_Texture_Sunglow1		= BS_LoadTexture("sunshine1.png"	,2+8+vram,1			,1		,3)
	BS_Texture_Sunglow2		= BS_LoadTexture("sunshine2.png"	,2+8+vram,1			,1		,5)
	BS_Texture_Moonglow		= BS_LoadTexture("moonmask.png"		,2+8+vram,1			,1		,3)
	BS_Texture_SingleStar	= BS_LoadTexture("dot.png"			,2+8+vram)
	BS_Texture_HazeGradient	= BS_LoadTexture("haze.png"			,2+8+vram)
	
	' Loading Images
	BS_DomeBulge=LoadTexture(BS_TexturePath+"dome.png")
	BS_Numbers=LoadTexture(BS_FontPath+BS_FPSfont)
	
End Function


' -----------------------------------------------------------------------------------------------
' Inits the whole scene
' -----------------------------------------------------------------------------------------------
Function BS_InitBlitzSky()
	
	' Create color gradients
	BS_InitGradients()
	
	' Load all textures and images
	BS_LoadMedia()
		
	' Init Scene
	BS_InitScene()

	' Inits the moon quad
	BS_InitMoon() ; Cls

	EntityTexture BS_Moon1,BS_Texture_MoonQuad1
	EntityTexture BS_Moon2,BS_Texture_MoonQuad2	
	
	' Init Player Position
	BS_InitPlayer(BS_PlayerStartX,BS_PlayerStartY,BS_PlayerStartZ)
	
	' prepare Screen
	MoveMouse BS_ScreenWidth/2,BS_ScreenHeight/2
	'SetBuffer BackBuffer()
	
End Function


' -----------------------------------------------------------------------------------------------
' Creates smooth color gradients
' -----------------------------------------------------------------------------------------------
Function BS_InitGradients()
	
	' Gradients
	RestoreData BS_Ambientlight	; BS_CreateGradient(9,BS_ColorSteps,False,AmbientR,AmbientG,AmbientB)
	RestoreData BS_ClsColor		; BS_CreateGradient(9,BS_ColorSteps,False,CLScolorR,CLScolorG,CLScolorB)
	RestoreData BS_HazeColor		; BS_CreateGradient(9,BS_ColorSteps,False,HazeColR,HazeColG,HazeColB)
	RestoreData BS_LightColor		; BS_CreateGradient(9,BS_ColorSteps,False,LightR,LightG,LightB)
	RestoreData BS_SunLightColor	; BS_CreateGradient(9,BS_ColorSteps,False,SunLightR,SunLightG,SunLightB)
	RestoreData BS_OceanColor		; BS_CreateGradient(7,BS_ColorSteps,False,OceanR,OceanG,OceanB)
	RestoreData BS_Sunsize			; BS_CreateGradient(9,BS_ColorSteps,False,Sunsizes,Sunsizes,Sunsizes)
	RestoreData BS_StarLight		; BS_CreateGradient(7,BS_ColorSteps,False,StarAlpha,StarAlpha,StarAlpha)
	RestoreData BS_Brightness		; BS_CreateGradient(7,BS_ColorSteps,False,BrightnessR,BrightnessG,BrightnessB)
	RestoreData BS_MoonAlpha		; BS_CreateGradient(9,BS_ColorSteps,False,MoonAlpha,MoonAlpha,MoonAlpha)
	RestoreData BS_SunAspect		; BS_CreateGradient(9,BS_ColorSteps,False,SunAspectX,SunAspectY,SunAspectZ)
	RestoreData BS_SunLightRange	; BS_CreateGradient(9,BS_ColorSteps,False,SunLightRange,SunLightRange,SunLightRange)
	RestoreData BS_MoonLightRange	; BS_CreateGradient(9,BS_ColorSteps,False,MoonLightRange,MoonLightRange,MoonLightRange)
	
End Function


' -----------------------------------------------------------------------------------------------
' Outputs runtime Text info
' -----------------------------------------------------------------------------------------------
Function BS_UpdateInfos()
	
	Local i%,infos$[8],percent#
	
	' calculate day percent
	percent=BS_Normalize(BS_SunAngle,0,360,0,100)
	
	infos[0]="Triangles rendered...: "'+TrisRendered()
	infos[1]="Frames per Second....: "+FPS
	infos[2]="Day Time = Angle = %.: "+BS_CalculateTime()+" = "+BS_RoundTo(BS_SunAngle,2)+"° = "+BS_RoundTo(percent,2)+"%"
	infos[3]="Horizontal Range.....: "+Int(BS_Scale)+"m ["+Int((BS_Scale/1000.0)^2)+"km²]"
	infos[4]="Viewing Distance.....: "+BS_CameraMinRange+"m to "+(BS_CameraMaxRangeMulti*BS_Scale)+"m"
	infos[5]="Current Position XYZ.: "+BS_RoundTo(EntityX(BS_Camera),2)+","+BS_RoundTo(EntityY(BS_Camera),2)+","+BS_RoundTo(EntityZ(BS_Camera),2)
	infos[6]="Camera FOV/View angle: "+Int(BS_CameraFOV)+"° / "+BS_RoundTo(EntityYaw(BS_Camera)*-1,2)+"°"
	infos[7]=""
	infos[8]="Press F1 for help"
	
	' shadowed text output (slow)
	For i=0 To 8 ; BS_ShadowText(0,(i*15),infos[i],1,255,255,255,0,0,0) ; Next
	
End Function


' -----------------------------------------------------------------------------------------------
' Outputs text with a shadow
' -----------------------------------------------------------------------------------------------
Function BS_ShadowText(x%,y%,textstring$,offset%,rt%,gt%,bt%,rs%,gs%,bs%,centerx%=0,centery%=0)
	
	SetColor rs,gs,bs 
	Text x+offset,y+offset,textstring
	SetColor rt,gt,bt 
	Text x,y,textstring
	
End Function


' -----------------------------------------------------------------------------------------------
' Rounds a value (ex. 12.345678 with 2 digits -&gt; 12.34 or 90.0 -&gt; 90.00)
' -----------------------------------------------------------------------------------------------
Function BS_RoundTo$(value#,digits%=2)
	
	Local i%
	Local factor#=10^digits
	Local ints%=Int(Floor(value))
	Local modstring$=String(Int((value-ints)*factor))
	Local modlength%=Len(modstring)
	
	If modlength&lt;digits Then
		For i=1 To (digits-modlength)
			modstring=modstring+"0"
		Next
	EndIf
	
	Return ints+"."+modstring
	
End Function
	
' -----------------------------------------------------------------------------------------------
' Displays runtime help
' -----------------------------------------------------------------------------------------------
Function BS_DrawHelp()
	
	Local hints$[25],i%
	
	hints[0]="BLITZ SKY HELP"
	hints[1]=""
	hints[2]="KEYBOARD"
	hints[3]="--------------------------------------------------------------------------"
	hints[4]="1-0      predefined sun angles from 90...270 degrees, 0 is midnight"
	hints[5]="Q/W      decrease/increase sun angle"
	hints[6]="SPACE    WireFrame on/off"
	hints[7]="RTShift  speedup player 25x"
	hints[8]="F        Fog on/off"
	hints[9]="T        Star Twinkle on/off"
	hints[10]="B        Sun Brightness on/off"
	hints[11]="G        show/hide Ground"
	hints[12]="O        show/hide Ocean"
	hints[13]="H        show/hide Haze"
	hints[14]="S        show/hide Sky"
	hints[15]="I        show/hide Infos (hide to boost FPS)"
	hints[16]="L        show/hide Light sources"
	hints[17]="Arrows   move forward/backwards and sidewards"
	hints[18]=""
	hints[19]="MOUSE"
	hints[20]="--------------------------------------------------------------------------"
	hints[21]="LMB      move up"
	hints[22]="RMB      speedup player 100x"
	hints[23]="Wheel    Change Camera FOV"
	hints[24]="Move     look around"
	
	' shadowed text output (very slow)
	For i=0 To 24 ; BS_ShadowText(0,i*15,hints[i],1,255,255,255,0,0,0) ; Next
	
End Function


' -----------------------------------------------------------------------------------------------
' Calculate human readable time from angle time
' -----------------------------------------------------------------------------------------------
Function BS_CalculateTime$()
	
	Local time%=BS_SunAngle*4
	Local hours%=Int(Floor(time/60.0))
	Local minutes%=time-(hours*60)
	
	Return BS_ZERO(hours)+":"+BS_ZERO(minutes)
	
	
End Function


' -----------------------------------------------------------------------------------------------
' Apng BS_ZEROs in front of single digit numbers (ex. 9-&gt;09)
' -----------------------------------------------------------------------------------------------
Function BS_ZERO$(number%,lenght%=2)
	
	Local i%,r$
	
	For i=0 To lenght-Len(String(number))-1 ; r$=r$+"0" ; Next
	Return r$+String(number)
	
End Function


' -----------------------------------------------------------------------------------------------
' Creates a haze ring around the world
' -----------------------------------------------------------------------------------------------
Function BS_InitHaze:TMesh(radius#,h#,segments,fx%=0,r%=255,g%=255,b%=255,a1#=0.0,a2#=1.0)
	
	Local ang1#,ang2#,inc#,x1#,z1#,x2#,z2#
	Local uu1#,uu2#,uv1#,uv2#,uv3#
	Local v0%,v1%,v2%,v3%,v4%,v5%
	
	Local mesh:TMesh=CreateMesh()
	Local surf:TSurface=CreateSurface(mesh)
	
	inc=360.0/segments
	
	' complete circle
	While ang1&lt;360
		
		' Angle
		ang2=(ang1+inc) Mod 360
		
		' Vertex coordinates
		x1=radius*Cos(ang1)
		z1=radius*Sin(ang1)
		x2=radius*Cos(ang2)
		z2=radius*Sin(ang2)
		
		' UV-coordinates
		uu1=1.0-(ang1/180.0)
		uu2=1.0-(ang1+inc)/180.0
		uv1=1.00
		uv2=0.50
		uv3=0.00
		
		' Add and color vertices
		v0=AddVertex(surf,x1,-h,z1,uu1,uv1) ; VertexColor surf,v0,r,g,b,a1
		v1=AddVertex(surf,x2,-h,z2,uu2,uv1) ; VertexColor surf,v1,r,g,b,a1
		v2=AddVertex(surf,x1, 0,z1,uu1,uv2) ; VertexColor surf,v2,r,g,b,a2
		v3=AddVertex(surf,x2, 0,z2,uu2,uv2) ; VertexColor surf,v3,r,g,b,a2
		v4=AddVertex(surf,x1, h,z1,uu1,uv3) ; VertexColor surf,v4,r,g,b,a1
		v5=AddVertex(surf,x2, h,z2,uu2,uv3) ; VertexColor surf,v5,r,g,b,a1
		
		' Add triangles
		AddTriangle surf,v0,v1,v3
		AddTriangle surf,v3,v2,v0
		AddTriangle surf,v2,v5,v4
		AddTriangle surf,v5,v2,v3
		
		' increase angle
		ang1=ang1+inc
		
	Wend
	
	EntityFX mesh,fx
	
	Return mesh
	
End Function


' -----------------------------------------------------------------------------------------------
' Update game logic, check for user input
' -----------------------------------------------------------------------------------------------
Function BS_UpdateBlitzSky()
	
	' check for User Input
	BS_CheckInput()
	
	' Update Lights
	BS_UpdateScene()
	
	' ground
	If BS_ShowGround=1 Then ShowEntity BS_Ground Else HideEntity BS_Ground
	
	' wireframe
	Wireframe BS_ShowWireframe
	
	' haze
	If BS_ShowHaze=1 Then ShowEntity BS_Haze Else HideEntity BS_Haze
	
	' lights
	If BS_ShowLights=0 Then
		HideEntity BS_SunLightPivot
		HideEntity BS_MoonLightPivot
		HideEntity BS_SunPivot
		HideEntity BS_MoonPivot
	Else
		ShowEntity BS_SunLightPivot
		ShowEntity BS_MoonLightPivot
		ShowEntity BS_SunPivot
		ShowEntity BS_MoonPivot
	EndIf
	
	' brightness
	If BS_ShowBrightness=0 Then HideEntity BS_SunGlow Else ShowEntity BS_SunGlow
	
	' ocean
	If BS_ShowOcean=0 Then
		HideEntity BS_Ocean
		HideEntity BS_Shore
	Else
		ShowEntity BS_Ocean
		ShowEntity BS_Shore
	EndIf
	
	' sky
	If BS_ShowSky=0 Then
		HideEntity BS_Stratosphere
		HideEntity BS_Sky
		HideEntity BS_Starsphere
	Else
		ShowEntity BS_Stratosphere
		ShowEntity BS_Sky
		ShowEntity BS_Starsphere
	EndIf
	
	' camera
	CameraFogMode BS_Camera,BS_ShowFog
	CameraRange BS_Camera,BS_CameraMinRange,BS_Scale*BS_CameraMaxRangeMulti
	CameraFogRange BS_Camera,BS_FogMinRange,BS_Scale*BS_FogMaxRangeMulti
	
End Function


' -----------------------------------------------------------------------------------------------
' creates a moon sphere and photographs it to a quad texture
' -----------------------------------------------------------------------------------------------
Function BS_InitMoon()
	Local sphere:TMesh,light:TLight
	Local tempcam:TCamera,x%,y%
	Local buffer1:TTexture,buffer2:TTexture
	Local rgb%,r%,g%,b%,a%
	
	sphere=CreateSphere(32)
	EntityTexture sphere,BS_Texture_MoonSphere
	EntityFX sphere,2+8
	ScaleEntity sphere,1,1,1
	PositionEntity sphere,0,-10000,0
	
	AmbientLight 2,2,2
	light=CreateLight(1,sphere)
	PositionEntity light,0,0,0
	MoveEntity light,16,0,0
	PointEntity light,sphere
	LightRange light,16
	
	RotateEntity sphere,20,135,0
	TurnEntity sphere,0,BS_MoonPhase,0
	
	tempcam=CreateCamera()
	PositionEntity tempcam,0,-10000,0
	MoveEntity tempcam,0,0,2
	PointEntity tempcam,sphere
	
	CameraViewport tempcam,0,0,BS_MoonTextureSize,BS_MoonTextureSize
	HideEntity BS_SunLightPivot
	HideEntity BS_MoonLightPivot
	RenderWorld
	RenderWorld
	
	BS_Texture_MoonQuad1 = CreateTexture(BS_MoonTextureSize, BS_MoonTextureSize,1)
	BS_Texture_MoonQuad2 = CreateTexture(BS_MoonTextureSize, BS_MoonTextureSize,1)
	
	BackBufferToTex BS_Texture_MoonQuad1
	'BackBufferToTex BS_Texture_MoonQuad2

	ShowEntity BS_MoonLightPivot
	ShowEntity BS_SunLightPivot
	
	' normally a texture with alpha flag would be created here, but this is a
	' workaround to prevent a strange createtexture alpha bug which leads to
	' disappearing textures - if loaded as texture they won't vanish :-)
	' if you change the moon texture size be sure to change the size of the
	' source texture file, too!
	'buffer1=TextureBuffer(BS_Texture_MoonQuad1)
	'buffer2=TextureBuffer(BS_Texture_MoonQuad2)
	
	'LockBuffer buffer1
	'LockBuffer buffer2
	'LockBuffer BackBuffer()

	For y=0 To BS_MoonTextureSize-1
		
		For x=0 To BS_MoonTextureSize-1
			
			'rgb=ReadPixelFast(x,y,BackBuffer())
			rgb = ReadPixel(BS_Texture_MoonQuad1.pixmap, x,y)
			a=255
			
			r=(rgb &amp; $ff0000)/$10000
			g=(rgb &amp; $ff00)/$100
			b=(rgb &amp; $ff)
			
			If r=0 And g=0 And b=0 Then a=0
			
			'rgb=a*$1000000+r*$10000+g*$100+b
			'WritePixelFast x,y,rgb,buffer1
			'WritePixel BS_Texture_MoonQuad1.pixmap, x,y, rgb
			
			rgb=a*$1000000+255*$10000+255*$100+255
			'WritePixelFast x,y,rgb,buffer2
			WritePixel BS_Texture_MoonQuad2.pixmap, x,y, rgb
			
		Next
		
	Next

	'UnlockBuffer BackBuffer()
	'UnlockBuffer buffer2
	'UnlockBuffer buffer1
	
	FreeEntity tempcam
	FreeEntity sphere
	FreeEntity light
	
End Function


' -----------------------------------------------------------------------------------------------
' Converts FOV (Field of View) angle to Camerazoom value
' -----------------------------------------------------------------------------------------------
Function BS_SetFOV#(angle#)
	
	Return 1.0/Tan(angle/2.0)
	
End Function


' -----------------------------------------------------------------------------------------------
' Set player to a given position and aligns him to the exact height there
' -----------------------------------------------------------------------------------------------
Function BS_InitPlayer#(x#,y#,z#)
	
	PositionEntity BS_Player,x,y,z
	PositionEntity BS_Camera,x,y,z
	
End Function


' -----------------------------------------------------------------------------------------------
' Check User Input (Keyboard / Mouse)
' -----------------------------------------------------------------------------------------------
Function BS_CheckInput()
	
	Local mwheel%
	
	' F1: show help (and replaces Info)
	If VKeyHit(59) Then BS_ShowHelp=1-BS_ShowHelp ; BS_ShowInformation=0
	
	' Key Q/W: change light angle
	BS_ManualUpdate=False
	If VKeyDown(16) Then
		BS_SunAngle=BS_SunAngle-(1.0/10)
		BS_MoonAngle=BS_MoonAngle-(1.0/10)
		BS_ManualUpdate=True
	EndIf
	If VKeyDown(17) Then
		BS_SunAngle=BS_SunAngle+(1.0/10)
		BS_MoonAngle=BS_MoonAngle+(1.0/10)
		BS_ManualUpdate=True
	EndIf
	
	' Key 1-9: predefined angle 0-180° in 22.5° steps
	If VKeyDown(2)  Then BS_SunAngle= 90.0 ; BS_MoonAngle=BS_SunAngle
	If VKeyDown(3)  Then BS_SunAngle=112.5 ; BS_MoonAngle=BS_SunAngle
	If VKeyDown(4)  Then BS_SunAngle=135.0 ; BS_MoonAngle=BS_SunAngle
	If VKeyDown(5)  Then BS_SunAngle=157.5 ; BS_MoonAngle=BS_SunAngle
	If VKeyDown(6)  Then BS_SunAngle=180.0 ; BS_MoonAngle=BS_SunAngle
	If VKeyDown(7)  Then BS_SunAngle=202.5 ; BS_MoonAngle=BS_SunAngle
	If VKeyDown(8)  Then BS_SunAngle=225.0 ; BS_MoonAngle=BS_SunAngle
	If VKeyDown(9)  Then BS_SunAngle=247.5 ; BS_MoonAngle=BS_SunAngle
	If VKeyDown(10) Then BS_SunAngle=270.0 ; BS_MoonAngle=BS_SunAngle
	If VKeyDown(11) Then BS_SunAngle=  0.0 ; BS_MoonAngle=BS_SunAngle
	
	' Backspace = Save screenshot
	BS_SaveScreenshot=False ; If VKeyHit(14) Then BS_SaveScreenshot=True
	
	' Alt GR = show/hide FPS
	If VKeyHit(184) Then BS_ShowFPS=1-BS_ShowFPS
	
	' Space = Wireframe on/off
	If VKeyHit(57) Then BS_ShowWireframe=1-BS_ShowWireframe
	
	' F = Fog on/off
	If VKeyHit(33) Then BS_ShowFog=1-BS_ShowFog
	
	' G = Ground on/off
	If VKeyHit(34) Then BS_ShowGround=1-BS_ShowGround
	
	' T = Twinkle on/off
	If VKeyHit(20) Then BS_ShowStarTwinkle=1-BS_ShowStarTwinkle
	
	' H = Haze on/off
	If VKeyHit(35) Then BS_ShowHaze=1-BS_ShowHaze
	
	' I = show/hide Infos
	If VKeyHit(23) Then BS_ShowInformation=1-BS_ShowInformation ; BS_ShowHelp=0
	
	' L = show/hide Light sources
	If VKeyHit(38) Then BS_ShowLights=1-BS_ShowLights
	
	' B = show/hide Brightness
	If VKeyHit(48) Then BS_ShowBrightness=1-BS_ShowBrightness
	
	' S = show/hide Sky
	If VKeyHit(31) Then BS_ShowSky=1-BS_ShowSky
	
	' O = show/hide Ocean
	If VKeyHit(24) Then BS_ShowOcean=1-BS_ShowOcean
	
	' Mousewheel: change FOV angle
	mwheel=MouseZSpeed()
	If mwheel&lt;0 Then
		BS_CameraFOV=BS_CameraFOV+5
		If BS_CameraFOV&gt;150 Then BS_CameraFOV=150
		CameraZoom BS_Camera,BS_SetFOV(BS_CameraFOV)
	EndIf
	If mwheel&gt;0 Then
		BS_CameraFOV=BS_CameraFOV-5
		If BS_CameraFOV&lt;30 Then BS_CameraFOV=30
		CameraZoom BS_Camera,BS_SetFOV(BS_CameraFOV)
	EndIf
	
End Function


' -----------------------------------------------------------------------------------------------
' Init additional elements like cam, light, water
' -----------------------------------------------------------------------------------------------
Function BS_InitScene()
	
	Local scale#
	
	BS_Skypivot=CreatePivot()
	
	' Create Player
	BS_Player=CreatePivot()
	
	' Create Camera
	BS_Camera=CreateCamera()
	EntityType BS_Camera,1
	EntityRadius BS_Camera,BS_CameraCollisionRadius
	TurnEntity BS_Camera,0,-BS_PlayerStartR,0
	
	' Lightpivots
	BS_SunLightPivot=CreatePivot()
	BS_MoonLightPivot=CreatePivot()
	
	' Sun/Moon Pivot
	BS_SunPivot=CreatePivot()
	BS_MoonPivot=CreatePivot()
	
	' create main pivot for all places
	BS_PlacesPivot=CreatePivot()
	
	' Suns
	BS_Sun1=BS_CreateQuad(255,255,255,1.0,1+2+8)
	EntityTexture BS_Sun1,BS_Texture_Sun1
	EntityParent BS_Sun1,BS_SunPivot
	PositionEntity BS_Sun1,-128,0,0

	BS_Sun2=BS_CreateQuad(255,192,192,0.5,1+2+8)
	EntityTexture BS_Sun2,BS_Texture_Sun2
	EntityParent BS_Sun2,BS_SunPivot
	PositionEntity BS_Sun2,-128,0,0
	EntityBlend BS_Sun2,3

	BS_Sun3=BS_CreateQuad(255,192,128,0.25,1+2+8)
	EntityTexture BS_Sun3,BS_Texture_Sun2
	EntityParent BS_Sun3,BS_SunPivot
	PositionEntity BS_Sun3,-128,0,0
	EntityBlend BS_Sun3,3
	
	' Sunshines
	BS_SunBright1=BS_CreateQuad(255,192,96,1,1+2+8+32)
	EntityParent BS_SunBright1,BS_SunPivot
	PositionEntity BS_SunBright1,-128,0,0
	EntityTexture BS_SunBright1,BS_Texture_Sunglow1,0,1

	BS_SunBright2=BS_CreateQuad(0,0,0,0,2+8+32)
	EntityParent BS_SunBright2,BS_SunPivot
	PositionEntity BS_SunBright2,-128,0,0
	EntityTexture BS_SunBright2,BS_Texture_Sunglow2,0,1
	
	' Moons
	BS_Moon1=BS_CreateQuad(0,0,0,0,1+2+8)
	EntityParent BS_Moon1,BS_MoonPivot
	PositionEntity BS_Moon1,-128,0,0
	EntityBlend BS_Moon1,3

	BS_Moon2=BS_CreateQuad(0,0,0,0,1+2+8)
	EntityParent BS_Moon2,BS_MoonPivot
	PositionEntity BS_Moon2,-128,0,0

	' Sunglow
	BS_SunGlow=BS_CreateQuad(255,255,255,1,1+2+8+32)
	EntityTexture BS_SunGlow,BS_Texture_Sun2
	EntityParent BS_SunGlow,BS_SunPivot
	PositionEntity BS_SunGlow,-128,0,0
	
	' Moonshines
	BS_MoonBright1=BS_CreateQuad(0,0,0,0,1+2+8+32)
	EntityParent BS_MoonBright1,BS_MoonPivot
	PositionEntity BS_MoonBright1,-128,0,0
	EntityTexture BS_MoonBright1,BS_Texture_Sunglow2,0,1
	EntityBlend BS_MoonBright1,1
	BS_MoonBright2=BS_CreateQuad(0,0,0,0,1+2+8+32)
	EntityParent BS_MoonBright2,BS_MoonPivot
	PositionEntity BS_MoonBright2,-128,0,0
	EntityTexture BS_MoonBright2,BS_Texture_Sunglow2,0,1
	EntityBlend BS_MoonBright2,5
	
	' Sunlight source
	BS_SunLight=CreateLight(BS_SunLightType)
	EntityParent BS_SunLight,BS_SunLightPivot
	PositionEntity BS_SunLight,-BS_Scale*10,0,0
	
	' Moonlight source
	BS_MoonLight=CreateLight(BS_MoonLightType)
	EntityParent BS_MoonLight,BS_MoonLightPivot
	PositionEntity BS_MoonLight,-BS_Scale*10,0,0
	
	' Shore
	BS_Shore=BS_CreatePatch(BS_ShorePatchResolution,1.0/BS_ShorePatchResolution,0,0,0,255,255,255,0.5,2+16+32)
	If BS_IsIsland Then
		EntityTexture BS_Shore,BS_Texture_HorizonMask,0,1
		ScaleEntity BS_Shore,BS_Scale*2,1,BS_Scale*2
	Else
		ScaleEntity BS_Shore,BS_Scale,1,BS_Scale
	EndIf
	EntityTexture BS_Shore,BS_Texture_Water1,0,2
	EntityTexture BS_Shore,BS_Texture_Water2,0,3
	EntityBlend BS_Shore,3
	
	' Water
	BS_Ocean=BS_CreatePatch(BS_OceanPatchResolution,1.0/BS_OceanPatchResolution,0,0,0,0,0,0,0,2+16+32)
	If BS_IsIsland Then
		EntityTexture BS_Ocean,BS_Texture_HorizonMask,0,1
		ScaleEntity BS_Ocean,BS_Scale*2,1,BS_Scale*2
	Else
		ScaleEntity BS_Ocean,BS_Scale,1,BS_Scale
	EndIf
	
	' Ground
	BS_Ground=BS_CreatePatch(BS_GroundPatchResolution,1.0/BS_GroundPatchResolution,0,0,0,0,0,0,1,2+32)
	PositionEntity BS_Ground,0,BS_Minheight*1.1,0
	EntityTexture BS_Ground,BS_Texture_HorizonMask,0,1
	
	' Strato Ice Clouds
	BS_Stratosphere=BS_CreatePatch(16,1.0/16,0,0,0,0,0,0,1.0,2+8+16+32)
	BS_DeformMesh(BS_Stratosphere,BS_DomeBulge,17,17,256.0*16,255,255,255,False)
	ScaleEntity BS_Stratosphere,128,128,128
	EntityParent BS_Stratosphere,BS_Skypivot
	UpdateNormals BS_Stratosphere
	EntityTexture BS_Stratosphere,BS_Texture_Clouds3,0,1
	
	' Sky with Clouds
	BS_Sky=BS_CreatePatch(16,1.0/16,0,0,0,0,0,0,1.0,2+8+16+32)
	BS_DeformMesh(BS_Sky,BS_DomeBulge,17,17,256.0*16,255,255,255,False)
	ScaleEntity BS_Sky,128,128,128
	EntityParent BS_Sky,BS_Skypivot
	UpdateNormals BS_Sky
	EntityTexture BS_Sky,BS_Texture_Clouds1,0,2
	EntityTexture BS_Sky,BS_Texture_Clouds2,0,3
	
	' Horizon Haze
	BS_Haze=BS_InitHaze(1,BS_HazeHeight,BS_Hazesegments,1+2+8+32,255,255,255,1,1)
	EntityTexture BS_Haze,BS_Texture_HazeGradient,0,1
	ScaleEntity BS_Haze,128,128,128
	EntityParent BS_Haze,BS_Skypivot
	
	' Star Hemisphere with Fix stars
	BS_Starsphere=BS_InitStarSphere(BS_Stars,BS_StarSizeMin,BS_StarSizeMax,BS_FixStarsProportion,BS_StarMinBrightness,BS_StarMaxBrightness,16,1.0,1+2+8+32)
	EntityTexture BS_Starsphere,BS_Texture_SingleStar,0,1
	ScaleEntity BS_Starsphere,128,128,128
	EntityParent BS_Starsphere,BS_Skypivot
	
	' scale sky objects
	scale=BS_SkyObjectsMulti
	ScaleEntity BS_Sun1,		scale*0.25,scale*0.25,scale*0.25
	ScaleEntity BS_Sun2,		scale*0.50,scale*0.50,scale*0.50
	ScaleEntity BS_Moon1,		scale*0.33,scale*0.33,scale*0.33
	ScaleEntity BS_Moon2,		scale*0.33,scale*0.33,scale*0.33
	ScaleEntity BS_MoonBright1,	scale*2.00,scale*2.00,scale*2.00
	ScaleEntity BS_MoonBright2,	scale*4.00,scale*4.00,scale*4.00
	
	' Reorder sky entities
	EntityOrder BS_Starsphere,50
	EntityOrder BS_Moon2,41
	EntityOrder BS_Moon1,40
	EntityOrder BS_SunGlow,33
	EntityOrder BS_Stratosphere,32
	EntityOrder BS_Sky,31
	EntityOrder BS_Haze,30
	EntityOrder BS_MoonBright1,24
	EntityOrder BS_MoonBright2,23
	EntityOrder BS_SunBright1,22
	EntityOrder BS_SunBright2,21
	EntityOrder BS_Sun3,12
	EntityOrder BS_Sun2,11
	EntityOrder BS_Sun1,10
	
End Function


' -----------------------------------------------------------------------------------------------
' Updates a Patch with new ARGB
' -----------------------------------------------------------------------------------------------
Function BS_UpdatePatch(mesh:TMesh,r%,g%,b%,a#,usealpha%=False,usevertexcolors%=False)
	
	Local surf:TSurface=GetSurface(mesh,1)
	Local v%
	
	For v=0 To CountVertices(surf)-1
		If usealpha Then a=VertexAlpha(surf,v)
		If usevertexcolors Then
			r=VertexRed(surf,v)
			g=VertexGreen(surf,v)
			b=VertexBlue(surf,v)
		EndIf
		
		VertexColor surf,v,r,g,b,a
	Next
	
End Function


' -----------------------------------------------------------------------------------------------
' Updates light FX by angle
' -----------------------------------------------------------------------------------------------
Function BS_UpdateScene()
	
	Local sunsize#,time%,scale#,xx#,yy#,moonlight%
	Local aspectx#,aspecty#,aspectz#
	
	' small increase of sun and moon angle (only if no manual change is running)
	If Not BS_ManualUpdate Then
		BS_SunAngle=BS_SunAngle+(1.0/250)
		BS_MoonAngle=BS_MoonAngle+(1.0/250)
	EndIf
	
	' Limit angle to 0...360 degrees
	BS_SunAngle=BS_SunAngle Mod 360.0 ; If BS_SunAngle&lt;0 Then BS_SunAngle=360.0
	BS_MoonAngle=BS_MoonAngle Mod 360.0 ; If BS_MoonAngle&lt;0 Then BS_MoonAngle=360.0
	
	' calc values for time, sunsize and scale from current sunangle
	time=Int(BS_SunAngle*(BS_ColorSteps/360.0))
	sunsize=BS_Normalize(Sunsizes[time],0,255,0.0,1.75)
	scale=32.0
	
	' only continue if angle has been changed
	If BS_SunAngle&lt;&gt;BS_OldSunAngle Then
		
		' rotate light angle and point light sources/quads to pivot/cam
		
		' problem here????
		'----------------------
		RotateEntity BS_SunPivot,-BS_SunInclination,0,BS_SunAngle+90
		RotateEntity BS_SunLightPivot,-BS_SunInclination,0,BS_SunAngle+90
		RotateEntity BS_MoonPivot,-BS_MoonInclination,0,BS_MoonAngle-90
		RotateEntity BS_MoonLightPivot,-BS_MoonInclination,0,BS_MoonAngle-90

		PointEntity BS_SunLight,BS_SunLightPivot
		PointEntity BS_MoonLight,BS_MoonLightPivot
		PointEntity BS_Moon1,BS_Camera
		PointEntity BS_Moon2,BS_Camera
		PointEntity BS_Sun1,BS_Camera
		PointEntity BS_Sun2,BS_Camera
		PointEntity BS_Sun3,BS_Camera
		PointEntity BS_SunGlow,BS_Camera
		PointEntity BS_SunBright1,BS_Camera',1
		PointEntity BS_SunBright2,BS_Camera',1
		PointEntity BS_MoonBright1,BS_Camera
		PointEntity BS_MoonBright2,BS_Camera
		
		
		' Set Gradient Colors
		AmbientLight AmbientR[time],AmbientG[time],AmbientB[time]
		
		' update cam
		CameraClsColor BS_Camera,CLScolorR[time],CLScolorG[time],CLScolorB[time]
		CameraFogColor BS_Camera,HazeColR[time],HazeColG[time],HazeColB[time]
		
		' update lights
		moonlight=MoonAlpha[time]*(90-BS_MoonPhase)/180.0
		LightColor BS_SunLight,LightR[time],LightG[time],LightB[time]
		LightColor BS_MoonLight,moonlight,moonlight*1.25,moonlight*1.5
		LightRange BS_SunLight,SunLightRange[time]/255.0*BS_SunLightIntensity*BS_Scale
		LightRange BS_MoonLight,MoonLightRange[time]/255.0*BS_MoonLightIntensity*BS_Scale*(MoonAlpha[time]/255.0)
		
		' update patches / quads
		BS_UpdatePatch(BS_Moon1,255,255,255,MoonAlpha[time]/255.0)
		BS_UpdatePatch(BS_Moon2,CLScolorR[time],CLScolorG[time],CLScolorB[time],1)
		BS_UpdatePatch(BS_MoonBright1,255,255,255,(MoonAlpha[time]/255.0)*(90-BS_MoonPhase)/135.0)
		BS_UpdatePatch(BS_MoonBright2,128,192,255,(MoonAlpha[time]/255.0)*(90-BS_MoonPhase)/270.0)
		BS_UpdatePatch(BS_Haze,HazeColR[time],HazeColG[time],HazeColB[time],1,True,False)
		BS_UpdatePatch(BS_Ocean,OceanR[time],OceanG[time],OceanB[time],BS_WaterAlpha)
		BS_UpdatePatch(BS_SunBright2,SunLightR[time],SunLightG[time],SunLightB[time],0.75*sunsize^sunsize)
		BS_UpdatePatch(BS_Starsphere,255,255,255,StarAlpha[time]/255.0,False,True)
		
		' Sun aspect
		aspectx#=SunAspectX[time]/16.0
		aspecty#=SunAspectY[time]/16.0
		aspectz#=SunAspectZ[time]/16.0
		'ScaleEntity BS_SunBright1,aspectx*BS_SkyObjectsMulti*0.5,aspecty*BS_SkyObjectsMulti*0.5,aspectz*BS_SkyObjectsMulti*0.5
		'ScaleEntity BS_SunBright2,aspectx*BS_SkyObjectsMulti*1.0,aspecty*BS_SkyObjectsMulti*1.0,aspectz*BS_SkyObjectsMulti*1.0
		
		' adaptive ground resize to prevent ugly horizon lines
		xx=1+Exp(1.0/BS_CamY*50) ; If xx&gt;20 Or BS_CamY&lt;0 Then xx=20
		yy=(1-(1.0/Exp(BS_CamY*0.001)))
		ScaleEntity BS_Ground,BS_Scale*xx,1,BS_Scale*xx
		PositionEntity BS_Ground,0,BS_Minheight*1.1,0
		
		' scale sky objects
		ScaleEntity BS_Sun3,scale*sunsize^2,scale*sunsize^2,scale*sunsize^2
		
		' Water movement
		If BS_ShoreMove Then
			BS_ShoreMove1=(BS_ShoreMove1+(0.2*BS_ShoreSpeed)) Mod 360
			BS_ShoreMove2=(BS_ShoreMove2+(0.1*BS_ShoreSpeed)) Mod 360
		EndIf
		PositionEntity BS_Shore,0,BS_WaterLevel+Sin(BS_ShoreMove1)+(Sin(BS_ShoreMove2)/10.0),0
		PositionEntity BS_Ocean,0,BS_WaterLevel+Sin(BS_ShoreMove1)+(Sin(BS_ShoreMove2)/10.0),0
		
		' Wave simulation
		BS_WaveMove1=BS_SunAngle/180.0
		BS_WaveMove2=BS_SunAngle/90.0
		PositionTexture BS_Texture_Water1,-BS_WaveMove2*BS_WaterSpeed,-BS_WaveMove2*BS_WaterSpeed
		PositionTexture BS_Texture_Water2,-BS_WaveMove1*BS_WaterSpeed,BS_WaveMove1*BS_WaterSpeed
		
		' Cloud movement
		BS_CloudMove=BS_SunAngle/360.0
		PositionTexture BS_Texture_Clouds1,-BS_CloudMove*BS_CloudSpeed,0
		PositionTexture BS_Texture_Clouds2,-BS_CloudMove*BS_CloudSpeed,0
		PositionTexture BS_Texture_Clouds3,-BS_CloudMove*BS_CloudSpeed,0
		
		' Star twinkle
		If BS_ShowStarTwinkle Then BS_UpdateStarTwinkle()
		
		BS_OldSunAngle=BS_SunAngle
		
	EndIf
	
	' Sun brightness FX
	If BS_ShowBrightness Then BS_UpdateBrightness()

End Function


' -----------------------------------------------------------------------------------------------
' Animates star twinkle
' -----------------------------------------------------------------------------------------------
Function BS_UpdateStarTwinkle()
	
	Local s:bbstartwinkle
	Local surf:TSurface=GetSurface(BS_Starsphere,1)
	For s:bbstartwinkle = EachIn startwinkle_list
		s.brightness=(s.brightness+s.frequency) Mod 180
		
		BS_UpdateStar(surf,s.id*2,(Sin(s.brightness)*(s.sourcecolor/2))+(s.sourcecolor/2))
	Next
		
End Function


' -----------------------------------------------------------------------------------------------
' Updates Sun Brightness FX
' -----------------------------------------------------------------------------------------------
Function BS_UpdateBrightness()
	
	Local sx#,sy#,sz#
	Local mx#,my#,mz#
	Local brightness#,a#,x#,y#
	
	' get sun quad position in 3D space
	sx=EntityX(BS_Sun1,1)
	sy=EntityY(BS_Sun1,1)
	sz=EntityZ(BS_Sun1,1)
	CameraProject BS_Camera,sx,sy,sz
	
	' calculate 2D position of sun quad
	x=1-Abs(BS_Normalize(ProjectedX(),0,BS_ScreenWidth-1,-1,1))
	y=1-Abs(BS_Normalize(ProjectedY(),0,BS_ScreenHeight-1,-1,1))
	If x&lt;0 Then x=0
	If y&lt;0 Then y=0
	
	' exponential brightness
	brightness=(Exp(x)*Exp(y))
	
	' Normalize value and update sunglow quad size
	a=BS_Normalize(brightness,0,6,0.0,1)
	BS_UpdatePatch(BS_SunGlow,255,255,255,a,False,True)
	ScaleEntity BS_SunGlow,BS_BrightnessMulti*a,BS_BrightnessMulti*a,BS_BrightnessMulti*a
	
	' get moon quad position in 3D space
	mx=EntityX(BS_Moon1,1)
	my=EntityY(BS_Moon1,1)
	mz=EntityZ(BS_Moon1,1)
	CameraProject BS_Camera,mx,my,mz
	
	' calculate 2D position of sun quad
	x=1-Abs(BS_Normalize(ProjectedX(),0,BS_ScreenWidth-1,-1,1))
	y=1-Abs(BS_Normalize(ProjectedY(),0,BS_ScreenHeight-1,-1,1))
	If x&lt;0 Then x=0
	If y&lt;0 Then y=0
	
	' exponential brightness
	brightness=(Exp(x)*Exp(y))
	
	' Normalize value and update sunglow quad size
	a=BS_Normalize(brightness,0,6,0.25,1)
	ScaleEntity BS_MoonBright1,BS_BrightnessMulti*a*0.33,BS_BrightnessMulti*a*0.33,BS_BrightnessMulti*a*0.33
	ScaleEntity BS_MoonBright2,BS_BrightnessMulti*a,BS_BrightnessMulti*a,BS_BrightnessMulti*a
	
End Function


' -----------------------------------------------------------------------------------------------
' Updates a single star
' -----------------------------------------------------------------------------------------------
Function BS_UpdateStar(surf:TSurface,triangle%,col%)
	
	Local tri%,v0%,v1%,v2%
	
	For tri=triangle To triangle+1
		
		v0=TriangleVertex(surf,tri,0)
		v1=TriangleVertex(surf,tri,1)
		v2=TriangleVertex(surf,tri,2)
		VertexColor surf,v0,col,col,col,VertexAlpha(surf,v0)
		VertexColor surf,v1,col,col,col,VertexAlpha(surf,v1)
		VertexColor surf,v2,col,col,col,VertexAlpha(surf,v2)
		
	Next
	
End Function


' -----------------------------------------------------------------------------
' BS_Normalize a value
' -----------------------------------------------------------------------------
Function BS_Normalize#(value#=128.0,value_min#=0.0,value_max#=255.0,norm_min#=0.0,norm_max#=1.0)
	
	Return ((value#-value_min#)/(value_max#-value_min#))*(norm_max#-norm_min#)+norm_min#
	
End Function


' -----------------------------------------------------------------------------------------------
' Create a simple quad, with alpha and fx
' -----------------------------------------------------------------------------------------------
Function BS_CreateQuad:TMesh(r%=255,g%=255,b%=255,alpha#=1.0,fx%=0,centered%=False)
	
	Local mesh:TMesh,surface:TSurface,v1%,v2%,v3%,v4%,s#
	
	If centered Then s#=0.5 Else s#=1.0
	
	mesh=CreateMesh()
	surface=CreateSurface(mesh)
Rem
	v1=AddVertex (surface,-s,  s,0,1,0)
	v2=AddVertex (surface, s,  s,0,0,0)
	v3=AddVertex (surface,-s, -s,0,1,1)
	v4=AddVertex (surface, s, -s,0,0,1)
	
	VertexColor surface,v1,r,g,b,alpha
	VertexColor surface,v3,r,g,b,alpha
	VertexColor surface,v2,r,g,b,alpha
	VertexColor surface,v4,r,g,b,alpha
	
	AddTriangle(surface,v2,v4,v3)
	AddTriangle(surface,v2,v3,v3)
End Rem

    AddVertex surface, -s,  s, 0, 0,0 
    AddVertex surface,  s,  s, 0, 1,0
    AddVertex surface, -s, -s, 0, 0,1 
    AddVertex surface,  s, -s, 0, 1,1

	AddTriangle surface,0,1,2
	AddTriangle surface,2,1,3
	
	EntityFX mesh,fx
	
	FlipMesh mesh
	
	Return mesh
	
End Function


' -----------------------------------------------------------------------------------------------
' Create a simple patch with alpha and fx
' -----------------------------------------------------------------------------------------------
Function BS_CreatePatch:TMesh(size%,scale#,px#,py#,pz#,r%,g%,b%,a#,fx%)
	
	Local x%,z%,v#,u#,v0%,v1%,v2%,v3%
	
	' create mesh and surface
	Local mesh:TMesh=CreateMesh()
	Local surf:TSurface=CreateSurface(mesh)
	
	For z=0 To size
		
		For x=0 To size
			
			' calculate uv coordinates that the texture fits to the tile
			u=x*1.0/size
			v=z*1.0/size*-1
			
			' set vertexposition
			BS_VertexBuffer(x,z)=AddVertex (surf,-((size)/2.0)+x,0.0,-((size)/2.0)+z,u,v)
			VertexColor surf,BS_VertexBuffer(x,z),r,g,b,a#
			
		Next
		
	Next
	
	' set triangles
	For z=0 To size-1
		
		For x=0 To size-1
			
			v0=BS_VertexBuffer(x,z)
			v1=BS_VertexBuffer(x+1,z)
			v2=BS_VertexBuffer(x+1,z+1)
			v3=BS_VertexBuffer(x,z+1)
			
			AddTriangle (surf,v0,v2,v1)
			AddTriangle (surf,v0,v3,v2)
			
		Next
		
	Next
	
	' position, scale and fx
	PositionEntity mesh,px,py,pz
	ScaleMesh mesh,scale,scale,scale
	EntityFX mesh,fx
	
	Return mesh
	
End Function


' -----------------------------------------------------------------------------------------------
' Mesh Deform, uses a heightmap for deformation
' -----------------------------------------------------------------------------------------------
Function BS_DeformMesh(mesh:TMesh,image:TTexture,sizex%,sizez%,divider#,r%,g%,b%,heightalpha=True)
	
	Local x%,z%,h#,index%
	
	Local surf:TSurface=GetSurface(mesh,1)
	'Local buffer%=ImageBuffer(image)
	
	'LockBuffer buffer
	
	For z=0 To sizex-1
		
		For x=0 To sizez-1
			
			index = x + (sizex*z)
			
			'h=(ReadPixelFast(x,z,buffer) &amp; $ff0000)/$10000
			h=(ReadPixel(image.pixmap,x,z) &amp; $ff0000)/$10000
			
			VertexCoords surf,index,VertexX(surf,index),h/divider,VertexZ(surf,index)
			
			If heightalpha Then
				VertexColor surf,index,r,g,b,BS_Normalize(h,0,255,0.0,1.0)
			Else
				VertexColor surf,index,r,g,b,1
			EndIf
			
		Next
		
	Next
	
	'UnlockBuffer buffer
	
End Function


' -----------------------------------------------------------------------------------------------
' Creates a smooth gradient out of a few data field RGB values and a percentage value
' -----------------------------------------------------------------------------------------------
Function BS_CreateGradient(colors%,steps%,inverse=False,R%[],G%[],B%[])
	
	Global BS_GradientPercent[colors+1]
	Global BS_GradientRed[colors+1],BS_GradientGreen[colors+1],BS_GradientBlue[colors+1]
	
	Local i%,pos1%,pos2%,pdiff%
	Local rdiff%,gdiff%,bdiff%
	Local rstep#,gstep#,bstep#
	Local counter%=0
	
	' read data, inverse if set
	If inverse Then
		For i=colors To 1 Step -1
			ReadData BS_GradientPercent(i),BS_GradientRed(i),BS_GradientGreen(i),BS_GradientBlue(i)
			BS_GradientPercent(i)=100.0-BS_GradientPercent(i)
		Next
	Else
		For i=0 To colors-1
			ReadData BS_GradientPercent(i),BS_GradientRed(i),BS_GradientGreen(i),BS_GradientBlue(i)
		Next
	EndIf
	
	' gradient loop
	While counter&lt;colors
		
		' convert percent to step position
		pos1=BS_GradientPercent(counter)*steps*1.0/100
		pos2=BS_GradientPercent(counter+1)*steps*1.0/100
		
		' calc delta and steps
		pdiff=pos2-pos1
		rdiff%=BS_GradientRed(counter)-BS_GradientRed(counter+1)
		gdiff%=BS_GradientGreen(counter)-BS_GradientGreen(counter+1)
		bdiff%=BS_GradientBlue(counter)-BS_GradientBlue(counter+1)
		rstep#=rdiff*1.0/pdiff
		gstep#=gdiff*1.0/pdiff
		bstep#=bdiff*1.0/pdiff
		
		' calc new colors and store to blitzarray
		For i=0 To pdiff
			
			R[pos1+i]=Int(BS_GradientRed(counter)-(rstep*i))
			G[pos1+i]=Int(BS_GradientGreen(counter)-(gstep*i))
			B[pos1+i]=Int(BS_GradientBlue(counter)-(bstep*i))
			
		Next
		
		' increase counter
		counter=counter+1
		
	Wend
	
End Function


' -----------------------------------------------------------------------------------------------
' Creates a star Hemisphere
' -----------------------------------------------------------------------------------------------
Function BS_InitStarSphere:TMesh(stars%,fmin#,fmax#,fix#,mincol%,maxcol%,range#,scale#=1.0,fx%=False)
	
	Local star:TMesh,mesh:TMesh,surf:TSurface
	Local i%,size#,h%
	Local starangle#
	
	' create a single Star quad
	star=BS_CreateQuad()
	
	' create the hemisphere
	mesh = CreateMesh()
	surf = CreateSurface(mesh)
	If fx Then EntityFX mesh,fx
	
	' create stars
	For i=1 To stars
		
		' Add Star Twinkle ID and random brightness start
		Local s:bbstartwinkle = New bbstartwinkle
		s.id=i-1
		s.brightness=Rand(0,360)
		
		' create new surface if there are too many vertices
		If CountVertices(surf)&gt;=65532 Then surf=CreateSurface(mesh)
		
		' Star size
		size=Rnd(fmin,fmax)*scale
		If Rnd(1)&gt;(1.0-fix) Then size=fmax*1.5*scale
		ScaleEntity star,size,size,size
		
		' reposition quad to camera position
		PositionEntity star,BS_CamX,BS_CamY,BS_CamZ
		
		' turn starquad, move away and point back to cam
		RotateEntity star,0,0,0
		starangle=Rnd(0,Rnd(0,-90))
		TurnEntity star,starangle,Rnd(0,360),0
		MoveEntity star,0,0,range*scale
		PointEntity star,BS_Camera
		
		' Star Color
		If starangle&gt;-10 Then h=BS_Normalize(starangle,0,-10,0,mincol) Else h=Rand(mincol,maxcol)
		
		' Star Twinkle Source Color
		s.sourcecolor=h
		s.frequency=Rnd(1,5)
		
		' Add star quad to star hemisphere
		BS_AddToMesh(star,mesh,h,h,h,1)
		
	Next
	
	' delete star quad
	FreeEntity star
	
	Return mesh
	
End Function


' -----------------------------------------------------------------------------------------------
' Add a mesh to another mesh (first surface only)
' -----------------------------------------------------------------------------------------------
Function BS_AddToMesh(source:TMesh,target:TMesh,r%,g%,b%,a#) 
	
	Local vert%[3],vr%[3],vg%[3],vb%[3],va#[3]
	Local oldvert%,i1%,i2%
	Local surf1:TSurface=GetSurface(source,1)
	Local surf2:TSurface=GetSurface(target,1)
	
	For i1=0 To CountTriangles(surf1)-1
		
		For i2=0 To 2
			
			oldvert = TriangleVertex(surf1,i1,i2)
			
			vr[i2]=r
			vg[i2]=g
			vb[i2]=b
			va[i2]=a
			
			TFormPoint VertexX(surf1,oldvert),VertexY(surf1,oldvert),VertexZ(surf1,oldvert),source,target 
			vert[i2]=AddVertex(surf2,TFormedX(),TFormedY(),TFormedZ(),VertexU(surf1,oldvert),VertexV(surf1,oldvert)) 
			VertexColor surf2,vert[i2],r,g,b,a
			
		Next 
		
		AddTriangle(surf2,vert[0],vert[1],vert[2])
		
	Next 
	
End Function


' -----------------------------------------------------------------------------------------------
' Free flight camera mode
' -----------------------------------------------------------------------------------------------
Function BS_FreeCam()
	
	Local movex#,movez#,dx#,dy#,dk#,dt%,s#,t%
	Local movespeed#,pitch#
	Local up%=0
	
	ShowMouse()
	
	BS_SpeedMultiplicator=1
	
	' LMB/RMB: move up / speed increase
	If MouseDown(1) Then up=1
	If VKeyDown(54) Then BS_SpeedMultiplicator=25
	If MouseDown(2) Then BS_SpeedMultiplicator=100
	
	' Arrows = Move
	movex=VKeyDown(205)-VKeyDown(203)
	movez=VKeyDown(200)-VKeyDown(208)
	
	movespeed=BS_PlayerSpeed*BS_SpeedMultiplicator
	
	' smooth camera movement
	t=MilliSecs()
	dt=t-BS_DeltaTimeOld
	BS_DeltaTimeOld=t
	dk=Float(dt)/16.666
	s=0.1*dk
	dx=(BS_ScreenWidth/2-MouseX())*0.01*dk
	dy=(BS_ScreenHeight/2-MouseY())*0.01*dk
	TurnEntity BS_Camera,-dy,dx*s*8,0
	pitch=EntityPitch(BS_Camera,1)
	If pitch&gt;85 Then pitch=85 Else If pitch&lt;-85 Then pitch=-85
	RotateEntity BS_Camera,pitch,EntityYaw(BS_Camera,1),0,1	
	MoveEntity BS_Camera,movex*movespeed,up*movespeed,movez*movespeed
	
End Function


' -----------------------------------------------------------------------------------------------
' Gets current positions of Player and Camera and repositions Pivots to Camera Position
' -----------------------------------------------------------------------------------------------
Function BS_UpdatePositions()
	
	' get current Camera position
	BS_CamX=EntityX(BS_Camera)
	BS_CamY=EntityY(BS_Camera)
	BS_CamZ=EntityZ(BS_Camera)
	
	' get current Camera position
	BS_PlayerX=EntityX(BS_Player)
	BS_PlayerY=EntityY(BS_Player)
	BS_PlayerZ=EntityZ(BS_Player)
	
	' attach light and sky pivots to camera position (not vertical)
	PositionEntity BS_SunPivot,BS_CamX,BS_CamY,BS_CamZ
	PositionEntity BS_MoonPivot,BS_CamX,BS_CamY,BS_CamZ
	PositionEntity BS_Skypivot,BS_CamX,BS_CamY,BS_CamZ
	
End Function



' -----------------------------------------------------------------------------------------------
' Color Gradient Source (Percent,R,G,B)
' -----------------------------------------------------------------------------------------------

#BS_Ambientlight
DefData   0.0, 16, 32, 48
DefData  15.0, 16, 32, 48
DefData  25.0,128, 96, 64
DefData  35.0,128,128,128
DefData  50.0,128,128,128
DefData  65.0,128,128,128
DefData  75.0,128, 96, 64
DefData  85.0, 16, 32, 48
DefData 100.0, 16, 32, 48

#BS_Sunsize
DefData   0.0,  0,  0,  0
DefData  10.0, 32, 32, 32
DefData  25.0,192,192,192
DefData  35.0, 96, 96, 96
DefData  50.0, 96, 96, 96
DefData  65.0, 96, 96, 96
DefData  75.0,192,192,192
DefData  90.0, 32, 32, 32
DefData 100.0,  0,  0,  0

#BS_StarLight
DefData   0.0,255,255,255
DefData  15.0,128,128,128
DefData  25.0,  0,  0,  0
DefData  50.0,  0,  0,  0
DefData  75.0,  0,  0,  0
DefData  85.0,128,128,128
DefData 100.0,255,255,255

#BS_MoonAlpha
DefData   0.0,255,255,255
DefData  15.0,255,255,255
DefData  20.0,192,192,192
DefData  25.0, 32, 32, 32
DefData  50.0,  8,  8,  8
DefData  75.0, 32, 32, 32
DefData  80.0,192,192,192
DefData  85.0,255,255,255
DefData 100.0,255,255,255

#BS_Brightness
DefData   0.0,  0,  0,  0
DefData  25.0,255,255,255
DefData  35.0,128,128,128
DefData  50.0, 64, 64, 64
DefData  65.0,128,128,128
DefData  75.0,255,255,255
DefData 100.0,  0,  0,  0

#BS_ClsColor
DefData   0.0,   4, 8, 16
DefData  15.0,   4, 8, 16
DefData  25.0, 32, 48, 64
DefData  35.0,100,150,200
DefData  50.0,100,150,200
DefData  65.0,100,150,200
DefData  75.0, 32, 48, 64
DefData  85.0,   4, 8, 16
DefData 100.0,   4, 8, 16

#BS_HazeColor
DefData   0.0, 12, 24, 48
DefData  15.0, 12, 24, 48
DefData  25.0, 64, 64, 64
DefData  35.0,200,224,255
DefData  50.0,200,224,255
DefData  65.0,200,224,255
DefData  75.0, 64, 64, 64
DefData  85.0, 12, 24, 48
DefData 100.0, 12, 24, 48

#BS_LightColor
DefData   0.0,  0,  0,  0
DefData  15.0,  0,  0,  0
DefData  25.0,255,192,128
DefData  35.0,255,255,192
DefData  50.0,255,255,255
DefData  65.0,255,255,192
DefData  75.0,255,192,128
DefData  85.0,  0,  0,  0
DefData 100.0,  0,  0,  0

#BS_SunLightColor
DefData   0.0,  0,  0,  0
DefData  20.0,128,128, 80
DefData  25.0,255,255,160
DefData  35.0,255,255,192
DefData  50.0,255,255,255
DefData  65.0,255,255,192
DefData  75.0,255,255,160
DefData  80.0,128,128, 80
DefData 100.0,  0,  0,  0

#BS_OceanColor
DefData   0.0,  8, 16, 24
DefData  25.0,  0, 32, 96
DefData  35.0, 32, 64,192
DefData  50.0, 32,128,255
DefData  65.0, 32, 64,192
DefData  75.0,  0, 32, 96
DefData 100.0,  8, 16, 24

#BS_SunAspect
DefData   0.0, 64, 64, 64
DefData  20.0, 64, 64, 64
DefData  25.0,192, 64,192
DefData  35.0, 64, 64, 64
DefData  50.0, 64, 64, 64
DefData  65.0, 64, 64, 64
DefData  75.0,192, 64,192
DefData  80.0, 64, 64, 64
DefData 100.0, 64, 64, 64

#BS_SunLightRange
DefData   0.0,  0,  0,  0
DefData  10.0, 32, 32, 32
DefData  25.0,128,128,128
DefData  35.0,160,160,160
DefData  50.0,192,192,192
DefData  65.0,160,160,160
DefData  75.0,128,128,128
DefData  90.0, 32, 32, 32
DefData 100.0,  0,  0,  0

#BS_MoonLightRange
DefData   0.0,255,255,255
DefData  10.0,128,128,128
DefData  25.0,  0,  0,  0
DefData  35.0,  0,  0,  0
DefData  50.0,  0,  0,  0
DefData  65.0,  0,  0,  0
DefData  75.0,  0,  0,  0
DefData  90.0,128,128,128
DefData 100.0,255,255,255
</textarea><br><br>This is the 'demo' converted:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import sidesign.minib3d
Import "bbtype.bmx"
Import "bbvkey.bmx"

Include "blitzsky.bmx"

Graphics3D 800,600,32,2

Global BS_Screenwidth%=GraphicsWidth()
Global BS_Screenheight%=GraphicsHeight()
Global BS_Scale=32768

BS_InitBlitzSky()

Local dummy:TMesh=CreateCube()
ScaleEntity dummy,28,28,28
EntityTexture dummy, BS_Texture_MoonQuad1

' -----------------------------------------------------------------------------------------------
' Main Loop
' -----------------------------------------------------------------------------------------------
While Not VKeyHit(1)
	
	Local BS_FrameElapsed%,BS_FrameTicks%,BS_FrameTween#,t%
	
	Repeat BS_FrameElapsed=MilliSecs()-BS_FrameTime Until BS_FrameElapsed
	BS_FrameTicks=BS_FrameElapsed/BS_FramePeriod
	BS_FrameTween=Float(BS_FrameElapsed Mod BS_FramePeriod)/Float(BS_FramePeriod)

	For t=1 To BS_FrameTicks
		
		' Update World
		BS_UpdateBlitzSky()
		
		BS_FrameTime=BS_FrameTime+BS_FramePeriod
		'If t=BS_FrameTicks Then CaptureWorld
		
		BS_FreeCam()
		UpdateWorld
		
		' Update Pivots
		BS_UpdatePositions()
		
	Next
	
	
	' tweened render
	RenderWorld 'BS_FrameTween
	
	' Infotext
	If BS_ShowInformation Then BS_UpdateInfos()
	If BS_ShowHelp Then BS_DrawHelp()
	
	' Draw FPS digits
	BS_UpdateFPS()
	
	' Save Screenshot
	'If BS_SaveScreenshot=True Then SaveBuffer (BackBuffer(),Replace(CurrentDate()+" "+CurrentTime()+".bmp",":",""))
	
	Flip 0
	
	' get current FPS value
	FPS=BS_GetFPS()
	
Wend

End
</textarea><br><br>To run this, you also need to convert the DDS textures from BlitzSky to png, since miniB3D has no DDS support.<br><br>Can someone help fix this?? It's a shame having it almost working...<br><br><font class="tiny">Last edited 2010</font> <br><br></td></tr></table><br>
<a name="1060638"></a>

<a name="1060639"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> [edit] nevermind ive found the files, ill have a look at this tonight :)<br><br><font class="tiny">Last edited 2010</font> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
