<!DOCTYPE html><html lang="en" ><head ><title >MiniB3D - MaxGUI vs wxMax</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >MiniB3D - MaxGUI vs wxMax</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=119" >MiniB3D Module</a>/<a href="#bottom" >MiniB3D - MaxGUI vs wxMax</a><br><br>
<a name="1150316"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chapman7</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am comparing MaxGUI's and wxMax's average FPS using a small test with MiniB3D.  I need someone else to look over the code (in particular with wxMax).  I removed the normal '60 Timer' on both but I think the method I used for wxMax may have hindered the outcome.<br><br>MaxGUI Code:<br><pre class=code>
SuperStrict


Import SideSign.MiniB3D
Import MaxGUI.Drivers
SetGraphicsDriver GLGraphicsDriver(), GRAPHICS_BACKBUFFER|GRAPHICS_DEPTHBUFFER


Local Window:TGadget = CreateWindow("MaxGUI + MiniB3D", 50, 50, 512, 512, Null, WINDOW_TITLEBAR)
Local Canvas:TGadget = CreateCanvas(0, 0, ClientWidth(Window), ClientHeight(Window), Window, 0)
SetGadgetLayout Canvas, 1, 1, 1, 1


TGlobal.Width = ClientWidth(Window)
TGlobal.Height = ClientHeight(Window)
TGlobal.Depth = 16
TGlobal.Mode = 0
TGlobal.Rate = 60

SetGraphics CanvasGraphics(Canvas)
TGlobal.GraphicsInit()


Local Camera:TCamera = CreateCamera()
PositionEntity Camera, 0, 0, -10

Local Light:TLight = CreateLight(1)
Local cube:TMesh=CreateCube()
Local sphere:TMesh=CreateSphere()
Local cylinder:TMesh=CreateCylinder()
Local cone:TMesh=CreateCone() 

PositionEntity cube,-6,0,0
PositionEntity sphere,-2,0,0
PositionEntity cylinder,2,0,0
PositionEntity cone,6,0,0

Local old_ms:Int=MilliSecs()
Local renders:Int
Local fps:Int


While True
	SetGraphics CanvasGraphics(Canvas)
	TurnEntity Cube, 0, 1, 0
	RenderWorld
			
			
	renders=renders+1
	If MilliSecs()-old_ms&gt;=1000
		old_ms=MilliSecs()
		fps=renders
		renders=0
	EndIf
	Text 0, 0, "FPS: "+fps
			
	Flip 0 
Wend
</pre><br><br>wxMax Code:<br><pre class=code>
SuperStrict

Framework wx.wxApp
Import wx.wxFrame
Import wx.wxPanel
Import wx.wxGLCanvas
Import sidesign.minib3d

Global prog:MyApp = New MyApp
prog.Run()

Type MyApp Extends wxApp
	Field frame:wxFrame
	Field panel:wxPanel
	Field canvas:TMiniB3D
	Method OnInit:Int()
		frame = New wxFrame.Create(,,"MiniB3d Sample", 0, 0, 512, 512)
		frame.Center()
		panel = New wxPanel.Create(frame, wxID_ANY, 0, 0, 512, 512)
		canvas = TMiniB3D(New TMiniB3D.Create(panel, wxID_ANY, GRAPHICS_BACKBUFFER|GRAPHICS_DEPTHBUFFER, 0, 0, 512, 512))
		frame.show()				
		Return True
	End Method
End Type

Type TMiniB3D Extends wxGLCanvas
	Field init:Int = 0
	Method OnPaint(event:wxPaintEvent)
		If init = 0 Then		
			SetGraphics CanvasGraphics( Self )
			TGlobal.width = 512
			TGlobal.height = 512
			TGlobal.depth = 16
			TGlobal.mode = 0
			TGlobal.GraphicsInit()
			init = 1		
			InitMB3D()		
		End If
		RenderMB3D()
	End Method
End Type


Global Cube:TMesh
Global old_ms:Int=MilliSecs()
Global renders:Int
Global fps:Int


Function InitMB3D()

	Local Camera:TCamera = CreateCamera()
	PositionEntity Camera, 0, 0, -10
	
	Local Light:TLight = CreateLight(1)
	cube:TMesh=CreateCube()
	Local sphere:TMesh=CreateSphere()
	Local cylinder:TMesh=CreateCylinder()
	Local cone:TMesh=CreateCone() 
	
	PositionEntity cube,-6,0,0
	PositionEntity sphere,-2,0,0
	PositionEntity cylinder,2,0,0
	PositionEntity cone,6,0,0
		
	RenderWorld

End Function

Function RenderMB3D()

	TurnEntity cube, 0, 1, 0
	RenderWorld

	renders=renders+1
	If MilliSecs()-old_ms&gt;=1000
		old_ms=MilliSecs()
		fps=renders
		renders=0
	EndIf
	Text 0, 0, "FPS: "+fps
	
	Flip
	prog.canvas.Refresh()

End Function
</pre><br><br>Using an AMD Radeon HD 6570 I get:<br>MaxGUI - 1275<br>wxMax -  950<br>This is in no way a final outcome or result.  Like I mentioned above, I need someone to look at the code for me.<br><br>Cole <br><br></td></tr></table><br>
<a name="1150319"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> In combination with MaxGui try this additional commands:<br><br><pre class=code>....
SetGraphicsDriver GLMax2DDriver()
Local Canvas:TGadget = CreateCanvas(0, 0, 800, 600, Window)

SetGraphics CanvasGraphics(Canvas)

TGlobal.Width = 800	
TGlobal.Height = 600
TGlobal.Depth = 0
TGlobal.Mode = 2
TGlobal.Rate = 60
TGlobal.GraphicsInit()
....
CreateTimer(60)
While WaitEvent()
     .....
</pre> <br><br></td></tr></table><br>
<a name="1150325"></a>

<a name="1150326"></a>

<a name="1150327"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chapman7</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Does GLMax2DDriver() give better benefits over GLGraphicsDriver()?  Other than that, I think the MaxGUI one is already solid.  If you are familiar with wxMax though, you should check the code and see if we can do anything differently.<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1150333"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oh, sorry, I did missunderstand you. I thought you could not get any results with combination MaxGui and MiniB3D also. So I wrote you my standard solution.<br><br>If yours is working, there is no need to change the driver....<br><br>Sorry, I'm not familiar with wxMax. <br><br></td></tr></table><br>
<a name="1150505"></a>

<a name="1150507"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chapman7</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Okay so I was able to tweak the wxMax code a bit better and now the FPS is just shy of MaxGUI's.<br><br>MaxGUI - 1275<br>wxMax - 1250<br><br>I am not sure if anyone can really tweak if further, if they can awesome.  But anyways, here is the code:<br><br><pre class=code>
SuperStrict

Framework wx.wxApp
Import wx.wxFrame
Import wx.wxPanel
Import wx.wxGLCanvas
Import sidesign.minib3d


Global Cube:TMesh
Global old_ms:Int=MilliSecs()
Global renders:Int
Global fps:Int

Global prog:MyApp = New MyApp
prog.Run()

Type MyApp Extends wxAppMain
	Field frame:wxFrame
	Field panel:wxPanel
	Field canvas:TMiniB3D
	Global shouldExit:Int = False
	Method OnInit:Int()
		frame = New wxFrame.Create(,,"MiniB3d Sample", 0, 0, 512, 512)
		frame.Center()
		panel = New wxPanel.Create(frame, wxID_ANY, 0, 0, 512, 512)
		canvas = TMiniB3D(New TMiniB3D.Create(panel, wxID_ANY, GRAPHICS_BACKBUFFER|GRAPHICS_DEPTHBUFFER, 0, 0, 512, 512))
		frame.show()				
		Return True
	End Method
	Method MainLoop:Int()
		While True
		While Not Pending() And ProcessIdle() ; Wend
		While Pending()
			If Not Dispatch() Then
				shouldExit = True
				Exit
			End If
		Wend
		If shouldExit Then
			While pending() 
				dispatch() 
			Wend
			Return 0
		End If

		TurnEntity cube, 0, 1, 0
		RenderWorld
		renders=renders+1
		If MilliSecs()-old_ms&gt;=1000
			old_ms=MilliSecs()
			fps=renders
			renders=0
		EndIf
		Text 0, 0, "FPS: "+fps
		Flip

		Wend
	EndMethod
End Type

Type TMiniB3D Extends wxGLCanvas
	Field init:Int = 0
	Method OnPaint(event:wxPaintEvent)
		If init = 0 Then		
			SetGraphics CanvasGraphics( Self )
			TGlobal.width = 512
			TGlobal.height = 512
			TGlobal.depth = 16
			TGlobal.mode = 0
			TGlobal.GraphicsInit()
			init = 1		
			Local Camera:TCamera = CreateCamera()
			PositionEntity Camera, 0, 0, -10
			
			Local Light:TLight = CreateLight(1)
			cube:TMesh=CreateCube()
			Local sphere:TMesh=CreateSphere()
			Local cylinder:TMesh=CreateCylinder()
			Local cone:TMesh=CreateCone() 
			
			PositionEntity cube,-6,0,0
			PositionEntity sphere,-2,0,0
			PositionEntity cylinder,2,0,0
			PositionEntity cone,6,0,0
			RenderWorld
		End If
	End Method
End Type

</pre><br><br>You have to do this If init = 0 thing in the canvas' OnPaint method because for some reason, it works differently than if you were to put the code in the OnInit method, not sure why though.<br><br>Cole<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
