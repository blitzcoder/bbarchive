<!DOCTYPE html><html lang="en" ><head ><title >Light positions reported correctly?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Light positions reported correctly?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=119" >MiniB3D Module</a>/<a href="#bottom" >Light positions reported correctly?</a><br><br>
<a name="781464"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LT</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> While playing with shaders, I noticed that light positions seem to not be working correctly.  To test, I create a sphere and parent it to the light so that I can always see its position.  The light is parented to a pivot at the origin and I just turn that pivot on every frame.  The effect of the light on the cylinder in the middle is on the opposite side.  What the...?<br><br>If I'm crazy or just doing something stupid, please let me know!<br><br>Btw, does anyone know how to hide the pointer?  HidePointer is not supported in MiniB3D.<br><br><br><pre class=code>
SuperStrict

Framework BRL.FileSystem
Import "../minib3d.bmx"
'Import klepto.minib3d


Local width:Int = 1024, height:Int = 768
Local depth:Int = 32, mode:Int = 2


Graphics3D width, height, depth, mode


' set environment
AmbientLight(0,0,(0.2 * 255))
'HidePointer()


' create camera
Local cam:TCamera = CreateCamera()
PositionEntity cam, 0, 10, -50
CameraRange( cam, .1, 200000 )
SetCameraFOV( cam, 72 )


' create pivot point
Local piv:TPivot = CreatePivot()

' create light
Local light:TLight = CreateLight(0)
EntityParent light, piv
light.LightColor( 255, 55, 55 )

' create mesh for light
Local LDummy:TMesh = CreateSphere( 16, light )
ScaleEntity( LDummy, 10, 10, 10 )

' moving light and mesh should follow
PositionEntity( light, 0, -100, 155 )


' create cylinder
Local ent:TMesh = CreateCylinder( 12 )
ScaleEntity( ent, 20, 100, 20 )

' used by camera code
Local mxs# = 0
Local mys# = 0
Local move# = 5.0
MouseXSpeed() ' flush
MouseYSpeed() ' flush

' used by fps code
Local old_ms:Int = MilliSecs()
Local renders:Int
Local fps:Int = 10
Local anim_time:Float = 0


' main loop
While Not KeyDown( KEY_ESCAPE )		

	' mouse look
	mxs# = mxs# + ( MouseXSpeed() / 5.0 )
	mys# = mys# + ( MouseYSpeed() / 5.0 )
	RotateEntity cam, mys#, -mxs#, 0
	MoveMouse width / 2, height / 2
	MouseXSpeed() ' flush
	MouseYSpeed() ' flush

	' move camera forwards/backwards/left/right with cursor keys
	If KeyDown( KEY_UP ) = True Then MoveEntity cam, 0, 0, move#			' move camera forward
	If KeyDown( KEY_DOWN ) = True Then MoveEntity cam, 0, 0, -move#			' move camera back

	If KeyDown( KEY_LEFT ) = True Then MoveEntity cam, -move#, 0, 0			' move camera left
	If KeyDown( KEY_RIGHT ) = True Then MoveEntity cam , move# , 0 , 0		' move camera right

	TurnEntity piv, 0, -0.5, 0

	UpdateWorld
	RenderWorld
	renders = renders + 1

	' calculate fps
	If MilliSecs() - old_ms &gt;= 1000
		old_ms=MilliSecs()
		fps=renders
		renders=0
	EndIf
	
	
	Text 0, 20, "FPS: " + String( fps )
	Text 0, 40, "Light : " + EntityX( Light, True ) + " : " + EntityY( Light, True ) + " : " + EntityZ( Light, True ) 

	Flip( True )

Wend

Function SetCameraFOV ( cam:TCamera, FOV:Float )
   CameraZoom cam, 1.0 / Tan( FOV# / 2.0 )
End Function
</pre> <br><br></td></tr></table><br>
<a name="781499"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sveinung</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just add 'PointEntity(light,piv)' after you turn the piv.<br><br>HideMouse()<br><br>Sveinung <br><br></td></tr></table><br>
<a name="781501"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >simonh</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> It's because you've created a directional light which is pointing away from the sphere - creating a spot light (type 2) will produce the effect you're expecting. <br><br></td></tr></table><br>
<a name="781577"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LT</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Duh!  And 0 isn't even an option!  I took the code from the klepto shader example and it didn't occur to me that it wasn't a point light.  No wonder!<br><br>Thanks, Simon and Sveinung. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
