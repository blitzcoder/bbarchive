<!DOCTYPE html><html lang="en" ><head ><title >Entityalpha() bug??</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Entityalpha() bug??</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=119" >MiniB3D Module</a>/<a href="#bottom" >Entityalpha() bug??</a><br><br>
<a name="1025517"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hujiklo</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>Function check_picked_tiles()

For Local t:tile_set = EachIn tile_list

   If m_picked = t.mesh
     EntityAlpha t.mesh, 0.50
			
     'FreeEntity t.mesh
     'ListRemove tile_list, t
			
   Else

     EntityAlpha t.mesh, 1.0

EndIf
		
Next
		
If m_picked Then Print "PICKING!!!"
	
End Function</pre><br><br>The code above works if I use Tmesh instead of Tentities - if I use Tentities then only the last Tentity in the list will be set to 0.5 alpha if it is picked - all the others register as being picked but no alpha changes take place...or they all completely disappear except the 'picked' tile which gets alpha set to the correct 0.5.<br><br>What's going wrong - anybody please?<br><br>TMeshes use too much ram compared to Entities.<br><br>By the way - they get deleted fine if they are picked! Just the alpha doesn't change :( <br><br></td></tr></table><br>
<a name="1025519"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hujiklo</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>	For Local t:tile_set = EachIn tile_list
						
	EntityAlpha (t.ent, Rnd(0.0, 1.0))

	Next</pre><br><br><br>GAAAH!!! This works!! It sets them all randomly no problem! <br><br></td></tr></table><br>
<a name="1025549"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hujiklo</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Can anybody fix this? Honestly it's bloody weird and frustrating.<br><br>If I set the alpha value in the tile list <br>(EntityAlpha t.mesh, 1.0) to 0.999999 instead of 1.0 it all works normally!!<br><br>It's got to be a simple fix =[ <br><br></td></tr></table><br>
<a name="1025706"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >simonh</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> To begin with, I would advise using methods rather than functions, e.g.:<br><br>t.ent.EntityAlpha(1.0)<br><br>Rather than<br><br>EntityAlpha(t.ent,1.0)<br><br>The latter way only calls the former method anyway, and so is slower, and occasionally there may be a bug where a parameter isn't being passed from the function to the method.<br><br>If you want me to take a look, send me a working sample (including media) that demonstrates the problem in the smallest possible amount of code. <br><br></td></tr></table><br>
<a name="1025757"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hujiklo</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Okay - I ripped out the problem code and it will run where ever the miniB3D 'inc' folder &amp; "minib3d.bmx" is.<br><br>Main tile response function is at the bottom of the code. As you will see -a picked tile gets a correct entityFX setting but an alpha value is ignored unless it is the very last bottom right tile to be set.<br><br>Set the alpha value to 0.9999 instead of one and it all works.<br><br>Also - in the main loop, try remming out the BeginMax2D() &amp; EndMax2D() functions as this also has a strange effect on the entities - all colour is lost...so what's happening there?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import "minib3d.bmx"
'Import "bbtype.bmx"
'Import "bbvkey.bmx"


Strict


'GLOBAL POINTERS FOR VARIOUS MENU &amp; STARTUP ENTITIES

Global debug_load:Int ' For all quick debug uses

Global real_id:Int ' increased by 1 for every single unique object in game 

Global temp_mesh:TEntity ' more useful as globals for loading b3ds
Global temp_child:TEntity

Global level:Int ' Which game level stage

' LISTS

Global entity_list:TList = New TList ' list For entities
Global tile_list:TList = New TList ' for tiles obviously
' TEXTS

'-----------------
'CAMERA and RES GFX GLOBALS
'-----------------

Global camera:TCamera
Global WindowHandle:Int
Global gfx_changed:Int  ' global is set if GFX are changed
Global flip_value:Int ' fixes flip bug - auto resets vsysnch after window resize 
Global scr_scale_w:Float, scr_scale_h:Float' for scaling graphics
Global scr_aspect:Float ' Let me know which aspect for scale considerations - set in options type
Global desktop_width:Int, desktop_height:Int, desktop_depth:Int


Global sound%, Music%  'on or off
Global Sound_Volume%, Music_Volume%
Global VSync%, Graphics_Driver%, Sound_Driver%, Profile_Name$ = "Player"
Global scr_width:Int = 800, scr_height:Int = 600, scr_depth:Int, scr_mode:Int = 0 ' windowed
Global scr_hrtz:Int, anti_alias:Int


Global old_scr_width:Int = scr_width
Global old_scr_height:Int = scr_height
	



'MOUSE POINTER

Global m_picked:TEntity
Global m_old_picked:TEntity
Global m_clicked:Int ' BECAUSE MOUSEHIT CANNOT BE USED MORE THAN ONCE!!!!!! - add up in do_mouse function

' BUTTONS

' MENU SOUNDS

Global stage:Int
Global game_exit:Int

Type tile_set

	Field gfx_id:Int ' actual tile image or object id relating to the loaded tile gfx type above
	Field real_id:Int ' taken from the global 'id_count' and increased by 1 each new type
	Field ent:TEntity   ' actual entity mesh
	Field x:Float, y:Float, z:Float ' should be integers but positions can interpolate
	Field x_targ:Float, y_targ:Float, z_targ:Float ' target positions
	Field rx:Int, ry:Int, rz:Int' for now I will stick to integers for rotations
	Field rx_targ:Int, ry_targ:Int, rz_targ:Int' Target rotations	
	Field alpha:Float = 1.0
	
	

End Type


'&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;
'MAIN LOOP	
'&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;	


While game_exit = 0

	ShowMouse
	If KeyHit (KEY_ESCAPE) Then game_exit = 1

	'GameTime()' update game timer.bmx Does DELTA too

   		
	'process_game_stages() 
	
	Select True
	
		Case stage = 0
	
			set_up_camera_first()
			setup_game_level()

			stage = 1
	
		Case stage = 1

			generate_tileset()

			stage = 2

		Case stage = 2
			do_temp_stuff_here()

			update_2d_mouse()
			do_mouse_picks()
	
	End Select
	
	RenderWorld
	
	'--------------	
	' 2D GFX
	'--------------	
	BeginMax2D() ' MiniB3D.bmx
	'updatemax2d() ' 2D_stuff.bmx
	EndMax2D() ' MiniB3D.bmx


	'--------------	
	'--------------	
	
	
	Flip (1) ; Cls 'set by game.ini file - vsync adjusted in fadescreen (FIXES BUG!)
	
Wend

 

shutdown() ' in this bmx

'&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;
'MAIN LOOP	
'&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;	




Function shutdown()
	
	End

End Function



Global tile_1:TEntity


Function setup_game_level()

	tile_1 = CreateCube()
	ScaleEntity tile_1, 0.5, 0.5, 0.1
	HideEntity tile_1


End Function






Function set_up_camera_first()


	Graphics3D scr_width, scr_height, scr_depth, scr_mode


	camera = CreateCamera()
	Global cam_zoom:Float = 3
	CameraRange camera, 0.01, 50
	CameraZoom camera, cam_zoom
	PositionEntity camera, 0, 0, -(cam_zoom * 5.0)' Tile sizes parameters change so camera moves in or out
	AntiAlias (anti_alias)
	
	'set_window_up()
	
	HideMouse
	MoveMouse(scr_width / 1.5, scr_height / 2.0)
	
	
	'SET GLOBAL SCREEN RES RELATED SCALES
	
	scr_scale_w = ((100.0 / 640.0) * Float(scr_width)) / 100.0
	scr_scale_h = ((100.0 / 480.0) * Float(scr_height)) / 100.0
	old_scr_width:Int = scr_width ' remember for screen res toggles - sets message test 
	old_scr_height:Int = scr_height' to correct &amp; current screen res
		
		
	'Drop a light in
	make_default_light()


    
End Function


Global light2:TLight

Function make_default_light()

	light2 = CreateLight(2)
	LightColor light2, 255, 255, 255
	PositionEntity light2, 30, 30, -1





End Function







Global l1time:Float
Global l1x:Float, l1y:Float
Global cube:TMesh
Global back_frame_count:Float

Function do_temp_stuff_here()


	If cube = Null
	
		cube = CreateSphere()
		ScaleEntity cube, 0.1, 0.1, 0.1

	End If

	l1time:+1.0


	l1x = (Cos(l1time) * 2)
	l1y = (Sin(l1time) * 2)

	PositionEntity light2, l1x, l1y, -1.0
	PositionEntity cube, l1x, l1y, -1.0


End Function









'************************************************
'MOUSE FUNCTIONS
'************************************************

Function update_2d_mouse()

	
		
	' Need a variable for the whole program 
	' due to dodgy mousehit instruction	
	m_clicked = 0
	
	

	

	If MouseDown(1)
								
		m_clicked = 1
	
	Else
	
		If MouseDown(2)
								
			m_clicked = 2

		End If

	End If

	

	
	
End Function




Global mpx:Float
Global mpy:Float
Global mpz:Float
Global mx_old:Int
Global my_old:Int





Function do_mouse_picks()

	Local mx:Int = MouseX()
	Local my:Int = MouseY()
	Local i2:Int
	
	'PREFER to only do a new mousepick if the mouse has moved
	
	If (mx_old &lt;&gt; mx) Or (my_old &lt;&gt; my) ' CAN TAKE OUT FOR THIS EXAMPLE
		
		mx_old = mx
		my_old = my
		
		
		
		If m_picked &lt;&gt; m_old_picked ' CAN TAKE OUT - no difference
		
							
			check_picked_tiles() 'Call only if different object
					
			m_old_picked = m_picked ' place after check so we conserve old pick
		
					
		
		EndIf
		
		
			
		
	
		m_picked = CameraPick(camera, mx, my)
	
		If m_picked
			mpx = PickedX()
			mpy = PickedY()
			mpz = PickedZ()
					
		EndIf
	
	EndIf
	

End Function




' SET UP A TILESET ACCORDING TO GIVEN PARAMETERS

Global tempx:Float
Global tempy:Float
Global max_width:Int
Global max_height:Int
Global x_toff:Float, y_toff:Float


Function generate_tileset()

	
	SeedRnd MilliSecs()

	max_width = Rand(3, 6)
	max_height = Rand(3, 6)


	Local x:Float, y:Float, z:Float ' for tile positions
	
	Local i:Int = 0
	Local temp:Int = (max_width Mod 2) ' find out if odd or even spacing required


	
	If temp = 0 ' Even number

		x_toff = 1.5 ' offset all x cords by half the tile size..gets tiles neatly on either left or right of centre
		
	Else
		x_toff = 1.5
		
	End If

	
	temp:Int = (max_height Mod 2) ' find out if odd or even spacing required

	'If temp = 0 ' Even number

	y_toff = 1.0 ' offset all Y cords by half the tile size..gets tiles neatly on either left or right of centre

	'End If
	
	
	Local startx:Float = max_width / 2.0
	Local starty:Float = max_height / 2.0
	
	
	If x_toff &gt; 0 Then startx:-x_toff
	If y_toff &gt; 0 Then starty:-y_toff
	
	

	
	
	temp = (max_width * max_height)
	
	' MAKE AND PLACE TILES
	SeedRnd MilliSecs()
	For i = 1 To temp
	
		real_id:+1 ' update unique IDs
	
		Local t:tile_set
		
		t = New tile_set
		
		t.real_id = real_id
		t.gfx_id = 1 ' TEMPORARY
		t.ent = CopyEntity(tile_1, Null)
		
	
		Local r:Int = Rand(30, 255)
		Local g:Int = Rand(30, 255)
		Local b:Int = Rand(30, 255)
		
		EntityColor t.ent, r, g, b
		
		Print "r:" + r
		Print "g:" + g
		Print "b:" + b


		'Local brush:TBrush = CreateBrush(Rand(30, 255), Rand(30, 255) , Rand(30, 255))
		'Local brush:TBrush = CreateBrush (255, 255, 0)
		't.ent.PaintEntity ( brush)
		t.ent.EntityAlpha(t.alpha)
		' BUG FIX!! = 0.999999 - doesn't work with 1.0
		t.ent.EntityPickMode (2, True) ' Polygon &amp; can obscure
		

		
		'-------------------------
		' SET GRID POSITIONS
		'-------------------------
		If x &lt; max_width
			x = x + 1
		Else
		
			If y &gt; - max_height
				y = y - 1
			Else
		
				y = 0
			EndIf
			x = 1
		EndIf
		
	
		
		'X
		t.x = x - startx
		
		If x_toff &gt; 0 Then t.x:-2.0
		
		'Y
		t.y = y + starty
		
		If y_toff &gt; 0 Then t.y:+0.5
			
		'Z
		t.z = z
	
		
		
		t.ent.PositionEntity (t.x, t.y, t.z, True)
		ListAddLast(tile_list, t)
	
				
	Next
	
	tempx = startx
	tempy = starty



End Function








Function check_picked_tiles()

	
		
	For Local t:tile_set = EachIn tile_list
	
	
	
		If m_picked = t.ent
			t.alpha = 0.25
			t.ent.EntityAlpha(t.alpha)' THIS DOESN'T WORK - except for last entity in list
		
			t.ent.EntityFX 1 ' THIS WORKS!!
			
		
		
		Else
	
			't.alpha = 0.99999' TRY THIS - IT WORKS FOR SOME REASON!!?!?!?!
			t.alpha = 1.0
			t.ent.EntityAlpha(t.alpha)
		

			t.ent.EntityFX 0
		End If

	Next
	
	' HUD BUTTONS
	
	
	

End Function
</textarea> <br><br></td></tr></table><br>
<a name="1025766"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jkrankie</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> unedited, your code produces this on my mac:<br><br><img src="http://www.charliesgames.com/wpimages/s1.png"><br><br>Is that how it's supposed to look? The entities get brighter as you mouse over them.<br><br>Cheers<br>Charlie <br><br></td></tr></table><br>
<a name="1025779"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hujiklo</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> No they shouldn't get bigger - I think it's the 'full bright' effect. Place the mouse over the bottom right tile - it will go translucent, but none of the other tiles will when it is over them...this is why I made  them 'full bright' as well, to show that the code is working correctly and that they are being picked correctly. <br><br></td></tr></table><br>
<a name="1025808"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jkrankie</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah, i see what you mean. some of the other tiles turn translucent when the mouse is over the bottom right tile. <br><br>Definitely looks like a bug!<br><br>Cheers<br>Charlie <br><br></td></tr></table><br>
<a name="1025815"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Bug seems to be somewhere in your code, sorry. <br><br>If you use the code below:<br><br>m_picked = CameraPick(camera, mx, my)<br>If m_picked Then entityalpha m_picked, 0.5<br><br>You see that entityalpha works correctly.<br><br>I tried to find what is the problem exactly, but I couldn't find it :( <br><br></td></tr></table><br>
<a name="1025915"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> I still have this adapted version of minib3d, so it is more difficult to help. I have the usage of brushes customized.<br>But maybe a smaller test can shed some light to this problem. The following seems to work on my version:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import sidesign.minib3d

Graphics3D 800, 600, 0, 2

cam = CreateCamera()
MoveEntity cam, 0, 0, -15

Local list:TList = CreateList()

Local org:TMesh = CreateCube()
FitMesh org, -0.5, -0.5, -0.5, 1, 1, 1
For i# = -3 To 3
For j# = -3 To 3
	Local cube:TEntity = CopyEntity(org)
	cube.brush = CreateBrush(Rand(0,255),Rand(0,255),Rand(0,255))
	PositionEntity cube, i, j, 0
	ListAddLast list, cube
Next
Next
FreeEntity org


Repeat

	For c:TEntity = EachIn list
		'c.brush.alpha = Rnd(0, 1)
		EntityAlpha c, Rnd(0, 1)
	Next
	
	RenderWorld
	Flip

Until KeyHit(27)
</textarea><br>It does in essence the same, but it is shorter, and it might be easier to debug. <br><br></td></tr></table><br>
<a name="1025920"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hmmm.... something may be indeed wrong in miniB3D. The code translated to B3D works:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800, 600, 0, 2

Global cam = CreateCamera()
MoveEntity cam, 0, 0, -15

Local org=CreateCube()
ScaleEntity org, 0.5, 0.5, 0.5

For i# = -3 To 3
For j# = -3 To 3
	Local cube=CopyEntity(org)
	EntityColor cube, Rand(100,255), Rand(0,255), Rand(0,255)
	PositionEntity cube, i * 2, j * 2, 0
	EntityPickMode cube, 2, False
Next
Next

Repeat
	Cls
    Local model = CameraPick(cam, MouseX(), MouseY())

	If (model&gt;0) Then
   	   EntityAlpha model, Rnd(0, 1)
	End If
	
	UpdateWorld
	RenderWorld
	Flip

Until KeyHit(1)
End
</textarea><br><br>But the same code on BlitzMax produces strange results:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Import sidesign.minib3d

Graphics3D 800, 600, 0, 2

Global cam:TCamera = CreateCamera()
MoveEntity cam, 0, 0, -15

Local org:TEntity=CreateCube()
ScaleEntity org, 0.5, 0.5, 0.5

For Local i# = -3 To 3
For Local j# = -3 To 3
	'Local cube:TMesh = CreateCube()
	'ScaleEntity cube, 0.5, 0.5, 0.5
	Local cube:TEntity=CopyEntity(org)
	EntityColor cube, Rand(100,255), Rand(0,255), Rand(0,255)
	PositionEntity cube, i * 2, j * 2, 0
	EntityPickMode cube, 2, False
Next
Next
freeentity org

Repeat
	Cls
    Local model:TEntity = CameraPick(cam, MouseX(), MouseY())

	If (model) Then
   	   EntityAlpha model, Rnd(0, 1)
	End If
	
	UpdateWorld
	RenderWorld
	Flip

Until KeyHit(KEY_ESCAPE)
End
</textarea><br><br>And it's something related to copyentity, since when creating individual cubes, instead of using copyentity, it works. <br><br></td></tr></table><br>
<a name="1025924"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Found it: put EntityBlend cube, 3 when creating the cubes, then they will flicker correctly!<br>(Why it needs blendmode 3 I have no clue :P) <br><br></td></tr></table><br>
<a name="1025939"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hujiklo</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for trying guys - the problem is definitely related to entities, as if you use copymesh() it will work no problem also! But maybe you're missing the actual problem in my code above? If I set the original code to just assign random alpha values to the tiles then it will work no problem anyway.<br><br>The problem is to do with the alpha value 1.0 - if I set non-picked tiles to 0.999 (near as solid as possible) then it will all work as it should do and ANY picked tile will be set to 0.25 and the OTHER non-picked tiles will retain their correct values of 0.999 as well. But set that near solid value to 1.0 and only the last bottom right tile will have its alpha value set to 0.25 - all other picked tiles no matter where are ignored.<br><br>Hope you see what I'm getting at. It's best to comment out the entity FX values in the bottom function so that only alpha values should change and you can see what's going wrong clearer.<br><br>Also in the main loop - Rem out the updatemax2D() function calls and see what happens!!! <br><br></td></tr></table><br>
<a name="1025959"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well, this seems to work as well:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import sidesign.minib3d

Graphics3D 800, 600, 0, 2

cam = CreateCamera()
MoveEntity cam, 0, 0, -15

Local list:TList = CreateList()

Local org:TMesh = CreateCube()
FitMesh org, -0.5, -0.5, -0.5, 1, 1, 1
For i# = -3 To 3
For j# = -3 To 3
	Local cube:TEntity = CopyEntity(org)
	cube.brush = CreateBrush(Rand(0,255),Rand(0,255),Rand(0,255))
	PositionEntity cube, i, j, 0
	ListAddLast list, cube
	EntityPickMode cube, 2
Next
Next
FreeEntity org


Repeat

	If MouseHit(1) 
		CameraPick cam, MouseX(), MouseY()
		Local e:TEntity = PickedEntity()
		If e EntityAlpha e, 1-e.brush.alpha
	End If
	
	RenderWorld
	Flip

Until KeyHit(27)
</textarea> <br><br></td></tr></table><br>
<a name="1025962"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hujiklo</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Warner - You need to go through your entity types list each loop and reset their alpha values to 1.0 if they are not picked. Then you'll see the problem.<br><br><br><pre class=code>

Import sidesign.minib3d

Graphics3D 800, 600, 0, 2

cam = CreateCamera()
MoveEntity cam, 0, 0, -15

Local list:TList = CreateList()

Local org:TMesh = CreateCube()
FitMesh org, -0.5, -0.5, -0.5, 1, 1, 1
For i# = -3 To 3
For j# = -3 To 3
	Local cube:TEntity = CopyEntity(org)
	cube.brush = CreateBrush(Rand(0,255),Rand(0,255),Rand(0,255))
	PositionEntity cube, i, j, 0
	ListAddLast list, cube
	EntityPickMode cube, 2
Next
Next
FreeEntity org


Repeat

	If MouseHit(1) 
		CameraPick cam, MouseX(), MouseY()

		For e:TEntity = EachIn list

		If e:TEntity =PickedEntity()
		
		 EntityAlpha e:TEntity, 0.15
		
		Else
		
		EntityAlpha e:TEntity, 1

		EndIf
		
		Next
	End If
	
	RenderWorld
	Flip

Until KeyHit(27)

</pre><br><br>In this case only the top right tile is affected if you click it. <br><br></td></tr></table><br>
<a name="1026230"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> <textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Import sidesign.minib3d

Graphics3D 800, 600, 0, 2

Global cam:TCamera = CreateCamera()
MoveEntity cam, 0, 0, -15

Local org:TMesh=CreateCube()
ScaleEntity org, 0.5, 0.5, 0.5

Local list:TList = CreateList()

For Local i# = -3 To 3
For Local j# = -3 To 3
	Local cube:TMesh=CopyMesh(org)
	ScaleEntity cube, 0.5, 0.5, 0.5
	EntityColor cube, Rand(100,255), Rand(0,255), Rand(0,255)
	PositionEntity cube, i * 2, j * 2, 0
	EntityPickMode cube, 2, False
	EntityAlpha cube,1
	ListAddFirst list, cube
Next
Next
freeentity org

Repeat
	Cls
	CameraPick(cam, MouseX(), MouseY())
    Local model:TEntity = PickedEntity()

	For Local e:TEntity= EachIn list
		If e=model Then 
		   turnentity e,0,1,0
		   entityalpha e,Rnd(0,1)
		End If
	Next
	
	UpdateWorld
	RenderWorld
	Flip

Until KeyHit(KEY_ESCAPE)
End
</textarea><br><br>This also works - using CopyMesh instead of CopyEntity. <br><br></td></tr></table><br>
<a name="1026232"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> If you open up TMesh.bmx, there is the following piece of code. It should be changed:<br><pre class=code>
                        ' if surface contains alpha info, enable blending
                        If surf.alpha_enable=True
				glEnable(GL_BLEND)
				glDepthMask(GL_FALSE)
			Else
				glDisable(GL_BLEND)
				glDepthMask(GL_TRUE)
			EndIf
</pre><br>Seems replacing these lines by the following works:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
			' if surface contains alpha info, enable blending
			Local hasalpha% = False
			If surf.brush&lt;&gt;Null
				If surf.brush.alpha#&lt;1.0 Or surf.brush.blend=2 Or surf.brush.blend=3 Or surf.brush.fx&amp;32
					hasalpha=True		
				Else				
					If surf.brush.tex[0]&lt;&gt;Null hasalpha = True
				End If
			End If

			If alpha &lt; 1 Or hasalpha
				glEnable(GL_BLEND)
				glDepthMask(GL_FALSE)
			Else
				glDisable(GL_BLEND)
				glDepthMask(GL_TRUE)
			EndIf
</textarea> <br><br></td></tr></table><br>
<a name="1026240"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> I just changed a bunch of things, and suddenly it started working!<br><br>What I *remember* I changed:<br><br>On functions.bmx GetEntityBrush returned nothing! It was missing the RETURN keyword.<br><pre class=code>
Function GetEntityBrush:TBrush(ent:TEntity)
	Return TBrush.GetEntityBrush(ent)
End Function
</pre><br><br>On the sample, I created a brush for every cube, even the original one - since TMesh.CreateCube() doesn't create a default brush.<br><br>On TBrush.bmx, on CreateBrush, before the return brush, I added:<br><pre class=code>
brush.blend=1
brush.fx=0
</pre><br>(those are the default values on blitz3D, but they weren't set on miniB3D)<br><br>Also, o TEntity.bmx, in the TEntity type I changed:<br><pre class=code>
	'Field brush:TBrush=New TBrush
	Field brush:TBrush=CreateBrush(255,255,255)
</pre><br><br>On TMesh.bmx, before the "update matrix" comment I added:<br><pre class=code>
		mesh.brush.CreateBrush(255,255,255)
		mesh.paintmesh(mesh.brush)
</pre><br><br>and so far, the code below works:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Import sidesign.minib3d

Graphics3D 800, 600, 0, 2

Global cam:TCamera = CreateCamera()
MoveEntity cam, 0, 0, -15

Local org:TMesh=CreateCube()

Local list:TList = CreateList()

For Local i# = -3 To 3
For Local j# = -3 To 3
	Local cube:TEntity=org.CopyEntity()
	ScaleEntity cube, 0.5, 0.5, 0.5
	entityalpha cube, Rnd(0.5,1.0)
	EntityColor cube, Rand(150,255),Rand(50,255),Rand(50,255)

	PositionEntity cube, i * 2, j * 2, 0
	EntityPickMode cube, 2, False
	
	ListAddFirst list, cube
Next
Next
freeentity org


Repeat
	Cls
	CameraPick(cam, MouseX(), MouseY())
    Local model:TEntity = PickedEntity()

	For Local e:TEntity= EachIn list
		If e=model Then 
		   turnentity e,0,1,0
		   entityalpha e,Rnd(0,1)
		End If
	Next
	
	UpdateWorld
	RenderWorld

	Flip

Until KeyHit(KEY_ESCAPE)
End
</textarea><br><br>The only strange thing is, that if I set alpha=1 on the copied cubes, it doesn't work again. <br><br></td></tr></table><br>
<a name="1026242"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah! Thanks to Warner, there's the final piece!<br>Edit: Nope, still bugs out the rendering order on the bird.bmx demo<br><br>What worked here (for both that sample and the birds demo) was this:<br><br>Change this:<br><pre class=code>
			' if surface contains alpha info, enable blending
			If surf.alpha_enable=True
				glEnable(GL_BLEND)
				glDepthMask(GL_FALSE)
			Else
				glDisable(GL_BLEND)
				glDepthMask(GL_TRUE)
			EndIf
</pre><br>To this:<br><pre class=code>
			' if surface contains alpha info, enable blending			
			If surf.alpha_enable=True
				glEnable(GL_BLEND)
				glDepthMask(GL_FALSE)
			Else
				glDisable(GL_BLEND)
				glDepthMask(GL_TRUE)
			EndIf
			
			If alpha&gt;0 And alpha&lt;1 Then glEnable(GL_BLEND)

</pre><br><br>Edit: improved the fix, now it's faster and working as it should! <br><br></td></tr></table><br>
<a name="1026255"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hujiklo</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you guys! Heroes you are! <br><br>This should be reported to Simon so he can include it in an update for MiniB3D. <br><br>Great detective work!! <br><br></td></tr></table><br>
<a name="1026269"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >simonh</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes, will fix this in the main release. Good work guys. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
