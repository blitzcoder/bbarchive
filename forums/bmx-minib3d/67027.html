<!DOCTYPE html><html lang="en" ><head ><title >Problems with BackbuffertoTex()</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Problems with BackbuffertoTex()</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=119" >MiniB3D Module</a>/<a href="#bottom" >Problems with BackbuffertoTex()</a><br><br>
<a name="748424"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >klepto2</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> hi, I was currently checking out the BackbuffertoTex Function and realized that this doesn't work on any PC I was testing.<br>The curious is CubemapRender works perfectly and other r2t codes from others works also but somehow minib3ds version doesn't work.<br><br>Here is my code:<br><br><pre class=code>
Import "../MiniB3D.bmx"

Strict

Local width=640,height=480,depth=16,mode=0

Graphics3D width,height,depth,mode

Local cam:TCamera=CreateCamera()
CameraClsColor(Cam,255,0,0)
Local light:TLight=CreateLight()

Local ent1:TMesh=CreateCube()

Local tex:TTexture=LoadTexture("media/test.png")
EntityTexture ent1,tex

Local ent2:TMesh=TMesh(CopyEntity(ent1))
Local ent3:TMesh=TMesh(CopyEntity(ent1))
Local ent4:TMesh=TMesh(CopyEntity(ent1))
Local ent5:TMesh=TMesh(CopyEntity(ent1))
Local ent6:TMesh=TMesh(CopyEntity(ent1))

PositionEntity ent1,0,0,10
PositionEntity ent2,0,0,5
PositionEntity ent3,0,0,20
PositionEntity ent4,0,0,15
PositionEntity ent5,0,0,30
PositionEntity ent6,0,0,25

EntityAlpha ent2,0.5
EntityAlpha ent4,0.5

' used by camera code
Local mxs#=0
Local mys#=0
Local move#=0.5
MouseXSpeed() ' flush
MouseYSpeed() ' flush

' used by fps code
Local old_ms=MilliSecs()
Local renders
Local fps

Local Sprite:TMesh = CreateCube()
PositionEntity(Sprite,-4,0,5)

Local STex:TTexture = TTexture.CreateScreenTexture(256)
EntityTexture Sprite,STex

While Not KeyDown(KEY_ESCAPE)		

	If KeyHit(KEY_ENTER) Then DebugStop

	'' control camera
	
	' mouse look
	
	mxs#=mxs#+(MouseXSpeed()/5.0)
	mys#=mys#+(MouseYSpeed()/5.0)

	RotateEntity cam,mys#,-mxs#,0

	MoveMouse width/2,height/2
	MouseXSpeed() ' flush
	MouseYSpeed() ' flush

	' move camera forwards/backwards/left/right with cursor keys
	
	If KeyDown(KEY_UP)=True Then MoveEntity cam,0,0,move# ' move camera forward
	If KeyDown(KEY_DOWN)=True Then MoveEntity cam,0,0,-move# ' move camera back

	If KeyDown(KEY_LEFT)=True Then MoveEntity cam,-move#,0,0 ' move camera left
	If KeyDown(KEY_RIGHT)=True Then MoveEntity cam,move#,0,0 ' move camera right
	
	''
	If Renders Mod 1 = 0 Then Mirror(Cam,STex,Sprite)
	RenderWorld

	renders=renders+1
	
	' calculate fps
	If MilliSecs()-old_ms&gt;=1000
		old_ms=MilliSecs()
		fps=renders
		renders=0
	EndIf
	
	Text 0,0,"FPS: "+String(fps)

	Flip

Wend
End

Function Mirror(Cam:TCamera,STEx:TTexture,Sprite:TEntity)
	CameraViewport(Cam,0,0,256,256)
	CameraClsMode(Cam,False,True)
	HideEntity Sprite
	RenderWorld
	'Flip
	BackBufferToTex(STex)
	
	ShowEntity(Sprite)
	CameraViewport(Cam,0,0,640,480)
	CameraClsMode(Cam,True,True)
End Function
</pre><br><br>Am i doing something wrong? I really have to find out why this doesn't work as I need the function to establish shader based posteffects etc. .<br>thx for the help <br><br></td></tr></table><br>
<a name="748495"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >bradford6</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> EDIT: dont need these to test...(dou you have the test.png and the CreateScreenTexture function?<br>I would like to run your code to see if i can help but those look integral to the problem.)<br><br>Initially, it looks like backbuffertotex is working on a single pixel (not the 256x256 area) <br><br></td></tr></table><br>
<a name="748614"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >simonh</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> It's because of mipmaps.<br><br>You are copying only to the main texture, which will not be displayed unless you are very close to the texture. Otherwise, mipmaps will be displayed which haven't been copied to.<br><br>Use ClearTextureFilters after Graphics3D (to disable mipmapping) and it will work. <br><br></td></tr></table><br>
<a name="748658"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >klepto2</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> thx, now it works :)<br>@bradford: sry the CreatescreenTexture was just a testing function and I haven't updated the code. the above way it doesn't work neither with the CreateTexture() Function without using simons advice :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
