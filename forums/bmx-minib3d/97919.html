<!DOCTYPE html><html lang="en" ><head ><title >particles?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >particles?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=119" >MiniB3D Module</a>/<a href="#bottom" >particles?</a><br><br>
<a name="1140327"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pete Carter</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Has anyone got a particle system working with minib3d . I'm sure i could make something that works but im not sure how to make it fast. I know theres lots of 2d examples but im not sure what people mean by single surface? I thought the expensive part was drawing lots of times to a surface not how many there were. <br><br></td></tr></table><br>
<a name="1140334"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ima747</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> There's a large number of different particle type systems one could implement. What I personally do:<br><br>Create an empty mesh<br>Create an empty surface in the mesh<br>Create tons points/tris in the surface<br>Track/move each 3 point tri set as a particle<br><br>Manipulating the x/y/z/u/v coords of points is fast, and since there's only one object with one surface it's easy to upload the changes to the 3D card, and easy for the accelerator to handle it all. <br><br></td></tr></table><br>
<a name="1140338"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jkrankie</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> If speed is important, the code in here (pos 6) <a href="http://www.blitzbasic.com/Community/posts.php?topic=97398" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=97398</a> i got working with help from Adam. ~100,000 camera aligned billboard particles at 60fps. You'd need to tidy it up a bit i suppose, but the rendering is quick.<br><br>Otherwise i'd recommend using the single surface mesh method mentioned above.<br><br>Cheers<br>Charlie <br><br></td></tr></table><br>
<a name="1140347"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> I thought the expensive part was drawing lots of times to a surface not how many there were. <br></div><br><br>In 3D land, a surface is an array of verts and triangles with a texture. Traditionally, it is the unit that gets uploaded to the graphics card in a single burst, with the context having to be opened beforehand and closed afterwards. Therefore, packing as much as possible onto one surface reduces the bottleneck between the CPU and the graphics card, for systems like miniB3D that make heavy use of the CPU, by reducing the number of "bursts" in which things are fired across.<br><br>Once it's on the graphics card, hardware takes over and it doesn't really matter how many times the texture is repeated, because hardware is witchcraft (also, you only draw one texture to a surface; the UVs mean it can be repeated in many places but only one per layer is there). No loop, no multiple starts and stops. <br><br></td></tr></table><br>
<a name="1140366"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ima747</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> To further explain what Yasha said, a surface isn't what you see as a surface rendered in 3D, it's a concept for organizing data, as such you need a good understanding of what the terms really mean in the context of the engine and how it works to see why one "surface" is really fast even if it's very complex. <br><br></td></tr></table><br>
<a name="1140391"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pete Carter</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I didn't know that was how it works. I've got to say i still don't understand but i will have a go <br><br></td></tr></table><br>
<a name="1140396"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> A Surface is an object that holds geometry (vertices and triangles) that will be painted by a Brush.<br>A Brush can be considered as a 'material': it holds a texture (or composition of), color, shininess and other effects.<br><br>A Mesh can have as many Surfaces as you want, and each of these Surfaces can be painted by a single Brush.<br>If you paint an already painted Surface you'd just be changing the Brush currently assigned to it.<br><br>The more Surfaces a Mesh has, the more expensive it is to render it. <br>The way graphical hardware works, to render a complete Mesh you have to render each of its Surfaces consecutively (in as many 'passes' as there are Surfaces). You can't do all Surfaces at once.<br><br>So a Mesh with 100 Surfaces, each of these Surfaces with 1 triangle, is much slower to render (in other words, 'has more overhead') than a Mesh with 1 Surface that has 100 triangles in it. <br><br>So get this: if you have a collection of Surfaces all painted with the same Brush, why not combine the geometry these Surfaces hold into a single Surface?<br>By use of multiple-frame textures (known as texture-sheets or atlases) and intelligent handling of the geometry stored in a Surface as abstract primitives such as quads, for instance, you can have a whole lot of 'visually different' elements stored together, all in the same Surface.<br><br>The benefit of this optimization, known as 'batching', is that all this geometry will be rendered at once instead of in several passes.<br>This really speeds rendering for Meshes with plenty of repeated geometry, such as particle systems, special effects etc. (so this is not particularly useful for characters or environments, which take other forms of optimization). <br><br></td></tr></table><br>
<a name="1140451"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pete Carter</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for the info i will have a go at coding something and ask question later. Thanks again for the clear explanations <br><br></td></tr></table><br>
<a name="1140970"></a>

<a name="1140971"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamRedwoods</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> written for minib3d+monkey, but you are welcome to convert it to blitzmax, although there may be some challenges.<br><br>Usage is as simple as "sprite = TBatchSprite.CreateSprite()".<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

''batchsprites
''
'' these batchsprites are one mesh
''
'' - notes:
'' - may have to add a check that camera position &lt;&gt; origin position. If so, move origin out a touch from camera

Class TBatchSpriteMesh Extends TMesh

	Field surf:TSurface 
	Field free_stack:IntStack ''list of available vertex
	Field num_sprites =0
	Field sprite_list:List&lt;TBatchSprite&gt;
	'Field mat_sp:Matrix = New Matrix
	
	
	Function Create:TBatchSpriteMesh(parent_ent:TEntity=Null)
	
		Local mesh:TBatchSpriteMesh = New TBatchSpriteMesh
		
		mesh.AddParent(parent_ent)
		mesh.entity_link = entity_list.EntityListAdd(mesh)

		' update matrix
		If mesh.parent&lt;&gt;Null
			mesh.mat.Overwrite(mesh.parent.mat)
			mesh.UpdateMat()
		Else
			mesh.UpdateMat(True)
		Endif
		
		mesh.surf = mesh.CreateSurface()
		mesh.surf.ClearSurface()
		mesh.surf.vbo_dyn = True
		mesh.num_sprites = 0
		mesh.free_stack = New IntStack
				'mainlist
				
		mesh.EntityFX 1+2'+32 '' full bright+ use vertex colors + alpha
		mesh.brush.shine=0.0	
				
		mesh.classname = "BatchSpriteMesh"
		'mesh.is_sprite = True 'no
		mesh.is_update = True
		mesh.cull_radius = -999999.0
		
		mesh.sprite_list = New List&lt;TBatchSprite&gt;
	
		Return mesh
	End
	
	Method Update(cam:TCamera)
		
		'' wipe out rotation matrix
		mat.grid[0][0] = 1.0; mat.grid[0][1] = 0.0; mat.grid[0][2] = 0.0
		mat.grid[1][0] = 0.0; mat.grid[1][1] = 1.0; mat.grid[1][2] = 0.0
		mat.grid[2][0] = 0.0; mat.grid[2][1] = 0.0; mat.grid[2][2] = 1.0
		
		TBatchSprite.min_x=999999999.0
		TBatchSprite.max_x=-999999999.0
		TBatchSprite.min_y=999999999.0
		TBatchSprite.max_y=-999999999.0
		TBatchSprite.min_z=999999999.0
		TBatchSprite.max_z=-999999999.0
		
		For Local ent:TEntity = Eachin sprite_list
			
			ent.Update(cam)
			
			surf.reset_vbo=surf.reset_vbo|16
			
		Next
		
		''do our own bounds
		
		If num_sprites&gt;0
		
			Local width#=TBatchSprite.max_x-TBatchSprite.min_x
			Local height#=TBatchSprite.max_y-TBatchSprite.min_y
			Local depth#=TBatchSprite.max_z-TBatchSprite.min_z
	
			' get bounding sphere (cull_radius#) from AABB
			' only get cull radius (auto cull), if cull radius hasn't been set to a negative no. by TEntity.MeshCullRadius (manual cull)
	
			If width&gt;=height And width&gt;=depth
				cull_radius=width
			Else
				If height&gt;=width And height&gt;=depth
					cull_radius=height
				Else
					cull_radius=depth
				Endif
			Endif
			cull_radius=cull_radius * 0.5
			Local crs#=cull_radius*cull_radius
			cull_radius= Sqrt(crs+crs+crs)
		
		
			min_x = TBatchSprite.min_x
			min_y = TBatchSprite.min_y
			min_z = TBatchSprite.min_z
			max_x = TBatchSprite.max_x
			max_y = TBatchSprite.max_y
			max_z = TBatchSprite.max_z
			
			'If brush.tex[0] Then brush.tex[0].flags = brush.tex[0].flags | 16 |32 ''always clamp
			'If surf.brush.tex[0] Then surf.brush.tex[0].flags = surf.brush.tex[0].flags | 16 |32 ''always clamp
			
		Else
			''no more sprites in batch, reduce overhead
			surf.ClearSurface()
			free_stack.Clear()
			
		Endif
		
		
		
	End
End

Class TBatchSprite Extends TSprite
		
		Field batch_id:Int ''ids start at 1
		Field vertex_id:Int
		Field sprite_link:list.Node&lt;TBatchSprite&gt;
		
		Global min_x:Float, min_y:Float, max_x:Float, max_y:Float, min_z:Float, max_z:Float
		
		Global mainsprite:TBatchSpriteMesh[] = New TBatchSpriteMesh[10]	
		Global total_batch:Int =0
		
		Private
	
		Global temp_mat:Matrix = New Matrix
	
		Public
		
		Method New()
			
			''new batch sprite, not added to entity list
						
		End
		
		Method FreeEntity()
			
			mainsprite[batch_id].surf.VertexColor(vertex_id+0, 0,0,0,0)
			mainsprite[batch_id].surf.VertexColor(vertex_id+1, 0,0,0,0)
			mainsprite[batch_id].surf.VertexColor(vertex_id+2, 0,0,0,0)
			mainsprite[batch_id].surf.VertexColor(vertex_id+3, 0,0,0,0)
			
			mainsprite[batch_id].free_stack.Push(vertex_id)
			mainsprite[batch_id].num_sprites -=1
			
			sprite_link.Remove()
			
			If parent
				parent_link.Remove()
				parent=Null
			Endif
			mat=Null
			brush=Null
			
		End
		
		
		Method Copy()
			
			''use CreateSprite(), since they should all be the same
			
		End
		
				
		''
		''add a parent to the entire batch mesh
		''-- position only
		Function BatchSpriteParent(id:Int=0, ent:TEntity,glob:Int=True)
			
			If id = 0 Then id = total_batch
			If id = 0 Then Return
			
			mainsprite[id].EntityParent(ent, glob)
		
		End
		
		''
		''return the sprite batch main mesh entity
		''
		Method BatchSpriteEntity:TEntity()
			
			Return mainsprite[batch_id]
		
		End
		
		''
		''move the batch sprite origin for depth sorting
		''
		Method BatchSpriteOrigin(x:Float,y:Float,z:Float)
			
			mainsprite[batch_id].PositionEntity(x,y,z)
			
		End
		
		Function CreateBatchMesh:TBatchSpriteMesh( batchid:Int )

			While total_batch &lt; batchid Or total_batch =0
			
				total_batch +=1
				If total_batch &gt;= mainsprite.Length() Then mainsprite = mainsprite.Resize(total_batch+5)
		
				mainsprite[total_batch] = TBatchSpriteMesh.Create()
				
			Wend
			
			Return mainsprite[total_batch]
		End


		Function CreateSprite:TBatchSprite(idx:Int=0)
			''add sprite to batch
			'' never added to entity_list
			'' if idx=0 add to last created batch
				
			Local sprite:TBatchSprite=New TBatchSprite
			sprite.classname="BatchSprite"
	
			' update matrix
			If sprite.parent&lt;&gt;Null
				sprite.mat.Overwrite(sprite.parent.mat)
				sprite.UpdateMat()
			Else
				sprite.UpdateMat(True)
			Endif
			
			If idx = 0 Then sprite.batch_id = total_batch Else sprite.batch_id = idx
			If sprite.batch_id = 0 Then sprite.batch_id = 1
			Local id:Int = sprite.batch_id
			
			''create main mesh
			Local mesh:TBatchSpriteMesh
	
			If id &gt; total_batch
			
				mesh = CreateBatchMesh(id)
				If id=0 Then id = 1
				
			Else
			
				mesh = mainsprite[id]
				
			Endif

			''get vertex id
			Local v:Int, v0:Int
			
			If mesh.free_stack.IsEmpty()
				
				mesh.num_sprites +=1
				v = (mesh.num_sprites-1) * 4 '4 vertex per quad
				'v = (mesh.num_sprites-1) * 3 '3 vertex per sprite
				
				mesh.surf.AddVertex(-1,-1,0, 0, 1) ''v0
				mesh.surf.AddVertex(-1, 1,0, 0, 0)
				mesh.surf.AddVertex( 1, 1,0, 1, 0)
				mesh.surf.AddVertex( 1,-1,0, 1, 1)
				'mesh.surf.AddVertex(-1,-3,0, 0, 2) ''v0
				'mesh.surf.AddVertex(-1, -1,0, 0, 0)
				'mesh.surf.AddVertex( 3, -1,0, 2, 0)
				mesh.surf.AddTriangle(0+v,1+v,2+v)
				mesh.surf.AddTriangle(0+v,2+v,3+v)
				''v isnt guarateed to be v0, but seems to match up

				''since vbo expands, make sure to reset so we dont use subbuffer
				mesh.surf.reset_vbo=-1
				
			Else
			
				v = mesh.free_stack.Pop()
				mesh.num_sprites +=1
				
			Endif
					
			
			mesh.reset_bounds = False ''we control our own bounds
		
			sprite.vertex_id = v
			sprite.sprite_link = mesh.sprite_list.AddLast(sprite)

			Return sprite
	
		End 
		
		
		Function LoadBatchTexture(tex_file$,tex_flag:Int=1,id:Int=0)
			''
			''does not create sprite, just loads texture
			''
			
			If id&lt;=0 Or id&gt;total_batch Then id=total_batch
			If id=0 Then id=1
			
			CreateBatchMesh(id)
	
			Local tex:TTexture=TTexture.LoadTexture(tex_file,tex_flag)
			mainsprite[id].EntityTexture(tex)

			' additive blend if sprite doesn't have alpha or masking flags set
			If tex_flag&amp;2=0 And tex_flag&amp;4=0
			
				mainsprite[id].EntityBlend 3
				
			Endif

	
		End
		
		
		Method Update(cam:TCamera )


			If view_mode&lt;&gt;2
				
				'' add in mainsprite position offset
				
				Local x#=mat.grid[3][0] - mainsprite[batch_id].mat.grid[3][0]
				Local y#=mat.grid[3][1] - mainsprite[batch_id].mat.grid[3][1]
				Local z#=mat.grid[3][2] - mainsprite[batch_id].mat.grid[3][2]
			
				temp_mat.Overwrite(cam.mat)
				temp_mat.grid[3][0]=x
				temp_mat.grid[3][1]=y
				temp_mat.grid[3][2]=z
				mat_sp.Overwrite(temp_mat)

				
				If angle&lt;&gt;0.0
					mat_sp.RotateRoll(angle)
				Endif
				
				If scale_x&lt;&gt;1.0 Or scale_y&lt;&gt;1.0
					mat_sp.Scale(scale_x,scale_y,1.0)
				Endif
				
				If handle_x&lt;&gt;0.0 Or handle_y&lt;&gt;0.0
					mat_sp.Translate(-handle_x,-handle_y,0.0)
				Endif
				
			Else
				
				mat_sp.Overwrite(mat)
			
				If scale_x&lt;&gt;1.0 Or scale_y&lt;&gt;1.0
					mat_sp.Scale(scale_x,scale_y,1.0)
				Endif
				
			Endif
					
			''update main mesh
			
			'' rotate each point corner offset to face the camera with cam_mat
			'' use the mat.x.y.z for position and offset from that
			Local p0:Float[], p1:Float[], p2:Float[], p3:Float[]
			'Local temp_mat:Matrix = mat_sp.Copy() 'Inverse()
			Local o:Float[] = [mat_sp.grid[3][0],mat_sp.grid[3][1],mat_sp.grid[3][2]]
			
			Local m00:Float = mat_sp.grid[0][0]
			Local m01:Float = mat_sp.grid[0][1]
			Local m10:Float = mat_sp.grid[1][0]
			Local m11:Float = mat_sp.grid[1][1]
			Local m02:Float = mat_sp.grid[0][2]
			Local m12:Float = mat_sp.grid[1][2]
			
			'p0 = mat_sp.TransformPoint(-1.0,-1.0,0.0)
			p0 = [-m00 + -m10 + o[0] , -m01 + -m11 + o[1], m02 + m12 - o[2]]		
			'p1 = mat_sp.TransformPoint(-1.0,1.0,0.0)		
			p1 = [-m00 + m10 + o[0] , -m01 + m11 + o[1], m02 - m12 - o[2]]	
			'p2 = mat_sp.TransformPoint(1.0,1.0,0.0)
			p2 = [m00 + m10 + o[0] , m01 + m11 + o[1], -m02 - m12 - o[2]]			
			'p3 = mat_sp.TransformPoint(1.0,-1.0,0.0)
			p3 = [m00 - m10 + o[0] , m01 - m11 + o[1], -m02 + m12 - o[2]]
			
			'3 triangle sprite trick (does not work for all conditions, animated sprites)
			'p0 = [-m00 + -(m10+m10+m10) + o[0] , -m01 + -(m11+m11+m11) + o[1], m02 + m12+m12+m12 - o[2]]				
			'p1 = [-m00 + m10 + o[0] , -m01 + m11 + o[1], m02 - m12 - o[2]]	
			'p2 = [m00+m00+m00 + m10 + o[0] , m01+m01+m01 + m11 + o[1], -(m02+m02+m02) - m12 - o[2]]			
			'p3 = [0.0,0.0,0.0]
			
			mainsprite[batch_id].surf.VertexCoords(vertex_id+0,p0[0],p0[1],p0[2])
			mainsprite[batch_id].surf.VertexCoords(vertex_id+1,p1[0],p1[1],p1[2])
			mainsprite[batch_id].surf.VertexCoords(vertex_id+2,p2[0],p2[1],p2[2])
			mainsprite[batch_id].surf.VertexCoords(vertex_id+3,p3[0],p3[1],p3[2])
			
			Local r# = brush.red'*brush.alpha
			Local g# = brush.green'*brush.alpha
			Local b# = brush.blue'*brush.alpha
			Local a# = brush.alpha '*0.5
		
			mainsprite[batch_id].surf.VertexColorFloat(vertex_id+0, r,g,b,a)
			mainsprite[batch_id].surf.VertexColorFloat(vertex_id+1, r,g,b,a)
			mainsprite[batch_id].surf.VertexColorFloat(vertex_id+2, r,g,b,a)
			mainsprite[batch_id].surf.VertexColorFloat(vertex_id+3, r,g,b,a)
						
			''determine our own bounds
			
			min_x = Min5(p0[0],p1[0],p2[0],p3[0],min_x )
			min_y = Min5(p0[1],p1[1],p2[1],p3[1],min_y )
			min_z = Min5(p0[2],p1[2],p2[2],p3[2],min_z )
			
			max_x = Max5(p0[0],p1[0],p2[0],p3[0],max_x )
			max_y = Max5(p0[1],p1[1],p2[1],p3[1],max_y )
			max_z = Max5(p0[2],p1[2],p2[2],p3[2],max_z )
			
		End
		
End

Function Min5:Float(a:Float, b:Float, c:Float, d:Float, e:Float)
	
	Local r:Float = a
	Local t:Float = c
	
	If b&lt;r Then r=b
	If d&lt;t Then t=d
	If t&lt;r Then r=t
	
	If r &lt; e Then Return r Else Return e
	
End

Function Max5:Float(a:Float, b:Float, c:Float, d:Float, e:Float)
	
	Local r:Float = a
	Local t:Float = c
	
	If b&gt;r Then r=b
	If d&gt;t Then t=d
	If t&gt;r Then r=t
	
	If r &gt; e Then Return r Else Return e
	
End
</textarea><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1141424"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pete Carter</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> thanks having scanned though the code theres quite a bit i don't understand but ill give it a go first and then ask questions if needed. I want to learn how to code this not just have someone do it for me. I don't want people to think I one of the copy and paste coders. <br><br></td></tr></table><br>
<a name="1141440"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamRedwoods</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> TBatchSprite should be an existing addition to miniB3D, since it's actually in iminib3d.<br><br>Also, after taking a quick stab at adding it into bmax minib3d, it will require a small trick to get the camera info into the batchsprite update. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
