<!DOCTYPE html><html lang="en" ><head ><title >terrain</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >terrain</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=119" >MiniB3D Module</a>/<a href="#bottom" >terrain</a><br><br>
<a name="970558"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sorry about this mess, but I couldn't post this code in the archive, due to the license type. In this first post, I demonstrate a terrain system I for minib3d. In the last post, an updated version is available that makes the terrain compatible with collisions and pickmode.<br>This first version needs no modding of minib3d, and is therefore easier to test.<br><br>Mind that this code is optimised to run on slow machines, so you might want to adjust the 'detail' settings. Look for the term "detail" in the source.<br><br>[edit: updated]<br>I found a good tutorial online on ROAM terrains. I was playing around with the code. This is what I have now.<br>To run it, you need minib3d, and you need to supply a heightmap and two textures. (color+detail) The heightmap is automatically resized.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

'Based on a tutorial by Mark Duchaineau
'license notice:
'http://www.cognigraph.com/ROAM_homepage/COPYING.txt
'
'to change terrain detail, search for "terrain detail".
'it appears two times

Import sidesign.minib3d

Graphics3D 640, 480, 0, 2

'create camera
Local cam:TCamera = CreateCamera()

'create terrain
Local terr:TTerrain = LoadTerrain("heightmap.png", cam) '&lt;----eyepoint, not parent
'load textures
Local tex1:TTexture = LoadTexture("detail.png")
Local tex2:TTexture = LoadTexture("colormap.png")
ScaleTexture tex2, terr.size, terr.size

'apply textures&amp;fullbright
terr.brush.tex[1] = tex2
terr.brush.tex[0] = tex1
terr.brush.no_texs = 2
terr.brush.fx :| 1

MoveEntity cam, terr.size/2, 0, -terr.size/2

'used by FPS
Local old_ms:Int = MilliSecs()
Local renders:Int
Local fps:Int

Repeat

	Wireframe KeyDown(key_w)

	'move camera
	If KeyDown(37) TurnEntity cam, 0, +1, 0
	If KeyDown(39) TurnEntity cam, 0, -1, 0
	If KeyDown(38) MoveEntity cam, 0, 0, +1
	If KeyDown(40) MoveEntity cam, 0, 0, -1
	If KeyDown(40) MoveEntity cam, 0, 0, -1
	If KeyDown(65) MoveEntity cam, 0, 1, 0
	If KeyDown(90) MoveEntity cam, 0, -1, 0

	'place on terrain
	Local x% = EntityX(cam)
	Local y% = -EntityZ(cam)
	Local h# = 0
	If Not((x &lt; 0) Or (y &lt; 0) Or (x &gt; 511) Or (y &gt; 511)) Then
		h# = terr.height[x, y] * terr.vsize
	End If
	h# = (h + 4 + EntityY(cam)) / 2
	PositionEntity cam, EntityX(cam), h, EntityZ(cam)

	RenderWorld
	terr.Update()
	
	'fps
	renders :+ 1
	If MilliSecs() -old_ms &gt;= 1000
		old_ms = MilliSecs()
		fps = renders
		renders = 0
	EndIf	
	beginmax2d
	DrawText fps, 0, 0
	endmax2d
	
	Flip

Until KeyHit(27)

End
	
Type TTerrain Extends TEntity

	Const ROAM_LMAX% = 11 '&lt;-----------terrain detail here	
	Field size# 'terrainsize
	Field vsize# 'terrainheight

	Field level2dzsize#[ROAM_LMAX+1]' /* Max midpoint displacement per level     */
	Field height#[,] 'heightmap
	Field xcf#,ycf#,zcf# 'used to store camera position
	Field eyepoint:TCamera 'reference to camera
		
	Method CopyEntity:TTerrain(parent:TEntity)
		'empty
	End Method

	'create terrain	
	Method Create:TTerrain()
		For Local i% = 0 To ROAM_LMAX
			level2dzsize[i] = 0.3 / Sqr(1 Shl i) '&lt;-------------terrain detail here
		Next		
		brush = CreateBrush(255, 255, 255)
		Return Self
	End Method

	' used by RenderWorld
	Method Update()
		
		Local fog=False
		If glIsEnabled(GL_FOG)=GL_TRUE Then fog=True ' if fog enabled, we'll enable it again at end of each surf loop in case of fx flag disabling it
		
		glDisable(GL_ALPHA_TEST)
		
		If order&lt;&gt;0
			glDisable(GL_DEPTH_TEST)
			glDepthMask(GL_FALSE)
		Else
			glEnable(GL_DEPTH_TEST)
			glDepthMask(GL_TRUE)
		EndIf
	
		' if surface contains alpha info, enable blending
'		If brush.alpha_enable=True
'			glEnable(GL_BLEND)
'			glDepthMask(GL_FALSE)
'		Else
			glDisable(GL_BLEND)
			glDepthMask(GL_TRUE)
'		EndIf
	
		' blend modes
		Select Brush.Blend
			Case 0
				glBlendFunc(GL_SRC_ALPHA,GL_ONE_MINUS_SRC_ALPHA) ' alpha
			Case 1
				glBlendFunc(GL_SRC_ALPHA,GL_ONE_MINUS_SRC_ALPHA) ' alpha
			Case 2
				glBlendFunc(GL_DST_COLOR,GL_ZERO) ' multiply
			Case 3
				glBlendFunc(GL_SRC_ALPHA,GL_ONE) ' additive and alpha
		End Select
		
		Local ambient_red#,ambient_green#,ambient_blue#
			
		' fx flag 1 - full bright ***todo*** disable all lights?
		If Brush.FX&amp;1
			ambient_red#  =1.0
			ambient_green#=1.0
			ambient_blue# =1.0
		Else
			ambient_red#  =TGlobal.ambient_red#
			ambient_green#=TGlobal.ambient_green#
			ambient_blue# =TGlobal.ambient_blue#
		EndIf

		' fx flag 2 - vertex colors ***todo*** disable all lights?
		If Brush.fx&amp;2
			glEnable(GL_COLOR_MATERIAL)
		Else
			glDisable(GL_COLOR_MATERIAL)
		EndIf
			
		' fx flag 4 - flatshaded
		If Brush.fx&amp;4
			glShadeModel(GL_FLAT)
		Else
			glShadeModel(GL_SMOOTH)
		EndIf

		' fx flag 8 - disable fog
		If Brush.fx&amp;8
			glDisable(GL_FOG)
		EndIf
			
		' fx flag 16 - disable backface culling
		If Brush.fx&amp;16
			glDisable(GL_CULL_FACE)
		Else
			glEnable(GL_CULL_FACE)
		EndIf
										
		' light + material color
			
		Local ambient#[]=[ambient_red#,ambient_green#,ambient_blue#]	
		glLightModelfv(GL_LIGHT_MODEL_AMBIENT,ambient#)
			
		Local mat_ambient#[]=[Brush.red#,Brush.green#,Brush.blue#,Brush.alpha#]
		Local mat_diffuse#[]=[Brush.red#,Brush.green#,Brush.blue#,Brush.alpha#]
		Local mat_specular#[]=[Brush.shine#,Brush.shine#,Brush.shine#,Brush.shine#]
		Local mat_shininess#[]=[100.0] ' upto 128

		glMaterialfv(GL_FRONT,GL_AMBIENT,mat_ambient)
		glMaterialfv(GL_FRONT,GL_DIFFUSE,mat_diffuse)
		glMaterialfv(GL_FRONT,GL_SPECULAR,mat_specular)
		glMaterialfv(GL_FRONT,GL_SHININESS,mat_shininess)

		' textures				
		Local tex_count=0
		tex_count=brush.no_texs

		For Local ix=0 To tex_count-1
		
			If brush.tex[ix]&lt;&gt;Null

				' Main brush texture takes precedent over surface brush texture
				Local texture:TTexture,tex_flags,tex_blend,tex_coords,tex_u_scale#,tex_v_scale#,tex_u_pos#,tex_v_pos#,tex_ang#,tex_cube_mode,frame
				texture=brush.tex[ix]
				tex_flags=brush.tex[ix].flags
				tex_blend=brush.tex[ix].blend
				tex_coords=brush.tex[ix].coords
				tex_u_scale#=brush.tex[ix].u_scale#
				tex_v_scale#=brush.tex[ix].v_scale#
				tex_u_pos#=brush.tex[ix].u_pos#
				tex_v_pos#=brush.tex[ix].v_pos#
				tex_ang#=brush.tex[ix].angle#
				tex_cube_mode=brush.tex[ix].cube_mode
				frame=brush.tex_frame

				glActiveTextureARB(GL_TEXTURE0+ix)
				glClientActiveTextureARB(GL_TEXTURE0+ix)

				glEnable(GL_TEXTURE_2D)
				glBindTexture(GL_TEXTURE_2D,texture.gltex[frame]) ' call before glTexParameteri

				' masked texture flag
				If tex_flags&amp;4&lt;&gt;0
					glEnable(GL_ALPHA_TEST)
				Else
					glDisable(GL_ALPHA_TEST)
				EndIf
			
				' mipmapping texture flag
				If tex_flags&amp;8&lt;&gt;0
					glTexParameteri(GL_TEXTURE_2D,GL_TEXTURE_MAG_FILTER,GL_LINEAR)
					glTexParameteri(GL_TEXTURE_2D,GL_TEXTURE_MIN_FILTER,GL_LINEAR_MIPMAP_LINEAR)
				Else
					glTexParameteri(GL_TEXTURE_2D,GL_TEXTURE_MAG_FILTER,GL_LINEAR)
					glTexParameteri(GL_TEXTURE_2D,GL_TEXTURE_MIN_FILTER,GL_LINEAR)
				EndIf
				
				' clamp u flag
				If tex_flags&amp;16&lt;&gt;0
					glTexParameteri(GL_TEXTURE_2D,GL_TEXTURE_WRAP_S,GL_CLAMP_TO_EDGE)
				Else						
					glTexParameteri(GL_TEXTURE_2D,GL_TEXTURE_WRAP_S,GL_REPEAT)
				EndIf
				
				' clamp v flag
				If tex_flags&amp;32&lt;&gt;0
					glTexParameteri(GL_TEXTURE_2D,GL_TEXTURE_WRAP_T,GL_CLAMP_TO_EDGE)
				Else
					glTexParameteri(GL_TEXTURE_2D,GL_TEXTURE_WRAP_T,GL_REPEAT)
				EndIf
		
				' spherical environment map texture flag
				If tex_flags&amp;64&lt;&gt;0
					glTexGeni(GL_S,GL_TEXTURE_GEN_MODE,GL_SPHERE_MAP)
					glTexGeni(GL_T,GL_TEXTURE_GEN_MODE,GL_SPHERE_MAP)
					glEnable(GL_TEXTURE_GEN_S)
					glEnable(GL_TEXTURE_GEN_T)
				Else
					glDisable(GL_TEXTURE_GEN_S)
					glDisable(GL_TEXTURE_GEN_T)
				EndIf
				
				' cubic environment map texture flag
				If tex_flags&amp;128&lt;&gt;0
	
					glEnable(GL_TEXTURE_CUBE_MAP)
					glBindTexture(GL_TEXTURE_CUBE_MAP,texture.gltex[frame]) ' call before glTexParameteri
					
					glTexParameteri(GL_TEXTURE_CUBE_MAP,GL_TEXTURE_WRAP_S,GL_CLAMP_TO_EDGE)
					glTexParameteri(GL_TEXTURE_CUBE_MAP,GL_TEXTURE_WRAP_T,GL_CLAMP_TO_EDGE)
					glTexParameteri(GL_TEXTURE_CUBE_MAP,GL_TEXTURE_WRAP_R,GL_CLAMP_TO_EDGE)
					glTexParameteri(GL_TEXTURE_CUBE_MAP,GL_TEXTURE_MIN_FILTER,GL_NEAREST)
  						glTexParameteri(GL_TEXTURE_CUBE_MAP,GL_TEXTURE_MAG_FILTER,GL_NEAREST)
					
					glEnable(GL_TEXTURE_GEN_S)
					glEnable(GL_TEXTURE_GEN_T)
					glEnable(GL_TEXTURE_GEN_R)
					'glEnable(GL_TEXTURE_GEN_Q)

					If tex_cube_mode=1
						glTexGeni(GL_S,GL_TEXTURE_GEN_MODE,GL_REFLECTION_MAP)
						glTexGeni(GL_T,GL_TEXTURE_GEN_MODE,GL_REFLECTION_MAP)
						glTexGeni(GL_R,GL_TEXTURE_GEN_MODE,GL_REFLECTION_MAP)
					EndIf
					
					If tex_cube_mode=2
						glTexGeni(GL_S,GL_TEXTURE_GEN_MODE,GL_NORMAL_MAP)
						glTexGeni(GL_T,GL_TEXTURE_GEN_MODE,GL_NORMAL_MAP)
						glTexGeni(GL_R,GL_TEXTURE_GEN_MODE,GL_NORMAL_MAP)
					EndIf
			
				Else
				
					glDisable(GL_TEXTURE_CUBE_MAP)
					
					' only disable tex gen s and t if sphere mapping isn't using them
					If tex_flags&amp;64=0
						glDisable(GL_TEXTURE_GEN_S)
						glDisable(GL_TEXTURE_GEN_T)
					EndIf
					
					glDisable(GL_TEXTURE_GEN_R)
					'glDisable(GL_TEXTURE_GEN_Q)
					
				EndIf 

				Select tex_blend
					Case 0 glTexEnvf(GL_TEXTURE_ENV,GL_TEXTURE_ENV_MODE,GL_REPLACE)
					Case 1 glTexEnvf(GL_TEXTURE_ENV,GL_TEXTURE_ENV_MODE,GL_REPLACE)
					'Case 2 glTexEnvf(GL_TEXTURE_ENV,GL_TEXTURE_ENV_MODE,GL_MODULATE)
					Case 2 glTexEnvf(GL_TEXTURE_ENV,GL_COMBINE_RGB_EXT,GL_MODULATE)
					Case 3 glTexEnvf(GL_TEXTURE_ENV,GL_TEXTURE_ENV_MODE,GL_ADD)
					Case 4
						glTexEnvf GL_TEXTURE_ENV, GL_TEXTURE_ENV_MODE, GL_COMBINE_EXT
						glTexEnvf GL_TEXTURE_ENV, GL_COMBINE_RGB_EXT, GL_DOT3_RGB_EXT
					Case 5
						glTexEnvi(GL_TEXTURE_ENV,GL_TEXTURE_ENV_MODE,GL_COMBINE)
						glTexEnvi(GL_TEXTURE_ENV,GL_COMBINE_RGB,GL_MODULATE)
						glTexEnvi(GL_TEXTURE_ENV,GL_RGB_SCALE,2.0)
					Default glTexEnvf(GL_TEXTURE_ENV,GL_TEXTURE_ENV_MODE,GL_MODULATE)
				End Select
										
				' reset texture matrix
				glMatrixMode(GL_TEXTURE)
				glLoadIdentity()
							
				If tex_u_pos#&lt;&gt;0.0 Or tex_v_pos#&lt;&gt;0.0
					glTranslatef(tex_u_pos#,tex_v_pos#,0.0)
				EndIf
				If tex_ang#&lt;&gt;0.0
					glRotatef(tex_ang#,0.0,0.0,1.0)
				EndIf
				If tex_u_scale#&lt;&gt;0.0 Or tex_v_scale#&lt;&gt;0.0
					glScalef(tex_u_scale#,tex_v_scale#,1.0)
				EndIf
				
				' if spheremap flag=true then flip tex
				If tex_flags&amp;64&lt;&gt;0
					glScalef(1.0,-1.0,-1.0)
				EndIf
				
				' if cubemap flag=true then manipulate texture matrix so that cubemap is displayed properly 
				If tex_flags&amp;128&lt;&gt;0

					glScalef(1.0,-1.0,-1.0)
					
					' get current modelview matrix (set in last camera update)
					Local mod_mat![16]
					glGetDoublev(GL_MODELVIEW_MATRIX,Varptr mod_mat[0])

					' get rotational inverse of current modelview matrix
					Local new_mat:TMatrix=New TMatrix
					new_mat.LoadIdentity()
					
					new_mat.grid[0,0] = mod_mat[0]
					new_mat.grid[1,0] = mod_mat[1]
					new_mat.grid[2,0] = mod_mat[2]

					new_mat.grid[0,1] = mod_mat[4]
					new_mat.grid[1,1] = mod_mat[5]
					new_mat.grid[2,1] = mod_mat[6]

					new_mat.grid[0,2] = mod_mat[8]
					new_mat.grid[1,2] = mod_mat[9]
					new_mat.grid[2,2] = mod_mat[10]
					
					glMultMatrixf(new_mat.grid)

				EndIf
				
			EndIf
		
		Next
			
		' draw tris		
		glMatrixMode(GL_MODELVIEW)

		glPushMatrix()

		glMultMatrixf(mat.grid)		
		RecreateROAM()
		
		glPopMatrix()
		
		' disable all texture layers
		For Local ix=0 To tex_count-1
	
			glActiveTextureARB(GL_TEXTURE0+ix)
			glClientActiveTextureARB(GL_TEXTURE0+ix)
			
			' reset texture matrix
			glMatrixMode(GL_TEXTURE)
			glLoadIdentity()
			
			glDisable(GL_TEXTURE_2D)
			
			glDisable(GL_TEXTURE_CUBE_MAP)
			glDisable(GL_TEXTURE_GEN_S)
			glDisable(GL_TEXTURE_GEN_T)
			glDisable(GL_TEXTURE_GEN_R)
		
		Next
		
		' enable fog again if fog was enabled at start of func
		If fog=True
			glEnable(GL_FOG)
		EndIf

	End Method

	Method RecreateROAM()

'		eyepoint.ExtractFrustum()
'		eyepoint.MQ_Update()
'		eyepoint.Update()
	
		xcf = eyepoint.EntityX()
		ycf = eyepoint.EntityY()
		zcf = -eyepoint.EntityZ()
	
		Local i:Int
		Local v:Float[][] = New Float[][4]
	
		For i = 0 To 3
			v[i] = New Float[3]
		Next
			
		v[0][0] = 0.0 		v[0][2] = size
		v[1][0] = size 	v[1][2] = size
		v[2][0] = size 	v[2][2] = 0
		v[3][0] = 0.0 		v[3][2] = 0

		glBegin(GL_TRIANGLES)
			
		'/* recurse on the two base triangles */
		drawsub(0, v[0], v[1], v[2])
		drawsub(0, v[2], v[3], v[0])

		glEnd()		
	
	End Method

Rem
/*
 * recursively draw a bintree triangle
 *
 * this version does no caching Or crack fixups
 * recursion terminates when triangle error projection is small or
 * at level limit
 *
 * orientation of vertices:
 *
 *                     v1
 *                    /.\
 *                   / . \
 *                  /  .  \
 *                 /   .   \
 *                /    .    \
 *               v2----o----v0
 *                     ^
 *                     vc
 */
EndRem
	
	Method drawsub(l:Int, v0#[], v1#[], v2#[])
	
		Local vc:Float[3]' /* New (split) vertex */
		Local ds:Float' /* maximum midpoint displacement */
		Local dx#,dy#,dz#' /* difference vector */
		Local rd#' /* squared sphere bound radius */
		Local rc#' /* squared distance from vc To camera position */
	
		If (l &lt; roam_lmax) Then
				
			'/* compute split point of base edge */				
		    	vc[0] = (v0[0] + v2[0]) / 2
		     vc[2] = (v0[2] + v2[2]) / 2	
			vc[1] = height[vc[0], vc[2]] * vsize
	
		     ds = level2dzsize[l]
	
			'/* compute radius of diamond bounding sphere (squared) */
			Local x#,y#,z#
			x = vc[0]
			y = vc[1]
			z = vc[2]
			rd = 0.0
			dx = v0[0] - x
			dy = v0[1] - y
			dz = v0[2] - z
			rc = dx * dx + dy * dy + dz * dz
			If (rc &gt; rd) rd = rc
			dx = v1[0] - x
			dy = v1[1] - y
			dz = v1[2] - z
			rc = dx * dx + dy * dy + dz * dz  
			If (rc &gt; rd) rd = rc
			dx = v2[0] - x
			dy = v2[1] - y
			dz = v2[2] - z
			rc = dx * dx + dy * dy + dz * dz  
			If (rc &gt; rd) rd = rc
			rd = Sqr(rd)
					
			Local i%, m%
			m = 1
			
			For i = 0 To 5
				Local d# = eyepoint.frustum[i,0] * vc[0] + eyepoint.frustum[i,1] * vc[1] + eyepoint.frustum[i,2] * vc[2] + eyepoint.frustum[i,3]
				If d &lt;= -rd Then Return
				m = m Shl 1
			Next
	
		     '/* compute distance from split point To camera (squared) */
		     dx = vc[0] - xcf
		 	dy = vc[1] - ycf
			dz = vc[2] - zcf
		     rc = dx*dx+dy*dy+dz*dz
					
		     '/* If not error large on screen, recursively split */
		     If ((ds * ds) &gt; (rc * 0.00000001)) ;&lt;---------terrain detail here
		        drawsub(l + 1, v1, vc, v0)
		        drawsub(l + 1, v2, vc, v1)
		        Return
		     End If
		
	 	End If

		Local uv#[3]
									
		uv[0]=v0[0] uv[1]=v0[2] uv[2]=0
		glMultiTexCoord2fARB(GL_TEXTURE0, uv[0], uv[1])
		glMultiTexCoord2fARB(GL_TEXTURE1, uv[0], uv[1])
		'glTexCoord2fv(Varptr uv[0]) 
		glVertex3fv(Varptr v0[0])
		
		uv[0]=v1[0] uv[1]=v1[2] uv[2]=0
		glMultiTexCoord2fARB(GL_TEXTURE0, uv[0], uv[1])
		glMultiTexCoord2fARB(GL_TEXTURE1, uv[0], uv[1])
		'glTexCoord2fv(Varptr uv[0]) 
		glVertex3fv(Varptr v1[0])
		
		uv[0]=v2[0] uv[1]=v2[2] uv[2]=0
		glMultiTexCoord2fARB(GL_TEXTURE0, uv[0], uv[1])
		glMultiTexCoord2fARB(GL_TEXTURE1, uv[0], uv[1])
		'glTexCoord2fv(Varptr uv[0]) 
		glVertex3fv(Varptr v2[0])
						
	End Method

	Method Load(file$)
	
		If FileType(file$) &lt;&gt; 1 Then Return
		
		Local hmap:TPixmap
		hmap = LoadPixmap(file$)
		If hmap = Null Then Return

		vsize = 30
		size = 512
		hmap = ResizePixmap(hmap, size, size)
		height# = New Float[size+1, size+1]

		Local Pixel%

		For Local x% = 0 To size-1
		For Local y% = 0 To size-1
		
			Pixel = ReadPixel(hmap, x, y) &amp; $FF	
		  	height#[x, y] = Pixel / 255.0
			
		Next	
		Next
	
	End Method
	
End Type

Function LoadTerrain:TTerrain(filename$, cam:TCamera)

	If FileType(filename$) &lt;&gt; 1 Then RuntimeError "heightmap not found: " + filename$
	If cam = Null Then RuntimeError "camera not found"
	Global terr:TTerrain = New TTerrain.Create()
	terr.Load(filename$)
	terr.eyepoint = cam
	terr.class$ = "Terrain"
	terr.AddParent(Null)
	terr.EntityListAdd(terr.entity_list)
	
	terr.mat.LoadIdentity()

	' update matrix
	If terr.parent&lt;&gt;Null
		terr.mat.Overwrite(terr.parent.mat)
		terr.UpdateMat()
	Else
		terr.UpdateMat(True)
	EndIf	
	
	Return terr

End Function
</textarea><br>It has settings to change the detail level. Search for "detail".<br>---------------------------------------------------------------------------<br>About the license. Courtesey of Mark Duchaineau. The tutorials license is "free but copyrighted". Please read the license yourself. As I understand it, as long as you provide the terms, or a link to the terms on their site, you may use it freely: <a href="http://www.cognigraph.com/ROAM_homepage/COPYING.txt" target="_blank">http://www.cognigraph.com/ROAM_homepage/COPYING.txt</a><br>As far of any parts I wrote, so anything except the basics of RecreateRoam and DrawSub, are public domain. <br><br></td></tr></table><br>
<a name="970668"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chroma</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Warner, while your still in the beginning stages of development.  Maybe you could utilize the scenegraph code posted by Leadwerks to figure out what terrain to show.  And also have an object to dictate the size of the visible segments.<br><br>Say you have a terrain that's 100 segments by 100 segments.  You want to show the segment that the player is standing on plus all segments withing 10 unit distance.  But make it so you can batch them together.   Breaking the 100x100 segments into 5x5 sections etc. <br><br></td></tr></table><br>
<a name="972380"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you, Chroma. That is a good suggestion. The Leadwerks scenegraph code is indeed real fast. <br><br></td></tr></table><br>
<a name="973112"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Updated, please see the first post <br><br></td></tr></table><br>
<a name="973567"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> &lt;edit&gt;upload further below&lt;/edit&gt; <br><br></td></tr></table><br>
<a name="980998"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> This doesn't seem to compile for me... I keep getting stuck at C_AddVertex() in "inc/TTerrain.bmx".<br>Compile Error: Identifier 'C_AddVertex' not found <br><br></td></tr></table><br>
<a name="981072"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> It seems I forgot 'minib3d.bmx', where the C_ functions are declared. I updated the above post. Also, I uploaded my entire minib3d mod folder to yousendit. It contains the terrain functions, scenegraph and the quat/matrix-rotation commands.<br><a href="http://www.yousendit.com/transfer.php?action=batch_download&amp;batch_id=YkxJblRwY3k3bUJFQlE9PQ" target="_blank">http://www.yousendit.com/transfer.php?action=batch_download&amp;batch_id=YkxJblRwY3k3bUJFQlE9PQ</a> <br><br></td></tr></table><br>
<a name="993016"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> the download has expired, could you upload it again&gt;? <br><br></td></tr></table><br>
<a name="993126"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> The terrain supports pickmode 2 and collisions. If you want to get the height of a specific part, use Terrain.GetY(x, z). I never tried moving the terrain, nor did I try to scale it. I think that could work out problematic.<br>As for uploading, I gave this site a try, hopefully it is any good:<br><a href="http://www.easy-share.com/1908345153/sidesign.zip" target="_blank">http://www.easy-share.com/1908345153/sidesign.zip</a><br>It has my complete minib3d folder in it. If you only need the terrain, you should look into TTerrain.bmx, <br>as well as the c-functions, tcollisions.bmx, minib3d.bmx and err .. I think that was it. :/<br><br>Local terrain:TTerrain = LoadTerrain("") 'empty terrain<br><br>Or:<br>Local terrain:TTerrain = LoadTerrain("heightmap.png")<br><br>If you want to replace the heightmap by another, use this:<br>terrain.Load("heightmap2.png")<br><br>Instead of scaling, use:<br>terrain.vsize = 30.0<br><br>Terrain goes from (0,0) to (1024,-1024)<br>unless you alter 'size' before loading.<br><br>&lt;edit&gt; oh yes, also TGlobal.bmx <br><br></td></tr></table><br>
<a name="993271"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> thanks very much, <br><br></td></tr></table><br>
<a name="997440"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Charrua</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> hi<br>i'm developing a world creator for Blitz3D an BlitzMax+MiniB3D (AWC on the forums), Terrains are supported by Blitz3d and is afeature that i will incorporate on the editor but my knowledge in bmax is very poor. <br><br>Warner, may i use your code in my awc_loader.bmx?<br><br>thank's<br><br>Juan <br><br></td></tr></table><br>
<a name="997774"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sure, feel free to use it. The parts I wrote are public domain. However, the parts that came from the tutorial I followed are 'free but copyrighted'. If I understood correctly, it should be accompanied by the above copyright notice or a link to the site.<br>And the code is 'hacked together' -if that is correct English- and might not work perfectly. I should write a clean version from scratch, which is on my to-do list for 2014. In other words: I want to rewrite it one time, but I'm not sure if I'll ever get round to it. <br><br></td></tr></table><br>
<a name="997791"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Charrua</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> ok, thank's<br><br>i have to test it some, because, when the user loads a terrain in the editor, he will be using the Terrain feature of Blitz3d, and the object's placement (snap to surface by example) probably do not match when using the above code from BlitzMax...?<br>if you ever rewrote the code, let me know, probably in 2015 i'll be finishing the version 2.0 of my AWC!<br>by the way probably in a week i'll been posting the version 1.0, Terrain were goning to be unsuported under bmax but wiht this code i thing it has a chance.<br><br>thank's again<br><br>Juan <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
