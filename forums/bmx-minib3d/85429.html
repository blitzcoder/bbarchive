<!DOCTYPE html><html lang="en" ><head ><title >EntityPickMode eating up my ram</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >EntityPickMode eating up my ram</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=119" >MiniB3D Module</a>/<a href="#bottom" >EntityPickMode eating up my ram</a><br><br>
<a name="966371"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Digital Anime</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> For some reason the EntityPickMode I used in my TPlanet type is causing troubles as soon as I spawn a Planet (sphere with texture) by clicking with the mouse somewhere in the window.<br><br>Is there a good way to solve this?<br><br>Code is still a little messy and uses both MiniB3d and MaxGui and you need to ad a Planet.png as texture for standard planet.<br><br>The idea is to create a editor for making levels for a spaceshooter with lot's of freedom, but I need to solve this first before I can continue.<br><br><pre class=code>
Strict

Import sidesign.minib3d

Global w:Int = 800		'width
Global h:Int = 600		'heigth
Global b:Int = 32		'bit
Global f:Int = 60		'frequency

Graphics3D w, h, b, 2, f

ClearTextureFilters()

'Global
Global PlanetList:TList = CreateList()
Global PlanetTexture:TTexture = LoadTexture("Planet.png", 1)
If PlanetTexture = Null Then RuntimeError "Texture not found"

Global id:Int = 1
Global Planet:TPlanet

Global Cam:TCamera = CreateCamera()
CameraClsColor Cam, 0, 0, 0
PositionEntity Cam, 0, 0, -30
CreateLight()

Global plane:Tmesh = CreateCube()
ScaleEntity plane, 1000, 1000, 0
PositionEntity plane, 0, 0, 0
EntityAlpha plane, 0
EntityPickMode plane, 2, 0

'MaxGUI
Global Edit:TGadget
Global Label1:Tgadget
Global Label2:Tgadget
Global Label3:Tgadget

Edit = CreateWindow("Edit", 900, 0, 320, 500, Null, WINDOW_TITLEBAR)
Label1 = CreateLabel("id:", 20, 10, 80, 20, Edit)
Label2 = CreateLabel("x:", 20, 30, 80, 20, Edit)
Label3 = CreateLabel("y:", 20, 50, 80, 20, Edit)

'Main loop
While Not KeyHit(KEY_ESCAPE)

	'Control
	If MouseHit(1)
		TPlanet.Create(id, CreateSphere(16), 5, PickedX(), PickedY(), 0, 0, 0, 0, 0, 0.1, "", 0, PlanetTexture)
		id = id + 1
	EndIf

	If KeyHit(KEY_Z)
		PlanetList.Last
		FreeEntity Planet.mesh
		ListRemove PlanetList, PlanetList.Last()
	EndIf

	CameraPick(Cam, MouseX(), MouseY())
	
	MoveEntity Cam, KeyDown(KEY_RIGHT) - KeyDown(KEY_LEFT), KeyDown(KEY_UP) - KeyDown(KEY_DOWN), 0
	PositionEntity Cam, EntityX(Cam), EntityY(Cam), MouseZ() - 30
	PositionEntity plane, EntityX(Cam), EntityY(Cam), 0

	For Planet = EachIn PlanetList
		Planet.Update()
		Planet.Collision()
	Next
	
	UpdateWorld()
	RenderWorld()

	BeginMax2D()

	SetColor 0, 128, 255
	DrawText "Space Editor Alpha testing", 10, 10
	SetColor 255, 255, 255
	DrawText PickedX(), 10, 40
	DrawText PickedY(), 10, 50
	SetColor 255, 255, 255
	DrawLine (w / 2) - 10, (h / 2), (w / 2) + 10, (h / 2)
	DrawLine (w / 2), (h / 2) - 10, (w / 2), (h / 2) + 10

	PollEvent()
	Select EventID()
	Case EVENT_WINDOWCLOSE
	End
	End Select
	
	EndMax2D()
		
	Flip 1
Wend

End

Type TPlanet
	Field id:Int = 0
	Field mesh:TMesh
	Field scale:Float = 0
	Field x:Float = 0
	Field y:Float = 0
	Field z:Float = 0
	Field a:Float = 0
	Field xt:Float = 0
	Field yt:Float = 0
	Field ms:Float = 0
	Field rs:Float = 0
	Field name:String = ""
	Field pid:Int = 0
	Field txt:TTexture
	
	Function Create:TPlanet(id:Int, mesh:Tmesh, scale:Float, x_start:Float, y_start:Float, z_start:Float, a_start:Float, xt:Float, yt:Float, ms:Float, rs:Float, name:String, pid:Int, txt:TTexture)
		Local Planet:TPlanet = New TPlanet
		Planet.id = id				'Planet ID
		Planet.mesh = mesh			'Mesh
		Planet.scale = scale		'Scale
		Planet.x = x_start			'X position
		Planet.y = y_start			'Y position
		Planet.z = z_start			'Z position
		Planet.a = a_start			'Angle
		Planet.xt = xt				'X tilt
		Planet.yt = yt				'Y tilt
		Planet.ms = ms				'Movement speed
		Planet.rs = rs				'Rotation speed
		Planet.name = name			'Planet name
		Planet.pid = pid			'Parent planet ID
		Planet.txt = txt			'Texture
		
		PositionEntity(Planet.mesh, Planet.x, Planet.y, Planet.z)
		ScaleEntity(Planet.mesh, Planet.scale, Planet.scale, Planet.scale)
		EntityTexture(Planet.mesh, Planet.txt)
		RotateMesh (Planet.mesh, -90, 0, 0)
		RotateEntity (Planet.mesh, 60, 0, 0)
		EntityPickMode Planet.mesh, 2, 0		'&lt;- this one eats up my memmory
		ListAddLast PlanetList, Planet
				
		Return Planet
	End Function
	
	Method Update()
		Self.a = Self.a + Self.rs
		RotateMesh (Self.mesh, 0, 0, Self.rs)
		If PickedEntity() = Self.mesh
			EntityAlpha Self.mesh, 0.5
			SetGadgetText(Label1, "id: " + Self.id)
			SetGadgetText(Label2, "x: " + Self.x)
			SetGadgetText(Label3, "y: " + Self.y)
		Else
			EntityAlpha Self.mesh, 1
		EndIf
	End Method

	Method Collision()

	End Method
	
End Type
</pre><br><br>[edit] removed the unneeded UpdateWorld() <br><br></td></tr></table><br>
<a name="966745"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GIB3D</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Why are you calling UpdateWorld inside Method Update()?<br>I don't use BlitzMax (I use Blitz3D) but I'm sure you don't need to keep calling that when you already have it called elsewhere. <br><br></td></tr></table><br>
<a name="966786"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Digital Anime</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I already removed it myself indeed, but thanks for noticing :-p<br><br>But issue still remains. I just wonder if entitypickmode and objects don't mix for some reason or if I'm just missing/misplacing something.<br><br>Any help would be great<br><br>[edit] I narrowed it down to the commands pickentitymode combined with rotatemesh... As soon as a mesh with pickentitymode on starts to rotate the program starts eating more and more ram, and as soon when the rotation stops, memmory usage doesn't increase/decrease.<br><br>I think I need to get a workarround for this. <br><br></td></tr></table><br>
<a name="966984"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Digital Anime</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>
Import sidesign.minib3d

Strict

Graphics3D 800, 600, 32, 2, 60

Local Cam:TCamera = CreateCamera()
PositionEntity Cam, 0, 0, -5
CreateLight()

Local Sphere:TMesh = CreateSphere(8)
PositionEntity Sphere, 0, 0, 0
EntityPickMode Sphere, 2, 0

Repeat

	RotateMesh Sphere, 1, 1, 1
	
	CameraPick (Cam, MouseX(), MouseY())
	
	UpdateWorld()
	RenderWorld()
	
	BeginMax2D()
	
		DrawText "X = " + PickedX(), 10, 10
		DrawText "Y = " + PickedY(), 10, 20
		DrawText "Z = " + PickedZ(), 10, 30
	
	EndMax2D()
	
	Flip 1

Until KeyHit(KEY_ESCAPE)

End
</pre><br><br>I made a smaller example wich still eats up my ram.<br>Is this a memmory leak within MiniB3D?<br>Try this code with the taskmanager open and look if within the following minutes this code eats up your ram as well...<br><br>I don't see any programming error's in this small example myself, but if there is, please tell... :-( <br><br></td></tr></table><br>
<a name="967000"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GIB3D</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> When I used it in Blitz3D it works just fine.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800, 600, 32, 2

Local Cam = CreateCamera()
PositionEntity Cam, 0, 0, -5
CreateLight()

Local Sphere = CreateSphere(8)
PositionEntity Sphere, 0, 0, 0
EntityPickMode Sphere, 2, 0

Repeat

	RotateMesh Sphere, 1, 1, 1
	
	CameraPick (Cam, MouseX(), MouseY())
	
	UpdateWorld()
	RenderWorld()
	
	
	Text 10, 10, "X = " + PickedX()
	Text 10, 20, "Y = " + PickedY()
	Text 10, 30, "Z = " + PickedZ()
	
	Flip
	;Delay(13) This can be changed depending on the computer to give the CPU more time to think
Until KeyHit(1)

End
</textarea> <br><br></td></tr></table><br>
<a name="967007"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Digital Anime</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Seems like a small bug/memmory leak within MiniB3D then. Hope Simonh will fix it :-)<br><br>I just decided to get me Blitz3D SDK, and after downloading changing the code a bit I got this small example working perfectly.<br>I rather pay 100$ for something that works then hoping for a fix on something that is free. MiniB3D is still a great project and very stable, but in my case it's not fitted for creating my games, yet.<br><br>I will use MiniB3D again when it's more stable in the future, but for now I can continue programming...<br><br>The SDK version :<br><br><pre class=code>
Import blitz3d.blitz3dsdk

Import "bbtype.bmx"
Import "blitz3d.bmx"

Strict

bbBeginBlitz3D

bbGraphics3D 800, 600, 32, 2

Local Cam = bbCreateCamera()
bbPositionEntity Cam, 0, 0, -5
bbCreateLight()

Local Sphere = bbCreateSphere(8)
bbPositionEntity Sphere, 0, 0, 0
bbEntityPickMode Sphere, 2, 1

Repeat

	bbRotateMesh Sphere, 1, 1, 1
	
	bbCameraPick (Cam, bbMouseX(), bbMouseY())
	
	bbUpdateWorld()
	bbRenderWorld()
	
	bbText 10, 10, "X = " + bbPickedX()
	bbText 10, 20, "Y = " + bbPickedY()
	bbText 10, 30, "Z = " + bbPickedZ()
		
	bbFlip 1

Until KeyHit(KEY_ESCAPE)

End
</pre> <br><br></td></tr></table><br>
<a name="967027"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well, I don't know what it could be. Are you sure that it's EntityPickMode that does that? In Entity.bmx, the code for EntityPickMode is this:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
	Method EntityPickMode(no,obscure=True)
	
		' add to pick entity list if new mode no&lt;&gt;0 and not previously added
		If pick_mode=0 And no&lt;&gt;0
			ListAddLast(TPick.ent_list,Self)
		EndIf
		
		' remove from pick entity list if new mode no=0 and previously added
		If pick_mode&lt;&gt;0 And no=0
			ListRemove(TPick.ent_list,Self)
		EndIf
	
		pick_mode=no
		obscurer=obscure
			
	End Method
</textarea><br>I can't imagine that would require a lot of memory.<br><br>Instead of creating a sphere for each new planet, create one original sphere and use CopyEntity to create new spheres.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Import sidesign.minib3d

Global w:Int = 800		'width
Global h:Int = 600		'heigth
Global b:Int = 32		'bit
Global f:Int = 60		'frequency

Graphics3D w, h, b, 2, f

ClearTextureFilters()

'Global
Global PlanetList:TList = CreateList()
Global PlanetTexture:TTexture = LoadTexture("C:\Users\Bram\Desktop\GAMEBUILDER2.00 GUI\media\blocks.bmp", 1)
If PlanetTexture = Null Then RuntimeError "Texture not found"

Global id:Int = 1
Global Planet:TPlanet

Global Cam:TCamera = CreateCamera()
CameraClsColor Cam, 0, 0, 0
PositionEntity Cam, 0, 0, -30
CreateLight()

Global plane:Tmesh = CreateCube()
ScaleEntity plane, 1000, 1000, 0
PositionEntity plane, 0, 0, 0
EntityAlpha plane, 0
EntityPickMode plane, 2, 0

Global orgplanet:TEntity = CreateSphere(16)
RotateMesh (TMesh(orgplanet), -90, 0, 0)
HideEntity orgplanet

'MaxGUI
'Global Edit:TGadget
'Global Label1:Tgadget
'Global Label2:Tgadget
'Global Label3:Tgadget

'Edit = CreateWindow("Edit", 900, 0, 320, 500, Null, WINDOW_TITLEBAR)
'Label1 = CreateLabel("id:", 20, 10, 80, 20, Edit)
'Label2 = CreateLabel("x:", 20, 30, 80, 20, Edit)
'Label3 = CreateLabel("y:", 20, 50, 80, 20, Edit)

'Main loop
While Not KeyHit(KEY_ESCAPE)

	'Control
	If MouseHit(1)
		TPlanet.Create(id, CopyEntity(orgplanet), 5, PickedX(), PickedY(), 0, 0, 0, 0, 0, 0.1, "", 0, PlanetTexture)
		id = id + 1
	EndIf

	If KeyHit(KEY_Z)
		PlanetList.Last
		FreeEntity Planet.mesh
		ListRemove PlanetList, PlanetList.Last()
	EndIf

	CameraPick(Cam, MouseX(), MouseY())
	
	MoveEntity Cam, KeyDown(KEY_RIGHT) - KeyDown(KEY_LEFT), KeyDown(KEY_UP) - KeyDown(KEY_DOWN), 0
	PositionEntity Cam, EntityX(Cam), EntityY(Cam), MouseZ() - 30
	PositionEntity plane, EntityX(Cam), EntityY(Cam), 0

	For Planet = EachIn PlanetList
		Planet.Update()
		Planet.Collision()
	Next
	
	UpdateWorld()
	RenderWorld()

	BeginMax2D()

	SetColor 0, 128, 255
	DrawText "Space Editor Alpha testing", 10, 10
	SetColor 255, 255, 255
	DrawText PickedX(), 10, 40
	DrawText PickedY(), 10, 50
	SetColor 255, 255, 255
	DrawLine (w / 2) - 10, (h / 2), (w / 2) + 10, (h / 2)
	DrawLine (w / 2), (h / 2) - 10, (w / 2), (h / 2) + 10

	PollEvent()
	Select EventID()
	Case EVENT_WINDOWCLOSE
	End
	End Select
	
	DrawText GCMemAlloced(), 0, 0
	
	EndMax2D()
		
	Flip 1
Wend

End

Type TPlanet
	Field id:Int = 0
	Field mesh:TEntity
	Field scale:Float = 0
	Field x:Float = 0
	Field y:Float = 0
	Field z:Float = 0
	Field a:Float = 0
	Field xt:Float = 0
	Field yt:Float = 0
	Field ms:Float = 0
	Field rs:Float = 0
	Field name:String = ""
	Field pid:Int = 0
	Field txt:TTexture
	
	Function Create:TPlanet(id:Int, mesh:TEntity, scale:Float, x_start:Float, y_start:Float, z_start:Float, a_start:Float, xt:Float, yt:Float, ms:Float, rs:Float, name:String, pid:Int, txt:TTexture)
		Local Planet:TPlanet = New TPlanet
		Planet.id = id				'Planet ID
		Planet.mesh = mesh			'Mesh
		Planet.scale = scale		'Scale
		Planet.x = x_start			'X position
		Planet.y = y_start			'Y position
		Planet.z = z_start			'Z position
		Planet.a = a_start			'Angle
		Planet.xt = xt				'X tilt
		Planet.yt = yt				'Y tilt
		Planet.ms = ms				'Movement speed
		Planet.rs = rs				'Rotation speed
		Planet.name = name			'Planet name
		Planet.pid = pid			'Parent planet ID
		Planet.txt = txt			'Texture
		
		PositionEntity(Planet.mesh, Planet.x, Planet.y, Planet.z)
		ScaleEntity(Planet.mesh, Planet.scale, Planet.scale, Planet.scale)
		EntityTexture(Planet.mesh, Planet.txt)
		RotateEntity (Planet.mesh, 60-90, 0, 0)
		EntityPickMode Planet.mesh, 2, 0		'&lt;- this one eats up my memmory
		ListAddLast PlanetList, Planet
				
		Return Planet
	End Function
	
	Method Update()
		Self.a = Self.a + Self.rs
		RotateEntity Self.mesh, 0, 0, Self.a
		If PickedEntity() = Self.mesh
			EntityAlpha Self.mesh, 0.5
'			SetGadgetText(Label1, "id: " + Self.id)
'			SetGadgetText(Label2, "x: " + Self.x)
'			SetGadgetText(Label3, "y: " + Self.y)
		Else
			EntityAlpha Self.mesh, 1
		EndIf
	End Method

	Method Collision()

	End Method
	
End Type
</textarea> <br><br></td></tr></table><br>
<a name="967169"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Digital Anime</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> It's entitypickmode in combination with rotatemesh on the same entity/mesh and it only happens in MiniB3D, not in Blitz3D (according to GIA_Green_Fire_) and not in Blitz3D SDK in BlitzMax (wich I tested myself).<br><br>The same code of my small example uses about 15Mb of memmory in total and stays that way, while under MiniB3D it's getting bigger and bigger untill you stop it or your memmory runs out... <br><br></td></tr></table><br>
<a name="967182"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Uncle</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello all,<br><br>Last night I noticed the same thing and then I found this thread this morning.  I thought it was my bad coding, but it looks like MiniB3D does have a leak.  <br><br>I have an editor which uses a lot of rotatemesh commands.  Every time I use it I saw my memory usage increase.  I've been looking into why this happens, and I have managed to narrow the leak down to the TreeCheck(mesh:Tmesh) method in the TcolTree.bmx file.  I'm not a hundred percent sure what this function does, but it seems to create another mesh using C_AddSurface().  If you comment out this line the leak stops.  This isn't a fix though!  I don't understand the code enough yet to find the fix.  Perhaps Simon could take a look or point give an explanation of this method?<br><br>Cheers,<br><br>Unc <br><br></td></tr></table><br>
<a name="967264"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >simonh</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think I've found it - try changing C_AddSurface in tree.cpp to this:<br><br><pre class=code>	void C_AddSurface(MeshInfo &amp;mesh_info,int no_tris,int no_verts,short tris[],float verts[],int surface)
	{

		int t;

		for( t=0;t&lt;no_tris;++t ){

			int v0=(int)tris[t*3];
			int v1=(int)tris[t*3+1];
			int v2=(int)tris[t*3+2];
			
			MeshCollider::Triangle tri;
			
			tri.verts[0]=v0;
			tri.verts[1]=v1;
			tri.verts[2]=v2;
			
			tri.surface=surface;
			tri.index=t;
	
			mesh_info.tri_list.push_back(tri);
		}

		int v;

		for( v=0;v&lt;no_verts;++v ){
		
			float vx=verts[v*3]; // surf.VertexX(v)
			float vy=verts[v*3+1]; // surf.VertexY(v)
			float vz=verts[v*3+2]; // surf.VertexZ(v)
	
			MeshCollider::Vertex vert;
	
			vert.coords.x=vx;
			vert.coords.y=vy;
			vert.coords.z=vz;
	
			mesh_info.vert_list.push_back(vert);
		}

	}</pre><br><br>Basically the Triangle and Vertex objects were being created using 'new' (allocated on the heap, which then wasn't released), when they didn't need to be.<br><br>Let me know if this fixes it. <br><br></td></tr></table><br>
<a name="967300"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Uncle</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yep that fixed it :)  Thanks for your quick answer and great support Si. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
