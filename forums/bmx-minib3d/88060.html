<!DOCTYPE html><html lang="en" ><head ><title >Problem converting from Blitz3D...</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Problem converting from Blitz3D...</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=119" >MiniB3D Module</a>/<a href="#bottom" >Problem converting from Blitz3D...</a><br><br>
<a name="999466"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is the original B3D code:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Const KEY_UP=200
Const KEY_PAGEUP=201
Const KEY_LEFT=203
Const KEY_RIGHT=205
Const KEY_END=207
Const KEY_DOWN=208
Const KEY_ESC=1

Graphics3D 640,480,32,2

;---------------------
;standard camera stoof
;---------------------
Local camera=CreateCamera()
Local light=CreateLight(1,camera)

;---------------------------
;Create an Object To look at
;---------------------------
Local ship=CreateCube()
Local exhaust1=CreateCylinder(8,True,ship)
Local exhaust2=CreateCylinder(8,True,ship)

EntityColor ship,50,50,100
ScaleMesh ship,1,.5,2

EntityColor exhaust1,128,128,200	
EntityColor exhaust2,128,128,200
ScaleEntity exhaust1,0.4,1,0.4		
ScaleEntity exhaust2,0.4,1,0.4
RotateEntity exhaust1,90,0,0		
RotateEntity exhaust2,90,0,0
MoveEntity exhaust1,-1,-1.2,0		
MoveEntity exhaust2,1,-1.2,0

; fake Floor... no create plane :P~
Local cube=CreateCube(ship)
MoveEntity cube, 0,-2,0
ScaleEntity cube, 100, 1, 100
EntityColor cube, 0,0,64

CreateTrail(exhaust1,exhaust2,8)

Local speed#=1
Local timer=CreateTimer(50)
Local move#=0
Local turn#=0

Repeat

	WaitTimer(timer)

	If KeyDown(KEY_UP) move#=move#+.4			;Forwards
	If KeyDown(KEY_DOWN) move#=move#-.4		;Backward
	If KeyDown(KEY_RIGHT) turn#=turn#-4		;Left
	If KeyDown(KEY_LEFT) turn#=turn#+4			;Right
	
	move=move*.9
	
	RotateEntity ship,0,turn,0
	MoveEntity ship,0,0,move
	
	update_camera(ship,camera,5,2)
	
	updatetrails()
	
	UpdateWorld()
	RenderWorld()
	
	Flip
	

Until KeyHit(KEY_ESC)

End

;----------------------------------------------------------------------
;These are just handy functions that have nothing To do with the trails
;----------------------------------------------------------------------

;---------------------------------------
;a chase cam For use in 3rd person games
;---------------------------------------
Function update_camera(character,camera,distance#,height#,angle%=180)

	;get player values
	Local px#=EntityX(character)
	Local py#=EntityY(character)
	Local pz#=EntityZ(character)
	
	Local pa#=EntityYaw(character)+angle
	
	;get camera values
	Local cx#=EntityX(camera)
	Local cy#=EntityY(camera)
	Local cz#=EntityZ(camera)
	
	;If EntityDistance(camera,character)&gt;Float(distance*1.5) EntityType camera,c_none
	
	;work out New values
	cx=curvevalue(cx,newxvalue(px,pa,distance),6)
	cy=curvevalue(cy,py+height,7)
	cz=curvevalue(cz,newzvalue(pz,pa,distance),6)

	;update camera position

	PositionEntity camera,cx,cy,cz
	PointEntity camera,character
	
	;EntityType camera,c_camera
	
End Function

;--------------------
;curve value Function
;--------------------
Function CurveValue#(current#,destination#,amount#)

	current=current+((destination-current)/amount)
	If current&lt;0.01 And current&gt;-0.01 Then current=0
	Return current

End Function

;---------
;newxvalue
;---------
Function NewXValue#(current#,ang#,dist#)

	Return current-Sin(ang)*dist

End Function

;---------
;newyvalue
;---------
Function NewYValue#(current#,ang#,dist#)

	Return current+Sin(ang)*dist

End Function

;---------
;newzvalue
;---------
Function NewZValue#(current#,ang#,dist#)

	Return current+Cos(ang)*dist

End Function



Const trail_update#=2.0

Type tTrail
	Field id,brush,surface
	Field tdx#[99], tdy#[99], tdz#[99]
	Field update%, Startalpha#, alpha#
	Field point1, point2
	Field polys
	Field R,G,B
End Type

;---------------------
;create a trail object
;---------------------
Function CreateTrail.tTrail(point1, point2, polys=20, startalpha#=1.0, R=255, G=255, B=255)
Local x#,y#,z#

	If polys&gt;20 Then polys = 20
		
	t.tTrail=New tTrail

	;create mesh and set properties
	t\id = CreateMesh()
	t\brush = CreateBrush()
	BrushBlend t\brush,3
	BrushFX t\brush,2+16
	t\surface = CreateSurface(t\id,t\brush)
	t\alpha= startalpha
	t\Startalpha= startalpha
	t\polys = polys
	t\R=R
	t\G=G
	t\B=B
	
	;check there are two trail strat points
	If point1=False Then RuntimeError "must specify 'point1' for one side of the trail" Else t\point1=point1
	If point2=False Then RuntimeError "must specify 'point2' for one side of the trail" Else t\point2=point2
	
	;mid pount between two trial objects
	x=(EntityX(t\point1,1)+EntityX(t\point2,1))/2
	y=(EntityY(t\point1,1)+EntityY(t\point2,1))/2
	z=(EntityZ(t\point1,1)+EntityZ(t\point2,1))/2

	For i=0 To polys-1
		AddVertex t\surface,x,y,z,Float(i)/Float(polys),1,0
		AddVertex t\surface,x,y,z,Float(i)/Float(polys),0,0
		VertexNormal t\surface,i,0,-1,0
		VertexNormal t\surface,i+1,0,-1,0
		
		VertexColor t\surface,i, 255, 255, 255, StartAlpha
		
		If i&gt;0
			AddTriangle t\surface,i*2,i*2-1,i*2-2
			AddTriangle t\surface,i*2,i*2+1,i*2-1
		End If
	Next
	
	Return t
	
End Function

;-----------------
;update all trails
;-----------------
Function UpdateTrails()
;Local x#,y#,z#
Local alpha#
Local R,G,B

;loop through all trails
For t.tTrail = Each tTrail

	; Move the trail pieces along.
	t\update=t\update+1 : If t\update=trail_update Then t\update=0

	;update		
	cv = (CountVertices(t\surface)-1) 
   ;cv = t\polys
   alpha = t\StartAlpha

	If t\update=0 And cv &gt; 2 Then
		For i=2 To cv
			t\tdx[i] = (VertexX(t\surface,i-2) - VertexX(t\surface,i))/Float(trail_update)
			t\tdy[i] = (VertexY(t\surface,i-2) - VertexY(t\surface,i))/Float(trail_update)
			t\tdz[i] = (VertexZ(t\surface,i-2) - VertexZ(t\surface,i))/Float(trail_update)
			
			;t\alpha=1.0-(i * 0.1)
			t\alpha=t\StartAlpha-(i * 0.1)
			If t\alpha&lt;=0 Then t\alpha=0
			If t\alpha &gt; alpha Then t\alpha = alpha
			alpha = t\alpha
			
			R=t\R - (t\polys-i) * 10 : If R&lt;0 Then R=0
			G=t\G - (t\polys-i) * 10 : If G&lt;0 Then G=0
			B=t\B - (t\polys-i) * 10 : If B&lt;0 Then B=0
			VertexColor t\surface,i,R, G, B, alpha

			VertexCoords(t\surface,i,VertexX(t\surface,i)+t\tdx[i],VertexY(t\surface,i)+t\tdy[i],VertexZ(t\surface,i)+t\tdz[i])
		Next

	Else	
		For i=2 To cv
			VertexCoords(t\surface,i,VertexX(t\surface,i)+t\tdx[i],VertexY(t\surface,i)+t\tdy[i],VertexZ(t\surface,i)+t\tdz[i])
		Next
	End If
	
	;position the first two verts at the back of the entity
	VertexCoords(t\surface,0,EntityX(t\point1,1),EntityY(t\point1,1),EntityZ(t\point1,1))
	VertexCoords(t\surface,1,EntityX(t\point2,1),EntityY(t\point2,1),EntityZ(t\point2,1))
	
Next
			
End Function


;--------------------------------
;free trail and delete trail type
;--------------------------------
Function FreeTrail(t.tTrail)

	If t&lt;&gt;Null Then
	   If t\id&lt;&gt;0 Then FreeEntity t\id
	   Delete t
	End If
	
End Function
</textarea><br><br>Here's the BlitzMax code:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Import sidsign.miniB3D
Const trail_update#=2.0
Global tTrail_List:TList = CreateList()

Type tTrail
	Field id:TMesh
	Field brush:TBrush
	Field surface:TSurface
	Field tdx#[99], tdy#[99], tdz#[99]
	Field update%, Startalpha#, alpha#
	Field point1:TEntity 
	Field point2:TEntity
	Field polys%
	Field R%,G%,B%
End Type

';---------------------
';Create a trail Object
';---------------------
Function CreateTrail:tTrail(point1:TEntity, point2:TEntity, polys%=20, startalpha#=1.0, R%=255, G%=255, B%=255)
Local x#,y#,z#
Local i%

	If polys&gt;20 Then polys = 20
		
	Local t:tTrail=New tTrail

	';Create mesh And set properties
	t.id = CreateMesh()
	t.brush = CreateBrush()
	BrushBlend t.brush,3
	BrushFX t.brush,2+16
	t.surface = CreateSurface(t.id,t.brush)
	t.alpha= startalpha
	t.Startalpha= startalpha
	t.polys = polys
	t.R=R
	t.G=G
	t.B=B
	
	';check there are two trail strat points
	'If point1=Null Then RuntimeError "must specify 'point1' for one side of the trail" Else t.point1=point1
	'If point2=Null Then RuntimeError "must specify 'point2' for one side of the trail" Else t.point2=point2
	t.point1=point1
	t.point2=point2
	
	'Mid pount between two trial objects
	x=(EntityX(t.point1,True)+EntityX(t.point2,True))/2
	y=(EntityY(t.point1,True)+EntityY(t.point2,True))/2
	z=(EntityZ(t.point1,True)+EntityZ(t.point2,True))/2

	For i=0 To polys-1
		AddVertex t.surface,x,y,z,Float(i)/Float(polys),1,0
		AddVertex t.surface,x,y,z,Float(i)/Float(polys),0,0
		VertexNormal t.surface,i,0,-1,0
		VertexNormal t.surface,i+1,0,-1,0
		
		VertexColor t.surface,i, 255, 255, 255, StartAlpha
		
		If i&gt;0
			AddTriangle t.surface,i*2,i*2-1,i*2-2
			AddTriangle t.surface,i*2,i*2+1,i*2-1
		End If
	Next
	
	ListAddLast tTrail_List, t
	Return t
	
End Function

';-----------------
';update all trails
';-----------------
Function UpdateTrails()
';Local x#,y#,z#
Local alpha#
Local R%,G%,B%
Local cv%
Local i%

';loop through all trails
For Local t:tTrail = EachIn tTrail_List

	'; Move the trail pieces along:
	t.update=t.update+1 
	If t.update=trail_update Then t.update=0

	';update		
	cv = (CountVertices(t.surface)-1) 
    ';cv = t.polys * 3
    alpha = t.StartAlpha

	If t.update=0 And cv &gt; 2 Then
		For i=2 To cv
			t.tdx[i] = (VertexX(t.surface,i-2) - VertexX(t.surface,i))/Float(trail_update)
			t.tdy[i] = (VertexY(t.surface,i-2) - VertexY(t.surface,i))/Float(trail_update)
			t.tdz[i] = (VertexZ(t.surface,i-2) - VertexZ(t.surface,i))/Float(trail_update)
			
			';t.alpha=1:0-(i * 0:1)
			t.alpha=t.StartAlpha-(i * 0.1)
			If t.alpha&lt;=0 Then t.alpha=0
			If t.alpha &gt; alpha Then t.alpha = alpha
			alpha = t.alpha
			
			R=t.R - (t.polys-i) * 10 
			If R&lt;0 Then R=0
			G=t.G - (t.polys-i) * 10 
			If G&lt;0 Then G=0
			B=t.B - (t.polys-i) * 10 
			If B&lt;0 Then B=0
			VertexColor t.surface,i,R, G, B, alpha

			VertexCoords(t.surface,i,VertexX(t.surface,i)+t.tdx[i],VertexY(t.surface,i)+t.tdy[i],VertexZ(t.surface,i)+t.tdz[i])
		Next

	Else	
		For i=2 To cv
			VertexCoords(t.surface,i,VertexX(t.surface,i)+t.tdx[i],VertexY(t.surface,i)+t.tdy[i],VertexZ(t.surface,i)+t.tdz[i])
		Next
	End If
	
	';position the first two verts at the back of the entity
	VertexCoords(t.surface,0,EntityX(t.point1,1),EntityY(t.point1,1),EntityZ(t.point1,1))
	VertexCoords(t.surface,1,EntityX(t.point2,1),EntityY(t.point2,1),EntityZ(t.point2,1))
	
Next
			
End Function


';--------------------------------
';free trail And Delete trail Type
';--------------------------------
Function FreeTrail(t:tTrail)

	If t&lt;&gt;Null Then
	   If t.id&lt;&gt;Null Then FreeEntity t.id
	   t.id=Null
	   t=Null
	End If
	GCCollect()
End Function


Graphics3D 640,480,32,2

';---------------------
';standard camera stoof
';---------------------
Local camera:TCamera=CreateCamera()
Local light:TLight=CreateLight(1,camera)

';---------------------------
';Create an Object To look at
';---------------------------
Local ship:TMesh=CreateCube()
Local exhaust1:TMesh=CreateCylinder(8,True,ship)
Local exhaust2:TMesh=CreateCylinder(8,True,ship)

EntityColor ship,50,50,100
ScaleMesh ship,1,.5,2

EntityColor exhaust1,128,128,200	
EntityColor exhaust2,128,128,200
ScaleEntity exhaust1,0.4,1,0.4		
ScaleEntity exhaust2,0.4,1,0.4
RotateEntity exhaust1,90,0,0		
RotateEntity exhaust2,90,0,0
MoveEntity exhaust1,-1,-1.2,0		
MoveEntity exhaust2,1,-1.2,0

' fake floor... no create plane :P~
Local cube:TMesh=CreateCube(ship)
MoveEntity cube, 0,-2,0
ScaleEntity cube, 100, 1, 100
EntityColor cube, 0,0,64

CreateTrail(exhaust1,exhaust2,8)

Local speed#=1
Local timer:TTimer=CreateTimer(50)
Local move#=0
Local turn#=0

Repeat

	WaitTimer(timer)

	If KeyDown(KEY_UP) move#=move#+.4			';Forwards
	If KeyDown(KEY_DOWN) move#=move#-.4		';Backward
	If KeyDown(KEY_RIGHT) turn#=turn#-4			';Left
	If KeyDown(KEY_LEFT) turn#=turn#+4			';Right
	
	move=move*.9
	
	RotateEntity ship,0,turn,0
	MoveEntity ship,0,0,move
	
	update_camera(ship,camera,5,2)
	
	updatetrails()
	
	UpdateWorld()
	RenderWorld()
	
	Flip
	

Until KeyHit(KEY_ESCAPE)

End

';----------------------------------------------------------------------
';These are just handy functions that have nothing To do with the trails
';----------------------------------------------------------------------

';---------------------------------------
';a chase cam For use in 3rd person games
';---------------------------------------
Function update_camera(character:TEntity,camera:TCamera,distance#,height#,angle%=180)

	'get player values
	Local px#=EntityX(character)
	Local py#=EntityY(character)
	Local pz#=EntityZ(character)
	
	Local pa#=EntityYaw(character)+angle
	
	';get camera values
	Local cx#=EntityX(camera)
	Local cy#=EntityY(camera)
	Local cz#=EntityZ(camera)
	
	'If EntityDistance(camera,character)&gt;Float(distance*1.5) EntityType camera,c_none
	
	'work out New values
	cx=curvevalue(cx,newxvalue(px,pa,distance),6)
	cy=curvevalue(cy,py+height,7)
	cz=curvevalue(cz,newzvalue(pz,pa,distance),6)

	'update camera position

	PositionEntity camera,cx,cy,cz
	PointEntity camera,character
	
	'EntityType camera,c_camera
	
End Function

'--------------------
'curve value Function
'--------------------
Function CurveValue#(current#,destination#,amount#)

	current=current+((destination-current)/amount)
	If current&lt;0.01 And current&gt;-0.01 Then current=0
	Return current

End Function

'---------
'newxvalue
'---------
Function NewXValue#(current#,ang#,dist#)

	Return current-Sin(ang)*dist

End Function

'---------
'newyvalue
'---------
Function NewYValue#(current#,ang#,dist#)

	Return current+Sin(ang)*dist

End Function

'---------
'newzvalue
'---------
Function NewZValue#(current#,ang#,dist#)

	Return current+Cos(ang)*dist

End Function
</textarea><br><br>I don't know why, but on BlitzMax/miniB3D the trail keeps disappearing with no apparent reason. Can anyone please help me and pinpoint what I'm doing wrong? <br><br></td></tr></table><br>
<a name="999473"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >xlsior</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> No idea why it disappears, but I did notice that it comes back if you change directions: Go forward until the trail disappears. Then go backwards, until THAT trail disappears. Now going forward again the trail re-appears. <br><br></td></tr></table><br>
<a name="999475"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah, keeping it "turning" non stop, also makes the trail appears for a little while... O_o <br><br></td></tr></table><br>
<a name="999478"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> It's a bug on miniB3D!<br><br>Adding meshcullradius(t.id,10000) before the "next" on UpdateTrails fixes it, forcing an enormous cull radius on the updated mesh.<br><br>miniB3D is culling the mesh out, even the brush having BrushFX 16 (no culling). Still have to dig deeper into the code to fix this though...<br><br>EntityFX 16 has no effect either... are those not implemented? <br><br></td></tr></table><br>
<a name="999481"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I found a partial solution. Open TGlobal.bmx, go to RenderCamera and change:<br><br><pre class=code>
			Local inview%=cam.EntityInFrustum(mesh)
</pre><br><br>to<br><br><pre class=code>
			' Perform frustum cull			
			Local inview%

			If ((mesh.brush.fx And 16) = 16) Then 
			   inview=True
			Else
			   inview=cam.EntityInFrustum(mesh)
			End If
</pre><br><br>Now setting EntityFX 16 works, but BrushFX won't... and I don't have a clue why. <br><br></td></tr></table><br>
<a name="999483"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Now it get even more confusing!<br><br>this works:<br><pre class=code>
	t.id = CreateMesh()
	t.brush = CreateBrush()

	BrushFX t.id.brush,2+16	
	BrushBlend t.brush,3

	t.surface = CreateSurface(t.id,t.brush)
</pre><br><br>but this won't (only on bmax, on b3d it does!):<br><pre class=code>
	t.id = CreateMesh()
	t.brush = CreateBrush()

	BrushFX t.brush,2+16	
	BrushBlend t.brush,3

	t.surface = CreateSurface(t.id,t.brush)
</pre><br><br>Apparently, CreateSurface does not relate t.brush with t.id, and it's creating a new brush, while on B3D it just sets the brush from id (or overwrites it with the new one)<br><br>Edit: Yes, it is some relational thing going on. doing like this:<br><br><pre class=code>	
       ';Create mesh And set properties
	t.id = CreateMesh()
	t.brush = CreateBrush()

	BrushFX t.brush,2+16	
	BrushBlend t.brush,3
	
	t.surface = CreateSurface(t.id,t.brush)
	
	PaintEntity t.id,t.brush
</pre><br>Also works! Maybe this PaintEntity line should be added to CreateSurface?<br><br>Even a simple "t.id.brush = t.brush" (instead of PaintEntity) solves this issue... <br><br></td></tr></table><br>
<a name="999484"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Finally!<br><br>Besides the fix on TGlobal.bmx and RenderCamera, this is also needed:<br><br>on TMesh.bmx, on CreateSurface change:<br><br><pre class=code>
		If bru&lt;&gt;Null
			surf.brush=bru.Copy()
		EndIf
</pre><br><br>to:<br><br><pre class=code>
		If bru&lt;&gt;Null
		   surf.brush=bru.Copy()
	           brush = bru.Copy()
		EndIf
</pre><br><br>And the code from Blitz3D will behave as expected. Phew! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
