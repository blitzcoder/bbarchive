<!DOCTYPE html><html lang="en" ><head ><title >TFrameBuffer Type</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >TFrameBuffer Type</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=119" >MiniB3D Module</a>/<a href="#bottom" >TFrameBuffer Type</a><br><br>
<a name="914248"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >iroker</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> i have writen a FBO Class for MiniB3D<br><br>here is the code:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Type TFrameBuffer
	Field Texture:TTexture
	Field depthbuffer:Int[1]
	Field fbo:Int[1]

	Function SetBuffer:TFrameBuffer(Texture:TTexture)
		Local FB:TFrameBuffer = New TFrameBuffer
		FB.Texture = Texture
		FB.CreateFBO()
		Return FB
	End Function
   
   
	Method CreateFBO()

		glGenFramebuffersEXT(1, fbo)
		glBindFramebufferEXT(GL_FRAMEBUFFER_EXT, fbo[0])
		glGenRenderbuffersEXT(1, depthbuffer)
		glBindRenderbufferEXT(GL_RENDERBUFFER_EXT, depthbuffer[0])
		glRenderbufferStorageEXT(GL_RENDERBUFFER_EXT, GL_DEPTH_COMPONENT, Texture.Width, Texture.Height)
		glFramebufferRenderbufferEXT(GL_FRAMEBUFFER_EXT, GL_DEPTH_ATTACHMENT_EXT, GL_RENDERBUFFER_EXT, depthbuffer[0]) 
		
		
		glBindTexture(GL_TEXTURE_2D, Texture.gltex[0])
		'	  glTexImage2D(GL_TEXTURE_2D, 0, GL_RGBA8,  Texture.Width, Texture.Height, 0, GL_RGBA, GL_UNSIGNED_BYTE, Null);
		'	  glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_WRAP_S, GL_CLAMP_TO_EDGE)
		'	  glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_WRAP_T, GL_CLAMP_TO_EDGE)
		glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER, GL_LINEAR)
		glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_LINEAR_MIPMAP_LINEAR)
		glGenerateMipmapEXT(GL_TEXTURE_2D)
		
		glFramebufferTexture2DEXT(GL_FRAMEBUFFER_EXT, GL_COLOR_ATTACHMENT0_EXT,GL_TEXTURE_2D, Texture.gltex[0], 0)
		
		
		Local status:Int =  glCheckFramebufferStatusEXT(GL_FRAMEBUFFER_EXT)
	 
		Select status
			Case GL_FRAMEBUFFER_COMPLETE_EXT
				Print "FBO created"
			Case GL_FRAMEBUFFER_INCOMPLETE_ATTACHMENT_EXT
			  	Print "Incomplete attachment"
			Case GL_FRAMEBUFFER_INCOMPLETE_MISSING_ATTACHMENT_EXT
			  	Print "Missing attachment"
			Case GL_FRAMEBUFFER_INCOMPLETE_DIMENSIONS_EXT
			  	Print "Incomplete dimensions"
			Case GL_FRAMEBUFFER_INCOMPLETE_FORMATS_EXT
			  	Print "Incomplete formats"
			Case GL_FRAMEBUFFER_INCOMPLETE_DRAW_BUFFER_EXT
			 	Print "Incomplete draw buffer"
			Case GL_FRAMEBUFFER_INCOMPLETE_READ_BUFFER_EXT
			  	Print "Incomplete read buffer"
			Case GL_FRAMEBUFFER_UNSUPPORTED_EXT
				Print "Type is not Supported"
			Default
			      Print "oops"
		EndSelect
	End Method

	Method Destroy()	
		glDeleteFramebuffersEXT(1, fbo)
		glDeleteRenderbuffersEXT(1, depthbuffer)
	End Method
	   
	Method BindBuffer()
		glBindFramebufferEXT(GL_FRAMEBUFFER_EXT , fbo[0])
		glClear(GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT)
	End Method
	
	Method UnBindBuffer()
		glBindFramebufferEXT(GL_FRAMEBUFFER_EXT , 0)
		glBindTexture(GL_TEXTURE_2D,texture.gltex[0]) ' wrong?
		glGenerateMipmapEXT(GL_TEXTURE_2D) ' wrong?
		glBindTexture(GL_TEXTURE_2D,0) ' wrong?

	End Method

End Type
</textarea><br><br>to test use it with this one:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import "../MiniB3D.bmx"
Include "TFrameBuffer.bmx"

Graphics3D 1024,768
'ClearTextureFilters

Local pivot:TPivot=CreatePivot()
PositionEntity pivot,0,2,0

Local t_sphere:TMesh=CreateSphere( 8 )
EntityShininess t_sphere,.2
For Local t=0 To 359 Step 36
	Local sphere:TMesh=TMesh(CopyEntity( t_sphere,pivot ))
	EntityColor sphere,Rnd(256),Rnd(256),Rnd(256)
	TurnEntity sphere,0,t,0
	MoveEntity sphere,0,0,10
Next
FreeEntity t_sphere

Local FB_texture:TTexture=CreateTexture( 1024,1024 )
ScaleTexture FB_texture,-1,1
Local FB:TFrameBuffer = TFrameBuffer.SetBuffer(FB_texture)

Local cube:TMesh=CreateCube()
EntityTexture cube,FB_texture
PositionEntity cube,0,7,0
ScaleEntity cube,3,3,3

Local light:TLight=CreateLight()
TurnEntity light,45,45,0

Local camera:TCamera=CreateCamera()

Local plan_cam:TCamera=CreateCamera()

'CameraViewport plan_cam,0,0,FB_Texture.Width,FB_Texture.Height
plan_cam.vx=0
plan_cam.vy=0
plan_cam.vwidth=FB_Texture.Width
plan_cam.vheight=FB_Texture.Height

TurnEntity plan_cam,90,0,0
PositionEntity plan_cam,0,20,0

Local d#=-20

While Not KeyHit(KEY_ESCAPE)

	If KeyDown(KEY_UP) d=d+1
	If KeyDown(KEY_DOWN) d=d-1
	If KeyDown(KEY_LEFT) TurnEntity camera,0,-3,0
	If KeyDown(KEY_RIGHT) TurnEntity camera,0,+3,0
	
	PositionEntity camera,0,7,0
	MoveEntity camera,0,0,d
	
	TurnEntity cube,.1,.2,.3
	TurnEntity pivot,0,1,0
	
	UpdateWorld

		HideEntity camera
		ShowEntity plan_cam
	FB.BindBuffer()
		RenderWorld
      FB.UnBindBuffer()

'	BackBufferToTex(texture)

	ShowEntity camera
	HideEntity plan_cam
	RenderWorld

	Flip 1
Wend
</textarea><br><br>i have problems on ATI to generate the Mipmaps after FB creation with nvidia its working fine. Who can help? <br><br></td></tr></table><br>
<a name="914263"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >klepto2</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> 2 things i have to add to this ;) <br><br>Try your testcode with 2 cameras or test it in general and you may notice that you don't render to the fully texture area. At least this was a problem I had have.<br><br><br>the mipmapgeneration should be done only once while you create the framebuffer. if you want i can send you my current Texturebuffer extension with cubemap support. <br><br></td></tr></table><br>
<a name="914290"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >iroker</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> the mipmap generation (once at FBO creation)  works only with my nvidia card, both Ati's dont.<br><br>what do you mean with "dont render to the fully textured area"?<br><br><br><br>Texturebuffer extension with cubemap support? sounds good! yes <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
