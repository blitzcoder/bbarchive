<!DOCTYPE html><html lang="en" ><head ><title >Latest Version</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Latest Version</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=119" >MiniB3D Module</a>/<a href="#bottom" >Latest Version</a><br><br>
<a name="729937"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >simonh</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Latest Version: 0.52 - 5th Dec 08<br><br>Download from <a href="/file/get.php?file=/Products/demos/minib3d-v052.zip" target="_blank">here</a>. <br><br></td></tr></table><br>
<a name="922597"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >simonh</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Updated. Adds LoadAnimSeq and fixes bone memory leak. <br><br></td></tr></table><br>
<a name="924632"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matty47</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Quietly snuck that update in without a fanfare..................: ) <br><br></td></tr></table><br>
<a name="959438"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >*</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> si can ya please update the download with this change <a href="http://www.blitzbasic.com/Community/posts.php?topic=83935#947970" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=83935#947970</a><br><br>I know its only a small change but to keep changing it every download is a pain, I can imagine that it put a lot of people off from using minib3d as well. <br><br></td></tr></table><br>
<a name="959485"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> hey edzup how are you dealing with the gimble lock issue? You are making a spaceship game with minib3d? <br><br></td></tr></table><br>
<a name="959530"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >*</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Jeremy: well simply keep all rotation (XYZ) and rotate a pivot, store rotations, then rotate the camera. This for me eliminates gimbal lock, I did this first in DB and it works in minib3d too. <br><br></td></tr></table><br>
<a name="959555"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here is a small test I did last week that uses quaternions to rotate objects. Maybe it can also help avoiding gimbal lock:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import sidesign.minib3d

'----------------------------------------------------------------------------------------------------------------------------------------------------
'from various resources on the bb.com site

Const QuatToEulerAccuracy# = 0.001
Const QuatSlerpAccuracy#   = 0.0001

Type TQuaternion

	Field w#, x#, y#, z#
	
	Function Create:TQuaternion()
	
		Local tq:TQuaternion = New TQuaternion
		tq.x = 0
		tq.y = 0
		tq.z = 0
		tq.w = 1
		Return tq
		
	End Function
		
	Method FromEuler(pitch#, yaw#, roll#)
		
		' NB roll is inverted due To change in handedness of coordinate systems
		Local cr# = Cos(-roll/2)
		Local cp# = Cos(pitch/2)
		Local cy# = Cos(yaw/2)
	
		Local sr# = Sin(-roll/2)
		Local sp# = Sin(pitch/2)
		Local sy# = Sin(yaw/2)
	
		' These variables are only here To cut down on the number of multiplications
		Local cpcy# = cp * cy
		Local spsy# = sp * sy
		Local spcy# = sp * cy
		Local cpsy# = cp * sy
	
		' Generate the output quat
		w = cr * cpcy + sr * spsy
		x = sr * cpcy - cr * spsy
		y = cr * spcy + sr * cpsy
		z = cr * cpsy - sr * spcy
				
	End Method
	
	' result will be the same rotation as doing q1 Then q2 (order matters!)
	Method Multiply(q2:TQuaternion)
	
		Local a#, b#, c#, d#, e#, f#, g#, h#
	
		a = (w + x) * (q2.w + q2.x)
		b = (z - y) * (q2.y - q2.z)
		c = (w - x) * (q2.y + q2.z)
		d = (y + z) * (q2.w - q2.x)
		e = (x + z) * (q2.x + q2.y)
		f = (x - z) * (q2.x - q2.y)
		g = (w + y) * (q2.w - q2.z)
		h = (w - y) * (q2.w + q2.z)
	
		w = b + (-e - f + g + h) / 2
		x = a - ( e + f + g + h) / 2
		y = c + ( e - f + g - h) / 2
		z = d + ( e - f - g + h) / 2
		
	End Method
	
End Type

Type Entity
	Field parent:Entity
	Field children:TList
	Field ent:TEntity
	Field q:TQuaternion

	Function Create:Entity(en:TEntity)

		e:Entity = New Entity
		e.ent = en
		e.q = TQuaternion.Create()
		e.parent = Null
		e.children = CreateList()

		Return e

	End Function
	
	Method Move(x#, y#, z#)

		MoveEntity ent, x, y, z

		For e:Entity = EachIn children
			e.Move(x, y, z)
		Next
		
	End Method
	
	Method Turn(x#, y#, z#)
	
		Local q2:TQuaternion = New TQuaternion
		q2.FromEuler(x, y, z)
		Self.q.Multiply(q2)
		Self.ApplyRotation()
		
		Local fine:Entity = Self		
		
	End Method
	
	Method SetParent(e:Entity)
		If e = Self Then Return
		If e = Null Then 
			Self.RemoveParent(parent)
			Return
		End If
		
		parent = e
		
		ListAddLast(e.children, Self)
		
	End Method
	
	Method RemoveParent(e:Entity)
		If e = Self Then Return
		If e &lt;&gt; parent Then Return
		
		parent = Null
		ListRemove(e.children, Self)
	End Method
		
	' convert a Quat To a Rotation
	Method ApplyRotation()

		Local fin:TQuaternion = New TQuaternion
		Local fin2:TQuaternion = New TQuaternion
		
		fin.x = q.x
		fin.y = q.y
		fin.z = q.z
		fin.w = q.w
		
		Local fine:Entity = Self
		While (fine.parent &lt;&gt; Null)
			fine = fine.parent
			fin2.x = fine.q.x
			fin2.y = fine.q.y
			fin2.z = fine.q.z
			fin2.w = fine.q.w
			fin2.Multiply(fin)
			fin.x = fin2.x
			fin.y = fin2.y
			fin.z = fin2.z
			fin.w = fin2.w
		Wend
	
		Local sint#, cost#, sinv#, cosv#, sinf#, cosf#
		Local cost_temp#
	
		sint = (2 * fin.w * fin.y) - (2 * fin.x * fin.z)
		cost_temp = 1.0 - (sint * sint)
	
		If Abs(cost_temp) &gt; QuatToEulerAccuracy
			cost = Sqr(cost_temp)
		Else
			cost = 0
		EndIf
	
		If Abs(cost) &gt; QuatToEulerAccuracy
			sinv = ((2 * fin.y * fin.z) + (2 * fin.w * fin.x)) / cost
			cosv = (1 - (2 * fin.x * fin.x) - (2 * fin.y * fin.y)) / cost
			sinf = ((2 * fin.x * fin.y) + (2 * fin.w * fin.z)) / cost
			cosf = (1 - (2 * fin.y * fin.y) - (2 * fin.z * fin.z)) / cost
		Else
			sinv = (2 * fin.w * fin.x) - (2 * fin.y * fin.z)
			cosv = 1 - (2 * fin.x * fin.x) - (2 * fin.z * fin.z)
			sinf = 0
			cosf = 1
		EndIf
	
		' Generate the output rotation
		roll# = -ATan2(sinv, cosv) '  inverted due To change in handedness of coordinate system
		pitch# = ATan2(sint, cost)
		yaw# = ATan2(sinf, cosf)
						
		RotateEntity ent, pitch, yaw, roll

		For e:Entity = EachIn children
			e.ApplyRotation
		Next

	End Method

End Type


'----------------------------------------------------------------------------------------------------------------------------------------------------
'example


Graphics3D 800, 600, 0, 2
CreateLight()

cam:Entity = Entity.Create(CreateCamera())

cube:Entity = Entity.Create(CreateCube())
EntityColor cube.ent, 255, 0, 0
EntityFX cube.ent, 1

cam.Move(0, 0, -15)

Repeat

	'TurnEntity cube.ent, 1, 0, 0
	cube.Turn(1, 0, 0)
	RenderWorld
	Flip
	
Until KeyHit(key_escape)

End

'----------------------------------------------------------------------------------------------------------------------------------------------------
</textarea><br>Example is at the bottom of the code. <br><br></td></tr></table><br>
<a name="959560"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> yes I stole some of that code to ask about it in this forum in another thread.here:http://www.blitzbasic.com/Community/posts.php?topic=84524<br><br>But when I integrated it into minib3d myself the parenting system broke (i see you have sone parenting code now, but minib3d's parent system is different) and I wasnt sure how to implement 'translateentity'<br><br>I though it might be faster if Simon integrate it himself because he is also using quaternions for the animation system, but no word from him yet. <br><br></td></tr></table><br>
<a name="959596"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >*</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> I will post my new turn entity functions I have somewhere, they use quaternions for rotation :)<br><br><pre class=code>
Import sidesign.minib3d'"..\minib3d.bmx"					'include the 

minib3d system

' TurnEntity quaternion example - mimics the effect of Blitz3D's TurnEntity with the global 

flag set to false

Strict

Graphics3D 640,480, 32

Local camera:TCamera=CreateCamera()
PositionEntity camera,0,0,-5

Local light:TLight=CreateLight()

Local cone:TMesh=CreateCone(4) ' create cone

While Not KeyDown( KEY_ESCAPE )
	If KeyDown( KEY_UP )=True Then EdzUpTurnEntity( Cone, -1, 0, 0, True )
	If KeyDown( KEY_DOWN )=True Then EdzUpTurnEntity( Cone, 1, 0, 0, True )
	If KeyDown( KEY_LEFT )=True Then EdzUpTurnEntity( Cone, 0, -1, 0, True )
	If KeyDown( KEY_RIGHT )=True Then EdzUpTurnEntity( Cone, 0, 1, 0, True )
	If KeyDown( KEY_Z )=True Then EdzUpTurnEntity( Cone, 0, 0, -1, True )
	If KeyDown( KEY_X )=True Then EdzUpTurnEntity( Cone, 0, 0, 1, True )
	
	RenderWorld
	Flip
Wend
End

Function EdzUpTurnEntity( Ent:TEntity, X:Float, Y:Float, Z:Float, Glob:Int = False )
'	Local P:Float = 0.0
'	Local Y:Float = 0.0
'	Local R:Float = 0.0
	Local Pitch:Float = 0.0
	Local Yaw:Float = 0.0
	Local Roll:Float = 0.0
	
	Local Quat:TQuaternion = EulerToQuat( 0.0, 0.0, 0.0 )		'create cone quat
	Local Turn_Quat:TQuaternion = EulerToQuat( 0.0, 0.0, 0.0 )	'create turn quat
	
	If Glob=False
		Quat = EulerToQuat( EntityPitch( Ent, True ), EntityYaw( Ent, True ), EntityRoll( Ent, True ) )	'Set Ent Quat
		Turn_Quat = EulerToQuat( X, Y, Z )		'Set turn quat
		Quat = MultiplyQuats( Quat, Turn_Quat )	'Multiply Entity quat with turn quat
		Quat = NormalizeQuat( Quat )			'normalise quat
		QuatToEuler2( Quat.x, Quat.y, Quat.z, Quat.w, Pitch, Yaw, Roll )	'Entity quat to euler
		RotateEntity Ent, Pitch, Yaw, Roll
	Else
		RotateEntity Ent, EntityPitch( Ent )+X, EntityYaw( Ent )+Y, EntityRoll( Ent )+Z
	EndIf
End Function

' Leadwerks function
Function EulerToQuat:TQuaternion(pitch#,yaw#,roll#)
	Local cr#=Cos(-roll#/2.0)
	Local cp#=Cos(pitch#/2.0)
	Local cy#=Cos(yaw#/2.0)
	Local sr#=Sin(-roll#/2.0)
	Local sp#=Sin(pitch#/2.0)
	Local sy#=Sin(yaw#/2.0)
	Local cpcy#=cp#*cy#
	Local spsy#=sp#*sy#
	Local spcy#=sp#*cy#
	Local cpsy#=cp#*sy#
	Local q:TQuaternion=New TQuaternion
	q.w#=cr#*cpcy#+sr#*spsy#
	q.x#=sr#*cpcy#-cr#*spsy#
	q.y#=cr#*spcy#+sr#*cpsy#
	q.z#=cr#*cpsy#-sr#*spcy#
	Return q
End Function

' Leadwerks function
Const QuatToEulerAccuracy#=0.001
Function QuatToEuler2(x#,y#,z#,w#,pitch# Var,yaw# Var,roll# Var)
	Local sint#=(2.0*w*y)-(2.0*x*z)
	Local cost_temp#=1.0-(sint#*sint#)
	Local cost#
	If Abs(cost_temp#)&gt;QuatToEulerAccuracy
		cost#=Sqr(cost_temp#)
		Else
		cost#=0.0
	EndIf
	Local sinv#,cosv#,sinf#,cosf#
	If Abs(cost#)&gt;QuatToEulerAccuracy
		sinv#=((2.0*y*z)+(2.0*w*x))/cost#
		cosv#=(1.0-(2.0*x*x)-(2.0*y*y))/cost#
		sinf#=((2.0*x*y)+(2.0*w*z))/cost#
		cosf#=(1.0-(2.0*y*y)-(2.0*z*z))/cost#
		Else
		sinv#=(2.0*w*x)-(2.0*y*z)
		cosv#=1.0-(2.0*x*x)-(2.0*z*z)
		sinf#=0.0
		cosf#=1.0
	EndIf
	pitch#=ATan2(sint#,cost#)
	yaw#=ATan2(sinf#,cosf#)
	roll#=-ATan2(sinv#,cosv#)
End Function

Function MultiplyQuats:TQuaternion(q1:TQuaternion,q2:TQuaternion)

	Local q:TQuaternion=New TQuaternion
	
	q.w = q1.w*q2.w - q1.x*q2.x - q1.y*q2.y - q1.z*q2.z
	q.x = q1.w*q2.x + q1.x*q2.w + q1.y*q2.z - q1.z*q2.y
	q.y = q1.w*q2.y + q1.y*q2.w + q1.z*q2.x - q1.x*q2.z
	q.z = q1.w*q2.z + q1.z*q2.w + q1.x*q2.y - q1.y*q2.x

	Return q

End Function

Function NormalizeQuat:TQuaternion(q:TQuaternion)

	Local uv#=Sqr(q.w*q.w+q.x*q.x+q.y*q.y+q.z*q.z)

	q.w=q.w/uv
	q.x=q.x/uv
	q.y=q.y/uv
	q.z=q.z/uv

	Return q

End Function
</pre><br>This works for me, will be testing it further as soon as I have login sorted and Server selection <br><br></td></tr></table><br>
<a name="959640"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> that seems to work pretty good thanks <br><br></td></tr></table><br>
<a name="959644"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >*</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Was wondering why Si didnt add it to the next release ;) <br><br></td></tr></table><br>
<a name="959662"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> have you ever had problems with Tformpoint?<br><br>It applies rotation to a matrix without using quaternions, <br>this could be the cause of a bug I was having with bullets being placed in the wrong place with Tformpoint.<br>Parenting also uses Turnentity instead of edzupturnentity which could easily be fixed by us.<br>Positionentity also applies rotation to a matrix without quaternions<br><br>@Warner, very good, <br>the parenting function needs a global flag to place and rotate the child <br><br></td></tr></table><br>
<a name="959701"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >*</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Never ever had a problem with TFormPoint, for me it works 100% of the time just like it does on Blitz3d. Also I dont have a problem with Parenting entities. <br><br></td></tr></table><br>
<a name="967103"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> edit: problem solved<br><br>I found <a href="/posts.php?topic=83935#947903" target="_blank">this thread</a> and I've update the source code to fix the bug.<br><br>Sorry, I think you need to update your source code. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
