<!DOCTYPE html><html lang="en" ><head ><title >max2d on MaxGui canvas - resize trouble</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >max2d on MaxGui canvas - resize trouble</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=119" >MiniB3D Module</a>/<a href="#bottom" >max2d on MaxGui canvas - resize trouble</a><br><br>
<a name="832753"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Difference</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for a great v.0.45 release.<br><br>I'm trying to hack together a working example of max2d on a resizable canvas with minib3d.<br><br>I can't seem to make max2D resize to fit the canvas.<br>I was thinking SetViewport 0,0, TGlobal.width, TGlobal.height in the EVENT_WINDOWSIZE event should do it, but it doesn't seem to work.<br><br>Can somebody help?<br><br>(resize window larger and hit spacebar to see clipping of fireworks)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'Import "../minib3d.bmx"
Strict
Import sidesign.minib3d
Import maxgui.win32maxguiex

Global width ,height 


SetGraphicsDriver GLMax2DDriver(),GRAPHICS_BACKBUFFER|GRAPHICS_DEPTHBUFFER

Local win:TGadget=CreateWindow("MiniB3D with Max 2d in a GUI window", 10, 10, 512, 512 )

Local can:TGadget=CreateCanvas(0,0,ClientWidth(win),ClientHeight(win),win,0)
SetGadgetLayout can,1,1,1,1

TGlobal.width=ClientWidth(win)
TGlobal.height=ClientHeight(win)

TGlobal.depth=16
TGlobal.mode=0
TGlobal.rate=60

SetGraphics CanvasGraphics(can)

TGlobal.GraphicsInit()

Local cam:TCamera=CreateCamera()
PositionEntity cam,0,0,-10

Local light:TLight=CreateLight(1)

Local tex:TTexture=LoadTexture("media/test.png")

Local cube:TMesh=CreateCube()
Local sphere:TMesh=CreateSphere()
Local cylinder:TMesh=CreateCylinder()
Local cone:TMesh=CreateCone() 

PositionEntity cube,-6,0,0
PositionEntity sphere,-2,0,0
PositionEntity cylinder,2,0,0
PositionEntity cone,6,0,0

EntityTexture cube,tex
EntityTexture sphere,tex
EntityTexture cylinder,tex
EntityTexture cone,tex

Local cx#=0
Local cy#=0
Local cz#=0

Local pitch#=0
Local yaw#=0
Local roll#=0

' used by fps code
Local old_ms:Int=MilliSecs()
Local renders:Int
Local fps:Int

Local up_key:Int
Local down_key:Int
Local left_key:Int
Local right_key:Int

CreateTimer( 60 )

' Create a spark type
Type spark
	Field x#,y#,z#,vy#,xd#,yd#,zd#,r#,g#,b#,alpha#
End Type

' Load spark image
Global sparki:TImage=LoadImage("media/spark.png")

' Set no. of sparks to be created per firework
Global no_sparks=500

' Create spark list
Global spark_list:TList=New TList

' Load and set font
Global font:TImageFont=LoadImageFont("Arial.ttf",1)
SetImageFont font



While True

	WaitEvent()

	Select EventID()

		Case EVENT_KEYDOWN
		
			Select EventData()
			
				Case KEY_SPACE
					AddFireWork()
			
				Case KEY_ESCAPE
					End
				Case KEY_UP
					up_key=True
				Case KEY_DOWN
					down_key=True			
				Case KEY_LEFT
					left_key=True
				Case KEY_RIGHT
					right_key=True	
			EndSelect
			
		Case EVENT_KEYUP
		
			Select EventData()
				Case KEY_UP
					up_key=False
				Case KEY_DOWN
					down_key=False			
				Case KEY_LEFT
					left_key=False
				Case KEY_RIGHT
					right_key=False	
			EndSelect

		Case EVENT_WINDOWCLOSE
		
            End

		Case EVENT_WINDOWSIZE
		
			TGlobal.width=ClientWidth(win)
			TGlobal.height=ClientHeight(win)

			cam.CameraViewport(0,0,TGlobal.width,TGlobal.height)
							
			width=ClientWidth(win) 				
			height=ClientHeight(win)
									
									
			' alas no effect ?												
			SetViewport 0,0, 	TGlobal.width, TGlobal.height
									
							
			DebugLog "EVENT_WINDOWSIZE" 

		Case EVENT_TIMERTICK

			If up_key Then cz#=cz#+1.0
			If left_key Then cx#=cx#-1.0
			If right_key Then cx#=cx#+1.0
			If down_key Then cz#=cz#-1.0

			MoveEntity cam,cx#*0.5,cy#*0.5,cz#*0.5
			RotateEntity cam,pitch#,yaw#,roll#
			
			cx#=0
			cy#=0
			cz#=0

			RedrawGadget can
              
		Case EVENT_GADGETPAINT
			
			SetGraphics CanvasGraphics(can)

			TurnEntity cube,0,1,0

			RenderWorld
			
	BeginMax2D() ' MiniB3D function
	UpdateMax2D() ' your function
	EndMax2D() ' MiniB3D function

	Flip 0
	'SetClsColor 200,0,0
	'Cls
                                
	EndSelect
	
Wend




End


Function AddFireWork()

	' If space key pressed then create new set of sparks (new firework)

	'If KeyHit(KEY_SPACE) 'Or  2=2

		Local x#=Rand(-100,100)
		Local y#=Rand(-100,100)
		Local z#=200

		Local r#=Rand(255)
		Local g#=Rand(255)
		Local b#=Rand(255)

		For Local i=1 To no_sparks

			Local speed# = 0.1

			Local ang1# = Rnd!(360)
			Local ang2# = Rnd!(360)

			Local sp:spark=New Spark
			spark_list.AddLast sp

			sp.x=x#
			sp.y=y#
			sp.z=z#

			sp.xd=Cos(ang1#)*Cos(ang2#)*speed#
			sp.yd=Cos(ang1#)*Sin(ang2#)*speed#
			sp.zd=Sin(ang1#)*speed#
	
			sp.r=r
			sp.g=g
			sp.b=b
	
			sp.alpha=1
	
		Next

'	EndIf

End Function

Function UpdateMax2D()


	' Draw all sparks

	For Local sp:spark=EachIn spark_list

		' If spark alpha is above 0 then draw it...

		If sp.alpha&gt;0

			sp.x=sp.x+sp.xd*10.0
			sp.y=sp.y+sp.yd*10.0
			sp.z=sp.z+sp.zd*10.0
			sp.y=sp.y+sp.vy#
			sp.vy=sp.vy+0.02

			' Calculate x and y draw values based on x,y,z co-ordinates
			Local x#=(width/2.0)+((sp.x/sp.z)*500)
			Local y#=(height/2.0)+((sp.y/sp.z)*500)

			sp.alpha=sp.alpha-0.01

			SetColor sp.r#,sp.g#,sp.b#
			SetBlend LIGHTBLEND
			SetAlpha sp.alpha
			SetScale 20/sp.z,20/sp.z
			DrawImage sparki,x#,y#

		'...else remove spark from spark list

		Else

			spark_list.Remove sp

		EndIf

	Next

	SetBlend SOLIDBLEND
	SetScale 1,1
	SetColor 255,255,255
	DrawText "Press space to ignite firework",0,20
	
End Function
</textarea> <br><br></td></tr></table><br>
<a name="834514"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >klepto2</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sorry for the delay: here is a solution for your problem:<br><br>Replace the BeginMax2D() code block in your code with this:<br><br><pre class=code>
		
	BeginMax2D() ' MiniB3D function
	glViewport(0,0,TGlobal.Width,TGlobal.Height)
		glScissor(0,0,TGlobal.Width,TGlobal.Height)
</pre> <br><br></td></tr></table><br>
<a name="834525"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Leon Drake</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> ah looks like i may need to do that as well. <br><br></td></tr></table><br>
<a name="834527"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Leon Drake</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> LOL i just tried it without BeginMax2D and it draws the 2d in 3d. <br><br></td></tr></table><br>
<a name="834573"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Leon Drake</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> just tested this, it does align the 2d however its scaled instead of repositioned, so when i try to draw something like a wireframe its offcentered. <br><br></td></tr></table><br>
<a name="834593"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Difference</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for looking into this.<br><br>Yes, get what Leon gets too.<br>F.ex. the text get stretched with the example above. <br><br></td></tr></table><br>
<a name="834603"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >klepto2</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah, getting this also with the new minib3d 045, will have a closer look tomorrow. <br><br></td></tr></table><br>
<a name="835124"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >klepto2</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>
Function BeginMax2D()

Local x,y,w,h
		GetViewport(x,y,w,h)
		
		glDisable(GL_LIGHTING)
		glDisable(GL_DEPTH_TEST)
		glDisable(GL_SCISSOR_TEST)
		glDisable(GL_FOG)
		glDisable(GL_CULL_FACE)

		glMatrixMode GL_TEXTURE
		glLoadIdentity
		
		glMatrixMode GL_PROJECTION
		glLoadIdentity
		glOrtho 0,GraphicsWidth(),GraphicsHeight(),0,-1,1
		
		glMatrixMode GL_MODELVIEW
		glLoadIdentity
		
		SetViewport x,y,w,h
		
		
		Local MaxTex:Int 
		glGetIntegerv(GL_MAX_TEXTURE_UNITS, Varptr(MaxTex))

		
		For Local Layer = 0 Until MaxTex
			glActiveTexture(GL_TEXTURE0+Layer)
					
			glDisable(GL_TEXTURE_CUBE_MAP)
			glDisable(GL_TEXTURE_GEN_S)
			glDisable(GL_TEXTURE_GEN_T)
			glDisable(GL_TEXTURE_GEN_R)
	
			glDisable(GL_TEXTURE_2D)
		Next
		
		glActiveTexture(GL_TEXTURE0)
		
		'DrawRect - 10 , - 10 , 5 , 5
		
		glViewport(0,0,TGlobal.Width,TGlobal.Height)
		glScissor(0,0,TGlobal.Width,TGlobal.Height)
		

End Function

Function EndMax2D()

		glDisable(GL_TEXTURE_CUBE_MAP)
		glDisable(GL_TEXTURE_GEN_S)
		glDisable(GL_TEXTURE_GEN_T)
		glDisable(GL_TEXTURE_GEN_R)
	
		glDisable(GL_TEXTURE_2D)

		TGlobal.EnableStates()

End Function
</pre><br><br>Replace these Functions with the original ones or just insert them in your App to override the original ones. This should work. <br><br></td></tr></table><br>
<a name="835153"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Difference</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> YES! Those work. Thanks! <br><br></td></tr></table><br>
<a name="835912"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Leon Drake</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> awesome <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
