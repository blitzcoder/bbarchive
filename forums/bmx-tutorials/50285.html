<!DOCTYPE html><html lang="en" ><head ><title >Blitz2D/3D/+ like type access...</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Blitz2D/3D/+ like type access...</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=112" >BlitzMax Tutorials</a>/<a href="#bottom" >Blitz2D/3D/+ like type access...</a><br><br>
<a name="559255"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >fredborg</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>A lot of people coming from previous versions of Blitz seem to have problems grasping the concept of how BlitzMax handles types. The good news is that you can emulate oldschool Blitz behavior... It doesn't make it any simpler, but this is one way of doing it:<pre class=code>Type mytype
	Field _value:Int	= 0

	'
	' Global list which holds all instances
	Global _list:TList = New TList

	'
	' This adds a new instance to the global list
	Method New()
		_list.addlast(Self)
	EndMethod

	'
	' Inserts this instance after the 'other' instance
	Method InsertAfter(other:mytype)
		_list.remove(Self)
		_list.InsertAfterLink(Self,_list.FindLink(other))
	EndMethod

	'
	' Inserts this instance before the 'other' instance
	Method InsertBefore(other:mytype)
		_list.remove(Self)
		_list.InsertBeforeLink(Self,_list.FindLink(other))
	EndMethod
	
	'
	' Gets the next instance in the global list
	Method After:mytype()
		Local link:TLink = _list.FindLink(Self).NextLink()
		If link
			Return mytype(link.Value())
		Else
			Return Null
		EndIf
	EndMethod

	'
	' Gets the previous instance in the global list
	Method Before:mytype()
		Local link:TLink = _list.FindLink(Self).PrevLink()
		If link
			Return mytype(link.Value())
		Else
			Return Null
		EndIf
	EndMethod
	
	'
	' Removes this instance from the list
	Method Destroy()
		_list.remove(Self)
	EndMethod
	
	'
	' Removes all instances
	Function DestroyEach()
		_list.clear()
	EndFunction
	
	'
	' Gets the last instance in the list
	Function Last:mytype()
		Return mytype(_list.last())
	EndFunction

	'
	' Gets the first instance in the list
	Function First:mytype()
		Return mytype(_list.first())
	EndFunction
		
End Type

'
' Lets create a bunch of dummy type instances
For i = 0 To 5
	m:mytype = New mytype
	m._value = i
Next

'
' For..EachIn
Print "Going through each instance, using a For..EachIn loop"
For m:mytype = EachIn mytype._list
	Print m._value
Next
Print "---~n"

'
' InsertBefore
Print "Inserting new instance before first instance"
m:mytype = New mytype
m._value = 6
m.InsertBefore mytype.First()
For m:mytype = EachIn mytype._list
	Print m._value
Next
Print "---~n"

'
' InsertAfter
Print "Inserting new instance after first instance"
m:mytype = New mytype
m._value = 7
m.InsertAfter mytype.First()
For m:mytype = EachIn mytype._list
	Print m._value
Next
Print "---~n"

'
' While..Wend (forward)
Print "Going through each instance, using a While..Wend loop"
m:mytype = mytype.first()
While m
	Print m._value
	m = m.after()
Wend
Print "---~n"

'
' While..Wend (backward)
Print "Going Backwards through each instance, using a While..Wend loop"
m:mytype = mytype.last()
While m
	Print m._value
	m = m.before()
Wend
Print "---~n"

'
' Destroy
Print "Removing the first instance, and showing remaining instances"
m:mytype = mytype.first()
m.Destroy()
For m:mytype = EachIn mytype._list
	Print m._value
Next
Print "---~n"

'
' DestroyEach
Print "Removing each instance, and showing whats left"
mytype.DestroyEach()
For m:mytype = EachIn mytype._list
	Print m._value
Next
Print "---~n"

Print "That's all folks!"
</pre>As you can see the basic principle is to have a global list inside a type. And then when adding a new type instance (using bla:type = New type), it is automatically added to the type list.<br><br>As you will also notice is that to emulate all the oldschool Blitz commands you need quite a few methods and functions inside your type. The good part is that you will need to change very little to get it working on a new type of type.<br><br>One function/method is missing from this, namely the infamous 'Insert bla1 before/after bla2', but if you examine the code I'm sure you can add that yourself.<br><br>As mentioned at the beginning, it doesn't make it any easier to use a type, but typically you will only need a 'New()' and 'Destroy()' method...<br><br>Did that make any sense?<br><br>[Edit] Added InsertBefore + InsertAfter and tuned a bit... <br><br></td></tr></table><br>
<a name="559264"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Robert Cummings</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Very helpful :) <br><br></td></tr></table><br>
<a name="559299"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Who was John Galt?</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Interesting...<br>Didn't know you could overload 'New' like that. I take it Max creates a New type instance before calling your new() method on it (you don't explicitly create a new instance as far as I can see)? <br><br></td></tr></table><br>
<a name="559353"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >fredborg</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>Yep, the new type instance is created first and then the type specific New() method is called afterwards. It's an excellent implementation which allows you to do all sorts of weird initialization of type instances. <br><br></td></tr></table><br>
<a name="562324"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >EOF</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> InsertBefore and InsertAfter functions which can be adapted for the above:<br><pre class=code>Function InsertAfter(list:TList,a:Object,b:Object)
	list.InsertAfterLink(b,list.FindLink(a))
End Function

Function InsertBefore(list:TList,a:Object,b:Object)
	list.InsertBeforeLink(b,list.FindLink(a))
End Function


' example

Local t:TList=New TList
t.AddLast "Hello"
t.AddLast "world"

InsertAfter t,"Hello","my"
InsertBefore t,"world","magic"

For Local a$=EachIn t
	Print a$
Next</pre> <br><br></td></tr></table><br>
<a name="562408"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Robert Cummings</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Jolly good show. Will be using all. <br><br></td></tr></table><br>
<a name="573109"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Beaker</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Fredborgs example can be simplified a little to make it more transportable to other Types:<br><pre class=code>'Old style Blitz lists by fredborg
'beak tweak

Type mytype
	Field _value:Int	= 0

	'
	' Global list which holds all instances
	Global _list:TList

	'
	' This adds a new instance to the global list
	Method New()
		If _list = Null
			_list = New TList
		EndIf
		_list.addlast(Self)
	EndMethod
	
	'
	' Gets the next instance in the global list
	Method After:mytype()
		Local link:TLink = ListFindLink(_list,Self).NextLink()
		If link
			Return mytype(link.Value())
		Else
			Return Null
		EndIf
	EndMethod

	'
	' Gets the previous instance in the global list
	Method Before:mytype()
		Local link:TLink = ListFindLink(_list,Self).PrevLink()
		If link
			Return mytype(link.Value())
		Else
			Return Null
		EndIf
	EndMethod
	
	'
	' Removes this instance from the list
	Method Destroy()
		_list.remove(Self)
	EndMethod
	
	'
	' Removes all instances
	Function DestroyEach()
		_list.clear()
	EndFunction
	
	'
	' Gets the last instance in the list
	Function Last:mytype()
		Return mytype(_list.last())
	EndFunction

	'
	' Gets the first instance in the list
	Function First:mytype()
		Return mytype(_list.first())
	EndFunction
		
End Type

'
' Lets create a bunch of dummy type instances
For i = 0 To 5
	m:mytype = New mytype
	m._value = i
Next

'
' For..EachIn
Print "Going through each instance, using a For..EachIn loop"
For m:mytype = EachIn mytype._list
	Print m._value
Next
Print "---~n"

'
' While..Wend (forward)
Print "Going through each instance, using a While..Wend loop"
m:mytype = mytype.first()
While m
	Print m._value
	m = m.after()
Wend
Print "---~n"

'
' While..Wend (backward)
Print "Going Backwards through each instance, using a While..Wend loop"
m:mytype = mytype.last()
While m
	Print m._value
	m = m.before()
Wend
Print "---~n"

'
' Destroy
Print "Removing the first instance, and showing remaining instances"
m:mytype = mytype.first()
m.Destroy()
For m:mytype = EachIn mytype._list
	Print m._value
Next
Print "---~n"

'
' DestroyEach
Print "Removing each instance, and showing whats left"
mytype.DestroyEach()
For m:mytype = EachIn mytype._list
	Print m._value
Next
Print "---~n"

Print "That's all folks!"</pre> <br><br></td></tr></table><br>
<a name="573133"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >sswift</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Am I to understand that types in Blitzmax are now merely ordinary C style structures, no longer automatically treated as memberes of a set of linked lists?<br><br>Also, what the hell is the difference between a method and a function?  They look like they're used exaclty the same as one another.<br><br>And what's with the _ in front of all the type fields?  Unless that does something in BlitzMax, I say it's a bad programming convention because you're not supposed to start variable names with anything other than a letter, and it makes me think it does something, like when you put a * in front of something in C. :-) <br><br></td></tr></table><br>
<a name="573139"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Koriolis</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Am I to understand that types in Blitzmax are now merely ordinary C style structures <br></div>I'm not sure to see what you call "C style structures". Custom types are like what is called classes in most other languages.<div class="quote"> no longer automatically treated as memberes of a set of linked lists <br></div>Correct. This is really fine like that, more consistent. You can easily have any number of object lists you want, and BlitzMax comes with built-in list support, so that's really easy.<br><br><div class="quote"> Also, what the hell is the difference between a method and a function? <br></div>The same as in other OO languages. A method is always applied to given object, you can see a method call as a function call with an implicit ("hidden") parameter, which is the object on which the method call is applied.<br>But unlike a function with an object parameter, you can *override* a method. There is no way I can easily explain in a few lines what it really means, I suggest you read some OO tutorial (I seem to recall that Mark done such a tutorial, which is a good starting point and should be more than enough to start with).<br><br><div class="quote"> And what's with the _ in front of all the type fields? Unless that does something in BlitzMax, I say it's a bad programming convention because you're not supposed to start variable names with anything other than a letter <br></div>Who said that? This would be true in C, but it's BlitzMax. The leading underscore is only a naming convention, to easily see what is a field and what is not.<br>I personnaly don't like this convention, and don't use it. I prefer putting a *trailing* underscore on fields that should be treated as private (not accessed from the outside of the type), and only these ones.<br>I use a trailing underscore rather than a leading one, for 2 reasons:<br>- the leading underscore makes it hard to read, which is really not good. We read from left to right, and I usually don't need that little bit of hint, so better not have it get in the way. A trailing underscore on the other hand is far less annoying to me.<br>- C and C++ allow leading underscores only in very few cases (and then you'd better not use them at all). As I program in C++ every day, better not confuse myself and use something that is a no no in C++. Not to say what I program is 95% C++ code, exposed to BlitzMax via a DLL.<br><br>I really find this convention more sensible. <br><br></td></tr></table><br>
<a name="573140"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Beaker</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> You are correct: Type objects aren't automatically stored in a list in BlitzMax.<br><br>Difference between a method and a function: a method is applied to a particular object, a function isn't. Like so:<br><pre class=code>
myThing.doMethodThing() ' calls doMethodThing which changes myThing
myType.doFunctionThing() ' calls doFunctionThing but has no specific object to work with</pre><br><br>Almost like a function is a global call and a method is a local call.  Very handy.<br><br>You don't need an underscore.  I think fred probably used it so that it wouldn't clash with any other fields/globals/funcs etc. <br><br></td></tr></table><br>
<a name="573141"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >sswift</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> I still don't understand why you need both.<br><br>If you only could have methods in a type, is there anything you couldn't do that you could do if you also had functions? <br><br></td></tr></table><br>
<a name="573142"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Koriolis</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well it's like saying that given that arrays can contain any type of data, why not have everything be an array, and just make one-lenghted arrays if you need only one variable. Similarly, you can do with a method everything you can with a function, but the reverse is not true and when you only need a function, better just create a function.<br><br>If you write a dummy "Print" function, you probably don't need any object for that. Then use a function, not a method. Functions inside types are no different. Functions inside types work just like function outside types, you are just given the ability to put them inside the type, which provides a separate namespace (MyType1.Print and MyType2.Print won't collide). So by exampleif you have a simple function that you plan to use from inside methods of the types (or in the context of using objects of these types), then naturally you'll put them inside the type.<br><br>I guess this "namespace" issue answers your question. <br><br></td></tr></table><br>
<a name="573149"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Beaker</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Its all just part of the OO "big picture".<br><br>In some ways I disagree with Koriolis, in my eyes they are both very different.  A function can't be called on a specific object, and a method can't be called on anything but an object.<br><br>They both prevent you from using them in the wrong way, for example: you won't ever be able to call a method unless you have an object for it to manipulate.  <br><br>Similarly, a function can't be called unless you refer to its Type name (or namespace), which lets you have many functions named Hello() but each one is only associated with one Type:<br><br><pre class=code>Type a
	Field x,y
	
	Function Hello()
		Print "A says Hello"
	End Function
EndType

Type b
	Field q,p
	
	Function Hello()
		Print "B says Hello"
	End Function
EndType

a.Hello
b.Hello</pre> <br><br></td></tr></table><br>
<a name="573155"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Koriolis</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Even after rereading my post I hardly see how you could believe I was saying a function and a method are the same to me. I just said that you could technically use only methods as SSwift suggests, but I also implied it wouldn't make much sense.<br>I know I'm not often crystal clear, but here you'll make me paranoid about the crazy difference between what I say and what people understand.<br><br>An additional note: yes, function and methods are actually far closer than what you imply. Saying that a method can't be called on anything other than an object is true, but not enough. Because similarly a function taking an object parameter can't be called with a parameter that would be anything other than an (compatible) object. That's really the same thing here.<br><br>The real *BIG* difference is the ability to override a method, which is pretty much what OO is all about. And this ability is what makes the object you apply the method to such a particular (hidden and implcit) parameter. Here lies the very important difference to me.<br>Of course, that's the technical side of thing. From an OO programmer's view point it's healthier to really think in terms of objects, and "feel" methods as an operation that "belongs" to the type. <br><br></td></tr></table><br>
<a name="573254"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Beaker</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sorry, you are right, I was just nitpicking. :) <br><br></td></tr></table><br>
<a name="573323"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Duckstab[o]</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Type Functions are related to the Type itself<br><br>Type Methods are related to the instance of the type<br><br><pre class=code>
Global Playerlist:TList=CreateList() 'To Store all the players


Type Player
	
	Field Name$
	Field HP,MP
	
	Function Create(_Name$,_HP,_MP)
		Temp:Player=New Player
			Temp.Name$=_Name$
			Temp.HP=_HP
			Temp.MP=_MP
		ListAddLast playerlist,temp	
	End Function
	
End Type



Player.create("Bobby",100,40) 'creates a new player using the function create in the type and adds it to the playerlist
</pre><br><br>Now lets add some new functions to to the type<br><br><pre class=code>



Global Playerlist:TList=CreateList() 'To Store all the players

Type Player
	
	Field Name$
	Field HP,MP
	
	Function Create(_Name$,_HP,_MP)
		Temp:Player=New Player
			Temp.Name$=_Name$
			Temp.HP=_HP
			Temp.MP=_MP
		ListAddLast playerlist,temp	
	End Function
	
	Function PrintList()
		For Temp:Player=EachIn Playerlist
			Print
			Print "NAME= "+temp.Name$
			Print "HP  = "+temp.HP
			Print "MP  = "+temp.MP
			Print
		Next
	End Function
	
	
End Type

Player.create("Bobby",100,40) 'creates a new player using the function create in the type 
Player.create("Bilby",400,40)

Player.Printlist' list all players in playerlist
</pre><br><br>now using a combination of functions and type <br><br>Functions to create and find <br>Methods to alter an instance<br><br><pre class=code>
Global Playerlist:TList=CreateList() 'To Store all the players


Type Player
	
	Field Name$
	Field HP,MP
	
	Function Create(_Name$,_HP,_MP)
		Temp:Player=New Player
			Temp.Name$=_Name$
			Temp.HP=_HP
			Temp.MP=_MP
		ListAddLast playerlist,temp	
	End Function
	
	Function PrintList()
		For Temp:Player=EachIn Playerlist
			Print
			Print "NAME= "+temp.Name$
			Print "HP  = "+temp.HP
			Print "MP  = "+temp.MP
			Print
		Next
	End Function
	
	Function Find:Player(_Name$)
		For Local P:player = EachIn playerList
			If P.Name$ = _Name$ Then Return P
		Next
		Return Null
	End Function

	
	Method Add_HP(_Boost)
	  Hp:+_Boost
	End Method
	
	Method Add_MP(_Boost)
	  Mp:+_Boost
	End Method
	
End Type



Player.create("Bobby",100,40) 'creates a new player using the function create in the type 
Player.create("Bilby",400,40)

Player.Printlist' list all players in playerlist

player.Find("Bobby").Add_Hp(40) 'this find an instance using a type function and applys the method add_hp
player.Find("Bilby").Add_Hp(140)

Player.Printlist' list all players in playerlist

player.Find("Bobby").Add_Mp(40)
player.Find("Bilby").Add_Mp(140)

Player.Printlist' list all players in playerlist
</pre><br><br>The real gain in using method and functions is seen when you have several types and instances as using these kind programming styles will cut down the amount of coding and makes finding and altering instances faster and more reliable<br><br><pre class=code>


Global Playerlist:TList=CreateList() 'To Store all the players
Global Baddielist:TList=CreateList() 'To store all the baddies



Type Player
	
	Field Name$
	Field HP,MP
	
	Function Create(_Name$,_HP,_MP)
		Temp:Player=New Player
			Temp.Name$=_Name$
			Temp.HP=_HP
			Temp.MP=_MP
		ListAddLast playerlist,temp	
	End Function
	
	Function PrintList()
		For Temp:Player=EachIn Playerlist
			Print
			Print "NAME= "+temp.Name$
			Print "HP  = "+temp.HP
			Print "MP  = "+temp.MP
			Print
		Next
	End Function
	
	Function Find:Player(_Name$)
		For Local P:player = EachIn playerList
			If P.Name$ = _Name$ Then Return P
		Next
		Return Null
	End Function

	
	Method Add_HP(_Boost)
	  Hp:+_Boost
	End Method
	
	Method Add_MP(_Boost)
	  Mp:+_Boost
	End Method
	
End Type

Type Baddie
	
	Field Name$
	Field HP,MP
	
	Function Create(_Name$,_HP,_MP)
		Temp:Baddie=New Baddie
			Temp.Name$=_Name$
			Temp.HP=_HP
			Temp.MP=_MP
		ListAddLast Baddielist,temp	
	End Function
	
	Function PrintList()
		For Temp:Baddie=EachIn Baddielist
			Print
			Print "NAME= "+temp.Name$
			Print "HP  = "+temp.HP
			Print "MP  = "+temp.MP
			Print
		Next
	End Function
	
	Function Find:Baddie(_Name$)
		For Local B:Baddie = EachIn BaddieList
			If B.Name$ = _Name$ Then Return B
		Next
		Return Null
	End Function

	
	Method Add_HP(_Boost)
	  Hp:+_Boost
	End Method
	
	Method Add_MP(_Boost)
	  Mp:+_Boost
	End Method
	
End Type



Player.create("Bobby",100,40) 'creates a new player using the function create in the type 
Player.create("Bilby",400,40)

Player.Printlist' list all players in playerlist

player.Find("Bobby").Add_Hp(40)
player.Find("Bilby").Add_Hp(140)

Player.Printlist' list all players in playerlist

player.Find("Bobby").Add_Mp(40)
player.Find("Bilby").Add_Mp(140)

Player.Printlist' list all players in playerlist

baddie.create("Orc",300,20)
baddie.create("Goblin",200,10)

baddie.printlist' list all baddies in baddielist

baddie.find("Orc").Add_Hp(400)
baddie.find("Goblin").Add_Mp(50)

baddie.printlist' list all baddies in baddielist

</pre> <br><br></td></tr></table><br>
<a name="614107"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AD</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you Duckstab[o], your tutorial really helped me understand how Blitz Max uses types -- I like the way its changed, although it can be quite confused at first.<br><br>For a long time I've been working with the Blitz Basic version I got free with a magazine and I finally decided to upgrade because of the GUI stuff, but mainly so I could learn and do OO stuff.<br><br>Many thanks again. <br><br></td></tr></table><br>
<a name="666116"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> You should use tList.addfirst(), not tList.addlast():<br><a href="http://blitzbasic.com/Community/posts.php?topic=59747" target="_blank">http://blitzbasic.com/Community/posts.php?topic=59747</a> <br><br></td></tr></table><br>
<a name="881463"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> You should use tList.addfirst(), not tList.addlast() <br></div><br>Why? They both take the same amount of time to complete. There is no reason to suggest using one over the other unless there is some specific use case for it.<br>(like you specify in your link) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
