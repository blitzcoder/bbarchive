<!DOCTYPE html><html lang="en" ><head ><title >Introduction to TYPE Variables &amp; Linked Lists</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Introduction to TYPE Variables &amp; Linked Lists</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=112" >BlitzMax Tutorials</a>/<a href="#bottom" >Introduction to TYPE Variables &amp; Linked Lists</a><br><br>
<a name="1290636"></a>

<a name="1290637"></a>

<a name="1290638"></a>

<a name="1290643"></a>

<a name="1290645"></a>

<a name="1290665"></a>

<a name="1290666"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is really REALLY new for me, but now that I finally understand it, I must share it with the rest of you.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' Rainbow Rain - an experiment and tutorial in TYPE variables and LINKED LISTS
Strict

Const speed=10 ' change to 1 for maximum speed
Const depth=0 ' change to higher # for more raindrops per cycle

SeedRnd MilliSecs()
SetGraphicsDriver GLMax2DDriver(),0 ' zero forces front buffer
Graphics 768,576

Type particle ' My 1st attempt at using TYPE variables !
  Field x ' x-coordinates
  Field y ' y-coordinates
  Field color$ ' TYPE variables can hold more than one type AND value !
End Type ' this type has 2-integers and one string for it

Local list:TList=CreateList() ' this is called a LINKED LIST
Local dot:particle,ilist:particle ' a type of array holding more than one value
Local r,i

Repeat ' (* MAIN *)

Cls ' clear display so no streaks are left on screen
If KeyDown(32) ' hold down SPACE to add a raindrop
  For i=0 To depth
  dot=New particle ' we are DEFINING a special type of variable now
  dot.x=Rand(-576,767) ' x-position of rain
  dot.y=0 ' unnecessary it seems as by saying NEW it is already zero
  r=Rand(0,5) ' now we add an entirely different type, not integer but STRING !
  If r=0 Then dot.color$="purple"
  If r=1 Then dot.color$="red"
  If r=2 Then dot.color$="orange"
  If r=3 Then dot.color$="yellow"
  If r=4 Then dot.color$="green"
  If r=5 Then dot.color$="blue"
  ListAddFirst list,dot ' add this data to the linked list
  Next
EndIf

SetColor 255,255,255 ' white (for our rain count)
DrawText "Rain: "+list.count(),0,0 ' show how many drops are in the air
For ilist=EachIn list ' EACHIN will work with non-numeric values, and we're looping only as many raindrops as there are
  If ilist.color$="purple" Then SetColor 255,0,255
  If ilist.color$="red" Then SetColor 255,0,0
  If ilist.color$="orange" Then SetColor 255,128,0
  If ilist.color$="yellow" Then SetColor 255,255,0
  If ilist.color$="green" Then SetColor 0,255,0
  If ilist.color$="blue" Then SetColor 0,0,255
  DrawLine ilist.x,ilist.y,ilist.x+7,ilist.y+14 ' simple streak
  ilist.x:+1 ' move rain down by 2 and across by 1
  ilist.y:+2
  If ilist.y&gt;=576 Or ilist.x&gt;=768 Then list.remove ilist ' if out of boundaries, remove from list
Next ' and YES this does affect the total count, it's not zeroed out, it's actually REMOVED !
' powerful business linked lists are

Delay speed ' decrease this number to slow down animation effect
glflush ' show our work
Until KeyDown(27) ' (* END OF MAIN *)
</textarea><br><br><img src="http://www.writerscafe.org/uploads/rte/d3243a2c5c81c3ed5c77e51ce9e4fa91.png"><br><br>Basically it draws colored raindrops, but NOTICE that no arrays are used in this code to keep track of the raindrop positions OR their color NOR are we keeping track of where they've been in a separate array either.<br><br>No, this program is using a method called LINKED LISTS. In it, you can store multiple kinds of data from a TYPE variable.<br><br><img src="http://www.writerscafe.org/uploads/rte/7c224da60d3597175523d385a7c56a7e.png"><br><br>A TYPE variable as near as I can tell is a definition class, much like you are defining a variable to be integer, float, string, timage, or pixmap.<br><br>But it is none of these. Instead a TYPE variable is a bit like a box and can contain multiple kinds of data that can be recalled instantly from a LINKED LIST. And yes, you can even have arrays inside Types for added power and complexity.<br><br><img src="http://www.writerscafe.org/uploads/rte/93d01691d4fc127ad574256c18bbf669.png"><br><br>Think of a LINKED LIST as a large warehouse for these TYPE boxes. When you run the program, we are essentially taking the warehouse, raising it about 300 feet in the air, removing the roof, and any time you hit SPACE a box (raindrop) is created right at the top of it.<br><br>With gravity, it falls, but something interesting happens when it hits the ground.<br><br><img src="http://www.writerscafe.org/uploads/rte/628d3a5b316afe0300008917b641798f.png"><br><br>I have coded it above so when it hits the bottom of the screen it is removed. Now read that carefully, it is REMOVED, not zeroed out or changed to a negative value nor are we decreasing a total - in fact if you check, there is no variable recording the total raindrops in the air.<br><br>And you do not have to keep track of the number of elements in them. Unlike other programs where you 'zero' out a condition, or when you may have to do zero and blank out multiple array values just to get the desired effect it has been deleted.<br><br>No, instead you can tell your program that the item that is there is no longer needed and you can actually REMOVE IT from the linked list with the REMOVE command. In one command alone.<br><br>There are many advantages to this over conventional arrays. You can essentially fire and forget - create boxes on the fly when you need them and remove them just as easily, and you don't have to keep track of the number of them either, that is handled automatically in the LINKED LIST and EACHIN.<br><br>What are some things you could use with LINKED LISTS ? Particles for one, anything that can spawn and die rapidly and varies in the number all the time.<br><br>Imagine having one ship attack you in a top-view shooter and then when one enemy ship hasn't been hit for a-while it SPAWNS a new enemy ship for you to fight.<br><br>That would be a nightmare to do with normal arrays, but with TYPES and linked lists, it would be quite simple.<br><br>So overall BlitzMAX is more powerful than you give it credit for. Let it do the hard work for you to keep track of all your loose variable items and arrays, conducted entirely in a TYPE variable and created, recalled, modified, and removed, from a LINKED LIST.<br><br><b>Hope This Helps !</b> <br><br></td></tr></table><br>
<a name="1290683"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cocopino</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi. <br>A Blitzmax type can hold more than just variables, it can also contain functions/methods and has a constructor "New". This is where you should add the code that creates the particle.<br><br>Also, you could add a method "Draw", this will draw the current particle at its location. To illustrate I've moved around some of your code:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' Rainbow Rain - an experiment and tutorial in TYPE variables and LINKED LISTS
Strict

Const speed=10 ' change to 1 for maximum speed
Const depth=0 ' change to higher # for more raindrops per cycle

SeedRnd MilliSecs()
SetGraphicsDriver GLMax2DDriver(),0 ' zero forces front buffer
Graphics 768,576

Type particle ' My 1st attempt at using TYPE variables !

  Field x ' x-coordinates
  Field y ' y-coordinates
  Field color:Int ' TYPE variables can hold more than one type AND value !

  Method New()
	  Self.x = Rand(-576, 767) ' x-position of rain
	  Self.y = 0 ' unnecessary it seems as by saying NEW it is already zero
	  Self.color = Rand(0, 5)
  End Method
  
  Method Draw()
  	Select Self.color
  		Case 0
			SetColor 255, 0, 255
		Case 1
			SetColor 255, 0, 0
		Case 2
			SetColor 255, 128, 0
		Case 3
			SetColor 255, 255, 0
		Case 4
			SetColor 0, 255, 0
		Case 5
			SetColor 0, 0, 255
	End Select

	DrawLine Self.x, Self.y, Self.x + 7, Self.y + 14 ' simple streak
	Self.x:+1 ' move rain down by 2 and across by 1
	Self.y:+2
	
  End Method
  
End Type ' this type has 2-integers and one string for it

Local list:TList = CreateList() ' this is called a LINKED LIST
Local dot:particle,ilist:particle ' a type of array holding more than one value

Repeat ' (* MAIN *)

	Cls ' clear display so no streaks are left on screen
	If KeyDown(32) ' hold down SPACE to add a raindrop
	  For Local i = 0 To depth
	  dot=New particle ' we are DEFINING a special type of variable now
	  ListAddFirst list,dot ' add this data to the linked list
	  Next
	EndIf

	SetColor 255, 255, 255 ' white (for our rain count)
	DrawText "Rain: " + list.count(), 0, 0 ' show how many drops are in the air
	For ilist = EachIn list ' EACHIN will work with non-numeric values, and we're looping only as many raindrops as there are
		ilist.Draw()
		If ilist.y&gt;=576 Or ilist.x&gt;=768 Then list.remove ilist ' if out of boundaries, remove from list
	Next ' and YES this does affect the total count, it's not zeroed out, it's actually REMOVED !
	' powerful business linked lists are

	Delay speed ' decrease this number to slow down animation effect
	glFlush ' show our work
	
Until KeyDown(27) ' (* END OF MAIN *)
</textarea><br><br>The main purpose of structuring like this is manageability; you will certainly lose track of stuff like "where did I set the color of the particle again in my code?" otherwise.<br>Hope this helps. <br><br></td></tr></table><br>
<a name="1290722"></a>

<a name="1290723"></a>

<a name="1290724"></a>

<a name="1290725"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hotshot2005</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have added Functions in and Escape key....<br><br>This is more cleaner code in main loop(where repeat and until is!)<br><br><pre class=code>

' Rainbow Rain - an experiment and tutorial in TYPE variables and LINKED LISTS
Strict

Const speed=10 ' change to 1 for maximum speed
Const depth=0 ' change to higher # for more raindrops per cycle

SeedRnd MilliSecs()
SetGraphicsDriver GLMax2DDriver(),0 ' zero forces front buffer
Graphics 768,576

Type particle ' My 1st attempt at using TYPE variables !

  Field x ' x-coordinates
  Field y ' y-coordinates
  Field color:Int ' TYPE variables can hold more than one type AND value !

  Method New()
	  Self.x = Rand(-576, 767) ' x-position of rain
	  Self.y = 0 ' unnecessary it seems as by saying NEW it is already zero
	  Self.color = Rand(0, 5)
  End Method
  
  Method Draw()
  	Select Self.color
  		Case 0
			SetColor 255, 0, 255
		Case 1
			SetColor 255, 0, 0
		Case 2
			SetColor 255, 128, 0
		Case 3
			SetColor 255, 255, 0
		Case 4
			SetColor 0, 255, 0
		Case 5
			SetColor 0, 0, 255
	End Select

	DrawLine Self.x, Self.y, Self.x + 7, Self.y + 14 ' simple streak
	Self.x:+1 ' move rain down by 2 and across by 1
	Self.y:+2
	
  End Method
  
End Type ' this type has 2-integers and one string for it

Global list:TList = CreateList() ' this is called a LINKED LIST
Global dot:particle,ilist:particle ' a type of array holding more than one value

Function RenderWorld()

        Cls ' clear display so no streaks are left on screen
	If KeyDown(32) ' hold down SPACE to add a raindrop
	  For Local i = 0 To depth
	  dot=New particle' we are DEFINING a special type of variable now
	  ListAddFirst list,dot ' add this data to the linked list
	  Next
	EndIf

	SetColor 255, 255, 255 ' white (for our rain count)
	DrawText "Rain: " + list.count(), 0, 0 ' show how many drops are in the air
	For ilist = EachIn list ' EACHIN will work with non-numeric values, and we're looping only as many raindrops as there are
		ilist.Draw()
		If ilist.y&gt;=576 Or ilist.x&gt;=768 Then list.remove ilist ' if out of boundaries, remove from list
	Next ' and YES this does affect the total count, it's not zeroed out, it's actually REMOVED !
	' powerful business linked lists are

	Delay speed ' decrease this number to slow down animation effect
	glFlush ' show our work
	
End Function

Repeat '                    (* MAIN *)
	  RenderWorld()
Until KeyDown(Key_Escape) ' (* END OF MAIN *)

</pre> <br><br></td></tr></table><br>
<a name="1290726"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Some things to note...<br><br>"Self" is not required when referring to fields or methods in the Type.<br><br>I think you are playing with fire choosing to render directly to the front buffer. Each to their own of course, and YMMV. <br><br></td></tr></table><br>
<a name="1290729"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> My take on it.  Added a bit of gravity.  Press Enter to create a gust of wind. :)<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' Rainbow Rain - an experiment and tutorial in TYPE variables and LINKED LISTS
'Original version by dw817, modified by TomToad
SuperStrict

Const speed:Float=100.0 ' initial pixels per second rain will fall, using floats for more precise movement
Const gravity:Float = 20.0 'gravity applied to the drops
Const TerminalVelocity:Float = 1000.0 'Fastest rain will fall
Const Windmin:Float=75.0 ' The speed at which the rain will move sideways
Const WindMax:Float=300.0 'The Maximum speed at which the wind blows
Const WindAcceleration:Float = 700.0 'How fast the wind will gust
Const depth:Int=1 ' change to higher # for more raindrops per cycle
Const DropDelay:Float = 0.0166666666666667 'how often drops are added. Drops will be added at a rate of depth per DropDelay
Const ScreenWidth:Int = 800
Const ScreenHeight:Int = 600
Const ScreenDepth:Int = 0 'Use 32 for fullscreen

SeedRnd MilliSecs()
SetGraphicsDriver GLMax2DDriver() ' using backbuffer to prevent tearing.
Graphics ScreenWidth,ScreenHeight,ScreenDepth 'Enter graphics mode :)

Type particle ' My 1st attempt at using TYPE variables !
	Global List:TList = CreateList() 'List to hold all the drops.  Global makes it acessable to all instances, not just one
	Global WindSpeed:Float = WindMin
	Global WindGust:Float = -WindAcceleration
	
	Field x:Float ' x-coordinates
	Field y:Float ' y-coordinates
	Field Red:Int
	Field Green:Int
	Field Blue:Int ' dividing rgb components for easier handling
	Field CurrentSpeed:Float 'the current speed the drop will fall
	
	Method New() 'This is automatically called when an instance of the type is created.  We will do all our initializations here
		x = Rnd(-ScreenHeight,ScreenWidth) 'Creates a random float value
		y = -14 'start the drops just off the top
		red = Rand(0,1) * 255 'set each component randomly
		Green = Rand(0,1) * 255
		Blue = Rand(0,1) * 255
		If Red = 0 And Green = 0 And Blue = 0 'if the final color is black, we will change it to orange
			Red = 255
			Green = 128
		End If
		CurrentSpeed = Speed 'Set the current speed to the start speed
		List.AddLast(Self) 'add this instance to the list
	End Method
	
	Function Update(Delta:Float) 'This is where we will update the position of the drop, we will use delta to control timing
		For Local Drop:particle = EachIn List
			Drop.x :+ WindSpeed *Delta'drop is blown by wind
			Drop.y :+ Drop.CurrentSpeed * Delta 'drop falls by gravity
			Drop.CurrentSpeed :+ Gravity * Delta'acceleration due to gravity
			If Drop.CurrentSpeed &gt; TerminalVelocity 'drop will not accelerate beyond termonal velocity
				Drop.CurrentSpeed = TerminalVelocity
			End If
			If Drop.y &gt; ScreenHeight Or Drop.x &gt; ScreenWidth Then List.Remove(Drop) 'if drop is offscreen, remove from list
		Next
		WindSpeed :+ Windgust * Delta 'adjust the wind speed
		If WindSpeed &gt; WindMax Then WindSpeed = WindMax 'Clip the wind speed
		If WindSpeed &lt; WindMin Then WindSpeed = WindMin
	End Function
	
	Function Draw() 'this will draw all the drops on the screen
		For Local Drop:particle = EachIn List 'iterate through all the drops
			SetColor Drop.Red,Drop.Green,Drop.Blue 'set the color
			DrawLine Drop.x,Drop.y,Drop.x+14*Drop.WindSpeed/WindMax,Drop.y+14 'draw the streak
		Next
	End Function
	
End Type ' this type has 2-floats and 3 integers

Local OldTime:Int = MilliSecs() 'will be used in calculating delta
Local Time:Int
Local Delta:Float 'we will calculate delta based on a fraction of seconds
Local DelayPeriod:Float = DropDelay 'How much time before next drop cycle.  Set to DropDelay so first cycle will happen right away
Repeat ' (* MAIN *)
	Time = MilliSecs() 'How much time has passed?
	Delta = Float(Time-Oldtime)/1000.0 '1000 milliseconds in a second
	OldTime = Time 'Set Oldtime for the next loop
	

	Cls ' clear display so no streaks are left on screen
	If KeyDown(KEY_SPACE) ' hold down SPACE to add a raindrop
		If DelayPeriod &gt;= DropDelay
			Repeat
				For Local d:Int=1 To depth
			  		New particle ' we are DEFINING a special type of variable now
				Next
				DelayPeriod :- DropDelay
			Until DelayPeriod &lt; DropDelay
		Else
			DelayPeriod :+ Delta
		End If
	End If
	
	If KeyDown(KEY_ENTER) 'is the wind gusting or dying down?
		particle.WindGust = WindAcceleration
	Else
		particle.WindGust = -WindAcceleration
	End If
	
	particle.Update(Delta) 'update the raindrops
	particle.Draw() 'draw the raindrops
	
	SetColor 255,255,255 ' white (for our rain count)
	DrawText "Rain: "+particle.list.count(),0,0 ' show how many drops are in the air
	DrawText "Hold SPACE to add drops: Hold ENTER to create wind gust: ESC to quit",10,ScreenHeight-20
	
	Flip  'show our work, use flip to sync the drawing with the refresh to prevent tearing, use flip 0 to draw as fast as possible
Until KeyDown(KEY_ESCAPE) Or AppTerminate()' (* END OF MAIN *)
</textarea> <br><br></td></tr></table><br>
<a name="1290739"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello Gang. Looks like everyone had a shot at my code. :) That's fine. I'm not familiar with the command METHOD yet. One step at a time. I'll try to look into that later today.<br><br>Brucey, are you saying rendering to the front buffer is dangerous ? Can you explain, please ? <br><br></td></tr></table><br>
<a name="1290797"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >H&amp;K</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> @brucy,<br><br>Self however allows auto complete to kickin without you needing to remember the types methods or fields names. (not in the default ide maybe) <br><br></td></tr></table><br>
<a name="1292794"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hotshot2005</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I been making Shoot em up based on Link lists and I got problem at the moment.<br><br>I will post video of it plus the code too :) <br><br></td></tr></table><br>
<a name="1292974"></a>

<a name="1292975"></a>

<a name="1292976"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >okee</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Can paramaters be passed to a New() method ?<br>local t:Tmytype<br>t = New("stringvalue") TmyType<br><br>testing here and it doesn't seem to like it. <br><br></td></tr></table><br>
<a name="1292977"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> It is not possible.<br><br>For this you create a custom function/method.<br><pre class=code>
Method init:mytype(params)
self.x = paramXY
Return self
End Method

Local s:mytype = new mytype.init(paramsToUse)
</pre><br><br>Bye<br>Ron <br><br></td></tr></table><br>
<a name="1292978"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> No. New() just returns a new instance of a Type.<br><br>You will need to write some kind of constructor method/function. There are several typical ways to do it :<br><pre class=code>

Type TMyType
  Field x:int
  Field y:int

  ' the create method - useful when extending types.
  Method Create:TMyType(x:int, y:int)
    Self.x = x
    Self.y = y
    Return Self
  End Method

  ' the create function
  Function CreateFunc:TMyType(x:int, y:int)
    Local this:TMyType = New TMyType
    this.x = x
    this.y = y
    Return this
  End Function

End Type

' a create function alternative - old style, procedural based.
Function CreateMyType(x:int, y:int)
  ' optionally call one of the above
  Return New TMyType.Create(x, y)

  ' or implement a version of CreateFunc() here...
End Function
</pre> <br><br></td></tr></table><br>
<a name="1293039"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >okee</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah, i did have a constructor written that works nicely but had read in a few places about the New method but hadn't seen an example of it.<br>I really like the methods and functions within a type in Blitzmax<br>makes the code very clear and manageable, after mostly using blitzplus <br><br></td></tr></table><br>
<a name="1293378"></a>

<a name="1293379"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hotshot2005</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> There video of it for people to download it and somethings isn't right thought and here the code of it....<br><br><a href="https://www.dropbox.com/s/ubrnhkrw9nme85e/IMG_1308.MOV?dl=0" target="_blank">https://www.dropbox.com/s/ubrnhkrw9nme85e/IMG_1308.MOV?dl=0</a><br><br><pre class=code>
Graphics 640, 480, 0

Global Background = LoadImage("Data\Back_3.png")
Global Centre_Player = LoadImage("Data\Player_1.png")
Global Left_Player = LoadImage("Data\Player_3.png")
Global Right_Player = LoadImage("Data\Player_2.png")

' Enemys Animations
Global Enemy_Anim_Part_1=LoadImage("Data\Ennemy_3_1.png")


Global Player_X = 250
Global Player_Y = 400
Global Frames = 0

Global Enemys_Animations=0
Global Y
Global X2,Y2
Global SPEED=5

Type Alien
	 Field X, Y, Shoot
	
	  Method New_E()
	         X2 = Rand(20, 630) ' x-position of Enemys
	         y2 = y2 + 1
     End Method

     Method Draw()
  			Select Enemys_Animations
  				Case 0
                     DrawImage Enemy_Anim_Part_1,X2,y2
            End Select
	End Method

End Type

Global list:TList = CreateList()
Global dot:Alien,ilist:Alien ' a type of array holding more than one value

Function RenderWorld()

        Cls ' clear display so no streaks are left on screen
        
        TileImage Background, 0, Y
	    Y=Y+1
	
	    Select Frames
		      Case 1
		           DrawImage Centre_Player, Player_X, Player_Y
		      Case 2
		           DrawImage Left_Player, Player_X, Player_Y
		      Case 3
		           DrawImage Right_Player, Player_X, Player_Y
	    End Select
	
	   ' Control the Player on the Keyboard !!1
	   If KeyDown(KEY_LEFT)
		  Frames = 2
		  Player_X = Player_X - 1
       ElseIf KeyDown(KEY_RIGHT)
	      Frames = 3
	      Player_X = Player_X + 1
	   Else
	      ' IF No Keys press then go back Static ship !
	      Frames = 1
       EndIf
	
	   For Local i = 0 To depth
	       dot=New Alien' we are DEFINING a special type of variable now
	       ListAddFirst list,dot ' add this data to the linked list
	   Next
	
	   For ilist = EachIn list ' EACHIN will work with non-numeric values, and we're looping only as many raindrops as there are
	       ilist.Draw()
	
	       ' IF Aliens reached bottom of the screen
		   If ilist.y&gt;=480 
		       list.remove ilist ' if out of boundaries, remove from list
		   Else
		       ' IF had been Aliens Remove then Create more Aliens !
		       ilist.New_E()
		   EndIf		   
	   Next
	
	   Delay speed ' decrease this number to slow down animation effect

End Function


While Not KeyDown(KEY_ESCAPE)
    RenderWorld()	
	Flip
Wend
</pre> <br><br></td></tr></table><br>
<a name="1293380"></a>

<a name="1293381"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> For ilist = EachIn list<br>-&gt; For local ilist:alien = EachIn list<br>(saves to declare the variable before)<br><br><br>For Local i = 0 To depth<br>-&gt; how large is "depth" (it is not used anywhere else, Strict/Superstrict helps here)<br><br><br>' IF had been Aliens Remove then Create more Aliens !<br>ilist.New_E()<br>-&gt; what should that do? you adjust values of the current item/alien ...<br>-&gt; so for each entry in the list: if the y is &lt; 480, set x to random and increase y<br><br>I would adjust it a bit...<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict 'allows to use "for local i:int" multiple times in a function
Graphics 640, 480, 0

Global Background:TImage = CreateBackground() 'LoadImage("Data\Back_3.png")
Global Centre_Player:TImage = CreateShip(0) 'LoadImage("Data\Player_1.png")
Global Left_Player:TImage = CreateShip(-1) 'LoadImage("Data\Player_3.png")
Global Right_Player:TImage = CreateShip(1) 'LoadImage("Data\Player_2.png")

' Enemys Animations
Global Enemy_Anim_Part_1:TImage=CreateAlien() 'LoadImage("Data\Ennemy_3_1.png")


Global Player_X:int = 250
Global Player_Y:int = 400
Global Frames:int = 0

Global Enemys_Animations:int=0
Global backgroundY:int

Type Alien
	Field X:int, Y:int 'each alien has its OWN position
	Field speed:int=5
	Field Shoot:int
	Global list:TList = CreateList()
	Global maxAliens:int = 50 

	Method New()
		'random position for new aliens
		X = Rand(0, 640 - Enemy_Anim_Part_1.Width) ' x-position of Enemys
		y = - Enemy_Anim_Part_1.Height - Rand(50,70) 'start outside
		speed = Rand(2,4) '2-4
	End Method
	
	Method Move()
		y :+ speed
	End Method

	Method Update()
		Move()
		'Attack()
		'...
	End Method

	Method Draw()
		Select Enemys_Animations
			Case 0
				DrawImage Enemy_Anim_Part_1,X,Y
			Default
				DrawImage Enemy_Anim_Part_1,X,Y
		End Select
	End Method
End Type


Function UpdateWorld()
	'move background
    backgroundY :+ 1

	' Control the Player on the Keyboard !!1
	If KeyDown(KEY_LEFT)
		Frames = 2
		Player_X = Max(0, Player_X - 1) 'limit to x&gt;=0
	ElseIf KeyDown(KEY_RIGHT)
		Frames = 3
		Player_X = Min(640, Player_X + 1) 'limit to x&gt;=0
	Else
		' IF No Keys press then go back Static ship !
		Frames = 1
	EndIf

	
	'add as many aliens as missing
	For Local i = 0 Until alien.maxAliens - alien.list.Count()
		alien.list.AddFirst( new Alien )
	Next


	local remove:Alien[] 'array holding "to remove entries"
	For local entry:Alien = EachIn Alien.list
		entry.Update()

		' if out of boundaries, remove from list
		If entry.y &gt;= 480 
			remove :+ [entry] 'add to removal
		Endif
	Next

	'using this "remove outside of iterate-over-list" method avoids
	'concurrent modification of the list (else: might run into bugs)
	For local entry:Alien = Eachin remove
		Alien.list.Remove(entry)
	Next
End Function


Function RenderWorld()

	Cls ' clear display so no streaks are left on screen
        
    TileImage Background, 0, backgroundY
	
	For local entry:Alien = EachIn Alien.list
		entry.Draw()
	Next


	Select Frames
		  Case 1
			   DrawImage Centre_Player, Player_X, Player_Y
		  Case 2
			   DrawImage Left_Player, Player_X, Player_Y
		  Case 3
			   DrawImage Right_Player, Player_X, Player_Y
	End Select
End Function


While Not KeyDown(KEY_ESCAPE)
	UpdateWorld()
    RenderWorld()
	Delay 5 ' decrease this number to slow down animation effect
	Flip
Wend



Function CreateBackground:TImage()
	local img:TImage = CreateImage(100,100, 1)
	Cls
	SetColor 255,150,150
	DrawRect(0,0,50,50)
	DrawRect(50,50,50,50)
	SetColor 170,150,150
	DrawRect(50,0,50,50)
	DrawRect(0,50,50,50)
	SetColor 255,255,255
	GrabImage(img, 0,0)
	return img
End Function

Function CreateAlien:TImage()
	local img:TImage = CreateImage(50,50, 1)
	Cls
	SetColor 50,50,100
	DrawOval(3,0, 44,50)
	SetColor 100,100,200
	DrawOval(4,1,42,48)
	SetColor 5,5,5
	DrawOval(11,10,10,12)
	DrawOval(29,10,10,12)
	DrawOval(21,25,3,7)
	DrawOval(26,25,3,7)
	SetColor 255,255,255
	GrabImage(img, 0,0)
	return img
End Function

Function CreateShip:TImage(direction:int=0)
	local img:TImage = CreateImage(50,50, 1)
	Cls
	SetColor 250,250,45
	DrawOval(7,35,5,15)
	DrawOval(38,35,5,15)
	SetColor 250,100,25
	DrawOval(8,35,3,13)
	DrawOval(39,35,3,13)
	SetColor 120,120,45
	DrawOval(4,25,11,20)
	DrawOval(35,25,11,20)
	SetColor 150,150,115
	DrawOval(11 + direction*2,0, 28,50)
	GrabImage(img, 0,0)
	return img
End Function
</textarea><br><br>what did I change:<br>- splitted UpdateWorld() and RenderWorld()<br>- made it strict<br>- created some dummy gfx for others without media<br>- prepared some "oop" (Update() for each Alien)<br>- redone some logic (refill with new aliens if others died)<br><br>I left for you to create a class for the player, containing its coordinates, frame, speed ...<br><br>What did you do wrong: each of your units used the same "X2,Y2" and each time a unit was y &lt; 480, you set new values, which made them "jump around".<br><br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="1293391"></a>

<a name="1293396"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hotshot2005</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> thank you for helping and also good for explain on what I did wrong...<br><br>I am going to study them and see what I have learn :)<br><br>Now I going Enemys Animations, Bullets and collisions next on my own and see how goes :-) <br><br></td></tr></table><br>
<a name="1293397"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> So instead of removing the aliens when Y &gt; 480, you should set them to "dead" (or alive=false).<br><br>Then you remove all enemies wo are no longer alive (that "remove :+ [alien]"-portion).<br><br>This allows you to "kill" the aliens for various reasons: collision, out of screen, timer, ...<br><br>Instead of "alive = false" you could create multiple attributes: "dying" (eg. while still displaying an "dying" animation...).<br><br><br>Bullets should be custom types too (TBullet) so you can easily hold a list/array of them for each unit which is able to shoot (player, aliens?).<br><br><br>Good luck<br><br>bye<br>ron <br><br></td></tr></table><br>
<a name="1293402"></a>

<a name="1293403"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hotshot2005</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>So instead of removing the aliens when Y &gt; 480, you should set them to "dead" (or alive=false).<br><br>Then you remove all enemies wo are no longer alive (that "remove :+ [alien]"-portion).<br><br>This allows you to "kill" the aliens for various reasons: collision, out of screen, timer, ...<br><br>Instead of "alive = false" you could create multiple attributes: "dying" (eg. while still displaying an "dying" animation...).<br><br><br>Bullets should be custom types too (TBullet) so you can easily hold a list/array of them for each unit which is able to shoot (player, aliens?).<br> <br></div><br><br>I will take note of that but I going take step by step like sorting the Animations then take it from there.<br><br><br>I got the Animations working but I am worry about slowing the game down as I try slow the animations down<br><br>here the code on what I did<br><br><pre class=code>
	 Method Update()
	       Enemys_Animations=Enemys_Animations+1
	       If Enemys_Animations&gt;=6
	          Enemys_Animations=0
	          Delay 25
	       EndIf 
	
	 	   Move()
		'Attack()
		'...
	 End Method

	 Method Draw()
		   Select Enemys_Animations
			     Case 0
				       DrawImage Enemy_Anim_Part_1,X,Y
				 Case 1
				        DrawImage Enemy_Anim_Part_2,X,Y
				 Case 2
				        DrawImage Enemy_Anim_Part_3,X,Y
				 Case 3
				        DrawImage Enemy_Anim_Part_4,X,Y
                 Case 4
				        DrawImage Enemy_Anim_Part_5,X,Y
				 Case 5
				        DrawImage Enemy_Anim_Part_6,X,Y
		   End Select
	End Method
</pre> <br><br></td></tr></table><br>
<a name="1293407"></a>

<a name="1293411"></a>

<a name="1293412"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes... Delay is absolutely _wrong_ in that case.<br><br>So how could we increase the time between animations?<br><br>a) we could store a timer for each alien ... once it reaches 0, the next animation is set and the timer is reset to the "timer duration".<br>Timer-approach allows to run independently of FPS (speed of your computer)<br><br><pre class=code>
Field lastUpdate:int
Field animTimer:int
Field animTime:int = 100 'every 100 millisecs?
Field animation:int = 0
...

Method Update()
  UpdateAnimation()
  'other stuff we did in Update()
  'Move()
  'Attack()

  lastUpdate = Millisecs() 'not error prone, might be negative after 28days uptime
End Method

Method UpdateAnimation()
  'subtract time gone since last call to this instances Update()
  animTimer :- (Millisecs - lastUpdate)

  if animTimer &lt; 0
    animTimer = animTime
    animation :+ 1
    if animation &gt; 5 then animation = 0
  endif
End Method

</pre><br><br><br>b) Instead of storing an timer for each unit, you could calculate the time since the last "UpdateWorld()" call (similar to a), but this time only store that time once). Now you have some kind of "time since last update call" - which allows for a similar approach to a), but this time all units get the same "time since last update" instead of an individual one<br><br><br>BTW: instead of delay we now have the information of how many milliseconds have passed since the last update. Now imagine you do no longer say "move +1 each Update", but you declare: my units should move 10 pixels <b>per second</b>.<br><br>Now you just have to do "y :+ speed * lastUpdate/1000.0".<br>Speed is eg. "10". If last Update was 1 second ago, then the values would be: "y :+ 10 * 1000.0/1000.0" which means, add 10. If only 100 ms passed, then 1.0 would get added. And so on and so forth.<br><br>BUT ... to enable this kind of of movement, your "Y" must be of type "float" as "int" values only allow discrete steps. (y :+ 0.1 will stay "y" as it is rounded down automatically). Once you set them to "float", it should work.<br><br>Finally you should be able to remove that "delay 5" from your main loop.<br>If you want to slow down alien-movement you either decrease their individual speeds - or add a general "global speedMod:Float = 1.0" to your alien-type. Then extend "Y :+ speed * lastUpdate"  to something like "Y :+ speed * speedMod * lastUpdate".<br><br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="1294406"></a>

<a name="1294407"></a>

<a name="1294408"></a>

<a name="1294412"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hotshot2005</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> I don't think I have good job on Bullets link lists and I just don't know why Bullets isn't working!<br><br>I hope I havnt made more confusing!<br><br><pre class=code>
Strict 

Graphics 640, 480, 0

Global Background:TImage = LoadImage("Data\Back_3.png")
Global Centre_Player:TImage = LoadImage("Data\Player_1.png")
Global Left_Player:TImage = LoadImage("Data\Player_3.png")
Global Right_Player:TImage = LoadImage("Data\Player_2.png")

' Enemys Animations
Global Enemy_Anim_Part_1:TImage=LoadImage("Data\Ennemy_3_1.png")
Global Enemy_Anim_Part_2:TImage=LoadImage("Data\Ennemy_3_2.png")
Global Enemy_Anim_Part_3:TImage=LoadImage("Data\Ennemy_3_3.png")
Global Enemy_Anim_Part_4:TImage=LoadImage("Data\Ennemy_3_4.png")
Global Enemy_Anim_Part_5:TImage=LoadImage("Data\Ennemy_3_5.png")
Global Enemy_Anim_Part_6:TImage=LoadImage("Data\Ennemy_3_6.png")

Global Player_Bullet_Front:TImage=LoadImage("Data\Bullet_1.Png")

Global Player_X:Int = 250
Global Player_Y:Int = 400
Global Frames:Int = 0

Global animations:Int=0
Global BackgroundY:Int
Global speed:Int=5

Global animTimer:Int

Type Positions
     Field X:Int, Y:Int 'each alien or Tbullets has its OWN position
     Field Shoot:Int
End Type


Type Alien Extends Positions

     ' Alien Animations
     Field lastUpdate:Int
     Field animTimer:Int
     Field animTime:Int = 100 'every 100 millisecs?
     Field animations:Int = 0
	 
     Global list:TList = CreateList()
     Global maxAliens:Int = 10
	
     Method New()
            'random position for new aliens
            X = Rand(0, 640 - Enemy_Anim_Part_1.Width) ' x-position of Enemys
	    y = - Enemy_Anim_Part_1.Height - Rand(50,70) 'start outside
		    speed = Rand(2,4) '2-4 
     End Method
	
     Method Move()
            y :+ speed
     End Method

     Method Update()
       	    UpdateAnimation()
	    Move()
	    'Attack()
	    '...
	    lastUpdate = MilliSecs() 
     End Method
	
     Method UpdateAnimation()
            'subtract time gone since last call to this instances Update()
            animTimer :- (MilliSecs() - lastUpdate)

            If animTimer &lt; 0
               animTimer = animTime
               animations :+ 1
               If animations &gt; 5 Then animations = 0
            EndIf
     End Method

     Method Draw()
	    Select animations
	           Case 0
		        DrawImage Enemy_Anim_Part_1,X,Y
		   Case 1
		        DrawImage Enemy_Anim_Part_2,X,Y
		   Case 2
		        DrawImage Enemy_Anim_Part_3,X,Y
		   Case 3
		        DrawImage Enemy_Anim_Part_4,X,Y
                   Case 4
		        DrawImage Enemy_Anim_Part_5,X,Y
		   Case 5
		        DrawImage Enemy_Anim_Part_6,X,Y
	   End Select
     End Method
	
End Type


Global TBullets:TList=New TList

Type TBullet Extends Positions
     Global Bullets:TList = CreateList()
     
     ' Position of Player Bullets
     Method New()
            ' position for new Bullets
 	    X =  Player_X ' x-position of Player
     End Method
	
     Method BMove()
 	    y :- speed
     End Method

     Method BUpdate()
	       	
	    BMove()
	    BDraw()
	
	    'Attack()
     End Method

     Method BDraw()

            ' Users to press fire the Bullets!
	    If KeyDown(Key_Space)
	       BMOVE()		 
	    EndIf 
	
	    DrawImage Player_Bullet_Front,Player_X,Y	
     End Method
End Type

Function UpdateWorld()
             
         ' move background
         backgroundY :+ 1

        ' Control the Player on the Keyboard !!1
	If KeyDown(KEY_LEFT)
	   Frames = 2
	   Player_X = Player_X - 1
        ElseIf KeyDown(KEY_RIGHT)
	       Frames = 3
	       Player_X = Player_X + 1
	Else
	    ' IF No Keys press then go back Static ship !
	       Frames = 1
        EndIf


        'add as many aliens as missing
	For Local i = 0 Until alien.maxAliens - alien.list.Count()
	    alien.list.AddFirst( New Alien )
	Next

	Local remove:Alien[] 'array holding "to remove entries"
	
	For Local entry:Alien = EachIn Alien.list
            entry.Update()

	    ' if out of boundaries, remove from list
	    If entry.y &gt;= 480 
	       remove :+ [entry] 'add to removal
	    EndIf
	Next

	' using this "remove outside of iterate-over-list" method avoids
	'concurrent modification of the list (else: might run into bugs)
	
        For Local entry:Alien = EachIn remove
	    Alien.list.Remove(entry)
	Next
	
End Function

Function RenderWorld()

         Cls ' clear display so no streaks are left on screen
        
         TileImage Background, 0, BackgroundY

         For Local entry:Alien = EachIn Alien.list
	     entry.Draw()
	 Next
	
	 Select Frames
		   
                Case 1
	             DrawImage Centre_Player, Player_X, Player_Y
		Case 2
		     DrawImage Left_Player, Player_X, Player_Y
		Case 3
		     DrawImage Right_Player, Player_X, Player_Y
	    
         End Select
	
End Function


While Not KeyDown(KEY_ESCAPE)
      UpdateWorld()
      RenderWorld()	
      Flip
Wend
</pre> <br><br></td></tr></table><br>
<a name="1294478"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> Where in the code do you fire a bullet? whre Do you add a bullet to the list BULLETS and where do you call TBUllet.Draw()? <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
