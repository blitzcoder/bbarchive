<!DOCTYPE html><html lang="en" ><head ><title >Tiled - Importar mapa</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Tiled - Importar mapa</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=112" >BlitzMax Tutorials</a>/<a href="#bottom" >Tiled - Importar mapa</a><br><br>
<a name="1289464"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Júlio Caesar</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Posto um código simples para leituras de TileMaps feitos usando o programa Tiled (FREE).<br><br>Para que funcione, exporte o mapa para o seguinte formato: Arquivos de mapa do Flare (*.txt).<br><br>A imagem usada como Tileset no mapa deve estar no mesmo diretório do arquivo de texto contendo os dados do mapa, e uma observação muito importante: o mapa só deve ter um arquivo de imagem por mapa, ou seja, apenas um mapa de tiles.<br><br>Para a camada de colisão, crie uma layer do tipo CAMADA DE OBJETOS com o nome collision. Dentro dessa camada, podem ser criados objetos do tipo retângulo ou elípse (no caso deve ser um círculo perfeito). Se o objeto for do tipo retângulo, no campo Tipo do objeto, bote o nome de 'rect', se for elipse, ponha 'oval'.<br><br>Segue código abaixo:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Type TTiledMap
	'[header]
	Field fileUrl:String
	Field file:TStream
	Field width:Int
	Field height:Int
	Field tileWidth:Int
	Field tileHeight:Int
	Field orientation:String
	
	'[tilesets]
	Field tilesetUrl:String
	Field tileset:TImage
	
	'[layer]
	Field layer:TTiledLayer[]
End Type

Type TTiledLayer
	Field id:String
	Field data:Int[]
End Type

Function TiledMap_New:TTiledMap()
	Return New TTiledMap
End Function

Function TiledMap_Delete:Int(instance:TTiledMap Var)
	instance.file = Null
	instance.fileUrl = Null
	Release(instance.width)
	instance.width = Null
	Release(instance.height)
	instance.height = Null
	Release(instance.tileWidth)
	instance.tileWidth = Null
	Release(instance.tileHeight)
	instance.tileHeight = Null
	instance.orientation = Null
	instance.tilesetUrl = Null
	instance.tileset = Null
	instance.layer = Null
	instance = Null
	
	Return Null
End Function

Function TiledMap_LoadFile(instance:TTiledMap, fileUrl:String)
	instance.fileUrl = fileUrl
	instance.file = ReadStream(fileUrl)
End Function

Function TiledMap_ReadData(instance:TTiledMap)
	'[header]
	instance.file.ReadLine()
	instance.width = instance.file.ReadLine().Split("=")[1].ToInt()
	instance.height = instance.file.ReadLine().Split("=")[1].ToInt()
	instance.tileWidth = instance.file.ReadLine().Split("=")[1].ToInt()
	instance.tileHeight = instance.file.ReadLine().split("=")[1].ToInt()
	instance.orientation = instance.file.ReadLine().Split("=")[1]
	instance.file.ReadLine()
	instance.file.ReadLine()
	'[tilesets]
	instance.tilesetUrl = instance.file.ReadLine().Split("=")[1].Split(",")[0]
	Local imageTemp:TImage = LoadImage(ExtractDir(instance.fileUrl) + "/" + instance.tilesetUrl)
	Local cellsPerCollumn:Int = ImageWidth(imageTemp) / instance.tileWidth
	Local cellsPerLine:Int = ImageHeight(imageTemp) / instance.tileHeight
	Local cellCount:Int = cellsPerCollumn * cellsPerLine
	instance.tileset = LoadAnimImage(ExtractDir(instance.fileUrl) + "/" + instance.tilesetUrl, instance.tileWidth, instance.tileHeight, 0, cellCount)
	imageTemp = Null
	Release(cellsPerCollumn)
	cellsPerCollumn = Null
	Release(cellsPerLine)
	cellsPerLine = Null
	Release(cellCount)
	cellCount = Null
	'[layer]
	Local dataString:String
	Local dataStringArray:String[]
	Local layerId:Int = 0
	While Not (instance.file.Eof())
		If (instance.file.ReadLine().Contains("[layer]"))
			instance.layer:+[New TTiledLayer]
			instance.layer[layerId].id = instance.file.ReadLine().Split("=")[1]
			instance.file.ReadLine()
			For Local i:Int = 0 Until instance.height
				dataString:+instance.file.ReadLine()
			Next
			dataStringArray = dataString.Split(",")
			For Local i:Int = 0 Until Len(dataStringArray)
				instance.layer[layerId].data:+[dataStringArray[i].ToInt() - 1]
			Next
			dataString = ""
			dataStringArray = Null
			layerId:+1
		End If
	Wend
	instance.file.Close()	
End Function

Function TiledMap_DrawLayer(instance:TTiledMap, layerId:Int)
	SetColor(255, 255, 255)
	For Local y:Int = 0 Until instance.height
		For Local x:Int = 0 Until instance.width
			Local offset:Int = y * instance.width + x
			If (instance.layer[layerId].data[offset] &lt;&gt; - 1)
				DrawImage(instance.tileset, 0 + x * instance.tileWidth, 0 + y * instance.tileHeight, instance.layer[layerId].data[offset])
			End If
		Next
	Next
End Function

'COLLISION LAYER [extra]
Type TTiledLayerCollision
	Global List:TList = CreateList()
	
	Field id:String
	Field typeId:String
	Field location:Int[4]
End Type

Function TiledMap_ReadCollisionLayer(instance:TTiledMap)
	instance.file = ReadStream(instance.fileUrl)
	While Not (instance.file.Eof())
		If (instance.file.ReadLine().Contains("[collision]"))
			Local layer:TTiledLayerCollision = New TTiledLayerCollision
			layer.id = instance.file.ReadLine().Split("")[1]
			layer.typeId = instance.file.ReadLine().Split("=")[1]
			Local locationString:String = instance.file.ReadLine().Split("=")[1]
			Local locationStringArray:String[] = locationString.Split(",")
			layer.location[0] = locationStringArray[0].ToInt() * instance.tileWidth
			layer.location[1] = locationStringArray[1].ToInt() * instance.tileHeight
			layer.location[2] = locationStringArray[2].ToInt() * instance.tileWidth
			layer.location[3] = locationStringArray[3].ToInt() * instance.tileHeight
			TTiledLayerCollision.List.AddLast(layer)
			layer = Null
		End If
	Wend
	instance.file.Close()
	instance.file = Null
End Function

Graphics(1024, 768, 0, 60, GRAPHICS_BACKBUFFER)
SetVirtualResolution(640, 480)
SetBlend(ALPHABLEND)

Local map:TTiledMap = TiledMap_New()
TiledMap_LoadFile(map, "C:\Users\Julio\Desktop\RPGTest.txt")
TiledMap_ReadData(map)

'Layer collision
TiledMap_ReadCollisionLayer(map)

While Not (KeyHit(KEY_ESCAPE) Or AppTerminate())
	Cls()
	
	SetAlpha(1.0)
	SetColor(255, 255, 255)
	TiledMap_DrawLayer(map, 0)
	TiledMap_DrawLayer(map, 1)
	TiledMap_DrawLayer(map, 2)
	
	'Layer collision
	SetAlpha(0.5)
	SetColor(255, 0, 0)
	For Local collision:TTiledLayerCollision = EachIn TTiledLayerCollision.List
		If (collision.typeId = "oval")
			DrawOval(collision.location[0], collision.location[1], collision.location[2], collision.location[3])
		Else
			DrawRect(collision.location[0], collision.location[1], collision.location[2], collision.location[3])
		End If
	Next
	
	SetAlpha(1.0)
	SetColor(255, 255, 255)
	DrawText(_max2dDriver.ToString(), 0, 0)
	
	Flip()
Wend

TiledMap_Delete(map)

End
</textarea><br><br>E abaixo imagens de demonstração:<br><br><img src="https://scontent-atl3-1.xx.fbcdn.net/hphotos-xlt1/t31.0-8/12496441_971278036285558_936485430625601080_o.jpg"><br><img src="https://scontent-atl3-1.xx.fbcdn.net/hphotos-xaf1/t31.0-8/12489372_971278116285550_8367077305048626076_o.jpg"><br><br>Lembrando que é um teste simples apenas para aprendizado próprio. Estou compartilhando por achar interessante e pode ajudar alguém a ter uma base de como funciona a importação de mapas utilizando o software Tiled, que na minha opinião, é o melhor editor de mapa de tiles do mercado. <br><br></td></tr></table><br>
<a name="1290451"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Estou lendo o que você está dizendo aqui, mas o que é a sua pergunta ? Sua fonte de programa não é executado como é. Por favor, faça um download binário que inclui todos os meios necessários para que ele possa executar. <br><br></td></tr></table><br>
<a name="1290494"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Makis</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Any chance of an English tranlation? <br><br></td></tr></table><br>
<a name="1293105"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Makis. Yeah, I didn't know if languages outside English were permitted in BlitzMAX. Basically I'm asking him to upload a binary file that includes all media as his program, as is with unincluded files, does not run. <br><br></td></tr></table><br>
<a name="1295240"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Júlio Caesar</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello everyone. I have no doubts. Just posted the code to use maps made by Tiled program on BlitzMax. The images in the post are just to show the result. <br><br></td></tr></table><br>
<a name="1295241"></a>

<a name="1295242"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Júlio Caesar</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Recalling that the code is functional. It is free to anyone who wants to continue its implementation. I apologize for the delay to respond, because I was without internet.<br><br>Obs: To work just change the images and map files for you even do. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
