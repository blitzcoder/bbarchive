<!DOCTYPE html><html lang="en" ><head ><title >Smooth scrolling using SetOrigin()</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Smooth scrolling using SetOrigin()</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=112" >BlitzMax Tutorials</a>/<a href="#bottom" >Smooth scrolling using SetOrigin()</a><br><br>
<a name="1042909"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> <b>How to smooth scroll using SetOrigin()</b><br><br>	You want to create a huge map fro your game, much bigger than will fit on your screen at one.  The real question is, how do I scroll the game map smoothly as I move around?  The old-school way would be to calculate where all the graphics go relative to the game world, figure out where the screen is relative to the game world, then translate the graphics coordinates to the screen coordinates.    SetOrigin() does a lot of the work for you.  By telling Max2D where the screen is, you only need to draw the graphics at their proper world coordinates, no longer needing to transform into screen coordinates. <br>	For our example, we are going to create a simple driving game.  The map will be created with the simplest of editors, MSPaint.  To draw the map, create an image 320x320 pixels, with green (0,255,0) for the background.  We are going to use grey (128,128,128) for the road and white (255,255,255) for the sides of the road.  I find the easiest way to draw the map is to draw the sides of the road in white first, then floodfill the road with grey.  We are going to set a single red (255,0,0) pixel to represent the starting point of our car, and a sort of peachy pink (255,255,128) relative to the car to show the angle it will start.  go ahead and create your own map, or just use the map supplied below.<br><img src="http://a.imageshack.us/img214/9620/level3.png"><br>	We also need a car graphic.  When using angles in a program, I like to draw everything facing right.  This makes the math a little easier in the game.  Go ahead and create a .png at 32x32 pixels, or use the image below.<br><img src="http://a.imageshack.us/img821/4189/caruq.png"><br>	Now the first thing we need to do is to create a type for the track.  We will keep the track data in an array and copy the information from the pixmap to the array.  Accessing the array is much quicker than reading each pixel in the pixmap, and speed is needed when rendering the game.<br><pre class=code>Type Track
	Global Map:Int[320,320] 'Our Track

End Type
</pre><br>	You will notice that the map is created as a Global within the Type.  If there is only going to be one instance of a type within a program, I prefer to use Functions and Globals.  This will prevent the need to make an instance so everything can be accessed through Track.Map[x,y] instead of having to go through the trouble of doing<br><br>Global MyTrack:Track = New Track<br>MyTrack.Map[x,y]<br><br>It really comes down to preference.<br><br>	We will also need to create a type for the car<br><pre class=code>Type Car
	Global X:Float 'X position of car
	Global Y:Float 'Y position of car
	Global Angle:Float 'The angle of the car -0 degrees points right
	Global Image:TImage 'Our car image
	Global Speed:Float ' the current speed of the car

End Type
</pre><br>	Car contains information for position, angle, speed, and the TImage of the car.<br><br>	Now lets load everything into the array.  We will create a Load() function within the Track type.<br><pre class=code>	Function Load(Filename:String)
		Local AngleX:Float 'This will be used to calculate the starting angle of the car
		Local AngleY:Float
		Local Pixmap:TPixmap = LoadPixmap(Filename) 'Load in our map image
		If Not Pixmap Then Error("Couldn't open "+Filename) 'run custom error handler
		For Local y:Int = 0 To 319 'Go through each pixel of the map
			For Local x:Int = 0 To 319
				Map[x,y] = ReadPixel(Pixmap,x,y) &amp; $FFFFFF 'Copy the pixel color into the map
				If Map[x,y] = $FFFF80 'Is the pixel our angle indicator?
					Map[x,y] = $808080 'Change the color to grey
					AngleX = x * 32 'Store the Angle Indicator's x and y value
					AngleY = y * 32
				End If
				If Map[x,y] = $FF0000 'Is the pixel our car starting coordinates?
					Map[x,y] = $808080 'change color to grey
					Car.X = x * 32 'store the coordniates in the car type
					Car.Y = Y * 32
				End If
			Next
		Next
		Car.Angle = ATan2(AngleY-Car.Y,AngleX-Car.X) 'Calculate the angle relative to the car
		Car.Image = LoadImage("Car.png") 'load the car's image while we're here.
	End Function
</pre><br>	The function above should be self explanitory.  The Alpha is stripped from each pixel in the pixmap, and then copied into the Map array.  It will also do a check for the car's starting location, and the "peachy pink" pixel pointing at the angle we wish the car to go.  We also load the car's image while we are here.<br>	Notice that if the pixmap doesn't load, we go to our own error handler.  The handler simply ends any graphics mode we may be in and prints the error message.<br><pre class=code>Function Error(S:String)
	EndGraphics()
	RuntimeError(S)
	End
End Function
</pre><br>	Now to get the program going.  First, let's do a simple file requestor so we can pick the level we want to play.  Then we set up the graphics mode and the we will call the Load function.<br><pre class=code>Local Filename:String = RequestFile("Map..")'Get the file to load
If Not filename Then End 'If the user presses Cancel, we will end the program
Graphics 800,600,32 'Set the graphics mode
HideMouse 'Hide the mouse
AutoMidHandle True 'Midhandle any graphics we load
Track.Load(Filename) 'Now load everything in
</pre><br>	Next we will set up some variables that our program will need to render the screen properly<br><pre class=code>Local XO:Float 'Origin of the screen
Local YO:Float
Local XD:Int 'The Leftmost tile to draw
Local YD:Int 'The topmost tile to draw
Local Time:Int 'Using simple delta timing
Local Oldtime:Int = MilliSecs()
Local Delta:Float
</pre><br>	XO and YO is where we will hold the screen's upper left corner position relative to the track.  Note that the SetOrigin() function sets the graphics relative to the screen, which means that we need to negate XO and YO before passing it to the function.<br>	When we draw the tiles for the track,  There is no need to draw any of the tiles which are off the screen.  So we will use XD and YD to represent the leftmost and topmost tile to be drawn<br>	Time, Oldtime, and Delta are being used to implement a simple delta timing system.<br><br>	Next we will set up some constants to use in the game.<br><pre class=code>Const TerminalRoad:Float = 10.0 'The fastest you can go on the road
Const TerminalWalk:Float = 8.0 'The fastest you can drive along the side of the road
Const TerminalGrass:Float = 2.0 'The fastest you can drive on the grass
Const Acceleration:Float = 10.0 'Acceleration speed in pixels per second^2
Const Turn:Float = 180.0 'Speed at which you turn in degrees per second

Local Terminal:Float = TerminalRoad 'initialize the terminal speed to the road
</pre><br>	The constants TerminalXXXX represent the fastest you can drive on the various terrain.  The speed is represented in pixels per second.  Acceleration determines how quickly we can speed up.  Turn is how quickly we can turn the car.  Last we declare and initialize Terminal, which is the fastest speed the car can reach.<br>	Now we can begin the game loop.  Nothing complicated, just a while/wend which we can exit with pressing the escape key.<br><pre class=code>While Not KeyHit(KEY_ESCAPE)
Wend
</pre><br>	The first thing we will do in our Game loop is draw the graphics.  This is done by first calculating the offset our screen will have within the game.<br><pre class=code>	Cls
	XO = Car.X - 400 'We will try and keep the car in the middle of the screen
	YO = Car.Y - 400
	If XO &lt; 0 Then XO = 0 'If the car is near the edge, we stop scrolling
	If YO &lt; 0 Then YO = 0
	If XO &gt; 320 * 32 - 800 Then XO = 320 * 32 - 800
	If YO &gt; 320 * 32 - 600 Then YO = 320 * 32 - 600
	SetOrigin -XO,-YO 'Set the origin of the graphics
</pre><br>	We try to keep the car in the middle of the screen by moving the screen relative to the car.  If the car gets near the edge of the track, instead of drawing the blackness outside of the track, we simply stop moving the screen.  Remember that we are calculating the screen position relative to our graphics, but since SetOrigin actually sets the graphics relative to the screen, we need to negate the origin before passing it to SetOrigin.<br>	Now that we know where the screen is, we need to calculate which tiles need to be drawn<br><pre class=code>	XD = XO / 32 'calculating which tiles to draw is easy.
	YD = YO / 32 'Just divide the screen's position by the tile's dimentions
</pre><br>	Well, that was simple, wasn't it?  Actually there is one more step we need to do, find the lower right tile to be drawn.  We can do that in the track's draw function.  Saves us from needing to pass two extra parameters.  Speaking of the track's draw() function, we need to define that now.  The next snippet of code needs to go into the Track type.<br><pre class=code>	Function Draw(XD:Int, YD:Int)
		Local XE:Int = XD + 26 'The Rightmost tile to be drawn
		If XE &gt; 319 Then XE = 319 'Bouning check
		Local YE:Int = YD + 20 'The bottommost tile to be drawn
		If YE &gt; 319 Then YE = 319
		
		For Local Y:Int = YD To YE
			For Local X:Int = XD To XE
				SetColorRGB(Map[X,y]) 'In our simple example, the tiles are just colored squares
				DrawRect X*32-16,Y*32-16,32,32
			Next
		Next
		SetColor 255,255,255 'Don't forget to set the color back to white :)
	End Function
	
	Function SetColorRGB(Color:Int) 'Extract each color component for the SetColor function
		Local Red:Int = (Color &amp; $FF0000) Shr 16
		Local Green:Int = (Color &amp; $FF00) Shr 8
		Local Blue:Int = Color &amp; $FF
		SetColor Red,Green,Blue
	End Function
</pre><br><br>	Since our screen is 25 tiles wide and 19 tiles high, we just need to add that many tiles to the left-top one.  We need to actually add an extra tile, because as we scroll, a 26th tile to the right and a 20th tile to the bottom will be visible.  Also, if we are all the way to the right of the track or all the way to the bottom, there is a possibility for an array out-of-bounds error due to drawing the extra tile, so we need to check for the possibility.<br>	After we find the tiles to be drawn, we loop through them and draw them in the proper place.  We only need to calculate the x,y pixel values relative to the track.  No need to transform them to the screen since SetOrigin() has done that for us.<br>	We have defined a SetColorRGB() function.  Since the color components are packed in a single integer, we need to extract them to pass to the SetColor command.<br><br>	Now that we have finished drawing the screen, we need to draw the car.  We can create this function in the Car type.<br><pre class=code>	Function draw()
		SetRotation Angle 'Set the car's angle
		DrawImage Image,x,y 'draw it
		SetRotation 0 'return the angle to 0
	End Function
</pre><br>	Not much to it.  Once again, no need to transform the coordinates to the screen since SetOrigin has done the work for us.<br><br>	Now that we have defined the draw functions, let's call them from the main game loop<br><pre class=code>	Track.Draw(XD,YD)
	Car.Draw()
</pre><br>	Simple as that.  Just add a Flip, and our graphics are drawn.<br>	Before we do Flip, let's add a spedometer to the screen<br><pre class=code>	SetOrigin 0,0 'return the origin to 0,0 so the spedometer will be drawn in the correct position
	SetColor 0,0,0 'The spedometer background will be black
	DrawOval 350,550,100,100 'draw a circle at the bottom of the screen
	SetColor 255,255,255 'The needle will be white
	Local SpedometerAngle:Float = (Car.Speed*180.0)/TerminalRoad - 180 'Calculate the needle angle based on speed
	DrawLine 400,600,Cos(SpedometerAngle)*50+400,Sin(SpedometerAngle)*50+600 'draw the needle
	Flip 'flip the graphics
</pre><br>	First we return the screen's origin to 0,0.  Then we draw a half circle background.  Since the spedometer is at the bottom of the screen, we can just draw an entire circle there.  Next we need to calculate the angle of the needle.  The needle will go through a 180 degree range with 0 being left and 180 being right, so the formula (Car.Speed*180.0)/TerminalRoad - 180.  The needle will be all the way to the right at the fastest possible speed(TerminalRoad) and all the way left at a full stop.<br>	After drawing the needle, we flip the backbuffer.<br><br>	Next we need to actually move the car.   First we calculate the delta time.  Delta will hold the number of seconds passed since the last loop (usually will contain a fraction of a second)<br><pre class=code>	Time = MilliSecs()
	Delta = (Time - Oldtime) / 1000.0
	OldTime = Time
</pre><br>	All values will be multiplied by delta<br>	Next, we need to check if the up-arrow key is pressed and accelerate the car if it is, or decelrate smoothly if not.  If the down arrow key is pressed, we need to brake quickly.<br><pre class=code>	If KeyDown(KEY_UP)
		Car.Speed :+ Acceleration*Delta 'Accelerate the car
	Else If KeyDown(KEY_DOWN)
		Car.Speed :- Acceleration*Delta*2 'brake the car at double the acceleration rate
	Else
		Car.Speed :- Acceleration*Delta 'if the accelrator is not pressed, decelrate smoothly
	End If
	If Car.Speed &lt; 0 Then Car.Speed = 0 'Car at complete stop
	If Car.Speed &gt; Terminal Then Car.Speed = Terminal 'Car at fastest speed
</pre><br>	Next we need to check if the left or right arrow keys are pressed and turn the car<br><pre class=code>	If KeyDown(KEY_LEFT)
		Car.Angle :- Turn*Delta
		If Car.Angle &lt; -180.0 Then Car.Angle :+ 360.0
	End If
	If KeyDown(KEY_RIGHT)
		Car.Angle :+ Turn*Delta
		If Car.Angle &gt;= 180 Then Car.Angle :- 360.0
	End If
</pre><br>	Then we move the car<br><pre class=code>	Car.X :+ Cos(Car.Angle) * Car.Speed
	Car.Y :+ Sin(Car.Angle) * Car.Speed
	If Car.X &lt; 0 Then Car.X = 0 'Don't let the car off the edge of the map
	If Car.X &gt;= 320*32 Then Car.X = 320*32-1
	If Car.Y &lt; 0 Then Car.Y = 0
	If Car.Y &gt;= 320*32 Then Car.Y = 320*32-1
</pre><br>	So far, so good.  We just need to do one more thing before we are done.  Check the type of terrain the car is driving on and throttle the speed accordingly.<br><pre class=code>	Local Terrain:Int = Track.Map[(Car.X+16)/32,(Car.Y+16)/32]'Get the tile the car is currently driving over
	Select Terrain 'Change the terminal speed based on the terrain the car is on
		Case $FFFFFF
			Terminal = TerminalWalk
		Case $00FF00
			Terminal = TerminalGrass
		Case $808080
			Terminal = TerminalRoad
	End Select
</pre><br>	Now our code is complete.  For reference, here is the program in it's entirety.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

Type Track
	Global Map:Int[320,320] 'Our Track
	
	Function Load(Filename:String)
		Local AngleX:Float 'This will be used to calculate the starting angle of the car
		Local AngleY:Float
		Local Pixmap:TPixmap = LoadPixmap(Filename) 'Load in our map image
		If Not Pixmap Then Error("Couldn't open "+Filename) 'run custom error handler
		For Local y:Int = 0 To 319 'Go through each pixel of the map
			For Local x:Int = 0 To 319
				Map[x,y] = ReadPixel(Pixmap,x,y) &amp; $FFFFFF 'Copy the pixel color into the map
				If Map[x,y] = $FFFF80 'Is the pixel our angle indicator?
					Map[x,y] = $808080 'Change the color to grey
					AngleX = x * 32 'Store the Angle Indicator's x and y value
					AngleY = y * 32
				End If
				If Map[x,y] = $FF0000 'Is the pixel our car starting coordinates?
					Map[x,y] = $808080 'change color to grey
					Car.X = x * 32 'store the coordniates in the car type
					Car.Y = Y * 32
				End If
			Next
		Next
		Car.Angle = ATan2(AngleY-Car.Y,AngleX-Car.X) 'Calculate the angle relative to the car
		Car.Image = LoadImage("Car.png") 'load the car's image while we're here.
	End Function
	
	Function Draw(XD:Int, YD:Int)
		Local XE:Int = XD + 26 'The Rightmost tile to be drawn
		If XE &gt; 319 Then XE = 319 'Bouning check
		Local YE:Int = YD + 20 'The bottommost tile to be drawn
		If YE &gt; 319 Then YE = 319
		
		For Local Y:Int = YD To YE
			For Local X:Int = XD To XE
				SetColorRGB(Map[X,y]) 'In our simple example, the tiles are just colored squares
				DrawRect X*32-16,Y*32-16,32,32
			Next
		Next
		SetColor 255,255,255 'Don't forget to set the color back to white :)
	End Function
	
	Function SetColorRGB(Color:Int) 'Extract each color component for the SetColor function
		Local Red:Int = (Color &amp; $FF0000) Shr 16
		Local Green:Int = (Color &amp; $FF00) Shr 8
		Local Blue:Int = Color &amp; $FF
		SetColor Red,Green,Blue
	End Function
End Type

Type Car
	Global X:Float 'X position of car
	Global Y:Float 'Y position of car
	Global Angle:Float 'The angle of the car -0 degrees points right
	Global Image:TImage 'Our car image
	Global Speed:Float ' the current speed of the car
	
	Function draw()
		SetRotation Angle 'Set the car's angle
		DrawImage Image,x,y 'draw it
		SetRotation 0 'return the angle to 0
	End Function
End Type

Function Error(S:String)
	EndGraphics()
	RuntimeError(S)
	End
End Function

Local Filename:String = RequestFile("Map..")'Get the file to load
If Not filename Then End 'If the user presses Cancel, we will end the program
Graphics 800,600,32 'Set the graphics mode
HideMouse 'Hide the mouse
AutoMidHandle True 'Midhandle any graphics we load
Track.Load(Filename) 'Now load everything in

Local XO:Float 'Origin of the screen
Local YO:Float
Local XD:Int 'The Leftmost tile to draw
Local YD:Int 'The topmost tile to draw
Local Time:Int 'Using simple delta timing
Local Oldtime:Int = MilliSecs()
Local Delta:Float

Const TerminalRoad:Float = 10.0 'The fastest you can go on the road
Const TerminalWalk:Float = 8.0 'The fastest you can drive along the side of the road
Const TerminalGrass:Float = 2.0 'The fastest you can drive on the grass
Const Acceleration:Float = 10.0 'Acceleration speed in pixels per second^2
Const Turn:Float = 180.0 'Speed at which you turn in degrees per second

Local Terminal:Float = TerminalRoad 'initialize the terminal speed to the road

While Not KeyHit(KEY_ESCAPE)
	Cls
	XO = Car.X - 400 'We will try and keep the car in the middle of the screen
	YO = Car.Y - 400
	If XO &lt; 0 Then XO = 0 'If the car is near the edge, we stop scrolling
	If YO &lt; 0 Then YO = 0
	If XO &gt; 320 * 32 - 800 Then XO = 320 * 32 - 800
	If YO &gt; 320 * 32 - 600 Then YO = 320 * 32 - 600
	SetOrigin -XO,-YO 'Set the origin of the graphics
	
	XD = XO / 32 'calculating which tiles to draw is easy.
	YD = YO / 32 'Just divide the screen's position by the tile's dimentions
	
	Track.Draw(XD,YD)
	Car.Draw()
	
	SetOrigin 0,0 'return the origin to 0,0 so the spedometer will be drawn in the correct position
	SetColor 0,0,0 'The spedometer background will be black
	DrawOval 350,550,100,100 'draw a circle at the bottom of the screen
	SetColor 255,255,255 'The needle will be white
	Local SpedometerAngle:Float = (Car.Speed*180.0)/TerminalRoad - 180 'Calculate the needle angle based on speed
	DrawLine 400,600,Cos(SpedometerAngle)*50+400,Sin(SpedometerAngle)*50+600 'draw the needle
	Flip 'flip the graphics
	
	Time = MilliSecs()
	Delta = (Time - Oldtime) / 1000.0
	OldTime = Time
	
	If KeyDown(KEY_UP)
		Car.Speed :+ Acceleration*Delta 'Accelerate the car
	Else If KeyDown(KEY_DOWN)
		Car.Speed :- Acceleration*Delta*2 'brake the car at double the acceleration rate
	Else
		Car.Speed :- Acceleration*Delta 'if the accelrator is not pressed, decelrate smoothly
	End If
	If Car.Speed &lt; 0 Then Car.Speed = 0 'Car at complete stop
	If Car.Speed &gt; Terminal Then Car.Speed = Terminal 'Car at fastest speed

	If KeyDown(KEY_LEFT)
		Car.Angle :- Turn*Delta
		If Car.Angle &lt; -180.0 Then Car.Angle :+ 360.0
	End If
	If KeyDown(KEY_RIGHT)
		Car.Angle :+ Turn*Delta
		If Car.Angle &gt;= 180 Then Car.Angle :- 360.0
	End If
	Car.X :+ Cos(Car.Angle) * Car.Speed
	Car.Y :+ Sin(Car.Angle) * Car.Speed
	If Car.X &lt; 0 Then Car.X = 0 'Don't let the car off the edge of the map
	If Car.X &gt;= 320*32 Then Car.X = 320*32-1
	If Car.Y &lt; 0 Then Car.Y = 0
	If Car.Y &gt;= 320*32 Then Car.Y = 320*32-1
	
	Local Terrain:Int = Track.Map[(Car.X+16)/32,(Car.Y+16)/32]'Get the tile the car is currently driving over
	Select Terrain 'Change the terminal speed based on the terrain the car is on
		Case $FFFFFF
			Terminal = TerminalWalk
		Case $00FF00
			Terminal = TerminalGrass
		Case $808080
			Terminal = TerminalRoad
	End Select

Wend
</textarea><br>	Run the code above, and have fun.  be sure that the car.png image is in the same directory as the program.  Try modifying the program by adding checkpoints, AI, oil slicks, etc...<br>	<br>	For a bonus, here is the source to my PathMaker program.  create a 320x320 pixel png with a white background.  Draw a black line where you want the road to go.  Pathmaker will then create a road along the path.  Load the resulting pixmap in MSPaint and set your red and peachy-pink pixels, then load into the Driving program.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict
Local Filename:String = RequestFile("PathMaker File...")
If Not Filename Then End

Local Pixmap:TPixmap = LoadPixmap(Filename)
Local Track:TPixmap = CreatePixmap(320,320,PF_RGBA8888)
Local Color:Int
Local Test:Int
'convert road to proper colors
For Local y:Int = 0 To 319
	For Local x:Int = 0 To 319
		Color:Int = ReadPixel(Pixmap,x,y) &amp; $FFFFFF
		If Color = $FFFFFF
			WritePixel(Track,x,y,$FF00FF00)
		Else
			WritePixel(Track,x,y,$FF808080)
		End If
	Next
Next
'widen the road a bit
For Local Wide:Int = 1 To 2
	pixmap = CopyPixmap(Track)
	For Local x:Int = 0 To 319
		For Local y:Int = 0 To 319
			Color = ReadPixel(pixmap,x,y) &amp; $FFFFFF
			If color = $FF00
				For Local xd:Int = -1 To 1
					For Local yd:Int = -1 To 1
						If x+xd &gt;= 0 And x+xd &lt; 320 And y+yd &gt;= 0 And y+yd &lt; 320
							Test = ReadPixel(Pixmap,x+xd,y+yd) &amp; $FFFFFF
							If Test = $808080
								WritePixel(Track,x,y,$FF808080)
							End If
						End If
					Next
				Next
			End If
		Next
	Next
Next

'Add white border			
Pixmap = CopyPixmap(track)
For Local y:Int = 0 To 319
	For Local x:Int = 0 To 319
		Local Color:Int = ReadPixel(Pixmap,x,y) &amp; $FFFFFF
		If Color = $FF00
			For Local xd:Int = -1 To 1
				For Local yd:Int = -1 To 1
					If x+xd &gt;= 0 And x+xd &lt; 320 And y+yd &gt;= 0 And y+yd &lt; 320
						Test = ReadPixel(Pixmap,x+xd,y+yd)&amp;$FFFFFF
						If Test = $808080
							WritePixel(Track,x,y,$FFFFFFFF)
						End If
					End If
				Next
			Next
		End If
	Next
Next

Filename = RequestFile("Save Output As...","png",True)
SavePixmapPNG(track,Filename)
			
</textarea> <br><br></td></tr></table><br>
<a name="1043011"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GW</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I took your sourcecode and attached to it a neural net that trains the car to drive around the track automatically. <br>Because I created some custom maps and the NN is an additional include, the download is <a href="http://www.gutterbox.com/download/SmoothScroll.NNetCar.zip" target="_blank">HERE</a><br><br>The program doesn't draw graphics by default for the sake of speed, to check up on how the NN is learning, periodically hold down the space bar. <br><br></td></tr></table><br>
<a name="1043015"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jaydubeww</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Awesome tutorial. Ill definitely be looking at this in the future. <br><br></td></tr></table><br>
<a name="1154343"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >iaqsuk</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Tom Toad,<br>I am new to max and created couple of games in plus so far.  can this code be converted to plus?  lol, I need to learn max sooner or later.  Great script.  Thanks. <br><br></td></tr></table><br>
<a name="1154462"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I don't believe that Plus has a SetOrigin() command like Max does.  I've never programmed in Plus, but I have used BlitzBasic portion of Blitz3D and I believe the commands are similar.  However, it shouldn't be too complicated to mimic the behavior of SetOrigin.  You just need a couple of variables to hold the x offset and y offset, and then add them to all the coordinates before drawing to the screen.  That's basically what Max does behind the scenes anyway. <br><br></td></tr></table><br>
<a name="1154526"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >iaqsuk</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> After looking at the commands in max, it seems to be easier then plus especially in the keys commands but everything else seems to be very similar as far as coding.  I am going to start in max since I think I got the hang of plus.  Thanks for the response.  <br><br>The reason I asked if it can be your script can be mimiced to plus was, my map was in dim array(xx,xx) and your script makes it easier for the map anyways.  <br><br>I was planning on using a example script of a rpg game in plus with character movements for the car, but your example is way better in what I was planing on.  Planning on creating a street driving game and your script seems much easier.<br><br>Couple of request help on your script.  <br>1.  the map is 320,320 and once game starts, it's creates current views of 26 tiles x, 20 tiles y.  Is there any way to create the view much higher like 66 x 40 tiles higher?  or create a minimap on topright corner of the whole 320,320?<br>2.  I was looking at the Acceleration and I was trying to create a reverse speed.<br><br>I've been trying and trying to change your codes for both above and can't figure it out.  Please help if you can.  Thanks again. <br><br></td></tr></table><br>
<a name="1154909"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >iaqsuk</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi TomToad, or anyone that can help me.  I figured out the map to view more than 26 x 20 tiles to 96 x 90 tiles.  I also almost figured out the reversing the car.   Here's my code:<br><br>Const Reverse:Float = -10.0 'Acceleration speed in pixels per second^2<br><br>I added another contstant "Reverse"<br><br>I replace the original (KEY_DOWN) to (KEY_B) for brakes.<br><br>my code for (KEY-DOWN) is:<br>	<br>	If KeyDown(KEY_DOWN)<br>		Car.Speed :+ Reverse*Delta 'Reverse the car<br>	Else If KeyDown(KEY_C)<br>		Car.Speed :- Reverse*Delta*2 'brake the car at double the reverse rate<br>	Else<br>		Car.Speed :- Reverse*Delta 'if the reverse is not pressed, decelrate smoothly<br>	End If<br>	If Car.Speed &lt; 0 Then Car.Speed = -5 'Car at complete stop<br>	If Car.Speed &gt; Terminal Then Car.Speed = Terminal 'Car at fastest speed<br><br>then I added a "-5" if car.speed is &lt; 0, <br><br>and it goes reverse but does not slow down like original forward acceleration and also when the game starts the car goes slighly forward with pressing (KEY_UP).  <br><br>What am I doing wrong to fix the car going forward without pressing (KEY_UP),  the reverse will work for now even though it's not perfert.  Anyone,  Thanks. <br><br></td></tr></table><br>
<a name="1155212"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >iaqsuk</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi all, I was able to figure out putting car in reverse that behaved like the forward, but wasn't able to figure out the same speeds for grass and sidewalks in reverse.  Maybe when I have more time.  It was fun learning it, but I did find a blitzplus script that is similar to above script here:  <a href="http://www.blitzbasic.com/Community/posts.php?topic=55598#619100" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=55598#619100</a> .<br><br>I was able to create a forward &amp; reverse option on this script as well.  Hope it is useful for those who is still wanting these kinds of script.  Thanks. <br><br></td></tr></table><br>
<a name="1156286"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >iaqsuk</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi all, I found another forum with forward and reverse what I was trying to do here:  <a href="http://www.blitzbasic.com/Community/posts.php?topic=87085#988284" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=87085#988284</a> .  Thank. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
