<!DOCTYPE html><html lang="en" ><head ><title >3D, 2D Images</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='A Simple 3D imaging system, language=bb, category=3D Graphics - Misc'><meta name='author' content='LedgerARC'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=2>3D Graphics - Misc</a>/3D, 2D Images</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2576.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >3D, 2D Images by LedgerARC</td><td align="right">2009 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Unlike some 3D Imaging systems, this one can still use pixels for drawing, handling etc. The main point is that you can make images with transparency, fast rotation and scaling, easily. <br>I am currently working on getting a loadanimeimage3D() function going.<br>The system works a bunch slower than normal 2D commands, but you can make Images transparent, which is really the main bonus. Try not to have more than 150 images loaded, and no more than 20 images drawn at once. If any one has any pointers, I'd happily update the code.<br>I wrote the code in Protean, so the commenting might look a little weird in B3D, but it works very nicely in Protean. </td></tr><tr ><td class="cell"><pre class="code">;////////////////////////////////////////////////////////////////////////////////
;THIS SHOWS THE FUNCTIONALITY AND SPEED
;///////////////////////////////////////////////////////////////////////////////

Graphics3D 1024,768,0,2
SetBuffer BackBuffer()

Global Camera = CreateCamera()
SetDefault3DImagingCamera(Camera)

Local Image3D
Local HandleX
Local HandleY
Local DrawX
Local DrawY
Local SizeX
Local SizeY
Local Count
Local FrameRate
Local FrameTime
Local UpdateFrameRate
Local UpdateFrameRateDelay = 500
Local ImageUse = 1

FR3DAlwaysRefreshImages = True

AutoMidHandle3D(False)
Image3D = LoadImage3D("image1.png")
HandleX = ImageWidth3D(Image3D)/2
HandleY = ImageHeight3D(Image3D)/2
HandleImage3D(Image3D,HandleX,HandleY)

Dim Image2D(5)
For i = 1 To 5
	Image2D(i) = LoadImage("Image"+i+".png")
Next

While Not KeyDown(1)
	
	DrawX = MouseX()
	DrawY = MouseY()
	
	DrawImage3D(Image3D,DrawX,DrawY)
	
	If MouseDown(1)
		ScaleImage3D(Image3D,1.01,1.01)
	EndIf
	If MouseDown(2)
		ScaleImage3D(Image3D,.99,.99)
	EndIf
	If KeyHit(28)
		FlushKeys()
		Locate 0,GraphicsHeight()-FontHeight()*2
		SizeX = Input("Enter image width in pixels ")
		Locate 0,GraphicsHeight()-FontHeight()
		FlushKeys()
		SizeY = Input("Entity image height in pixels ")
		FlushKeys()
		ResizeImage3D(Image3D,SizeX,SizeY)
	EndIf
	If KeyDown(200)
		SetImageAlpha3D(Image3D,ImageAlpha3D#(Image3D)+.01)
	EndIf
	If KeyDown(208)
		SetImageAlpha3D(Image3D,ImageAlpha3D#(Image3D)-.01)
	EndIf
	If KeyHit(57)
		ScaleImage3D(Image3D,-1,-1)
	EndIf
	If KeyDown(203)
		RotateImage3D(Image3D,ImageRotation3D#(Image3D)+1)
	EndIf
	If KeyDown(205)
		RotateImage3D(Image3D,ImageRotation3D#(Image3D)-1)
	EndIf
	If KeyHit(16)
		HandleImage3D(Image3D,0,0)
	EndIf
	If KeyDown(17)
		HandleImage3D(Image3D,ImageXHandle3D(Image3D),ImageYHandle3D(Image3D)+1)
	EndIf
	If KeyHit(18)
		MidHandle3D(Image3D)
	EndIf
	If KeyDown(30)
		HandleImage3D(Image3D,ImageXHandle3D(Image3D)+1,ImageYHandle3D(Image3D))
	EndIf
	If KeyDown(31)
		HandleImage3D(Image3D,ImageXHandle3D(Image3D),ImageYHandle3D(Image3D)-1)
	EndIf
	If KeyDown(32)
		HandleImage3D(Image3D,ImageXHandle3D(Image3D)-1,ImageYHandle3D(Image3D))
	EndIf
	If KeyDown(46)
		Image3D = CopyImage3D(Image3D)
	EndIf
	If KeyDown(47)
		Free.FR3DImage = First FR3DImage
		If Free.FR3DImage &lt;&gt; Last FR3DImage
			FreeImage3D(Free\Img3D)
		EndIf
	EndIf
	If KeyDown(45)
		TileImage3D(Image3D)
	EndIf
	If KeyHit(44)
		FreeImage3D(Image3D,True,True)
		ImageUse = ImageUse + 1
		If ImageUse &gt; 5 Then ImageUse = 1
		Image3D = Make2D3D(Image2D(ImageUse))
	EndIf
	
	RenderWorld()
	Update3DImages()
	
	For LoopCount.FR3DImage = Each FR3DImage
		Count = Count + 1
	Next
	
	Text ImageXCorner3D(Image3D,DrawX,1),ImageYCorner3D(Image3D,DrawY,1),"&lt;---"+ImageXCorner3D(Image3D,DrawX,1)+" * "+ImageYCorner3D(Image3D,DrawY,1)+" Pixels",False,True
	Text ImageXCorner3D(Image3D,DrawX,2),ImageYCorner3D(Image3D,DrawY,2),"&lt;---"+ImageXCorner3D(Image3D,DrawX,2)+" * "+ImageYCorner3D(Image3D,DrawY,2)+" Pixels",False,True
	Text ImageXCorner3D(Image3D,DrawX,3),ImageYCorner3D(Image3D,DrawY,3),"&lt;---"+ImageXCorner3D(Image3D,DrawX,3)+" * "+ImageYCorner3D(Image3D,DrawY,3)+" Pixels",False,True
	Text ImageXCorner3D(Image3D,DrawX,4),ImageYCorner3D(Image3D,DrawY,4),"&lt;---"+ImageXCorner3D(Image3D,DrawX,4)+" * "+ImageYCorner3D(Image3D,DrawY,4)+" Pixels",False,True
	
	Text ImageXCorner3D(Image3D,DrawX,0),ImageYCorner3D(Image3D,DrawY,0),"Image Width = "+ImageWidth3D(Image3D),True,False
	Text ImageXCorner3D(Image3D,DrawX,0),ImageYCorner3D(Image3D,DrawY,0)+FontHeight()*-3,"Number of images loaded = "+Count,True,False
	Text ImageXCorner3D(Image3D,DrawX,0),ImageYCorner3D(Image3D,DrawY,0)+FontHeight()*-2,"Image X Handle = "+ImageXHandle3D(Image3D),True,False
	Text ImageXCorner3D(Image3D,DrawX,0),ImageYCorner3D(Image3D,DrawY,0)+FontHeight()*-1,"Image Y Handle = "+ImageYHandle3D(Image3D),True,False
	Text ImageXCorner3D(Image3D,DrawX,0),ImageYCorner3D(Image3D,DrawY,0)+FontHeight(),"Image Height = "+ImageHeight3D(Image3D),True,False
	Text ImageXCorner3D(Image3D,DrawX,0),ImageYCorner3D(Image3D,DrawY,0)+FontHeight()*2,"Image Alpha# = "+ImageAlpha3D(Image3D),True,False
	Text ImageXCorner3D(Image3D,DrawX,0),ImageYCorner3D(Image3D,DrawY,0)+FontHeight()*3,"Image Rotation = "+ImageRotation3D(Image3D),True,False
	
	Text 0,0,"Click Left mouse button to scale up"
	Text 0,FontHeight(),"Click Right mouse button to scale down"
	Text 0,FontHeight()*2,"Press Enter to enter specific scale amount"
	Text 0,FontHeight()*3,"Press 'Up' to increase image alpha"
	Text 0,FontHeight()*4,"Press 'Down' to decrease image alpha"
	Text 0,FontHeight()*5,"Press Spacebar to flip image"
	Text 0,FontHeight()*6,"Press 'Left' to rotate left"
	Text 0,FontHeight()*7,"Press 'Right' to rotate right"
	Text 0,FontHeight()*8,"Use W,A,S,D to change image X and Y handles"
	Text 0,FontHeight()*9,"Use E for middle handle, and Q for 0,0 handle"
	Text 0,FontHeight()*10,"Hold C to duplicate image"
	Text 0,FontHeight()*11,"Hold V to free images"
	Text 0,FontHeight()*12,"Hold X to tile image"
	Text 0,FontHeight()*13,"Press Z to change image with Make2D3D() (slow)"
	
	If UpdateFrameRate &lt; MilliSecs()
		UpdateFrameRate = MilliSecs() + UpdateFrameRateDelay
		FrameRate = MilliSecs() - FrameTime
		FrameRate = 1000/FrameRate
	EndIf
	FrameTime = MilliSecs()
	Text GraphicsWidth()/2,0,"FPS = "+FrameRate	
	
	Flip
	
	Count = 0
	SizeX = 0
	SizeY = 0
	
Wend
End





























































;////////////////////////////////////////////////////////////////////////////////////////
;THIS IS THE INCLUDE CODE
;////////////////////////////////////////////////////////////////////////////////////////
Global FR3DImageID ;The ID# of FR3DImage type
Global FR3DImagingCamera ;The Default 3D imaging camera
Global FR3DAutoMidHandle = False ;auto middle handle, use AutoMidHandle3D to switch
Global FR3DAlwaysRefreshImages = True ;Switch to false if you want to not hide the images every time Update3DImages() is called

Type FR3DImage
	
	Field Camera
	Field ID
	Field Img3D
	Field ZPos#
	Field ImgW#
	Field ImgH#
	Field ImgXHandle#
	Field ImgYHandle#
	Field Tex
	Field Temporary
	Field TemporaryReset
	Field Flags
	Field Alpha#
	Field Frames
	Field Rotate#
	
End Type

;;; &lt;summary&gt;Creates a 3D image&lt;/summary&gt;
;;; &lt;param name="TempPath"&gt;The full path/filename&lt;/param&gt;
;;; &lt;param name="TempFlag"&gt;The flag that the 3D image uses&lt;/param&gt;
;;; &lt;param name="TempZPos"&gt;Z buffer position for the 3D image to be drawn&lt;/param&gt;
;;; &lt;param name="Temp3DImagingCamera"&gt;The camera the 3D image is attached to&lt;/param&gt;
;;; &lt;remarks&gt;&lt;/remarks&gt;
;;; &lt;returns&gt;The 3D image as a sprite&lt;/returns&gt;
;;; &lt;subsystem&gt;Blitz.Images.3D&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function LoadImage3D(TempPath$,TempFlags=1,TempZPos#=2,Temp3DImagingCamera=0)
	
	Local TempImage3D
	Local TempTex
	Local TempImage
	Local TempWidth# = GraphicsWidth()
	Local TempScale# = TempZPos#/TempWidth#
	If Temp3DImagingCamera = 0 Then Temp3DImagingCamera = FR3DImagingCamera
	
	TempImage = LoadImage(TempPath$)
	TempTex = LoadTexture(TempPath$,TempFlags)
	TempImage3D = CreateSprite(Temp3DImagingCamera)
	PositionEntity TempImage3D,0,0,TempZPos#
	EntityTexture(TempImage3D,TempTex)
	ScaleSprite TempImage3D,ImageWidth(TempImage)*TempScale#,ImageHeight(TempImage)*TempScale#
	
	;Setup the Image3D Type
	NewFR3DImage.FR3DImage = New FR3DImage
	NewFR3DImage\Camera = Temp3DImagingCamera
	NewFR3DImage\ID = FRNewFR3DImageID()
	NewFR3DImage\Img3D = TempImage3D
	NewFR3DImage\ZPos# = TempZPos#
	NewFR3DImage\ImgW# = ImageWidth(TempImage)
	NewFR3DImage\ImgH# = ImageHeight(TempImage)
	If FR3DAutoMidHandle = False
		HandleSprite(TempImage3D,-1,1)
		NewFR3DImage\ImgXHandle# = -1
		NewFR3DImage\ImgYHandle# = 1
	Else
		HandleSprite(TempImage3D,0,0)
		NewFR3DImage\ImgXHandle# = 0
		NewFR3DImage\ImgYHandle# = 0
	EndIf
	NewFR3DImage\Tex = TempTex
	NewFR3DImage\Temporary = False
	NewFR3DImage\TemporaryReset = 0
	NewFR3DImage\Flags = TempFlags
	NewFR3DImage\Alpha# = 1
	NewFR3DImage\Frames = 1
	NewFR3DImage\Rotate# = 0
	
	FreeImage TempImage
	HideEntity TempImage3D
	
	Return TempImage3D
	
End Function

;;; &lt;summary&gt;Sets the 3D imaging camera&lt;/summary&gt;
;;; &lt;param name="TempCamera"&gt;The camera used for 3D images&lt;/param&gt;
;;; &lt;remarks&gt;&lt;/remarks&gt;
;;; &lt;returns&gt;&lt;/returns&gt;
;;; &lt;subsystem&gt;Blitz.Images.3D&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function SetDefault3DImagingCamera(TempCamera)
	
	FR3DImagingCamera = TempCamera
	
End Function

;;; &lt;summary&gt;Shows a 3D image with 2D corodinates&lt;/summary&gt;
;;; &lt;param name="TempImage"&gt;The handle of a 3D image&lt;/param&gt;
;;; &lt;param name="TempX"&gt;X position on-screen to draw the image&lt;/param&gt;
;;; &lt;param name="TempY"&gt;Y position on-screen to draw the image&lt;/param&gt;
;;; &lt;remarks&gt;&lt;/remarks&gt;
;;; &lt;returns&gt;&lt;/returns&gt;
;;; &lt;subsystem&gt;Blitz.Images.3D&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function DrawImage3D(TempImage,TempX#,TempY#,TempFrame=0)
	
	Local TempWidth# = GraphicsWidth()
	Local TempHeight# = GraphicsHeight()
	Local TempX0# = (TempWidth#/2)/(TempWidth#*.25)
	Local TempY0# = (TempHeight#/2)/(TempWidth#*.25)
	Local Temp3DX# = TempX#/(TempWidth#*.25)
	Local Temp3DY# = TempY#/(TempWidth#*.25)
	
	For Find.FR3DImage = Each FR3DImage
		
		If Find\Img3D = TempImage
			
			ShowEntity(Find\Img3D)
			EntityTexture(Find\Img3D,Find\Tex,TempFrame)
			PositionEntity Find\Img3D,-TempX0#+Temp3DX#,TempY0#-Temp3DY#,Find\ZPos#
			
		EndIf
		
	Next
	
End Function

;;; &lt;summary&gt;Increases the image ID by 1 and returns it&lt;/summary&gt;
;;; &lt;remarks&gt;&lt;/remarks&gt;
;;; &lt;returns&gt;FR3DImageID&lt;/returns&gt;
;;; &lt;subsystem&gt;Blitz.Images.3D&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function FRNewFR3DImageID()
	
	FR3DImageID = FR3DImageID + 1
	Return FR3DImageID
	
End Function

;;; &lt;summary&gt;Sets the handle for the 3D image in pixels&lt;/summary&gt;
;;; &lt;param name="TempImage"&gt;The handle of a 3D image&lt;/param&gt;
;;; &lt;param name="TempX"&gt;X position of handle in pixels&lt;/param&gt;
;;; &lt;param name="TempY"&gt;Y position of handle in pixels&lt;/param&gt;
;;; &lt;remarks&gt;&lt;/remarks&gt;
;;; &lt;returns&gt;&lt;/returns&gt;
;;; &lt;subsystem&gt;Blitz.Images.3D&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function HandleImage3D(TempImage,TempX#,TempY#)
	
	For Find.FR3DImage = Each FR3DImage
		
		If Find\Img3D = TempImage
			
			Local TempX3D# = TempX#/(Find\ImgW#/2)
			Local TempY3D# = TempY#/(Find\ImgH#/2)
			TempX3D# = TempX3D# - 1
			TempY3D# = -TempY3D# + 1
			HandleSprite Find\Img3D,TempX3D#,TempY3D#
			Find\ImgXHandle = TempX3D#
			Find\ImgYHandle = TempY3D#
			
		EndIf
		
	Next
	
End Function

;;; &lt;summary&gt;Sets the handle of the 3D image to the middle&lt;/summary&gt;
;;; &lt;param name="TempImage"&gt;The handle of a 3D image&lt;/param&gt;
;;; &lt;remarks&gt;&lt;/remarks&gt;
;;; &lt;returns&gt;&lt;/returns&gt;
;;; &lt;subsystem&gt;Blitz.Images.3D&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function MidHandle3D(TempImage)
	
	For Find.FR3DImage = Each FR3DImage
		
		If Find\Img3D = TempImage
			
			HandleSprite Find\Img3D,0,0
			Find\ImgXHandle# = 0
			Find\ImgYHandle# = 0
			
		EndIf
		
	Next
	
End Function

;;; &lt;summary&gt;Returns the X handle position of the image in pixels&lt;/summary&gt;
;;; &lt;param name="TempImage"&gt;The handle of a 3D image&lt;/param&gt;
;;; &lt;remarks&gt;&lt;/remarks&gt;
;;; &lt;returns&gt;The X handle of an image in pixels&lt;/returns&gt;
;;; &lt;subsystem&gt;Blitz.Images.3D&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function ImageXHandle3D#(TempImage)
	
	For Find.FR3DImage = Each FR3DImage
		
		If Find\Img3D = TempImage
			
			Return (Find\ImgXHandle#+1)*(Find\ImgW#/2)
			
			Return 
			
		EndIf
		
	Next
	
End Function

;;; &lt;summary&gt;Returns the Y handle position of the image in pixels&lt;/summary&gt;
;;; &lt;param name="TempImage"&gt;The handle of a 3D image&lt;/param&gt;
;;; &lt;remarks&gt;&lt;/remarks&gt;
;;; &lt;returns&gt;The Y handle of an image in pixels&lt;/returns&gt;
;;; &lt;subsystem&gt;Blitz.Images.3D&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function ImageYHandle3D#(TempImage)
	
	For Find.FR3DImage = Each FR3DImage
		
		If Find\Img3D = TempImage
			
			Return (-Find\ImgYHandle#+1)*(Find\ImgH#/2)
			
		EndIf
		
	Next
	
End Function

;;; &lt;summary&gt;Turns auto middle handle of 3D images on/off&lt;/summary&gt;
;;; &lt;param name="Enable"&gt;True or false for on or off&lt;/param&gt;
;;; &lt;remarks&gt;&lt;/remarks&gt;
;;; &lt;returns&gt;&lt;/returns&gt;
;;; &lt;subsystem&gt;Blitz.Images.3D&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function AutoMidHandle3D(Enable)
	
	If Enable &lt;&gt; False Then
		FR3DAutoMidHandle = True
	Else
		FR3DAutoMidHandle = False
	EndIf
	
End Function

;;; &lt;summary&gt;Updates all the 3D images&lt;/summary&gt;
;;; &lt;remarks&gt;&lt;/remarks&gt;
;;; &lt;returns&gt;&lt;/returns&gt;
;;; &lt;subsystem&gt;Blitz.Images.3D&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function Update3DImages()
	
	For Find.FR3DImage = Each FR3DImage
		
		If FR3DAlwaysRefreshImages = True
			
			HideEntity Find\Img3D
			
		EndIf
		
		If Find\Temporary = True
			
			FreeImage3D(Find\Img3D,Find\TemporaryReset,False)
			
		EndIf
		
	Next
	
End Function

;;; &lt;summary&gt;Set the alpha value for a 3D image&lt;/summary&gt;
;;; &lt;param name="TempImage"&gt;The handle of a 3D image&lt;/param&gt;
;;; &lt;param name="TempAlpha"&gt;a number between 1 and 0 1=solid, 0=invisible&lt;/param&gt;
;;; &lt;remarks&gt;&lt;/remarks&gt;
;;; &lt;returns&gt;&lt;/returns&gt;
;;; &lt;subsystem&gt;Blitz.Images.3D&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function SetImageAlpha3D(TempImage,TempAlpha#)
	
	If TempAlpha# &gt; 1 Then TempAlpha# = 1
	If TempAlpha# &lt; 0 Then TempAlpha# = 0
	
	For Find.FR3DImage = Each FR3DImage
		
		If Find\Img3D = TempImage
			
			EntityAlpha(Find\Img3D,TempAlpha#)
			Find\Alpha# = TempAlpha#
			
		EndIf
		
	Next
	
End Function

;;; &lt;summary&gt;Returns the alpha of a 3D image&lt;/summary&gt;
;;; &lt;param name="TempImage"&gt;The handle of a 3D image&lt;/param&gt;
;;; &lt;remarks&gt;&lt;/remarks&gt;
;;; &lt;returns&gt;The alpha value of the specified 3D image&lt;/returns&gt;
;;; &lt;subsystem&gt;Blitz.Images.3D&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function ImageAlpha3D#(TempImage)
	
	For Find.FR3DImage = Each FR3DImage
		
		If Find\Img3D = TempImage
			
			Return Find\Alpha#
			
		EndIf
		
	Next
	
End Function

;;; &lt;summary&gt;Copy's a 3D image&lt;/summary&gt;
;;; &lt;param name="TempImage"&gt;The handle of a 3D image&lt;/param&gt;
;;; &lt;remarks&gt;&lt;/remarks&gt;
;;; &lt;returns&gt;The newly copied Image&lt;/returns&gt;
;;; &lt;subsystem&gt;Blitz.Images.3D&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function CopyImage3D(TempImage)
	
	For Find.FR3DImage = Each FR3DImage
		
		If Find\Img3D = TempImage
			
			NewFR3DImage.FR3DImage = New FR3DImage
			NewFR3DImage\Camera = Find\Camera
			NewFR3DImage\ID = FRNewFR3DImageID()
			NewFR3DImage\Img3D = CopyEntity(Find\Img3D,Find\Camera)
			NewFR3DImage\ZPos# = Find\ZPos#
			NewFR3DImage\ImgW# = Find\ImgW#
			NewFR3DImage\ImgH# = Find\ImgH#
			NewFR3DImage\ImgXHandle# = Find\ImgXHandle#
			NewFR3DImage\ImgYHandle# = Find\ImgYHandle#
			NewFR3DImage\Tex = CreateTexture(TextureWidth(Find\Tex),TextureHeight(Find\Tex),Find\Flags,Find\Frames)
			CopyRect(0,0,TextureWidth(Find\Tex),TextureHeight(Find\Tex),0,0,TextureBuffer(Find\Tex),TextureBuffer(NewFR3DImage\Tex))
			EntityTexture(NewFR3DImage\Img3D,NewFR3DImage\Tex)
			NewFR3DImage\Temporary = False
			NewFR3DImage\TemporaryReset = False
			NewFR3DImage\Flags = Find\Flags
			NewFR3DImage\Alpha# = Find\Alpha#
			NewFR3DImage\Frames = Find\Frames
			NewFR3DImage\Rotate# = Find\Rotate#
			
			Return NewFR3DImage\Img3D
			
		EndIf
		
	Next
	
End Function

;;; &lt;summary&gt;Deletes a 3D image&lt;/summary&gt;
;;; &lt;param name="TempImage"&gt;The handle of a 3D image&lt;/param&gt;
;;; &lt;param name="TempResetOrder"&gt;If true, this will reset the order of all FR3DImage ID's&lt;/param&gt;
;;; &lt;param name="TempWait"&gt;True to not delete until next call to Update3DImages()&lt;/param&gt;
;;; &lt;remarks&gt;&lt;/remarks&gt;
;;; &lt;returns&gt;&lt;/returns&gt;
;;; &lt;subsystem&gt;Blitz.Images.3D&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function FreeImage3D(TempImage,TempResetOrder=True,TempWait=False)
	
	Local TempReseting = False
	
	For Find.FR3DImage = Each FR3DImage
		
		If TempReseting = True
			
			Find\ID = Find\ID - 1
			
		EndIf
		
		If Find\Img3D = TempImage
			
			If TempWait = False
				
				FreeEntity(Find\Img3D)
				FreeTexture(Find\Tex)
				Delete Find.FR3DImage
				TempReseting = TempResetOrder
				If TempReseting = True Then FR3DImageID = FR3DImageID - 1
				
			Else
				
				Find\Temporary = True
				Find\TemporaryReset = TempResetOrder
				
			EndIf
			
		EndIf
		
	Next
	
End Function

;;; &lt;summary&gt;Creates a blank 3D image&lt;/summary&gt;
;;; &lt;param name="TempWidth"&gt;Width of 3D image&lt;/param&gt;
;;; &lt;param name="TempHeight"&gt;Height of 3D image&lt;/param&gt;
;;; &lt;param name="TempFlags"&gt;The flag that the 3D image uses&lt;/param&gt;
;;; &lt;param name="TempFrames"&gt;How many  frames the 3D image has&lt;/param&gt;
;;; &lt;param name="TempZPos"&gt;Z buffer position for the 3D image to be drawn&lt;/param&gt;
;;; &lt;param name="Temp3DImagingCamera"&gt;The camera the 3D image is attached to&lt;/param&gt;
;;; &lt;remarks&gt;&lt;/remarks&gt;
;;; &lt;returns&gt;The 3D image as a sprite&lt;/returns&gt;
;;; &lt;subsystem&gt;Blitz.Images.3D&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function CreateImage3D(TempWidth,TempHeight,TempFlags=1,TempFrames=1,TempZPos#=2,Temp3DImagingCamera=0)
	
	Local TempImage3D
	Local TempGFXWidth# = GraphicsWidth()
	Local TempXScale# = TempZPos#/TempGFXWidth#
	Local TempYScale# = TempZPos#/TempGFXWidth#
	If Temp3DImagingCamera = 0 Then Temp3DImagingCamera = FR3DImagingCamera
	
	TempImage3D = CreateSprite(Temp3DImagingCamera)
	PositionEntity TempImage3D,0,0,TempZPos#
	ScaleSprite TempImage3D,TempWidth*TempXScale#,TempHeight*TempYScale#
	
	;Setup the Image3D Type
	NewFR3DImage.FR3DImage = New FR3DImage
	NewFR3DImage\Camera = Temp3DImagingCamera
	NewFR3DImage\ID = FRNewFR3DImageID()
	NewFR3DImage\Img3D = TempImage3D
	NewFR3DImage\ZPos# = TempZPos#
	NewFR3DImage\ImgW# = TempWidth
	NewFR3DImage\ImgH# = TempHeight
	If FR3DAutoMidHandle = False
		HandleSprite(TempImage3D,-1,1)
		NewFR3DImage\ImgXHandle# = -1
		NewFR3DImage\ImgYHandle# = 1
	Else
		HandleSprite(TempImage3D,0,0)
		NewFR3DImage\ImgXHandle# = 0
		NewFR3DImage\ImgYHandle# = 0
	EndIf
	NewFR3DImage\Tex = CreateTexture(TempWidth,TempHeight,TempFlags,TempFrames)
	NewFR3DImage\Temporary = False
	NewFR3DImage\TemporaryReset = 0
	NewFR3DImage\Flags = TempFlags
	NewFR3DImage\Alpha# = 1
	NewFR3DImage\Frames = TempFrames
	NewFR3DImage\Rotate# = 0
	
	HideEntity TempImage3D
	
	Return TempImage3D
	
End Function

;;; &lt;summary&gt;Creates a 3D image from a loaded 2D image&lt;/summary&gt;
;;; &lt;param name="TempImage2D"&gt;The 2D image to make into a 3D image&lt;/param&gt;
;;; &lt;param name="TempFlag"&gt;The flag that the 3D image uses&lt;/param&gt;
;;; &lt;param name="TempZPos"&gt;Z buffer position for the 3D image to be drawn&lt;/param&gt;
;;; &lt;param name="Temp3DImagingCamera"&gt;The camera the 3D image is attached to&lt;/param&gt;
;;; &lt;remarks&gt;Vary Slow! much faster to use LoadImage3D()!&lt;/remarks&gt;
;;; &lt;returns&gt;The 3D image as a sprite&lt;/returns&gt;
;;; &lt;subsystem&gt;Blitz.Images.3D&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function Make2D3D(Temp2DImage,TempFlags=1,TempFrames=1,TempZPos#=2,Temp3DImaginfCamera=0)
	
	Local TempImage3D
	Local TempTex
	Local TempGFXWidth# = GraphicsWidth()
	Local TempXScale# = TempZPos#/TempGFXWidth#
	Local TempYScale# = TempZPos#/TempGFXWidth#
	If Temp3DImagingCamera = 0 Then Temp3DImagingCamera = FR3DImagingCamera
	
	TempTex = CreateTexture(ImageWidth(Temp2DImage),ImageHeight(Temp2DImage),TempFlags,TempFrames)
	TempImgWidth = ImageWidth(Temp2DImage)
	TempImgHeight = ImageHeight(Temp2DImage)
	ResizeImage(Temp2DImage,TextureWidth(TempTex),TextureHeight(TempTex))
	
	For TempCountFrames = 0 To TempFrames - 1
		
		SetBuffer TextureBuffer(TempTex,TempCountFrames)
		DrawImage(Temp2DImage,ImageXHandle(Temp2DImage),ImageYHandle(Temp2DImage),TempCountFrames)
		
	Next
	SetBuffer BackBuffer()
	ResizeImage(Temp2DImage,TempImgWidth,TempImgHeight)
	
	TempImage3D = CreateSprite(Temp3DImagingCamera)
	PositionEntity TempImage3D,0,0,TempZPos#
	EntityTexture(TempImage3D,TempTex)
	ScaleSprite TempImage3D,ImageWidth(Temp2DImage)*TempXScale#,ImageHeight(Temp2DImage)*TempYScale#
	
	NewFR3DImage.FR3DImage = New FR3DImage
	NewFR3DImage\Camera = Temp3DImagingCamera
	NewFR3DImage\ID = FRNewFR3DImageID()
	NewFR3DImage\Img3D = TempImage3D
	NewFR3DImage\ZPos# = TempZPos#
	NewFR3DImage\ImgW# = ImageWidth(Temp2DImage)
	NewFR3DImage\ImgH# = ImageHeight(Temp2DImage)
	If FR3DAutoMidHandle = False
		HandleSprite(TempImage3D,-1,1)
		NewFR3DImage\ImgXHandle# = -1
		NewFR3DImage\ImgYHandle# = 1
	Else
		HandleSprite(TempImage3D,0,0)
		NewFR3DImage\ImgXHandle# = 0
		NewFR3DImage\ImgYHandle# = 0
	EndIf
	NewFR3DImage\Tex = TempTex
	NewFR3DImage\Temporary = False
	NewFR3DImage\TemporaryReset = 0
	NewFR3DImage\Flags = TempFlags
	NewFR3DImage\Alpha# = 1
	NewFR3DImage\Frames = TempFrames
	NewFR3DImage\Rotate# = 0
	
	HideEntity TempImage3D
	
	Return TempImage3D
	
End Function

;;; &lt;summary&gt;Grabs a portion of the current drawing buffer and put it onto a 3D image&lt;/summary&gt;
;;; &lt;param name="TempImage"&gt;The handle of a 3D image&lt;/param&gt;
;;; &lt;param name="TempX"&gt;X position of current buffer to start from&lt;/param&gt;
;;; &lt;param name="TempY"&gt;Y position of current buffer to start from&lt;/param&gt;
;;; &lt;param name="TempFrame"&gt;Frame to insert the grabed image into&lt;/param&gt;
;;; &lt;remarks&gt;&lt;/remarks&gt;
;;; &lt;returns&gt;Vary Slow!&lt;/returns&gt;
;;; &lt;subsystem&gt;Blitz.Images.3D&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function GrabImage3D(TempImage,TempX,TempY,TempFrame=0)
	
	For Find.FR3DImage = Each FR3DImage
		
		If Find\Img3D = TempImage
			
			Local TempImg2D = CreateImage(Find\ImgW,Find\ImgH)
			
			CopyRect(TempX,TempY,Find\ImgW,Find\ImgH,0,0,GraphicsBuffer(),ImageBuffer(TempImg2D))
			ResizeImage TempImg2D,TextureWidth(Find\Tex),TextureHeight(Find\Tex)
			CopyRect(0,0,ImageWidth(TempImg2D),ImageHeight(TempImg2D),0,0,ImageBuffer(TempImg2D),TextureBuffer(Find\Tex))
			
		EndIf
		
	Next
	
End Function

;;; &lt;summary&gt;Tiles a 3D image over the screen&lt;/summary&gt;
;;; &lt;param name="TempImage"&gt;The handle of a 3D image&lt;/param&gt;
;;; &lt;param name="TempX"&gt;X position to start&lt;/param&gt;
;;; &lt;param name="TempY"&gt;Y position to start&lt;/param&gt;
;;; &lt;param name="TempFrame"&gt;Frame to Tile&lt;/param&gt;
;;; &lt;remarks&gt;VARY SLOW!!!!!&lt;/remarks&gt;
;;; &lt;returns&gt;&lt;/returns&gt;
;;; &lt;subsystemBlitz.Images.3D&gt;&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function TileImage3D(TempImage,TempX=0,TempY=0,TempFrame=0)
	
	Local TempNextX = TempX
	Local TempNextY = TempY
	Local TempImage3D
	
	For Find.FR3DImage = Each FR3DImage
		
		If Find\Img3D = TempImage
			
			For TempTileX = TempX To GraphicsWidth()
				
				For TempTileY = TempY To GraphicsHeight()
					
					If TempTileX = TempNextX 
						
						If TempTileY = TempNextY
							
							TempImage3D = CopyImage3D(Find\Img3D)
							FreeImage3D(TempImage3D,True,True)
							
							DrawImage3D(TempImage3D,TempTileX,TempTileY,TempFrame)
							
							TempNextY = TempNextY + Find\ImgH
							
						EndIf
						
					EndIf
					
				Next
				
				If TempTileX = TempNextX
					
					TempNextX = TempNextX + Find\ImgW
					TempNextY = TempY
					
				EndIf
				
			Next
			
		EndIf
		
	Next
	
End Function

;;; &lt;summary&gt;Scales a 3D image usng percents&lt;/summary&gt;
;;; &lt;param name="TempImage"&gt;The handle of a 3D image&lt;/param&gt;
;;; &lt;param name="TempXScale"&gt;X scale of 3D image&lt;/param&gt;
;;; &lt;param name="TempYScale"&gt;Y scale of 3D image&lt;/param&gt;
;;; &lt;remarks&gt;&lt;/remarks&gt;
;;; &lt;returns&gt;&lt;/returns&gt;
;;; &lt;subsystem&gt;Blitz.Images.3D&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function ScaleImage3D(TempImage,TempXScale#,TempYScale#)
	
	For Find.FR3DImage = Each FR3DImage
		
		If Find\Img3D = TempImage
			
			Local TempWidth# = GraphicsWidth()
			Local TempScale# = Find\ZPos#/TempWidth#
			
			ScaleSprite Find\Img3D,(Find\ImgW*TempScale#)*TempXScale#,(Find\ImgH*TempScale#)*TempYScale#
			Find\ImgW = Find\ImgW * TempXScale#
			Find\ImgH = Find\ImgH * TempYScale#
			
		EndIf
		
	Next
	
End Function

;;; &lt;summary&gt;Scales a 3D image usng pixels&lt;/summary&gt;
;;; &lt;param name="TempImage"&gt;The handle of a 3D image&lt;/param&gt;
;;; &lt;param name="TempXScale"&gt;X scale of 3D image&lt;/param&gt;
;;; &lt;param name="TempYScale"&gt;Y scale of 3D image&lt;/param&gt;
;;; &lt;remarks&gt;&lt;/remarks&gt;
;;; &lt;returns&gt;&lt;/returns&gt;
;;; &lt;subsystem&gt;Blitz.Images.3D&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function ResizeImage3D(TempImage,TempXScale,TempYScale)
	
	For Find.FR3DImage = Each FR3DImage
		
		If Find\Img3D = TempImage
			
			Local TempWidth# = GraphicsWidth()
			Local TempScale# = Find\ZPos#/TempWidth#
			
			ScaleSprite Find\Img3D,TempXScale*TempScale#,TempYScale*TempScale#
			Find\ImgW = TempXScale
			Find\ImgH = TempYScale
			
		EndIf
		
	Next
	
End Function

;;; &lt;summary&gt;Returns the width of a 3D image&lt;/summary&gt;
;;; &lt;param name="TempImage"&gt;The handle of a 3D image&lt;/param&gt;
;;; &lt;remarks&gt;&lt;/remarks&gt;
;;; &lt;returns&gt;The width of the specified 3D image&lt;/returns&gt;
;;; &lt;subsystem&gt;Blitz.Images.3D&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function ImageWidth3D#(TempImage)
	
	For Find.FR3DImage = Each FR3DImage
		
		If Find\Img3D = TempImage
			
			Return Find\ImgW#
			
		EndIf
		
	Next
	
End Function

;;; &lt;summary&gt;Returns the height of a 3D image&lt;/summary&gt;
;;; &lt;param name="TempImage"&gt;The handle of a 3D image&lt;/param&gt;
;;; &lt;remarks&gt;&lt;/remarks&gt;
;;; &lt;returns&gt;The height of the specified 3D image&lt;/returns&gt;
;;; &lt;subsystem&gt;Blitz.Images.3D&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function ImageHeight3D#(TempImage)
	
	For Find.FR3DImage = Each FR3DImage
		
		If Find\Img3D = TempImage
			
			Return Find\ImgH#
			
		EndIf
		
	Next
	
End Function

;;; &lt;summary&gt;Creates a 3D rectangle&lt;/summary&gt;
;;; &lt;param name="TempX"&gt;Starting X position&lt;/param&gt;
;;; &lt;param name="TempY"&gt;Starting Y position&lt;/param&gt;
;;; &lt;param name="TempWidth"&gt;Width of rectangle&lt;/param&gt;
;;; &lt;param name="TempHeight"&gt;Height of rectangle&lt;/param&gt;
;;; &lt;param name="TempTemp"&gt;If true, the rectangle will be deleted when Update3DImages() is called&lt;/param&gt;
;;; &lt;param name="TempResetOrder"&gt;If TempTemp if true, this will reset the order of FR3DImage\ID if true&lt;/param&gt;
;;; &lt;param name="TempZPos"&gt;Z buffer position for the 3D image to be drawn&lt;/param&gt;
;;; &lt;param name="Temp3DImagingCamera"&gt;The camera the 3D image is attached to&lt;/param&gt;
;;; &lt;remarks&gt;&lt;/remarks&gt;
;;; &lt;returns&gt;Will return the rectangle image as a sprite ONLY if TempTemp is false&lt;/returns&gt;
;;; &lt;subsystem&gt;Blitz.Images.3D&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function Rect3D(TempX,TempY,TempWidth,TempHeight,TempTemp=True,TempResetOrder=True,TempZPos#=2,Temp3DImagingCamera=0)
	
	
	If Temp3DImagingCamera = 0 Then Temp3DImagingCamera = FR3DImagingCamera
	Local TempImage3D = CreateImage3D(TempWidth,TempHeight,4,1,TempZPos#,Temp3DImagingCamera)
	If TempTemp = True FreeImage3D(TempImage3D,TempResetOrder,True)
	
	For Find.FR3DImage = Each FR3DImage
		
		If Find\Img3D = TempImage3D
			
			TempBuffer = GraphicsBuffer()
			SetBuffer TextureBuffer(Find\Tex)
			ClsColor ColorRed(),ColorGreen(),ColorBlue()
			Cls
			SetBuffer TempBuffer
			Exit
			
		EndIf
		
	Next
	
	DrawImage3D(TempImage3D,TempX,TempY)
	
	If TempTemp=False Return TempImage3D
	
End Function

;;; &lt;summary&gt;Rotates a 3D image&lt;/summary&gt;
;;; &lt;param name="TempImage"&gt;The handle of a 3D image&lt;/param&gt;
;;; &lt;param name="TempValue"&gt;Absolute rotation of the 3D image&lt;/param&gt;
;;; &lt;remarks&gt;&lt;/remarks&gt;
;;; &lt;returns&gt;&lt;/returns&gt;
;;; &lt;subsystem&gt;Blitz.Images.3D&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function RotateImage3D(TempImage,TempValue#)
	
	For Find.FR3DImage = Each FR3DImage
		
		If Find\Img3D = TempImage
			
			RotateSprite Find\Img3D,TempValue#
			Find\Rotate# = TempValue#
			
		EndIf
		
	Next
	
End Function

;;; &lt;summary&gt;Returns the Rotation of a 3D image&lt;/summary&gt;
;;; &lt;param name="TempImage"&gt;The handle of a 3D image&lt;/param&gt;
;;; &lt;remarks&gt;&lt;/remarks&gt;
;;; &lt;returns&gt;The rotation of a 3D image&lt;/returns&gt;
;;; &lt;subsystem&gt;Blitz.Images.3D&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function ImageRotation3D#(TempImage)
	
	For Find.FR3DImage = Each FR3DImage
		
		If Find\Img3D = TempImage
			
			Return Find\Rotate#
			
		EndIf
		
	Next
	
End Function

;;; &lt;summary&gt;Returns the X coords on-screen of the specified image's specified corner&lt;/summary&gt;
;;; &lt;param name="TempImage"&gt;The handle of a 3D image&lt;/param&gt;
;;; &lt;param name="TempX"&gt;X position on-screen that the image is drawn&lt;/param&gt;
;;; &lt;param name="TempCorner"&gt;The corner's number [0=center, 1=top-left, 2=bottom-left, 3=bottom-right, 4=top-left&lt;/param&gt;
;;; &lt;remarks&gt;&lt;/remarks&gt;
;;; &lt;returns&gt;The Image's corner's X position on-screen&lt;/returns&gt;
;;; &lt;subsystem&gt;Blitz.Images.3D&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function ImageXCorner3D#(TempImage,TempX#,TempCorner)
	
	Local TempDrawX# = TempX#
	Local TempHDist#
	Local TempAngle1#
	Local TempAngle2#
	Local TempDist#
	Local TempRotateX
	Local TempDistAngle#
	Local TempReturn#
	
	For Find.FR3DImage = Each FR3DImage
		
		If Find\Img3D = TempImage
			
			TempHDist# = Find\ImgW# ^ 2 + Find\ImgH# ^ 2
			TempHDist# = Sqr(TempHDist#)/2
			TempAngle1# = ASin(Find\ImgH#/2/TempHDist#) + -Find\Rotate#
			TempAngle2# = ACos(Find\ImgH#/2/TempHDist#) + -Find\Rotate#
			
			TempDist# = (Find\ImgW#/2 - ImageXHandle3D(Find\Img3D)) ^ 2 + (Find\ImgH#/2 - ImageYHandle3D(Find\Img3D)) ^ 2
			TempDist# = Sqr(TempDist#)
			
			If ImageXHandle3D(Find\Img3D) =&gt; Find\ImgW#/2 And ImageYHandle3D(Find\Img3D) &lt;= Find\ImgH#/2 Then
				TempRotateX = -Find\Rotate#
			EndIf
			If ImageXHandle3D(Find\Img3D) =&gt; Find\ImgW#/2 And ImageYHandle3D(Find\Img3D) =&gt; Find\ImgH#/2
				TempRotateX = Find\Rotate#
			EndIf
			If ImageXHandle3D(Find\Img3D) =&lt; Find\ImgW#/2 And ImageYHandle3D(Find\Img3D) =&gt; Find\ImgH#/2
				TempRotateX = Find\Rotate#
			EndIf
			If ImageXHandle3D(Find\Img3D) =&lt; Find\ImgW#/2 And ImageYHandle3D(Find\Img3D) =&lt; Find\ImgH#/2
				TempRotateX = -Find\Rotate#
			EndIf
			
			If TempDist# &lt;&gt; 0
				TempDistAngle# = ACos((Find\ImgW#/2 - ImageXHandle3D(Find\Img3D))/TempDist#) + TempRotateX
				TempDrawX# = TempX# + Cos(TempDistAngle#)*TempDist#
			EndIf
			
			Select TempCorner
				
				Case 0
					
					Return TempDrawX
					
				Case 1
					
					Return TempDrawX - Cos(TempAngle1#)*TempHDist#
					
					
				Case 2
					
					TempAngle2# = TempAngle2# + 90
					Return TempDrawX - Cos(TempAngle2#)*TempHDist#
					
				Case 3
					
					TempAngle1# = TempAngle1# + 180
					Return TempDrawX - Cos(TempAngle1#)*TempHDist#
					
				Case 4
					
					TempAngle2# = TempAngle2# + 270
					Return TempDrawX - Cos(TempAngle2#)*TempHDist#
					
			End Select
			
		EndIf
		
	Next
	
End Function

;;; &lt;summary&gt;Returns the Y coords on-screen of the specified image's specified corner&lt;/summary&gt;
;;; &lt;param name="TempImage"&gt;The handle of a 3D image&lt;/param&gt;
;;; &lt;param name="TempX"&gt;Y position on-screen that the image is drawn&lt;/param&gt;
;;; &lt;param name="TempCorner"&gt;The corner's number [0=center, 1=top-left, 2=bottom-left, 3=bottom-right, 4=top-left&lt;/param&gt;
;;; &lt;remarks&gt;&lt;/remarks&gt;
;;; &lt;returns&gt;The Image's corner's Y position on-screen&lt;/returns&gt;
;;; &lt;subsystem&gt;Blitz.Images.3D&lt;/subsystem&gt;
;;; &lt;example&gt;&lt;/example&gt;
Function ImageYCorner3D#(TempImage,TempY,TempCorner)
	
	Local TempDrawY = TempY
	Local TempHDist#
	Local TempAngle1#
	Local TempAngle2#
	Local TempDist#
	Local TempRotateY
	Local TempDistAngle#
	
	For Find.FR3DImage = Each FR3DImage
		
		If Find\Img3D = TempImage
			
			TempHDist# = Find\ImgW# ^ 2 + Find\ImgH# ^ 2
			TempHDist# = Sqr(TempHDist#)/2
			TempAngle1# = ASin(Find\ImgH#/2/TempHDist#) + -Find\Rotate#
			TempAngle2# = ACos(Find\ImgH#/2/TempHDist#) + -Find\Rotate#
			
			TempDist# = (Find\ImgW#/2 - ImageXHandle3D(Find\Img3D)) ^ 2 + (Find\ImgH#/2 - ImageYHandle3D(Find\Img3D)) ^ 2
			TempDist# = Sqr(TempDist#)
			
			If ImageXHandle3D(Find\Img3D) =&gt; Find\ImgW#/2 And ImageYHandle3D(Find\Img3D) &lt;= Find\ImgH#/2 Then
				TempRotateY = Find\Rotate#
			EndIf
			If ImageXHandle3D(Find\Img3D) =&gt; Find\ImgW#/2 And ImageYHandle3D(Find\Img3D) =&gt; Find\ImgH#/2
				TempRotateY = Find\Rotate#
			EndIf
			If ImageXHandle3D(Find\Img3D) =&lt; Find\ImgW#/2 And ImageYHandle3D(Find\Img3D) =&gt; Find\ImgH#/2
				TempRotateY = -Find\Rotate#
			EndIf
			If ImageXHandle3D(Find\Img3D) =&lt; Find\ImgW#/2 And ImageYHandle3D(Find\Img3D) =&lt; Find\ImgH#/2
				TempRotateY = -Find\Rotate#
			EndIf
			
			If TempDist# &lt;&gt; 0
				TempDistAngle# = ASin((Find\ImgH#/2 - ImageYHandle3D(Find\Img3D))/TempDist#) + TempRotateY
				TempDrawY = TempY + Sin(TempDistAngle#)*TempDist#
			EndIf
			
			Select TempCorner
					
				Case 0
					
					Return TempDrawY
					
				Case 1
					
					Return TempDrawY - Sin(TempAngle1#)*TempHDist#
					
				Case 2
					
					TempAngle2# = TempAngle2# + 90
					Return TempDrawY - Sin(TempAngle2#)*TempHDist#
					
				Case 3
					
					TempAngle1# = TempAngle1# + 180
					Return TempDrawY - Sin(TempAngle1#)*TempHDist#
					
				Case 4
					
					TempAngle2# = TempAngle2# + 270
					Return TempDrawY - Sin(TempAngle2#)*TempHDist#
					
			End Select
			
		EndIf
		
	Next
	
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
