<!DOCTYPE html><html lang="en" ><head ><title >Entity Recorder</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Records an entity's position and rotation to playback, language=bb, category=3D Graphics - Misc'><meta name='author' content='Rimmsy'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=2>3D Graphics - Misc</a>/Entity Recorder</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="423.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Entity Recorder by Rimmsy</td><td align="right">2002 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> my first code post! Hope someone likes it. </td></tr><tr ><td class="cell"><pre class="code">; Entity recorder

; by matt griffiths (Rims)
; use any way you like.
; any questions ca0mgr@hotmail.com

Const RECORD_FILE_VERSION$=&quot;1.00&quot;

Graphics3D 640,480,0,2
SetBuffer BackBuffer()

Type trail
	Field ent,life
End Type

; required global
Global recordFile=0
Global RECORD_state=0

Global leave_a_trail=1 ; 0 for no trail, 1 for.

; for freeLook(cam)
Global FL_Pitch#,FL_Yaw#,FL_Roll#,FL_XSpeed#,FL_YSpeed#,FL_ZSpeed#
Global camera=CreateCamera()
	MoveEntity camera,0,3,-5
	
	
	
	
	
; just make some objects to test with
plane=CreatePlane()
	EntityColor plane,255,0,0
	t=CreateTexture(32,32) : SetBuffer TextureBuffer(t)
	For i=0 To 31 Step 16
		For j=0 To 31 Step 16
			Color i*120,i*120,i*120
			Rect i,j,32/3,32/3
		Next
	Next
	SetBuffer BackBuffer()
	EntityTexture plane,t
	FreeTexture t	
c1=CreateCube()
	EntityColor c1,255,255,0
	PositionEntity c1,5,1,5	
c2=CopyEntity(c1)
	EntityColor c2,255,0,255
	PositionEntity c2,-5,1,-5
s1=CreateSphere()
	EntityColor s1,90,90,145
	EntityAlpha s1,0.4
	PositionEntity s1,-5,1,5
s2=CopyEntity(s1)
	PositionEntity s2,5,1,-5
	EntityBlend s2,3
	

; timing
gameFPS = 30 
framePeriod = 1000 / gameFPS 
frameTime = MilliSecs () - framePeriod
	

	
	
	
	
While KeyHit(1)=0
	Repeat 
		frameElapsed = MilliSecs () - frameTime 
	Until frameElapsed 
	frameTicks = frameElapsed / framePeriod 
	frameTween# = Float (frameElapsed Mod framePeriod) / Float (framePeriod) 
	
	For frameLimit = 1 To frameTicks 
		If frameLimit = frameTicks Then CaptureWorld 
		frameTime = frameTime + framePeriod
		
		; update everything...
		If KeyHit(19) Then startRecording(&quot;recording.rec&quot;)	; R
		If KeyHit(25) Then startPlayBack(&quot;recording.rec&quot;)	; P
	
		If KeyHit(57) Then stopRecordingAndPlayback()	; Space
	
		If RECORD_state=1 ; are we recording?
			record(camera)
		ElseIf RECORD_state=2 ; oh, we're playing back a file
			playback(camera) ; change camera to any object handle
		EndIf
	
		; handles all trail left behind
		If leave_a_trail
			For h.trail=Each trail
				If h\life &lt; 0
					; kill it
					FreeEntity h\ent
					Delete h
				Else
					h\life=h\life-1
					d#=(1.0/150.0)*Float(h\life) ; (current alpha is (1.0/Total Life)*current life)
					EntityAlpha h\ent,d
				EndIf
			Next
		EndIf
	
		FreeLook(Camera)
	
		UpdateWorld	
	Next
	
	RenderWorld	frametween
	
	Color 255,0,0
	If RECORD_state=1 Then Text 0,0,&quot;RECORDING&quot; ElseIf RECORD_state=2 Then Text 0,0,&quot;PLAYBACK&quot;
	
	Flip
Wend
End



Function record(ent)
	If ent=0 Then 
		DebugLog &quot;Entity does not exist&quot;
		Return	
	EndIf
	If recordFile=0 Then Return

	WriteFloat recordFile,EntityX(ent)
	WriteFloat recordFile,EntityY(ent)
	WriteFloat recordFile,EntityZ(ent)

	WriteFloat recordFile,EntityPitch(ent)
	WriteFloat recordFile,EntityYaw(ent)
	WriteFloat recordFile,EntityRoll(ent)
	
	If leave_a_trail
		If Rand(1,3)=1 ; only once every 3 cycles (on average)
			; optional... leaves a trail which fades out.
			t.trail=New trail
			t\ent=CreateCube()
			EntityColor t\ent,255,0,0
			ScaleEntity t\ent,0.5,0.5,0.5
			PositionEntity t\ent,EntityX(ent),EntityY(ent),EntityZ(ent)
			t\life=150
		EndIf
	EndIf
End Function


Function startRecording(file$)
	If recordFile &lt;&gt; 0 Then CloseFile recordFile
	recordFile=WriteFile(file)
	RECORD_state=1
	; write the recorder version
	WriteLine recordFile,RECORD_FILE_VERSION
End Function


Function startPlayBack(file$)
	recordFile=ReadFile(file)
	RECORD_state=2	
	; check the file and recorder versions
	r$=ReadLine(recordFile)
	If r &lt;&gt; RECORD_FILE_VERSION
		RuntimeError(&quot;Wrong file version (file: &quot;+r+&quot;. Rrecorder: &quot;+RECORD_FILE_VERSION+&quot;)&quot;)
		End
	EndIf
End Function


Function stopRecordingAndPlayback()
	If RECORD_state=1 Or RECORD_state=2 ; are we recording OR playing back?
		CloseFile recordFile
		RECORD_state=0
	EndIf	
End Function


Function playBack(entity)
	If entity=0 Then Return
	If recordFile=0 Then Return

	If Not Eof(recordFile)
		x#=ReadFloat(recordFile)
		y#=ReadFloat(recordFile)
		z#=ReadFloat(recordFile)
		
		PositionEntity entity,x,y,z
		
		New_pitch#=ReadFloat(recordFile)
		New_yaw#=ReadFloat(recordFile)
		New_roll#=ReadFloat(recordFile)
		
		RotateEntity entity,new_pitch,new_yaw,new_roll

		If leave_a_trail
			If Rand(1,3)=1 ; only once every 3 cycles on average
				; optional... leaves a trail which fades out.
				t.trail=New trail
				t\ent=CreateCube()
				EntityColor t\ent,255,0,0
				ScaleEntity t\ent,0.5,0.5,0.5
				PositionEntity t\ent,EntityX(entity),EntityY(entity),EntityZ(entity)
				t\life=150
			EndIf
		EndIf		
	Else
		CloseFile(recordFile)				
		RECORD_state=0
	EndIf	
End Function




; FreeLook function by Syntax Error
Function FreeLook(FL_Cam)
		FL_Pitch#=FL_Pitch#-(-MouseYSpeed()*0.02) : FL_Pitch#=FL_Pitch#/1.2
    FL_Yaw#=FL_Yaw#+-(MouseXSpeed()*0.02) : FL_Yaw#=FL_Yaw#/1.2
    MoveMouse (GraphicsWidth()/2,GraphicsHeight()/2)
    FL_ZSpeed#=FL_ZSpeed#+Float(KeyDown(17)-KeyDown(31))*0.12 : FL_ZSpeed#=FL_ZSpeed#/1.14;  w &amp; s
		FL_XSpeed#=FL_XSpeed#+Float(KeyDown(32)-KeyDown(30))*0.12 : FL_XSpeed#=FL_XSpeed#/1.14 ; a &amp; d
		FL_YSpeed#=FL_YSpeed#+Float(KeyDown(19)-KeyDown(33))*0.12 : FL_YSpeed#=FL_YSpeed#/1.14 ; r &amp; f
		FL_Roll#=(FL_Yaw#*1.1)-(FL_XSpeed#*2.3)
    MoveEntity FL_Cam,FL_XSpeed#,FL_YSpeed#+Abs(FL_Roll#*FL_XSpeed#)/50,FL_ZSpeed#
		Local cp#=EntityPitch(FL_Cam,True)+FL_Pitch#
		If cp&lt;-89 Then cp=-89
		If cp&gt;89 Then cp=89
		RotateEntity FL_Cam,cp,EntityYaw(FL_Cam)+FL_Yaw#,FL_Roll#
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Trader3564</td><td align="right"><font class=tiny>2007</font></td></tr></table></td></tr><tr ><td class="posttext"> Nice!<br>I have itsybitsy perfected the script and added a "PREFIX" feature to "TAG" your video file, in cases someone opens it with Notepad.<br><br>By default they will read "BLITZ ENTITY VID1.00" but you ma change that to whatever you want (like "MYGAME MOV1.2")<br><br><pre class=code>
Const rec_version$="1.00"
Const rec_prefix$="B3D ENTITY VID"

Global rec_file=0
Global rec_state=0

Function RecordEntity(e)
	WriteFloat rec_file,EntityX(e)
	WriteFloat rec_file,EntityY(e)
	WriteFloat rec_file,EntityZ(e)
	WriteFloat rec_file,EntityPitch(e)
	WriteFloat rec_file,EntityYaw(e)
	WriteFloat rec_file,EntityRoll(e)
End Function

Function StartRecording(filename$)
	If rec_file &lt;&gt; 0 Then CloseFile rec_file
	rec_file=WriteFile(filename)
	rec_state=1
	WriteLine rec_file,rec_prefix+rec_version
End Function

Function StartPlayback(filename$)
	rec_file=ReadFile(filename)
	rec_state=2	
	rv$=ReadLine(rec_file)
	rv=Right(rv,Len(rv)-Len(rec_prefix))
	If rv &lt;&gt; rec_version
		RuntimeError("Error in file "+FileName+". "+rec_prefix+" version is "+rv+", must be "+rec_version)
	EndIf
End Function

Function PlaybackEntity(e)
	If rec_file=0 Then Return

	If Not Eof(rec_file)
		x#=ReadFloat(rec_file)
		y#=ReadFloat(rec_file)
		z#=ReadFloat(rec_file)		
		pitch#=ReadFloat(rec_file)
		yaw#=ReadFloat(rec_file)
		roll#=ReadFloat(rec_file)

		PositionEntity e,x,y,z
		RotateEntity e,pitch,yaw,roll
	Else
		CloseRecFile()
	EndIf	
End Function

Function StopPlayback()
	CloseRecFile()
End Function

Function StopRecording()
	CloseRecFile()
End Function

Function CloseRecFile()
	If rec_file &lt;&gt; 0 Then 
		CloseFile rec_file
		rec_file=0
	End If
	rec_state=0
End Function
</pre> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Trader3564</td><td align="right"><font class=tiny>2007</font></td></tr></table></td></tr><tr ><td class="posttext"> xD i forget the part that should be used in the main loop:<br><br><pre class=code>
;PUT YOUR OTHER STUFF HERE

	If KeyHit(57) Then
		If rec_state=0 Then 
			StartRecording("recording1.rec")
		Else 
			StopRecording()
		End If
	End If
	If KeyHit(25) Then
		If rec_state=0 Then 
			StartPlayback("recording1.rec")
		Else
			StopPlayback()
		End If
	End If

	If rec_state=1
		RecordEntity(cam)
	ElseIf rec_state=2
		PlaybackEntity(cam)
	EndIf

;PUT YOUR OTHER STUFF HERE

	UpdateWorld
	RenderWorld

	If rec_state=1 Then 
		Text 10,50,"Recorder: RECORDING"
	ElseIf rec_state=2 Then 
		Text 10,50,"Recorder: PLAYING"
	Else	
		Text 10,50,"Recorder: OFF"
	End If
 
	Flip

;PUT YOUR OTHER STUFF HERE
</pre> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >N</td><td align="right"><font class=tiny>2007</font></td></tr></table></td></tr><tr ><td class="posttext"> You really like digging up this old code, don't you? O_o <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rimmsy</td><td align="right"><font class=tiny>2007</font></td></tr></table></td></tr><tr ><td class="posttext"> Hey, I'm not moaning. Thanks dude! <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
