<!DOCTYPE html><html lang="en" ><head ><title >openGL vertex/fragment program</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='example of using 1.5 compatible shaders, language=bmx, category=3D Graphics - Misc'><meta name='author' content='Warner'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=2>3D Graphics - Misc</a>/openGL vertex/fragment program</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2831.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >openGL vertex/fragment program by Warner</td><td align="right">2011 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Loads and executes simple fp1.0/vp1.0 shader programs in OpenGL. They should be compatible with INTEL G41 chips.<br><br>Commands can be found here: <a href="http://www.renderguild.com/gpuguide.pdf" target="_blank">http://www.renderguild.com/gpuguide.pdf</a><br>Quite convenient once you know how to read the table.<br>As a reference, I saved this one as well:<br><a href="http://www.opengl.org/registry/specs/NV/fragment_program.txt" target="_blank">http://www.opengl.org/registry/specs/NV/fragment_program.txt</a><br><a href="http://www.opengl.org/registry/specs/NV/vertex_program.txt" target="_blank">http://www.opengl.org/registry/specs/NV/vertex_program.txt</a><br>It shows in code examples what the opcodes do internally. </td></tr><tr ><td class="cell"><pre class="code">SuperStrict

Global llen% = 0

SetGraphicsDriver GLGraphicsDriver()
GLGraphics 800,600,0,0,GRAPHICS_BACKBUFFER|GRAPHICS_DEPTHBUFFER 

Global g_pixelProgramID%
Global g_vertexProgramID%
Global time#

'array to hold two triangles that form a square
Global g_triVertices:Float[] = ..
[..
     0.0,0.0,  0.0,0.0,-1.0, -1.0,-1.0, -4.0 ,..
     1.0,0.0,  0.0,0.0,-1.0,  1.0,-1.0, -4.0 ,..
     1.0,1.0,  0.0,0.0,-1.0,  1.0, 1.0, -4.0 ,..
..
     0.0,0.0,  0.0,0.0,-1.0, -1.0,-1.0, -4.0 ,..
     1.0,1.0,  0.0,0.0,-1.0,  1.0, 1.0, -4.0 ,..
     0.0,1.0,  0.0,0.0,-1.0, -1.0, 1.0, -4.0..
]

init() 'setup OPENGL
initShader() 'load and compile shaders

Repeat

	render() 'render square with shaders

Until KeyHit(27) 'ESC=exit

End

'
' init() - Setup OPENGL
'
Function init()

	glewInit()

	glEnable( GL_DEPTH_TEST ) 'enable z-buffer
	glEnable(GL_CULL_FACE) 'enable cull
	glCullFace(GL_BACK) 'set cullmode

	glClearColor( 0.85, 0.7, 0.5, 1.0 ) 'cameraclscolor

	glMatrixMode( GL_PROJECTION ) 'choose projection matrix
	glLoadIdentity() 'reset it
	gluPerspective( 45.0, 640.0 / 480.0, 0.1, 100.0 ) 'create camera view
	
End Function

'
' Load and compile shader
'
Function initShader()

	'
	' Check capabilities
	'
	
	Local Extensions$ = String.FromCString(Byte Ptr(glGetString(GL_EXTENSIONS)))
	Local VPSupport% = Extensions.Find("GL_ARB_vertex_program") &gt; -1
	Local FPSupport% = Extensions.Find("GL_ARB_fragment_program") &gt; -1
	If Not(VPSupport)
		Print "No Vertex Program support."
		End
	End If
	
	If Not(FPSupport)
		Print "No Fragment Program support."
		End 
	End If	

	'
	' Create the vertex program...
	'

    glGenProgramsARB( 1, Varptr g_vertexProgramID )
    glBindProgramARB( GL_VERTEX_PROGRAM_ARB, g_vertexProgramID )
    
    Local shader_assembly$ = readShaderData() 'read from data (DefData) below

    glProgramStringARB( GL_VERTEX_PROGRAM_ARB, GL_PROGRAM_FORMAT_ASCII_ARB, Len(shader_assembly), shader_assembly.ToCString() )
	
    If ( glGetError() &lt;&gt; GL_NO_ERROR )
	Print "ERROR in vert.shad."
	End 
    End If

	'
	' Create the fragment program...
	'

	' Create the vertex program
    glGenProgramsARB( 1, Varptr g_pixelProgramID )
    glBindProgramARB( GL_FRAGMENT_PROGRAM_ARB, g_pixelProgramID )
    
    shader_assembly = readShaderData() 'read shader from data (DEFDATA) below 

    glProgramStringARB( GL_FRAGMENT_PROGRAM_ARB, GL_PROGRAM_FORMAT_ASCII_ARB, Len(shader_assembly), shader_assembly.ToCString() )

    If ( glGetError() &lt;&gt; GL_NO_ERROR )
     Print "ERROR in pix.shad."
     End 
    End If

End Function

'
' readShaderData() - read shader from data (DEFDATA) below
'
Function readShaderData$()
	
	Local out$
	
	Repeat
	
		Local l$
		ReadData l$
		If l$ = "---" Exit
		
		out$ = out$ + l$ + "~r~n"
		
	Forever
	
	Return out$
	
End Function

'
' render() - render a square using shaders
'
Function Render()

     glClear( GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT )
	 
     glEnable( GL_VERTEX_PROGRAM_ARB ) 'enable shaders
	glBindProgramARB( GL_VERTEX_PROGRAM_ARB, g_vertexProgramID )
	
     glEnable( GL_FRAGMENT_PROGRAM_ARB )
	glBindProgramARB( GL_FRAGMENT_PROGRAM_ARB, g_pixelProgramID )

	time :+ 0.001 'counter (global)
		
	Local ff#[] = New Float[4] 'create array
	ff[0] = time 'insert into first value of array
	ff[1] = 0
	ff[2] = 0
	ff[3] = 0
     glProgramLocalParameter4fvARB(GL_FRAGMENT_PROGRAM_ARB,0,ff) 'pass (4-float) array to fragment shader (/program)
	
	glInterleavedArrays( GL_T2F_N3F_V3F, 0, g_triVertices ) 'set triangle array
	glDrawArrays( GL_TRIANGLES, 0, g_triVertices.length/8 ) 'draw triangle array

	glDisable( GL_FRAGMENT_PROGRAM_ARB ) 'disable shaders
	glDisable( GL_VERTEX_PROGRAM_ARB )

	Flip
End Function


#ShaderData

DefData "!!ARBvp1.0" 'calculation pro vertex
DefData ""
DefData "# Constant Parameters"
DefData "PARAM mvp[4] = { state.matrix.mvp }; # Model-view-projection matrix" 'read default openGL renderview matrix
DefData ""
DefData "# Per-vertex inputs"
DefData "ATTRIB inPosition = vertex.position;" 'read position
DefData "ATTRIB inColor    = vertex.color;" 'read color
DefData "ATTRIB inTexCoord = vertex.texcoord;" 'read uv coordinate
DefData ""
DefData "# Per-vertex outputs"
DefData "OUTPUT outPosition = result.position;" 'set output xyz
DefData "OUTPUT outColor    = result.color;" 'set output color
DefData "OUTPUT outTexCoord = result.texcoord;" 'set output uv
DefData ""
DefData "DP4 outPosition.x, mvp[0], inPosition;   # Transform the x component of the per-vertex position into clip-space"
DefData "DP4 outPosition.y, mvp[1], inPosition;   # Transform the y component of the per-vertex position into clip-space"
DefData "DP4 outPosition.z, mvp[2], inPosition;   # Transform the z component of the per-vertex position into clip-space"
DefData "DP4 outPosition.w, mvp[3], inPosition;   # Transform the w component of the per-vertex position into clip-space"
DefData ""
DefData "MOV outColor, inColor;       # Pass the color through unmodified"
DefData "MOV outTexCoord, inTexCoord; # Pass the texcoords through unmodified"
DefData ""
DefData "END"
DefData "---"

DefData "!!ARBfp1.0" 'calculation pro pixel/fragment
DefData "PARAM c = program.local[0];" 'Data passed from line 125 (array Local ff#)
DefData "TEMP colorX;" 'internal variable
DefData ""
DefData "MOV colorX, fragment.texcoord[0].x;" 'read u-coordinate into colorX
DefData "ADD colorX, colorX, fragment.texcoord[0].y;" 'add v-coordinate to colorX
DefData "ADD colorX, colorX, c.x;" 'add input from main program (time counter from BMX code)
DefData "MUL colorX, colorX, 25.0;" 'multiply by 25
DefData "FRC colorX, colorX;" 'leave only fractional part (x MOD 1)
DefData "MUL colorX, colorX, 2.0;" 'multiply by two
DefData "FLR colorX, colorX;" 'leave only integer part (x = floor(x))
DefData ""
DefData "MOV result.color, colorX;" 'move colorX into result color (rgba)
DefData ""
DefData "MOV result.color.a, 1;" ' set alpha to 1.0
DefData ""
DefData "END"
DefData "---"</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamRedwoods</td><td align="right"><font class=tiny>2011</font></td></tr></table></td></tr><tr ><td class="posttext"> I like that the shader is DefData'd into the code, and that you are using ARB commands, which I should tell you that ARB Shaders work on INTEL G41 chips.<br><br>NOTE: your above code needed a slight tweak to work for me:<br><pre class=code>
Function readShaderData$()
	
	Local out$
	
	Repeat
	
		Local l$
		ReadData l$
		If l$ = "---" Exit
		
		out$ = out$ + l$ + "~r~n"
		
	Forever
	
	Return out$
	
End Function
</pre><br><br><br>Furthermore, you can get better error descriptions by adding this to the two error calls:<br><pre class=code>
Local error_out:String = String.FromCString(glGetString(GL_PROGRAM_ERROR_STRING_ARB))
Print( error_out )
</pre> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>2011</font></td></tr></table></td></tr><tr ><td class="posttext"> I see, thanks. Updated. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
