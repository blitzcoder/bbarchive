<!DOCTYPE html><html lang="en" ><head ><title >Drawing 3d</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Some 3d drawing functions for points, lines and segments, language=bb, category=3D Graphics - Misc'><meta name='author' content='Giano'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=2>3D Graphics - Misc</a>/Drawing 3d</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="209.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drawing 3d by Giano</td><td align="right">2002 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Here you can find 3 methods <br>All of them must use a camera, and if are out of the camera's field of view, can't be drawed<br><br>1.plot3d(x#,y#,z#, camera)<br>Plot a point in 3d space<br>2.Line3d(x0#,y0#,z0#,x1#,y1#,z1#, camera)<br>Draw a line in 3d space <br>If the line extremes are out of camera fov then the line isn't drawed<br>3.segments3d(x0#,y0#,z0#,x1#,y1#,z1#, camera, segments=1)<br>Draw a segmented line in 3d space...better than line3d if the extremes are out of fov.<br><br>Remember to draw after RenderWorld<br>Ciao<br><br>Gianluca Sclano </td></tr><tr ><td class="cell"><pre class="code">;*********************************************************************************
;*** Drawing 3d with Blitz
;*** Remember to use this functions after Renderworld
;*** Sometimes becouse extremes points of lines are out of the camera's
;*** field of view, the lines aren't drawed...Use segments that draws more
;*** then a line from 2 points.
;*** Hold mouse to move  Happyness ;) 
;*********************************************************************************
;*** 2002 By Gianluca SCLANO (BitmaniaK)
;*** sclano@hotmail.com
;*** gianluca.sclano@tiscalinet.it
;*********************************************************************************


;*********************************************************************************
;*** Plot a 3d Pixel
;*********************************************************************************
Function plot3d(x#,y#,z#, camera)
	CameraProject camera,x#,y#,z# 
	If ProjectedZ() Then Plot ProjectedX# ( ) ,ProjectedY# ( )
End Function	

;*********************************************************************************
;*** Draw a fast single 3d line 
;*********************************************************************************
Function Line3d(x0#,y0#,z0#,x1#,y1#,z1#, camera)
	CameraProject (camera,x0,y0,z0)
	px0#=ProjectedX# ( ) 
	py0#=ProjectedY# ( ) 
	pz0#=ProjectedZ# ( ) 

	CameraProject (camera,x1,y1,z1)
	If ProjectedZ#( ) &gt;0 And pz0&gt;0 Then Line px0,py0,ProjectedX# ( ) ,ProjectedY# ( ) 
End Function

;*********************************************************************************
;*** Draw a multi segments 3d line 
;*********************************************************************************
Function segments3d(x0#,y0#,z0#,x1#,y1#,z1#, camera, segments=1)

	stepx#=(x1-x0)/segments
	stepy#=(y1-y0)/segments
	stepz#=(z1-z0)/segments
	x1 = x0+stepx#
	y1 = y0+stepy#
	z1 = z0+stepz#

	For t= 1 To segments
		CameraProject (camera,x0,y0,z0)
		px0#=ProjectedX( ) 
		py0#=ProjectedY( ) 
		pz0#=ProjectedZ( ) 

		CameraProject (camera,x1,y1,z1)
		If ProjectedZ#( )&gt;0 And pz0&gt;0 Then Line px0,py0,ProjectedX#( ) ,ProjectedY#( ) 
		x0 = x1 : y0 = y1 : z0 = z1
		x1 = x0+stepx# : y1 = y0+stepy# : z1 = z0+stepz#		
	Next
End Function

;*********************************************************************************
;*** THE TEST :)
;*********************************************************************************
Type entity3D
	Field x#,y#,z#
	Field dx#,dy#,dz#
	Field counter
End Type

Type point3D
	Field x#,y#,z#
	Field dy#
End Type


Function testDraw3d(points=100)
	Graphics3D 800,600,16
	SetBuffer BackBuffer()
	
	camera=CreateCamera()
	light=CreateLight()
	PositionEntity camera,0,50,-100
	RotateEntity camera,30,0,0   
	cube=CreateCube()
	For t=1 To 15
		m=CopyEntity(cube)
		PositionEntity m,Rnd(-50,50),Rnd(-30,30),Rnd(-50,50)
		EntityColor m,Rand(0,255),Rand(0,255),Rand(0,255)
	Next
;*** Create some types for lines extremes	
	For t=1 To points
		e.entity3D = New entity3D
		e\x=Rnd(-10,10)
		e\y=Rnd(-10,10)
		e\z=Rnd(-10,10)
		e\dx=Rnd(-3,3)
		e\dy=Rnd(-3,3)
		e\dz=Rnd(-3,3)
		e\counter = 50 + Rand(20)
	Next
;*** Create some types for points
	For t=1 To points
		p.point3D = New point3D 
		p\x=Rnd(-50,50)
		p\y=Rnd(-10,10)
		p\z=Rnd(-50,50)
		p\dy = Rnd(.1,1)
	Next
	colorCycle=0

;*** The loop begins
   While Not KeyHit(1)
   
   	mx#=mx#+MouseXSpeed()*.3
   	mz#=mz#-MouseYSpeed()*.3
   	my#=my#+MouseZSpeed()*5
   	MoveMouse 400,300
   	
   	If MouseDown(1)
		PositionEntity camera,EntityX(camera)+mx#,EntityY(camera)+my#,EntityZ(camera)+mz#
	Else
		PositionEntity cube,EntityX(cube)+mx#,EntityY(cube)+my#,EntityZ(cube)+mz#
   	End If
   	
   	;reset mouse vars
   	mx#=0 : mz#=0 : my#=0
   	
   	UpdateWorld
;**** Update lines
   	For e.entity3D  = Each entity3D 
		e\x=e\x+e\dx
		e\y=e\y+e\dy
		e\z=e\z+e\dz
		e\counter = e\counter -1
		If e\counter &lt;0 
			e\dx=Rnd(-3,3)
			e\dy=Rnd(-3,3)
			e\dz=Rnd(-3,3)
			
			e\counter = 50 + Rand(20)		
		End If
	Next
;*** Update Points	
	For p.point3D = Each point3D 
		p\y = p\y+p\dy
		If p\y&gt;50 
			p\dy=-Abs(p\dy)
		Else If p\y&lt;-50
			p\dy=Abs(p\dy)
		End If
	Next
;*** RENDER	
   	RenderWorld
;*** NOW USE THE FUNCTIONS

;*** Drawing functions
	colorCycle=(colorCycle+1) Mod 256
;*** 3d lines
   	For e.entity3D  = Each entity3D 
		If e&lt;&gt;Last entity3D
			ne.entity3D = After e
		Else
			ne.entity3D = First entity3D
		End If
		Color 255-colorCycle,colorCycle,255-colorCycle
		line3d(e\x,e\y,e\z,ne\x,ne\y,ne\z, camera)
	Next
	
;*** 3d Points	
	For p.point3D = Each point3D 
		Color Rand(255),255,255
		plot3d(p\x,p\y,p\z, camera)
	Next
;*** 3d segments	
	Color 255-colorCycle,255-colorCycle,255
	segments3d(0,0,0,EntityX(cube),EntityY(cube),EntityZ(cube),camera,10)
   	Flip
   Wend
;*** end loop
End Function

;*********************************************************************************
testDraw3d(50)
End
;*********************************************************************************</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
