<!DOCTYPE html><html lang="en" ><head ><title >Displacement Mapping</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Using a texture to modify the height of a mesh's polygons., language=bb, category=3D Graphics - Misc'><meta name='author' content='Ziltch'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=2>3D Graphics - Misc</a>/Displacement Mapping</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="465.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Displacement Mapping by Ziltch</td><td align="right">2002 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Here is a displacement mapping function and some routines to show it off.<br><br>Function displace(mesh,texture,texsize#=256,amp#=1,wrap=false)<br><br>Mesh = the entity you wish to modify<br>The texture needs to be square ie 256=256x256,512=512x512<br>amp = The heightmap.<br>The wrap command is my attempt at handling spheres, etc.<br><br>You need lots of vertices in a object for this to look good.  So it not for the polygon shy!<br><br>In the example code press the space bar to bumpmap and create simple random landscapes.<br><br>The bumpmap function also needs the numcolor and gettetxcol functions if you with to use it in your own code.<br><br>Have fun! </td></tr><tr ><td class="cell"><pre class="code">; Vetex displacment mapping function and example code
; ADAmor Ziltch 2002

Graphics3D 800,600
SetBuffer BackBuffer()

light=CreateLight(2)
PositionEntity light,-1000,50,-10
LightColor     light,255,255,40
light2=CreateLight(2)
PositionEntity light2,1000,50,-10
LightColor     light,55,55,255
light3=CreateLight(2)
PositionEntity light2,0,50,1000
LightColor     light,220,210,55
AmbientLight   125,125,7

Global you = CreatePivot()
PositionEntity you,0,00,-300
cam = CreateCamera(you)
camerarange    cam,1,5000

.createscene

;--cube grid
Global cubegrid = createsquare(51)
PositionEntity cubegrid,0,300,1000
ScaleEntity    cubegrid,2000,200,2000
entitycolor    cubegrid,50,50,150
rotateEntity   cubegrid ,180,0,0

cubegridtex =  gridtex(256,32,12, 70,230,10, 120,50,250)
EntityTexture  cubegrid,cubegridtex,0,1

;--bump plane
Global bumpplane = createsquare(100)
PositionEntity bumpplane ,0,-500,1000
ScaleEntity    bumpplane ,2000,200,2000
entitycolor    bumpplane ,150,50,150

bumptex=CreateTexture(256,256)
spot(bumptex,256,25)
EntityTexture bumpplane,bumptex,0,0

;--bump ball
Global bumpball = createsphere(30)
PositionEntity bumpball ,0,0,1000
ScaleEntity    bumpball ,90,90,90
entitycolor    bumpball ,150,50,150

entityTexture bumpball,cubegridtex,0,0

PointEntity you,cubegrid
rotateentity you,15,0,0

setfont LoadFont("Arial",14,False,False,False)

BumpAlready = false
While Not KeyHit(1)

    spd =  MouseZ()+2  ; mousewheel is speed
    moveyou(spd)

    If KeyHit(57)then
		  if (not BumpAlready) Then   ;space for bumpmap
        displace(cubegrid,cubegridtex,256,20)
        displace(bumpplane,bumptex,256,20)
        displace(bumpball,cubegridtex,256,5)
        BumpAlready = true
      else
  		  freeentity cubegrid
	  	  freeentity bumpplane
  		  freeentity bumpball
  		  goto createscene
      end if
    end if

    TurnEntity cubegrid,0,.1,0
    TurnEntity bumpball,-.1,.1,.1
    UpdateNormals cubegrid
 
    RenderWorld
    color 250,250,100
    text 10,10," Hit the SPACEBAR for displacment. Arrows,home,end,endpgup,pgdwn with ctrl for movement. Mouse wheel for speed.  W for Wireframe.  Look in code for more keys"
    Flip
Wend


Function displace(mesh,texture,texsize#=256,amp#=1,wrap=false)

  For sc = 1 To CountSurfaces(mesh)

    surf = GetSurface(mesh,sc)
    If surf = 0 Then
      RuntimeError "Cant find surface to displace with"
      End
    End If
    maxvert = CountVertices(surf)

    For vc = 0 To maxvert-1

       bx#  = VertexX(surf,vc)
       by#  = VertexY(surf,vc)
       bz#  = VertexZ(surf,vc)
       bnx# = VertexNX(surf,vc)
       bny# = VertexNY(surf,vc)
       bnz# = VertexNZ(surf,vc)
       bu#  = VertexU(surf,vc)
       bv#  = VertexV(surf,vc)
       
       If wrap Then
         If (bu = 1)  Then bu = 0
         If (bv =0)  Then bu = 0 : bv = 0
         If (bv =1)  Then bu = 0 : bv = 1
       End If
       
       tx# = bu*texsize
       ty# = bv*texsize
       gettexcol(Texture,tx,ty)
       Cr#=numcolR      ; based off red channel  as this seems to look best

       If (cr &gt; 0) Then
         bxx# = bx + bnx * (Cr/255) * (amp/10)
         byy# = by + bny * (Cr/255) * (amp/10)
         bzz# = bz + bnz * (Cr/255) * (amp/10)
         VertexCoords surf,vc,bxx,byy,bzz
       End If
    Next
  Next
  UpdateNormals mesh
End Function

Global numcolR#,numcolG,numcolB
Function numcolor(num#)
;convert number to r g b values
  numcolR=num  Shr 16 And %11111111
  numcolG=num Shr 8 And %11111111
  numcolB=num And %11111111
End Function

Function gettexcol(tex,x,y)
; get results from numcolR, numcolG, numcolB
  SetBuffer TextureBuffer(tex)
  LockBuffer TextureBuffer(tex)
  numcolor(ReadPixelFast(x,y))
  UnlockBuffer TextureBuffer(tex)
  SetBuffer BackBuffer()
End Function




;------ VVVV  these functions are just to create an example bdisplacement map    VVVV

Function createsquare(segs#=5,parent=0)

    mesh=CreateMesh( parent )
    surf=CreateSurface( mesh )

    l# =-.5
    b# = -.5
    tvc= 0

		;create all the vertices first
    Repeat
		  u# = l + .5
		  v# = b + .5
      AddVertex surf,l,0,b,u,v
      tvc=tvc + 1
      l = l + 1/segs
      If l &gt; .5 Then
        l = -.5
        b = b + 1/segs
      End If
    Until b &gt; .5

    vc# =0

    ;create polys
    vc# =0
    Repeat

      AddTriangle (surf,vc,vc+segs+1,vc+segs+2)
      AddTriangle (surf,vc,vc+segs+2,vc+1)

      vc = vc + 1
      tst# =  ((vc+1) /(segs+1)) -Int ((vc+1) /(segs+1))

      If (vc &gt; 0) And (tst=0) Then
        vc = vc + 1
      End If

    Until vc=&gt;tvc-segs-1
    UpdateNormals mesh
    Return mesh

End Function


Function moveyou(spd=1)

    If  KeyDown( 205 ) Then
      If KeyDown(157) Then
        MoveEntity you,spd,0,0    ; ctrl -&gt; straff right
      Else 
       TurnEntity you,0,-2,0      ; -&gt; turn right
      End If
    End If
    If  KeyDown( 203 ) Then
      If KeyDown(157) Then
        MoveEntity you,-spd,0,0   ; ctrl &lt;- straff left
      Else 
        TurnEntity you,0,2,0      ; &lt;- turn left
      End If
    End If
    If  KeyDown( 199 ) Then
      If KeyDown(157) Then
        TurnEntity you,0,0,2      ; ctrl home roll left
      Else
        TurnEntity you,-2,0,0     ; home  pitch left
      End If
    End If

    If  KeyDown( 207 ) Then 
      If KeyDown(157) Then
        TurnEntity you,0,0,-2     ; ctrl end roll right
      Else
        TurnEntity you,2,0,0      ; end roll right
      End If
    End If

    If  MouseDown(1) Or KeyDown( 200 )  Then MoveEntity you,0,0,spd  ; up arrow forward
    If MouseDown(2) Or KeyDown( 208 ) Then MoveEntity you,0,0,-spd   ; down arrow back
    If KeyDown(201) Then MoveEntity you,0,spd,0  ; pgup raise
    If KeyDown(209) Then MoveEntity you,0,-spd,0 ; pgdown lower

    If KeyHit( 17) Then wf = Not wf : WireFrame wf  ;w for wireframe

    If KeyHit(68) Then  ; F10 for snapshot
       If sscnt = 0 Then sscnt = 1000
       sscnt = sscnt + 1
       SaveBuffer(FrontBuffer(),"snapshot"+Right(Str(sscnt),3)+".bmp")
    End If

    If KeyHit(36) Then joy = Not joy   ; j key for joystick
    If joy Then
      jyaw#=-JoyXDir()
      jpitch#=-JoyYDir()
      TurnEntity you,jpitch,jyaw,0
      If JoyDown(7) Then MoveEntity you,0,0,spd
      If JoyDown(8) Then MoveEntity you,0,0,-spd

    End If
    
End Function
Global WF,joy


Function  spot(tex,texsize,numspots,clstrue=true)

	SetBuffer  TextureBuffer(tex)

  if clstrue then
	  color 0,0,0
    Rect 0,0,texsize,texsize,1
  end if
	
	lockbuffer TextureBuffer(tex)
  for sc = 1 to numspots
    spotsize=Rand(25,64): if spotsize &gt;60 then spotsize=spotsize*2
    rx=Rand(spotsize+1,texsize-spotsize)
    ry=Rand(spotsize+1,texsize-spotsize)
    For a# = 1 To spotsize-1 step 1
		  i# = 0
			while i &lt; 360
			  sx=sin(i)*a+rx
			  sy=cos(i)*a+ry
				i = i + .5
			  rc=readpixel(sx,sy,texturebuffer(tex))
			
  			numcolor(rc)
  		  nr#=(abs (sin((a-1)/4))*(spotsize-a)*2+numcolR*.9)mod 255

        nc=Colornum(nr,numcolG+1,numcolB+1)
        writepixelfast sx,sy,nc
      wend

    Next
	next

	unlockbuffer TextureBuffer(tex)
	SetBuffer BackBuffer()
	
End Function



Function colornum(r,g,b)
 return ((r Shl 16) + (g Shl 8) + b)
End Function



Function gridtex(s=256,st=128,width=16,colr=0,colg=0,colb=0,backcolr=0,backcolg=0,backcolb=0)
  tex=CreateTexture(s,s)
  SetBuffer TextureBuffer(tex)
  Color backcolr,backcolg,backcolb
  Rect 0,0,s,s,1
  a = 0
  i#=s/260
  Repeat
    Color colr,colg,colb
    For w= 0 To width-1
      Line a+w,0,a+w,s
      Line 0,a+w,s,a+w
    Next
    a = a + st
  Until a =&gt; s
  SetBuffer BackBuffer()
  Return tex

End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
