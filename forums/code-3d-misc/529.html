<!DOCTYPE html><html lang="en" ><head ><title >Texture-HUD</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='a texture based HUD, language=bb, category=3D Graphics - Misc'><meta name='author' content='tobiasm767'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=2>3D Graphics - Misc</a>/Texture-HUD</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="529.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Texture-HUD by tobiasm767</td><td align="right">2002 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> a texture based HUD </td></tr><tr ><td class="cell"><pre class="code">; #################################
; ###        TEXTURE-HUD        ###
; ###            V1.0           ###
; #################################
;
;	(c) 2002 Tobias Müller
;	lanpage@freenet.de / www.LAN-Byte.de.gs
;
;
;
;	hud_create( parent, CamZoom#=1.0 ) - create the hud. you MUST set camera as parent
;	hud_Text( X, Y, Text, red=255, green=255, blue=255, delete_Text_after_render=0, x_center=0 )
;	hud_clear()	- delete all hud-text and render the hud
;	hud_del_text() - delete only the hud-text
;	hud_delete() - delete the hud
;	hud_render() - render the hud


;set the hud texture size (bigger texture -&gt; smaller text)
Const tex_size = 256


Global hud_sprite
Global hud_texture
Global hud_redraw=0


Type hud_Text
	Field X%
	Field Y%
	Field Msg$
	Field r
	Field g
	Field b
	Field del
	Field x_center
End Type




;run example
example()




Function hud_create( parent , CamZoom#=1.0 )


	hud_sprite = CreateSprite( parent )
	SpriteViewMode( hud_sprite,2)

	hud_texture = CreateTexture(tex_size,tex_size,1)


	EntityTexture( hud_sprite, hud_texture)

	

	;3:4 = 0.75 
	ScaleSprite( hud_sprite, 1, 0.75)
	MoveEntity( hud_sprite,0,0, 1 * CamZoom )

	EntityOrder( hud_sprite, -1000 )
	EntityBlend(hud_sprite,3)
	

End Function




Function hud_Text( X, Y, msg$, r=255, g=255, b=255, del=0,x_center=0 )

	a.Hud_Text = New Hud_Text 
	a\X = X
	a\Y = Y
	a\msg = msg
	a\r = r
	a\g = g
	a\b = b
	a\del = del
	a\x_center = x_center

	hud_redraw=1

End Function



Function hud_clear()

		For a.Hud_text = Each Hud_Text
			Delete a
		Next

	SetBuffer TextureBuffer( hud_texture )
		;black background
		Color 0,0,0
		Rect 0,0,tex_size, tex_size,1

		Flip(False)
	SetBuffer(BackBuffer())


End Function





Function hud_del_text()

	For a.Hud_text = Each Hud_Text
		Delete a
	Next


End Function

Function hud_delete()

	hud_del_text()

	FreeTexture(hud_texture)
	FreeEntity(hud_sprite)

End Function






Function hud_render()

	If hud_redraw=1 Then

		SetBuffer TextureBuffer( hud_texture )	
			;black background
			Color 0,0,0
			Rect 0,0,tex_size, tex_size,1
	
			;write text
			For a.Hud_Text = Each Hud_text
				Color( a\r, a\g, a\b )
				Text( a\X, a\Y, a\msg, a\x_center )
		
				If a\del=1 Then temp=1:Delete a		
			Next

			Flip(False)
	
		SetBuffer BackBuffer()
	
		If temp = 1 Then
			;a hud text were deleted -&gt; render the hud again
			hud_redraw=1
		Else
			hud_redraw=0
		EndIf
	EndIf

End Function











Function example()

	Graphics3D(800,600,0,2)
	SetBuffer BackBuffer()
	
	camera=CreateCamera()
	CameraClsColor(camera,34,85,136)
	
	hud_create(camera)
	hud_text(tex_size/2,tex_size/2,"HUD-TEST",255,0,0,False,True)

	;something for the background
	cube=CreateCube()
	EntityColor(cube,64,64,64)
	MoveEntity(cube,0,0,4)

	light=CreateLight()
	MoveEntity(light,10,3,1)

	While(Not KeyHit(1))
		hud_render()
		
		hud_text(0,0,CurrentTime(),255,255,255,True,False)
		
		TurnEntity(cube,1,1,1)
	
	UpdateWorld()
	RenderWorld()
	Flip
	Wend

End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
