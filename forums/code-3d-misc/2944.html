<!DOCTYPE html><html lang="en" ><head ><title >Record, Write, Read, Play, the position and rotation of an entity</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='An example on how to have a replay in a video game, language=bb, category=3D Graphics - Misc'><meta name='author' content='RemiD'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=2>3D Graphics - Misc</a>/Record, Write, Read, Play, the position and rotation of an entity</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2944.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Record, Write, Read, Play, the position and rotation of an entity by RemiD</td><td align="right">2012 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> An example on how to have a replay in a video game </td></tr><tr ><td class="cell"><pre class="code">;Record, Write, Read, Play, the position and rotation of an entity
;F1 to switch from Idle mode to Record mode and from Record mode to Idle mode
;F2 to switch from Idle mode to Play mode and from Play mode to Idle mode
;The characters move with their AI when in Idle Mode or in Record Mode, and they move with the Replay data when in Play mode.

Graphics3D(800,600,32,2)

HidePointer()

SeedRnd(MilliSecs())

Camera = CreateCamera()
CameraRange(Camera,0.1,1000)
PositionEntity(Camera,0,0,0)

GroundMesh = CreateCube()
ScaleEntity(GroundMesh,64.0/2,0.1/2,64.0/2)
PositionEntity(GroundMesh,64.0/2,-0.1/2,64.0/2)
EntityColor(GroundMesh,025,075,025)

CharacterXMesh = CreateCube()
ScaleMesh(CharacterXMesh,0.5/2,1.8/2,0.25/2)
PositionMesh(CharacterXMesh,0,1.8/2,0)
HideEntity(CharacterXMesh)

Dim CharacterMesh(5)
Global CharactersCount% = 0

For CId% = 1 To 5
 CharactersCount% = CharactersCount% + 1
 CId% = CharactersCount%
 CharacterMesh(CId%) = CopyEntity(CharacterXMesh)
 PositionEntity(CharacterMesh(CId%),Rnd(0,64),0,Rnd(0,64))
 RotateEntity(CharacterMesh(CId%),0,Rnd(-180,180),0)
 EntityColor(CharacterMesh(CId%),Rand(000,255),Rand(000,255),Rand(000,255))
Next

Const Idle% = 0
Const Record% = 1
Const Play% = 2

Global TrackingMode% = Idle%
Global File
Global FileName$ = "Tracking.dat"

PositionEntity(Camera,32,32,-16)
RotateEntity(Camera,45,0,0)

SunLight = CreateLight(1)
LightColor(SunLight,255,255,255)
PositionEntity(SunLight,32,1024,-1024)
RotateEntity(SunLight,45,0,0)
AmbientLight(125,125,125)

Repeat

 Cls()
 
 CharactersUpdate()
 
 TrackingModeUpdate()

 SetBuffer(BackBuffer())
 RenderWorld()

 Text(0,0,"Triangles : "+TrisRendered())
 Text(0,20,"LoopTime% : "+LoopTime%)
 Text(0,40,"TrackingMode% : "+TrackingMode%)

 Flip(1)

 ;Calculates the time of a loop  
 EndTime% = MilliSecs()    
 LoopTime% = EndTime% - StartTime%  
 StartTime% = MilliSecs()

Until(KeyDown(1)=True)

Function CharactersUpdate()
 If( TrackingMode% = Idle% Or TrackingMode% = Record%)
  For CId% = 1 To CharactersCount%
   If( EntityX(CharacterMesh(CId%)) &gt; 0 And EntityX(CharacterMesh(CId%)) &lt; 64 And EntityZ(CharacterMesh(CId%)) &gt; 0 And EntityZ(CharacterMesh(CId%)) &lt; 64 )
    MoveEntity(CharacterMesh(CId%),0,0,0.1)
   Else
    RotateEntity(CharacterMesh(CId%),0,Rnd(-180,180),0)
    MoveEntity(CharacterMesh(CId%),0,0,0.1)
   EndIf
  Next
 EndIf
End Function

Function TrackingModeUpdate()
 If(KeyHit(59)=True)
  If(TrackingMode% = Idle%)
   TrackingMode% = Record%
   File = WriteFile(FileName$)
  ElseIf(TrackingMode% = Record%)
   TrackingMode% = Idle%
   CloseFile(File)
  EndIf
 EndIf 
 If(KeyHit(60)=True)
  If(TrackingMode% = Idle%)
   TrackingMode% = Play%
   File = ReadFile(FileName$)
  ElseIf(TrackingMode% = Play%)
   TrackingMode% = Idle%
   CloseFile(File)
  EndIf
 EndIf
 If(TrackingMode% = Record%)
  TrackingRecordWrite()
 ElseIf(TrackingMode% = Play%)
  TrackingReadPlay()
 EndIf
End Function

Function TrackingRecordWrite()
 For CId% = 1 To CharactersCount%
  WriteFloat(File,EntityX(CharacterMesh(CId%)))
  WriteFloat(File,EntityY(CharacterMesh(CId%)))
  WriteFloat(File,EntityZ(CharacterMesh(CId%)))
  WriteFloat(File,EntityPitch(CharacterMesh(CId%)))
  WriteFloat(File,EntityYaw(CharacterMesh(CId%)))
  WriteFloat(File,EntityRoll(CharacterMesh(CId%)))
 Next
End Function

Function TrackingReadPlay()
 If(Not Eof(File))
  For CId% = 1 To CharactersCount%
   X# = ReadFloat(File)
   Y# = ReadFloat(File)
   Z# = ReadFloat(File)		
   Pitch# = ReadFloat(File)
   Yaw# = ReadFloat(File)
   Roll# = ReadFloat(File)
   PositionEntity(CharacterMesh(CId%),X#,Y#,Z#)
   RotateEntity(CharacterMesh(CId%),Pitch#,Yaw#,Roll#)
  Next
 Else
  CloseFile(File)
  TrackingMode% = Idle%
 EndIf		
End Function

End</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
