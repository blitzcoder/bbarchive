<!DOCTYPE html><html lang="en" ><head ><title >Light Mapping example</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='A demonstration of using lightmaps within simple geometry, language=bb, category=3D Graphics - Misc'><meta name='author' content='col'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=2>3D Graphics - Misc</a>/Light Mapping example</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="383.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Light Mapping example by col</td><td align="right">2002 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> The program creates a simple square room and has some 'lights' bouncing around within. You can alter the number of lights if you want to.<br>The technique is really simple. Using a standard sphere as a fake light, each light has six lightmaps (each is a two polygon quad) : top, bottom, left, right, front and back. The lightmaps always follow the sphere but are locked to there appropriate wall, for example the top lightmap will move in the x and z directions following the light but is locked to the ceiling. The lightmaps are just offset from the 'room' polygons so as to help stop 'Z fighting'. To create the fading effect just alter the alpha setting depending on the distance to walls/roof/ceiling using simple maths ( the 'room' is fixed geometry ). The bouncing is also 'hard coded' in that there is no polygon collision detection going on, again I just used maths. The code does use a couple of textures which are provided in the download.<br><br><a href="http://www.btinternet.com/~nrg_studio/BouncingLights.zip" target="_blank">Download BouncingLights.zip (612K)</a><br><br>Regards,<br><br>Colonel<br>NRG Studio<br>nrg_studio@... </td></tr><tr ><td class="cell"><pre class="code">Graphics3D 800,600,32,2

AppTitle &quot;Bouncing Lights - LightMapping Demonstration - NRG Studio&quot;

ClearTextureFilters

Global test.particle,texLight

Type particle
	Field entMain
	Field entposy
	Field entnegy
	Field entposx
	Field entnegx
	Field entposz
	Field entnegz
	Field r,g,b
	Field x#,y#,z#,dx#,dy#,dz#
End Type

cam=CreateCamera()

wall=CreateMesh()
surf=CreateSurface(wall)
AddVertex(surf,-6,1,6,  0,0)	;0 : -z top left
AddVertex(surf,6,1,6,   6,0)	;1 : -z top right
AddVertex(surf,6,-1,6,  6,1)	;2 : -z bottom right
AddVertex(surf,-6,-1,6, 0,1)	;3 : -z bottom left

AddVertex(surf,-6,1,-6,  0,0)	;4 : +z top left
AddVertex(surf,6,1,-6,   6,0)	;5 : +z top right
AddVertex(surf,6,-1,-6,  6,1)	;6 : +z bottom right
AddVertex(surf,-6,-1,-6, 0,1)	;7 : +z bottom left

AddVertex(surf,-6,1,-6 ,0,0)	;8 : -x
AddVertex(surf,-6,1,6  ,6,0)	;9 : -x
AddVertex(surf,-6,-1,6 ,6,1)	;10: -x
AddVertex(surf,-6,-1,-6,0,1)	;11: -x

AddVertex(surf,6,1,6,0,0)		;12: +x
AddVertex(surf,6,1,-6,6,0)		;13: +x
AddVertex(surf,6,-1,-6,6,1)		;14: +x
AddVertex(surf,6,-1,6,0,1)		;15: +x

AddTriangle(surf,0,1,2)		;-z
AddTriangle(surf,0,2,3)		;-z
AddTriangle(surf,5,7,6)		;+z
AddTriangle(surf,5,4,7)		;+z
AddTriangle(surf,8,9,10)	;-x
AddTriangle(surf,8,10,11)	;-x
AddTriangle(surf,12,14,15)	;+x
AddTriangle(surf,12,13,14)  ;+x

Floors=CreateMesh()
surf=CreateSurface(Floors)
AddVertex(surf,-6,-1,-6,0,0)
AddVertex(surf,6,-1,-6,4,0)
AddVertex(surf,6,-1,6,4,4)
AddVertex(surf,-6,-1,6,0,4)

AddTriangle(surf,2,1,0)
AddTriangle(surf,3,2,0)

roof=CreateMesh()
surf=CreateSurface(roof)
AddVertex(surf,-6,1,-6,0,0)
AddVertex(surf,6,1,-6,4,0)
AddVertex(surf,6,1,6,4,4)
AddVertex(surf,-6,1,6,0,4)

AddTriangle(surf,0,1,2)
AddTriangle(surf,0,2,3)

EntityColor floors,200,200,200 ;darken the floor a little

;load textures
texWall=LoadTexture(&quot;wall.jpg&quot;)
texFloor=LoadTexture(&quot;floor.jpg&quot;,8)
texRoof=LoadTexture(&quot;roof.jpg&quot;,8)
texLight=LoadTexture(&quot;light.bmp&quot;,2)

;set textures
EntityTexture wall,texWall
EntityTexture floors,texFloor
EntityTexture roof,texRoof

;Make things a little dark
AmbientLight 90,90,90

;Add 2 lights to show the spheres
light1=CreateLight()
light2=CreateLight()
RotateEntity light1,90,0,0
RotateEntity light2,270,0,0

;Move viewpoint back a bit to get more lights into view
PositionEntity cam,0,0,-4

;Some scratch variables
help=False
lights=0
maxtris=0
maxtriframe=0

;initialize 10 light sources
For n=0 To 9
	AddLight:lights=lights+1
Next


While Not KeyDown(1)
	;move view with the mouse
	mx#=MouseX()/2-400
	RotateEntity cam,0,mx,0
		
	;update each light
	For test.particle=Each particle
		UpdateLight(test)
	Next	
	
	;Render 3D to the backbuffer
	RenderWorld
	
	;calculate frames per second - Credit goes to Kostik (David Tews)
	counter=counter+1
	If time=0 time=MilliSecs()
	If time+1001 &lt;MilliSecs()
		rate=counter
		counter=0
		time=MilliSecs()
	EndIf

	If TrisRendered()&gt;maxtris maxtris=TrisRendered():maxtriframe=rate
	
	;do numpad + and -
	If KeyHit(78) AddLight: lights=lights+1
	If KeyHit(74) And lights&gt;1 DeleteLight(First particle): lights=lights-1
	
	;TAB for help
	If KeyHit(15) help=Not help
	If Not help
		Text 0,0,&quot;Tab for HELP ON/OFF&quot;
	Else
		Text 0,0,&quot;Move mouse to look&quot;
		Text 0,12,&quot;Numpad + and - to increase/decrease number of lights&quot;
		Text 0,36,&quot;INFO:&quot;
		Text 0,48,&quot;No of lights rendered       - &quot;+lights
		Text 0,60,&quot;No of triangles rendered    - &quot;+TrisRendered()
		Text 0,72,&quot;Maximum triangles rendered  - &quot;+maxtris+&quot; at &quot;+maxtriframe+&quot; FPS&quot;
		Text 0,84,&quot;No of rrames rendered per s - &quot;+rate
	EndIf
	
	Flip
Wend

For test.particle=Each particle
	Deletelight(test)
Next

End

Function UpdateLight(test.particle)
	test\x=test\x+test\dx
	test\y=test\y+test\dy
	test\z=test\z+test\dz
	If test\x&gt;5.9 Or test\x&lt;-5.9 test\dx=-test\dx
	If test\y&gt;.9 Or test\y&lt;-.9 test\dy=-test\dy
	If test\z&gt;5.9 Or test\z&lt;-5.9 test\dz=-test\dz
		
	EntityAlpha test\entposx,test\x-5
	EntityAlpha test\entnegx,-5-test\x
	EntityAlpha test\entnegy,-test\y
	EntityAlpha test\entposy,test\y
	EntityAlpha test\entposz,test\z-5
	EntityAlpha test\entnegz,-5-test\z
				
	PositionEntity test\entMain,test\x,test\y,test\z
	PositionEntity test\entnegy,test\x,-.99,test\z
	PositionEntity test\entposy,test\x,.99,test\z
	PositionEntity test\entposx,5.99,test\y,test\z
	PositionEntity test\entnegx,-5.99,test\y,test\z
	PositionEntity test\entposz,test\x,test\y,5.99
	PositionEntity test\entnegz,test\x,test\y,-5.99
End Function

Function DeleteLight(light.particle)
	FreeEntity light\entmain
	FreeEntity light\entnegx
	FreeEntity light\entposx
	FreeEntity light\entnegy
	FreeEntity light\entposy
	FreeEntity light\entnegz
	FreeEntity light\entposz
	Delete light.particle
End Function

Function AddLight()
	test.particle=New particle
	test\entMain=CreateSphere()
	ScaleEntity test\entMain,.1,.1,.1
	test\x=Rnd(5)-2.5
	test\y=Rnd(1)-.4
	test\z=Rnd(5)-2.5
	test\dx=Rnd(.1)-.05
	test\dy=Rnd(.1)-.05
	test\dz=Rnd(.1)-.05
	test\r=Rnd(155)+100
	test\g=Rnd(155)+100
	test\b=Rnd(155)+100
	EntityColor test\entMain,test\r,test\g,test\b
	EntityAlpha test\entmain,.2
	
	;now for the lightmaps
	;first the floor
	negy=CreateMesh()
	surf=CreateSurface(negy)
	AddVertex(surf,-1,0,1,0,0)
	AddVertex(surf,1,0,1,1,0)
	AddVertex(surf,1,0,-1,1,1)
	AddVertex(surf,-1,0,-1,0,1)
	AddTriangle(surf,0,1,2)
	AddTriangle(surf,0,2,3)

	EntityColor negy,test\r,test\g,test\b
	EntityTexture negy,texLight
	EntityFX negy,1
	test\entnegy=negy
	
	;then the roof
	posy=CopyMesh(negy)
	RotateMesh posy,180,0,0
	EntityColor posy,test\r,test\g,test\b
	EntityTexture posy,texLight
	EntityFX posy,1	
	test\entposy=posy
	
	;posx
	posx=CopyMesh(posy)
	RotateMesh posx,-90,90,0
	EntityColor posx,test\r,test\g,test\b
	EntityTexture posx,texLight
	EntityFX posx,1
	test\entposx=posx

	;negx
	negx=CopyMesh(posx)
	RotateMesh negx,0,180,0
	EntityColor negx,test\r,test\g,test\b
	EntityTexture negx,texLight
	EntityFX negx,1
	test\entnegx=negx
	
	;posz
	posz=CopyMesh(posx)
	RotateMesh posz,0,90,0
	EntityColor posz,test\r,test\g,test\b
	EntityTexture posz,texLight
	EntityFX posz,1
	test\entposz=posz
	
	;negz
	negz=CopyMesh(posz)
	RotateMesh negz,0,180,0
	EntityColor negz,test\r,test\g,test\b
	EntityTexture negz,texLight
	EntityFX negz,1
	test\entnegz=negz

Return</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
