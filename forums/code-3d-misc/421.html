<!DOCTYPE html><html lang="en" ><head ><title >2D BSP</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Demonstrates 2-dimensional bsp trees., language=bb, category=3D Graphics - Misc'><meta name='author' content='JoshK'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=2>3D Graphics - Misc</a>/2D BSP</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="421.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >2D BSP by JoshK</td><td align="right">2002 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This demonstrates how bsp trees work in 2 dimensions.  I will be expanding it to include a third axis.  This wasn't such a big deal after all, now was it? </td></tr><tr ><td class="cell"><pre class="code">;2-Dimensional BSP Tree
;by Joshua Klint

AppTitle &quot;2-Dimensional BSP Tree&quot;
Graphics 700,320,16,2
SetBuffer BackBuffer()

Type node
Field x1,y1,x2,y2,nx#,ny#
Field child.node[1]
Field index,flags
Field red,green,blue
End Type

Global intersectedx#
Global intersectedy#
Global countnodes
Global master.node

ClsColor 255,255,255
logstring$=&quot;Click and drag to create a node.&quot;

Repeat

For n.node=Each node
	Color 0,0,0
	x#=(n\x2-n\x1)/2+n\x1
	y#=(n\y2-n\y1)/2+n\y1
	Line x,y,x+n\nx*6,y+n\ny*6
	Color n\red,n\green,n\blue
	Line n\x1,n\y1,n\x2,n\y2
	Color 0,0,0
	Text n\x1,n\y1,n\index
	Next

If pointwithinrect(MouseX(),MouseY(),0,0,399,300)
	If MouseHit(1)
		If Not drawmode
			logstring$=&quot;&quot;
			drawmode=True
			x1=MouseX()
			y1=MouseY()
			x2=x1
			y2=y1
			EndIf
		EndIf
	If MouseDown(1)
		x2=MouseX()
		y2=MouseY()
		Color 255,0,0
		Line x1,y1,x2,y2
		Color 200,200,200
		For n.node=Each node
			If intersectlines(x1,y1,x2,y2,n\x1,n\y1,n\x2,n\y2)
				Line intersectedx,0,intersectedx,300
				Line 0,intersectedy,400,intersectedy
				EndIf
			Next
		Else
		If drawmode
			drawmode=False
			logstring$=&quot;Created new node.&quot;
			For n.node=Each node
				If intersectlines(x1,y1,x2,y2,n\x1,n\y1,n\x2,n\y2)
					logstring=&quot;Created multiple nodes.&quot;
					Exit
					EndIf
				Next
			n.node=New node
			n\red=Rnd(255)
			n\green=Rnd(255)
			n\blue=Rnd(255)
			n\x1=x1
			n\y1=y1
			n\x2=x2
			n\y2=y2
			nx#=y2-y1
			ny#=x2-x1
			l#=Sqr(nx^2+ny^2)
			n\nx=nx/l
			n\ny=-ny/l
			n\index=countnodes
			countnodes=countnodes+1
			If master.node=Null
				master=n
				Else
				placenode n,master
				EndIf
			EndIf
		EndIf
	EndIf

If master&lt;&gt;Null
	Color 0,0,0
	displaybsptree master,550,20,60
	EndIf
	
Color 208,207,192
Rect 0,300,400,2
Rect 400,0,2,GraphicsHeight()
Color 0,0,0
Text 5,304,logstring
Flip
Cls
Forever

Function pointwithinrect(x#,y#,boxx#,boxy#,width#,height#)
If x&gt;boxx
	If x&lt;boxx+width
		If y&gt;boxy
			If y&lt;boxy+height Return True
			EndIf
		EndIf
	EndIf
End Function

Function intersectlines(x1#,y1#,x2#,y2#,x3#,y3#,x4#,y4#,segment=True)
den#=(y4-y3)*(x2-x1)-(x4-x3)*(y2-y1)
If den=0.0 Return False
ua#=((x4-x3)*(y1-y3)-(y4-y3)*(x1-x3))/den
ub#=((x2-x1)*(y1-y3)-(y2-y1)*(x1-x3))/den
If segment
	If ua&gt;1.0 Or ua&lt;0.0 Or ub&gt;1.0 Or ub&lt;0.0 Return False
	EndIf
intersectedx#=x1+ua*(x2-x1)
intersectedy#=y1+ua*(y2-y1)
Return True
End Function

;If result=0 point is on line.
;If result=1 point is behind line.
;if result=-1 point is in front of line.
Function pointonline(x#,y#,x1#,y1#,x2#,y2#)
m#=(y2-y1)/(x2-x1)
b#=-m*x1+y1
yvalue#=m*x+b
If yvalue=y Return 0
If yvalue&gt;y Return -1*Sgn(x2-x1)
If yvalue&lt;y Return 1*Sgn(x2-x1)
End Function

Function placenode(n.node,m.node)
result=pointonline(n\x1,n\y1,m\x1,m\y1,m\x2,m\y2)
result2=pointonline(n\x2,n\y2,m\x1,m\y1,m\x2,m\y2)
If result=-1 result=0
If result2=-1 result2=0
If result&lt;&gt;result2
	intersectlines(n\x1,n\y1,n\x2,n\y2,m\x1,m\y1,m\x2,m\y2,False)
	n2.node=New node
	n2\x1=intersectedx
	n2\y1=intersectedy
	n2\x2=n\x2
	n2\y2=n\y2
	n\x2=intersectedx
	n\y2=intersectedy
	n2\index=countnodes
	n2\nx=n\nx
	n2\ny=n\ny
	n\red=Rnd(255)
	n\green=Rnd(255)
	n\blue=Rnd(255)
	countnodes=countnodes+1
	If m\child[result2]=Null
		m\child[result2]=n2
		Else
		placenode n2,m\child[result2]
		EndIf
	EndIf
If m\child[result]=Null
	m\child[result]=n
	Else
	placenode n,m\child[result]
	EndIf
End Function

Function displaybsptree(n.node,x,y,width,layer=0)
Text x,y,n\index,1,1
offset=width/2^layer
If n\child[0]&lt;&gt;Null
	Line x,y,x-offset,y+40
	displaybsptree n\child[0],x-offset,y+40,width,layer+1
	EndIf
If n\child[1]&lt;&gt;Null
	Line x,y,x+offset,y+40
	displaybsptree n\child[1],x+offset,y+40,width,layer+1
	EndIf
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
