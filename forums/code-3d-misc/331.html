<!DOCTYPE html><html lang="en" ><head ><title >DigitText</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Displaying text on a HUD using a custom mesh, language=bb, category=3D Graphics - Misc'><meta name='author' content='markrosten'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=2>3D Graphics - Misc</a>/DigitText</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="331.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >DigitText by markrosten</td><td align="right">2002 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Want to display some graphics fonts in your 3d view, but don't want to use overlayed 2d?<br><br>These routines create and update a digit mesh which is essentially a flat mesh using seperate<br>surfaces to display your text for a given text string, using a loaded 2d font image for the<br>characters.<br><br>The example code shows how to set up your camera in order to get the screen positioning correct,<br>as the routines place the digit text mesh in relative 2d space. So you give the create routine 2d x<br>and y coordinates and a parent camera, and it places the entity at the correct position in 3d space<br>so that the digit mesh's text is the correct pixel size and position relative to your 2d screen.<br><br>The routine that determines which font image frame index to use expects your font image characters<br>to be ordered in a certain order. Here is the order:<br>Index  Character<br>-----  ---------<br>0         ,<br>1         -<br>2         .<br>3         /<br>4         :<br>5         =<br>6         ?<br>7         0<br>8         1<br>9-16      2-9<br>17        A<br>18-43     B-Z<br><br>You can of course just change the UpdateDigitText() function to use a different frame index list<br>for your font, which you will need to do anyway to use characters not covered by my font.<br><br>You can download a font image example here:<br><img src="http://www.planetflibble.com/rds/digifont.png"><br><br>If you have any problems of questions on this code, feel free to email them to me.<br><br>Mark Rosten<br>markbasic@... </td></tr><tr ><td class="cell"><pre class="code">;digit sprites for HUD
;by Mark Rosten
;markbasic@planetflibble.com


Graphics3D 640,480				;for example only. not needed



;=================================== declares

;-- hud digit text declares
Global digitbrush, digitbrushblank
Global digitlasttxt$

;-- load texture font frames
Global fonttexwidth = 16
Global fonttexheight = 16
Global fonttexture = LoadAnimTexture( &quot;digifont.png&quot;, 4+16+32, fonttexwidth, fonttexheight, 0, 43 ) 
If fonttexture = 0 Then RuntimeError( &quot;File 'digifont.png' not found!&quot; )



;=================================== functions


;------------- create digit entity (for hud display text)
Function CreateDigitEntity( parent, numdigits, x, y, CamZoom#=1.0 )

	;-- create work brushes?
	If Not digitbrush
	
		; create work brushes
		digitbrush = CreateBrush()
		digitbrushblank = CreateBrush()

		; clear blank digit brush texture
		; (reset all pixels in texture to alpha 0, colour 0,0,0 )
		tex = CreateTexture(1,1,4)
		SetBuffer TextureBuffer(tex)
		For ty=0 To TextureHeight(tex)-1
			For tx=0 To TextureWidth(tex)-1
				WritePixel tx,ty,0
			Next
		Next
		SetBuffer BackBuffer()

		; assign blank alpha'ed texture to blank work brush
		BrushTexture digitbrushblank, tex

		; release texture
		FreeTexture tex
	EndIf

	;-- create digit mesh
	ent = CreateDigitMesh( numdigits, parent )

	;-- adjust start position y by font height
	y = y + fonttexheight

	;-- set entity properties
	EntityFX ent, 1+4+8 ;full bright, flatshaded, disable fog
	EntityOrder ent, -1 ;bring to front

	;-- position entity relative to camera
	;   (if you have multiple cameras then you will need to hide the digit entity before
	;    rendering the cameras that you don't want the digit entity visible in)
	PositionEntity ent,x-(GraphicsWidth()/2),(GraphicsHeight()/2)-y,(GraphicsWidth()/2) * CamZoom#

	;un-comment this to ignore the positioning in 2d space and view as normal mesh
	;PositionEntity ent, -50, 0, 100 

	;-- return newly created entity
	Return ent

End Function


;------------- create digit mesh (quads joined together. one surf per quad)
Function CreateDigitMesh( numdigits=1, parent )

	;-- create mesh
	mesh = CreateMesh( parent )

	;-- determine x and y sizes for single digit segment
	xsiz = fonttexwidth-1
	ysiz = fonttexheight-1

	;-- create seperate quad per digit segment
	For digit = 1 To numdigits

		; create a surface for this quad
		surf = CreateSurface( mesh )

		; create corner vertices for quad
		v0 = AddVertex( surf, x, ysiz, 0, 0, 0 )		;top left
		v1 = AddVertex( surf, x+xsiz, ysiz, 0, 1, 0 )	;top right
		v2 = AddVertex( surf, x+xsiz, 0, 0, 1, 1 )		;bottom right
		v3 = AddVertex( surf, x, 0, 0, 0, 1 )			;bottom left

		; create 2 triangles to create quad
		AddTriangle surf, v0, v1, v2
		AddTriangle surf, v0, v2, v3

		; increment x start position for next quad
		x = x + xsiz

	Next

	;-- update normals (not sure if needed)
	UpdateNormals mesh

	;-- return newly created mesh
	Return mesh

End Function


;------------- update surface textures for digit mesh to show text
Function UpdateDigitText( ent, txt$, newx=-999, newy=-999, camzoom#=1.0 )

	;-- change in position?
	If newx &lt;&gt; -999
		If newx &lt;&gt; digitlastx Or newy &lt;&gt; digitlasty
			PositionEntity ent,newx-(GraphicsWidth()/2),(GraphicsHeight()/2)-newy,(GraphicsWidth()/2) * CamZoom#
		EndIf
	EndIf

	;-- if no change in text since last call, then don't need to do anything
	If txt$ = digitlasttxt$ Then Return

	;-- store this text
	digitlasttxt$ = txt$

	;-- convert to uppercase
	txt$ = Upper$(txt$)

	;-- texture digits 1 to length of passed text
	For i = 1 To Len(txt$)

		; only texture available surfaces
		If i &lt;= CountSurfaces( ent )

			; get this digit's surface handle
			surf = GetSurface( ent, i )

			; determine texture frame
			; (this is based on layout of font image loaded)
			f = 0
			Select Mid$(txt$,i,1)
				Case &quot;,&quot;
					f = 0
				Case &quot;-&quot;
					f = 1
				Case &quot;.&quot;
					f = 2
				Case &quot;/&quot;
					f = 3
				Case &quot;:&quot;
					f = 4
				Case &quot;=&quot;
					f = 5
				Case &quot;?&quot;
					f = 6
				Case &quot;0&quot;,&quot;1&quot;,&quot;2&quot;,&quot;3&quot;,&quot;4&quot;,&quot;5&quot;,&quot;6&quot;,&quot;7&quot;,&quot;8&quot;,&quot;9&quot;
					f = Int(Mid$(txt$,i,1))+7
				Default
					If Mid$(txt$,i,1) &gt;= &quot;A&quot; And Mid$(txt$,i,1) &lt;= &quot;Z&quot;
						f = (Asc(Mid$(txt$,i,1)) - 64) + 16
					EndIf
			End Select

			; set brush texture and paint surface with it?
			If f &gt; 0
				BrushTexture digitbrush, fonttexture, f
				PaintSurface surf, digitbrush
			Else
				; no available font character for this digit. so use blank digit
				PaintSurface surf, digitbrushblank
			EndIf
		Else
			; text exceeds number of surfaces in digit mesh, so exit loop
			Exit
		EndIf
	Next

	;-- clear remaining digits?
	If Len(txt$) &lt; CountSurfaces( ent )

		; go through remaining digit surfaces..
		For i = Len(txt$)+1 To CountSurfaces( ent )
			; get this digit's surface handle
			surf = GetSurface( ent, i )

			; use blank digit for this surface
			PaintSurface surf, digitbrushblank
		Next

	EndIf

End Function


;=================================== example usage
;                                    (remove this section when copying in to your program)

;create light
light = CreateLight()			;for test only. not needed

;initialise camera
camera = CreateCamera()
camzoom# = 1.0
CameraZoom camera,camzoom#
CameraRange camera,1,((GraphicsWidth()/2) * camzoom#)+1000

;setup digit mesh (8 digits in this example)
numdigits = 14
mydigit_entity = CreateDigitEntity( camera, numdigits, 64, 64, camzoom# )

;create cube to rotate in front of camera
cube = CreateCube()
EntityColor cube, 128,0,0
PositionEntity cube, 0, 0, 5

;render loop
While Not KeyHit(1)
	test$ = Str$(cnt) : cnt = cnt + 1
	If Len(test$) &lt; numdigits Then test$ = String$(&quot;0&quot;,8-Len(test$)) + test$
	test$ = &quot;Test &quot;+test$

	;if mouse button then pass new x/y 2d coordinates to fuction
	;otherwise just update digit text
	If MouseDown(1) And detach=False
		UpdateDigitText( mydigit_entity, test$, MouseX(), MouseY(), camzoom# )
	Else
		UpdateDigitText( mydigit_entity, test$ )
	EndIf

	;turn dummy cube
	TurnEntity cube, .5, 1, .5

	;toggle wireframe on W key
	If KeyHit(17)
		showwire = Not showwire
		WireFrame showwire
	EndIf

	;toggle digit text detach from camera
	If KeyHit(57)
		detach = Not detach
		If detach
			EntityParent mydigit_entity, cube
			PositionEntity mydigit_entity, 0, 0, 10
		Else
			EntityParent mydigit_entity, camera
			RotateEntity mydigit_entity, 0, 0, 0
			UpdateDigitText( mydigit_entity, test$, 64, 64, camzoom# )
		EndIf
	EndIf

	;update and render world
	UpdateWorld
	RenderWorld

	;instructions text
	Text 0,0,&quot;Hold mouse button to move digit text in 2d space&quot;
	Text 0,12,&quot;Press W to toggle wireframe mode&quot;
	Text 0,24,&quot;Press SPACE to toggle digit text attachment to camera or cube&quot;

	Flip
Wend
End</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
