<!DOCTYPE html><html lang="en" ><head ><title >Terragen terrain loader</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Loads a Terragen map as a Blitz terrain (mesh only -- no textures!), language=bb, category=3D Graphics - Misc'><meta name='author' content='BlitzSupport'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=2>3D Graphics - Misc</a>/Terragen terrain loader</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="447.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Terragen terrain loader by BlitzSupport</td><td align="right">2002 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Creates a Blitz terrain from a Terragen map. It's currently hard-coded for Terragen 0.8.44 maps -- see source for lots of disclaimers :)<br><br>To use it...<br><br> Get Terragen from <a href="http://www.planetside.co.uk/" target="_blank">PlanetSide</a>;<br> Install and run it;<br> In the Landscape view, click on 'Generate Terrain';<br> In the new window, click 'Generate Terrain', then 'Close';<br> Click the number box below the map image (says "7680 metres" to start with);<br> In the new window, choose 129 (128 x 128 map), 257 (256 x 256) or 513 (512 x 512), then OK;<br> Click 'Save', and save your .ter file wherever you want;<br> In your Blitz3D program, use blah = LoadTerragenTerrain () to load the terrain as 'blah'...<br> Ta-da!<br><br>Now go play with Terragen's renderer instead :) </td></tr><tr ><td class="cell"><pre class="code">; -----------------------------------------------------------------------------
; Terragen map (.ter) loader...
; -----------------------------------------------------------------------------
; Works only with maps created by Terragen 0.8.44 -- not guaranteed to work
; with later maps, or ".ter" maps created with other tools! Yes, it's nasty
; ol' hard-codin' time...
; -----------------------------------------------------------------------------
; james @ hi - toro . com
; -----------------------------------------------------------------------------

; -----------------------------------------------------------------------------
; Demo -- use CURSORS plus A and Z...
; -----------------------------------------------------------------------------

Graphics3D 640, 480

cam = CreateCamera ()
CameraClsColor cam, 64, 96, 128
CameraRange cam, 0.1, 9000

light = CreateLight ()
TurnEntity light, 0, -45, 0

terrain = LoadTerragenMap ("blah.ter", 5000, 1, True) ; CHANGE NAME OF TERRAIN FILE!

PositionEntity cam, 0, TerrainY (terrain, 0, 0, 0) + 250, 0
TurnEntity cam, 20, -45, 0

Repeat

	If KeyDown (203) TurnEntity cam, 0, 1, 0, 1
	If KeyDown (205) TurnEntity cam, 0, -1, 0, 1
	If KeyDown (200) TurnEntity cam, 1, 0, 0
	If KeyDown (208) TurnEntity cam, -1, 0, 0
	If KeyDown (30) MoveEntity cam, 0, 0, 10
	If KeyDown (44) MoveEntity cam, 0, 0, -10
	
	UpdateWorld
	RenderWorld

	Flip
	
Until KeyHit (1)

End

; -----------------------------------------------------------------------------
; LoadTerragenMap (terr$, [detail, scaler#, autotex, texwidth, texheight])
; -----------------------------------------------------------------------------
; Loads a Terragen (0.8.44) terrain map and returns a Blitz terrain handle...
; -----------------------------------------------------------------------------

; -----------------------------------------------------------------------------
; Required parameters...
; -----------------------------------------------------------------------------

; 	terr$		-- a Terragen [0.8.44] terrain file (.ter)

; -----------------------------------------------------------------------------
; Optional parameters...
; -----------------------------------------------------------------------------

;	detail		-- terrain detail (see TerrainDetail commands docs)
; 	scaler#		-- amount to scale resulting terrain by
; 	autotex		-- True to apply crude texturing
; 	texwidth	-- over-ride texture width
; 	texheight	-- over-ride texture height

; 	Regarding the last two -- textures default to the size of the map (eg.
; 	256 x 256, 512 x 512, etc). If you over-ride the texture size, make sure
; 	it's SMALLER than the map or it'll screw up! You must set both -- best
; 	to use the same value for width AND height...

; -----------------------------------------------------------------------------
; Examples:
; -----------------------------------------------------------------------------

; terrain = LoadTerragenMap ("blah.ter")
; terrain = LoadTerragenMap ("blah.ter", 5000, 0.25, False)
; terrain = LoadTerragenMap ("blah.ter", 2000, 1, True, 64, 64)

Function LoadTerragenMap (terr$, detail = 2000, scaler# = 1.0, autotex = False, texwidth = 0, texheight = 0)

	tbuffer = GraphicsBuffer () ; Store current buffer in case we write to texture...

	total = FileSize (terr$)	; Total size of file

	ter = ReadFile (terr$)

	If ter

		; "Let's parse!" -- Sandra Bullock in forthcoming hi-tech action/hacking movie...

		; -------------------------------------------------------------------------
		; Terragen .ter identifier...
		; -------------------------------------------------------------------------
	
		For b = 1 To 16
			terrinfo$ = terrinfo$ + Chr (ReadByte (ter))
		Next
		
		If terrinfo$ &lt;&gt; "TERRAGENTERRAIN "
			RuntimeError "Not a Terragen terrain file!"
		EndIf
	
		; -------------------------------------------------------------------------
		; Size...
		; -------------------------------------------------------------------------
	
		For b = 1 To 4
			sizeinfo$ = sizeinfo$ + Chr (ReadByte (ter))
		Next
		
		If sizeinfo$ &lt;&gt; "SIZE"
			RuntimeError "File corrupt in SIZE section!"
		EndIf
		
		size = ReadInt (ter)
		
		; -------------------------------------------------------------------------
		; X Points...
		; -------------------------------------------------------------------------
	
		For b = 1 To 4
			xinfo$ = xinfo$ + Chr (ReadByte (ter))
		Next
		
		If xinfo$ &lt;&gt; "XPTS"
			RuntimeError "File corrupt in XPTS section!"
		EndIf
		
		xpts = ReadInt (ter)
	
		; -------------------------------------------------------------------------
		; Y Points...
		; -------------------------------------------------------------------------
	
		For b = 1 To 4
			yinfo$ = yinfo$ + Chr (ReadByte (ter))
		Next
		
		If yinfo$ &lt;&gt; "YPTS"
			RuntimeError "File corrupt in YPTS section!"
		EndIf
		
		ypts = ReadInt (ter)
	
		; -------------------------------------------------------------------------
		; Scale...
		; -------------------------------------------------------------------------
	
		For b = 1 To 4
			scaleinfo$ = scaleinfo$ + Chr (ReadByte (ter))
		Next
		
		If scaleinfo$ &lt;&gt; "SCAL"
			RuntimeError "File corrupt in SCAL section!"
		EndIf
		
		xscale# = ReadFloat (ter)
		yscale# = ReadFloat (ter)
		zscale# = ReadFloat (ter)
	
		terrain = CreateTerrain (xpts - 1)
		If terrain = 0 Then RuntimeError "Doh @ CreateTerrain!"
		TerrainDetail terrain, 5000, True
		
		; -------------------------------------------------------------------------
		; Planet radius...
		; -------------------------------------------------------------------------
	
		For b = 1 To 4
			cradinfo$ = cradinfo$ + Chr (ReadByte (ter))
		Next
		
		If cradinfo$ &lt;&gt; "CRAD"
			RuntimeError "File corrupt in CRAD section!"
		EndIf
		
		crad# = ReadFloat (ter)
	
		; -------------------------------------------------------------------------
		; Curved terrain ("CRVM")... switches between 1 and 0, but uses 4 bytes...?
		; -------------------------------------------------------------------------
	
		For b = 1 To 4
			crvminfo$ = crvminfo$ + Chr (ReadByte (ter))
		Next
		
		If crvminfo$ &lt;&gt; "CRVM"
			RuntimeError "File corrupt in CRVM section!"
		EndIf
		
		crvm = ReadInt (ter)
		
		; -------------------------------------------------------------------------
		; Altitude data in 16-bit words...
		; -------------------------------------------------------------------------
	
		For b = 1 To 4
			altwinfo$ = altwinfo$ + Chr (ReadByte (ter))
		Next
		
		If altwinfo$ &lt;&gt; "ALTW"
			RuntimeError "File corrupt in ALTW section!"
		EndIf
		
		hscale	= ReadShort (ter)
		hbase	= ReadShort (ter)
		
		; -------------------------------------------------------------------------
		; Automatically create texture (if requested)...
		; -------------------------------------------------------------------------

		If autotex
			If (texwidth = 0) Or (texheight = 0)
				texwidth = xpts - 1
				texheight = ypts - 1
			EndIf
			tex = CreateTexture (texwidth, texheight)
			If tex
				divx# = (xpts - 1) / TextureWidth (tex)
				divy# = (ypts - 1) / TextureHeight (tex)			
				SetBuffer TextureBuffer (tex)
			EndIf
		EndIf
		
		; Start position for linear data traversal...
		
		xpt = 1
		ypt = ypts
	
		; -------------------------------------------------------------------------
		; Main data...
		; -------------------------------------------------------------------------

		While Not Eof (ter)
	
			height = ReadShort (ter)
			newheight# = (height / 65536.0) - 0.5 ; Blitz'll wrap the values (they're wrong :)
			
			If tex
				; Draw to texture... don't ask what all this means. Dunno.
				rgb = (255 - ((newheight + 0.5) * 127)) / 2
				Color rgb * 0.65, rgb * 0.85, rgb * 0.45	
				Plot (xpt - 1) / divx, (ypt - 1) / divy
			EndIf

			count = count + 1 ; Data byte count...

			If FilePos (ter) &gt; (total - 7) Then Exit ; Reached end of data...

			; Traversing the linear data...
			
			xpt = xpt + 1: If xpt &gt; xpts Then xpt = 1: ypt = ypt - 1
	
			; Applying the data...
			
			ModifyTerrain terrain, xpt - 2, ypts - (ypt - 2), newheight
					
		Wend
		
		; -------------------------------------------------------------------------
		; Two null characters...? Too lazy to check the docs now (which I only found AFTER figuring the format out!)
		; -------------------------------------------------------------------------
	
		ReadByte (ter)
		ReadByte (ter)
		
		; -------------------------------------------------------------------------
		; End of file marker ("EOF ")... 
		; -------------------------------------------------------------------------
	
		For b = 1 To 4
			eofinfo$ = eofinfo$ + Chr (ReadByte (ter))
		Next
		
		If eofinfo$ &lt;&gt; "EOF "
			RuntimeError "File corrupt in EOF[ ] section!"
		EndIf
	
		CloseFile ter
			
	EndIf

	If tex
		ScaleTexture tex, xpts - 1, ypts - 1
		EntityTexture terrain, tex
	EndIf
	
	TerrainDetail terrain, detail, True
	TerrainShading terrain, True
	ScaleEntity terrain, scaler * xscale, scaler * hscale * yscale, scaler * zscale

	SetBuffer tbuffer
	
	Return terrain
		
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
