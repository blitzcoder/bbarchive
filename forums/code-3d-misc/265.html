<!DOCTYPE html><html lang="en" ><head ><title >Carve through the fog</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Wispy fog that a moving entity can carve a trail through, language=bb, category=3D Graphics - Misc'><meta name='author' content='Vorderman'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=2>3D Graphics - Misc</a>/Carve through the fog</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="265.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Carve through the fog by Vorderman</td><td align="right">2002 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> A small program to test a method of making a cloud of wispy fog. As the player entity moves through the fog it will carve a slowly decaying trail behind it.<br><br>NOTE : you'll need to have a 16x16 BMP called &quot;Fog Particle.bmp&quot; in the same folder you run the program from. The BMP needs to be a greyscale blob such as used for lens flares or smoke particles.<br><br>Move the mouse around to move the player (orange blob).<br><br>Move slower for best results.<br><br>Could look cool if applied to the trail from a rocket or smoke around an explosion or similar. </td></tr><tr ><td class="cell"><pre class="code">AppTitle &quot;Wispy Fog&quot;
Graphics3D 800,600,32,2
SetBuffer BackBuffer()
Color 255,255,255
SeedRnd MilliSecs()
HidePointer


;-------------- CONSTANTS ---------------------------
Const gameFPS 		= 50				;frame limiting
Const MAX_FOG		= 50
Const SPREAD#		= 5
Const GLOBAL_MUSH#  = 1.0


;-------------- GLOBALS -----------------------------
Global CAM_yaw#		= 0.0
Global CAM_zoom#	= 20
Global NULLPIVOT 	= CreatePivot()


;-------------- TYPES -------------------------------
Type TYPE_FOG
	Field x#,y#,z#
	Field xs#,zs#
	Field sprite
	Field ax#,az#
	Field mushiness#
End Type

Dim FOG.TYPE_FOG(MAX_FOG)
For a=1 To MAX_FOG
	FOG.TYPE_FOG(a) = New TYPE_FOG
	FOG(a)\sprite = LoadSprite(&quot;fog particle.bmp&quot;,1) ;load a 16x16 pixel greyscale sprite
	ScaleSprite FOG(a)\sprite,2.5,2.5
	FOG(a)\x# = Rnd#(-SPREAD#,SPREAD#)
	FOG(a)\y# = 1
	FOG(a)\z# = Rnd#(-SPREAD#,SPREAD#)
	FOG(a)\xs# = 0.0
	FOG(a)\zs# = 0.0
	FOG(a)\ax# = FOG(a)\x#
	FOG(a)\az# = FOG(a)\z#
	PositionEntity FOG(a)\sprite,FOG(a)\x#,FOG(a)\y#,FOG(a)\z#
	FOG(a)\mushiness# = 0.0
Next


;-------------- ENTITIES ------------------------------
Global player = CreateSphere()
PositionEntity player,0,1.0,20
EntityColor player,255,100,0

Global camera1=CreateCamera()
CameraViewport camera1,0,0,800,600
PositionEntity camera1,0,20,-20
CameraClsColor camera1,50,50,100
RotateEntity camera1,45,0,0

AmbientLight 150,150,150
light1 = CreateLight(2)
PositionEntity light1,500,500,-500


;-------------- PRE-GAME SETUP ---------------------------
framePeriod = 1000 / gameFPS
frameTime = MilliSecs () - framePeriod
MoveMouse 400,300


;-------------- M A I N  L O O P ---------------------------
While Not KeyHit(1)

	;frame limiting
    Repeat
        frameElapsed = MilliSecs () - frameTime
    Until frameElapsed
    frameTicks = frameElapsed / framePeriod
    frameTween# = Float (frameElapsed Mod framePeriod) / Float (framePeriod)

	;game logic loop
    For frameLimit = 1 To frameTicks

		;frame limiting    
        If frameLimit = frameTicks Then CaptureWorld
        frameTime = frameTime + framePeriod

		;mouse to move blob
		MoveEntity player,(MouseXSpeed() / 10.0),0,-(MouseYSpeed() / 10.0)
		MoveMouse 400,300
		
		;update the fog
		FUNC_process_fog()

		;update game world logic
        UpdateWorld

    Next


	;render
	RenderWorld frameTween


	;calculate FPS
	If (frameTicks&gt;0) 
		FPS = gameFPS/frameTicks
	Else
		FPS = gameFPS
	EndIf


	;on-screen FPS counter
	Color 255,255,255
	Text 700,10,&quot;FPS:&quot;+FPS
	Text 350,10,&quot;WISPY FOG TEST&quot;
	
	Text 290,550,&quot;USE MOUSE TO MOVE ORANGE BLOB&quot;
	Color 255,0,0
	Text 295,562,&quot;MOVE SLOWLY FOR BEST RESULTS&quot;
	
	;flip buffers	
	Flip

Wend

End






Function FUNC_process_fog()

	For f.TYPE_FOG = Each TYPE_FOG

		dist# = EntityDistance(f\sprite , player)
		PositionEntity NULLPIVOT,f\ax#,1.0,f\az#
		dist2# = EntityDistance(f\sprite , NULLPIVOT)
				
		EntityAlpha f\sprite,((1.0/2.0)*(1.0-(dist2#/2.0)))

		If ( dist# &lt; 2.0 ) And (dist2# &lt; 2.0)
				
			PointEntity f\sprite , player
			TurnEntity f\sprite,0,180,0
			MoveEntity f\sprite ,0,0,(dist# / 5.0);0.1
			f\x# = EntityX#(f\sprite)
			f\z# = EntityZ#(f\sprite)
		
			f\mushiness# = GLOBAL_MUSH#
		Else

			If (f\mushiness# =&lt; 0.0)
				PointEntity f\sprite,NULLPIVOT
				dist# = EntityDistance(f\sprite , NULLPIVOT)
				MoveEntity f\sprite,0,0,(dist#/30.0)
			Else
				f\mushiness# = f\mushiness - 0.1
			EndIf

			f\x# = EntityX#(f\sprite)
			f\z# = EntityZ#(f\sprite)
	
		EndIf	
	Next
	
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> Edit: oops nevermind. <br>---------------------<br>Nice effects though :) <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Vorderman</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> Hahahaha, I think you get the award for "Oldest thread revivial for no reason" :) <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
