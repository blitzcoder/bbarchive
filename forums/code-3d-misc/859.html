<!DOCTYPE html><html lang="en" ><head ><title >Shadow Mapper</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Ultra Fast shadow mapper for blitz terrains, language=bb, category=3D Graphics - Misc'><meta name='author' content='TartanTangerine (was Indiepath)'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=2>3D Graphics - Misc</a>/Shadow Mapper</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="859.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Shadow Mapper by TartanTangerine (was Indiepath)</td><td align="right">2003 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Something I knocked up in a couple of hours, needs improving.. </td></tr><tr ><td class="cell"><pre class="code">; ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
; 			ULTRA FAST TERRAIN SHADOW MAPPER
;	       bY Tim Fisher a.k.a Flynn (C) 2003 v0.2
; ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

; I am sure this can be optimised!!!! :)


Graphics 640,480,16,2


; Load Your Terrain bitmap file
Global termap = LoadImage("terrain01.bmp")
Global LMSize = ImageWidth(termap)

; Initialise the mapping arrays
Dim ter#(1,1)
Dim Lmap#(1,1)


time1 = MilliSecs()

; Initialise the arrays
Initialise(termap,LMSize)


; Render the LightMap/Shadowmap. RenderLMAP(SunXPos,SunYPos,SunZPos,Lightmapsize)
shadow = RenderLMAP(4000,-1000,7000,LMSize)


SetBuffer BackBuffer()

; Do some blurring of the image (makes it look better!!)
LMBLurIMage(shadow,1)




DrawImage termap,0,0
DrawImage shadow,260,0
time2 = MilliSecs() - time1
Text 0,0,"FINISHED in "+time2 + "ms"
Text 0,10,"Hit Any Key to Save Image"
Flip

WaitKey
SaveImage (shadow,"shadow.bmp")

End

; ---------------------------------------------------------------
; Renders the Light Map according to the position of the sun
; sunX,SunY,sunZ	=	Suns Position relative to image origin
; LMAPSize			=   Size in Pixels of the Terrain Image
; ---------------------------------------------------------------

Function RenderLMAP(sunX,sunY,sunZ,LMAPSize)

	shadow = CreateImage(LMAPSize,LMAPSize)
	SetBuffer ImageBuffer(shadow)
	ClsColor 255,255,255
	Cls
	
	For x = 0 To LMAPSize
		For y = 0 To LMAPSize
			If LockedLine(x,y,ter(x,y),sunX,sunY,sunZ) Then LMAP(x,y) = 100
			
		Next
	Next

	SetBuffer ImageBuffer(shadow)
	LockBuffer ImageBuffer(shadow)
	For x = 0 To LMAPSize
		For y = 0 To LMAPSize
			col = lmap(x,y)
			If col &lt;&gt; 255 Then WritePixelFast (x,y,col Shl 16 Or col Shl 8 Or col)
		Next
		Next
	UnlockBuffer ImageBuffer(shadow)
	Return shadow
End Function

; ------------------------------------------------------------------------
; Initialises the arrays and popultates according to the terrain map data
; termap			=	the terrain map to use
; LMAPSize			=   Size in Pixels of the Terrain Image
; I have used arrays since I need to do many look ups and if I used 
; ReadPixelFast everytime I needed some info then the whole thing would 
; take minutes and not seconds!!!!
; ------------------------------------------------------------------------

Function Initialise(termap,LMapSize)
	Dim ter#(LMapSize,LMapSize)
	Dim Lmap#(LMapSize,LMapSize)
	
	SetBuffer ImageBuffer(termap)
	LockBuffer ImageBuffer(termap)

	For x = 0 To LMapSize
		For y = 0 To LMapSize
			col = ReadPixelFast( x,y)
			ter(x,y) = col Shr 16 And $FF
			Lmap(x,y) = 255
		Next
	Next
	UnlockBuffer ImageBuffer(termap)
End Function

; ---------------------------------------------------------------
; This creates a ray from the specific pixel to the sun,
; if the ray hits a point on the Terrainmap that is higher than
; the projected pixels vector then that pixel is shadowed. 
; x1,y1,z1 			=   The Pixel to check for Shadows
; x2,y2,z2			=   The position of the sun
; ---------------------------------------------------------------

Function LockedLine(x1#,y1#,z1#,x2#,y2#,z2#)
	
	Local steps,xI#
	
	x2 = x2-x1
	y2 = y2-y1
	z2 = z2-z1
	If Abs(x2)&gt;Abs(y2) steps = Abs(x2) Else steps = Abs(y2)
	xI = x2 / steps
	y2 = Y2 / steps
	z2 = z2 / steps
	
	While (x1 &lt;= LMSize) And (y1 &lt;= LMSize) And (z1 &lt;= LMSize) And (x1 =&gt; 0) And (y1 =&gt; 0) And (z1 =&gt; 0)
	
		; does the ray collide with the terrain??
		If (ter#(x1,y1) &gt; z1) Then Return True
	
		x1=x1+xI:y1=y1+y2:z1=z1+z2
		
	Wend
	Return False
End Function

; --------------------------------------------------------------------
; An image blur routine I borrowed off of BlitzBasic
; -------------------------------------------------------------------

Function LMBlurImage(Image, radius = 1)

	ImgBuf = ImageBuffer(Image)
	
	LockBuffer(ImgBuf)
	
	W% = ImageWidth(Image)
	H% = ImageHeight(Image)

	; Go thru all the pixels
	For y% = 0 To H-1
		For x% = 0 To W-1
		
			; Measure the box to get the pixel samples from
			ix1 = x - radius
			iy1 = y - radius
			ix2 = x + radius
			iy2 = y + radius
			
			; Prevent it going out of bound
			If ix1 &lt; 0 Then ix1 = 0
			If iy1 &lt; 0 Then iy1 = 0
			If ix2 &gt; W-1 Then ix2 = W-1
			If iy2 &gt; H-1 Then iy2 = H-1
			
			r = 0 : g = 0 : b = 0
			num = 0
			
			; Run thru all the sampled box
			For y2% = iy1 To iy2
				For x2% = ix1 To ix2
					
					; Sum the sampled pixel 
					argb = ReadPixelFast(x2, y2, ImgBuf) And $FFFFFF
					ar = (argb Shr 16 And %11111111)
					ag = (argb Shr 8 And %11111111)
					ab = (argb And %11111111)
					
					r = r + ar
					g = g + ag
					b = b + ab
					
					num = num + 1
				Next	
			Next
			
			; Get the average value
			r = r / num
			g = g / num
			b = b / num

			rgb = b Or (g Shl 8) Or (r Shl 16)
			WritePixelFast(x, y, rgb, ImgBuf)

		Next
	Next
	
	UnlockBuffer(ImgBuf)
	
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Really great stuff ! It is possible to make the same thing if i have some objects in my map ? <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> There is a strange problem ;) the shadow rendering is not<br>the same in windowed mode and in full screen mode ???? <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Filax,<br><br>The problem is that your desktop maybe set to 16bit, therefore when the program reads the pixels from the image buffer they are only 16bit and not 32bit. hence the strange looking images. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
