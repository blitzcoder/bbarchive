<!DOCTYPE html><html lang="en" ><head ><title >Spacegame tech demo</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Spacegame tech demo, language=bb, category=3D Graphics - Misc'><meta name='author' content='Krischan'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=2>3D Graphics - Misc</a>/Spacegame tech demo</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2393.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Spacegame tech demo by Krischan</td><td align="right">2009 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This is a small demo made of parts from my current project. It demonstrates a simple Freelancer-like steering and a planet with atmosphere glow. When you get close to the planet the atmosphere effect shows up and it even includes a check if you are at the dayside or nightside and fades the stars at the dayside. The glowing ring always keeps the same size as the planet. Have fun!<br><br>Steering: Mouse, Arrows<br>Red Cross: Flight direction<br>Green Box: Target direction<br>SPACE = Wireframe on/off </td></tr><tr ><td class="cell"><pre class="code">AppTitle "Planet Atmosphere Glow Demo"
; by Krischan webmaster(at)jaas.de

; Constants
Const ScreenWidth%			= 800			; Screen width
Const ScreenHeight%			= 600			; Screen height
Const ColorDepth%			= 32			; Color depth
Const ScreenMode%			= 2				; Screen Mode
Const MouseSpeed#			= 0.1			; Mousespeed
Const CameraSmoothness#		= 10			; Smoothness of movement
Const Scale#				= 12.756		; Approx. the diameter of earth (1 unit = 1000km)
Const GameTime%				= 20			; Game speed
Const CursorSize%			= 33			; Size of cursor and crosshair image
Const TurnSpeed#			= 2.0			; Turnspeed of player
Const RollSpeed#			= 1.0			; Roll speed
Const MaxRollAngle#			= 20.0			; Maximum roll angle
Const RingDetail%			= 60			; Number of segments

; Glow Color scheme
Const R1%=192,G1%=224,B1%=255				; Surface near color
Const R2%=128,G2%=160,B2%=255				; Surface far color

; Variables
Global CameraSpeed#			= Scale/10.0	; Movement speed
Global TargetDistance#		= 100.0*Scale	; Mousetarget distance to player
Global FrameTime			= MilliSecs()	; Initialize Frame Timer
Global Period%				= 1000/75.0		; Calc frame period (here: 75FPS)

; help variables and objects
Global MX%,MY%								; Mouse position
Global Player%,MouseTarget%,Cam%,Ship%		; Player
Global Cursor%,Cross%						; Images
Global Planet%,Light%,Sun%,StarBox%			; Scene objects
Global GlowPivot%							; Pivot of Glow
Global Glow1%,Glow2%						; Glow rings
Global GlowScale#							; Glow scale indicator
Global Tween#								; Frame tween
Global MoveSpeed#							; movespeed in km/h

; Init 3D
Graphics3D ScreenWidth,ScreenHeight,ColorDepth,ScreenMode

; Init Player and Scene
InitPlayer()
InitScene()

; Center mouse and hide pointer
MoveMouse ScreenWidth/2,ScreenHeight/2
HidePointer

; Main loop
While Not KeyHit(1)
	
	; Frame tweening
	Tween#=Float(MilliSecs()-FrameTime)/Float(GameTime) : FrameTime=MilliSecs()
	
	; SPACE = Wireframe
	If KeyHit(57) Then wf%=1-wf : WireFrame wf
	
	; Get Distance player to planet
	Local distance#=EntityDistance(Cam,Planet)
	
	; Update atmosphere glow
	UpdateAtmosphere(distance)
	
	; Attach Starbox to camera
	PositionEntity StarBox,EntityX(Player),EntityY(Player),EntityZ(Player)
	
	;Sun points To Player
	PointEntity Sun,Player
	
	UpdateWorld
	RenderWorld
	
	;Move Player
	Movement(GlowScale,distance)
	
	; Draw cursor and crosshair by chasing the mousetarget
	CameraProject Cam,EntityX(MouseTarget,1),EntityY(MouseTarget,1),EntityZ(MouseTarget,1)
	DrawImage Cursor,MX-(CursorSize/2),MY-(CursorSize/2)
	DrawImage Cross,ProjectedX()-(CursorSize/2),ProjectedY()-(CursorSize/2)
	
	; Statistics
	Text 0, 0,"Triangles rendered....: "+TrisRendered()
	Text 0,15,"Distance To Surface...: "+Int((distance-Scale)*1000)+" km"
	Text 0,30,"Move speed............: "+MoveSpeed+" km/h"
	
	Flip 0
	
Wend

End

; The functions that creates the mesh and surface for a ring
Function CreateRing(fx%=0,blend%=0)
	
	Local a1#,a2#,a3#,a4#,angle%
	Local v0%,v1%,v2%,v3%
	
	Local mesh=CreateMesh()
	Local surf=CreateSurface(mesh)
	
	; Ring FX
	If fx&gt;0 Then EntityFX mesh,fx
	If blend&gt;0 Then EntityBlend mesh,blend
	
	Return mesh
	
End Function

; Re-creates the ring vertices and triangles with different values
Function UpdateRing(mesh%,radius1#=1.0,radius2#=2.0,segments%=360,r1%=255,g1%=255,b1%=255,alpha1#=1.0,r2%=0,g2%=0,b2%=0,alpha2#=1.0,scale#=0.0)
	
	Local a1#,a2#,angle%
	Local v0%,v1%,v2%,v3%,v%
	
	; get and clear the surface
	Local surf=GetSurface(mesh,1)
	ClearSurface surf,1,1
	
	; Limit segments
	If segments&gt;360 Then segments=360
	
	; Create ring
	For angle=0 To segments
		
		a1=angle*360.0/segments
		a2=angle*360.0/segments+180.0/segments
		
		; Calc vertex points
		v0=AddVertex(surf,radius1*Cos(a1),radius1*Sin(a1),0,0,0)
		v1=AddVertex(surf,radius2*Cos(a2),radius2*Sin(a2),scale,1,1)
		
		; Color
		VertexColor surf,v0,r1,g1,b1,alpha1
		VertexColor surf,v1,r2,g2,b2,alpha2
		
	Next
	
	; Create Triangles
	For v=0 To CountVertices(surf)-3
		
		AddTriangle(surf,v,v+1,v+2)
		
	Next
	
	Return mesh
	
End Function

; Initialize player, camera
Function InitPlayer()
	
	Local i%
	
	; Player pivot
	Player=CreatePivot()
	PositionEntity Player,-Scale*2,0,Scale*2
	
	; Ship mesh
	Ship=CreateCube(Player)
	EntityFX Ship,1
	ScaleEntity Ship,0.2,0.05,0.2
	PositionEntity Ship,0,-0.5,1.5
	EntityOrder Ship,-1000
	
	; Mousetarget in space
	MouseTarget=CreatePivot(Ship)
	MoveEntity MouseTarget,0,0,TargetDistance
	
	; Camera
	Cam=CreateCamera(Player)
	PositionEntity Cam,0,0,0
	CameraRange Cam,0.01,1000*Scale
	
	; Create cursor image
	Cursor=CreateImage(CursorSize,CursorSize)
	SetBuffer ImageBuffer(Cursor)
	For i=0 To 2
		Color 0,Int(255.0/(1+i)),0
		Rect i,i,CursorSize-(2*i),CursorSize-(2*i),0
	Next
	
	; Create crosshair image
	Cross=CreateImage(CursorSize,CursorSize)
	SetBuffer ImageBuffer(Cross)
	Color 255,0,0
	Line (CursorSize-1)/2.0,0,(CursorSize-1)/2.0,CursorSize
	Line 0,(CursorSize-1)/2.0,CursorSize,(CursorSize-1)/2.0
	
	; reset buffer and color
	SetBuffer BackBuffer()
	Color 255,255,255
	
End Function

; Initialize scene
Function InitScene()
	
	Local startex%,i%,col%,rgb%
	
	; Planet
	Planet=CreateSphere(60)
	ScaleEntity Planet,Scale,Scale,Scale
	EntityColor Planet,32,192,64
	
	; Directional Sunlight
	Light=CreateLight(1)
	PositionEntity Light,0,0,-Scale*200
	LightRange Light,200*Scale
	AmbientLight 16,16,16
	
	; Sun
	Sun=CreateQuad()
	ScaleEntity Sun,Scale*10,Scale*10,Scale*10
	EntityFX Sun,1
	EntityColor Sun,255,255,192
	EntityParent Sun,Light
	PositionEntity Sun,0,0,0
	EntityBlend Sun,3
	EntityTexture Sun,CreateSunTexture()
	
	; Simple Starbox
	StarBox=CreateCube()
	startex=CreateTexture(1024,1024)
	LockBuffer TextureBuffer(startex)
	For i=1 To 1000
		col=Rand(0,255)
		rgb=col*$10000+col*$100+col
		WritePixelFast Rand(0,1023),Rand(0,1023),rgb,TextureBuffer(startex)
	Next
	UnlockBuffer TextureBuffer(startex)
	EntityTexture StarBox,startex
	ScaleEntity StarBox,10,10,10
	EntityOrder StarBox,1
	EntityFX StarBox,1
	FlipMesh StarBox
	
	; Glow
	GlowPivot=CreatePivot()
	Glow1=CreateRing(1+2+16+32,1)
	Glow2=CreateRing(1+2+16+32,3)
	EntityParent Glow1,GlowPivot
	EntityParent Glow2,GlowPivot
	EntityOrder Glow1,1
	EntityOrder Glow2,1
	
	; Player points to planet first
	PointEntity Player,Planet
	TurnEntity Player,0,-20,-90
	
End Function

; Update atmosphere glow and background color
Function UpdateAtmosphere(distance#)
	
	Local s2#,s2d#,aa#,bb#,cc#
	Local angle#,intensity#,clscol#,horizon#
	
	; Calculate ring scale with the help of two right-angled triangles and trigonometry
	s2#=Scale^2
	s2d#=s2/distance
	GlowScale#=(Sqr(s2+(Scale/Tan(90-(90-(90-ATan(Sqr((distance-(s2d))*(s2d))/(distance-(s2d)))))))^2))/Scale
	
	; Calculcate the sun light angle (1 = exactly between sun and planet, 0 = exactly behind the planet)
	aa#=EntityDistance(Cam,Planet)
	bb#=EntityDistance(Planet,Light)
	cc#=EntityDistance(Cam,Light)
	angle#=ACos((aa^2+cc^2-bb^2)/(2*aa*cc))/180.0
	If angle&gt;1 Then angle=1
	If angle&lt;0 Then angle=0
	
	; Calculate the glow intensity according to sun light angle and distance to planet
	intensity#=1-(1.0/Exp(GlowScale*angle))
	If intensity&lt;0 Then intensity=0
	If intensity&gt;1 Then intensity=1
	
	; Calculate the horizon glow scale multiplicator
	horizon#=1-(1.2/Exp(GlowScale*angle/2.0))
	If horizon&lt;0 Then horizon=0
	If horizon&gt;1 Then horizon=1
	
	; Update ring intensity according to sun light angle and distance to planet
	UpdateRing(Glow1,0.6*Scale,0.01*Scale,RingDetail,R1*intensity,G1*intensity,B1*intensity,angle,R2*(angle-intensity),G2*(angle-intensity),B2*(angle-intensity),   0,Scale/3.0)
	UpdateRing(Glow2,(1.0-(horizon/10.0))*Scale,(1.05+(horizon/10.0))*Scale,RingDetail,R1*intensity,G1*intensity,B1*intensity,1.0-horizon,R2*angle,G2*angle,B2*angle,0,horizon*2)
	
	; Scale the rings and always point to player
	ScaleEntity GlowPivot,GlowScale,GlowScale,GlowScale
	PointEntity GlowPivot,Player
	
	; Calculate the background color to simulate atmosphere penetration
	clscol#=(1-(5.0/Exp(GlowScale*angle/2.0)))*intensity
	If clscol&lt;0 Then clscol=0
	If clscol&gt;1 Then clscol=1
	CameraClsColor Cam,R2*clscol,G2*clscol,B2*clscol
	
	; Change Starbox alpha
	EntityAlpha StarBox,1-clscol
	
End Function

; Player movement
Function Movement(scale#,distance#)
	
	Local roll#,mox#,moz#,cx#,cz#
	Local t1#,t2#
	
	; get mouse position
	MX=MouseX()
	MY=MouseY()
	
	; Movement with speed limit
	cx=(KeyDown(205)-KeyDown(203))*CameraSpeed
	cz=(KeyDown(200)-KeyDown(208))*CameraSpeed
	
	; Arrow left/right = roll
	If KeyDown(203) Then roll=RollSpeed
	If KeyDown(205) Then roll=-RollSpeed
	
	; Normalize Mouse position (-1 to +1)
	t1=Normalize(MY,0,ScreenHeight,-1,1)
	t2=Normalize(MX,0,ScreenWidth,1,-1)
	
	; Slower cursor movement in the center of the screen
	;If t1&lt;0 Then t1=(Abs(t1)^2.0)*-1 Else t1=t1^2.0
	;If t2&lt;0 Then t2=(Abs(t2)^2.0)*-1 Else t2=t2^2.0
	
	; Rotate ship mesh and turn player pivot
	RotateEntity Ship,t1*MaxRollAngle,t2*MaxRollAngle,t2*MaxRollAngle*2
	TurnEntity Player,t1*TurnSpeed*Tween,t2*TurnSpeed*Tween,roll*TurnSpeed*Tween
	
	; Move the player forward/backward
	MoveEntity Player,0,0,(cz*1.0/(scale)^3)*Tween
	
	; Calculate actual movespeed
	MoveSpeed#=(cz*1.0/(scale)^3)*3600000
	
End Function

; Normalize a value
Function Normalize#(value#=128.0,value_min#=0.0,value_max#=255.0,norm_min#=0.0,norm_max#=1.0)
	
	Return ((value-value_min)/(value_max-value_min))*(norm_max-norm_min)+norm_min
	
End Function

Function CreateQuad()
	
	; Create mesh and surface
	Local mesh%=CreateMesh()
	Local surf%=CreateSurface(mesh)
	
	; Add vertices
	Local v0%=AddVertex(surf,  1.0,  1.0, 0.0, 0.0, 0.0 )	; upper left
	Local v1%=AddVertex(surf, -1.0,  1.0, 0.0, 1.0, 0.0 )	; upper right
	Local v2%=AddVertex(surf, -1.0, -1.0, 0.0, 1.0, 1.0 )	; lower right
	Local v3%=AddVertex(surf,  1.0, -1.0, 0.0, 0.0, 1.0 )	; lower left
	
	; Connect vertices
	AddTriangle surf,v0,v1,v2
	AddTriangle surf,v0,v2,v3
	
	Return mesh
	
End Function

; Create a simple sun texture
Function CreateSunTexture()
	
	Local tex%=CreateTexture(512,512,2)
	Local tb%=TextureBuffer(tex)
	
	Local i#,j%,col%,rgb%
	
	SetBuffer tb
	LockBuffer tb
	
	; Intensity steps
	For j=0 To 255
		
		; Exponential falloff
		col=Int((1.0/Exp(j*0.075))*100000000)
		If col&gt;255 Then col=255
		rgb=col*$1000000+col*$10000+col*$100+col
		
		; Draw circles
		For i=0 To 360 Step 0.1
			WritePixelFast 256+(Sin(i)*j),256+(Cos(i)*j),rgb,tb
		Next
		
	Next
	
	UnlockBuffer tb
	SetBuffer BackBuffer()
	
	Return tex
	
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CakeMonitor</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> This is pure gold!  Only a little bit of tweaking and it is the exact control system I was developing.  You have saved me soooo much work.  The glow and lighting system for the sun and planets is also exceptional imo and has given me inspiration for overcoming various problems I will shortly be facing.<br><br>You will be fully credited ofc when my project hits the shelves. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> Yeah use it, it is public domain. My own engine will be even more advanced than this, but this demo shows the basic idea behind it. There is only a little bug in the very basic function to determine if you are between the planet and the sun or behind the planet: if you are exactly behind the planet and facing the sun, my triangle calculation fails. You need to limit the very small angle numbers or the glow will begin to flicker for a small angle range:<br><br><b>Failing Line:</b><br><pre class=code>If angle&gt;1 Then angle=1
If angle&lt;0 Then angle=0</pre><br><b>Patch:</b><br><pre class=code>If angle Then lastangle#=angle#
Local s2#,s2d#,aa#,bb#,cc#

...

If Not angle And lastangle&lt;0.5 Then angle=0.01
If Not angle And lastangle&gt;0.5 Then angle=0.99
If angle&lt;0.01 Then angle=0.01
If angle&gt;0.99 Then angle=0.99</pre><br>This is not a 100% solution but the numbers are so small that you wouldn't notice the difference.<br><br>To see the bug just change the player position line in the Function "Initplayer" to this and move backwards without moving the mouse, the ring will begin to flicker or even disappear:<br><pre class=code>PositionEntity Player,0,0,Scale*3</pre> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CakeMonitor</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> Hello again Christian,<br><br>I've been testing this quite a bit and have noticed one other tiny issue.  When you get very close to the surface of the planet (about 10km), you can see what i think is one the glow surfaces from behind.  It obscures the planet surface.  <br><br>Hopefully this image I put on imageShack works...<br><br><br><br>[URL]http://img231.imageshack.us/my.php?image=atmosissuejl9.jpg[/URL]<br><br>Have you too had this issue and any ideas on a fix?<br><br><br>ps by the way, when you say that this is a small portion of your project, you make me drool  and I cannot help but feel that my contribution to the space game genre will be strictly inferior to yours!  Even so, I shall trudge on and wish you all the best with your project. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> Hi CakeMonitor. Yeah I know this issue, it's just the first value of the CameraRange which is not small enough anymore. You are so close to the planet that the camera can't project it, adjust it to a lower value to "fix" the issue.<br><br>In my project I plan to "auto-land" the ship when you reached about 10km height (and to fake the landing with some cloud and atmosphere entry fx), so I don't care. You could try to auto-update the camerarange depending on the distance to the surface, but it is not designed for that.<br><br>And thanks for the wishes - I am a biiiiiig Starflight 1 fan (Binary Systems, 1986) and I think I am able to warp this type of game with my own additions into 3D space with Blitz. At this moment I am working on a realtime planet generator using banks and blendings, you can take a look at it here:<br><br><b>http://www.blitzbasic.com/codearcs/codearcs.php?code=2416</b><br><br>Generate your unique Class M planet in 200ms! Still in an early stage but able to generate ten thousands of unique surfaces out of 10 images! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> Well, here is an update with a whole solar system. I had some problems with the correct distances but I think I solved it - could be better but it is working and sufficient as an example. Have fun.<br><br>Steering:<br>Mouse= Target direction<br>Arrows = move forward/backward and roll<br>LMB = 10x Zoom<br>RMB = move 10x faster<br>SPACE = Wireframe<br><br><b>Demo screenshot:</b><br><img src="http://www.christianhart.de/bb/solar/solar_blank.jpg"><br><br><b>Pimped screenshot, uses 99% of the code below:</b><br><img src="http://www.christianhart.de/bb/solar/solar_pimped.jpg"><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics3D 800,600,32,2


;Include "milkyway.bb"


; the answer to all questions
SeedRnd 42


; Frametween stuff
Global GameSpeed%=60
Global Screenwidth%=GraphicsWidth()
Global Screenheight%=GraphicsHeight()
Global FramePeriod%=1000/GameSpeed
Global FrameTime%=MilliSecs()-FramePeriod


; variables
Global RingDetail%	= 120
Global TargetDist#	= 10000
Global RollSpeed#	= 1.0
Global TurnSpeed#	= 2.0
Global MaxRoll#		= 20.0
Global Speed#		= 500
Global CursorSize%	= 33


; Glow Color scheme
Const R1%=192,G1%=224,B1%=255
Const R2%=128,G2%=160,B2%=255


; player / movement
Global MX%,MY%,Player%,MouseTarget%,Cam%,Ship%,Cursor%,Cross%,movedistance#,speedtimer%,mm#,wf%


; nearest object
Global nearestdist#,nearestscale#,nearestname%,nearestglowscale#


; scene objects
Global Light%,Sun0%,Sun1%,Sun2%,Sun3%;,MilkyWay%
Global masterpivot%,solidpivot%,wiredpivot%


; homekeeping
Global localx#,localy#,localz#
Global globalx#,globaly#,globalz#
Global simx#,simy#,simz#
Global newx#,newy#,newz#
Global oldx#,oldy#,oldz#


; planet type
Type planet
	
	Field id%
	Field Planet%
	Field GlowPivot%
	Field Glow1%
	Field Glow2%
	Field Scale#
	Field glowscale#
	Field d#
	Field angle#
	Field intensity#
	Field horizon#
	Field clscol#
	Field orbit%
	Field radius#
	Field atmofactor#
	
End Type


; Init Player and Scene
InitPlayer()
InitScene()

; initiate homekeeping and movespeed calculation
HomeKeeping(Player,masterpivot,1000)
oldx=simx
oldy=simy
oldz=simz


; Center mouse and hide pointer
MoveMouse Screenwidth/2,Screenheight/2
HidePointer


; Main loop
While Not KeyHit(1)
	
	Cls
	
	Local FrameElapsed%,FrameTicks%,FrameTween#,t%
	
	Repeat FrameElapsed=MilliSecs()-FrameTime Until FrameElapsed
	FrameTicks=FrameElapsed/FramePeriod
	FrameTween=Float(FrameElapsed Mod FramePeriod)/Float(FramePeriod)
	
	; main tweened loop
	For t=1 To FrameTicks
		
		FrameTime=FrameTime+FramePeriod : If t=FrameTicks Then CaptureWorld
		
		; SPACE = Wireframe
		If KeyHit(57) Then wf%=1-wf : WireFrame wf
		
		; updates scene
		UpdateScene(p.planet)
		
		; moves player
		Movement()
		
		; keeps the player in its "home area"
		HomeKeeping(Player,masterpivot,1000)
		
		; calcs the speed/sec
		CalcSpeed()
		
		; sorts the planets
		SortPlanets()
		
		; updates the planets atmospheres
		UpdatePlanets(p.planet)
		
		; attaches galaxy to player
		;PositionEntity MilkyWay,localx,localy,localz
		
		UpdateWorld
		
	Next
	
	; tweened render Wireframe
	WireFrame 1
	ShowEntity wiredpivot
	HideEntity solidpivot
	;HideEntity MilkyWay
	CameraClsMode Cam,1,1
	RenderWorld FrameTween
	
	; tweened render solid objects
	WireFrame wf
	HideEntity wiredpivot
	ShowEntity solidpivot
	;ShowEntity MilkyWay
	CameraClsMode Cam,0,0
	RenderWorld FrameTween
	
	; draw planet names
	DrawPlanetNames(p)
	
	; Draw cursor and crosshair by chasing the mousetarget
	CameraProject Cam,EntityX(MouseTarget,1),EntityY(MouseTarget,1),EntityZ(MouseTarget,1)
	DrawImage Cursor,MX-(CursorSize/2),MY-(CursorSize/2)
	DrawImage Cross,ProjectedX()-(CursorSize/2),ProjectedY()-(CursorSize/2)
	
	; Statistics
	Text 0, 0,"Triangles rendered....: "+TrisRendered()
	Text 0,15,"Distance To Sun.......: "+(EntityDistance(Player,Sun0)/100)+" Mio. km"
	Text 0,30,"Nearest Planet........: "+Int((nearestdist-nearestscale)*100)+"km [Planet "+nearestname+"]"
	Text 0,45,"Movespeed.............: "+(movedistance*100)+"km/s ["+((movedistance*100)/299792)+"c]"
	Text 0,60,"Player Position X/Y/Z.: "+simx+" / "+simy+" / "+simz
	
	Flip 0
	
Wend

End


; updates the planets distances, the speed multiplicator, the camerarange, the lightrange and the sunscale
Function UpdateScene(p.planet)
	
	Local cr#,sunscale#
	
	For p.planet = Each planet
		
		p\d=EntityDistance(Player,p\Planet)
		
		; Update atmosphere glow
		UpdateAtmosphere(p)
		
	Next
	
	; calculate speed multiplicator
	If (nearestdist-nearestscale)&lt;50000 Then mm#=(nearestdist-nearestscale)/50000 Else mm=1
	
	; adjust camerarange
	cr=Abs(mm):If cr&lt;0.1 Then cr=0.1
	CameraRange Cam,cr,5000000
	
	; adjust lightrange
	LightRange Light,EntityDistance(Player,Sun0)*2
	
	; sun quads point to player
	PointEntity Sun0,Player
	PointEntity Sun1,Player
	PointEntity Sun2,Player
	PointEntity Sun3,Player
	
	; adjust sunscale
	sunscale=((1-(1.0/Exp(EntityDistance(Player,Sun0)*0.0000001)))*500000)
	ScaleEntity Sun0,sunscale,sunscale,sunscale
	
End Function


; updates the planets atmospheres
Function UpdatePlanets(p.planet)
	
	Local flag%=0
	
	For p.planet = Each planet
		
		; first planet = nearest planet
		If p\d&gt;0 And flag=0 Then
			
			flag=1
			
			; set nearest variables
			nearestdist#=p\d
			nearestscale#=p\Scale*1.005
			nearestname%=p\id
			nearestglowscale#=p\glowscale
			
			; update background color ("sky")
			CameraClsColor Cam,R2*p\clscol,G2*p\clscol,B2*p\clscol
			
			; fade galaxy and stars
			;UpdatePatch(Starbox,255,255,255,1-(p\clscol*1.1),False,True)
			;UpdatePatch(Fogpanorama,255,255,255,1-(p\clscol*1.1),False,True)
			;UpdatePatch(Starsphere,255,255,255,1-(p\clscol*1.1),False,True)
			
			; switch blend mode
			EntityBlend p\Planet,1
			ShowEntity p\GlowPivot
			
			; Update ring intensity according to sun light angle and distance to planet
			UpdateRing(p\Glow1,0.6*p\Scale,0.01*p\Scale,RingDetail,R1*p\intensity,G1*p\intensity,B1*p\intensity,p\angle,R2*(p\angle-p\intensity),G2*(p\angle-p\intensity),B2*(p\angle-p\intensity),   0,p\atmofactor*p\Scale/3.0)
			UpdateRing(p\Glow2,(1.0-(p\horizon/10.0))*p\Scale,(1.05+(p\horizon/10.0))*p\Scale,RingDetail,R1*p\intensity,G1*p\intensity,B1*p\intensity,1.0-p\horizon,R2*p\angle,G2*p\angle,B2*p\angle,0,p\atmofactor*p\horizon*2)
			
			; Scale the rings and always point to player
			ScaleEntity p\GlowPivot,p\glowscale,p\glowscale,p\glowscale
			PointEntity p\GlowPivot,Player
			
		Else
			
			; switch blend mode and hide glow
			EntityBlend p\Planet,3
			HideEntity p\GlowPivot
			
		EndIf
		
		; fade orbit
		UpdatePatch(p\orbit,64,64,64,0.5/Exp(nearestglowscale),False)
		
	Next
	
End Function


; draws the planet name above the current position, visible only
Function DrawPlanetNames(p.planet)
	
	; draw planet names
	For p.planet = Each planet
		
		If EntityInView(p\Planet,Cam) Then
			
			CameraProject Cam,EntityX(p\Planet)-globalx,EntityY(p\Planet)-globaly,EntityZ(p\Planet)-globalz
			Text ProjectedX()-4,ProjectedY()-20,p\id
			
		EndIf
		
	Next
	
End Function


; keeps the player in a given distance while the world can move far away
Function HomeKeeping(player%,world%,homesize%=100)
	
	; store local player position
	localx=EntityX(player)
	localy=EntityY(player)
	localz=EntityZ(player)
	
	; check X axis
	While localx&gt;homesize
		globalx=globalx+homesize
		localx=localx-homesize
		PositionEntity player,localx,localy,localz
		MoveEntity world,-homesize,0,0
	Wend
	While localx&lt;-homesize
		globalx=globalx-homesize
		localx=localx+homesize
		PositionEntity player,localx,localy,localz
		MoveEntity world,homesize,0,0
	Wend
	
	; check Y axis
	While localy&gt;homesize
		globaly=globaly+homesize
		localy=localy-homesize
		PositionEntity player,localx,localy,localz
		MoveEntity world,0,-homesize,0
	Wend
	While localy&lt;-homesize
		globaly=globaly-homesize
		localy=localy+homesize
		PositionEntity player,localx,localy,localz
		MoveEntity world,0,homesize,0
	Wend
	
	; check Z axis
	While localz&gt;homesize
		globalz=globalz+homesize
		localz=localz-homesize
		PositionEntity player,localx,localy,localz
		MoveEntity world,0,0,-homesize
	Wend
	While localz&lt;-homesize
		globalz=globalz-homesize
		localz=localz+homesize
		PositionEntity player,localx,localy,localz
		MoveEntity world,0,0,homesize
	Wend
	
	; store simulated player position
	simx=localx+globalx
	simy=localy+globaly
	simz=localz+globalz
	
End Function


; sorts planets by distance to player
Function SortPlanets()
	
	Local o.planet,no.planet,o1.planet,o2.planet,t%
	
	no=After First planet
	
	While no&lt;&gt;Null
		
		o=no
		no=After o
		o1=o:t=o\d
		
		Repeat
			
			o2=Before o1
			If o2=Null Then Exit
			If t&gt;=o2\d Then Exit
			o1=o2
			
		Forever
		
		o2=o
		Insert o2 Before o1
		
	Wend
	
End Function


; The functions that creates the mesh and surface for a ring
Function CreateRing(fx%=0,blend%=0,parent%=False,x#=0.0,y#=0.0,z#=0.0,order%=False)
	
	Local mesh=CreateMesh(),surf%
	
	surf=CreateSurface(mesh)
	
	; Ring fx
	If fx Then EntityFX mesh,fx
	If blend Then EntityBlend mesh,blend
	If parent Then EntityParent mesh,parent
	If order Then EntityOrder mesh,order
	PositionEntity mesh,x,y,z
	
	Return mesh
	
End Function


; Re-creates the ring vertices and triangles with different values
Function UpdateRing(mesh%,radius1#=1.0,radius2#=2.0,segments%=360,r1%=255,g1%=255,b1%=255,alpha1#=1.0,r2%=0,g2%=0,b2%=0,alpha2#=1.0,scale#=0.0)
	
	Local a1#,a2#,angle%
	Local v0%,v1%,v%
	
	; get and clear the surface
	Local surf=GetSurface(mesh,1)
	ClearSurface surf,1,1
	
	; Limit segments
	If segments&gt;360 Then segments=360
	
	; Create ring
	For angle=0 To segments
		
		a1=angle*360.0/segments
		a2=angle*360.0/segments+180.0/segments
		
		; Calc vertex points
		v0=AddVertex(surf,radius1*Cos(a1),radius1*Sin(a1),0,0,0)
		v1=AddVertex(surf,radius2*Cos(a2),radius2*Sin(a2),scale,1,1)
		
		; Color
		VertexColor surf,v0,r1,g1,b1,alpha1
		VertexColor surf,v1,r2,g2,b2,alpha2
		
	Next
	
	; Create Triangles
	For v=0 To CountVertices(surf)-3
		
		AddTriangle(surf,v,v+1,v+2)
		
	Next
	
	Return mesh
	
End Function


; Initialize player, camera
Function InitPlayer()
	
	Local i%
	
	; Player pivot
	Player=CreatePivot()
	PositionEntity Player,-7280,25,-6961
	
	; Ship mesh
	Ship=CreateCube(Player)
	EntityFX Ship,1
	ScaleEntity Ship,0.2,0.05,0.2
	PositionEntity Ship,0,-0.5,1.5
	EntityOrder Ship,-1000
	
	; Mousetarget in space
	MouseTarget=CreatePivot(Ship)
	PositionEntity MouseTarget,0,0,TargetDist
	
	; Camera
	Cam=CreateCamera(Player)
	PositionEntity Cam,0,0,0
	CameraRange Cam,1,5000000
	CameraFogMode Cam,True
	CameraFogRange Cam,500000,1000000
	
	; Create cursor image
	Cursor=CreateImage(CursorSize,CursorSize)
	SetBuffer ImageBuffer(Cursor)
	For i=0 To 2
		Color 0,Int(255.0/(1+i)),0
		Rect i,i,CursorSize-(2*i),CursorSize-(2*i),0
	Next
	
	; Create crosshair image
	Cross=CreateImage(CursorSize,CursorSize)
	SetBuffer ImageBuffer(Cross)
	Color 255,0,0
	Line (CursorSize-1)/2.0,0,(CursorSize-1)/2.0,CursorSize
	Line 0,(CursorSize-1)/2.0,CursorSize,(CursorSize-1)/2.0
	
	; reset buffer and color
	SetBuffer BackBuffer()
	Color 255,255,255
	
End Function


; Initialize scene
Function InitScene()
	
	Local i%,suntex%
	
	masterpivot=CreatePivot()
	solidpivot=CreatePivot(masterpivot)
	wiredpivot=CreatePivot(masterpivot)
	
	; Sunlight
	Light=CreateLight(2,solidpivot)
	PositionEntity Light,0,0,0
	AmbientLight 0,0,0
	LightRange Light,100
	
	; Create Sun Texture
	suntex=CreateSunTexture()
	
	; Sun Quads
	Sun0=CreateQuad(Light,1000,suntex,3,1+8,255,255,255,1.00)
	Sun1=CreateQuad(Light,1500,suntex,3,1+8,255,192,128,1.00)
	Sun2=CreateQuad(Light,3000,suntex,3,1+8,255,255,224,0.75)
	Sun3=CreateQuad(Light,6000,suntex,3,1+8,255,255,224,0.50)
	
	; Milkyway background
	;MilkyWay=InitMilkyway(1000,0.5,10,40,Sun0,10,0,0,0,1,128,96,64,1,0,0,0,1)
	
	For i=1 To 9
	
		p.planet = New planet
		p\planet=CreateSphere(64,solidpivot)
		p\scale=50+Rnd(i*10,i*2*10)
		p\atmofactor=Sqr(p\scale^4.2)/10000.0
		p\radius=(4+3*2^(i-1))*1000
		p\id=i
		
		RotateEntity p\planet,0,Rnd(0,360),0
		MoveEntity p\planet,0,0,p\radius
		ScaleEntity p\planet,p\scale,p\scale,p\scale
		EntityColor p\planet,32,128,64
		
		p\glowpivot=CreatePivot(solidpivot)
		PositionEntity p\glowpivot,EntityX(p\planet),EntityY(p\planet),EntityZ(p\planet)
		p\glow1=CreateRing(1+2+16+32,1,p\glowpivot,0,0,0,1)
		p\glow2=CreateRing(1+2+16+32,3,p\glowpivot,0,0,0,1)
		
		p\orbit=CreateOrbit(360,0,p\radius,p\radius,128,255,128,0.0)
		EntityParent p\orbit,wiredpivot
	
	Next
	
	; Player points to planet first
	PointEntity Player,Sun0
	
End Function


; updates a patch
Function UpdatePatch(mesh%,r%,g%,b%,a#,usealpha%=False,usevertexcolors%=False)
	
	Local surf%=GetSurface(mesh,1),v%
	
	For v=0 To CountVertices(surf)-1
		
		If usealpha Then a=VertexAlpha(surf,v)
		
		If usevertexcolors Then
			
			r=VertexRed(surf,v)
			g=VertexGreen(surf,v)
			b=VertexBlue(surf,v)
			
		EndIf
		
		VertexColor surf,v,r,g,b,a
	Next
	
End Function


; Update atmosphere glow and background color
Function UpdateAtmosphere#(p.planet)
	
	Local s2#,s2d#,aa#,bb#,cc#
	
	; calculate ring scale with the help of two right-angled triangles and trigonometry
	s2#=p\Scale^2
	s2d#=s2/p\d
	p\glowscale#=(Sqr(s2+(p\Scale/Tan(90-(90-(90-ATan(Sqr((p\d-(s2d))*(s2d))/(p\d-(s2d)))))))^2))/p\Scale
	
	; calculcate the sun light angle (1 = exactly between sun and planet, 0 = exactly behind the planet)
	aa#=EntityDistance(Cam,p\Planet)
	bb#=EntityDistance(p\Planet,Light)
	cc#=EntityDistance(Cam,Light)
	p\angle=ACos((aa^2+cc^2-bb^2)/(2*aa*cc))/180.0
	If p\angle&gt;1 Then p\angle=1
	If p\angle&lt;0 Then p\angle=0
	
	; calculate the glow intensity according to sun light angle and distance to planet
	p\intensity=1-(1.0/Exp(p\glowscale*p\angle))
	If p\intensity&lt;0 Then p\intensity=0
	If p\intensity&gt;1 Then p\intensity=1
	
	; calculate the horizon glow scale multiplicator
	p\horizon=1-(1.2/Exp(p\glowscale*p\angle/2.0))
	If p\horizon&lt;0 Then p\horizon=0
	If p\horizon&gt;1 Then p\horizon=1
	
	; calculate the background color to simulate atmosphere penetration
	p\clscol=(1-(5.0/Exp(p\glowscale*p\angle/2.0)))*p\intensity
	If p\clscol&lt;0 Then p\clscol=0
	If p\clscol&gt;1 Then p\clscol=1
	
End Function


; Player movement
Function Movement()
	
	Local roll#,cx#,cz#,multi%,limit#
	Local t1#,t2#
	
	; get mouse position
	MX=MouseX()
	MY=MouseY()
	
	; Movement with speed limit
	cx=(KeyDown(205)-KeyDown(203))*Speed
	cz=(KeyDown(200)-KeyDown(208))*Speed
	
	; LMB = adjust Camerazoom
	If MouseDown(1) Then CameraZoom Cam,10 Else CameraZoom Cam,1
	
	; RMB or RSHIFT = speedup 10x
	If MouseDown(2) Or KeyDown(54) Then multi=10 Else multi=1
	
	; Arrow left/right = roll
	If KeyDown(203) Then roll=RollSpeed
	If KeyDown(205) Then roll=-RollSpeed
	
	; Normalize Mouse position (-1 to +1)
	t1=Normalize(MY,0,Screenheight,-1,1)
	t2=Normalize(MX,0,Screenwidth,1,-1)
	
	; Rotate ship mesh and turn player pivot
	RotateEntity Ship,t1*MaxRoll,t2*MaxRoll,t2*MaxRoll*2
	TurnEntity Player,t1*TurnSpeed,t2*TurnSpeed,roll*TurnSpeed
	
	; Move the player forward/backward
	MoveEntity Player,0,0,cz*Abs(mm)*multi
	
End Function


; quick distance calculation between two 3D points
Function Distance#(x1#,y1#,z1#,x2#,y2#,z2#)
	
	Return Sqr((x1-x2)*(x1-x2)+(y1-y2)*(y1-y2)+(z1-z2)*(z1-z2))
	
End Function


; calculates moved units per second
Function CalcSpeed#(interval%=1000)
	
	Local ms%=MilliSecs()
	
	; check each second
	If ms&gt;speedtimer+interval Then
		
		; store current position
		newx=simx
		newy=simy
		newz=simz
		
		; calculate Distance between current and old position
		movedistance=Distance(oldx,oldy,oldz,newx,newy,newz)
		
		; store old position
		oldx=simx
		oldy=simy
		oldz=simz
		
		; reset timer
		speedtimer=ms
		
	EndIf
	
End Function

; Normalize a value
Function Normalize#(value#=128.0,value_min#=0.0,value_max#=255.0,norm_min#=0.0,norm_max#=1.0)
	
	Return ((value-value_min)/(value_max-value_min))*(norm_max-norm_min)+norm_min
	
End Function


; creates a 3D wireframe orbit line
Function CreateOrbit(segments%,mesh=0,r1#=1,r2#=1.5,r%=255,g%=255,b%=255,a#=1.0)
	
	Local la#,na#,i%
	
	For i = 1 To segments
		
		na=la+(360.0/segments)
		mesh=CreateLine(mesh,Cos(la)*r1,0,Sin(la)*r2,Cos(na)*r1,0,Sin(na)*r2,r,g,b,a)
		la=na
		
	Next
	
	Return mesh
	
End Function


; creates a 3D line (for orbit)
Function CreateLine(mesh,x0#,y0#,z0#,x1#,y1#,z1#,r%=255,g%=255,b%=255,a#=1.0) 
	
	Local surf%,v1%,v2%
	
	If mesh Then
		
		surf=GetSurface(mesh,1)
		
	Else
		
		mesh = CreateMesh() 
		surf = CreateSurface(mesh) 
		EntityFX mesh,1+2+16+32
		
	End If 
	
    v1=AddVertex(surf,x1,y1,z1)
	v2=AddVertex(surf,x0,y0,z0)
	AddTriangle surf,v1,v1,v2
	
    VertexColor surf,v1,r,g,b,a
	VertexColor surf,v2,r,g,b,a
	
	Return mesh 
	
End Function


; advanced quad creation
Function CreateQuad(parent%=False,scale#=1.0,tex%=False,blend%=False,fx%=False,r%=255,g%=255,b%=255,a#=1.0)
	
	; create mesh and surface
	Local mesh%=CreateMesh()
	Local surf%=CreateSurface(mesh)
	
	; add vertices
	Local v0%=AddVertex(surf, 1, 1,0,0,0)		; upper left
	Local v1%=AddVertex(surf,-1, 1,0,1,0)		; upper right
	Local v2%=AddVertex(surf,-1,-1,0,1,1)		; lower right
	Local v3%=AddVertex(surf, 1,-1,0,0,1)		; lower left
	
	; connect vertices
	AddTriangle surf,v0,v1,v2
	AddTriangle surf,v0,v2,v3
	
	; parent, fx, texcture and blending
	If parent Then EntityParent mesh,parent
	If fx Then EntityFX mesh,fx
	If tex Then EntityTexture mesh,tex
	If blend Then EntityBlend mesh,blend
	
	; color and alpha
	EntityColor mesh,r,g,b
	EntityAlpha mesh,a
	
	; vertexcolor and vertexalpha
	VertexColor surf,v0,r,g,b,a
	VertexColor surf,v1,r,g,b,a
	VertexColor surf,v2,r,g,b,a
	VertexColor surf,v3,r,g,b,a
	
	; scale
	ScaleEntity mesh,scale,scale,scale
	
	Return mesh
	
End Function


; Create a simple but impressive sun texture
Function CreateSunTexture()
	
	Local tex%=CreateTexture(512,512,3)
	Local tb%=TextureBuffer(tex)
	
	Local i#,j%,col%,rgb%
	
	SetBuffer tb
	LockBuffer tb
	
	; Intensity steps
	For j=0 To 255
		
		col=255-j
		If col&gt;255 Then col=255
		rgb=col*$1000000+col*$10000+col*$100+col
		
		; Draw circles
		For i=0 To 360 Step 0.1
			WritePixelFast 256+(Sin(i)*j),256+(Cos(i)*j),rgb,tb
		Next
		
	Next
	
	UnlockBuffer tb
	SetBuffer BackBuffer()
	
	Return tex
	
End Function</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> It's looking absolutley beautiful, and very smooth/fast too!<br><br>I dunnoif it's important for your use, though your <pre class=code>p\radius=(4+3*2^(i-1))*1000
</pre> is very similar.<br><br>Unless you want astronomically accurate mean distances for your planets, a quick method (especially if static) is to use the Titus-Bode approximation:<br><br><pre class=code>
Function Titus_Bode_Distance#(Planet_Number%)
Return 0.4+(0.3*(2^(Planet_Number-2)))
End Function
</pre><br><br>You also need to assume a value for dwarf-planets etc.  such as the asteroid field between Mars and Jupiter to maintain consistency, so the 'Planet_Number' list should be:<br><br>1 - Mercury<br>2 - Venus<br>3 - Earth<br>4 - Mars<br>5 - (Asteroids)<br>6 - Jupiter<br>7 - Saturn<br>8 - Uranus<br>9 - Pluto<br><br>Note the exception of Neptune, due to its greatly obliquatwed orbit and interference from Pluto and other TNO gravitational effects.<br><br>The approximation still gives excellent results quickly. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
