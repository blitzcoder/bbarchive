<!DOCTYPE html><html lang="en" ><head ><title >your own gadgets! (Blitzplus, average skills)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >your own gadgets! (Blitzplus, average skills)</h1><a href="forums.php" >BlitzPlus Forums</a>/<a href="topics.php?forum=100" >BlitzPlus Tutorials</a>/<a href="#bottom" >your own gadgets! (Blitzplus, average skills)</a><br><br>
<a name="340207"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> When the B+ gadgets don't satisfy your needs anymore, and you fear that BlitzMax will still take a while, then read on!<br><br>Using banks, you can do your own gadgets, which can be way more advanced than any 'internal' gadget. The basic idea is that you store all your gadget-properties in a bank, and use the bankhandle for everything gadget-related (drawing, modifying etc.)<br><br>Compare:<br><br>window=CreateWindow("title",0,0,640,480)<br><br>with<br><br>bla=CreateSomething(32,32,64,24,window)<br><br>From this perspective, working with banks is the most clean method to do your own gadgets. And after adding "CreateSomething(x,y,width,height,parent)" to a .decl file to make it highlight in your code, it feels like you're working with an internal gadget!<br><br>The only 2 downsides are that you need your own events-checks and that the normal gadget modifers (SetGadgetShape etc. etc.) won't work on these new gadgets). But personally I'd say these downsides are really no match for all the possible goodies that are in store for you!<br><br>Basically there're several functions that you always need:<br>- a create function<br>- an update function<br><br>and if the function should react to events then you need another one:<br>- an event function<br><br>additionally, if you wish to read or write properties of your gadget then you need these 2 functions aswell:<br>- get functions<br>- set functions<br><br>A create function creates a bank for you in which to store all your properties.<br><br>An update function draws your gadget.<br><br>An event function does all the event handling and can modify your gadget properties.<br><br>And set &amp; get functions write or read properties.<br><br>Note that once you created your own gadget, the gadget handle is the only variable you'll ever need in your source. You don't need globals or other vars, for the simple reason that all vars are stored inside the gadget-bank.<br><br>Another example of custom gadget I already made is a window. This window has a border based on a bitmap and with a get function you get back the panel of that custom window. On this panel you can place blitz gadgets or your own gadgets. With a little effort you can make a gadget that contains even more gadgets. In this case the sub-gadget event-checks are inside the parent event-checks. In short, a scrollbar would be made out of 1 main gadget with 3 subgadgets (an up-button, a down-button and the scrollbar itself). This works perfectly! And you need only one event function call in your mainloop.<br><br>With a little more creativity you can load images inside a create-function, and store the image-handles inside the bank. A primitive image-button is within everyone's reach then.<br><br>There are more ways to enhance the whole gadget system. You could for example use the first 4 bytes of each bank to store some ID. By checking on the ID you can make sure that you don't accidentaly send the wrong bankhandle to an eventchecker, updater or get/set.<br><br>Gadgets can be complete large objects, one of my own gadgets is a complete zoom window (to draw tiles/sprites). Once I created it, and once I put an events checker in my mainloop I can draw in it with 2 colors! This way, building apps is a piece o' cake!<br><br>The source below is as small as possible, without all the comfy extras, to make it a bit more readable. The color-system could be somewhat more advanced as well (didn't want to spend more than a few seconds to it :), but the whole idea is clear.<br><br><pre class=code>
;
; your own gadgets in Blitzplus using banks
;
; tutorial by CS^TBL - 2004
;


app=CreateWindow("gadget example",0,0,640,480)

; a single something
something=CreateSomething(32,32,64,24,app,80,120,160)


; an array of somethings  (nice way to do a virtual drumcomputer this way ^^;)

Dim bla(31)

For t=0 To 31

	bla(t)=CreateSomething(32+(t*16),128,16,32,app,192,0,0) ; create a row of somethings
	
	If Rnd(1,5)&lt;2 SetSomethingState(bla(t),True) ; switch on a few
	
Next


;----------------------------------------------------------------------------------------------------
; ----- MAIN loop -----------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------------
quit=False

Repeat
	WaitEvent()
	If EventID()=$803 quit=True


	; check on events
	SomethingEvents(something)
	
	For t=0 To 31
		SomethingEvents(bla(t))
	Next
	
Until quit

;----------------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------------

; and free all the somethings again
FreeBank something
For t=0 To 31
	FreeBank bla(t)
Next

End ; bye!


;----------------------------------------------------------------------------------------------------
Function GetSomethingState(bank)
	If BankSize(bank)&lt;8 End
	Return PeekByte(bank,4)
End Function

;----------------------------------------------------------------------------------------------------
Function SetSomethingState(bank,state)
	If BankSize(bank)&lt;8 End
	PokeByte bank,4,state 
	UpdateSomething(bank)
End Function

;----------------------------------------------------------------------------------------------------
Function SomethingEvents(bank)

	If BankSize(bank)&lt;8 End

	canvas=PeekInt(bank,0)
	status=PeekByte(bank,4)
	
	If EventSource()=canvas
	
		If EventID()=$201
		
			If EventData()=1

				status=(status+1) Mod 2
				
				PokeByte bank,4,status
				
				UpdateSomething(bank)

			EndIf
		EndIf
	
	EndIf	

End Function

;----------------------------------------------------------------------------------------------------
Function UpdateSomething(bank)

	If BankSize(bank)&lt;8 End

	canvas=PeekInt(bank,0)
	status=PeekByte(bank,4)

	r=PeekByte(bank,5)
	g=PeekByte(bank,6)
	b=PeekByte(bank,7)
	
	width=GadgetWidth(canvas)
	height=GadgetHeight(canvas)
	
	SetBuffer CanvasBuffer(canvas)
	
		Color 0,0,0:Rect 0,0,width,height,False
		Color 255,255,255:Rect 1,1,width-2,height-2,False
		
		r=Limit(r+(status*64),0,255)
		g=Limit(g+(status*64),0,255)
		b=Limit(b+(status*64),0,255)

		Color r,g,b

		Rect 2,2,width-4,height-4,True
		
	FlipCanvas canvas

End Function

;----------------------------------------------------------------------------------------------------
Function CreateSomething(x,y,width,height,parent,r,g,b)

	; adr	name			size
	; --------------------------------------
	; 00	canvas		1 int ( 4 bytes)
	; 04	status		1 byte
	; 05	red			1 byte
	; 06	greens		1 byte
	; 07	blue			1 byte
	;
	; total: 8 bytes
	
	bank=CreateBank(8)
	canvas=CreateCanvas(x,y,width,height,parent)
	
	PokeInt bank,0,canvas
	PokeByte bank,4,status
	PokeByte bank,5,r
	PokeByte bank,6,g
	PokeByte bank,7,b
	
	UpdateSomething(bank)
	
	Return bank
	
End Function

;----------------------------------------------------------------------------------------------------
;-- just a handy global function --------------------------------------------------------------------
;----------------------------------------------------------------------------------------------------
Function Limit(value,minvalue,maxvalue)
	If value&lt;minvalue Return minvalue
	If value&gt;maxvalue Return maxvalue
	Return value
End Function
</pre> <br><br></td></tr></table><br>
<a name="340276"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Red</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Very interesting <br><br></td></tr></table><br>
<a name="351466"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Agamer</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> LOoks a good way to make up for the lack of gadjets <br><br></td></tr></table><br>
<a name="351482"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> here's another example.. a scroll-panel<br><br>Notice that found a dumb thingy while preparing this post.. I'm too lazy to fix it however.. :)<br><br>For obvious reasons the scrollpanel needs to be as wide as the parent-panel.. so having an arguement stating the width of you scrollpanel would'n much useful when you can get the gadgetwidth of the parent panel to get that width ;)<br><br><br>anyway (it's just an example, soon enough you should be making your own gadgets anyway), enjoy.. <br><br><pre class=code>
app=CreateWindow("",0,0,640,480)


; create 2 parent panels
panel1=CreatePanel(20,20,256,256,app)
panel2=CreatePanel(320,20,256,256,app)

; create the scrollpanels
scrollpanel1=CreateVScrollpanel(256,768,panel1,32,160,160,160)
scrollpanel2=CreateVScrollpanel(256,512,panel2,32,160,160,160)

; put some example crap on the scrollpanels
For t=0 To 10
	CreateButton(t,32,20+t*64,64,24,VScrollpanelPanel(scrollpanel1)) 
	CreateButton(t,32+t*5,20+t*32,64,24,VScrollpanelPanel(scrollpanel2)) 
Next

; main loop
quit=False
Repeat
	WaitEvent()
	
	If EventID()=$803 quit=True

	; if you don't include these, you can't scroll!	
	VScrollpanelEvents (scrollpanel1)
	VScrollpanelEvents (scrollpanel2)
	
Until quit

; clean-up
FreeBank scrollpanel1
FreeBank scrollpanel2

; bye!
End







; returns the actual panelhandle of the scrollpanel..
Function VScrollpanelPanel(bank)
	Return PeekInt(bank,4)
End Function



Function VScrollpanelEvents(bank)

	canvas=PeekInt(bank,8)
	
	If EventSource()=canvas
	
		If EventID()=$201 
			PokeByte bank,19,1 ; drag=true
			PokeShort bank,20,MouseY() ; start coord.
		EndIf
		
		If EventID()=$202
			PokeByte bank,19,0 ; drag=false
		EndIf
		
		If PeekByte(bank,19) ; drag ?
			If EventID()=$203
			
				y=MouseY()
				
				panel=PeekInt(bank,4)
				oldy=PeekShort(bank,20)
				
				ycoord=GadgetY(panel)+y-oldy
				ycoord=Limit(ycoord,-PeekShort(bank,14)+PeekShort(bank,22),0)
				
				SetGadgetShape panel,GadgetX(panel),ycoord,GadgetWidth(panel),GadgetHeight(panel)

				PokeShort bank,20,y
				
			EndIf
		EndIf
		
	EndIf

End Function

Function CreateVScrollpanel(width,height,parent,dragwidth,r,g,b)

	If width*height=0 Break"Incorrect dimensions!"
	If height&lt;GadgetHeight(parent) Break"Panel height-bigger than parent-height!"
	If Not parent Break"Parent missing"
	If Not dragwidth Break"Dragbar zero"
	r=Limit(r,0,255)
	g=Limit(g,0,255)
	b=Limit(b,0,255)
	
	; adr	name		size
	; 00	ID			4 bytes    &lt;- reserved for an optional 4-chars ID check! not used right now..
	; 04	panel		1 int (4 bytes)
	; 08	dragcanvas	1 Int (4 bytes)
	; 12	width		1 short (2 bytes)
	; 14	height		1 short (2 bytes)
	; 16	r			1 byte
	; 17	g			1 byte
	; 18	b			1 byte
	; 19	dragstatus	1 byte
	; 20	dragY		1 short (2 bytes)
	; 22	parntheight	1 short (2 bytes)

	bank=CreateBank(24)
	
	panel=CreatePanel(0,0,width,height,parent)
	SetPanelColor panel,r,g,b
	canvas=CreateCanvas(width-dragwidth,0,dragwidth,height,panel)
	SetCanvas canvas
		ClsColor r,g,b:Cls
		For y=2 To height-1 Step 4
			For x=2 To dragwidth-1 Step 4
				xo=(y Mod 8)/4
				Bcolor r*1.3,g*1.3,b*1.3
				Plot x+xo,y
				Bcolor r*0.7,g*0.7,b*0.7
				Plot x+xo+1,y+1
			Next
		Next
		Bcolor r*1.3,g*1.3,b*1.3
		Line 0,0,0,height-2
		Line 0,0,dragwidth-2,0
		
		Bcolor r*0.7,g*0.7,b*0.7
		Line dragwidth-1,1,dragwidth-1,height-2
		Line 1,height-1,dragwidth-2,height-1
		
	FlipCanvas canvas
	
	PokeInt bank,4,panel
	PokeInt bank,8,canvas
	PokeShort bank,12,width
	PokeShort bank,14,height
	PokeByte bank,16,r
	PokeByte bank,17,g
	PokeByte bank,18,b
	
	PokeShort bank,22,GadgetHeight (parent)
	
	Return bank

End Function

; misc. needed global functions.. some useful, some just for the lazy coder (SetCanvas) :)

Function Limit(value,minvalue,maxvalue)
	If value&lt;minvalue Return minvalue
	If value&gt;maxvalue Return maxvalue
	Return value
End Function

Function Break(s$)
	Notify s$
	End
End Function

Function Bcolor(r,g,b)
	r=Limit(r,0,255)
	g=Limit(g,0,255)
	b=Limit(b,0,255)
	Color r,g,b
End Function

Function SetCanvas(bla)
	SetBuffer CanvasBuffer(bla)
End Function
</pre> <br><br></td></tr></table><br>
<a name="1025340"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stamm</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> That's really very useful. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
