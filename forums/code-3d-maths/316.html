<!DOCTYPE html><html lang="en" ><head ><title >Area Collision Lib</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='As the title says.., language=bb, category=3D Graphics - Maths'><meta name='author' content='David Bird(Birdie)'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=13>3D Graphics - Maths</a>/Area Collision Lib</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="316.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Area Collision Lib by David Bird(Birdie)</td><td align="right">2002 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> As the title says.. </td></tr><tr ><td class="cell"><pre class="code">;
;	Area Collision Library
;	David Bird
;	enquire@davebird.fsnet.co.uk
;
;syntax

;SetSphereRegion(posx,posy,posz,radius)
;SetCubeRegion(posx,posy,posz,size)
;SetRectRegion(posx,posy,posz,width,height,depth)
;PointInRegion(regType,checkx,checky,checkz)
;SphereInRegion(regType,checkx,checky,checkz,radius to check)
;EntityInRegion(regType,Entity,Radius) Problem No way of obtaining entity radius
;FreeAllRegions()
;FreeRegion(r.reg)
;GetEntityRegion.reg(ent,x#,y#,z#,rd#=1)
;GetSphereRegion.reg(x#,y#,z#,rad#)
;GetPointRegion.reg(x#,y#,z#)

Global AC_DEBUG=False ;set this this to true to show regions
Type reg
	Field typ	;0-sphere 1-cube 2-rect
	Field piv
	Field rad#
	Field sx#
	Field sy#
	Field sz#
	Field lpiv
	Field tx#
	Field ty#
	Field tz#
	Field debugent
End Type
Function SetAC_DEBUG(d=False)
	AC_DEBUG=d
End Function

Function MoveRegion(r.reg,x#,y#,z#)
	MoveEntity r\piv,x,y,z
End Function

Function TurnRegion(r.reg,x#,y#,z#,Glob=0)
	TurnEntity r\piv,x,y,z,glob
End Function

Function PositionRegion(r.reg,x#,y#,z#,Glob=0)
	PositionEntity r\piv,x,y,z,Glob
End Function

Function TranslateRegion(r.reg,x#,y#,z#,Glob=0)
	TranslateEntity r\piv,x,y,z,Glob
End Function

Function SetSphereRegion.reg(x#,y#,z#,rad#,parent=0)
	r.reg=New reg
	r\piv=CreatePivot(parent)
	r\lpiv=CreatePivot()
	PositionEntity r\piv,x,y,z
	r\rad=rad
	r\typ=0			;only spheres unto now
	If AC_DEBUG=True Then
		t=CreateSphere(10,r\piv)
		EntityAlpha t,.25
		EntityFX t,16
		ScaleEntity t,rad,rad,rad
		r\debugent=t
	End If
	Return r
End Function

Function SetCubeRegion.reg(x#,y#,z#,size#,parent=0)
	r.reg=New reg
	r\piv=CreatePivot(parent)
	r\lpiv=CreatePivot()
	PositionEntity r\piv,x,y,z
	r\sx=size
	r\sy=size
	r\sz=size
	r\typ=1		;only spheres unto now
	If AC_DEBUG=True Then
		t=CreateCube(r\piv)
		EntityAlpha t,.25
		EntityFX t,16
		ScaleEntity t,size,size,size
		r\debugent=t
	End If

	Return r
End Function

Function SetRectRegion.reg(x#,y#,z#,w#,h#,d#,parent=0)
	r.reg=New reg
	r\piv=CreatePivot(parent)
	r\lpiv=CreatePivot()
	PositionEntity r\piv,x,y,z
	r\sx=w
	r\sy=h
	r\sz=d
	r\typ=1
	If AC_DEBUG=True Then
		t=CreateCube(r\piv)
		EntityAlpha t,.25
		EntityFX t,16
		ScaleEntity t,w,h,d
		r\debugent=t
	End If
	Return r
End Function

Function PointInRegion(r.reg,x#,y#,z#)
	typ=r\typ
	If typ=2 Then t=1
	Select typ
		Case 0								;-point in sphere
			dx#=EntityX(r\piv,True)-x
			dy#=EntityY(r\piv,True)-y
			dz#=EntityZ(r\piv,True)-z
			rad#=Sqr(dx^2+dy^2+dz^2)
			If rad&lt;r\rad Then Return True
		Case 1								;- point in cube or rectangle
			If x&lt;EntityX(r\piv,True)+r\sx And x&gt;EntityX(r\piv,True)-r\sx
				If y&lt;EntityY(r\piv,True)+r\sy And y&gt;EntityY(r\piv,True)-r\sy
					If z&lt;EntityZ(r\piv,True)+r\sz And z&gt;EntityZ(r\piv,True)-r\sz
						Return True
					End If
				End If
			End If			
	End Select
	Return False
End Function

Function EntityInRegion(ent,r.reg,rd#=1)
	Return SphereInRegion(r,EntityX(ent,True),EntityY(ent,True),EntityZ(ent,True),rd);??? radius
End Function

Function SphereInRegion(r.reg,x#,y#,z#,rd#)
	typ=r\typ
	If typ=2 Then t=1
	Select typ
		Case 0								;-Sphere in sphere
			dx#=EntityX(r\piv,True)-x
			dy#=EntityY(r\piv,True)-y
			dz#=EntityZ(r\piv,True)-z
			rad#=Sqr(dx^2+dy^2+dz^2)-rd
			If rad&lt;r\rad Then Return True
		Case 1								;-Sphere in cube
			dx#=EntityX(r\piv,True)-x
			dy#=EntityY(r\piv,True)-y
			dz#=EntityZ(r\piv,True)-z
			rad#=Sqr(dx^2+dy^2+dz^2)
			dx=dx/rad
			dy=dy/rad
			dz=dz/rad
			dx=EntityX(r\piv,True)-(dx*(rad-rd))
			dy=EntityY(r\piv,True)-(dy*(rad-rd))
			dz=EntityZ(r\piv,True)-(dz*(rad-rd))
			If dx&lt;EntityX(r\piv,True)+r\sx And dx&gt;EntityX(r\piv,True)-r\sx
				If dy&lt;EntityY(r\piv,True)+r\sy And dy&gt;EntityY(r\piv,True)-r\sy
					If dz&lt;EntityZ(r\piv,True)+r\sz And dz&gt;EntityZ(r\piv,True)-r\sz
						Return True
					End If
				End If
			End If			
	End Select
	Return False
End Function

Function GetPointRegion.reg(x#,y#,z#)
	;returns the first region that a point is within
	For r.reg=Each reg
		If PointInRegion(r,x,y,z)=True Then Return r
	Next
	Return Null
End Function

Function GetSphereRegion.reg(x#,y#,z#,rad#)
	;returns the first region that a point is within
	For r.reg=Each reg
		If SphereInRegion(r,x,y,z,rad)=True Then Return r
	Next
	Return Null
End Function

Function GetEntityRegion.reg(ent,rd#=1)
	;returns the first region that a point is within
	For r.reg=Each reg
		If EntityInRegion(ent,r,rd)=True Then Return r
	Next
	Return Null
End Function

Function FreeAllRegions()
	For r.reg=Each reg
		If r\piv&lt;&gt;0 Then FreeEntity r\piv
		If r\lpiv&lt;&gt;0 Then FreeEntity r\lpiv
		Delete r
	Next
End Function

Function FreeRegion(r.reg)
		If r\piv&lt;&gt;0 Then FreeEntity r\piv
		If r\lpiv&lt;&gt;0 Then FreeEntity r\lpiv
		Delete r
End Function

Function UpdateRegions()
	For r.reg=Each reg
		r\tx=EntityX(r\piv,True)-EntityX(r\lpiv,True)
		r\ty=EntityY(r\piv,True)-EntityY(r\lpiv,True)
		r\tz=EntityZ(r\piv,True)-EntityZ(r\lpiv,True)
		PositionEntity r\lpiv,EntityX(r\piv,True),EntityY(r\piv,True),EntityZ(r\piv,True)
	Next
End Function

;only used in debug
Function HideRegions()
	For r.reg=Each reg
		HideEntity r\debugent
	Next
End Function

Function ShowRegions()
	For r.reg=Each reg
		ShowEntity r\debugent
	Next
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
