<!DOCTYPE html><html lang="en" ><head ><title >Predict time of collision for two moving objects</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='A function for detecting the time when a collision will occur between two moving entities (maths only - no blitz specific 3d functions actually used), language=bb, category=3D Graphics - Maths'><meta name='author' content='Matty'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=13>3D Graphics - Maths</a>/Predict time of collision for two moving objects</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="3183.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Predict time of collision for two moving objects by Matty</td><td align="right">2015 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This function takes a set of parameters - the positions and velocities of a pair of moving objects and returns the time (in number of frames) that the entities will collide.<br><br>The minimum and maximum time that you wish to allow for is passed in as well, and also a tolerance (which should always be some figure greater than zero)<br><br>There is error handling and validation within the function - if the user passes in invalid combinations of minimum/maximum time then it will return a particular value.<br><br>The return value of the function is the number of frames / position updates at those particular velocities that the entities will collide.<br><br>If they will not collide (for whatever reason) then it will return a -1.<br><br><br><br>I have included an example in the function "example".  This example will let the user move a little dot(green) around the screen by clicking locations on the map, and an enemy dot (purple) will chase and shoot at the player (yellow dot) when they are able to hit (as detected by the function).<br><br>The mathematics are pretty straight forward.<br><br>The two objects have a position vector and a velocity vector which is dependent linearly on time 't'.<br><br>The intersection point on each axis is found in terms of 't'.<br>If the 't' value found for each axis is within a given tolerance then the average of those 't' value is returned.<br><br>The function escapes as soon as a condition is met that means they won't collide. Eg - the difference in velocity in any component direction is zero, or the time required for any of the components is outside the specified range.<br><br><br>As an extra piece of useless information - I will definitely be using this in my own code for a game I'm writing for detecting whether an AI should shoot at a target - and also detecting whether two moving spaceships are going to collide etc.....<br><br>Hope you find it useful too.<br><br>from Matt </td></tr><tr ><td class="cell"><pre class="code">;Author - Matt Lloyd 2015
;
;Function - whenwillicollide#()
;
;Used to determine the time at which a moving object (straight line) will collide with another moving object (straight line)
;
;The example (see example function) lets the user move a little 'spaceship' around the map by clicking on the screen, the AI unit
;will 'chase' and shoot at the player when they have a likely shot....of course - if the user changes direction then it will miss! (I'm not 
;a fortune teller!)
;
;
;Hopefully this comes in useful - I will find it useful anyway....
;




;test example of usage...
example()


;function definition..
Function whenwillicollide#(dimensions,posx#,posy#,posz#,velx#,vely#,velz#,targetposx#,targetposy#,targetposz#,targetvelx#,targetvely#,targetvelz#,mintime#,maxtime#,tolerance#=0.1)
;Parameters are:
;
;Dimensions is a value from 1 to 3....indicates the number of dimensions to use...
;For example - a 2d game would use '2', a 3d game would use '3', and I'm not sure why you would use less than 2 dimensions...but hey...
;
;
;Position of first entity x,y,z (floats)
;Velocity of first entity in x,y,z (floats)
;
;Position of second entity x,y,z (floats)
;Velocity of second entity in x,y,z (floats)
;
;Minimum time to accept (float)
;Maximum time to accept (float)
;
;Tolerance (float) - allowable difference between times in each coordinate system...optional value........
;
;Return values are:
;
;-1 if the vectors never intersect (either because they are skew lines or because they intersect outside of the allowable range or because they do not
;intersect on the same axis within the specified tolerance
;
;-2 if the mintime or maxtime are set incorrectly (ie less than zero or maxtime is less than mintime)
;
;-3 if the dimensions has an invalid value (must be either 2 or 3)
;
;
;Assumes two entities are NOT at the same location..ie not already collided!
;


Local intersectiontime#[2] ;array containing the intersection time of each coordinate....
;0 = x index, 1 = y index, 2 = z index

If(dimensions&lt;2 Or dimensions&gt;3) Then Return -3

If(mintime&lt;0 Or maxtime&lt;0 Or maxtime&lt;mintime) Then Return -2


If(targetvelx - velx)=0  Then Return -1
intersectiontime[0] = (posx - targetposx) / (targetvelx - velx)
If(intersectiontime[0]&lt;mintime Or intersectiontime[0]&gt;maxtime) Then Return -1
If(targetvely - vely)=0 Then Return -1
intersectiontime[1] = (posy - targetposy) / (targetvely - vely)
If(intersectiontime[1]&lt;mintime Or intersectiontime[1]&gt;maxtime) Then Return -1
If (Abs(intersectiontime[1]-intersectiontime[0]))&gt;tolerance Then Return -1
If(dimensions=2) Then Return (intersectiontime[0]+intersectiontime[1])*0.5
If(targetvelz - velz)=0 Then Return -1
intersectiontime[2] = (posz - targetposz) / (targetvelz - velz)
If(intersectiontime[2]&lt;mintime Or intersectiontime[2]&gt;maxtime) Then Return -1
If (Abs(intersectiontime[2]-intersectiontime[1]))&gt;tolerance Then Return -1
If (Abs(intersectiontime[2]-intersectiontime[0]))&gt;tolerance Then Return -1

Return (intersectiontime[0]+intersectiontime[1]+intersectiontime[2])*0.33333

End Function

Function example()
;
;
;2 dimensional example....

Local maxvel = 2.0,maxbulletvel = 4.0
Local playerX#,playerY#,playervelX#,playervelY#,playeraccelX#,playeraccelY#
Local enemyX#,enemyY#,enemyvelX#,enemyvelY#,enemyaccelX#,enemyaccelY#
Local enemybulletX#,enemybulletY#,enemybulletvelX#,enemybulletvelY#,enemybulletlife
Local startedchasing=0


Graphics 512,512,0,2

playerx = Rnd(256)+128
playery = Rnd(256)+128
playervelx = 0
playervely = 0
playeraccelx = 0
playeraccely = 0

enemyx = Rnd(256)+128
enemyy = Rnd(256)+128

enemybulletx = -1
enemybullety = -1
enemybulletlife = 0


SetBuffer BackBuffer()
Repeat

playerx = playerx + playervelx
playery = playery + playervely

playervelx = playervelx + playeraccelx
playervely = playervely + playeraccely

If(playervelx*playervelx+playervely*playervely)&gt;maxvel*maxvel Then 
	playervelx = playervelx * maxvel/Sqr((playervelx*playervelx+playervely*playervely))
	playervely = playervely * maxvel/Sqr((playervelx*playervelx+playervely*playervely))
EndIf

enemyx = enemyx + enemyvelx
enemyy = enemyy + enemyvely

enemyvelx = enemyvelx + enemyaccelx
enemyvely = enemyvely + enemyaccely

If(enemyvelx*enemyvelx+enemyvely*enemyvely)&gt;maxvel*maxvel Then 
	enemyvelx = enemyvelx * maxvel/Sqr((enemyvelx*enemyvelx+enemyvely*enemyvely))
	enemyvely = enemyvely * maxvel/Sqr((enemyvelx*enemyvelx+enemyvely*enemyvely))
EndIf

;move the bullet....
If(enemybulletlife&gt;0) Then

enemybulletx = enemybulletx + enemybulletvelx
enemybullety = enemybullety + enemybulletvely
enemybulletlife = enemybulletlife - 1


EndIf


If(MouseDown(1)) Then ;player provides thrust towards point pressed by mouse....
	dx# = MouseX() - playerx
	dy# = MouseY() - playery
	length# = dx*dx+dy*dy
	If(length&gt;0)
		length = Sqr(length)
		dx = 0.65*dx/length
		dy = 0.65*dy/length
		
		playeraccelx = dx
		playeraccely = dy
		startedchasing = 1
	EndIf

EndIf 

;enemy tracks player....and moves around chasing them......(after the player starts moving)
If(startedchasing=1) Then 
	dx# = playerx - enemyx
	dy# = playery - enemyy
	length# = dx*dx+dy*dy
	If(length&gt;0)
		length = Sqr(length)
		dx = 0.55*dx/length
		dy = 0.55*dy/length
		
		enemyaccelx = dx
		enemyaccely = dy
		startedchasing = 1
	EndIf

	If(enemybulletlife&lt;=0) Then 
		;;;see if we should fire a bullet.....
		
		;bullet starts from enemy pos
		enemybulletx = enemyx
		enemybullety = enemyy
		
		;bullet starts with same velocity as enemy (ie good old vector addition - assuming non relativistic speeds!
		enemybulletvelx = enemyvelx
		enemybulletvely = enemyvely
		
		
		dx# = playerx - enemybulletx
		dy# = playery - enemybullety
		length# = dx*dx+dy*dy
		If(length&gt;0)
			length = Sqr(length)
			dx = dx / length
			dy = dy / length
			
			;bullet velocity will be faster than ships - it will be..twice as fast as the maxvel...
			enemybulletvelx = enemybulletvelx + dx
			enemybulletvely = enemybulletvely + dy
			
			If(enemybulletvelx*enemybulletvelx+enemybulletvely*enemybulletvely)&gt;(maxbulletvel)*(maxbulletvel) Then
				enemybulletvelx = enemybulletvelx * (maxbulletvel) / Sqr((enemybulletvelx*enemybulletvelx+enemybulletvely*enemybulletvely))
				enemybulletvely = enemybulletvely * (maxbulletvel) / Sqr((enemybulletvelx*enemybulletvelx+enemybulletvely*enemybulletvely))
			EndIf 
			
			;okay but will we hit our target? if so then we want to fire...
			;now....the life of the bullet we will say is...60 frames....just an arbitrary number...can be anything....but most games I've written usually have a maximum 'lifetime' of the bullet (especially useful if it continues to fly off into deep space!)
			
			;this is where I call the function above - if it returns a non zero value then I want to fire!
			enemybulletlife = whenwillicollide(2,enemybulletx,enemybullety,0,enemybulletvelx,enemybulletvely,0,playerx,playery,0,playervelx,playervely,0,0,300,(maxbulletvel+maxvel+2))			

			If(enemybulletlife&lt;0)
				enemybulletlife = 0
			Else
				;enemybulletlife = 60 ;(uncomment this and the bullet will only travel until it reaches where it should have hit the player at...)
			EndIf	
		EndIf
	EndIf 


EndIf
Cls
Color 0,255,0
Rect playerx-1,playery-1,3,3,1

Color 255,0,255
Rect enemyx-1,enemyy-1,3,3,1

Color 255,255,0
If(enemybulletlife&gt;0) Then 
	Rect enemybulletx-1,enemybullety-1,3,3,1
EndIf
Flip

Until KeyDown(1)

End


End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rick Nasher</td><td align="right"><font class=tiny>2015</font></td></tr></table></td></tr><tr ><td class="posttext"> My compliments. Might come in handy and should be fairly easy to use in 3d environement too as you took provisions for that. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
