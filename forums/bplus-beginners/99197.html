<!DOCTYPE html><html lang="en" ><head ><title >How To Use LockedPixels() ?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >How To Use LockedPixels() ?</h1><a href="forums.php" >BlitzPlus Forums</a>/<a href="topics.php?forum=95" >BlitzPlus Beginners Area</a>/<a href="#bottom" >How To Use LockedPixels() ?</a><br><br>
<a name="1163299"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> From the limited information given, LockedPixels returns a 'bank' - I assumed this to be a BankHandle as used by the other Bank commands within B+, however, the compiler crashes (as in actually exits with a windows error, not a standard compilation/Blitz error message) when it reaches the CopyBank function here:<br><br><pre class=code>
	Local Buffer=GraphicsBuffer()
	
	Local Image=LoadImage(Path)
	If (Not (Image))
		Return
	End If
	
	Local IB=ImageBuffer(Image)
	
	Local W=ImageWidth(Image)
	Local H=ImageHeight(Image)
	Local Pixels=W*H
	Local Size=Pixels Shl 2
	
	Local White
	Local Black
		
	Local Bank=CreateBank(Size)
	
	LockBuffer(IB)
	Local Temp=LockedPixels(IB)

;Next Line Causes Crash!
	CopyBank Temp,0,Bank,0,Size</pre><br><br>Clearly this is the wrong usage, but aside from manually POKEing each pixel (as one would with Blitz3D), how can LockedPixels be put to effective use? <br><br></td></tr></table><br>
<a name="1163301"></a>

<a name="1163302"></a>

<a name="1173992"></a>

<a name="1173993"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's something I posted three years ago. I don't remember when I actually wrote the code.<br><br><a href="/posts.php?topic=88265#1002125" target="_blank">LockedPixels quick intro</a><br><br>Note the buffer is 200 pixels wide, and my graphics card at that time used 832 bytes. It was padding the image buffer so the number of pixels was a multiple of 16, so number of bytes is a multiple of 64.<br><br>I just ran the code again and the pitch ( width in bytes ) is 896. At 4 bytes per pixel that is 224 pixels. So my current card/driver is padding each row with an extra 24 pixels. I don't know why it needs all that extra space, but it shows you can't make any assumptions about the true width of the buffer. You have to rely on LockedPitch().<br><br><font class="tiny">Last edited 2013</font> <br><br></td></tr></table><br>
<a name="1163396"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's a nice plasma demo, which I changed slightly to match my 1920x1080 monitor.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Global SCREEN_WIDTH=1920, SCREEN_HEIGHT=1080
Graphics SCREEN_WIDTH,SCREEN_HEIGHT,32,1


; Plasma effect written by Mark Tiffany for Viper menu screens.
Global Plasma_Cycle
Global Plasma_Width
Global Plasma_Height
Global Plasma_Image
Const MAXSIN=256
Global LSinB
Global Plasma_Speed=0
Global Plasma_On=True

;----------------------- TEST CODE
;

SetBuffer BackBuffer()
ClsColor 255,255,255
Cls
PlasmaSetup()
Repeat
	SetBuffer BackBuffer()
	PlasmaDraw()
	Flip False
	If KeyHit(1) Then Exit
Forever
End

;----------------------- END OF TEST CODE

Global buffer_fmt,buffer_bank,buffer_offset_x,buffer_offset_y,buffer_base
Global buffer_offset_x2,buffer_offset_x4,buffer_offset_x8
Global buffer_offset_y2,buffer_offset_y4,buffer_offset_y8
Global buffer_rowsize,buffer_rowsize2,buffer_rowsize4,mask

Function PlasmaSetup()
	Plasma_Image=CreateImage(SCREEN_WIDTH,SCREEN_HEIGHT,1,4)
	buff=ImageBuffer(Plasma_Image)
	SetBuffer buff
	ClsColor 255,255,255
	Cls	
	LockBuffer(buff)
	buffer_fmt=LockedFormat(buff)
	buffer_bank=LockedPixels(buff)
	buffer_offset_y=LockedPitch(buff)
	If buffer_fmt=1 Then 
		buffer_offset_x=4
		buffer_base=17
		mask=$40
	ElseIf buffer_fmt=1 Then 
		buffer_offset_x=4
		buffer_base=17
		mask=$20		
	ElseIf buffer_fmt=3 Then
		buffer_offset_x=3
		buffer_base = 140
		mask=$FF
	ElseIf buffer_fmt=4 Then
		buffer_offset_x=4
		buffer_base = 140
		mask=$FF
	End If	
	UnlockBuffer(buff)
	
	LSinB = CreateBank(MAXSIN+1)
	
	For n=0 To MAXSIN
		If buffer_fmt&lt;3 Then
			PokeByte LSinB,n,buffer_base + 6 * Sin(n*360.0/256.0)
		Else
			PokeByte LSinB,n, buffer_base + 50 * Sin(n*360.0/256.0)
		End If
	Next
	Plasma_Width = Ceil((SCREEN_WIDTH/8)+0.5)-2
	Plasma_Height = Ceil((SCREEN_HEIGHT/8)+0.5)-2

	buffer_offset_x2 = buffer_offset_x * 2
	buffer_offset_x4 = buffer_offset_x * 4
	buffer_offset_x8 = buffer_offset_x * 8
	buffer_offset_y2 = buffer_offset_y * 2
	buffer_offset_y4 = buffer_offset_y * 4	
	buffer_offset_y8 = buffer_offset_y * 8
	buffer_rowsize   = buffer_offset_x * SCREEN_WIDTH
	buffer_rowsize2  = buffer_rowsize * 2
	buffer_rowsize4  = buffer_rowsize * 4
	
	time=MilliSecs()
	PlasmaDraw()
	Plasma_Speed=MilliSecs()-time
	If Plasma_Speed&gt;100 Then
		Plasma_On=False
	Else
		Plasma_On=True
	End If
	
End Function

Function PlasmaDraw()
	Local rgb
	Local base_r,base_g,base_b
	Local r,g,b
	
	If Plasma_On Then
		
		; cycle
		If Plasma_Cycle &gt; 254 Then
			Plasma_Cycle = 0
		Else
			Plasma_Cycle = Plasma_Cycle + 1
		End If
	
		buff=ImageBuffer(Plasma_Image)
		SetBuffer buff
		LockBuffer(buff) 
		
		base_r=Plasma_Cycle
		base_g=192+Plasma_Cycle
		base_b=256-Plasma_Cycle
		
		For y = 0 To Plasma_Height
			offset = buffer_offset_y8 * y 
			ioffset = offset
			y2=y Shl 1
			r = base_r+y2
			g = base_g+y2
			b = base_b-y 
			For x = 0 To Plasma_Width
				rr = PeekByte(LSinB,((r - x) And mask))
				gg = PeekByte(LSinB,((g + x + x) And mask))
				bb = PeekByte(LSinB,((b - x) And mask))			
				If buffer_fmt=1 Then
					rgb=(rr Shl 11) Or (gg Shl 5) Or bb
					rgb=(rgb Shl 16) Or rgb
				ElseIf buffer_fmt=2
					rgb=(rr Shl 10) Or (gg Shl 5) Or bb
					rgb=(rgb Shl 16) Or rgb
				ElseIf buffer_fmt=3
					rgb=(rr Shl 16) Or (gg Shl 8) Or bb
				ElseIf buffer_fmt=4
					rgb=$7F000000 Or (rr Shl 16) Or (gg Shl 8) Or bb
				End If		
							
				PokeInt buffer_bank,offset,rgb
				CopyBank buffer_bank,offset,buffer_bank,offset+buffer_offset_x,buffer_offset_x
				CopyBank buffer_bank,offset,buffer_bank,offset+buffer_offset_x2,buffer_offset_x2
				CopyBank buffer_bank,offset,buffer_bank,offset+buffer_offset_x4,buffer_offset_x4				
				offset=offset+buffer_offset_x8
			Next
			CopyBank buffer_bank,ioffset,buffer_bank,ioffset+buffer_offset_y,buffer_rowsize
			CopyBank buffer_bank,ioffset,buffer_bank,ioffset+buffer_offset_y2,buffer_rowsize2
			CopyBank buffer_bank,ioffset,buffer_bank,ioffset+buffer_offset_y4,buffer_rowsize4
		Next	
		UnlockBuffer(buff)
	End If
	SetBuffer BackBuffer()
	DrawBlock Plasma_Image,0,0
End Function</textarea> <br><br></td></tr></table><br>
<a name="1163445"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks, Floyd -<br><br>I need to look into what's going on a bit more closely since I'm not completely understanding what's going on, but I should be able to figure it out from both examples you've provided. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
