<!DOCTYPE html><html lang="en" ><head ><title >recursion</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >recursion</h1><a href="forums.php" >BlitzPlus Forums</a>/<a href="topics.php?forum=95" >BlitzPlus Beginners Area</a>/<a href="#bottom" >recursion</a><br><br>
<a name="780108"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >julianbury</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello World!<br><br>I am trying to understand recursion.<br>Can anyone please show me how to make this test case work?<br><br><pre class=code>
Global csd						; currently selected directory
Global dir$, root$="C:\Program Files\" 			; lets try it on this
Global fileout=WriteFile("C:\log.txt")			; write a log here

ListDir( root$ )					; call the recursive function
CloseFile fileout					; close the log
End

Function ListDir( dir$ )
	WriteLine( fileout, dir$)
	csd=ReadDir(dir$)
	Repeat
		file$=NextFile(csd)
		If 2=FileType(file$) And file$&gt;".."	; a candidate?
			dir$=dir$+"\"+file$		; add to the filepath
			WriteLine( fileout, dir$)	; and record it
			csd=ReadDir(dir$)		; select the new path
			ListDir(dir$)			; and read the contents
		EndIf
		file$=NextFile(csd)
		If 1=FileType(file$) Then
			WriteLine(fileout, file$)	; log any filenames
		EndIf
	Until file$=""
End Function
</pre> <br><br></td></tr></table><br>
<a name="780280"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> In short: recursion is a function calling itself. To prevent infinite loops there's usually a check built-in.<br><br>A small 'n simple example:<br><pre class=code>rec 10
Notify "done"
End

Function rec(a,counter=0)
	If counter&gt;8 Return ; the check
	
	counter=counter+1
	
	a=a*2 ; the actual functionality of this function
	
	DebugLog "counter   = "+counter+", 'a' value = "+a
	DebugLog "---------------"
	
	rec a,counter ; and call it again.
End Function</pre> <br><br></td></tr></table><br>
<a name="780285"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> ..and ofcourse the famous recursive tree.. or plant..<br><br><pre class=code>window=CreateWindow("tree",0,0,640,480)
canvas=CreateCanvas(0,0,640,480,window)

SetBuffer CanvasBuffer(canvas)
	tree 320,240,32,180
FlipCanvas canvas

Repeat
	WaitEvent()
	If EventID()=$803 End
Forever

Function tree(x,y,l#,a,counter=0)
	If counter&gt;10 Return ; toy with the '10'
	counter=counter+1
	
	nx=x+Sin(a)*l
	ny=y+Cos(a)*l
	
	Line x,y,nx,ny
	
	l=l*.9 ; &lt;- to toy with
	tree nx,ny,l,a+20,counter ; toy with both '20' numbers
	tree nx,ny,l,a-20,counter
	
End Function</pre> <br><br></td></tr></table><br>
<a name="780648"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >julianbury</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you, CS_TBL, for those examples   :-)<br><br>Your first example makes the bare-bones machanism clear (excellant!).<br><br>I really wanted corrections for my routine so as to be able to print the contents of a folder and its sub-folders to a text file.<br><br>Is my code too messy to bother with?<br><br>Going up and down directories is what is most confusing to me.<br><br>Can you help with that?<br><br>Thank you for your kind attention  :-)<br><br>Julian (-_-) <br><br></td></tr></table><br>
<a name="780658"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> atm no, I'm trying to debug your stuff, but for some odd reason, filetype returns 0 for dirs and files.<br><br>[edit] arf, classic bug.. FileType(dir$+file$) <br><br></td></tr></table><br>
<a name="780666"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've more or less rebuilt the whole shebang, it now outputs a string rather than directly writing to a file. This way it's less hardwired, e.g. you can just view this list now, without having to load it back. Also, all the globals are gone. Globals = evil.<br><br>The Showtext function is not mandatory, it's just a small self-contained textviewer..<br>usage: Showtext text$,width,height<br>when omitted, width and height default to 640,480<br>when either width or height &lt;1 then the viewer is full-screen.<br><br><pre class=code>
Showtext ListDir$("i:\")

; mystring$=ListDir$("c:\")

End

Function ListDir$(dir$)
	
	csd=ReadDir(dir$)
	If Not csd Return

	Repeat
		file$=NextFile$(csd)
		If file$="" Exit

		If FileType(dir$+file$)=1
			out$=out$+dir$+file$+Chr$(13)+Chr$(10)
		EndIf
		
		If FileType(dir$+file$)=2 And file$&lt;&gt;".." And file$&lt;&gt;"."
			out$=out$+ListDir$(dir$+file$+"\")
		EndIf
	Forever
	Return out$
End Function

Function Showtext(t$,w=640,h=480)
	cw=ClientWidth(Desktop())
	ch=ClientHeight(Desktop())
	If w&lt;1 Or h&lt;1
		w=cw:h=ch
	EndIf
	If w&gt;cw w=cw
	If h&gt;ch h=ch
	window=CreateWindow("Show text",cw/2-w/2,ch/2-h/2,w,h,0,3)
	textarea=CreateTextArea(0,0,ClientWidth(window),ClientHeight(window),window)
	SetTextAreaColor textarea,128,192,255
	SetTextAreaColor textarea,0,32,64,1	
	SetTextAreaText textarea,t$
	Repeat
		WaitEvent()
		If EventSource()=window
			If EventID()=$803 Exit
			If EventID()=$802
				SetGadgetShape textarea,0,0,ClientWidth(window),ClientHeight(window)
			EndIf
		EndIf
	Forever
	FreeGadget window
End Function
</pre><br><br>'ave fun<br><br><br>*update*<br><br>Flags added for certain layout thingies.<br><br><pre class=code>Function ListDir$(dir$,flags=1+4+8+16,d=0)
; flags:
;
; +1  show dir path+name
; +2  show path in filename
; +4  recursive indent
; +8  [] around dirs
; +16 &lt;&gt; around files
	
	csd=ReadDir(dir$)
	If Not csd Return "error"

	Repeat
		file$=NextFile$(csd)
		If file$="" Exit


		; recursive indenting:		
		recindent$=""
		If (flags Shr 2) And 1 recindent$=String$(" ",d*4)
		

		; normal file?
		If FileType(dir$+file$)=1
			out$=out$+recindent$+indent$
			If (flags Shr 4) And 1 out$=out$+"&lt;"
			If (flags Shr 1) And 1 out$=out$+dir$ ; show full path?
			out$=out$+file$
			If (flags Shr 4) And 1 out$=out$+"&gt;"
			out$=out$+Chr$(13)+Chr$(10)
		EndIf

		; dir?	
		If FileType(dir$+file$)=2 And file$&lt;&gt;".." And file$&lt;&gt;"."
			If (flags Shr 0) And 1 ; show dir path+name?
				out$=out$+recindent$
				If (flags Shr 3) And 1 out$=out$+"["
				out$=out$+dir$+file$
				If (flags Shr 3) And 1 out$=out$+"]"
				out$=out$+Chr$(13)+Chr$(10)
			EndIf
			out$=out$+ListDir$(dir$+file$+"\",flags,d+1)
		EndIf
	Forever
	
	Return out$
End Function</pre> <br><br></td></tr></table><br>
<a name="782089"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >julianbury</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi CS_TBL (^_^)<br><br>That is a spectacular response!<br><br>Thank you so much :-D<br><br>Julian (-_-)<br><br><a href="http://joolian.net/" target="_blank">http://joolian.net/</a> <br><br></td></tr></table><br>
<a name="783631"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >julianbury</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am sorry to bother you with this ...<br>I've been trying to make this work but keep getting an error: "Invalid Stream Handle"<br><br>What am I doing wrong?<br><br><pre class=code>
;=============================================================
; First_List - A folders and files lister.
; To produce a file called "First.txt".
; A sister routine, Second_List, will produce "Second.txt".
; First.txt and Second.txt to be compared. 
; Differences to be written to "Third.txt".
;=============================================================

fileout=WriteFile( "First.txt" )
	ListDir$( "C:\Program Files\", 31, 0)
CloseFile(fileout)

End

Function ListDir$(dir$,flags,d)
; flags:
;
; +1	show dir path+name
; +2	show path in filename
; +4	recursive indent
; +8	[] around dirs
; +16 &lt;&gt; around files
	
	csd=ReadDir(dir$)
	If Not csd Return "error"

	Repeat
		file$=NextFile$(csd)
		If file$="" Exit

		; recursive indenting:		
		recindent$=""
		If (flags Shr 2) And 1 recindent$=String$(" ",d*4)

		; normal file?
		If FileType(dir$+file$)=1
			out$=out$+recindent$+indent$
			If (flags Shr 4) And 1 out$=out$+"&lt;"
			If (flags Shr 1) And 1 out$=out$+dir$ ; show full path?
			out$=out$+file$
			If (flags Shr 4) And 1 out$=out$+"&gt;"
			out$=out$+Chr$(13)+Chr$(10)
		EndIf

		; dir?	
		If FileType(dir$+file$)=2 And file$&lt;&gt;".." And file$&lt;&gt;"."
			If (flags Shr 0) And 1 ; show dir path+name?
				out$=out$+recindent$
				If (flags Shr 3) And 1 out$=out$+"["
				out$=out$+dir$+file$
				If (flags Shr 3) And 1 out$=out$+"]"
				out$=out$+Chr$(13)+Chr$(10)
			EndIf
			out$=out$+ListDir$(dir$+file$+"\",flags,d+1)
		EndIf

		; record the data in First.txt
		WriteLine(fileout, out$)
	Forever

	Return 0
End Function
</pre> <br><br></td></tr></table><br>
<a name="783648"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >plash</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Make the fileout variable a Global (replace 'fileout=WriteFile( "First.txt" )' with 'Global fileout=WriteFile( "First.txt" )'), or pass it to the function through another parameter. <br><br></td></tr></table><br>
<a name="783692"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hm, but this defeats the initial idea of having things not being hardwired. If you want to save the result, just save the <i>string</i> you got back from that function. <br><br></td></tr></table><br>
<a name="783704"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> So, why have this writeline thing in that function? It would mean that you can only use this function for one single thing: write the dir content to a file.<br><br>If you delete this line from that function and return out$ (as I initially had) then this function can be used to create a string, and afterwards you can do two things with it: save it, or show it. This makes such a function way more practical.<br><br>Actually, when I read your 'description', you intend to compare dir content, why do you need to write to files anyway? Just compare strings.<br><br>Did I read correctly that you'd have another routine for another text file, doing exactly the same? So, 2 identical functions, just with a different name? <br><br></td></tr></table><br>
<a name="783762"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> And:<br><pre class=code>ListDir$( "C:\Program Files\", 31, 0)</pre><br><br>Why add ,0 there and wipe it here:<br><br><pre class=code>Function ListDir$(dir$,flags,d)</pre><br><br>The ,d parameter is -in this case- only relevant for the function itself (internally), the user doesn't need to know about its existance, and therefor the user also doesn't need to be able to use or change it, so why give this 0 to the user?<br>?<br><br>You're making things far more complex than they need to be! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
