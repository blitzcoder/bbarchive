<!DOCTYPE html><html lang="en" ><head ><title >Viewing ICON files</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Viewing ICON files</h1><a href="forums.php" >BlitzPlus Forums</a>/<a href="topics.php?forum=95" >BlitzPlus Beginners Area</a>/<a href="#bottom" >Viewing ICON files</a><br><br>
<a name="512688"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi. I am making an app at the moment that involves the user choosing an icon file using the RequestFile() dialog box to be used in the app. However, I want the icon they selected to be diplayed in a Canvas but the LoadImage() command doesn't work with proper ICON files. However if I open JPEG or BMP, it displays it fine so I know it's not the programming. Is there another function or work around I could use because the program really wouldn't be that good without it. I would prefer it if I didn't have to use DLLs but if I do, could you tell me what I have to do. I am a newbie in the DLL field.<br><br>Thanks In Advance <br><br></td></tr></table><br>
<a name="512790"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Snarkbait</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I can probably hack together a loader for you. Gimme a few hours. <br><br></td></tr></table><br>
<a name="512796"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Snarkbait</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oops, nevermind, instead of reinventing the wheel, I see you can use the FreeImage wrapper. Here: <a href="http://www.blitzcoder.com/cgi-bin/showcase/showcase_showentry.pl?id=snarty09252003024804&amp;comments=no" target="_blank">http://www.blitzcoder.com/cgi-bin/showcase/showcase_showentry.pl?id=snarty09252003024804&amp;comments=no</a><br><br>put all the files in the 'decls' and 'data' folder in your blitzplus 'userlibs' directory. Not sure how it deals with .ico files with multiple images... I may still finish writing my own routine cuz I'm a file format geek like that. <br><br></td></tr></table><br>
<a name="512973"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> That's great. However, I am making a program that involves reading &amp; changing icons, and sometimes the icon is, an icon for an executable or DLL, like WINWORD.EXE. There isn't a winword.ico file so I need to somehow extract the icon in the resources of the executable. Any ideas<br><br>Thanks<br><br>Seb <br><br></td></tr></table><br>
<a name="512987"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MattVonFat</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I don't know exactly which command it is but if you look at the Win API reference there is a command to get the icon from an executable. <br><br></td></tr></table><br>
<a name="512992"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kev</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> seb, ExtractIconA found in 'shell32.dll' <br><br>checkout microsoft's msdn<br><br><a href="http://msdn.microsoft.com/library/default.asp?url=/library/en-us/winui/winui/windowsuserinterface/resources/icons/iconreference/iconfunctions/extracticon.asp" target="_blank">http://msdn.microsoft.com/library/default.asp?url=/library/en-us/winui/winui/windowsuserinterface/resources/icons/iconreference/iconfunctions/extracticon.asp</a> <br><br></td></tr></table><br>
<a name="513243"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> How would I actually implement this function into Blitz+(Ie.e. declerations in UserLibs) and how would I use this to display an image?<br><br>Thanks for all you help<br><br>Seb <br><br></td></tr></table><br>
<a name="513256"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kev</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> hi seb<br><br>heres .decls put this in your blitzplus userlibs folder<br><br><pre class=code>

;
; Blitz Basic Plus Mixed decls.
;

.lib "kernel32.dll"
kernel32_GetModuleHandle%(lpModuleName%):"GetModuleHandleA" 

.lib "user32.dll"
user32_DestroyIcon%(hIcon%):"DestroyIcon"
user32_DrawIcon%(hdc%,x%,y%,hIcon%):"DrawIcon"
user32_GetDC%(hwnd%):"GetDC"


.lib "shell32.dll"
shell32_ExtractIcon%(hInst%,lpszExeFileName$,nIconIndex%):"ExtractIconA"

</pre><br><br>and an example that open the icon inside the exe then draws it on the window.<br><br><pre class=code>

; open icon in exe file.
exe_icon = shell32_ExtractIcon(kernel32_GetModuleHandle(0),"test.exe",0)

; create window.
window = CreateWindow("test icon example",200,200,400,300,0,11) 

; get the windows device context
window_hdc = user32_GetDC(QueryObject(window,1))

; draw icon.
user32_DrawIcon window_hdc,100,10,exe_icon

; wait until the user closes one of the windows 
Repeat 
If WaitEvent()=$803 Then Exit 
Forever 

; free icon.
If exe_icon &gt; 0 Then
	user32_DestroyIcon exe_icon
EndIf

End

</pre><br><br>kev <br><br></td></tr></table><br>
<a name="513477"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cheers, thanks alot. By the way, what is the window device content. Also, when you compile your program, do you have to put any DLL files packaged with it.<br><br>Thanks for all your help<br><br>Seb<br><br>P.S: Will this work with normal icon files aswell? (*.ico) <br><br></td></tr></table><br>
<a name="513511"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Again,<br>I have come accross a bit of a problem still. It draws the icon great (Thanks Kev) but I can't find a way to remove the icon off the screen. If you tell it to draw the icon again in the same place, the icon is drawn over it and you can see the previos icon underneath in the transparent parts. I tried DestroyImage but that just removes the icon from the memory but doesn't remove the icon drawn on the window. Any ideas???<br><br>Thanks<br><br>Seb<br><br>P.S: It works great with ICO files aswell. Thanks again, Kev. You've saved me a lot of late night hours. <br><br></td></tr></table><br>
<a name="513592"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kev</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Seb<br><br>to remove the icon before redrawing a new one use a panel free it then recreate it. this works example below.<br><br><pre class=code>

; open icon in exe file.
exe_icon = shell32_ExtractIcon(kernel32_GetModuleHandle(0),"test.exe",0)

; create window.
window = CreateWindow("test icon example",200,200,400,300,0,11) 

panel = CreatePanel(10,10,200,200,window)

; get the panels device context
window_hdc = user32_GetDC(QueryObject(panel ,1))

exe_icon_1 = shell32_ExtractIcon(kernel32_GetModuleHandle(0),"test2.exe",0)

; draw icon.
user32_DrawIcon window_hdc,100,10,exe_icon_1

MouseWait

FreeGadget panel
panel = CreatePanel(10,10,200,200,window)
; get the panels device context
window_hdc = user32_GetDC(QueryObject(panel ,1))

; draw icon.
user32_DrawIcon window_hdc,100,10,exe_icon

; wait until the user closes one of the windows 
Repeat 
If WaitEvent()=$803 Then Exit 
Forever 

; free icon.
If exe_icon &gt; 0 Then
	user32_DestroyIcon exe_icon
EndIf

; free icon.
If exe_icon_1 &gt; 0 Then
	user32_DestroyIcon exe_icon_1
EndIf

End
</pre><br><br>some information on device contexts found on msdn<br><a href="http://msdn.microsoft.com/library/default.asp?url=/library/en-us/gdi/devcons_0g6r.asp" target="_blank">http://msdn.microsoft.com/library/default.asp?url=/library/en-us/gdi/devcons_0g6r.asp</a><br><br>kev <br><br></td></tr></table><br>
<a name="513623"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Once Again! - Finally though, one other thing. You know the declerations file you supplied, it declares the "ExtractIconA" function whereas on the MSDN it refers to it symply as "ExtractIcon". Is there any difference between these 2 commands and if so what difference is there.<br><br>Also, how do you specify a parameter as boolean in a decleration file. (Strings are specified using $, Integer with %, Floating Point with # Boolean ???) E.g. the RealDriveType function requires you to pass the drive number and also a reserved boolean value of 0. This value doesn't change. I have delcared it in the declarations file as:<br><pre class=code>.lib "shell32.dll"
DriveType%(DriveNumber%,0):"RealDriveType"</pre><br>But it doesn't work. Blitz+ won't open with this command and it comes up with an error. I tried the following, which works:<br><pre class=code>.lib "shell32.dll"
DriveType%(DriveNumber%,Reserved%):"RealDriveType"</pre><br>However, I don't know if I used the right variable type to specify a boolean value. (Are integar and boolean the same.) It just seems a bit pointless having to put a 0 every time the function is called when it's the same every time. Can it be done?<br><br>Hope you understand what I mean. More information on this can be found at <a href="http://msdn.microsoft.com/library/default.asp?url=/library/en-us/shellcc/platform/shell/reference/functions/realdrivetype.asp" target="_blank"> this MSDN link.</a><br><br>Thanks<br><br>Seb <br><br></td></tr></table><br>
<a name="513680"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Snarkbait</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well, in case it still might be useful to you, here is a blitz include that will load most icons into an image you can use all the normal blitz image functions on, however it won't extract the icons from .exe files. This routine can also load the mask information from the icon file as an image.<br><br>A blitz plus example is included.<br><br><pre class=code>;loadICOfile.bb include
;
; for 4-bit,8-bit and 24-bit .ico files
; will load 16x16, 32x32 and 48x48 icon files
;
;
; by snarkbait snarkbait66@...
;



Dim pallette256(255,2)

Type icoinfo
Field bCount,bWidth,bHeight,bColorCount,bReserved,wPlanes,wBitCount,dwBytesInRes,dwImageOffset
End Type 

Global black = argb(0,0,0)
Global white = argb(255,255,255)

Function getICOinfo$(icon$)
	icofile = ReadFile(icon$)
	If Not icofile RuntimeError "file not found"
	; icon header
	idReserved = ReadShort(icofile) ;should be 0
	idType = ReadShort(icofile);should be 1
	If idType &lt;&gt; 1 RuntimeError "Not a valid .ico file"
	idCount = ReadShort(icofile) ; number of icons in file
	info$ = "Icon file has " + idCount + " icons."
	For iconcount = 1 To idCount
		bWidth = ReadByte(icofile)
		bHeight = ReadByte(icofile)
		bColorCount = ReadByte(icofile) ;# entries in pallette table
		bReserved = ReadByte(icofile) ; should be 0
		wPlanes = ReadShort(icofile) ;?
		wBitCount = ReadShort(icofile) ;bpp
		dwBytesInRes = ReadInt(icofile) ;total bytes of image including AND &amp; XOR info
		dwImageOffset = ReadInt(icofile) ; offset to beginning of img data
		index$ = index$ + " Icon#" + iconcount + ":" + bWidth + "x" + bWidth + " - " + wBitCount + " bits per pixel   :"
	Next
	CloseFile icofile
	info$ = info$ + index$
	Return info$
End Function




Function loadICOimage(icon$,icoImageNumber = 1,returnMask = False)
	icofile = ReadFile(icon$)
	If Not icofile RuntimeError "file not found"
	; icon header
	idReserved = ReadShort(icofile) ;should be 0
	idType = ReadShort(icofile);should be 1
	If idType &lt;&gt; 1 RuntimeError "Not a valid .ico file"
	idCount = ReadShort(icofile) ; number of icons in file
	For iconcount = 1 To idCount
		;Icon Dir Entry
		ico.icoinfo = New icoinfo
		ico\bCount = iconcount
		ico\bWidth = ReadByte(icofile)
		ico\bHeight = ReadByte(icofile)
		ico\bColorCount = ReadByte(icofile) ;# entries in pallette table
		ico\bReserved = ReadByte(icofile) ; should be 0
		ico\wPlanes = ReadShort(icofile) ;?
		ico\wBitCount = ReadShort(icofile) ;bpp
		ico\dwBytesInRes = ReadInt(icofile) ;total bytes of image including AND &amp; XOR info
		ico\dwImageOffset = ReadInt(icofile) ; offset to beginning of img data
	Next 
	
	; read image entries 
	For ico.icoinfo = Each icoinfo
		If ico\bCount = icoImageNumber	
			SeekFile(icofile,ico\dwImageOffset)
			biSize = ReadInt(icofile)
			biWidth = ReadInt(icofile)
			biHeight = ReadInt(icofile) ; x 2
			biPlanes = ReadShort(icofile)
			biBitCount = ReadShort(icofile)
			biCompression = ReadInt(icofile)
			biSizeimage = ReadInt(icofile)
			; go to bitmap info
			SeekFile(icofile,ico\dwImageOffset + biSize)
			
			Select biBitCount
			
			Case 4 ; 16-colour pallette
				; read pallette
				For color_value = 0 To 15
					For RGB = 0 To 2
						readval = ReadByte(icofile)
						pallette256(color_value,RGB) = readval 
					Next
					useless = ReadByte(icofile) ;reserved byte
				Next 
				If Not returnMask 
					newimage = CreateImage(biWidth,biHeight/2)
					SetBuffer ImageBuffer(newimage)
					LockBuffer
					For ycount = biHeight/2 To 1 Step -1
						For xcount = 1 To biWidth Step 2
							readval = ReadByte(icofile)
							leftbits = readval Shr 4
							rtbits = readval And $f
							WritePixelFast xcount - 1,ycount - 1,argb(pallette256(leftbits,2),pallette256(leftbits,1),pallette256(leftbits,0))
							WritePixelFast xcount,ycount - 1,argb(pallette256(rtbits,2),pallette256(rtbits,1),pallette256(rtbits,0))
						Next
					Next				
					UnlockBuffer 
				Else
					SeekFile(icofile,ico\dwImageOffset + biSize + (2^biBitCount * 4) + (biWidth ^ 2/2))
					newimage = CreateImage(biWidth,biHeight/2)
					SetBuffer ImageBuffer(newimage)
					LockBuffer
					If biWidth = 32
						For ycount = biHeight/2 To 1 Step -1
							For xcount = 0 To 3
								readval = ReadByte(icofile)
								xpos = 0
								For bits = 8 To 1 Step -1
									readbit = (readval And (2^bits - 1)) Shr (bits - 1)
									If readbit
										WritePixelFast (xcount * 8) + xpos,ycount - 1,white
									Else
										WritePixelFast (xcount * 8) + xpos,ycount - 1,black
									EndIf
									xpos = xpos + 1
								Next
							Next
						Next
					Else
						If biWidth = 16
							For ycount = biHeight/2 To 1 Step -1
								For xcount = 0 To 1
									readval = ReadByte(icofile)
									xpos = 0
									For bits = 8 To 1 Step -1
										readbit = (readval And (2^bits - 1)) Shr (bits - 1)
										If readbit
											WritePixelFast (xcount * 8) + xpos,ycount - 1,white
										Else
											WritePixelFast (xcount * 8) + xpos,ycount - 1,black
										EndIf
										xpos = xpos + 1
									Next
								Next
								skip = ReadShort(icofile)
							Next
						Else
							If biWidth = 48
								For ycount = biHeight/2 To 1 Step -1
									For xcount = 0 To 5
										readval = ReadByte(icofile)
										xpos = 0
										For bits = 8 To 1 Step -1
											readbit = (readval And (2^bits - 1)) Shr (bits - 1)
											If readbit
												WritePixelFast (xcount * 8) + xpos,ycount - 1,white
											Else
												WritePixelFast (xcount * 8) + xpos,ycount - 1,black
											EndIf
											xpos = xpos + 1
										Next
									Next
									skip = ReadShort(icofile)
								Next
							EndIf
						EndIf 
					EndIf
					UnlockBuffer
				EndIf 
				CloseFile icofile
				Return newimage
				
			Case 8
				;read pallette
				For color_value = 0 To 255
					For RGB = 0 To 2
						readval = ReadByte(icofile)
						pallette256(color_value,RGB) = readval 
					Next
					useless = ReadByte(icofile) ;reserved byte
				Next 
				
				;draw image
				If Not returnMask 
					newimage = CreateImage(biWidth,biHeight/2)
					SetBuffer ImageBuffer(newimage)
					LockBuffer
					For ycount = biHeight/2 To 1 Step -1
						For xcount = 1 To biWidth
							readval = ReadByte(icofile)
							WritePixelFast xcount - 1,ycount - 1,argb(pallette256(readval,2),pallette256(readval,1),pallette256(readval,0))
						Next
					Next				
					UnlockBuffer 
				;read AND mask
				Else
					SeekFile(icofile,ico\dwImageOffset + biSize + (2^biBitCount * 4) + (biWidth ^ 2))
					newimage = CreateImage(biWidth,biHeight/2)
					SetBuffer ImageBuffer(newimage)
					LockBuffer
					If biWidth = 32
						For ycount = biHeight/2 To 1 Step -1
							For xcount = 0 To 3
								readval = ReadByte(icofile)
								xpos = 0
								For bits = 8 To 1 Step -1
									readbit = (readval And (2^bits - 1)) Shr (bits - 1)
									If readbit
										WritePixelFast (xcount * 8) + xpos,ycount - 1,white
									Else
										WritePixelFast (xcount * 8) + xpos,ycount - 1,black
									EndIf
									xpos = xpos + 1
								Next
							Next
						Next
					Else
						If biWidth = 16
							For ycount = biHeight/2 To 1 Step -1
								For xcount = 0 To 1
									readval = ReadByte(icofile)
									xpos = 0
									For bits = 8 To 1 Step -1
										readbit = (readval And (2^bits - 1)) Shr (bits - 1)
										If readbit
											WritePixelFast (xcount * 8) + xpos,ycount - 1,white
										Else
											WritePixelFast (xcount * 8) + xpos,ycount - 1,black
										EndIf
										xpos = xpos + 1
									Next
								Next
								skip = ReadShort(icofile)
							Next
						Else
							If biWidth = 48
								For ycount = biHeight/2 To 1 Step -1
									For xcount = 0 To 5
										readval = ReadByte(icofile)
										xpos = 0
										For bits = 8 To 1 Step -1
											readbit = (readval And (2^bits - 1)) Shr (bits - 1)
											If readbit
												WritePixelFast (xcount * 8) + xpos,ycount - 1,white
											Else
												WritePixelFast (xcount * 8) + xpos,ycount - 1,black
											EndIf
											xpos = xpos + 1
										Next
									Next
									skip = ReadShort(icofile)
								Next
							EndIf
						EndIf 
					EndIf
					UnlockBuffer
				EndIf 
				CloseFile icofile
				Return newimage
	
			Case 24
				If Not returnmask
					newimage = CreateImage(biWidth,biHeight/2)
					SetBuffer ImageBuffer(newimage)
					LockBuffer
					For ycount = biHeight/2 To 1 Step -1
						For xcount = 1 To biWidth
							readblue = ReadByte(icofile)
							readgreen = ReadByte(icofile)
							readred = ReadByte(icofile)
							WritePixelFast xcount - 1,ycount - 1,argb(readred,readgreen,readblue)
						Next
					Next				
					UnlockBuffer 
				Else
					SeekFile(icofile,ico\dwImageOffset + biSize + (biWidth ^ 2 * 3))
					newimage = CreateImage(biWidth,biHeight/2)
					SetBuffer ImageBuffer(newimage)
					LockBuffer
					If biWidth = 32
						For ycount = biHeight/2 To 1 Step -1
							For xcount = 0 To 3
								readval = ReadByte(icofile)
								xpos = 0
								For bits = 8 To 1 Step -1
									readbit = (readval And (2^bits - 1)) Shr (bits - 1)
									If readbit
										WritePixelFast (xcount * 8) + xpos,ycount - 1,white
									Else
										WritePixelFast (xcount * 8) + xpos,ycount - 1,black
									EndIf
									xpos = xpos + 1
								Next
							Next
						Next
					Else
						If biWidth = 16
							For ycount = biHeight/2 To 1 Step -1
								For xcount = 0 To 1
									readval = ReadByte(icofile)
									xpos = 0
									For bits = 8 To 1 Step -1
										readbit = (readval And (2^bits - 1)) Shr (bits - 1)
										If readbit
											WritePixelFast (xcount * 8) + xpos,ycount - 1,white
										Else
											WritePixelFast (xcount * 8) + xpos,ycount - 1,black
										EndIf
										xpos = xpos + 1
									Next
								Next
								skip = ReadShort(icofile)
							Next
						Else
							If biWidth = 48
								For ycount = biHeight/2 To 1 Step -1
									For xcount = 0 To 5
										readval = ReadByte(icofile)
										xpos = 0
										For bits = 8 To 1 Step -1
											readbit = (readval And (2^bits - 1)) Shr (bits - 1)
											If readbit
												WritePixelFast (xcount * 8) + xpos,ycount - 1,white
											Else
												WritePixelFast (xcount * 8) + xpos,ycount - 1,black
											EndIf
											xpos = xpos + 1
										Next
									Next
									skip = ReadShort(icofile)
								Next
							EndIf
						EndIf 
							 
					EndIf
						 
					UnlockBuffer
				EndIf
				CloseFile icofile
				Return newimage
				
			End Select
		EndIf 
	Next 
End Function

Function argb(red,green,blue)
	Return (blue Or (green Shl 8) Or (red Shl 16) Or ($ff Shl 24))
End Function 
</pre><br><br>sample file (blitz plus)<br><pre class=code>
Include "loadicofile.bb"

Global main = CreateWindow("ICO Loader",0,0,800,600,0,9)

this$ = RequestFile$("Open ICO Image","ico")

SetStatusText main,getICOinfo$(this$)

starttime# = MilliSecs()
convertedimage = loadicoimage(this$,1,False)
MidHandle convertedimage

;ScaleImage convertedimage,5,5
endtime# = MilliSecs() - starttime

canvas = CreateCanvas(100,100,200,200,main)
canvas2 = CreateCanvas(400,100,200,200,main)



SetBuffer CanvasBuffer(canvas)
ClsColor 128,128,128
Cls 

DrawBlock convertedimage,100,100
Text 0,0,"Time to load:" + endtime + " msecs"
FlipCanvas canvas

imagemask = loadicoimage(this$,1,True)
MidHandle imagemask
;ScaleImage imagemask,5,5
SetBuffer CanvasBuffer(canvas2)
ClsColor 128,128,128
Cls 

DrawBlock imagemask,100,100
Text 0,0,"Time to load:" + endtime + " msecs"
FlipCanvas canvas2

WaitKey()
End 

</pre> <br><br></td></tr></table><br>
<a name="513713"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kev</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Seb<br><br>for example the declared function "ExtractIcon" is found within c header files. "ExtractIconA" or "ExtractIconEx" both these function calls can be found in "shell32.dll" the latter function enables extra params when making the call. in the blitz .decs we declare the function call.<br><br>i dont think boolean values are supported i use ('integar' %) for them.<br><br>Download ApiViewer for a good list of available api calls and what .dll these can be found in.<br><br><a href="http://www.activevb.de/rubriken/apiviewer/index-apiviewereng.html" target="_blank">http://www.activevb.de/rubriken/apiviewer/index-apiviewereng.html</a><br><br>kev <br><br></td></tr></table><br>
<a name="513748"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for all your help. (Kev &amp; Snarkbait). My program is running fine. Found a bug that I managed to fix myself (shock, shock, horror, horror). When you draw the image using Kev's method onto the window, then minimize the window, when the window is restored, the icon disappears. I got around this by including the AppResume event in the program loop, which when it is triggered, redraws the image(s) on the screen.<br><br>Snarkbait - I tried to follow your code through and succeeded mostly. - LOL. (I've made a Blitz Compression utility so my Bytes/Shorts/Integer stuff seems to be up to scratch). However, where did you manage to find the file structure of the ICON file. I used <a href="http://www.wotsit.org" target="_blank">Wotsit's Format</a> but it didn't have much stuff on it. Do you know of any other good websites?<br><br>Thanks for all your help<br><br>Seb<br><br>P.S: Thanks for telling me about that ApiViewer, it will come in very handy. :-) <br><br></td></tr></table><br>
<a name="513808"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Snarkbait</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> I used here:<br><br><a href="http://www.awitness.org/delphi_pascal_tutorial/source2/icon_file_format.html" target="_blank">http://www.awitness.org/delphi_pascal_tutorial/source2/icon_file_format.html</a><br><br>and here:<br><a href="http://www.daubnet.com/formats/ICO.html" target="_blank">http://www.daubnet.com/formats/ICO.html</a><br><br>Nobody had info on the 24-bit icon (i was testing with the GLET one) so I figured that one out on my own.<br><br>Any part of my code you didn't get, let me know and I'll explain it... I didn't comment it enough lol. <br><br></td></tr></table><br>
<a name="513834"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Snarkbait</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> Seb: I put a better commented version in the code archives here:<br><a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=1358" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=1358</a> <br><br></td></tr></table><br>
<a name="514088"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kev</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> hi seb,<br><br>ive uploaded this little file error.exe this returns information on errors returned by kernel32.dll GetLastError() this will help when debugging any api calls you make.<br><br><a href="http://members.aol.com/kev12340poole/error.exe" target="_blank">http://members.aol.com/kev12340poole/error.exe</a><br><br>kev <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
