<!DOCTYPE html><html lang="en" ><head ><title >HtmlView -- horizontal scroll?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >HtmlView -- horizontal scroll?</h1><a href="forums.php" >BlitzPlus Forums</a>/<a href="topics.php?forum=95" >BlitzPlus Beginners Area</a>/<a href="#bottom" >HtmlView -- horizontal scroll?</a><br><br>
<a name="553116"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >rod54</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> HtmlView window -- Is it possible to get a horizontal scroll bar as well as a vertical ?<br><br>Thanks, <br><br></td></tr></table><br>
<a name="555003"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >rod54</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> 5 days and not a peep ... <br><br></td></tr></table><br>
<a name="560581"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >rod54</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> 2 weeks ... guess I will just have to figure it out my self.. <br><br></td></tr></table><br>
<a name="560955"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Beaker</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> There is a horizontal scrollbar, check here:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">;BLITZBROWSE by Beaker 2004 (updated)



;SETUP WINDOW
Global window=CreateWindow( "Blitz Browse",0,0,800,600)
SetMinWindowSize window,200,0


;SETUP MENUS
filemenu = CreateMenu ("  &amp;File  ",0,WindowMenu(window))
	openmenuitem = CreateMenu ("&amp;Open...",1,filemenu)

CreateMenu ("",999,filemenu)
	exitmenuitem = CreateMenu ("E&amp;xit",9,filemenu)

bookmenu = CreateMenu ("  &amp;Bookmarks  ",0,WindowMenu(window))
	addbookmenuitem = CreateMenu ("&amp;Add bookmark",21,bookmenu)
	organisebookmenuitem = CreateMenu ("&amp;Organise bookmarks",22,bookmenu)
	DisableMenu organisebookmenuitem
	defaultmenuitem = CreateMenu ("Set as &amp;default homepage",23,bookmenu)
	CreateMenu ("",999,bookmenu)



If FileType("bookmark.txt") &lt;&gt; 1	;then create a bookmark file with default bookmarks
	bookfile = WriteFile("bookmark.txt")
	Restore bookdata
	Read bookname$
	Read bookURL$
	While bookname$ &lt;&gt; "END"
		WriteLine bookfile,bookname$
		WriteLine bookfile,bookURL$
		Read bookname$
		Read bookURL
	Wend
	CloseFile bookfile
EndIf

;read the bookmarks into the bookmark menu
Global bookf=500
bookfile = ReadFile("bookmark.txt")
While Not Eof(bookfile)
	bookname$ = ReadLine(bookfile)
	bookURL$ = ReadLine(bookfile)
	If bookURL&lt;&gt;""
		CreateMenu (bookname,bookf,bookmenu)
		bookf = bookf +1
	EndIf
Wend
CloseFile bookfile
CreateMenu ("",999,bookmenu)

helpmenu = CreateMenu("  &amp;Help  ",0,WindowMenu(window))
aboutmenu = CreateMenu("&amp;About",1000,helpmenu)

UpdateWindowMenu window


If FileType("default.txt") &lt;&gt; 1	;then create the default.txt file with the default homepage
	deffile = WriteFile("default.txt")
		WriteLine deffile,"http://www.blitzbasic.com"
	CloseFile deffile
EndIf

deffile = ReadFile("default.txt")
	Global defURL$ = ReadLine (deffile)
CloseFile deffile




;SETUP BUTTONS
panel = CreatePanel (0,0,800,40,window,0)
	SetGadgetLayout panel,1,0,1,0
	backbutt = CreateButton ("Back",5,5,60,30,panel)
	forebutt = CreateButton ("Forward",70,5,60,30,panel)
	refreshbutt = CreateButton ("Refresh",135,5,60,30,panel)
	homebutt = CreateButton ("Home",200,5,60,30,panel)
	Global URLfield = CreateTextField (265,10,350,20,panel)
	URLgo = CreateButton ("GO!",620,10,60,20,panel)


;SETUP HTML VIEW
Global html=CreateHtmlView( 0,45,ClientWidth(window),ClientHeight(window)-50,window, 3 )
SetGadgetLayout html,1,1,1,1


Global current$
GoURL(defURL)


;MAIN LOOP
While WaitEvent()
	DebugLog "eventID $"+Hex(EventID())+"  eventDATA "+EventData()+"  eventSOURCE "+EventSource()+"  "+html

	Select EventID()
		Case $1001	;MENU EVENTS
			Select EventData()
				Case 1	;Open Local File
					localURL$ = RequestFile("Open local file","htm,html,jpg,gif,png")
					If FileType (localURL) = 1
						GoURL(localURL)
					EndIf
				Case 9	;Close program
					End
				Case 21	;Add bookmark
					bookfile = OpenFile("bookmark.txt")
					found = False
					While Not Eof(bookfile)
						bookname = ReadLine(bookfile)
						bookURL = ReadLine(bookfile)
						If bookURL = current
							Notify "URL already in bookmarks"
							found = True
							Exit
						EndIf
					Wend
					If found = False
						WriteLine bookfile,current
						WriteLine bookfile,current
						CreateMenu (current,bookf,bookmenu)
						bookf = bookf +1
						UpdateWindowMenu window
					EndIf
					CloseFile bookfile
				Case 23	;Set as default homepage
					deffile = WriteFile("default.txt")
						WriteLine deffile,current$
					CloseFile deffile
				Case 1000	;About
					Notify "Blitz Browse by Beaker 2004"+Chr(13)+"(bookmarks are in the bookmark.txt file)"
			End Select
			If EventData() &gt;= 500	;Jump to a specific bookmark
				f = 0
				bookfile = ReadFile("bookmark.txt")
				While Not Eof(bookfile)
					bookname$ = ReadLine (bookfile)
					bookURL = ReadLine (bookfile)
					If f = EventData()-500 Then
						GoURL(bookURL)
						Exit
					EndIf
					f = f +1
				Wend
			EndIf	
		Case $401	;BUTTON EVENTS
			Select EventSource()
				Case html	; catch clicking on URL links
					url$=HtmlViewEventURL$(html)
					Select EventData()
						Case 0	;page load event
							DebugLog "Loaded"
							; Set label's text to that of the htmlview event url
						;	SetGadgetText label,"loaded "+url$
						Case 1	;user navigate event
						;	SetGadgetText label,"navigating to "+url$
							goURL(url)
;							HtmlViewGo html,url$
					End Select
				
				Case backbutt
						HtmlViewBack html
				Case forebutt
						HtmlViewForward html
				Case refreshbutt
						GoURL(HtmlViewCurrentURL(html))
				Case homebutt
					deffile = ReadFile("default.txt")
						GoURL(ReadLine (deffile))
					CloseFile deffile
				Case URLgo
					GoURL(TextFieldText(URLfield))
					
				Case URLfield
					If EventData() = 13
						GoURL(TextFieldText(URLfield))
					EndIf
				Case html
					SetGadgetText URLfield ,HtmlViewCurrentURL(html)
					
			End Select
					
		Case $803	;WINDOW CLOSED EVENT
			Select EventSource()
				Case window
					End
			End Select
	End Select
	

Wend
End



Function GoURL(URL$)
	current = URL$
	SetGadgetText URLfield,current
	HtmlViewGo html,current
	SetGadgetText window,"Blitz Browse - "+current
End Function




.bookdata
Data "Blitz Basic"
	Data "http://www.blitzbasic.com"
Data "Blitz Coder"
	Data "http://www.blitzcoder.com"
Data "FONText bitmap font creation"
	Data "http://www.playerfactory.co.uk"
Data "Redflame Games and Tools"
	Data "http://www.redflame.net"
Data "Blitz Base"
	Data "http://www.blitzbase.de/"
Data "Game Making Tools forum"
	Data "http://www.playerfactory.co.uk"
Data "END"
	Data "END"</textarea> <br><br></td></tr></table><br>
<a name="561259"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >rod54</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks --- guess its a matter of what size your window is at the time .... if there is no need to have a horizontal <br>scroll there isn't one there. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
