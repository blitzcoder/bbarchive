<!DOCTYPE html><html lang="en" ><head ><title >For...Each freezes game. A newbie needs help</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >For...Each freezes game. A newbie needs help</h1><a href="forums.php" >BlitzPlus Forums</a>/<a href="topics.php?forum=95" >BlitzPlus Beginners Area</a>/<a href="#bottom" >For...Each freezes game. A newbie needs help</a><br><br>
<a name="1292032"></a>

<a name="1292033"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >mbrenner</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey everyone! Thanks for helping :). I'm a newbie at this and just need some help. My code is below.<br><br><pre class=code>Graphics 800,600,0,2
SetBuffer BackBuffer()

Dim array(10,10)

Data 1,1,1,1,1,1,1,1,1,1
Data 1,1,1,1,1,1,1,1,1,1
Data 1,1,1,1,1,1,1,1,1,1
Data 1,1,1,0,0,0,0,1,1,1
Data 1,1,1,0,0,0,0,1,1,1
Data 1,1,1,0,0,0,0,1,1,1
Data 1,1,1,0,0,0,0,1,1,1
Data 1,1,1,1,1,1,1,1,1,1
Data 1,1,1,1,1,1,1,1,1,1
Data 1,1,1,1,1,1,1,1,1,1


;###########################################
;#            Type Definitions             #
;###########################################
Type brick
	Field x,y
	Field image
	Field hitpoints
End Type

Type player
	Field lives
	Field x,y
	Field image
End Type

Type ball
	Field x,y
	Field vX, vY
	Field image
End Type

;###########################################
;#        Variable and New Types           #
;###########################################
Global weakbrick.brick = New brick
weakbrick\x = 150
weakbrick\y = 150
weakbrick\image = LoadImage("brick.bmp")
weakbrick\hitpoints = 1

Global ball.ball = New ball
ball\x = 400
ball\y = 300
ball\vX = 4
ball\vY = 4
ball\image = LoadImage("ball.bmp")

Global player.player = New player
player\lives = 3
player\x = 350
player\y = 580
player\image = LoadImage("player.bmp")

Global background1 = LoadImage("background1.bmp")
Global readinfo = 1

;###########################################
;#               Main Loop                 #
;###########################################
While Not KeyHit(1)
	Cls
	drawImages()
	drawlevel()
	movement()
	collisions()
	Flip
Wend


;###########################################
;#    Performs all Collision Detection     #
;###########################################
Function collisions()
	;Tests to see whether ball collides with sides
	If ball\y &gt; 595 Then ball\vY = -ball\vY
	If ball\x &gt; 796 Then ball\vX = -ball\vX
	If ball\y &lt; 0 Then ball\vY = -ball\vY
	If ball\x &lt; 0 Then ball\vX = -ball\vX
	
	;If the ball hits the player paddle	
	If ImagesCollide(player\image, player\x, player\y, 0, ball\image, ball\x,ball\y, 0) Then
		ball\vY = -ball\vY
	EndIf
	
	If ImagesCollide(weakbrick\image, weakbrick\x, weakbrick\y, 0, ball\image, ball\x,ball\y, 0) Then
		ball\vY = -ball\vY
	EndIf
	
	;If the player paddle hits the edges
	If player\x &lt; 0 Then player\x = 0
	If player\x &gt; 721 Then player\x = 721		
	
End Function


;###########################################
;#           Draws all images              #
;###########################################
Function drawImages()
	DrawImage(background1,0,0)
	DrawImage(player\image,player\x,player\y)
	DrawImage(ball\image,ball\x,ball\y)
End Function

Function drawlevel()
	For y = 1 To 10
		For x = 1 To 10
			If readinfo = 1 Then Read array(x,y)
			If array(x,y) = 1 Then
				DrawImage(weakbrick\image, x * 50, y * 20)
			EndIf
		Next
	Next
	readinfo = 0
End Function


;###########################################
;#            Movement Inputs              #
;###########################################
Function movement()
	;ball movement
	ball\x = ball\x + ball\vX
	ball\y = ball\y + ball\vY
	
	;player movement
	If KeyDown(203) Then
		player\x = player\x - 5
	Else If KeyDown(205) Then
		player\x = player\x + 5
	EndIf
End Function

End</pre><br><br><br>I'm trying to create a breakout game and I have multiple bricks drawn to the screen. However, each time I use the FOR EACH method to try to add behaviors to the bricks it freezes the game. Why does it do this?<br><br>Everyone in advance.<br><br>-- Matt <br><br></td></tr></table><br>
<a name="1292043"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >okee</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> i would sort out the array sizes first<br><br>Dim array(10,10)<br>creates an array 11 x 11 as arrays start at 0<br>so an array with 10 elements would be<br>Dim array(9,9)<br><br>then use<br>for x = 0 to 9<br><br>At the moment it's just creating an empty element at the beginning<br>What was the for each code that was being used ? <br><br></td></tr></table><br>
<a name="1292081"></a>

<a name="1292083"></a>

<a name="1292084"></a>

<a name="1292085"></a>

<a name="1292113"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dan</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> In your code, you have defined only 1 instance of the Brick type. <br><br>here is how i would do it:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Graphics 320,240,32,6 ;b3d ...
;Graphics 320,240,32,7 ;b3d ...

Graphics 320,240,0,2

Type dat
	Field x,y
	Field typ
End Type 

;setup the level:
For y=1 To 4
	For x=1 To 7
		Read dx  ; 0,1,2 or 3 
		If dx&gt;0
			brick.dat = New dat
			brick\typ=dx	;Brick type 
			brick\x=x*10	;Brick x size 
			brick\y=y*10	;Brick y size
		End If
	Next ;y
Next ;x

;main loop
Repeat
	Cls
	Gosub drawlevel
	Flip
	
Until KeyHit(1)


End

.drawlevel

For tmp.dat = Each dat
	Text tmp\x,tmp\y,tmp\typ
Next

Return

Data 0,0,0,0,0,0,0
Data 0,1,2,3,2,1,0
Data 0,1,2,0,2,1,0
Data 0,1,2,3,2,1,0
</textarea><br><br>Then you can use collision detection like this:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
for tmp.dat = each dat 
	If ImagesCollide(brickimage, tmp\x, tmp\y, 0, ball\image, ball\x,ball\y, 0) Then
		ball\vY = -ball\vY
                delete tmp
	EndIf 
next
</textarea> <br><br></td></tr></table><br>
<a name="1292118"></a>

<a name="1292190"></a>

<a name="1292191"></a>

<a name="1292192"></a>

<a name="1292193"></a>

<a name="1292195"></a>

<a name="1292196"></a>

<a name="1292197"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dan</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> A small game, for Blitz3d - click on maximize box ;)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics 120,100,32,6 ;bb3d ...
Graphics 120,100,32,7 ;bb3d ...

;Graphics 120,100,0,2 ; Blitzplus

SetBuffer BackBuffer()

Font=LoadFont("Courier",13,False,False,False)
SetFont font

Type info
	Field x,y
	Field shot
	Field timer
	Field movtimer
	Field score
End Type

player.info = New info
player\x = 32
player\y = 90
player\timer=MilliSecs()
player\movtimer=MilliSecs()
player\shot=0

Type dat
	Field x,y
	Field typ
End Type 

;setup the level:
maxscore=0
For y=1 To 4
	For x=1 To 7
	
		Read dx         ; 0,1,2,3 to 9
		
		If dx&lt;0 Or dx&gt;9 Then dx=Rand(0,9)     ;numbers between 0 and 9 please
		
		If dx&gt;0
			brick.dat = New dat
			brick\typ=dx			;Brick type 
			brick\x=2+(x*12)		;Brick x size 
			brick\y=3+(y*10)		;Brick y size
			maxscore=maxscore+dx	;add score, to display the game over
		End If
		
	Next ;y
Next ;x

;main loop
Repeat
	Cls
	Gosub drawlevel 		;Draw level
	Gosub drawplayer		;Draw player
	If player\shot=1 
		Gosub drawbullet	;Draw bullet
	EndIf
	
	Flip					;Flip to see
	
Until KeyHit(1)


End

.drawplayer
If MilliSecs()-player\movtimer&gt;10     ;player movement delay
	player\movtimer=MilliSecs()
	dx=KeyDown(205)-KeyDown(203)      ;nice trick, makes dx=-1 for left  and dx=1 for right arrow key
	player\x=player\x+dx
EndIf

;stop the player movement
If player\x&lt;0 Then player\x=0          ; left side
If player\x&gt;100 Then player\x=100      ; right side


If (KeyDown(29) Or KeyDown(157)) And player\shot=0  ;left or right ctrl
	bullet.info = New info
	bullet\x=player\x
	bullet\y=player\y
	player\timer=MilliSecs()
	player\shot=1
EndIf

Color $ff,$ff,$ff
Text player\x,player\y,"^"

Return

.drawbullet
If player\shot=1
	
	
	If bullet\y&lt;=-20 
		Delete bullet 
		player\shot=0
		Return
	Else
	  If MilliSecs()-player\timer=&gt;5    ;Speed of the bullet
		player\timer=MilliSecs()
		bullet\y=bullet\y-1
	  EndIf 
	EndIf
	
	For tmp.dat = Each dat                                   ;Collision detection:
		If (bullet\x=&gt;tmp\x-2 And bullet\x&lt;=tmp\x+4) And (bullet\y=&gt;tmp\y And bullet\y&lt;=tmp\y+4)
			player\score=player\score+tmp\typ        ;Score
			Delete bullet                            ;Remove bullet
			Delete tmp                               ;remove brick
			player\shot=0                            ;Alow player to shot again
			Return                                   ;Exit the drawbullet function, because bullet is not active anymore.
		EndIf
	Next
	
	Color $ff,$ff,$0
	Text bullet\x,bullet\y,"|"       ;Draw the bullet
EndIf
Return 

.drawlevel
Color $ff,$ff,$ff
Text 1,1,"Score = "+Player\Score         ;Display score

For tmp.dat = Each dat                   ;Display level data
    f=45*tmp\typ
    Color $ff-f,f,$ff
	Text tmp\x,tmp\y,tmp\typ
Next

If player\score=&gt;maxscore
Color $ff,$ff,$ff 
  Text 10,28,"Game Over"
  Text 10,42,"Press Esc"
EndIf

Return

Data 4,0,0,9,0,0,4
Data 0,1,2,3,2,1,0
Data 0,1,2,0,2,1,0
Data 0,1,2,3,2,1,0
</textarea><br><br>(edit #xxl) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
