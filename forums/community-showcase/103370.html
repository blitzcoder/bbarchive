<!DOCTYPE html><html lang="en" ><head ><title >IRC in BlitzMax</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >IRC in BlitzMax</h1><a href="forums.php" >Community Forums</a>/<a href="topics.php?forum=61" >Showcase</a>/<a href="#bottom" >IRC in BlitzMax</a><br><br>
<a name="1243995"></a>

<a name="1244000"></a>

<a name="1244001"></a>

<a name="1244002"></a>

<a name="1244008"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ferret</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I used this to connect a chatbot to a irc server.<br><br>irc.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'Module ferret.irc
'ModuleInfo "Name: Ferret IRC"
'ModuleInfo "Description: IRC mod"
'ModuleInfo "Author: Ferret"
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Type TIRC
	Field Host:String
	Field Server:String
	Field Port:Int
	Field NickName:String
	Field NickPass:String
	Field Channel:String
	Field ChannelList:TList
	Field Connected:Int
	Field InChannel:Int
	Field Stream:TSocketStream
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	Method New()
		Self.Host = "chat.freenode.net"
		Self.Server = "chat.freenode.net"
		Self.Port = 6667 
		Self.NickName = "Guest"
		Self.NickPass = "abc123"
		Self.Channel = "##Project-ACE"
		Self.ChannelList = CreateList()
	End Method
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	Method Delete()
		ClearList Self.ChannelList
		Self.ChannelList = Null
	End Method
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	Method Connect:Int()
		If Connected = True Then Self.Disconnect()
		Self.Stream = RequestSocketStream(HostIp(Self.Server),Self.Port)
		If Self.Stream 
			WriteLine Self.Stream,"NICK "+Self.NickName
		  	WriteLine Self.Stream,"USER "+Self.NickName+" "+Self.Host+" "+Self.Server+" :"+Self.NickName
			WriteLine Self.Stream,"NickServ IDENTIFY "+Self.NickName+" "+Self.NickPass
			Self.Host = ""
			Self.Connected = True
			Return 1
		EndIf
		Return 0
	End Method
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	Method Disconnect()
		If Self.Stream
			WriteLine Self.Stream,"PART " + Self.Channel
			WriteLine Self.Stream,"QUIT"
			CloseStream(Self.Stream)
		EndIf
		Self.Connected = False
	End Method
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	Method Recieve:String()
		If Self.Stream
			If SocketReadAvail(SocketStreamSocket(Self.Stream))
				Local linein:String = ReadLine(Self.Stream)
				If Self.Host = "" Then Self.Host = Mid(linein,2,Len(linein)-Len(Mid(linein,Instr(linein," "),Len(linein)-Instr(linein," ")))-2)
				If Instr(linein,"PING")
					WriteLine Self.Stream,"PONG :"+Self.Host
				Else
					Return linein
				EndIf
			EndIf
		EndIf
		Return ""
	End Method
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	Method Send(lineout:String)
		If Self.Stream
			WriteLine Self.Stream,lineout
		EndIf
	End Method
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	Method Message(lineout:String)
		If Self.Stream
			WriteLine Self.Stream,\":\"+Self.NickName+\"!~~\"+Self.NickName+\"@...+" PRIVMSG "+ Self.Channel+" :" + lineout
		EndIf
	End Method
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	Method JoinChannel(channel:String)
		If Self.Stream
			If Not Mid(channel,1,1) = "#"
				channel = "#" + channel
			EndIf
			WriteLine Self.Stream,"JOIN " + channel
			ListAddLast Self.ChannelList,channel
			Self.Channel = channel
			Self.InChannel = True
		EndIf
	End Method
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	Method ChangeChannel(channel:String)
		If ListContains(Self.ChannelList,channel)
			For Self.Channel = EachIn Self.ChannelList
				If Self.Channel = channel Then Exit
			Next
		ElseIf Self.Stream
			WriteLine Self.Stream,"JOIN " + channel
			ListAddLast Self.ChannelList,channel
			Self.Channel = channel
			Self.InChannel = True
		EndIf
	End Method
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	Method PartChannel(channel:String)
		If Self.Stream
			WriteLine Self.Stream,"PART " + channel
		EndIf
		ListRemove Self.ChannelList,channel
		If ListIsEmpty(Self.ChannelList)
			Self.Channel = ""
			Self.InChannel = False
		Else
			For Self.Channel = EachIn Self.ChannelList
				Exit
			Next
			Self.InChannel = True
		EndIf	
	End Method
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	Method RequestSocketStream:TSocketStream(ip:Int, port:Int)
		Local s:TSocket = CreateTCPSocket()
		If Not s.Connect(ip, port)
			s.Close
			Return Null
		End If
		Return CreateSocketStream(s, True)
	End Method
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	Method AcceptSocketStream:TSocketStream(port:Int, timeout:Int)
		Local conn:TSocket = CreateTCPSocket()
		If Not conn.Bind(port)
			conn.Close
			Return Null
		End If
		conn.Listen(0)
		Local s:TSocket = conn.Accept(timeout)
		conn.Close
		If Not s Return Null
		Return CreateSocketStream(s, True)
	End Method
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
End Type
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''</textarea><br><br><br>Code below is only needed for the example.<br><br>example.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
SuperStrict

'Import ferret.irc
Import brl.Retro

Include "irc.bmx"
Include "console.bmx"
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
AppTitle = "IRC"
Graphics 800,600

Global InputString:String
Global OutputString:String

Global Console:TConsole = New TConsole

Global Irc:TIRC = New TIRC
Irc.Host = "chat.freenode.net"
Irc.Server = "chat.freenode.net"
Irc.Port = 6667 
Irc.NickName = "P-ACE"
Irc.NickPass = "abc123"
Irc.Channel = "##Project-ACE"

If Irc.Connect()
	Irc.JoinChannel(Irc.Channel)
Else
	Console.AddLine("Connection failed.",200,0,0)
EndIf
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Repeat
	If AppTerminate() Or KeyHit(key_escape) Then Quit()
	
	Update()
	Console.Update()
	
	Flip
Forever
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Function Update()
	InputString = Irc.Recieve()
	If InputString &lt;&gt; ""		
		If Instr(Mid(InputString,1,Instr(InputString,":",2)),"PRIVMSG") 'And (Instr(InputString,"LIST")=False)
			Console.AddLine("&lt;"+Mid(InputString,Instr(InputString,"#"),(Len(InputString)-Instr(InputString,"#",2))-(Len(InputString)-Instr(InputString," :",2)))+"&gt; &lt;"+Mid(InputString,2,Instr(InputString,"!")-2)+"&gt; "+Mid(InputString,Instr(InputString,":",2)+1,(Len(InputString)-Instr(InputString,":",2))),50,200,255) 
			
		ElseIf Instr(InputString,":",2)
			Console.AddLine(Mid(InputString,Instr(InputString,":",2)+1,Len(InputString)-Instr(InputString,":",2)),255,150,0)	
		Else
			Console.AddLine(Mid(InputString,2,Len(InputString)-1),255,255,0)
		EndIf
		InputString = ""
	EndIf
	
	OutputString = Console.GetLine()
	If OutputString &lt;&gt; ""
		If Mid(OutputString,1,1) = "/"
			CatchCommand(OutputString)
		Else
			Irc.Message(OutputString)
			Console.AddLine("&lt;"+Irc.Channel+"&gt; &lt;"+Irc.NickName + "&gt; " + OutputString,0,255,100)
		EndIf
		OutputString = ""
	EndIf
End Function
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Function CatchCommand(command:String)
	If Instr(command," ") Then command = Mid(command,2,Instr(command," ")-2)
	
	If Lower(command) = "/exit" Or Lower(command) = "/quit"
		Quit()
		
	ElseIf Lower(command) = "/connect"
		Irc.Connect()
		
	ElseIf Lower(command) = "/disconnect"
		Irc.Disconnect()
		
	ElseIf Lower(command) = "/join"
		Irc.JoinChannel(Mid(command,Instr(command," ")+1,Len(command)-Instr(command," ")))
		
	ElseIf Lower(command) = "/part"
		Irc.PartChannel(Mid(command,Instr(command," ")+1,Len(command)-Instr(command," ")))
	
	ElseIf Lower(command) = "/channel"
		Irc.ChangeChannel(Mid(command,Instr(command," ")+1,Len(command)-Instr(command," ")))
		
	Else
		Irc.Send(Mid(command,2,Len(command)-1)+" "+Mid(command,Instr(command," ")+1,Len(command)-Instr(command," ")))
		
	EndIf
End Function
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Function Quit()
	If Confirm("Exit?")
		Irc.Disconnect()
		Irc = Null
		'ConsoleSaveLog()
		End
	EndIf
End Function
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''</textarea><br><br>console.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'Module ferret.console
'ModuleInfo "Name: Ferret console"
'ModuleInfo "Description: Console mod"
'ModuleInfo "Author: Ferret"
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Type TLine
	Field Time:String
	Field Content:String
	Field Red:Int, Green:Int, Blue:Int
End Type
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Type TConsole
	Field LineIn:String
	Field LineOut:String
	Field Timer:Float
	Field Extension:String
	Field LineList:TList
	Field LineDrawLimit:Int
	Field Key:Int
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''	
	Method New()
		Self.LineList = CreateList()
		Self.LineDrawLimit = 21
	End Method
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''	
	Method Delete()
		ClearList(Self.LineList)
		Self.LineList = Null
	End Method
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''	
	Method Update()
		Self.Draw()
		
		Self.LineOut = ""
		Self.key = GetChar()
		If Self.key &gt;= 32 And Self.Key &lt;= 126
			Self.LineIn = Self.LineIn + Chr(Self.key)
		ElseIf Self.Key = 8
			If Self.LineIn &lt;&gt; "" Then Self.LineIn = Left(Self.LineIn , Len(Self.LineIn) - 1)
		ElseIf Self.Key = 13
			'If Self.LineIn &lt;&gt; "" Then Self.LineOut = Self.LineIn
			Self.LineOut = Self.LineIn
			Self.LineIn = ""
		End If
		If MilliSecs() &gt; Self.Timer
			If Self.Extension = "_"
				Self.Extension = "" 
			Else
				Self.Extension = "_"
			EndIf
			Self.Timer = MilliSecs() + 500
		EndIf
		
		SetColor(0,255,100)
		DrawText(Self.LineIn + Self.Extension, 16,GraphicsHeight() - 22)	
	End Method
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	Method GetLine:String()
		Return Self.LineOut
	End Method
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	Method AddLine(content:String,red:Int=255,green:Int=255,blue:Int=255,width:Int=65)
		Local L:TLine = New TLine
		L.Time = CurrentTime()
		L.Content = content
		L.Red = red
		L.Green = green
		L.Blue = blue
		ListAddLast(Self.LineList,L)
	End Method
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	Method Draw(x:Int=16,y:Int=16,width:Int=100,yStep:Int=20)
	
		SetColor(0,200,100)
		DrawRect(0,GraphicsHeight()-32,GraphicsWidth(),32)
		SetColor(0,0,0)
		DrawRect(0,0,GraphicsWidth(),GraphicsHeight()-32)
		DrawRect(2,(GraphicsHeight()-32)+2,GraphicsWidth()-4,(32)-4)
		
		y = GraphicsHeight() - 464
		If CountList(Self.LineList) &gt; Self.LineDrawLimit Then y = y - ((CountList(Self.LineList)-Self.LineDrawLimit)*20)
		For Local L:TLine = EachIn Self.LineList
			SetColor L.Red, L.green, L.blue
			DrawText L.Content,x,y
			y :+ yStep
		Next
	End Method
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	Method SaveLog()
		Local time:String = CurrentTime()
		time = Mid(time,1,2) + "-" + Mid(time,4,2) + "-" + Mid(time,7,2)
		
		Local fileout:TStream = WriteFile("Conversation_"+CurrentDate()+"_"+time+".txt")
			WriteLine(fileout,"Log " + CurrentDate())
			WriteLine(fileout,"")
			For Local L:TLine = EachIn Self.LineList
				WriteLine(fileout,L.Time + " - " + L.Content)
			Next 
		CloseFile fileout
	End Method
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
End Type
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''</textarea> <br><br></td></tr></table><br>
<a name="1243998"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Who was John Galt?</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Good work, Ferret. Looks like some nice clean OOP code, too. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
