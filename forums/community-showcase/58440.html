<!DOCTYPE html><html lang="en" ><head ><title >AI Food Gathering code</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >AI Food Gathering code</h1><a href="forums.php" >Community Forums</a>/<a href="topics.php?forum=61" >Showcase</a>/<a href="#bottom" >AI Food Gathering code</a><br><br>
<a name="649984"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rob Farley</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sunday afternoon coding...<br><br>A sort of ant type food gathering AI thing going on...<br><br>There's a bunch of food and a bunch of workers.<br><br>Workers go find food.<br><br>If a worker sees another worker with food and they are closer to the base they will take it off the other one to form a sort of chain.<br><br>Anyway... more curious than useful!<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics 1024,768
SetBuffer BackBuffer()


Global BaseX = GraphicsWidth()/2
Global BaseY = GraphicsHeight()*.75
Global Food = 0


Type Worker
	Field x#,y#
	Field Carrying
	Field Direction#
	Field NewDirection#
	Field FoodExchanged
End Type

Type Food
	Field x#,y#
	Field Quantity
End Type

Function FoodNew()
	F.Food = New Food
	F\x = Rand(0,GraphicsWidth()-1)
	F\y = Rand(0,GraphicsHeight()-1)
	F\Quantity = Rand(1,100)
End Function

Function WorkerNew()
	W.Worker = New Worker
	W\x = BaseX
	W\y = BaseY
	W\Carrying = 0
	W\direction = Rand(0,359)
	W\NewDirection = w\direction
End Function


Function Draw()
	
	For F.Food = Each Food
		Color 0,255,0
		Rect f\x-10,f\y-5,21,11,True
		Color 0,0,0
		Text f\x,f\y,f\quantity,True,True
		
	Next
	
	For W.Worker = Each Worker
		If w\carrying Then Color 255,255,255 Else Color 200,200,255
		Oval w\x-5,w\y-5,11,11,True
		Color 0,0,0
		Line w\x,w\y,w\x+(Sin(w\direction)*5),w\y-(Cos(w\direction)*5)
	Next
	
	Color 255,0,0
	Oval BaseX - 20, BaseY-20,41,41,False
	Text basex,basey, food,True,True
End Function

Function UpdateWorkers()
	For W.Worker = Each Worker
	
		; if you're carrying food you're at 50% speed
		slowdown# = 1
		If w\carrying Then slowdown# = .5
		ad# = AngleDifference(w\direction,w\newdirection)
		
		If ad&gt;90 Then slowdown = 0
		
		w\Direction = WrapAngle(w\Direction + (ad/20))
		
		w\x = w\x + (Sin(w\direction) * slowdown)
		w\y = w\y - (Cos(w\direction) * slowdown)
		
		If w\x &lt; 0 Then w\newdirection = Rand(45,135)
		If w\y &lt; 0 Then w\newdirection = Rand(135,225)
		If w\x &gt; GraphicsWidth() Then w\newdirection = Rand(225,315)
		If w\y &gt; GraphicsHeight() Then w\newdirection = (Rand(-45,45) + 360) Mod 360
		
		; Find food
		For F.Food = Each Food
			d = Dis(w\x,w\y,f\x,f\y) 
			If d &lt; 100 Then
				w\newdirection = (ATan2(f\y-w\y,f\x-w\x) + 90+360) Mod 360
			EndIf
			
			If d &lt; 10 And w\carrying = 0 Then
				ReduceFood(F.Food)
				w\carrying = True
			EndIf

		Next
		
		If w\foodexchanged &gt; 0 Then w\foodexchanged = w\foodexchanged - 1
		
		; Find another worker with food who's further away from the base than you
		; and take the food from him to free him to search for more
		If w\carrying = False And w\foodexchanged = 0 Then
			For w1.worker = Each worker
				d = Dis(w\x,w\y,w1\x,w1\y) 
				If d &lt; 100 And w1\carrying = True And dis(w\x,w\y,basex,basey) &lt; dis(w1\x,w1\y,basex,basey) Then
					w\newdirection = (ATan2(w1\y-w\y,w1\x-w\x) + 90+360) Mod 360
				EndIf
				
				If d &lt; 15 And w1\carrying = True And w\carrying = False Then
					w\carrying = True
					w1\carrying = False
					w1\foodexchanged = 100
					w1\newdirection = Rand(0,359)
				EndIf
			Next
		EndIf
		
		; If you've got food, head for base
		If w\carrying Then
			w\newdirection = (ATan2(BaseY-w\y,BaseX-w\x) + 90+360) Mod 360
		EndIf	
		
		; If you're at base drop off the food and go searching again		
		If Dis(w\x,w\y,BaseX,BaseY) &lt; 20 And w\carrying Then
			w\carrying = False
			w\newdirection = Rand(0,359)
			Food = Food + 1
			
		EndIf
		
		
		
		
	Next
End Function

Function ReduceFood(F.Food)
	If f\quantity &gt; 0 Then f\quantity = f\quantity - 1
	If f\quantity = 0 Then Delete f
End Function

Function Dis(x1#,y1#,x2#,y2#)
	x=(x1-x2)
	y=(y1-y2)
	Return Sqr((x*x)+(y*y))
End Function

Function AngleDifference#(angle1#,angle2#) 
	Return ((angle2 - angle1) Mod 360 + 540) Mod 360 - 180 
End Function

Function WrapAngle#(value#)
	Return value+360 Mod 360
End Function

For n=1 To 20
	FoodNew()
Next
For n=1 To 100
	workernew()
Next


Repeat
	Cls
	updateworkers()
	Draw()
	Flip
Until KeyHit(1)</textarea> <br><br></td></tr></table><br>
<a name="649986"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> hey that's neat. Reminds me of the ants that invaded our dining room last autumn! <br><br></td></tr></table><br>
<a name="649992"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Red Ocktober</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> cool... could be used in a  Comand &amp; Conquer sort of game...<br><br>--Mike <br><br></td></tr></table><br>
<a name="650008"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Regular K</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Wheres the stomp button? I want to squish the ones in the corner.<br><br>Pretty cool, do they depend on food? That would be cool to see them eventually die off due to lack of food. <br><br></td></tr></table><br>
<a name="650097"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rob Farley</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just made it more interesting... Added a caller mode. When one finds food if there's no-one else calling it will become a caller to attract the others.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics 1024,768
SetBuffer BackBuffer()




Global BaseX = GraphicsWidth()/2
Global BaseY = GraphicsHeight()*.75
Global Food = 0
SpeedUp = 5


Type Worker
	Field x#,y#
	Field Carrying
	Field Direction#
	Field NewDirection#
	Field FoodExchanged
	Field Caller
End Type

Type Food
	Field x#,y#
	Field Quantity
End Type

Function FoodNew()
	F.Food = New Food
	F\x = Rand(0,GraphicsWidth()-1)
	F\y = Rand(0,GraphicsHeight()-1)
	F\Quantity = Rand(1,100)
End Function

Function WorkerNew()
	W.Worker = New Worker
	W\x = BaseX
	W\y = BaseY
	W\Carrying = 0
	W\direction = Rand(0,359)
	W\NewDirection = w\direction
End Function


Function Draw()
	
	For F.Food = Each Food
		Color 0,255,0
		Rect f\x-10,f\y-5,21,11,True
		Color 0,0,0
		Text f\x,f\y,f\quantity,True,True
		
	Next
	
	For W.Worker = Each Worker
		Color 150,150,150
		If w\carrying Then Color 0,255,0
		If w\caller Then Color 0,255,255: Oval w\x-count,w\y-count,count*2+1,count*2+1,False
		Oval w\x-5,w\y-5,11,11,True
		Color 0,0,0
		Line w\x,w\y,w\x+(Sin(w\direction)*5),w\y-(Cos(w\direction)*5)
	Next
	
	Color 255,0,0
	Oval BaseX - 20, BaseY-20,41,41,False
	Text basex,basey, food,True,True
End Function

Function UpdateWorkers()
	For W.Worker = Each Worker
	
		; if you're carrying food you're at 50% speed
		slowdown# = 1
		If w\carrying Then slowdown# = .5
		If w\caller Then slowdown = 0
		ad# = AngleDifference(w\direction,w\newdirection)
		
		If ad&gt;90 Then slowdown = 0
		
		w\Direction = WrapAngle(w\Direction + (ad/20))
		
		w\x = w\x + (Sin(w\direction) * slowdown)
		w\y = w\y - (Cos(w\direction) * slowdown)
		
		If w\x &lt; 0 Then w\newdirection = Rand(45,135)
		If w\y &lt; 0 Then w\newdirection = Rand(135,225)
		If w\x &gt; GraphicsWidth() Then w\newdirection = Rand(225,315)
		If w\y &gt; GraphicsHeight() Then w\newdirection = (Rand(-45,45) + 360) Mod 360
		
		
		If w\caller = False Then
		
			If w\foodexchanged &gt; 0 Then w\foodexchanged = w\foodexchanged - 1
			
			; Find another worker with food who's further away from the base than you
			; and take the food from him to free him to search for more
			If w\carrying = False And w\foodexchanged = 0 Then
				For w1.worker = Each worker
					d = Dis(w\x,w\y,w1\x,w1\y) 
					If d &lt; 100 And w1\carrying = True And dis(w\x,w\y,basex,basey) &lt; dis(w1\x,w1\y,basex,basey) Then
						w\newdirection = (ATan2(w1\y-w\y,w1\x-w\x) + 90+360) Mod 360
					EndIf
					
					If d &lt; 300 And w1\caller = True 
						w\newdirection = (ATan2(w1\y-w\y,w1\x-w\x) + 90+360) Mod 360
					EndIf
					
					If d &lt; 15 And w1\carrying = True And w\carrying = False Then
						w\carrying = True
						w1\carrying = False
						w1\foodexchanged = 100
						w1\newdirection = Rand(0,359)
					EndIf
				Next
			EndIf
		
		
			; Only find food if you're not carrying food
			If w\carrying = False Then 
				; Find food
				For F.Food = Each Food
					d = Dis(w\x,w\y,f\x,f\y) 
					If d &lt; 100 Then
						w\newdirection = (ATan2(f\y-w\y,f\x-w\x) + 90+360) Mod 360
					EndIf
					
					If d &lt; 20 Then
					; check for callers
						found = False
						For w1.worker = Each worker
							If Dis(w\x,w\y,w1\x,w1\y)&lt;50 And w1\caller = True Then found = True
						Next
						; if no callers become a caller
						If found = False Then w\caller = True
					EndIf
									
					If d &lt; 10 Then
						ReduceFood(F.Food)
						w\carrying = True
					EndIf
		
				Next
			EndIf
			

			
			; If you've got food, head for base
			If w\carrying Then
				w\newdirection = (ATan2(BaseY-w\y,BaseX-w\x) + 90+360) Mod 360
			EndIf
			
	
			
			; If you're at base drop off the food and go searching again		
			If Dis(w\x,w\y,BaseX,BaseY) &lt; 20 And w\carrying Then
				w\carrying = False
				w\newdirection = Rand(0,359)
				Food = Food + 1
				
			EndIf
		Else
		; You are a caller
			Found = False
			For F.Food = Each Food
				If Dis(w\x,w\y,f\x,f\y) &lt; 50 Then found = True
			Next
			If found = False Then w\caller = False
		EndIf
			
			
		
		
	Next
End Function

Function ReduceFood(F.Food)
	If f\quantity &gt; 0 Then f\quantity = f\quantity - 1
	If f\quantity = 0 Then Delete f
End Function

Function Dis(x1#,y1#,x2#,y2#)
	x=(x1-x2)
	y=(y1-y2)
	Return Sqr((x*x)+(y*y))
End Function

Function AngleDifference#(angle1#,angle2#) 
	Return ((angle2 - angle1) Mod 360 + 540) Mod 360 - 180 
End Function

Function WrapAngle#(value#)
	Return value+360 Mod 360
End Function

For n=1 To 100
	FoodNew()
Next
For n=1 To 100
	workernew()
Next

Global count

Repeat
	Cls
	For n=1 To Speedup
	updateworkers()
	Next
	Draw()
	count = (count + 10) Mod 100
	Flip
Until KeyHit(1)</textarea> <br><br></td></tr></table><br>
<a name="650118"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Very good rob ! :) like always :) <br><br></td></tr></table><br>
<a name="650123"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Regular K</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Even better :D<br><br>I made something comparable to this.<br><br>Everyone had a job: Baker, Guard or Thief.<br><br>Bakers would make food. Thiefs would steal food (if they cant afford it from the baker) or they would buy it. Guards would buy food, or persue thiefs (if seen when stealing). Everyone had to sleep and eat.<br><br>I dont know why I deleted it... <br><br></td></tr></table><br>
<a name="650128"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >boomboom</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well why not now add preditors! that try to eat the workers. And guards. who have to guard the workers? <br><br></td></tr></table><br>
<a name="650352"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlackJumper</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice... especially the 'caller' mode.<br><br>I did something similar a long way back where the ants could emerge from a 'hive' North, South, East or West. A neural network then evolved the best strategy for sending out workers based on which direction provided the best harvest.<br><br>There was also a huge magnifying glass that let you slow down the ants or frazzle them out of existence...<br><br>Unfortunately, I didn't understand Blitz collisions very well at the time, and ants kept 'popping' out of the world as they got forced beneath the ground or escaped from the arena. I eventually abandoned it for this reason. <br><br></td></tr></table><br>
<a name="653349"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >boomboom</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> I had a little program myself. This is what I came up with (Blitz3D)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Ant AI System 0.1
;Chris Bate

	Graphics3D 600,600,0,2


;;===========================;;
;;					VARIABLES					;;
;;------------------------------------------------------;;

;Frame Limiting
;-----------------------
	Global FrameTime
	Global Period
	Global FPS = 30
;-----------------------

;Ant Control
;-----------------------
	OutsideBoundry = 19
	
	SeedRnd MilliSecs() : NestX = Rnd(-10,10) : Delay 1
	SeedRnd MilliSecs() : NestZ = Rnd(-10,10)

	AntAmount = 400
	
	AntSpeed# = 0.1
		AntSpeedRnd = 2

	AntAttentionSpan# = 0.8 ;Amount of time (in seconds) an ant will stay on the same course without changing directions
		AntAttentionRnd# = 2
		
	FoodNumbers = 10
	FoodSpread = 18
	FoodAmount = 500

	ColType_Ant=1
	ColType_Nest=2
	ColType_Food=3

	
;-----------------------


;;===========================;;
;;						SETUP						;;
;;------------------------------------------------------;;

;Scene Setup
;-----------------------
	Camera = CreateCamera()
		CameraClsColor Camera, 0,40,0
		CameraProjMode Camera,2
		CameraZoom Camera,0.05
		PositionEntity Camera,0,20,0
		RotateEntity Camera,90,0,0

	AmbientLight 255,255,255

	Nest = CreateCube()
		PositionEntity Nest, NestX,0,NestZ
		EntityType Nest,ColType_Nest

	AntMesh = CreateCube()
		HideEntity AntMesh
;-----------------------

;Ant Type
;-----------------------
	Type Ant
		Field Model
		Field Size
		Field Speed#
		Field Direction
		Field AttentionSpan#
		Field AttentionLeft ; How many more left cycles until he changes direction
		Field Target ; Current Food Target
		Field Mode$
	End Type 

	For i = 0 To AntAmount
		SeedRnd MilliSecs() : Delay 1

		Hive.Ant = New Ant
			Hive\Model = CopyEntity (AntMesh)
				EntityColor Hive\Model, Rnd(139,180),100,30
				PositionEntity Hive\Model, NestX, 0, NestZ
				ScaleEntity Hive\Model, 0.1,0.1,0.1
				EntityType Hive\Model,ColType_Ant
				EntityRadius Hive\Model,0.1
				TurnEntity Hive\Model,0,Rnd(-0,360),0
			Hive\Speed = AntSpeed * Rnd(1,AntSpeedRnd)
			Hive\AttentionSpan = 0
			Hive\Mode = "Looking For Food"
	Next
;-----------------------

;Food Type
;-----------------------
	Type FoodT
		Field Model#
		Field Amount#
	End Type
		
	For i = 0 To FoodNumbers
		SeedRnd MilliSecs() : Delay 1

		Food.FoodT = New FoodT
			Food\Model = CreateSphere()
			EntityType Food\Model,ColType_Food
			PositionEntity Food\Model, EntityX (Nest),0,EntityZ(Nest)
			If EntityDistance (Food\Model, Nest) &lt; 5 Then
				PositionEntity Food\Model, Rnd(-FoodSpread,FoodSpread),0,Rnd(-FoodSpread,FoodSpread)
			End If
			EntityColor Food\Model, 0,200,0
			Food\Amount = FoodAmount + Rnd (-(FoodAmount/3),+(FoodAmount/3))
			ScaleEntity Food\Model, (Food\Amount/300),1,(Food\Amount/300)
	Next 
;-----------------------


	;SetBuffer BackBuffer()
	Collisions ColType_Ant,ColType_Food,1,2



;;===========================;;
;;					MAIN LOOP					;;
;;------------------------------------------------------;;

	While Not KeyHit(1) 

	;Cls

		If KeyHit(57) Then
			For Hive.Ant = Each Ant
				Hive\Mode = "Looking For Food"
			Next
		End If 


		For Hive.Ant = Each Ant

			Select Hive\Mode

				Case "Looking For Food"
					If Hive\AttentionSpan &gt; 0
						Gosub MoveAnt
						Hive\AttentionSpan = Hive\AttentionSpan - 1
					Else
						SeedRnd MilliSecs() : Delay 1
						TurnEntity Hive\Model,0,Rnd(-90,90),0
						Hive\AttentionSpan = (AntAttentionSpan * Rnd(1,AntAttentionRnd)) * FPS
					End If
					If EntityX (Hive\Model) &gt; OutsideBoundry Or EntityX (Hive\Model) &lt; -OutsideBoundry Or EntityZ (Hive\Model) &gt; OutsideBoundry Or EntityZ (Hive\Model) &lt; -OutsideBoundry Then
						Hive\Mode = "Gone Outside Boundry"
					End If
					If EntityCollided ( Hive\Model,ColType_Food ) Then
						Hive\Target= CollisionEntity (Hive\Model, 1)
						Hive\Mode = "Found Food"
					End If 

				Case "Gone Outside Boundry"
					PointEntity Hive\Model, Nest
					Hive\Mode = "Looking For Food"

				Case "Found Food"
					If EntityDistance (Hive\Model,Nest) &lt;1.2 Then
						PointEntity Hive\Model, Hive\Target
					End If
					If EntityDistance (Hive\Model,Hive\Target) &lt;1.2 Then
						For Food.FoodT = Each FoodT
							If Hive\Target = Food\Model Then Food\Amount = Food\Amount -1
							ScaleEntity Food\Model, (Food\Amount/300),1,(Food\Amount/300)
						Next
						PointEntity Hive\Model, Nest
					End If
					Gosub MoveAnt

			End Select

		Next



		For Food.FoodT = Each FoodT
			If Food\Amount &lt; 0 Then
				For Hive.Ant = Each Ant
					If Hive\Target = Food\Model Then Hive\Mode = "Looking For Food"
				Next
				FreeEntity Food\Model
				Delete Food
			End If
		Next


		LimitFrameRate()
		UpdateWorld
		RenderWorld

;		i=10
;		For Food.FoodT = Each FoodT
;			Text 10,i, "Food Amount=" + Food\Amount
;			i = i+10
;		Next

		Flip
	Wend
	End

;;===========================;;
;;					FUNCTIONS					;;
;;------------------------------------------------------;;

Function LimitFrameRate()
If FrameTime = 0 Then
Period = 1000 / FPS
FrameTime = MilliSecs()
EndIf

; Make sure the framerate isn't above the specified setting
While (FrameTime + Period) &gt; MilliSecs()
Delay 1
Wend
FrameTime = MilliSecs()
End Function

.MoveAnt
	MoveEntity Hive\Model,Rnd(-0.1,0.1),0,Hive\Speed
Return

</textarea><br><br>The only borrowed code is the FPS Limiting Function. I am still working on it. Going to add scent trailers when I get the chance (like the Robs Caller Mode but more based on ants scent trails) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
