<!DOCTYPE html><html lang="en" ><head ><title >Dilbert.com daily comic</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Dilbert.com daily comic</h1><a href="forums.php" >Community Forums</a>/<a href="topics.php?forum=61" >Showcase</a>/<a href="#bottom" >Dilbert.com daily comic</a><br><br>
<a name="520607"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Picklesworth</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I wanted to be able to read my two favourite comic strips every day with ease (translation: the only ones that are consitently funny, like comics are meant to be but aren't). So, I signed up for the free dilbert.com comic mailing service. Turns out I have to pay money to get it on Sundays, I am sent an image of the entire web page rather than just the comic, and thus I may as well just bookmark the web site or something. Consequently, I decided to have a shot at coding it myself; so I took a peak at the code archives, borrowed an image loading function, did a bit of confusing string stuff, and it worked first time with absolutely no errors at all!<br>Sadly, I couldn't get Calvin &amp; Hobbes... But Dilbert is great, so I'm happy :D<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
AppTitle "Dilbert.com daily comic By Mr. Picklesworth"

Graphics 800,600,0,2
Const BGr=200,BGg=200,BGb=200
ClsColor BGr,BGg,BGb 
Color 0,0,0

; -----------------------------------------------------------------------------
; Load an image from the web, straight into our game!
; -----------------------------------------------------------------------------
Cls
Text 20, 20, "Downloading file - Please wait..."
Flip
comic = LoadWebImage (DilbertImagePath())

; -----------------------------------------------------------------------------
; Check for failure
; -----------------------------------------------------------------------------
If comic = 0
	RuntimeError "Failed to load web image!": End	
EndIf

MaskImage comic,BGr,BGg,BGb ;Am I forced to mask the image?!
Cls
DrawImage comic,10,10

WaitKey
End

Function DilbertImagePath$()
	If BlitzGet("http://www.dilbert.com/index.html",CurrentDir(),"temp_web_page.txt")
		page = OpenFile("temp_web_page.txt")
		Repeat
			inLine$=ReadLine(page)
			tag$ = "&lt;IMG SRC="+Chr(34)
			check = Instr(inLine$,"&lt;IMG SRC="+Chr(34)+"/comics/dilbert/archive/images/")
			If check
				PathStart=check+Len(tag$)
				PathEnd=Instr(inLine$,Chr(34),PathStart)
				Path$ = Mid(inLine$,PathStart,PathEnd-PathStart)
				CloseFile page
				DeleteFile "temp_web_page.txt"
				Return "http://www.dilbert.com"+Path$
			EndIf
		Until Eof(page)
		DeleteFile "temp_web_page.txt"
	EndIf	
End Function

; -----------------------------------------------------------------------------
; LoadWebImage -- uses BlitzGet Deluxe, based on Mark Sibly's HTTPGet
; -----------------------------------------------------------------------------
; james@...
; -----------------------------------------------------------------------------

Function LoadWebImage (webFile$)
	If BlitzGet (webFile$, CurrentDir (), "temp_web_image.bmp")
		Image = LoadImage ("temp_web_image.bmp")
		DeleteFile "temp_web_image.bmp"
	EndIf
	Return Image
End Function

Function BlitzGet (webFile$, saveDir$, saveFile$)

	; -------------------------------------------------------------------------
	; Strip "http://" if provided
	; -------------------------------------------------------------------------
	If Left (webFile$, 7) = "http://" Then webFile$ = Right (webFile$, Len (webFile$) - 7)

	; -------------------------------------------------------------------------
	; Split into hostname and path/filename to download
	; -------------------------------------------------------------------------
	slash = Instr (webFile$, "/")
	If slash
		webHost$ = Left (webFile$, slash - 1)
		webFile$ = Right (webFile$, Len (webFile$) - slash + 1)
	Else
		webHost$ = webFile$
		webFile$ = "/"
	EndIf
		
	; -------------------------------------------------------------------------
	; Add trailing slash to download dir if not given
	; -------------------------------------------------------------------------
	If Right (saveDir$, 1) &lt;&gt; "\" Then saveDir$ = saveDir$ + "\"

	; -------------------------------------------------------------------------
	; Save filename -- get from webFile$ if not provided
	; -------------------------------------------------------------------------
	If saveFile$ = ""
		If webFile = "/"
			saveFile$ = "Unknown file.txt"
		Else
			For findSlash = Len (webFile$) To 1 Step - 1
				testForSlash$ = Mid (webFile$, findSlash, 1)
				If testForSlash$ = "/"
					saveFile$ = Right (webFile$, Len (webFile$) - findSlash)
					Exit
				EndIf
			Next
			If saveFile$ = "" Then saveFile$ = "Unknown file.txt"
		EndIf
	EndIf

	; DEBUG
	; RuntimeError "Web host: " + webHost$ + Chr (10) + "Web file: " + webFile$ + Chr (10) + "Save dir: " + saveDir$ + Chr (10) + "Save file: " + saveFile$

	www = OpenTCPStream (webHost$, 80)

	If www
	
		WriteLine www, "GET " + webFile$ + " HTTP/1.1" ; GET / gets default page...
		WriteLine www, "Host: " + webHost$
		WriteLine www, "User-Agent: BlitzGet Deluxe"
		WriteLine www, "Accept: */*"
		WriteLine www, ""
		
		; ---------------------------------------------------------------------
		; Find blank line after header data, where the action begins...
		; ---------------------------------------------------------------------
				
		Repeat
			header$ = ReadLine (www)
			If Left (header$, 16) = "Content-Length: "	; Number of bytes to read
				bytesToRead = Right (header$, Len (header$) - 16)
			EndIf
		Until header$ = "" Or (Eof (www))
		
		If bytesToRead = 0 Then Goto skipDownLoad
		
		; ---------------------------------------------------------------------
		; Create new file to write downloaded bytes into
		; ---------------------------------------------------------------------
		save = WriteFile (saveDir$ + saveFile$)
		If Not save Then Goto skipDownload

		; ---------------------------------------------------------------------
		; Incredibly complex download-to-file routine...
		; ---------------------------------------------------------------------
				
		For readWebFile = 1 To bytesToRead
		
			If Not Eof (www) Then WriteByte save, ReadByte (www)
			
			; Call BytesReceived with position and size every 100 bytes (slows down a LOT with smaller updates)
			
			;tReadWebFile = readWebFile			
			;If tReadWebFile Mod 1000 = 0 Then BytesReceived (readWebFile, bytesToRead)

		Next

		CloseFile save
		
		; Fully downloaded?
		If (readWebFile - 1) = bytesToRead
			success = 1
		EndIf
		
		; Final update (so it's not rounded to nearest 100 bytes!)
		;BytesReceived (bytesToRead, bytesToRead)

		.skipDownload
		CloseTCPStream www
		
	Else
	
		RuntimeError "Failed to connect"
		
	EndIf
	
	Return success
	
End Function

; -----------------------------------------------------------------------------
; User-defined update function, called every 100 bytes of download -- alter to suit!
; -----------------------------------------------------------------------------
; TIP: Pass a user-defined type instead, with all data (this stuff plus URL, local filename, etc)
; -----------------------------------------------------------------------------
Function BytesReceived (posByte, totalBytes)
	; Example update code...
	Cls
	Text 20, 20, "Downloading file -- please wait..."
	Text 20, 40, "Received: " + posByte + "/" + totalBytes + " bytes (" + Percent (posByte, totalBytes) + "%)"
	Flip
End Function

; -----------------------------------------------------------------------------
; Handy percentage function
; -----------------------------------------------------------------------------
Function Percent (part#, total#)
	Return Int (100 * (part / total))
End Function
</textarea><br><br>Two questions for you now: <br>Why is it so slow to use Blitz to load a web site? Can I speed it up without any effort on my part?<br>How do I tell an application to just freeze? Right now I have a very silly delay in there that repeats itself endlessly. <br><br></td></tr></table><br>
<a name="520618"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ryan Moody</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> How do I tell an application to just freeze? <br></div><br><br>With the WaitKey() command, perhaps?<br><br>Ryan <br><br></td></tr></table><br>
<a name="520626"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Perturbatio</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> change your repeat forever loop to:<br><pre class=code>
While Not KeyDown(1)
Wend
</pre><br><br>*EDIT*<br>you might want to change the following:<br><pre class=code>
			If tReadWebFile Mod 100 = 0 Then BytesReceived (readWebFile, bytesToRead)
</pre><br>to:<br><pre class=code>
			If tReadWebFile Mod 1000 = 0 Then BytesReceived (readWebFile, bytesToRead)
</pre><br>it takes way too long to download otherwise (on broadband at least). <br><br></td></tr></table><br>
<a name="520668"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Picklesworth</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah, I didn't know the download speed problem was that simple :)<br>Thanks Perturbio!<br>Updated<br><br><br>My repeat forever loop is not closing on keydown 1 because people usually press the x at the top of the window :D<br>However, come to think of it, the program is simple enough to just get away with WaitKey. <br><br></td></tr></table><br>
<a name="521979"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Erroneouss</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Heh, I made something like this a while back, except for <br>my stocks! :)<br><br>So... Because I LOVVVVEEE comics, I have to use your thingie, and I also made one for daily Garfield, and soon, Get Fuzzy! :) <br><br></td></tr></table><br>
<a name="522199"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Picklesworth</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Actually, the code above only works on Sundays, because the images are Gifs every other day of the week.<br>Just make it open up the windows picture viewer (or just call execfile and hope for the best, like I did) with the path for the temporary image. Unless you feel like putting in a Gif loader :)<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
AppTitle "Dilbert.com daily comic By Dylan McCall"

Graphics 800,600,0,2
Const BGr=200,BGg=200,BGb=200
ClsColor BGr,BGg,BGb 
Color 0,0,0

; -----------------------------------------------------------------------------
; Load an image from the web, straight into our game!
; -----------------------------------------------------------------------------
Cls
Text 20, 20, "Downloading file - Please wait..."
Flip
comic = LoadWebImage (DilbertImagePath())

; -----------------------------------------------------------------------------
; Check for failure
; -----------------------------------------------------------------------------
If Not comic
	RuntimeError "Failed to load web image!" : End	
EndIf

MaskImage comic,BGr,BGg,BGb ;Am I forced to mask the image?!
Cls
DrawImage comic,10,10

WaitKey
End

Function DilbertImagePath$()
	If BlitzGet("http://www.dilbert.com/index.html",CurrentDir(),"temp_web_page.txt")
		page = OpenFile("temp_web_page.txt")
		Repeat
			inLine$=Lower(ReadLine(page))
			tag$ = "&lt;img src="+Chr(34)
			check = Instr(inLine$,tag$+"/comics/dilbert/archive/images/")
			If check
				PathStart=check+Len(tag$)
				PathEnd=Instr(inLine$,Chr(34),PathStart)
				Path$ = Mid(inLine$,PathStart,PathEnd-PathStart)
				CloseFile page
				DeleteFile "temp_web_page.txt"
				Return "http://www.dilbert.com"+Path$
			EndIf
		Until Eof(page)
		DeleteFile "temp_web_page.txt"
	EndIf	
End Function

; -----------------------------------------------------------------------------
; LoadWebImage -- uses BlitzGet Deluxe, based on Mark Sibly's HTTPGet
; -----------------------------------------------------------------------------
; james@...
; -----------------------------------------------------------------------------

Function LoadWebImage (webFile$)
	If BlitzGet (webFile$, CurrentDir(), "temp_web_image.bmp")		
		Repeat
			If FileType("temp_web_image.bmp")=1
				;Img = LoadImage ("temp_web_image.bmp")
				ExecFile "temp_web_image.bmp"
				End
				;Return Img
			EndIf		
		Forever
	EndIf
End Function

Function BlitzGet (webFile$, saveDir$, saveFile$,DEBUG=False)

	; -------------------------------------------------------------------------
	; Strip "http://" if provided
	; -------------------------------------------------------------------------
	If Left (webFile$, 7) = "http://" Then webFile$ = Right (webFile$, Len (webFile$) - 7)

	; -------------------------------------------------------------------------
	; Split into hostname and path/filename to download
	; -------------------------------------------------------------------------
	slash = Instr (webFile$, "/")
	If slash
		webHost$ = Left (webFile$, slash - 1)
		webFile$ = Right (webFile$, Len (webFile$) - slash + 1)
	Else
		webHost$ = webFile$
		webFile$ = "/"
	EndIf
		
	; -------------------------------------------------------------------------
	; Add trailing slash to download dir if not given
	; -------------------------------------------------------------------------
	If Right (saveDir$, 1) &lt;&gt; "\" Then saveDir$ = saveDir$ + "\"

	; -------------------------------------------------------------------------
	; Save filename -- get from webFile$ if not provided
	; -------------------------------------------------------------------------
	If saveFile$ = ""
		If webFile = "/"
			saveFile$ = "Unknown file.txt"
		Else
			For findSlash = Len (webFile$) To 1 Step - 1
				testForSlash$ = Mid (webFile$, findSlash, 1)
				If testForSlash$ = "/"
					saveFile$ = Right (webFile$, Len (webFile$) - findSlash)
					Exit
				EndIf
			Next
			If saveFile$ = "" Then saveFile$ = "Unknown file.txt"
		EndIf
	EndIf

	; DEBUG
	If DEBUG Then RuntimeError "Web host: " + webHost$ + Chr (10) + "Web file: " + webFile$ + Chr (10) + "Save dir: " + saveDir$ + Chr (10) + "Save file: " + saveFile$

	www = OpenTCPStream (webHost$, 80)
	If www
	
		WriteLine www, "GET " + webFile$ + " HTTP/1.1" ; GET / gets default page...
		WriteLine www, "Host: " + webHost$
		WriteLine www, "User-Agent: BlitzGet Deluxe"
		WriteLine www, "Accept: */*"
		WriteLine www, ""
		
		; ---------------------------------------------------------------------
		; Find blank line after header data, where the action begins...
		; ---------------------------------------------------------------------
				
		Repeat
			header$ = ReadLine (www)
			If Left (header$, 16) = "Content-Length: "	; Number of bytes to read
				bytesToRead = Right (header$, Len (header$) - 16)
			EndIf
		Until header$ = "" Or (Eof (www))
		
		If bytesToRead = 0 Then Goto skipDownLoad
		
		; ---------------------------------------------------------------------
		; Create new file to write downloaded bytes into
		; ---------------------------------------------------------------------
		save = WriteFile (saveDir$ + saveFile$)
		If Not save Then Goto skipDownload

		; ---------------------------------------------------------------------
		; Incredibly complex download-to-file routine...
		; ---------------------------------------------------------------------
				
		For readWebFile = 1 To bytesToRead
		
			If Not Eof (www) Then WriteByte save, ReadByte (www)
			
			; Call BytesReceived with position and size every 100 bytes (slows down a LOT with smaller updates)
			
			;tReadWebFile = readWebFile			
			;If tReadWebFile Mod 1000 = 0 Then BytesReceived (readWebFile, bytesToRead)

		Next

		CloseFile save
		
		; Fully downloaded?
		If (readWebFile - 1) = bytesToRead
			success = 1
		EndIf
		
		; Final update (so it's not rounded to nearest 100 bytes!)
		;BytesReceived (bytesToRead, bytesToRead)

		.skipDownload
		CloseTCPStream www
		
	Else
	
		RuntimeError "Failed to connect"
		
	EndIf
	
	Return success
	
End Function

; -----------------------------------------------------------------------------
; User-defined update function, called every 100 bytes of download -- alter to suit!
; -----------------------------------------------------------------------------
; TIP: Pass a user-defined type instead, with all data (this stuff plus URL, local filename, etc)
; -----------------------------------------------------------------------------
Function BytesReceived (posByte, totalBytes)
	; Example update code...
	Cls
	Text 20, 20, "Downloading file -- please wait..."
	Text 20, 40, "Received: " + posByte + "/" + totalBytes + " bytes (" + Percent (posByte, totalBytes) + "%)"
	Flip
End Function

; -----------------------------------------------------------------------------
; Handy percentage function
; -----------------------------------------------------------------------------
Function Percent (part#, total#)
	Return Int (100 * (part / total))
End Function
</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
