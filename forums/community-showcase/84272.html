<!DOCTYPE html><html lang="en" ><head ><title >Pocket Invaders</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Pocket Invaders</h1><a href="forums.php" >Community Forums</a>/<a href="topics.php?forum=61" >Showcase</a>/<a href="#bottom" >Pocket Invaders</a><br><br>
<a name="952097"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >EOF</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> This shooter was an old community project which I have tided up a little. The code has been restructed, graphics animated, and sfx cleaned up<br><br><b>SOURCE CODE INCLUDED</b> - Feel free to add to the code<br>It really needs the hi-score sorting out (non-functional at the  moment)<br>Timing code is very basic and the screen is window only<br>The idea was to create a 'pocket' type shooter<br><br>CONTROLS<br>===============<br>CURSORS LEFT / RIGHT - Move ship<br>SPACE or CURSOR UP - Shoot<br><br><img src="http://i289.photobucket.com/albums/ll234/syntax_photos/pi_screen1.png"><br><br>DOWNLOAD HERE - &gt; <a href="http://dabz.syntaxbomb.com/Files/pocketinvaders_01.zip" target="_blank">Pocket Invaders 01</a>  - Courtesy of DabHand <br><br></td></tr></table><br>
<a name="952117"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for sharing!<br><br>Btw: Aren't the aliens supposed to die instantly (= 1 hit kills)? <br><br></td></tr></table><br>
<a name="952131"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >EOF</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> In this version the aliens have a certain amount of strength before they die (otherwise the levels tend to race by) <br><br></td></tr></table><br>
<a name="952215"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Having trouble downloading this.  The zip is about 40K and says it's corrupted.  Tried several times... <br><br></td></tr></table><br>
<a name="952222"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >InvisibleKid</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> dl'd fine here.<br><br>strange i don't remember this as a community project before, but it's a fun little game... thanks <br><br></td></tr></table><br>
<a name="952223"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >EOF</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Jake,<br>It could be the SkyDrive service. It is generally horrible to use. For reference the file should be 1.81MB <br><br></td></tr></table><br>
<a name="952232"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Had difficulties downloading it as well. Can E-Mail it to Grey if needed?! <br><br></td></tr></table><br>
<a name="952264"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >xlsior</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> strange i don't remember this as a community project before, but it's a fun little game... thanks  <br></div><br><br>It's a really old one, from around the time BlitzMax itself was first released: <a href="http://www.blitzbasic.com/Community/posts.php?topic=47006" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=47006</a> <br><br></td></tr></table><br>
<a name="952270"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> I can't download it either, can somebody email it to me so I can at lest post it in my filefront page. Free, easy and fast.:) <br><br></td></tr></table><br>
<a name="952273"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dabhand</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> If Jim doesn mind:-<br><br><a href="http://dabz.syntaxbomb.com/Files/pocketinvaders_01.zip" target="_blank">http://dabz.syntaxbomb.com/Files/pocketinvaders_01.zip</a><br><br>If you want Jim, here is the tiny URL for it if you want to throw this up elsewhere:- <br><br><a href="http://tinyurl.com/dcbd75" target="_blank">http://tinyurl.com/dcbd75</a><br><br>Dabz <br><br></td></tr></table><br>
<a name="952279"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> thanks! Dabs. I will put it also in my downloads page along with other games with source I have there, if It's not a problem Jim. <br><br></td></tr></table><br>
<a name="952293"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Skydrive still weird for me.  Got from SyntaxBomb, thanks Dabz <br><br></td></tr></table><br>
<a name="952294"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Neat works well.  Perhaps some of the aliens could be 1-hitters just for that feeling of satisfaction. <br><br></td></tr></table><br>
<a name="952365"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dabhand</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Jim... I've just validated your account at Syntaxbomb mate... <br><br>@Chaps.. No bother! ;)<br><br>Dabz <br><br></td></tr></table><br>
<a name="952367"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >EOF</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Excellent .. Thanks Dabs! <br><br></td></tr></table><br>
<a name="952434"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dabhand</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> No bother Jim, its Qubes birthday today, so he'll probably sort out your hosting tomorrow mate, if he's not too rough! :D<br><br>But he'll sort it for you! ;)<br><br>Dabz <br><br></td></tr></table><br>
<a name="952512"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> I managed to add some code I had in my library sligtly modified. Let me know how bad I messed it up.;) <br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Rem
	Pocket Space Invaders
	Original concept and code by Paul Grayston - Yavin(Team Rebellion Software)
	Original start date [2nd Jan 2005 - 1am]
	Adapted by Jim Brown [April 2009]
	TODO
	============================
	1. Implement hi-score name entry. See Game.Died() function
End Rem 


SuperStrict


' To keep the games executable small we only include
' the necessary modules for the game
?Win32
Framework BRL.D3D7Max2D
?Linux
Framework BRL.GLMax2D
?MacOS
Framework BRL.GLMax2D
?
Import BRL.FreeAudioAudio
Import BRL.LinkedList
Import BRL.Math
Import BRL.Random
Import BRL.WAVLoader
Import BRL.PNGLoader
Import BRL.FreeTypeFont
Import BRL.Retro

Const VER$="v0.1"


AppTitle="Pocket Invaders "+VER$
?Win32
SetGraphicsDriver D3D7Max2DDriver()
?Linux
SetGraphicsDriver GLMax2DDriver()
?MacOS
SetGraphicsDriver GLMax2DDriver()
?

' Variable declarations
Global GFX_Width:Int 	= 	520
Global GFX_Height:Int 	= 	600
Const NUMALIENTYPES:Int=6 ' number of unique alien designs

' hiscore table
Global Hscore$[5+1,2+1]
For Local looper:Int=0 To 5
	Hscore$[looper,1]="Name-"+String(looper+1)
	Hscore$[looper,2]= "0"
Next

'Create the main graphics window
Graphics GFX_Width,GFX_Height,0,0
SetBlend alphablend
SetImageFont LoadImageFont("arial",32)


'********************************************************************
' load the media
'********************************************************************
Type Gfx
	Global Bkg:TImage , Menu:TImage , GameOver:TImage
	Global Block:TImage , playerBullet:TImage , Player:TImage
	Global Alien:TImage , alienBullet:TImage , Ufo:TImage
	Global GetReady:Timage
End Type

SetMaskColor 0,0,0
Gfx.Bkg 				= LoadImage("img/bkg2.png")
Gfx.Menu 				= LoadImage ("img/titlescreen.png")
Gfx.GameOver 			= LoadImage("img/gameover.png")
SetMaskColor 255,0,255
Gfx.Block 			= LoadImage ("img/block.png")
Gfx.playerBullet 		= LoadImage ("img/playerbullet.png")
Gfx.Player 			= LoadAnimImage("img/ship.png",40,40,0,2)
Gfx.Alien 			= LoadAnimImage("img/alienset.png",32,32,0,NUMALIENTYPES*2)
Gfx.alienBullet		= LoadAnimImage ("img/alienBullet.png",10,20,0,3)
Gfx.Ufo				= LoadImage("img/ufo.png")
gfx.getReady			= LoadImage("img/getReady.png")

MidHandleImage Gfx.Block
MidHandleImage Gfx.Alien
MidHandleImage Gfx.playerBullet
MidHandleImage Gfx.alienBullet
MidHandleImage gfx.getReady


Type Sfx
	Global PlayerShoot:TSound 	= LoadSound("sfx/playershoot.wav")
	Global AlienShoot:TSound 	= LoadSound("sfx/alienshoot.wav")
	Global AlienShunt:TSound 	= LoadSound("sfx/shuntdown.wav")
	Global PlayerDie:TSound 		= LoadSound("sfx/killplayer.wav")
	Global AlienHit:TSound 		= LoadSound("sfx/hitalien.wav")
	Global AlienDie:TSound 		= LoadSound("sfx/killalien.wav")
	Global BlockHit:TSound		= LoadSound("sfx/hitblock.wav")
	Global abBlockHit:TSound		= LoadSound("sfx/ab_hitblock.wav")
	Global UfoAppear:TSound 		= LoadSound("sfx/ufo.wav")
	Global UfoDie:TSound 		= LoadSound("sfx/killufo.wav")
End Type


'********************************************************************
' Fields common to all game objects
'********************************************************************
Type CommonFields
	Field X:Float
	Field Y:Float
	Field frame:Int
	Field speed:Float
	Field life:Int
End Type


'********************************************************************
' MAIN GAME CONTROL CODE
'********************************************************************

Type Game
	Const TITLESCREEN%=0 , PLAYING%=1 , INJURED%=2
	Const GAMEOVER%=3 , INITGAME%=4 ,GETREADY%=5
	Const ENTERNAME%=6,ENDGAME%=7, EXITGAME%=9
	Global SpeedIncrease:Float
	Global Mode:Int
	Global TimeOut:Int
	'
	Function Run()
		Repeat
			Select Game.Mode
				Case Game.TITLESCREEN					; Game.MainTitle
				Case Game.INITGAME						; Game.Init
				Case Game.GETREADY						; game.Ready
				Case Game.PLAYING , Game.INJURED 			; Game.Play
				Case game.ENTERNAME						; game.GetName
				Case Game.GAMEOVER   					; Game.Died
				Case Game.EXITGAME						; game._Exit
				Case game.ENDGAME						; Return
			End Select
		Flip()
		Forever
	End Function
	'
	Function Init()
		SeedRnd MilliSecs()
		Game.SpeedIncrease=1.1
		block.Init
		player1.level=1 ; player1.score=0 ; player1.life=3
		LoadWave player1.level
		Game.Mode=game.GETREADY
		wait1.init()
		text.Create(200,450)
		
	End Function
	'
	Function Reset()
		alien.list.Clear
		playerBullet.list.Clear
		alienBullet.list.Clear
		particle.list.Clear
		player1.level=1
		player1.x=GFX_Width/2
		player1.y=GFX_Height-(Gfx.Player.Height)
		player1.life=3
		ufo1.life=0
	End Function
	'
	Function GetName()
		Cls
		SetColor $ff,$ff,$ff
		DrawImage Gfx.Menu,0,0
		SetColor 0,$ff,0
		DrawText "HighScore",152,245
		For Local looper:Int=0 Until 5
			DrawText Hscore$[looper,1],148,282+looper*24
			DrawText Hscore$[looper,2],280,282+looper*24
		Next
		DrawText "Player1 Name :",80,450
		DrawText "       score :"+Hscore$[5,2],80,475
		
		text.draw
		Local n$ = text.input_Name()
		If n$ = text.INVALIDSTR n$ = "Player1"
		
		If n$ Then
			 Hscore$[5,1] = n$
			Local i:Int = 5
			Repeat
				If Hscore$[i,2].toint()&gt; Hscore$[i-1,2].toint()
				   Local temp$ = Hscore$[i-1,1]
				   Hscore$[i-1,1] = hscore[i,1]
				   Hscore$[i,1] = temp
				   temp$ = Hscore$[i-1,2]
				   Hscore$[i-1,2] = hscore[i,2]
				   Hscore$[i,2] = temp
				Else
					Exit
				EndIf 
				i:-1
			Until i = 0
			game.mode = game.TITLESCREEN
		EndIf
		
		If KeyHit(KEY_ESCAPE) Game.Mode=Game.EXITGAME
		Flip
	End Function
	'
	Function MainTitle()
		Cls
		SetColor $ff,$ff,$ff
		DrawImage Gfx.Menu,0,0
		SetColor 0,$ff,0
		DrawText "PRESS SPACE",158,GFX_Height-72
		DrawText "HighScore",152,245
		For Local looper:Int=0 Until 5
			DrawText Hscore$[looper,1],148,282+looper*24
			DrawText Hscore$[looper,2],280,282+looper*24
		Next
		Flip
		If KeyHit(KEY_SPACE)
			Game.Mode=Game.INITGAME ; FlushKeys
		EndIf
		If KeyHit(KEY_ESCAPE) Game.Mode=Game.EXITGAME
	End Function 
	'
	Function Play()
		Game.Update ; Game.Render
		If player1.life=0 And Game.TimeOut&lt;80
			Game.Mode=Game.GAMEOVER
			Game.Reset ; FlushKeys ; Return
		EndIf
		If KeyHit(KEY_ESCAPE)
			Game.Mode=Game.TITLESCREEN ; FlushKeys
			Game.Reset
		EndIf
	End Function
	
	Function Ready()
		game.Render()
		game.update()
		wait1.update()
		wait1.render()
		If KeyHit(KEY_ESCAPE)
			Game.Mode=Game.TITLESCREEN ; FlushKeys
			Game.Reset
		EndIf
	End Function 
	'
	Function Update()
		Global timeelapsed:Int
		' simple update limiter
		If MilliSecs()&lt;timeelapsed+12 Return
		timeelapsed=MilliSecs()
		ufo1.Update
		player1.Update
		playerBullet.Update
		alienBullet.Update
		alien.Update()
		particle.Update
		' Proceeds to next level when aliens are cleared
		If alien.list.Count()=0
			player1.level:+1
			If player1.level=&gt;11 Then player1.level=1
			loadWave player1.level
			player1.score:+ (block.list.count()*50)
			block.Init
			Game.Mode = Game.GETREADY
			wait1.init()
			playerBullet.List.clear()
		End If
		' if player got hit .. out of action for a while
		If Game.Mode=Game.INJURED
			Game.TimeOut:-1
			If Game.TimeOut=0 Then Game.Mode=Game.PLAYING
		EndIf
	End Function
	'
	Function Render()
		Global oldTime%,frameCount%,FPS%
		'SetScale Float(GFX_Width)/Float(bkg.Width) , Float(GFX_Height)/Float(bkg.Height)
		SetColor 255,255,255
		DrawImage Gfx.Bkg,0,0
		SetScale 1.0,1.0
		DrawText "Scr: "+Player1.score,1,3
		DrawText "Lvl: "+player1.level,GFX_Width-58,3
		If player1.score &gt; Hscore[0,2].toint()
			DrawText "Hsc: "+Player1.score,GFX_Width/2,3
		Else
			DrawText "Hsc: "+Hscore[0,2].toint(),GFX_Width/2,3
		EndIf
		' draw lives
		SetScale 0.6,0.6
		For Local n%=0 Until Player1.life
			DrawImage Gfx.Player,8+n*38,GFX_Height-26
		Next
		SetScale 1.0,1.0
		playerBullet.Render
		alienBullet.Render
		block.Render
		alien.Render
		ufo1.Render
		Player1.Render
		particle.Render
		' FPS timer
		If MilliSecs()&gt;oldTime
		   oldTime=MilliSecs()+1000
		   FPS=frameCount ; frameCount=0
		Else
		   frameCount:+1
		EndIf
		'
	End Function
	'
	Function Died()
		Cls
		SetColor $ff,$ff,$ff
		DrawImage Gfx.GameOver,0,0
		SetColor 0,$ff,0
		DrawText "PRESS SPACE / ESCAPE",158,GFX_Height-72
		Flip
		'
		' TODO: manual hi-score name entry not implemented
		' this part is not properly implemented
		If player1.score=&gt;Int(Hscore$[5,2])
			Hscore$[5,1]="player1"
			Hscore$[5,2]=String(player1.score)
		End If
		'
		If KeyHit(KEY_SPACE) Or KeyHit(KEY_ESCAPE)
			Game.Mode=Game.ENTERNAME ; player1.score=0
			FlushKeys
		EndIf
	End Function
	'
	Function _exit()
		scoreTable.SaveFile()
		game.Mode = game.ENDGAME
	End Function
End Type


'********************************************************************
' PARTICLE CODE
'********************************************************************
Type particle
	Global list:TList = New TList
	Field x     :Float
	Field y     :Float
	Field velx  :Float
	Field vely  :Float
	Field speed :Float
	Field life  :Int
	Field sz:Int
	Field r%,g%,b%
	'
	Method New()	
		Local angle:Int	
		angle=Rand(1,360) ; speed=2 ; life=50
		velx=Cos(angle)*(speed+Rand(-1,1))
		vely=Sin(angle)*(speed+Rand(-1,1))
		particle.list.AddLast Self
	End Method
	'
	Function Explode(x:Float,y:Float,rad:Int,r:Int,g:Int,b:Int , size:Int=4)
		For Local looper:Int= 1 To 30
			Local p:particle = New particle
			p.position x,y,rad
			p.r=r ; p.g=g ; p.b=b
			p.sz=size
		Next
	End Function
	'
	Function position(x:Float,y:Float,rad:Int=5)
		For Local p:particle = EachIn particle.list
			If p.x=0 And p.y=0
				p.x=x+Rand(-rad,rad)
				p.y=y+Rand(-rad,rad)
			End If
		Next
	End Function
	'
	Function Update()
		For Local p:particle = EachIn particle.list
			p.x:+p.velx ;	p.y:+p.vely
			p.r:-5 ; p.g:-5 ; p.b:-5
			If p.r&lt;0 p.r=0
			If p.g&lt;0 p.g=0
			If p.b&lt;0 p.b=0
			p.life:-1
			If p.life=&lt;0 particle.list.Remove p
		Next
	End Function
	'
	Function Render()
		For Local p:particle = EachIn particle.list
			SetColor p.r,p.g,p.b
			DrawRect p.x,p.y,p.sz,p.sz
		Next
	End Function
End Type



'********************************************************************
' PLAYER CODE
''********************************************************************
Type player Extends CommonFields
	Field Score:Int
	Field level:Int
	Field GunTimer:Int
	Field GunInterval:Int
	'
	Method New()
		guntimer=MilliSecs()
		guninterval=170 ; life=3 ; speed=2.0
		level=1 ; score=0
		x=GFX_Width/2 ; y=GFX_Height-(Gfx.Player.Height)
	End Method
	'
	Method Update()
		' move player left
		If KeyDown(KEY_LEFT) And player1.life&gt;0
			x:-Speed
			If x&lt;1 Then x=1
		EndIf
		' move player right
		If KeyDown(KEY_RIGHT) And player1.life&gt;0
			x:+Speed
			If x&gt;GFX_Width-(Gfx.Player.Width) Then x=GFX_Width-(Gfx.Player.Width)
		EndIf
		' shoot a player bullet
		If KeyDown(KEY_UP) Or KeyDown(KEY_SPACE)
			If Game.Mode=Game.PLAYING And playerBullet.list.Count()&lt;=2
				If MilliSecs()-guntimer=&gt;guninterval
					' fire off a new bullet
					playerBullet.Create player1.x+(Gfx.Player.Width/2)-2,player1.y-2
					guntimer=MilliSecs() ; frame=6
				EndIf
			EndIf		
		EndIf
	End Method
	'	
	Method Render()
		If Game.Mode=Game.INJURED
			SetColor 128+Sin(MilliSecs())*90,0,0
		Else
			SetColor 255,255,255
		EndIf
		DrawImage Gfx.Player,x,y,Sgn(frame)
		If frame&gt;0 Then frame:-1
		SetAlpha 1.0
	End Method
End Type



'********************************************************************
' ALIEN CODE
'********************************************************************
Type alien Extends CommonFields
	Global list:TList = New TList
	Global animframe:Int
	Global animframetimer:Int
	Global MoveSpeed:Float
	Global guntimer:Int
	Global guninterval:Int
	Field value:Int
	Field level:Int
	Field count:Int
	Field counter:Int
	Field img:TImage

	'
	Method New()
		guntimer=MilliSecs()
		guninterval=700-(player1.level*50)
		count = 500+Rand(2000)
		counter = Rand(count)
		alien.list.AddLast Self
	End Method
	'
	Function ShuntAllDown()
		alien.MoveSpeed=-alien.MoveSpeed
		PlaySound Sfx.AlienShunt
		For Local a:alien = EachIn alien.list
			a.x:+alien.MoveSpeed ; 
			If game.mode = game.PLAYING Then a.y:+10
		Next
		If alien.MoveSpeed&lt;0 Then alien.MoveSpeed:-.15 Else alien.MoveSpeed:+.15
	End Function
	'
	Function Update()
		If alien.list.Count()=0 Return
		For Local thisAlien:alien=EachIn alien.list
			If thisAlien.counter  &gt; 0 And game.Mode = game.PLAYING Then thisAlien.counter :-1
			' decide if this alien should shoot
			If Game.Mode=Game.PLAYING
				'added semi-smart shooting ****************************************************
				If ImageInRange(thisAlien,player1,gfx.alien,gfx.player) And thisAlien.counter = 0
					alien.guntimer=MilliSecs()+Rand(500)
					alienBullet.Create thisAlien.x,thisAlien.y
					thisAlien.counter = thisAlien.count
				'***********************************************************************************
				ElseIf MilliSecs()-alien.guntimer&gt;alien.guninterval
					alien.guntimer=MilliSecs()+Rand(500)
					alienBullet.Create thisAlien.x,thisAlien.y
				EndIf
			EndIf
			' check if each block collides with alien
			For Local thisBlock:block = EachIn block.list
				If ImagesOverlap(thisBlock,thisAlien , Gfx.Block,Gfx.Alien)
					thisBlock.y:+5 ; thisBlock.life:-2
					thisAlien.life:-1
					If thisAlien.life&lt;=0
						thisAlien.Destroy
						Return
					EndIf
				End If
			Next
			' check if player gets hit by alien
			If Game.Mode=Game.PLAYING
				If ImagesOverlap(thisAlien,player1 , Gfx.Alien,Gfx.Player)
					particle.Explode player1.x,player1.y,3,255,40,255
					particle.Explode player1.x,player1.y,4,160,140,30
					particle.Explode player1.x,player1.y,5,16,140,230
					Game.Mode=Game.INJURED ; Game.TimeOut=180
					PlaySound Sfx.PlayerDie
					player1.life:-1 ; thisAlien.Destroy
					Return
				End If
			EndIf
			' move the aliens sideways
			thisAlien.x:+alien.MoveSpeed
			If alien.MoveSpeed&lt;0 ' check left
				If (thisAlien.x+alien.MoveSpeed)&lt;=0 Then alien.ShuntAllDown
			Else ' check to the right
				If (thisAlien.x+alien.MoveSpeed)&gt;=GFX_Width Then alien.ShuntAllDown
			EndIf
			' check Y postion and life
			If thisAlien.y&gt;GFX_Height Or thisAlien.life=&lt;0
				thisAlien.Destroy
			EndIf
		Next
		' update animation frame
		alien.animframetimer:+1
		If alien.animframetimer=32
			alien.animframe=1-alien.animframe
			alien.animframetimer=0
		EndIf
	End Function
		'
	Function Render()
		For Local thisAlien:alien=EachIn alien.list
			Select thisAlien.level
				Case 1 ; SetColor 185,255,155
				Case 2 ; SetColor 255,255,0
				Case 3 ; SetColor 255,0,0
				Case 4 ; SetColor 0,255,255
				Case 5 ; SetColor 0,0,255
				Case 6 ; SetColor 255,0,255
			End Select
			DrawImage Gfx.Alien , thisAlien.x , thisAlien.y , 6-thisAlien.level + (alien.animframe*NUMALIENTYPES)
		Next
	End Function
	'
	Method Destroy()
		Select level
			Case 1 ; particle.Explode x,y,4,185,255,155
			Case 2 ; particle.Explode x,y,4,255,255,0
			Case 3 ; particle.Explode x,y,4,255,0,0
			Case 4 ; particle.Explode x,y,4,0,255,255
			Case 5 ; particle.Explode x,y,4,0,0,255
			Case 6 ; particle.Explode x,y,4,255,0,255
		End Select
		alien.list.Remove Self
	End Method
End Type


'********************************************************************
' UFO CODE
'********************************************************************
Type ufo Extends CommonFields
	'
	Method Appear()
		If life&lt;&gt;0 Return
		x=-20 ; speed=1.4
		If Rnd(1)&gt;.5
			speed=-speed ; x=GFX_Width+20
		EndIf
		y=24 ; life=1 ; PlaySound Sfx.UfoAppear
	End Method
	'
	Method Update()
		If life=0
			If Rand(1000)=8 ufo1.Appear
		EndIf
		x:+speed
		If x&lt;-28 Or x&gt;GFX_Width+28
			life=0 ; Return
		EndIf
	End Method
	'
	Method Render()
		If life&lt;&gt;0
			SetColor $ff,$ff,$ff
			DrawImage Gfx.Ufo,x,y
		EndIf
	End Method
End Type


'********************************************************************'
' BLOCK CODE
'********************************************************************
Type block Extends CommonFields
	Global list:TList = New TList
	Field Origin:Int	
	'
	Function Init()
		block.list.Clear
		For Local n:Int =1 To 3
			Local b:Block = New Block
			b.x=n*(GFX_Width/3)-Gfx.Block.Width*2
			b.y=GFX_Height-10-Gfx.Player.Height
			b.Origin = GFX_Height-20-(Gfx.Player.height)
			b.life=250
			block.list.AddLast b
		Next
	End Function
	'
	Function Render()
		For Local thisBlock:block=EachIn block.list
			SetColor 255,thisBlock.life,thisBlock.life
			DrawImage Gfx.Block,thisBlock.x,thisBlock.y
			If thisBlock.life=&lt;0 Then block.list.Remove thisBlock
		Next
	End Function
End Type


'********************************************************************
'	playerBullet CODE
'********************************************************************
Type playerBullet Extends CommonFields
	Global list:TList = New TList
	' create a new player bullet
	Function Create(xPos:Int,yPos:Int)
		Local b:playerBullet=New playerBullet
		b.x=xPos ; b.y=yPos ; b.speed=3.8 ; b.life=1
		playerBullet.list.AddLast b
		PlaySound Sfx.playershoot
	End Function
	' check player bullets against blocks, aliens, and ufo
	' update the bullet position and draw it too
	Function Update()
		SetColor 255,255,255
		For Local thisBullet:playerBullet=EachIn playerBullet.list
			' check 'player bullets' collide with Blocks
				For Local thisBlock:Block = EachIn block.list
					If ImagesOverlap(thisBullet,thisBlock , Gfx.playerBullet,Gfx.Block)
						thisBullet.life=0 ; thisBlock.life:-20
						If thisBlock.life=&lt;0 Then thisBlock.life=0
						If thisBlock.y&gt;thisBlock.Origin
							thisBlock.y:-2
							If thisBlock.y&lt;thisBlock.origin Then thisBlock.y=thisBlock.origin
						End If
						PlaySound Sfx.BlockHit
						particle.Explode thisBlock.x,thisBlock.y,1,150,150,100
					End If
				Next
				' check 'player bullets' collide with aliens
				For Local thisAlien:alien = EachIn alien.list
					If ImagesOverlap(thisBullet,thisAlien , Gfx.playerBullet,Gfx.Alien)
						thisBullet.life=0 ; thisAlien.life:-1
						If thisAlien.life=&lt;0
							player1.score:+thisAlien.value
							thisAlien.Destroy
							PlaySound Sfx.AlienDie
						Else
							particle.Explode thisAlien.x,thisAlien.y,48,155,155,155 , 2
							PlaySound Sfx.AlienHit
						EndIf
					End If
				Next
				' check 'player bullets' collide with ufo
				If ufo1.life&lt;&gt;0
					If ImagesOverlap(thisBullet,ufo1 , Gfx.playerBullet,Gfx.Ufo)
						player1.score:+50
						ufo1.life=0
						particle.Explode ufo1.x,ufo1.y,3,$ff,$ff,$ff
						PlaySound Sfx.UfoDie
					EndIf
				EndIf
			' update 'player bullets' position
			thisBullet.y:-thisBullet.speed
			If thisBullet.y&lt;0 Or thisBullet.life=0 Then playerBullet.list.Remove thisBullet
		Next
	End Function
	'
	Function Render()
		For Local thisBullet:playerBullet=EachIn playerBullet.list
			DrawImage Gfx.playerBullet,thisBullet.x,thisBullet.y
		Next
	End Function
End Type



'********************************************************************
'	alienBullet CODE
'********************************************************************

Type alienBullet Extends CommonFields
	Global list:TList = New TList
	' create a new alien bullet
	Function Create(posX:Int,posY:Int)
		Local ab:alienBullet=New alienBullet
		ab.x=posX ; ab.y=posY ; ab.life=1 ; ab.frame=0
		ab.speed:+(Game.SpeedIncrease+(player1.level)+Rnd(2.8))/1.0
		alienBullet.list.AddLast ab
		PlaySound Sfx.AlienShoot
	End Function
	'
	Function Update()
		SetColor 255,255,255
		For Local thisalienBullet:alienBullet=EachIn alienBullet.list
		' check 'alien bullets' against blocks
			For Local thisBlock:block = EachIn block.list
				If ImagesOverlap(thisalienBullet,thisBlock , Gfx.alienBullet,Gfx.Block)
					thisalienBullet.life=0 ; thisBlock.life:-30
					If thisBlock.life=&lt;0 Then thisBlock.life=0
					PlaySound Sfx.abBlockHit
					particle.Explode thisBlock.x,thisBlock.y,1,100,100,100
				End If
			Next
			' check if player gets hit
			If Game.Mode=Game.PLAYING
				If ImagesOverlap(thisalienBullet,player1 , Gfx.alienBullet,Gfx.Player)
					particle.Explode player1.x,player1.y,3,255,40,255
					particle.Explode player1.x,player1.y,4,160,140,30
					particle.Explode player1.x,player1.y,5,16,140,230
					Game.Mode=Game.INJURED ; Game.TimeOut=180
					PlaySound Sfx.PlayerDie
					player1.life:-1 ; thisalienBullet.life=0
				End If
			EndIf
			' update position
			thisalienBullet.y:+thisalienBullet.speed
			thisalienBullet.frame = (thisalienBullet.frame+1)Mod 3
			If thisalienBullet.y&gt;GFX_Height Or thisalienBullet.life&lt;=0 Then alienBullet.list.Remove thisalienBullet
		Next
	End Function
	'
	Function Render()
		SetColor $ff,$ff,$ff
		For Local thisalienBullet:alienBullet=EachIn alienBullet.list
			DrawImage Gfx.alienBullet,thisalienBullet.x,thisalienBullet.y,thisalienBullet.frame
		Next
	End Function
End Type

Type wait Extends CommonFields
	Field Duration:Int
	Field time:Int
	Field angle:Float
	
	Method New()
		x = GraphicsWidth()/2
		y = GraphicsHeight()/2
		duration = 3000
		angle = 0
	End Method
	
	Method init()
		time = MilliSecs()
		angle = 0
	End Method
	
	Method update()
		If time +Duration &lt; MilliSecs()game.Mode = game.PLAYING
		angle = (angle + 90.0/duration) Mod 360
	End Method
	
	Method Render()
		SetScale 1+Sin(angle)*2,1+Sin(angle)*2
		SetColor 255,255,255
		DrawImage gfx.GetReady,x,y
		SetScale 1,1
	End Method
End Type
		

Type TText

	Field x				:Int
	Field y				:Int
	
	Field text			:String
	Field charWidth		:Int
	Field charHeight		:Int
	Field displayWidth		:Int
	Field displayHeight		:Int
	
	Field color			:Tcolor
	Field oldcolor			:Tcolor
	Field backcolor		:Tcolor
	Field back			:Int		
	Const FNAMELENGTH		:Int = 12
	Const MAXDISPLAY		:Int =  12
	Const INVALIDSTR		:String = "*/\:?&lt;&gt;"+Chr(34)+Chr(0)+Chr(KEY_BACKSPACE)+Chr(KEY_ENTER)
	
	Method Create(x:Int,y:Int)

		Self.x = x
		Self.y = y
		
		charWidth = TextWidth("X")
		charHeight= TextHeight("X")
		
		displayWidth = charWidth * MAXDISPLAY+charWidth
		displayHeight = charHeight
		
		color = New Tcolor
		color.red = 0
		color.green = $FF
		color.blue = 0
		backcolor = New Tcolor
		backcolor.red = 0
		backcolor.green = 0 
		backcolor.blue = 180
		oldcolor = New Tcolor
	
	End Method
	Method Input_Name:String(x:Int = 0,y:Int= 0)
		
		Local c:String = Chr(GetChar())
		If text.length &lt; FNAMELENGTH Then 
			If Not Instr(invalidstr,c)
				text=text+c
			EndIf
		EndIf
		If c[0] = KEY_BACKSPACE And text.length&gt;0 Then
			text = Left$(text,text.length-1) 
		ElseIf c[0] = KEY_ENTER 
			Local name:String = text
			text = Null
			If name.length = 0 Then name = INVALIDSTR 
			Return name
		EndIf
		Return Null

	End Method
	
	Method SetString(s:String)
		text =  s
	End Method
	
	Method GetString:String()
		Return text
	End Method
	Method ClearField()
		text = Null
	End Method
	Method draw(x:Int=0,y:Int=0)
		
		Local displayString:String
		GetColor oldcolor.red,oldcolor.green,oldcolor.blue
		Local cx:Int = Self.x + x
		Local cy:Int = Self.y + y
		SetAlpha .7
		SetColor backcolor.red, backcolor.green, backcolor.blue
		DrawRect cx,cy,displayWidth,displayHeight
		SetAlpha 1.0
		SetColor color.red,color.green,color.blue
		If text.length &lt; MAXDISPLAY
			displayString = text
		Else
			displayString = Right(text,MAXDISPLAY)
		EndIf
'		displays the visible part of the string.
		DrawText displayString,cx+2,cy
'		draw cursor
		DrawRect cx+2+TextWidth(DisplayString),cy+TextHeight(displayString)-4,charWidth,4
		
		SetColor oldcolor.red,oldcolor.green,oldcolor.blue
		
	End Method

End Type

Type TScoreTable
	Field _FileName:String=Null
	Field text:String
	
	Method init:Int(fileName:String)
		If Not ReadDir(ExtractDir(fileName)) CreateDir(ExtractDir(fileName))
		If Not ReadDir(ExtractDir(fileName))Return False
		If -1 = FileSize(filename) If Not CreateFile(filename) Return False
		_FileName = filename
	 	Return True
	End Method
	
	Method LoadFile:Int()
		If _FileName = Null Then Return False
		Local file:TStream
		Local Size:Int = FileSize(_FileName)
		If size &gt; 0
			file=OpenFile(_FileName) 
			If Not file Return False
			For Local index:Int = 0 Until 6
				Hscore$[index,1]= ReadString(file,12)
				Hscore$[index,2]= String(ReadInt(file)) 
			Next
			CloseFile(file)
			Return True
		Else
			Return False
		EndIf
		
	End Method
	
	Method SaveFIle:Int()
		If _FileName=Null Return False
		Local file:TStream
		Local Size:Int = FileSize(_FileName)
         	Clear_File()
		file = OpenFile(_FileName)
		If file 
			For Local index:Int = 0 Until 6
				WriteString file,LSet(Hscore$[index,1],12)
				WriteInt file,Hscore$[index,2].toint()
			Next
			CloseFile(file)
		Else
			Return False 
		EndIf	
		Return 1
	End Method
	
	Method Clear_File:Int()
		If _FileName = Null Then Return False
		DeleteFile(_FileName)
		CreateFile(_FileName)
	EndMethod
End Type

Type Tcolor
	Field 	red		:Int,..
		 	green	:Int,..
		 	blue		:Int
End Type

'********************************************************************
'	RETURN 'TRUE' IF TWO IMAGES ARE OVERLAPPING
'********************************************************************

Function ImagesOverlap:Int(i1:CommonFields,i2:CommonFields , img1:TImage,img2:TImage)
	' sprite 1 rect area
	Local x0:Int = i1.x - img1.handle_x
	Local y0:Int = i1.y - img1.handle_y
	Local x1:Int = x0   + img1.width
	Local y1:Int = y0   + img1.height
	' sprite 2 rect area
	Local x2:Int = i2.x - img2.handle_x
	Local y2:Int = i2.y - img2.handle_y
	Local x3:Int = x2   + img2.width
	Local y3:Int = y2   + img2.height
	' check overlapping
	If x0 &gt; x3 Or x1 &lt; x2 Then Return False
	If y0 &gt; y3 Or y1 &lt; y2 Then Return False
	Return True
End Function

Function ImageInRange:Int(i1:commonFields,i2:commonFields,img1:Timage,img2:Timage)
	' sprite 1 pos
	Local x0:Int = i1.x - img1.handle_x * 3
	Local x1:Int = x0   + img1.width
	' sprite 2 pos
	Local x2:Int = i2.x - img2.handle_x
	Local x3:Int = x2   +img2.width
	' check overlapping
	If x0 &gt; x3 Or x1 &lt; x2 Then Return False
	Return True
End Function

'********************************************************************
'	ALIEN PATTERN WAVE LOADER
'********************************************************************

Function LoadWave(id:Int)
	Game.SpeedIncrease:+0.2
	alien.MoveSpeed=Game.SpeedIncrease
	Local px:Int , py:Int , w:String
	If id=11 Or id=1 Then RestoreData level
	For py = 1 To 6
		ReadData w
		For px =0 Until Len(w)
			If w[px]=120 ' character 'x'
				Local a:alien=New alien
				a.x=px*Gfx.Alien.Width ; a.y=24+(py*Gfx.Alien.Height)
				a.value=6-py ; a.level=py ; a.life = 6 - (py+1)/1.5
			End If			
		Next
	Next
End Function

'********************************************************************
' GAME INITIALISATION
'********************************************************************
Global Player1:player = New player
Global ufo1:ufo=New ufo
Global wait1:wait = New wait
Global text:Ttext = New Ttext
Global scoreTable:TScoreTable = New TScoreTable 
scoreTable.init("score\myscore.dat")
scoreTable.loadFile()


'********************************************************************
' RUN THE GAME LOOP
'********************************************************************
Game.Run
End



'********************************************************************
' ALIEN WAVE PATTERN DATA
'********************************************************************
	
#Level
'level 1
DefData "xx....xx"
DefData ".xx..xx."
DefData "..x..x.."
DefData "........"
DefData ".xxxxxx."
DefData "..xxxx.."
'level 2
DefData "...xx..."
DefData "...xx..."
DefData "...xx..."
DefData "..xxxx.."
DefData ".xxxxxx."
DefData "xxxxxxxx"
'level 3
DefData "..x..x.."
DefData ".xxxxxx."
DefData "xx.xx.xx"
DefData "xx.xx.xx"
DefData "..x..x.."
DefData ".x.xx.x."
'level 4
DefData ".xxxxxx."
DefData "x..xx..x"
DefData "x.x..x.x"
DefData "xx....xx"
DefData "x......x"
DefData ".x....x."
'level 5
DefData "..xxxx.."
DefData ".xxxxxx."
DefData ".x....x."
DefData ".xxxxxx."
DefData "xxxxxxxx"
DefData "..xxxx.."
'level 6
DefData "..x.x.x.x.."
DefData "x.x.x.x.x.x"
DefData "..x.x.x.x.."
DefData "x.x.x.x.x.x"
DefData "..x.x.x.x.."
DefData "..x.x.x.x.."
'level 7
DefData "xxxxxxxxx"
DefData "..x.x.x."
DefData "xxxxxxxxx"
DefData "...x.x.."
DefData "xxxxxxxxx"
DefData "..xxxxx.."
'level 8
DefData ".xx......"
DefData "x..x....."
DefData "x..x..xx."
DefData ".xx..x..x"
DefData ".....x..x"
DefData "......xx."
'level 9
DefData "..xxxxxx.."
DefData ".xx....xx."
DefData "xxx....xxx"
DefData "..xxxxxx.."
DefData "...xxxx..."
DefData "..xxxxxx.."
'level 10
DefData "..xxxxxxxxx."
DefData ".x.xx......x"
DefData ".x..xxx....x"
DefData ".x....xxx..x"
DefData ".x......xx.x"
DefData "..xxxxxxxxx."
</textarea><br>add this image to the "img" directory as<br>"getready.png"<br><img src="http://img2.imageshack.us/img2/7485/getready.png"><br><br>replace "alienbullet.png" with this:<br><img src="http://img511.imageshack.us/img511/2619/alienbullet.png"><br>or get the game from my signature link <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
