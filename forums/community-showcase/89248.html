<!DOCTYPE html><html lang="en" ><head ><title >Quick shadows</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Quick shadows</h1><a href="forums.php" >Community Forums</a>/<a href="topics.php?forum=61" >Showcase</a>/<a href="#bottom" >Quick shadows</a><br><br>
<a name="1013443"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kalisme</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I sort of stayed up tonight (first day of class today, I should of been sleeping!), and I slapped together this from the shadow code I put in the code archives ages ago.<br><br>I remember some people being annoyed that it wasn't coded as usable functions instead of the badly written tutorial... So I think I'm going to add an actual library in the code archives when I clean and fix this up...<br><br>This is much simpler and faster (I think... not really sure) than the "shaKAL" library I wrote years ago... I think it looks nicer too. I was inspired to go back and look into the code because I recently saw the "ombres" shadow library for blitz... it was the fastest and simplest one I have seen. So when I looked through it I noticed one big thing that sped it up was lack of recreating meshes and instead just copying them without culling... I was too lazy to learn how to use the library (plus I don't like using other peoples libraries... I worry it'll make me lazy), so I started coding a new library.<br><br>This example is has the library just badly slapped into the code, I'm still cleaning it up, so please don't judge me by my messy coding. If there's any interest I'll gladly post the library as open source. My mane goal is to keep it really simple... Not even worrying about blurring textures or anything. I figure it might work nicely for casual games and so on.<br>Anywho, ahem:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Quick little shadow thingy
;-Kevin "Kalisme" Laherty 2010


Graphics3D 640,480

Dim TriVertex(3)

Dim TriVx#(3)
Dim TriVy#(3)
Dim TriVz#(3)

Dim TriVnx#(3)
Dim TriVny#(3)
Dim TriVnz#(3)


Global shad_x = 100000
Global shad_y = 100000
Global shad_z = 100000


Dim ShadowVertex(3)

Global sh_whit = CreateTexture(16,16)
TextureBlend sh_whit,3
Color 255,255,255
Rect 0,0,20,20
CopyRect 1,1,16,16,0,0,BackBuffer(),TextureBuffer(sh_whit)

Global sh_white = CreateBrush()
BrushTexture sh_white,sh_whit
BrushAlpha sh_white,0


Global sh_piv=CreatePivot()

Type s_caster
	Field ent,mesh,scl#,off#
End Type

Type s_light
	Field ent,dist#
End Type

Type s_receiver
	Field ent,mesh,ndg#
End Type	

Type shmesh
	Field ent,tex,surf
End Type

Global ssize=128
;
Global shadowcam=CreateCamera() 
CameraViewport shadowcam,0,0,ssize,ssize 	;the viewport the shadows will be rendered through.
CameraClsColor shadowcam,255,255,255 		;Yup, white background for shadow textures.
CameraProjMode shadowcam,2					;Orthographic view, sorry, I've only figured out very simple directional shadows.
CameraZoom shadowcam,.06/xscale# 			;though the function "shadow_test" does this every time it's used, here it is for you to see anyway. It's not needed here.
CameraFogMode shadowcam,1					;The fog is put on so it seems like everythings white... to make a caster visable to the camera, disable fogging to that object.
CameraFogRange shadowcam,1,1
CameraFogColor shadowcam,255,255,255

HideEntity shadowcam ;we don't want it onscreen now.

Function shadow_test(s_camera,g_camera,sh_light,s_caster,s_receiver,tex,ldis#,Xscale#,offset#)
	
	
	
	CameraZoom s_camera,.06/Xscale#		;&lt;-setting up the zoom for the shadow rendering camera.
	
;cleartexture(tex)	;&lt;- I added this because alot of graphics cards seem to render fog differant to mine.. Hopefully this fixes the blurring problem.
	HideEntity g_camera			;The in game camera must be hidden,
	ShowEntity s_camera			;and the shadow camera must become visable.
	
	EntityFX s_caster,1+8		;&lt;- setting up the caster:
	EntityColor s_caster,0,0,0	;It needs to be black and unnefexted by fog and lights.
	EntityFX s_receiver,1
;The shadow rendering camera must be placed at
;the light source Then pointed at the caster.
	
	PositionEntity s_camera,EntityX(sh_light,1)+shad_x,EntityY(sh_light,1)+shad_y,EntityZ(sh_light,1)+shad_z
	PositionEntity s_caster,EntityX(s_caster) + shad_x,EntityY(s_caster) + shad_y,EntityZ(s_caster) + shad_z
	PositionEntity s_receiver,EntityX(s_receiver) + shad_x,EntityY(s_receiver) + shad_y,EntityZ(s_receiver) + shad_z
	MoveEntity s_caster,0,offset#,0
	PointEntity s_camera,s_caster
	
;PositionEntity s_camera,EntityX(sh_light,1),EntityY(sh_light,1),EntityZ(sh_light,1)
;PointEntity s_camera,s_caster
	
;Here we render the camera and convert it to a texture.
	RenderWorld()
	Color 255,255,255: Rect 0,0,ssize,ssize,0 ;&lt;-this rectangle is used to cull anything that tries to poke outside of the textures boundry box.
	CopyRect 0,0,ssize,ssize,0,0,BackBuffer(),TextureBuffer(tex)
	
	
	PositionEntity s_camera,EntityX(sh_light,1),EntityY(sh_light,1),EntityZ(sh_light,1)
	MoveEntity s_caster,0,-offset#,0
	PositionEntity s_caster,EntityX(s_caster) - shad_x,EntityY(s_caster) - shad_y,EntityZ(s_caster) - shad_z
	PositionEntity s_receiver,EntityX(s_receiver) - shad_x,EntityY(s_receiver) - shad_y,EntityZ(s_receiver) - shad_z
	PointEntity s_camera,s_caster
;EntityTexture s_receiver,tex
	
;The surface and verticy loop.
;This loops through every surface of the receiver mesh,
;Then every surfaces vertecies.
;Our main focus should be the
;"Tformpoint" And "vertextexcoords" commands.
;These commands actually paint the texture onto the
;receiver correctly.
	For s=1 To CountSurfaces(s_receiver) 
		surf=GetSurface(s_receiver,s) 
		For v=0 To CountVertices(surf)-1 
			TFormPoint VertexX(surf,v),VertexY(surf,v),VertexZ(surf,v),s_receiver,s_camera
			VertexTexCoords surf,v,(TFormedX()/Xscale#)/32+0.5,1-((TFormedY()/Xscale#)/32+.5)
			
			
			PositionEntity sh_piv,VertexX(surf,v)+EntityX(s_receiver,1),VertexY(surf,v)+EntityY(s_receiver),VertexZ(surf,v)+EntityZ(s_receiver)
			dis2# =EntityDistance(sh_piv,s_camera)
			diss#=(ldis#-dis2#)/ldis#
			dis#=1-diss#
;VertexColor surf,v,(128*dis#)*2,(128*dis#)*2,(128*dis#)*2
			VertexColor surf,v,(128*dis#)*2,(128*dis#)*2,(128*dis#)*2
			
			
			
		Next 
	Next 
	
	EntityBlend s_receiver,2
	EntityFX s_receiver,1+2+8+16
	
;Here we just switch the attributes of the caster back to normal.
;In an actual game engine you might have to make this more complex.
;I suggest keeping the old attributes in tempory memory then
;calling them back at about this point.
	EntityFX s_caster,0
	EntityColor s_caster,200,0,0
	
;here we hide the shadow camera and bring back the game camera,
;ready to go back to the main game loop like nothing ever happend.
	ShowEntity g_camera
	HideEntity s_camera
	
End Function


;This last little function holds no real importance, it just clears a texture and makes it white.
;I added this because when I first released this code I found alot of graphics cards don't make
;the fog as thick as mine (I'm guessing it's because my card it cheap), and the shadow texture
;Grabbed the old shadow texture off the ground and blurred with the new one...
;very annoying and unpretty.... Oh well... Hope this works much better.
Function cleartexture(tex)
	SetBuffer TextureBuffer( tex ) 
	Color 255,255,255
	Rect 0,0,TextureWidth(tex)+1,TextureHeight(tex)+1
	SetBuffer BackBuffer()
End Function


Function create_caster(mesh,scl#,off#)
	
	d.s_caster = New s_caster
	
	d\ent = mesh
	d\scl = scl#
	d\off#=off#
	
End Function


Function create_receiver(mesh,ndg#)
	
	d.s_receiver = New s_receiver
	
	d\ent = mesh
	d\ndg# = ndg#
	
End Function


Function create_light(mesh,dist)
	
	d.s_light = New s_light
	
	d\ent = mesh
	d\dist = dist
	
End Function

Function clean_mesh()
	For d.shmesh = Each shmesh
		FreeEntity d\ent
		FreeTexture d\tex
		Delete d
	Next
End Function

Function render_shad()
	clean_mesh()
	a#=0
	For li.s_light = Each s_light
		a#=a#+1
		For ca.s_caster = Each s_caster
			For re.s_receiver = Each s_receiver
				If ca\ent &lt;&gt; re\ent 
					final(li\ent,ca\ent,re\ent,li\dist#,ca\scl#,ca\off#)
				EndIf			
			Next
		Next
	Next
End Function


Function final(ent1,ent2,ent3,ldis#,scl#,off#)
	ndg#=.000001
	caster = CopyEntity(ent2)
	d.shmesh = New shmesh
	d\ent = CopyMesh(ent3)
				;MoveEntity d\ent,5,5,8
	PositionEntity d\ent,EntityX(ent3,1),EntityY(ent3,1),EntityZ(ent3,1)
	;fix texture problem
	For a = 1 To CountSurfaces(d\ent)
		sur = GetSurface(d\ent,a)
		;EntityTexture d\ent,sh_white,0,a-1
		;If a-1 &lt;&gt; 0
		;PaintSurface sur,sh_white
		;EndIf
	Next
	
	d\tex=CreateTexture(ssize,ssize,16+32) ;create the shadow texture
	TextureBlend d\tex,3
	
	EntityTexture d\ent,d\tex
	
	shadow_test(shadowcam,camera,ent1,caster,d\ent,d\tex,ldis#,scl#,off#)
	FreeEntity caster
End Function

Const fps=60 


box1=CreateCube()
ScaleMesh box1,300,100,300
EntityColor box1,0,200,50

box2=CreateCube()
ScaleMesh box2,10,50,20
PositionEntity box2,100,150,50
EntityColor box2,100,20,0

box3=CreateCube()
ScaleMesh box3,10,50,60
PositionEntity box3,0,150,0
EntityColor box3,80,20,50


box4=CreateCube()
ScaleMesh box4,70,10,20
PositionEntity box4,50,200,20


ball=CreateSphere()
ScaleMesh ball,70,70,70
PositionEntity ball,70,80,20

create_receiver(box1,4)

create_caster(box2,5,0)
create_receiver(box2,4)

create_caster(box3,5,0)
create_receiver(box3,4)

create_caster(box4,5,0)


create_caster(ball,5,0)
create_receiver(ball,4)


Global camera=CreateCamera()
	PositionEntity camera,0,200,-800
	TurnEntity camera,20,0,0

	light= CreateLight(2)
	light_ball=CreateSphere(6,light) ;&lt;---here a green ball is attached to the light....
	EntityColor light_ball,0,200,0   ;    it holds no importance... it's just so you can see it.
	ScaleEntity light_ball,20,20,20
	EntityFX light_ball,1
	PositionEntity light,-20,600,-60
	create_light(light,1000)
	
	light2= CreateLight(2)
	light_ball2=CreateSphere(6,light2) ;&lt;---here a green ball is attached to the light....
	EntityColor light_ball2,0,200,0   ;    it holds no importance... it's just so you can see it.
	ScaleEntity light_ball2,20,20,20
	EntityFX light_ball2,1
	PositionEntity light2,50,600,-60
	create_light(light2,5000)
	
	period=1000/fps
time=MilliSecs()-period


;-------------------------------------------------------------------Main loop
While Not KeyHit(1)
	render_shad()
	
;==========================the mainloop (fps reasons)========
	Repeat
		elapsed=MilliSecs()-time
	Until elapsed
	ticks=elapsed/period
	tween#=Float(elapsed Mod period)/Float(period)
	For k=1 To ticks
		time=time+period
		If k=ticks Then CaptureWorld
		;basic game tasks
		updatecamera()
		UpdateWorld
		TurnEntity light, 0,.5,0
		MoveEntity light,0,0,5
		TurnEntity box4,1,0,3
	Next
	RenderWorld
;===========================================================	
	Flip
Wend
End
;-------------------------------------------------------------------end of main loop

Function updatecamera()
If KeyDown(200) Then MoveEntity camera,0,0,4
If KeyDown(203) Then TurnEntity camera,0,2,0
If KeyDown(205) Then TurnEntity camera,0,-2,0
If KeyDown(208) Then MoveEntity camera,0,0,-4
If KeyDown(30) Then TurnEntity camera,1,0,0
If KeyDown(44) Then TurnEntity camera,-1,0,0
End Function
</textarea><br><br>Oh, and by the way; this is for blitz3d.<br>I know there's alot of shadow stuff out there that's more fancy than this, but it was fun to code. Hope you like.<br><br>Expect the finished library very soon.<br><br><br>Oh, quick add:<br>it was just pointed out that this is a bit "choppy"... If so, try changing the line:<br>Global ssize=128<br>to<br>Global ssize=64<br>(or 32)<br><br>Or keep it at 128 and remove a light source by removing this line:<br>create_light(light2,5000)<br><br>Well, I'll see if I can get the next release faster ;) <br><br></td></tr></table><br>
<a name="1013446"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> nice job, unfortunately it is too chopy for a game. <br><br></td></tr></table><br>
<a name="1013451"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kalisme</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> yeah, I think something isn't getting cleared from memory... should be able to fix it though, plus this demonstration uses two light sources... Alot of blitz3d shadow libraries say not to use more than one... I shouldn't of used more than one, but I figured "meh, I'll give it a go". That and the shadows are 128x128... lower base 2 numbers are a bit smoother. I have have to add a few simple speed up tricks and fix up some of the code, but I think I did ok so far for one nights work. <br><br></td></tr></table><br>
<a name="1013554"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> well that speeds it up a bit.  Still wouldnt work for an FPS... maybe an RTS or RPG good work anyway.  perhaps you should put this in the code archives? <br><br></td></tr></table><br>
<a name="1013585"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kalisme</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I figured I'll fix up the code a bit before I post it...<br>try to speed it up, get the brush texture part working better (so it works better with lightmapped meshes), clean up the code and then I'll post it as an include library in the code archives. <br><br></td></tr></table><br>
<a name="1014755"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jfk EO-11110</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Anyway, thanks for sharing. Just saved locally. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
