<!DOCTYPE html><html lang="en" ><head ><title >AndrewT's Stuff [Image-Heavy]</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >AndrewT's Stuff [Image-Heavy]</h1><a href="forums.php" >Community Forums</a>/<a href="topics.php?forum=61" >Showcase</a>/<a href="#bottom" >AndrewT's Stuff [Image-Heavy]</a><br><br>
<a name="995120"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AndrewT</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is some math-related stuff I've been working on recently that I thought I'd show you guys. <br><br><u>Mandelbrot Explorer</u><br><br><img src="http://img4.imageshack.us/img4/5388/mandelbrot1.png"><br><img src="http://img4.imageshack.us/img4/7233/mandelbrot2.png"><br><br><u>Parametric Systems Plotter</u><br><br>Butterfly Curve:<br><br><img src="http://img4.imageshack.us/img4/9304/butterflyf.png"><br><br>Epicycloid:<br><br><img src="http://img4.imageshack.us/img4/9351/epicycloid.png"><br><br>Lissajous:<br><br><img src="http://img4.imageshack.us/img4/3508/lissajous.png"><br><br><u>DFS Maze Generation</u><br><br><img src="http://img30.imageshack.us/img30/9503/dfsmaze.png"><br><br><u>L-Systems</u><br><br><img src="http://img22.imageshack.us/img22/9020/lsystem2.png"><br><img src="http://img22.imageshack.us/img22/6349/lsystem1.png"><br><img src="http://img196.imageshack.us/img196/3563/lsystem3.png"><br><br><u>Vector Fields</u><br><br><img src="http://img4.imageshack.us/img4/7933/vectorfields2.png"><br><br>Feedback is welcome. If you'd like to see any of the code, feel free to ask. <br><br></td></tr></table><br>
<a name="995126"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_Skully</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> That stuff is awesome<br><br>I love looking at math creations... <br><br></td></tr></table><br>
<a name="995131"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I like the vectorfields, do the dots move in realtime? <br><br></td></tr></table><br>
<a name="995134"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AndrewT</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yea. There are two systems: one based on cells, and one based on functions.<br><br>In cellular system, the field is divided into cells which are assigned angles, and a particle's velocity will change depending on the cell it is currently in. It results in a more organic, random look. That's the system you see in the last shot I posted.<br><br>In the function-based system, the velocity of a particle is determined by a function of its position. It results in a more formulaic appearance. This is a screenshot of the field (x, y) -&gt; (y, -x).<br><br><img src="http://img12.imageshack.us/img12/8817/vectorfields1.png"> <br><br></td></tr></table><br>
<a name="995145"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_Skully</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thats really interesting... I should try something like this using my tilemax engine... each cell already has a vector based force configurable per cell... as well as surface and spacial friction.  Do the particles in this have energy as well or are they just dragged around by the forces?  Cells going off the edges, are they wrapped? <br><br></td></tr></table><br>
<a name="995149"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warpy</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice! <br><br></td></tr></table><br>
<a name="995202"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AndrewT</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Thats really interesting... I should try something like this using my tilemax engine... each cell already has a vector based force configurable per cell... as well as surface and spacial friction. Do the particles in this have energy as well or are they just dragged around by the forces? Cells going off the edges, are they wrapped? <br></div><br><br>The particles are just dragged around by forces in the cellular system--it's a very simple system. If they go off the edge then they do get wrapped around. In the function-based system, however, they are not wrapped when the go over the edge, instead every frame ten random particles are deleted, and ten are added. <br><br></td></tr></table><br>
<a name="996243"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >elcoo</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Very interesting stuff you've got there!<br>May I know how many particles there are on the first "Vector fields" example and how many fps you got? <br><br></td></tr></table><br>
<a name="996345"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> That is really interesting. I would like to see the code for the Parametric System Plotter and everything below except for the maze generator if that's possible. I have the DFS Maze Generator. <br><br></td></tr></table><br>
<a name="997537"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AndrewT</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Very interesting stuff you've got there!<br>May I know how many particles there are on the first "Vector fields" example and how many fps you got? <br></div><br><br>I've since changed the settings so I can't give you an exact number, but in a test I just did I'm getting a solid 60FPS up to about 26000 particles, then it starts decreasing. This is on a Geforce GTS 250, AMD Phenom II X4 820, 6GB RAM.<br><br><div class="quote"> That is really interesting. I would like to see the code for the Parametric System Plotter and everything below except for the maze generator if that's possible. I have the DFS Maze Generator. <br></div><br><br>No problem, I'll try and post it tonight--I'm cleaning up the parametric plotter code and working on an expression evaluator so you can edit the system in real-time. <br><br></td></tr></table><br>
<a name="997859"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DavidDC</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Vector Fields gets my vote if you are posting code :-) <br><br></td></tr></table><br>
<a name="998124"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AndrewT</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> <b>Parametric Plotter</b> - Sorry, no editing in realtime--the expression evaluator was far too slow to perform the tens of thousands of operations required every second. Change the TX and TY expressions on lines 80 and 81 to change the system. I also have several systems commented out that you can look at.<br><br><pre class=code>SuperStrict

Global MinX:Float = -6.0, MinY:Float = -4.0, MaxX:Float = 6.0, MaxY:Float = 4.0

Graphics 1024, 768, 0

Local TX:Float, TY:Float, GX:Float = Float(GraphicsWidth()), GY:Float = Float(GraphicsHeight()), X:Float, Y:Float, OX:Float, OY:Float
Local T:Double, TStart:Float, TEnd:Float, Ticks:Int
Local e:Float = 2.71828183

T = 0.0
TStart = 0.0
TEnd = 2500.0
Ticks = 1000

Local A:Double, AInc:Float

A = 0.0

Local MZS:Float
Local MXS:Float, MYS:Float
Local Animation:Int = 1

Repeat

	SetClsColor(20, 20, 20)
	Cls

	MXS = MouseXSpeed()
	MYS = MouseYSpeed()
	MZS = MouseZSpeed()
	
	If KeyHit(KEY_SPACE)
		Animation = 1 - Animation
	EndIf
	
	If KeyHit(KEY_LSHIFT)
		A = 0.0
	EndIf
	
	If MZS &gt; 0.0
		Local R:Float = MaxX - MinX
		MinX :+ R / 10.0
		MaxX :- R / 10.0
		R = MaxY - MinY
		MinY :+ R / 10.0
		MaxY :- R / 10.0	
	EndIf
	
	If MZS &lt; 0.0
		Local R:Float = MaxX - MinX
		MinX :- R / 10.0
		MaxX :+ R / 10.0
		R = MaxY - MinY
		MinY :- R / 10.0
		MaxY :+ R / 10.0	
	EndIf
	
	If MouseDown(3)
		Local OMinX:Float = MinX, OMinY:Float = MinY
		MinX :- MXS * 0.001 * (MaxX - MinX)
		MinY :+ MYS * 0.001 * (MaxY - MinY)
		MaxX :- MXS * 0.001 * (MaxX - OMinX)
		MaxY :+ MYS * 0.001 * (MaxY - OMinY)
	EndIf

	If Animation
		A :+ 0.01
	EndIf

	For Local I:Int = 1 To Ticks
		
		OX = X
		OY = Y
	
		T = Float(I) / Float(Ticks) * (TEnd - TStart) + TStart
		
		'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
		
		TX = Sin(T) * (e^Cos(T) - 2 * Cos(4 * T) - Sin(T / 12) ^ 5)
		TY = Cos(T) * (e^Cos(T) - 2 * Cos(4 * T) - Sin(T / 12) ^ 5)
		
		'''''''''''''''''''''''''''''''''''''''''
		
		'Local R:Float, K:Float
		
		'R = 1.0
		'K = Sin(A/10.0)
		
		'TX = R * (K + 1.0) * Cos(T) - R * Cos((K + 1.0) * T)
		'TY = R * (K + 1.0) * Sin(T) - R * Sin((K + 1.0) * T)
		
		'''''''''''''''''''''''''''''''''''''''''''''''''''''''''
		
		'Local A2:Float, B:Float, Ang:Float
		
		'A2 = Sin(A / 30.0)
		'B = Cos(A / 30.0)
		'Ang = 90.0
		
		'TX = A2 * Sin(A2 * T + Ang)
		'TY = B * Sin(B * T)
		
		'TX = Expression("cos(t)*cos(a*t)")
		'TY = Sin(T) * Cos(A*T)
		
		'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
		
		X = ((TX - MinX) / (MaxX - MinX)) * (GX - 1)
		Y = GY - ((TY - MinY) / (MaxY - MinY)) * (GY - 1)
		
		If I = 1
			OX = X
			OY = Y
		EndIf
		
		SetColor(Abs(Sin(T * 6.0)) * 255.0, Abs(Cos(T * 4.0)) * 255.0, Abs(Sin(T * 3.4)) * 255.0)
		
		DrawLine(OX, OY, X, Y)
	
	Next
	
	SetColor(255, 255, 255)
	DrawText("Use the mouse wheel to zoom.", 10, 10)
	DrawText("Hold down the middle mouse button and move the mouse to move the curve.", 10, 25)
	DrawText("Press the space key to toggle animation.", 10, 40)
	DrawText("Press the shift key to reset animation.", 10, 55)
	DrawText("A: " + A, 10, 100)
	DrawText("Ticks: " + Ticks, 10, 115)
	DrawText("Start: " + TStart, 10, 130)
	DrawText("End: " + TEnd, 10, 145)

	Flip
	
Until KeyHit(KEY_ESCAPE) Or AppTerminate()

Function Equal:Int(Num:Float, Num2:Float, Margin:Float = 0.005)
	If Num &gt; Num2 - (Margin / 2) And Num &lt; Num2 + (Margin / 2)
		Return 1
	EndIf
	Return 0
EndFunction

Function Pow:Double(Base:Double, Exponent:Double)
	Return Base ^ Exponent
EndFunction

Function TextInput:String(Prompt:String, X:Int, Y:Int)
	Local I:String
	While KeyHit(KEY_ENTER) = 0
		Cls
		Local C:Int = GetChar()
		If C &gt; 0
			If C = 8
				I = I[..I.length - 1]
			Else
				I :+ Chr(c)
			EndIf
		EndIf
		DrawText(Prompt + I, X, Y)
		Flip
	EndWhile
	Return I
EndFunction</pre><br><br><b>L-Systems</b><br><br><pre class=code>SuperStrict

Global DrawLength:Float = 20.0
Global AngleChange:Float = 60.0
Global StartX:Float = 500.0
Global StartY:Float = 500.0
Global StartA:Float = 180.0
Global SavedX:Float[10000]
Global SavedY:Float[10000]
Global SavedAngle:Float[10000]
Global NumSaved:Int = 0

Graphics 1024, 768, 1
SeedRnd(MilliSecs())

Local Scroll:Float
Local MoveX:Float, MoveY:Float
Local Iterations:Int = 1
Local Axiom:String = "A"
Local ProdRule:String = "A[-A-A+A++A]A[+A+A--A-A]A"
Local SystemString:String = CreateLSystemString(Axiom, ProdRule, Iterations)

SetClsColor(0, 0, 30)
SetColor(220, 220, 255)

Repeat

	Cls
	
	DrawText("Scroll Mouse Wheel to change DrawLength", 0, 0)
	DrawText("SHIFT + Scroll Mouse Wheel to change AngleChange", 0, 12)
	DrawText("UP and DOWN keys to increase/decrease number of iterations.", 0, 24)
	DrawText("LEFT and RIGHT keys to rotate L-system.", 0, 36)
	DrawText("CONTROL to change production rule.", 0, 48)
	
	Scroll = MouseZSpeed()
	MoveX = MouseXSpeed()
	MoveY = MouseYSpeed()
	
	If Scroll
		If KeyDown(KEY_LSHIFT)
			AngleChange = AngleChange + Scroll
		Else
			DrawLength = DrawLength + (Scroll * (DrawLength / 20.0))
		EndIf
	EndIf
	
	If MouseDown(3)
		StartX = StartX + MoveX
		StartY = StartY + MoveY
	EndIf
	
	If KeyHit(KEY_UP)
		Iterations :+ 1
		SystemString = CreateLSystemString(Axiom, ProdRule, Iterations)
	EndIf
	If KeyHit(KEY_DOWN)
		Iterations :- 1
		SystemString = CreateLSystemString(Axiom, ProdRule, Iterations)
	EndIf
	
	If KeyDown(KEY_LEFT)
		StartA = StartA - 1
	EndIf
	If KeyDown(KEY_RIGHT)
		StartA = StartA + 1
	EndIf
	
	If KeyHit(KEY_LCONTROL)
		ProdRule = TextInput("A -&gt; ", 10, 10)
		SystemString = CreateLSystemString(Axiom, ProdRule, Iterations)
	EndIf

	LSystem(SystemString)

	Flip

Until KeyHit(KEY_ESCAPE) Or AppTerminate()

Function CreateLSystemString:String(Axiom:String, ProductionRule:String, Iterations:Int)
	
	Local _LSystemString:String = Lower(Axiom)
	ProductionRule = Lower(ProductionRule)

	For Local I:Int = 1 To Iterations
	
		_LSystemString = Replace(_LSystemString, "a", ProductionRule)
		
	Next
	
	Return _LSystemString
	
EndFunction

Function LSystem(LS:String)
	
	
	Local X:Float, Y:Float, A:Float
	
	X = StartX
	Y = StartY
	A = StartA
	
	Local OX:Float, OY:Float
	
	OX = X
	OY = Y
	
	For Local I:Int = 1 To Len(LS)
	
		Local Char:String = Mid(LS, I, 1)
		
		If Char = "a"
			X = X + Sin(A) * DrawLength
			Y = Y + Cos(A) * DrawLength
			DrawLine(OX, OY, X, Y)
			OX = X
			OY = Y
		EndIf
		
		If Char = "+"
			A = A + AngleChange
		EndIf
		
		If Char = "-"
			A = A - AngleChange
		EndIf
		
		If Char = "["
			SavedX[NumSaved] = X
			SavedY[NumSaved] = Y
			SavedAngle[NumSaved] = A
			NumSaved :+ 1
		EndIf
		
		If Char = "]"
			X = SavedX[NumSaved - 1]
			Y = SavedY[NumSaved - 1]
			A = SavedAngle[NumSaved - 1]
			OX = X
			OY = Y
			NumSaved :- 1
		EndIf
		
	Next
	
EndFunction

Function TextInput:String(Prompt:String, X:Int, Y:Int)
	Local I:String
	While KeyHit(KEY_ENTER) = 0
		Cls
		Local C:Int = GetChar()
		If C &gt; 0
			If C = 8
				I = I[..I.length - 1]
			Else
				I :+ Chr(c)
			EndIf
		EndIf
		DrawText(Prompt + I, X, Y)
		Flip
	EndWhile
	Return I
EndFunction</pre><br><br><b>Cellular Vector Fields</b><br><br><pre class=code>SuperStrict

Global Cells:Float[64, 64]

For Local X:Int = 0 To 63
	For Local Y:Int = 0 To 63
		Cells[X, Y] = Rnd() * 360.0
	Next
Next

Function DrawField()
	SetScale(2.0, 2.0)
	For Local X:Int = 0 To 63
		For Local Y:Int = 0 To 63
			Local TX:Float = (Float(X) / 63.0) * Float(GraphicsWidth()) + Float(GraphicsWidth()) / 128.0
			Local TY:Float = (Float(Y) / 63.0) * Float(GraphicsHeight()) + Float(GraphicsHeight()) / 128.0
			SetColor(100, 100, 100)
			DrawLine(TX + Cos(Cells[X, Y]) * 4, TY + Sin(Cells[X, Y]) * 4, TX, TY)
		Next
	Next
EndFunction

Function SetCell(X:Int, Y:Int, Ang:Float)
	If X &gt; 63
		X = X Mod 63
	EndIf
	If Y &gt; 63
		Y = Y Mod 63
	EndIf
	If X &lt; 0
		X = 64 + X
	EndIf
	If Y &lt; 0
		Y = 64 + Y
	EndIf
	Cells[X, Y] = Ang
EndFunction

Type Particle

	Field X:Float
	Field Y:Float
	
	Field Ang:Float
	
	Global List:TList
	Global TurnSpeed:Float = 3.0
	Global Velocity:Float = 1.2
	
	Function Init()
		List = CreateList()
	EndFunction
	
	Function AddParticles(Num:Int)
		For Local I:Int = 1 To Num
			Local P:Particle = New Particle
			P.X = Rand(0, GraphicsWidth() - 1)
			P.Y = Rand(0, GraphicsHeight() - 1)
			List.AddLast(P)
		Next
	EndFunction
	
	Function RemoveParticles(Num:Int)
		For Local I:Int = 1 To Num
			List.RemoveLast()
		Next
	EndFunction
	
	Function UpdateAll()
		Local TX:Float, TY:Float
		For Local P:Particle = EachIn List
			
			If P.X &gt; GraphicsWidth()
				P.X = 0
			EndIf
			If P.Y &gt; GraphicsHeight()
				P.Y = 0
			EndIf
			If P.X &lt; 0
				P.X = GraphicsWidth()
			EndIf
			If P.Y &lt; 0
				P.Y = GraphicsHeight()
			EndIf
			
			Local Angle:Float = Cells[Int((P.X / Float(GraphicsWidth())) * 63.0), Int((P.Y / Float(GraphicsHeight())) * 63.0)]
			
			If Angle &gt; P.Ang + TurnSpeed
				P.Ang :+ TurnSpeed
			EndIf
			If Angle &lt; P.Ang - TurnSpeed
				P.Ang :- TurnSpeed
			EndIf
			
			If P.Ang &gt; 360.0
				P.Ang = P.Ang Mod 360.0
			EndIf
	
			P.X :+ Cos(P.Ang) * Velocity
			P.Y :+ Sin(P.Ang) * Velocity

			SetColor(255, 255, 255)
			Plot(P.X, P.Y)
		
		Next
	EndFunction
	
EndType

Graphics 640, 480, 1

Particle.Init()
Particle.AddParticles(2000)

Local MZS:Float
Local MXS:Float, MYS:Float

Local Arrows:Int = 0

Repeat

	Cls
	
	SetColor(255, 255, 255)
	SetScale(1.0, 1.0)
	DrawText("Press the space key to toggle the indicators.", 10, 10)
	DrawText("Scroll the mouse wheel to add/remove particles.", 10, 25)
	DrawText("Hold the left mouse button and drag the mouse to change the angle values.", 10, 40)
	
	If KeyHit(KEY_SPACE)
		Arrows = 1 - Arrows
	EndIf
	
	MXS = MouseXSpeed()
	MYS = MouseYSpeed()
	
	MZS = MouseZSpeed()
	If MZS &gt; 0
		Particle.AddParticles(MZS * 200)
	Else
		If MZS &lt; 0
			Particle.RemoveParticles(-MZS * 200)
		EndIf
	EndIf
	
	If MouseDown(1)
		If MXS And MYS
			Local Ang:Float = ATan2(Float(MYS), Float(MXS))
			Local CX:Int = Int((Float(MouseX()) / Float(GraphicsWidth())) * 63.0)
			Local CY:Int = Int((Float(MouseY()) / Float(GraphicsHeight())) * 63.0)
			SetCell(CX, CY, Ang)
			SetCell(CX - 1, CY, Ang)
			SetCell(CX + 1, CY, Ang)
			SetCell(CX, CY - 1, Ang)
			SetCell(CX, CY + 1, Ang)
			SetCell(CX - 1, CY - 1, Ang)
			SetCell(CX + 1, CY - 1, Ang)
			SetCell(CX - 1, CY + 1, Ang)
			SetCell(CX + 1, CY + 1, Ang)
		EndIf
	EndIf

	If Arrows
		DrawField()
	EndIf
	
	Particle.UpdateAll()

	Flip
	
Until KeyHit(KEY_ESCAPE) Or AppTerminate()</pre><br><br><b>Function-Based Vector Fields</b><br><br><pre class=code>SuperStrict

Global MinX:Float = -1.0, MinY:Float = 1.0, MaxX:Float = 1.0, MaxY:Float = -1.0

Type Particle

	Field X:Float
	Field Y:Float
	
	Field VX:Float
	Field VY:Float
	
	Global List:TList
	
	Function Init()
		List = CreateList()
	EndFunction
	
	Function AddParticles(Num:Int)
		For Local I:Int = 1 To Num
			Local P:Particle = New Particle
			P.X = Rand(0, GraphicsWidth() - 1)
			P.Y = Rand(0, GraphicsHeight() - 1)
			List.AddLast(P)
		Next
	EndFunction
	
	Function RemoveParticles(Num:Int)
		For Local I:Int = 1 To Num
			List.RemoveLast()
		Next
	EndFunction
	
	Function RemoveFirstParticles(Num:Int)
		For Local I:Int = 1 To Num
			List.RemoveFirst()
		Next
	EndFunction
	
	Function RemoveRandomParticles(Num:Int)
		For Local I:Int = 1 To Num
			List.Remove(List.ValueAtIndex(Rand(0, List.Count() - 1)))
		Next
	EndFunction
	
	Function UpdateAll()
		RemoveRandomParticles(10)
		AddParticles(10)
		Local TX:Float, TY:Float, R:Float
		For Local P:Particle = EachIn List
			
				If P.X &gt; GraphicsWidth()
					P.X = 1
				EndIf
				If P.Y &gt; GraphicsHeight()
					P.Y = 1
				EndIf
				If P.X &lt; 0
					P.X = GraphicsWidth()
				EndIf
				If P.Y &lt; 0
					P.Y = GraphicsHeight()
				EndIf
				
				TX = (P.X - (GraphicsWidth() / 2))
				TX = TX / (Float(GraphicsWidth()) / 2)
				TX = TX * (MaxX - MinX)
				TY = (P.Y - (GraphicsHeight() / 2))
				TY = TY / (Float(GraphicsHeight()) / 2)
				TY = TY * (MaxY - MinY)

				R = Sqr(TX^2 + TY^2)
			
				''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
				'EDIT THIS PART TO CHANGE THE APPEARANCE OF THE FIELD
				'TX = the particle's X position from -1.0 to 1.0
				'TY = the particle's Y position from -1.0 to 1.0
				''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
				
				P.VX = TY
				P.VY = -TX
				
				'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
	
				P.X :+ P.VX
				P.Y :+ P.VY
				
				Local Ang:Float = ATan2(P.VY, P.VX)
				Local Vel:Float = Sqr(P.VX * P.VX + P.VY * P.VY)
				DrawLine(P.X + Cos(Ang) * Vel * 4, P.Y + Sin(Ang) * Vel * 4, P.X, P.Y)
				
				'Plot(P.X, P.Y)
		Next
	EndFunction
	
EndType

Graphics 1024, 768, 32

Particle.Init()

Particle.AddParticles(5000)

Local MZS:Float

Repeat

	Cls
	
	MZS = MouseZSpeed()
	
	If MZS
		MinX :+ Float(MZS) / 10.0
		MinY :- Float(MZS) / 10.0
		MaxX :- Float(MZS) / 10.0
		MaxY :+ Float(MZS) / 10.0
	EndIf

	Particle.UpdateAll()

	Flip
	
Until KeyHit(KEY_ESCAPE) Or AppTerminate()</pre> <br><br></td></tr></table><br>
<a name="998133"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Retimer</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Beautiful stuff! <br><br></td></tr></table><br>
<a name="998167"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is all really fascinating to me, Andrew!<br><br>I do have a coupla little questions though:<br><br>Mathematically, can the vector fields be considered continuous? Does that work with the code or is it only possible to constrain them to the individual pixels/ var coords?<br><br>and, how are you dealing with i or j for SQR(0-1) with respect to your fractal stuff?<br>I was looking into something kinda related a while back, and after trawling the net for methods of working with complex no's and i or j even what I could find was all in other languages which I couldn't easily convert...<br><br>If it's a 'trade secret' for you, no probs :) <br><br></td></tr></table><br>
<a name="998183"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Thareh</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Good Stuff!<br>Look at the last one (Function-Based Vector Fields) with a litte higher speed for a while, and your vision will be messed up for a while :D <br><br></td></tr></table><br>
<a name="998233"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AndrewT</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Mathematically, can the vector fields be considered continuous? Does that work with the code or is it only possible to constrain them to the individual pixels/ var coords? <br></div><br><br>I'm not quite sure what you mean--can you clarify a bit? Keep in mind that my knowledge of math is currently limited to Honors Pre-Calc :P<br><br><div class="quote"> and, how are you dealing with i or j for SQR(0-1) with respect to your fractal stuff?<br>I was looking into something kinda related a while back, and after trawling the net for methods of working with complex no's and i or j even what I could find was all in other languages which I couldn't easily convert... <br></div><br><br>I came up with the following complex number UDT:<br><br><pre class=code>Type Complex

	Field A:Float 'real part
	Field B:Float 'imaginary part coefficient
	
	Method Multiply(C:Complex)
	
		Local Copy:Complex = New Complex
		Copy.A = C.A
		Copy.B = C.B
		
		Local TempA:Float = A
	
		A = (A * Copy.A) + (-1 * B * Copy.B)
		B = (TempA * Copy.B) + (B * Copy.A)
		
		'(a + bi)(c + di) = ac + i(ad + bc) + (-1)bd
		
	EndMethod
	
	Method Add(C:Complex)
		
		Local Copy:Complex = New Complex
		Copy.A = C.A
		Copy.B = C.B
		
		A = A + C.A
		B = B + C.B
		
		'(a + bi) + (c + di) = (a + c) + (b + d)i
		
	EndMethod
	
	Method Absolute:Float()
		
		Return Sqr(A * A + B * B)
		
	EndMethod
	
	Method AbsoluteSq:Float()
		
		Return A * A + B * B
		
	EndMethod
EndType</pre><br><br>Because the concept of i in this case is purely abstract, there's no need to explicitly define its value. The only operation which will actually take its value into account is multiplication when we have to square it, and we know that i * i is -1, so that doesn't present a problem either. Hope that helps, and if you have any other questions I'll do my best to answer them.<br><br>I'm going to try and clean up these demos a bit by adding comments and implementing some more features so that they can be posted on my website. If you have any requests for demos feel free to ask and I'll see what I can do. :) <br><br></td></tr></table><br>
<a name="998266"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> this is really nice. thanks for posting the code. <br>I can spend hours messing with this.<br>Hopefully I can do some creative stuff with it.<br><br>Thanks a lot.<br>If you care here is some code for a sphere with nice effects. It may be down your alley and it's quite interesting to play with.<br><br><pre class=code>

Strict
Global dots_in_ball# = 4440.0								' How many dots are in the ball?

' Globals For the 3d.

Type Tnode
	Field threeDx#
	Field threeDy#
	Field threeDz#
	Field color
	Global twoDx1#
	Global twoDy1#
	Global twoDx2#
	Global twoDy2#
	
End Type

Global numpoints# = dots_in_ball								' Number of points in the point table.
Global distance#  = 400.0										' Needed For perspective.
Global vx#													' X location.
Global vy#													' Y location.
Global vz#													' Z location.
Global multiplier!=150.0
' Arrays used by 3d code.
Global points#[numpoints, 3]									' Holds the point locations of the game over 3d.

Graphics 640,480,32

initdots()

While Not KeyDown(KEY_ESCAPE)
	threed()												' Call the threed thing.
	SetColor 255,255,255
	DrawText "Depth Colored Real 3D Dot Ball",0,0
	DrawText "origina Coding by Tracer For the Blitz CD.",0,15
	DrawText "modified by me",0,30 
	DrawText "Use up/down arrow Keys To adjust multiplier",0,45
	DrawText "multiplier = "+multiplier,0,60
	Local dummy = MouseX()										' Removes the mouse from fullscreen.
	If KeyDown(KEY_DOWN) Then 
		multiplier =multiplier-0.00005
		initdots()
	EndIf
	If KeyDown(KEY_UP) Then
		multiplier=multiplier+0.00005
		initdots() 
	EndIf
	
	Flip(0)													' Flip the screen.
	Cls														' Clear the screen.
Wend
Function initdots()
For Local t= 0 To dots_in_ball-1
	Local xd# = ((t*multiplier) Mod 180.0)-90.0    'Rnd(-90,90)
	points[t,0] = (Cos(xd) * 10.0) * (Cos(t) * 10.0)
	points[t,1] = (Cos(xd) * 10.0) * (Sin(t) * 10.0)
	points[t,2] = Sin(xd) * 100.0
Next

End Function 

Function threed()
	vx# = vx# + 0.5											' X rotation speed of ball.
	vy# = vy# + 0.5											' Y rotation speed of ball.
	vz# = vz# + 0.5											' Z rotation speed of ball.

	For Local n = 0 To numpoints-1
		Local x3d = points[n, 0]
		Local y3d = points[n, 1]
		Local z3d = points[n, 2]
       
		Local ty# = ((y3d * Cos(vx#)) - (z3d * Sin(vx#)))
		Local tz# = ((y3d * Sin(vx#)) + (z3d * Cos(vx#)))
		Local tx# = ((x3d * Cos(vy#)) - (tz# * Sin(vy#)))
		tz# = ((x3d * Sin(vy#)) + (tz# * Cos(vy#)))
		Local ox# = tx#
		tx# = ((tx# * Cos(vz#)) - (ty# * Sin(vz#)))
		ty# = ((ox# * Sin(vz#)) + (ty# * Cos(vz#)))
		Local nx  = Int(512 * (tx#) / (distance - (tz#))) + 320
		Local ny  = Int(240 - (512 * ty#) / (distance - (tz#)))
		SetColor 0,tz+100,(tz#+100)
		Plot nx,ny'
	Next
End Function

' This Function looks at the z-value of the pixel
' And sets the color accoordingly.
Function Color(t#)
	SetColor 0,0,(t+200.0)',(t+200.0),(t+200.0)
End Function
</pre> <br><br></td></tr></table><br>
<a name="1002255"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AndrewT</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sorry I haven't posted anything in awhile, I've been busy.  That's an awesome demo jesse, thanks!<br><br>I've improved upon the fractal explorer a bit, I'll post the source sometime in the next week.<br><br>Burning Ship (zoomed in):<br><br><img src="http://img709.imageshack.us/img709/8741/burningship3.png"><br><br>Mandelbrot (zoomed in):<br><br><img src="http://img69.imageshack.us/img69/1676/mandelbrot3.png"> <br><br></td></tr></table><br>
<a name="1002338"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AndrewT</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sorry about the double post, but I've been working on some more fractals, this time using Iterated Function Systems (IFS). Here are a couple randomly generated fractals I've made thus far:<br><br><img src="http://img695.imageshack.us/img695/6236/ifs36.png"><br><img src="http://img192.imageshack.us/img192/1016/ifs106.png"><br><img src="http://img69.imageshack.us/img69/3928/ifs116.png"> <br><br></td></tr></table><br>
<a name="1002536"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ryan Burnside</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> Some really cool looking stuff there. :) <br><br></td></tr></table><br>
<a name="1002537"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yahfree</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> Woah, if you stare in the center of the Function-Based Vector Fields example, then look around, everything moves... it's like a trippy optical illusion <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
