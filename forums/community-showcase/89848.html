<!DOCTYPE html><html lang="en" ><head ><title >Ants! - Quick Worms Clone ;)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Ants! - Quick Worms Clone ;)</h1><a href="forums.php" >Community Forums</a>/<a href="topics.php?forum=61" >Showcase</a>/<a href="#bottom" >Ants! - Quick Worms Clone ;)</a><br><br>
<a name="1021110"></a>

<a name="1147591"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Using Matt's 2d deformable terrain code:<br><br><a href="http://www.blitzmonkeys.com/index.php?topic=426.0" target="_blank">http://www.blitzmonkeys.com/index.php?topic=426.0</a><br><br>I've cooked this up in a few hours: ANTS!<br><br>Video of Ants:<br><iframe width="560" height="345" src="http://www.youtube.com/embed/cxrJ8rrlvqQ" frameborder="0" allowfullscreen></iframe><br><br><img src="http://sites.google.com/site/therevillsgames/blitz/ant.png"><br><br>Exe, Blitz Max Source and Graphics:<br><br><a href="http://sites.google.com/site/therevillsgames/blitz/Ants.zip" target="_blank">http://sites.google.com/site/therevillsgames/blitz/Ants.zip</a><br><br>Aim using the mouse, left clicks fires, cursor keys to move - one short then its the next player's turn...<br><br>Feel free to update and modify the code :D<br><br>Ants.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict 
AppTitle = "ANTS!"
Const SCREENWIDTH% = 800, SCREENHEIGHT% = 600
Const GRAVITY# = 0.3

Include "game.bmx"
Include "terrainLines.bmx"
Include "sprite.bmx"

SeedRnd MilliSecs()
HideMouse()

global game:TGame = New TGame

game.setUp()
game.mainLoop()

End
</textarea><br><br>game.bmx ** UPDATED 11/04/10 **<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Type TGame
	Field backgroundImage:TImage
	Field foregroundImage:TImage
	Field antImage:TImage
	Field explodeImage:TImage
	Field bulletImage:TImage
	Field titleScreenImage:TImage
	Field controlImage:TImage

	Field hs1:hshape ' main terrain
	Field hs2:hshape ' eraser
	
	Field playerInControlIndex% = 0
	Field maxPlayers% = 1 ' zero based
	
	Method setup()
		Graphics SCREENWIDTH, SCREENHEIGHT
		loadGraphics()
		createTerrain()
		TPlayer.CreatePlayer(Rnd(20,100),0,antImage, 0)
		TPlayer.CreatePlayer(Rnd(700,SCREENWIDTH-20),0,antImage, 1)
	End Method
	
	Method loadGraphics()
		Self.backgroundimage = LoadImage("gfx\sky.png")
		Self.foregroundImage = LoadImage("gfx\ground.png")
		Self.titlescreenimage = LoadImage("gfx\Ants.png")
		AutoMidHandle True
		Self.antimage = LoadImage("gfx\ant.png")
		Self.explodeimage = LoadImage("gfx\explode.png")
		Self.bulletimage = LoadImage("gfx\bullet.png")
		Self.controlImage = LoadImage("gfx\control.png")
	End Method
	
	Method createTerrain()
		DrawImage titleScreenImage, 800/2 - titleScreenImage.width/2, 50
		DrawText "Please Wait...",0,280
		Flip
		hs1 = generateShape("gfx\ground.png")
		hs2 = generateShape("gfx\bullet.png")
	End Method
	
	Method generateShape:hshape(imagePath$)
		Local shape:hshape = hshape.Create(imagePath)
		Local dot:hshape = hshape.Create()
		dot.AddLine( 0, 1, 0 )	
		Local tmp:TPixmap=LoadPixmap(imagePath)
		Local argb:Int
		Local a:Int	
		
		For Local t:Int = 0 To tmp.height - 1
			shape.AddLine( 0, tmp.width - 1, t )
		Next
		
		For Local x% = 0 To tmp.width - 1
			For Local y% = 0 To tmp.height - 1
				argb = ReadPixel(tmp,x,y)
				a = (argb Shr 24) &amp; $ff
				If a = 0 Then
					dot.Position(x, y )		
					shape .Subtract( dot)
				End If;
			Next
		Next
		Return shape	
	End Method

	Method mainLoop()
		While (Not AppTerminate() And Not KeyHit( KEY_ESCAPE ))
			draw()
		Wend
	End Method
	
	Method draw()
		SetBlend SOLIDBLEND
		DrawImage Self.backgroundimage, 0, 0
		SetBlend ALPHABLEND
		hs1.draw()
		
		For Local s:TSprite = EachIn TSprite.spriteList
			s.Update(hs1)
			s.Draw()
		Next

		drawMouse()
		Flip
	End Method
	
End Type

Function drawMouse()
	DrawLine MouseX()-5,MouseY(), MouseX()+5,MouseY()
	DrawLine MouseX(),MouseY()-5, MouseX(),MouseY()+5
End Function

Function RectsOverlap%(x0#, y0#, w0#, h0#, x2#, y2#, w2#, h2#)
	If x0 &gt; (x2 + w2) Or (x0 + w0) &lt; x2 Then Return False
	If y0 &gt; (y2 + h2) Or (y0 + h0) &lt; y2 Then Return False
	Return True
End Function
</textarea><br><br>sprite.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Type TSprite
	Field x#, y#
	Field dx#, dy#
	Field image:TImage
	Field direction:Int
	Field scaleX# = 1, scaleY# = 1
	Field jumping%, canjump%
	Field w%, h%
	Field w2%, h2%
	Global spriteList:TList = New TList
		
	Function Create:TSprite(x#,y#,image:TImage)
		Local s:TSprite = New TSprite
		spriteList.AddLast s
		s.x = x
		s.y = y
		s.image = image
		s.dx = 2
		s.direction = 1
		s.jumping = 0
		s.imageDetails()
		Return s
	End Function
	
	Method SetScaleXY(sx#,sy#)
		scaleX = sx
		scaleY = sy
	End Method
	
	Method imageDetails()
		w = image.width
		h = image.height
		w2 = w/2
		h2 = h/2
	End Method
	
	Method draw()
		SetScale (scaleX, scaleY)
		DrawImage image, x, y
		SetScale (1,1)		
	End Method 
	
	Method update( terrain:hshape )
		If ( terrain.CollidePixel( x, y - dy - h2 - 0.5 ) ) And dy &lt; 0
			dy = 0
		End If
	
		If ( Not terrain.CollidePixel( x, y + dy + h2 + 0.5 ) )
			dy :+ GRAVITY
			If ( dy &gt; 3.98 ) dy = 3.98
		Else
			dy = 0
			jumping = 0
			canJump = 1			
		End If 
		y :+ dy 
		boundCheck()
	End Method
	
	Method boundCheck()
		If ( y &gt; 600 ) y = 0
		If x &lt; 0 x = 800
		If x &gt; 800 x = 0	
	End Method
	
	Method kill()
		spriteList.remove(Self)
	End Method	
	
End Type

Type TPlayer Extends TSprite
	Field index%
	Field health#
	
	Function CreatePlayer:TPlayer (x#,y#,image:TImage,index%)
		Local s:TPlayer = New TPlayer 
		spriteList.AddLast s
		s.x = x
		s.y = y
		s.image = image
		s.dx = 2
		s.direction = 1
		s.jumping = 0
		s.imageDetails()
		s.index = index
		s.health = 100
		Return s
	End Function
	
	Method update(terrain:hshape)
		Super.update(terrain)
		
		If game.playerInControlIndex = index Then
		
			If KeyDown(KEY_RIGHT) Or KeyDown(KEY_D)
				If ( Not terrain.CollidePixel( x + dx + w2, y + h2 ) ) And ( Not terrain.CollidePixel( x + dx + w2, y ) )
					x:+dx
					direction = 1
				Else
					If ( Not terrain.CollidePixel( x + dx + w2, y - h2 ) ) 
						x:+dx
						direction = 1
						y:-1
					EndIf
				EndIf
			EndIf
	
			If KeyDown(KEY_LEFT) Or KeyDown(KEY_A)
				If ( Not terrain.CollidePixel( x - dx - w2, y + h2 ) ) And ( Not terrain.CollidePixel( x - dx - w2, y ) )
					x:-dx
					direction = -1
				Else
					If ( Not terrain.CollidePixel( x - dx - w2, y - h2 ) ) 
						x:-dx
						direction = -1
						y:-1
					EndIf
				EndIf
			EndIf
			
			If (KeyDown(KEY_UP) Or KeyDown(KEY_W)) And jumping = 0 And canJump
				dy = -8
				jumping = 1
				canJump = 0
			EndIf
			
			If MouseHit(1)
				Local time# = 30
				Local dx# = (MouseX() - x) / time
				Local dy# = 0-(((0.5*GRAVITY*(time*time))+y-MouseY())/time)
	
				TBullet.CreateBullet(x, y, dx, dy, game.bulletImage, Self)
				game.playerInControlIndex:+1
				If game.playerInControlIndex &gt; game.maxPlayers
					game.playerInControlIndex = 0
				EndIf
			EndIf
			
			SetScaleXY(direction, 1)
		EndIf
		If health&lt;=0 kill()
	End Method
	
	Method draw()
		Super.draw()
		If game.playerincontrolindex = index DrawImage game.controlImage , x, y - h * 2
		
		SetColor 255,0,0
		DrawRect x - w2,y - h ,health/3,10			
		SetColor 255,255,255		
	End Method
	
End Type

Type TExpode Extends TSprite
	Field timeAlive#
	
	Function CreateExplode:TExpode (x#,y#,image:TImage)
		Local s:TExpode = New TExpode 
		spriteList.AddLast s
		s.x = x
		s.y = y
		s.image = image
		s.imageDetails()
		s.direction = 1
		s.timeAlive = 5
		Return s
	End Function
	
	Method Update(terrain:hshape)
		If timeAlive &gt; 0
			timeAlive:-1
		Else
			kill()
		EndIf
	End Method
End Type

Type TBullet Extends TSprite
	Field owner:TPlayer
	Function CreateBullet:TBullet (x#,y#,dx#,dy#,image:TImage, owner:TPlayer)
		Local s:TBullet = New TBullet 
		s.x = x
		s.y = y
		s.dx = dx
		s.dy = dy
		s.image = image
		s.imageDetails()
		s.direction = 1
		s.owner = owner
		spriteList.addlast s
		Return s
	End Function
		
	Method update(terrain:hshape)
		For Local s:TPlayer=EachIn TSprite.spriteList
			If s &lt;&gt; owner
				If rectsOverlap(s.x-s.w2, s.y-s.h2, s.h ,s.w, x-w2, y-h2, h, w)
					TExpode.CreateExplode( x + dx , y + dy , game.explodeImage)
					kill()
					s.health :-10
				End If
			EndIf
		Next
	
	
		If ( Not terrain.CollidePixel( x + dx, y + dy  ) )
			x:+dx
			y:+dy
			dy:+GRAVITY	
		Else
			game.hs2.Position( x + dx - w2, y + dy - h2 ) 
			terrain.Subtract( game.hs2 )
			TExpode.CreateExplode( x + dx , y + dy , game.explodeImage) 
			kill()
		EndIf
		If x &lt; 0 x = 800
		If x &gt; 800 x = 0
		If y &gt; 600 kill()
	End Method
	
End Type
</textarea><br><br>terrainLines.bmx  ** UPDATED 9/05/10 **<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' Horizontal line type
Type hline
	Field _start:Int
	Field _end:Int
	Field _y:Int
	Field _offsetX:Int
	Field _offsetY:Int
	
	Method DrawTextured( img:TImage )
		DrawSubImageRect(img, _start, _y, _end - _start + 1, 1, _start - _offsetX, _y - _offsetY, _end - _start + 1, 1)
	End Method

	Method draw()
		DrawLine( _start, _y, _end, _y )
	End Method 

	
	Function Create:hline ( s:Int, e:Int, y:Int )
		Local hl:hline = New hline
		hl._start = s
		hl._end = e
		hl._y = y
		Return hl
	End Function 
	
	' Subtract another hline from this one, if this line is
	' deleted completely this function returns true.
	'
	' If this line is split, hlDest contains the newly created
	' line.
	Method Split:Int ( hl:hline, hlDest:hline Var )
		If ( hl._y = _y )
			If ( _start &lt;= hl._start )
				If ( _end &gt;= hl._end )
					If ( hl._end + 1 &lt; _end )
						hlDest = hline.Create( hl._end + 1, _end, _y )
					End If 
					_end = hl._start - 1
					Return ( _end &lt;= _start )
				Else If ( _end &gt;= hl._start )
					_end = hl._start - 1
					Return ( _end &lt;= _start )
				End If 
			Else If ( _start &lt;= hl._end )
				If ( _end &lt;= hl._end )
					Return True
				Else 
					_start = hl._end + 1
					Return ( _end &lt;= _start )
				End If 
			End If 
		End If 
		Return False 
	End Method

	
	Method Move( mx:Int, my:Int )
		_start :+ mx
		_end :+ mx
		_y :+ my
	End Method
	
	Method Collide:Int ( x:Int, y:Int )
		If ( _y = y ) Return ( x &gt;= _start And x &lt;= _end )
		Return False 
	End Method 
	
End Type 

' A shape created from the horizontal lines..
Type hshape
	Field img:TImage
	Field lines:TList
	Field x:Int, y:Int
	
	Function Create:hshape ( filename:String = "" )
		Local hs:hshape = New hshape
		hs.lines = New TList
		If ( Len( filename ) ) hs.img = LoadImage( filename )
		Return hs
	End Function 
	
	Method Draw()
		If ( img )
			For Local hl:hline = EachIn lines
				hl.DrawTextured( img )
			Next 
		Else 
			For Local hl:hline = EachIn lines
				hl.Draw()
			Next 
		End If 
	End Method 
	
	Method AddLine( s:Float, e:Float, y:Float )
		lines.AddLast( hline.Create( s, e, y ) )
		SortList( lines, True, hshape.SortLinesByHeight )
	End Method 
	
	Function SortLinesByHeight:Int ( h0:Object, h1:Object )
		Return hline(h1)._y &lt; hline(h0)._y
	End Function 
	
	Method Position( _x:Int, _y:Int )
		Local xmove:Int = _x - x
		Local ymove:Int = _y - y
		x :+ xmove
		y :+ ymove
		If ( xmove Or ymove )
			For Local hlThis:hline = EachIn lines
				hlThis.Move(xmove, ymove)
				hlThis._offsetX = _x
				hlThis._offsetY = _y
			Next
		End If 
	End Method 
	
	Method Subtract( hs:hshape )
		For Local hlOther:hline = EachIn hs.lines
			For Local hlThis:hline = EachIn lines
				Local hlNew:hline
				If ( hlOther._y &lt; hlThis._y ) Exit 
				If ( hlThis.Split( hlOther, hlNew ) )
					lines.Remove( hlThis )
				End If 
				If ( hlNew ) 
					lines.AddLast( hlNew )
					SortList( lines, True, hshape.SortLinesByHeight )
				End If 
			Next
		Next 
	End Method 
	
	Method CollidePixel:Int ( x:Int, y:Int )
		For Local hlThis:hline = EachIn lines
			If ( hlThis._y &gt; y ) 
				Return False 
			Else If ( hlThis._y = y And hlThis.Collide( x, y ) )
				Return True 
			End If 
		Next 
		Return False 
	End Method 
	
End Type 
</textarea><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1021116"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_Skully</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Not sure why but this is CRAWLING on my laptop.  Mind you I think my video card hates DX... OpenGL is an order of magnitude faster and yes, I have updated them and my video drivers :) <br><br></td></tr></table><br>
<a name="1021140"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GfK</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Not sure why but this is CRAWLING on my laptop <br></div>They're meant to crawl - they're ants. <br><br></td></tr></table><br>
<a name="1021150"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> How does it run for everyone else? Whats your specs Skully? <br><br></td></tr></table><br>
<a name="1021168"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> RUns fine here, I'd say it's a great inspiration! I used to absolutely love worms! We had a giant blast with it...replaced the samples on the Amiga back in the day...laughing till tears ran! :D <br><br></td></tr></table><br>
<a name="1021171"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warpy</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Runs fine on my little laptop. Why is it sort of real-time? <br><br></td></tr></table><br>
<a name="1021225"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pete Carter</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> runs ok on my macbook, not sure why the holes made by the bullets are not round? but apart from that good bit of code. <br><br></td></tr></table><br>
<a name="1021229"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice <br><br></td></tr></table><br>
<a name="1021275"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Have a read here on how it works:<br><br><a href="http://www.matibee.co.uk/wpsite/?p=39" target="_blank">http://www.matibee.co.uk/wpsite/?p=39</a><br><br>@Pete, the holes are not round because I quickly created the shape (remember its all based on horizontal lines ;)) :<br><br><pre class=code>
		hs2 = hshape.Create()
		For Local t:Int = 0 To 10
			hs2.AddLine( 10 - t, 20+t, t )
		Next
		For Local t:Int = 11 To 15
			hs2.AddLine( 0, 30, t )
		Next 
		Local i% = 0
		For Local t:Int = 16 To 25
			i:+1
			hs2.AddLine( i, 30-i, t )
		Next
</pre><br><br>I guess I could load in the image and do the same to the bullet as what I am doing with the terrian to get it better :) <br><br></td></tr></table><br>
<a name="1021308"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ive added a new method to game.bmx:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
	Method generateShape:hshape(imagePath$)
		Local shape:hshape = hshape.Create(imagePath)
		Local dot:hshape = hshape.Create()
		dot.AddLine( 0, 1, 0 )	
		Local tmp:TPixmap=LoadPixmap(imagePath)
		Local argb:Int
		Local a:Int	
		
		For Local t:Int = 0 To tmp.height - 1
			shape.AddLine( 0, tmp.width - 1, t )
		Next
		
		For Local x% = 0 To tmp.width - 1
			For Local y% = 0 To tmp.height - 1
				argb = ReadPixel(tmp,x,y)
				a = (argb Shr 24) &amp; $ff
				If a = 0 Then
					dot.Position(x, y )		
					shape .Subtract( dot)
				End If;
			Next
		Next
		Return shape	
	End Method
</textarea><br><br>The holes now should be round :) Ive updated the zip file with this change.<br><br>Video of Ants:<br><a href="http://www.youtube.com/watch?v=cxrJ8rrlvqQ" target="_blank">http://www.youtube.com/watch?v=cxrJ8rrlvqQ</a> <br><br></td></tr></table><br>
<a name="1021399"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pete Carter</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> nice update, looks much better! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
