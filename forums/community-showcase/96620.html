<!DOCTYPE html><html lang="en" ><head ><title >Snowflakes</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Snowflakes</h1><a href="forums.php" >Community Forums</a>/<a href="topics.php?forum=61" >Showcase</a>/<a href="#bottom" >Snowflakes</a><br><br>
<a name="1118693"></a>

<a name="1118694"></a>

<a name="1118695"></a>

<a name="1118713"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Again, a useless but very nice piece of code for the holidays, let it snow... The image is an old wallpaper from <a href="http://www.bunnybua.com" target="_blank">www.bunnybua.com</a>, a little bit redesigned by me for this demo. The MP3 Track can be freely downloaded <a href="http://www.jamendo.com/en/track/442222" target="_blank">from Jamendo here</a> (or use your own track). The code uses a background image and a matching matte in greyscale for the snow.<br><br>Merry christmas to you all!<br><br><img src="http://www.christianhart.de/bb/snow/snow.jpg"><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics3D 1024,768,32,1

SetBuffer BackBuffer()

Global SW%=GraphicsWidth()
Global SH%=GraphicsHeight()

Const startflakes%=0   ; start amount of flakes
Const totalflakes%=50000   ; maximum amount of flakes
Const snowchance#=50      ; chance in % for new flakes
Const chanceflakes#=100    ; randomize amount of new flakes (1...x)
Const chancestick#=0.8     ; change that a flake sticks to the matte (here: 80%)

Type snow
	
	Field x#,y#
	Field col%
	Field dirx#,diry#
	Field depth%
	
End Type

CreateSnow(startflakes)

logo=LoadImage("image.jpg") : Global lbuffer=ImageBuffer(logo)
matte=LoadImage("matte.png") : Global mbuffer=ImageBuffer(matte)

Dim SNOW%(SW,SH)
LockBuffer mbuffer
For y=0 To SH-1
	For x=0 To SW-1
		r=(ReadPixelFast(x,y,mbuffer) And $ff0000)/$10000
		SNOW(x,y)=0
		If r&gt;192 Then SNOW(x,y)=1
	Next
Next
UnlockBuffer mbuffer

buffer=BackBuffer()

timer=CreateTimer(60)

music=LoadSound("Frozen Silence-Silent night (piano).mp3")

LoopSound music

PlaySound music

While Not KeyHit(1)
	
	DrawImage logo,0,0
	
	LockBuffer buffer
	
	AppTitle UpdateSnow(buffer)+" Flakes"
	
	UnlockBuffer buffer
	
	Flip 0
	
	WaitTimer timer
	
Wend

End

Function UpdateSnow(buffer%)
	
	Local counter%=0
	
	LockBuffer buffer
	
	For s.snow = Each snow
		
		recycle=False
		
		counter=counter+1
		
		s\x=s\x+s\dirx
		s\y=s\y+s\diry
		
        If s\y&gt;SH-1 Then RecycleFlake(s)
		If s\y&lt;0 Then RecycleFlake(s)
		
        If s\y&gt;=0 And s\x&gt;=0 And s\x&lt;SW And s\y&lt;SH Then
			
			fx=Int(s\x)
			fy=Int(s\y)
			
			dist%=Rand(-1,1)
			
			If fx&lt;dist Then fx=(SW-dist)+fx
			If fx&gt;(SW-dist) Then fx=fx-(SW-dist-1)
			
			If SNOW(fx,fy)&lt;&gt;0 And Rnd(1)&gt;chancestick Then
				
				fx=fx+(SNOW(fx-dist,fy+1)-SNOW(fx+dist,fy+1))
				
				If fx&lt;dist Then fx=(SW-dist)+fx
				If fx&gt;(SW-dist) Then fx=fx-(SW-dist-1)
				
				If fy&lt;1 Then fy=1
				If fy&gt;SH-2 Then fy=SH-2
				
				If SNOW(fx,fy)&lt;&gt;0 Then
					
					SNOW(fx,fy-1)=(SNOW(fx-dist,fy)+SNOW(fx+dist,fy)+SNOW(fx,fy-1)+SNOW(fx,fy+1))*Rnd(0.1,0.25)
					
					RecycleFlake(s)
					
					If fx&gt;0 And fx&lt;SW-1 And fy&gt;0 And fy&lt;SH-1 Then
					
						LockBuffer lbuffer
						rgb=s\col*$10000+s\col*$100+s\col
						WritePixelFast fx,fy,rgb,lbuffer
						
						If Rnd(1)&gt;0.75 Then
						
							WritePixelFast fx-1,fy,rgb,lbuffer
							WritePixelFast fx,fy+1,rgb,lbuffer
							
							
						EndIf
						
						If Rnd(1)&gt;0.75 Then
							
							WritePixelFast fx+1,fy,rgb,lbuffer
							WritePixelFast fx,fy-1,rgb,lbuffer
							
						EndIf
						
						UnlockBuffer lbuffer
						
					EndIf
					
				EndIf
				
			EndIf
			
		EndIf
		
        rgb=s\col*$10000+s\col*$100+s\col
		
		If s\y&gt;1 And s\y&lt;SH-2 And s\x&gt;1 And s\x&lt;SW-2 Then
			
			WritePixelFast Int(s\x),Int(s\y),rgb,buffer
			
			If Rnd(1)&gt;0.75 Then
				
				WritePixelFast Int(s\x-1),Int(s\y),rgb,buffer
				WritePixelFast Int(s\x),Int(s\y+1),rgb,buffer
				
				
			EndIf
			
			If Rnd(1)&gt;0.75 Then
				
				WritePixelFast Int(s\x+1),Int(s\y),rgb,buffer
				WritePixelFast Int(s\x),Int(s\y-1),rgb,buffer
				
			EndIf
			
		EndIf
		
	Next
	
    c=Rand(1,chanceflakes)
	If counter+c&lt;=totalflakes And Rnd(1)&gt;(1-(snowchance/100.0)) Then
		For i=1 To c
			CreateNewFlake()
		Next
	EndIf
	
	UnlockBuffer buffer
	
	Return counter
	
End Function

Function RecycleFlake(s.snow)
	
	s\x=Rnd(0,SW-1)
	s\y=Rnd(-SH,-1)
	s\col=Rand(224,255)
	s\dirx=Rnd(-1,1)
	s\diry=Rnd(0.5,2)
	
End Function

Function CreateSnow(flakes%=1)
	
	Local i%
	
	For i=1 To flakes
		
		CreateNewFlake()
		
	Next
	
End Function

Function CreateNewFlake()
	
	s.snow = New snow
	s\x=Rnd(0,SW-1)
	s\y=Rnd(-SH,0)
	s\col=Rand(224,255)
	s\dirx=Rnd(-1,1)
	s\diry=Rnd(0.5,1)
	
End Function</textarea><br><br>Images needed:<br><br><b>image.jpg</b><br><img src="http://www.christianhart.de/bb/snow/image.jpg"><br><br><b>matte.png</b><br><img src="http://www.christianhart.de/bb/snow/matte.png"><br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1118711"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Very nice -- I've always liked this effect since way back in the 8-bit days! <br><br></td></tr></table><br>
<a name="1118714"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> James - I updated the codebox a few minutes ago - the flakes are now more fluffy, looks much better than just single pixels. <br><br></td></tr></table><br>
<a name="1118813"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah, yes, that's nicer. <br><br></td></tr></table><br>
<a name="1118843"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Steve Elliott</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Neat :D <br><br></td></tr></table><br>
<a name="1118857"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hehe. Very festive indeed. Very nice :) <br><br></td></tr></table><br>
<a name="1118865"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I converted this to Blitzmax (and redesigned the code a little bit, added a sinus movement and the falling flakes are images now). It works so far but there is one problem. On my system, the FPS drops and drops and I don't know why. Any idea how to speed this up or improve it?<br><br>Full Demo with source, EXE and media: <a href="http://www.christianhart.de/bb/snow/letitsnow.zip" target="_blank">Download ZIP [5MB]</a><br><br><img src="http://www.christianhart.de/bb/snow/letitsnow.jpg"><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' Let it snow by Krischan
' -----------------------
'
' Image taken from www.bunnybua.com, redesigned for this demo
' Music taken from www.jamendo.com/en/track/442222, converted to OGG

SuperStrict

Incbin "image.jpg"
Incbin "matte.png"
Incbin "snow.png"
Incbin "Frozen Silence-Silent night (piano).ogg"

Graphics 1024,768,32,60

Global SW:Int=GraphicsWidth()
Global SH:Int=GraphicsHeight()

Global SNOW:Int[SW+1,SH+1]
Global s:snowflake

Const startflakes:Int		= 0		' start amount of flakes
Const totalflakes:Int		= 20000	' maximum amount of flakes
Const snowchance:Float		= 50		' chance in % For New flakes
Const chanceflakes:Float	= 100		' randomize amount of New flakes (1...x)
Const chancestick:Float	= 0.8		' change that a flake sticks To the matte (here: 80%)

Type snowflake

	Global List:TList = CreateList()
	
	Method New()
		ListAddLast(List, Self)
	End Method
	
	Field x:Float,y:Float
	Field col:Int
	Field dirx:Float,diry:Float
	Field style:Float
	Field sinus:Float
	
End Type

Global logo:TPixmap=LoadPixmap("incbin::image.jpg")
Global matte:TPixmap=LoadPixmap("incbin::matte.png")
Global flake:TImage=LoadImage("incbin::snow.png")

For Local y:Int=0 To SH-1

	For Local x:Int=0 To SW-1
	
		Local rgb:Int=ReadPixel(matte,x,y)
		Local r:Int=(rgb &amp; $ff0000) / $10000
		
		SNOW[x,y]=0
		
		If r&gt;192 Then SNOW[x,y]=1
	Next
	
Next

Global music:TSound = LoadSound("incbin::Frozen Silence-Silent night (piano).ogg", True)
PlaySound music

CreateSnow(startflakes)

SetBlend ALPHABLEND

While Not KeyHit(KEY_ESCAPE)

	Cls

	DrawPixmap logo,0,0
		
	UpdateSnow(s)
	
	Flip 0
	
Wend

EndGraphics()

End

Function RecycleFlake(s:snowflake)
	
	s.x=Rnd(0,SW-1)
	s.y=Rnd(-SH,-1)
	s.col=Rand(224,255)
	s.dirx=Rnd(-1,1)
	s.diry=Rnd(0.5,2)
	s.style=Rand(0,3)
	s.sinus=Rnd(360)
	
End Function

Function CreateSnow(flakes:Int=1)
		
	For Local i:Int=1 To flakes
		
		CreateNewFlake()
		
	Next
	
End Function

Function CreateNewFlake()
	
	Local s:snowflake = New snowflake
	
	RecycleFlake(s)
		
End Function

Function UpdateSnow:Int(s:snowflake)
	
	Local counter:Int=0
	Local rgb:Int
	Local recycle:Int
	Local dist:Int
	Local fx:Int,fy:Int
	Local c:Int,i:Int
		
	For s:snowflake = EachIn snowflake.List
		
		recycle=False
		
		counter=counter+1
		
		s.sinus=s.sinus+Rnd(3) Mod 360
		
		s.x=s.x+s.dirx+(Sin(s.sinus)*0.5)
		s.y=s.y+s.diry
		
        	If s.y&gt;SH-1 Then RecycleFlake(s)
		If s.y&lt;0 Then RecycleFlake(s)
		
        	If s.y&gt;=0 And s.x&gt;=0 And s.x&lt;SW And s.y&lt;SH Then
			
			fx=Int(s.x)
			fy=Int(s.y)
			
			dist=Rand(-1,1)
			
			If fx&lt;dist Then fx=(SW-dist)+fx
			If fx&gt;(SW-dist) Then fx=fx-(SW-dist-1)
			
			If fx&lt;1 Then fx=1
			If fx&gt;SW-2 Then fx=SW-2
			If fy&lt;1 Then fy=1
			If fy&gt;SH-2 Then fy=SH-2
				
			If SNOW[fx,fy]&lt;&gt;0 And Rnd(1)&gt;chancestick Then
				
				If fx&lt;dist Then fx=(SW-dist)+fx
				If fx&gt;(SW-dist) Then fx=fx-(SW-dist-1)
				
				If fx&lt;1 Then fx=1
				If fx&gt;SW-2 Then fx=SW-2
				If fy&lt;1 Then fy=1
				If fy&gt;SH-2 Then fy=SH-2
				
				If SNOW[fx,fy]&lt;&gt;0 Then
									
					RecycleFlake(s)
					
					If fy-1&gt;0 Then SNOW[fx,fy-1]=1
					If fx-1&gt;0 Then SNOW[fx-1,fy]=1
					If fx+1&lt;SW-1 Then SNOW[fx+1,fy]=1
					
					If fx&gt;0 And fx&lt;SW-1 And fy&gt;0 And fy&lt;SH-1 Then
					
						rgb=toARGB(s.col,s.col,s.col,s.col)

						WritePixel(logo,fx,fy,rgb)
						
						If Rnd(1)&gt;0.75 Then
						
							WritePixel(logo, fx-1,fy,rgb)
							WritePixel(logo, fx,fy+1,rgb)
														
						EndIf
						
						If Rnd(1)&gt;0.75 Then
							
							WritePixel(logo, fx+1,fy,rgb)
							WritePixel(logo, fx,fy-1,rgb)
							
						EndIf
						
					EndIf
					
				EndIf
				
			EndIf
			
		EndIf
			
		If s.y&gt;1 And s.y&lt;SH-2 And s.x&gt;1 And s.x&lt;SW-2 Then
		
			DrawSubImageRect(flake,fx-1,fy-1,3,3,(s.style*3),0,3,3)
	
		EndIf
		
	Next
	
	c=Rand(1,chanceflakes)
	
	If counter+c&lt;=totalflakes And Rnd(1)&gt;(1-(snowchance/100.0)) Then
		For i=1 To c
			CreateNewFlake()
		Next
	EndIf
	
	Return counter
	
End Function

Function toARGB:Int(a:Int,r:Int,g:Int,b:Int)

	Return ((a Shl 24)|(r Shl 16)|(g Shl 8)|b)
	
End Function</textarea> <br><br></td></tr></table><br>
<a name="1118869"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> How long does it take before it drops? Seems fine here after a few minutes in release mode (even if debug mode is very slow)... <br><br></td></tr></table><br>
<a name="1118873"></a>

<a name="1118874"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> The debug mode is indeed very slow. In normal mode it starts at 70-80 FPS (with zero flakes) and drops to 50-60 with 20000 flakes, the maximum. After a minute it drops to 40-30, still with 20000 flakes. First I tried to simulate the falling snowflake code from my B3D version but I couldn't find out how to write the flake pixels to a transparent pixmap over the background image in the loop (impossible?), so I went for the current solution with a separate 12x3 pixel snowflake variation image and DrawSubImageRect.<br><br>Strange - in the B3D version I can go up to 50.000 flakes before it drows below 60FPS.<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1119066"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grindalf</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> It just shows how B3D is superior :P <br><br></td></tr></table><br>
<a name="1119290"></a>

<a name="1119291"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice. Wait a second, where's the Monkey version? :P<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1119297"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >John G</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Very nice snow sticking effect. <br><br></td></tr></table><br>
<a name="1167326"></a>

<a name="1167327"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Bump. It's christmas time again! This time with precompiled executables in B3D and BMAX + source + media in one ZIP file.<br><br>Download: <a href="http://www.christianhart.de/bb/letitsnow/letitsnow.zip" target="_blank">letitsnow.zip</a> (3.5MB)<br><br><img src="http://www.christianhart.de/bb/snow/letitsnow.jpg"><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1167343"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CyBeRGoth</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Great stuff I am now fully in the Christmas spirit! hehe <br><br></td></tr></table><br>
<a name="1167348"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >psychicbottle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> haha, very nice here. :p <br><br></td></tr></table><br>
<a name="1167351"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >shinkiro1</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice one. <br><br></td></tr></table><br>
<a name="1167738"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well I've been bored and so I updated the demo a little bit, optimized the source and used maxgui this time. Now includes a happy new year firework, press Space to switch between the two modes, in firework mode press LMB for a new rocket and RMB to reset the rockets.<br><br>Merry christmas and a happy new year to you all!<br><br>Download Demo: <a href="http://www.christianhart.de/bb/letitsnow/letitsnow2_demo.zip" target="_blank">letitsnow2_demo.zip</a> (8.2MB)<br>Download Source: <a href="http://www.christianhart.de/bb/letitsnow/letitsnow2_source.zip" target="_blank">letitsnow2_source.zip</a> (1.8MB)<br>(I didn't include the music in the source to save filespace, you can download the tracks from the links in the source comment and convert them to OGG using Audacity for example and put them in the music/ folder or use your own)<br><br>Screenshots (looks even better in action):<br><a href="http://www.christianhart.de/bb/letitsnow/merrychristmas.jpg" target="_blank"><img src="http://www.christianhart.de/bb/letitsnow/merrychristmas_t.jpg"></a> <a href="http://www.christianhart.de/bb/letitsnow/happynewyear.jpg" target="_blank"><img src="http://www.christianhart.de/bb/letitsnow/happynewyear_t.jpg"></a><br><br>Here the full source:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' Let it snow by Krischan
' -----------------------
'
' Image from www.bunnybua.com, redesigned by Krischan
'
' Xmas Music from Frozen Silence: www.jamendo.com/en/track/442222
' License: <a href="http://creativecommons.org/licenses/by-nc-sa/3.0/" target="_blank">http://creativecommons.org/licenses/by-nc-sa/3.0/</a>
'
' Firework Music from Goose Creek Bands: <a href="http://soundclick.com/share.cfm?id=3762899" target="_blank">http://soundclick.com/share.cfm?id=3762899</a>
' License: <a href="http://creativecommons.org/licenses/by-nd/3.0/" target="_blank">http://creativecommons.org/licenses/by-nd/3.0/</a>
'
' Rocket Sound FX from <a href="http://www.freesounds.org" target="_blank">http://www.freesounds.org</a>
' License: <a href="http://creativecommons.org/licenses/by/3.0/" target="_blank">http://creativecommons.org/licenses/by/3.0/</a>

SuperStrict

Import "santa.o"

Framework BRL.GLMax2D
Import BRL.WAVloader
Import BRL.TGAloader
Import BRL.Pixmap
Import BRL.Random
Import BRL.OGGloader
Import BRL.DirectSoundAudio
Import BRL.Timer
Import BRL.RamStream
Import BRL.EventQueue
Import MaxGUI.Drivers
Import MaxGUI.ProxyGadgets

Incbin "images/xmas.tga"
Incbin "images/newyear.tga"

Incbin "music/Frozen Silence-Silent night (piano).ogg"
Incbin "music/goosecreekbands+watermusic.ogg"

Incbin "particles/snow.tga"
Incbin "particles/particle1.tga"
Incbin "particles/particle2.tga"
Incbin "particles/particle3.tga"
Incbin "particles/particle4.tga"
Incbin "particles/particle5.tga"
Incbin "particles/particle6.tga"
Incbin "particles/particle7.tga"
Incbin "particles/particle8.tga"
Incbin "particles/rocket.tga"
Incbin "particles/bottle.tga"

Incbin "sound/explode1.ogg"
Incbin "sound/explode2.ogg"
Incbin "sound/explode3.ogg"
Incbin "sound/explode4.ogg"
Incbin "sound/explode5.ogg"
Incbin "sound/explode6.ogg"
Incbin "sound/explode7.ogg"
Incbin "sound/launch.ogg"

Global SW:Int=1024	
Global SH:Int=640

Global s:snowflake
Global fireworkcnt:Int
Global particlecnt:Int
Global mode:Int=0
Global musicchannel:TChannel

Const startflakes:Int		= 0		' start amount of flakes
Const totalflakes:Int		= 5000	' maximum amount of flakes
Const snowchance:Float		= 25		' chance in % For New flakes
Const chanceflakes:Float	= 25		' randomize amount of New flakes (1...x)
Const chancestick:Float	= 0.9		' change that a flake sticks To the matte (here: 50%)

Local  timer:TTimer=CreateTimer(125)

Type snowflake

	Global List:TList = CreateList()
	
	Method New()

		ListAddLast(List, Self)

	End Method
	
	Field x:Float,y:Float
	Field col:Int
	Field dirx:Float,diry:Float
	Field style:Float
	Field sinus:Float
	Field alpha:Float
	
End Type

Type firework

	Global List:TList = CreateList()
	
	Method New()

		ListAddLast(List, Self)

	End Method

	Method Remove()

		ListRemove(List,Self)

	End Method

	Field x:Float[500],y:Float[500]
	Field dirx:Float[500],diry:Float[500]
	Field size:Float[500]
	Field grav:Float[500]
	Field speed:Float[500]
	Field particle:Int[500]

	Field timer:Float
	Field starttimer:Int
	Field life:Int
	Field lifetimer:Int
	Field exploded:Int
	Field num:Int
	Field init:Int

End Type

' create window and canvas
Global window:TGadget=CreateWindow("Let it snow",ClientWidth(Desktop())/2-(SW/2),ClientHeight(Desktop())/2-(SH/2),SW+12,SH+24,Null,WINDOW_TITLEBAR | WINDOW_RESIZABLE)
MaximizeWindow(window)
Global canvas:TGadget=CreateCanvas(0,0,ClientWidth(window),ClientHeight(window),window,0)
SetGadgetLayout canvas,1,1,1,1
SetGraphics CanvasGraphics(canvas)

' load media
Global explode:TSound[8]
Global particle:TImage[9]

For Local i:Int=1 To 8

	particle[i]=LoadImage("incbin::particles/particle"+i+".tga")

Next

For Local i:Int=1 To 7

	explode[i]=LoadSound("incbin::sound/explode"+i+".ogg")

Next

Global fly:TSound=LoadSound("incbin::sound/launch.ogg")
Global logo1:TPixmap=LoadPixmap("incbin::images/xmas.tga")
Global logo2:TPixmap=LoadPixmap("incbin::images/newyear.tga")
Global flake:TImage=LoadImage("incbin::particles/snow.tga")
Global rocket:TImage=LoadImage("incbin::particles/rocket.tga")
Global bottle:TImage=LoadImage("incbin::particles/bottle.tga")
Global image1:TPixmap=ResizePixmap(logo1,SW,SH)
Global image2:TPixmap=ResizePixmap(logo2,SW,SH)
Global music1:TSound=LoadSound("incbin::music/Frozen Silence-Silent night (piano).ogg", True)
Global music2:TSound=LoadSound("incbin::music/goosecreekbands+watermusic.ogg",True)
If mode=0 Then musicchannel=PlaySound(music1) Else musicchannel=PlaySound(music2)

SW=ClientWidth(window)
SH=ClientHeight(window)
image1=ResizePixmap(logo1,SW,SH)
image2=ResizePixmap(logo2,SW,SH)

' set Escape hotkey
SetHotKeyEvent(KEY_ESCAPE,Null,CreateEvent(EVENT_GADGETACTION,window,0))
SetHotKeyEvent(KEY_SPACE,Null,CreateEvent(EVENT_GADGETACTION,window,1))

' init snow
CreateSnow(startflakes)

SetGraphics CanvasGraphics(canvas)

For Local i:Int=-6 To 6
	NewFirework(i)
	fireworkcnt:+1
Next

' ----------------------------------------------------------------------------
' Main Loop
' ----------------------------------------------------------------------------
While True

	particlecnt=0

	ActivateGadget(canvas)

	WaitEvent()

	If EventSource()=timer Then

		If mode=0 Then

			DrawPixmap(image1,0,0)
			SetGadgetText(window,"Let it snow "+UpdateSnow(s)+" flakes")

		Else

			DrawPixmap(image2,0,0)

			SetBlend(ALPHABLEND)
			SetAlpha(1.0)
			SetColor(255,255,255)
			SetRotation(0)

			For Local f:firework = EachIn firework.List

				If (f.y[1]&lt;(SH-(SH*f.timer)) Or f.x[1]&lt;0 Or f.x[1]&gt;SW) And f.exploded=0 Then

					For Local i:Int=1 To f.num

						f.x[i]=f.x[1]
						f.y[i]=f.y[1]					
						f.grav[i]=Rnd(0.1,0.5)
						f.speed[i]=Rnd(Rnd(1,4))

						If Rnd(1)&gt;0.8 Then
							f.particle[i]=Rand(1,7)
							f.size[i]=Rnd(4,16)
						Else
							f.particle[i]=Rand(7,8)
							f.size[i]=Rnd(4,16)							
						EndIf

					Next

					f.exploded=1
					f.lifetimer=MilliSecs()
					SetChannelVolume(PlaySound(explode[Rand(1,7)]),Rnd(0.5,1))

				EndIf

				If f.exploded=1 Then

					SetBlend(LIGHTBLEND)
					SetRotation(0)

					For Local i:Int=1 To f.num

						f.dirx[i]=Sin(i*(360.0/f.num))*f.speed[i]
						f.diry[i]=Cos(i*(360.0/f.num))*f.speed[i]

						f.x[i]:+f.dirx[i]
						f.y[i]:-f.diry[i]

						Local a:Float=(f.lifetimer+f.life-MilliSecs())*1.0/(f.life/4.0)

						SetAlpha(a)

						Local col:Int=Rand(128,255)
						SetColor(col,col,col)

						If f.x[i]&gt;0 And f.x[i]&lt;SW And f.y[i]&gt;0 Then

							particlecnt:+1
							DrawImageRect(particle[f.particle[i]],f.x[i],f.y[i],f.size[i],f.size[i])

						EndIf

					Next

				EndIf

				If f.exploded=1 And MilliSecs()&gt;f.lifetimer+f.life Then

					f.remove
					NewFirework()
					'fireworkcnt:-1

				EndIf

				SetAlpha(1.0)
				SetColor(255,255,255)
				SetBlend(ALPHABLEND)

				If f.exploded=0 And MilliSecs()&gt;f.starttimer

					If f.init=0 Then
						SetChannelVolume(PlaySound(fly),Rnd(0.25,1))
						f.init=1
					EndIf

					Local oldx:Float=f.x[1]
					Local oldy:Float=f.y[1]

					f.x[1]:+f.dirx[1]
					f.y[1]:-f.diry[1]
					f.dirx[1]=f.dirx[1]*1.05

					Local vecx:Float=f.x[1]-oldx
					Local vecy:Float=f.y[1]-oldy

					SetRotation(ATan2(vecy,vecx)+90)
					DrawImageRect(rocket,f.x[1],f.y[1],8,32)

				Else If f.exploded=0 And MilliSecs()&lt;f.starttimer

					SetRotation(0)
					DrawImageRect(rocket,f.x[1],f.y[1],8,32)

				EndIf

			Next

			SetRotation(0)
			SetAlpha(1.0)
			SetColor(255,255,255)

			For Local x:Int=-6 To 6

				DrawImageRect(bottle,SW/2-16+(x*64),SH*0.9,32,32)

			Next

			SetGadgetText(window,fireworkcnt+" Rockets and "+particlecnt+" particles flying around")

		EndIf

		Flip -1

	EndIf

	If EventID()=EVENT_GADGETACTION And EventData()=0 Then End
	If EventID()=EVENT_GADGETACTION And EventData()=1 Then
		mode=1-mode
		If mode=0 Then
			StopChannel(musicchannel)
			musicchannel=PlaySound(music1)
		Else
			StopChannel(musicchannel)
			musicchannel=PlaySound(music2)
		EndIf
	EndIf 

	Select EventID()

		Case EVENT_WINDOWCLOSE		End

		Case EVENT_APPTERMINATE	End

		Case EVENT_MOUSEDOWN		If EventData()=1 Then
								NewFirework()
								fireworkcnt:+1
							Else
								For Local f:firework = EachIn firework.List

									f.remove
									fireworkcnt=0

								Next

								For Local i:Int=-6 To 6

									NewFirework(i)
									fireworkcnt:+1

								Next

							EndIf


		Case EVENT_WINDOWSIZE		SW=ClientWidth(window)
							SH=ClientHeight(window)
							image1=ResizePixmap(logo1,SW,SH)
							image2=ResizePixmap(logo2,SW,SH)
							SetGadgetShape(canvas,0,0,ClientWidth(window),ClientHeight(window))
							SetViewport 0,0,ClientWidth(window),ClientHeight(window)

							For s:snowflake = EachIn snowflake.List

								ListRemove(snowflake.List,s)

							Next

							SetGraphics CanvasGraphics(canvas)
							RedrawGadget canvas


	End Select

Wend

Function NewFirework(init:Int=-1000)

	Local x:Float

	Local f:firework = New firework

	If init=-1000 Then

		x=SW/2-5+(Rand(-6,6)*64)

	Else

		x=SW/2-5+(init*64)

	EndIf

	Local y:Float=SH*0.9-24
	Local dirx:Float=Rnd(-0.2,0.2)
	Local diry:Float=Rnd(5,10)
	
	f.num=Rand(100,500)

	f.x[1]=x
	f.y[1]=y
	f.dirx[1]=dirx
	f.diry[1]=diry
		
	f.timer=Rnd(0.5,1.0)
	f.life=Rand(1000,3000)
	f.lifetimer=0
	f.exploded=0
	f.starttimer=MilliSecs()+Rand(500,2000)

End Function

' ----------------------------------------------------------------------------
' recycle casted snowflake
' ----------------------------------------------------------------------------
Function RecycleFlake(s:snowflake,init:Int=False)
	
	s.x=Rnd(0,SW-1)
	s.y=0
	If init Then s.y=Rnd(-SH,0)

	s.col=Rand(232,255)
	s.dirx=Rnd(-1,1)
	s.diry=Rnd(0.5,2)
	s.style=Rand(0,3)
	s.sinus=Rnd(360)
	s.alpha=Rnd(0.5,1)
	
End Function

' ----------------------------------------------------------------------------
' initial cast snowflakes
' ----------------------------------------------------------------------------
Function CreateSnow(flakes:Int=1)
		
	For Local i:Int=1 To flakes
		
		CreateNewFlake(True)
		
	Next
	
End Function

' ----------------------------------------------------------------------------
' cast a new snowflake
' ----------------------------------------------------------------------------
Function CreateNewFlake(init:Int=False)
	
	Local s:snowflake = New snowflake
	
	RecycleFlake(s,init)
		
End Function

' ----------------------------------------------------------------------------
' calculate ARGB value
' ----------------------------------------------------------------------------
Function toARGB:Int(a:Int,r:Int,g:Int,b:Int)

	Return ((a Shl 24)|(r Shl 16)|(g Shl 8)|b)
	
End Function

' ----------------------------------------------------------------------------
' read transparent pixels from image for collision
' ----------------------------------------------------------------------------
Function CheckCollision:Int(x:Int,y:Int)

	Return (ReadPixel(image1,x,y) &amp; $ff000000) Shr 24

End Function

' ----------------------------------------------------------------------------
' write pixel to image
' ----------------------------------------------------------------------------
Function UpdateCollision(s:snowflake,x:Int,y:Int)

	WritePixel(image1,x,y,toARGB(s.col*s.alpha,s.col*Rnd(0.9,1),s.col*Rnd(0.95,1),s.col))

End Function

' ----------------------------------------------------------------------------
' update snowflakes
' ----------------------------------------------------------------------------
Function UpdateSnow:Int(s:snowflake)
	
	Local counter:Int=0
	Local rgb:Int
	Local dist:Int
	Local fx:Int,fy:Int
	Local c:Int,i:Int
	Local ax:Int,ay:Int

	SetBlend(ALPHABLEND)
	
	For s:snowflake = EachIn snowflake.List
	
		counter=counter+1
		
		' smooth random movement
		s.sinus=s.sinus+Rnd(3) Mod 360

		' move horizontal and vertical		
		s.x=s.x+s.dirx+(Sin(s.sinus)*0.5)
		s.y=s.y+s.diry

		' flake off screen? recycle!		
        	If s.y&gt;SH-1 Then RecycleFlake(s)
		If s.x&lt;0 Then RecycleFlake(s)
		If s.x&gt;SW Then RecycleFlake(s)
		
		fx=Int(s.x)
		fy=Int(s.y)
		dist=Rand(-1,1)
			
		' limit horizontal/vertical
		If fx&lt;1 Then fx=1 Else If fx&gt;SW-2 Then fx=SW-2
		If fy&lt;1 Then fy=1 Else If fy&gt;SH-2 Then fy=SH-2

		' check for collision with matte				
		If CheckCollision(fx,fy)&gt;0 And Rnd(1)&gt;chancestick Then
				
			' create new collision pixel in image
			UpdateCollision(s,fx,fy)

			' random brightness						
			rgb=toARGB(s.col*s.alpha,s.col*Rnd(0.9,1),s.col*Rnd(0.95,1),s.col)

			' add aditional pixel
			If Rnd(1)&gt;0.5 Then
				ax:Int=Rand(-1,1)
				ay:Int=Rand(-1,0)
				WritePixel(image1,fx+ax,fy+ay,rgb)
			EndIf

			' add aditional pixel
			If Rnd(1)&gt;0.5 Then
				ax:Int=Rand(-1,1)
				ay:Int=Rand(-1,0)
				WritePixel(image1,fx+ax,fy+ay,rgb)
			EndIf
						
			' recycle old flake
			RecycleFlake(s)

		Else

			If s.y&gt;0 Then
			' draw moving flake
			SetAlpha(s.alpha)
			SetColor(255,255,255)
			DrawSubImageRect(flake,fx-1,fy-1,3,3,(s.style*3),0,3,3)'
			EndIf
					
		EndIf
		
	Next

	' cast new snowflakes	
	c=Rand(1,chanceflakes)
	If counter+c&lt;=totalflakes And Rnd(1)&gt;(1-(snowchance/100.0)) Then
		For i=1 To c
			CreateNewFlake()
		Next
	EndIf
	
	Return counter
	
End Function</textarea> <br><br></td></tr></table><br>
<a name="1167744"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dabhand</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> Bah Humb.... Ohhhhh, nice!!! :)<br><br>Dabz <br><br></td></tr></table><br>
<a name="1167784"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JBR</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> Really nice and peaceful.<br>Jim <br><br></td></tr></table><br>
<a name="1167802"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JonnyEnglish</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> Wow..cool! <br><br></td></tr></table><br>
<a name="1167806"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Steve Elliott</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes, a stressful day...then I put this on lol...very soothing. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
