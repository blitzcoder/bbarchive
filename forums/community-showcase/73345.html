<!DOCTYPE html><html lang="en" ><head ><title >Waponez Type engine</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Waponez Type engine</h1><a href="forums.php" >Community Forums</a>/<a href="topics.php?forum=61" >Showcase</a>/<a href="#bottom" >Waponez Type engine</a><br><br>
<a name="819638"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >itsdanreed</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> <a href="http://img85.imageshack.us/my.php?image=screenshotwaponezpw7.png" target="_blank"><img src="http://img85.imageshack.us/img85/3441/screenshotwaponezpw7.th.png"></a><br><br>It's nothing great, I made this using BlitzMAX 1.26, and using some sprites for the PureBASIC example "Waponez II". This code contains no elements from the PureBASIC example, and I feel it's somewhat superior. <br>Anyways, comments are welcome!<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Type GameEntity
	Field X:Float							= 0
	Field Y:Float							= 0
	Field Width:Float						= 0
	Field Height:Float						= 0
	Field Frame:Float						= 0
	Field Destructed:Int					= False
	Field Sprites:TImage[ ]
	
	Method AddFrame:Int( Url:String )
		' Add one element to Sprites[].
		Sprites = Sprites[..(Sprites.Length + 1)]
		
		' Load new image.
		Sprites[Sprites.Length - 1] = LoadImage( Url )
		
		' Return new frame number.
		Return(Sprites.Length - 1)
	End Method
	
	Method Render( )
		If (Sprites.Length &gt; 0)
			' Update dimension variables for current frame.
			Width = ImageWidth(Sprites[Int(Frame)])
			Height = ImageHeight(Sprites[Int(Frame)])
			
			' Draw Image
			DrawImage(Sprites[Int(Frame)], X, Y)
		EndIf
	End Method
	
	Method Distance:Float( Entity:GameEntity )
		Return (Sqr((X - Entity.X) ^ 2 + (Y - Entity.Y) ^ 2))
	End Method
	
	Method Collided:Int( Entity:GameEntity )
		Return(ImagesCollide(Sprites[Frame], X, Y, 0, Entity.Sprites[Entity.Frame], Entity.X, Entity.Y, 0))
	End Method
	
	Method Update( ) Abstract
	Method ToString:String( ) Abstract
End Type

Type GEPlayer Extends GameEntity
	Const SPEED								= 15
	
	Field dX:Float
	Field dY:Float
	Field FireTimer:Int							= 0
	
	Method New( )
		AddFrame("Data/Player_1.png")
		AddFrame("Data/Player_2.png")
		AddFrame("Data/Player_3.png")
			
		X = (GraphicsWidth( ) / 2) - (ImageWidth(Sprites[Int(Frame)]) / 2)
		Y = (GraphicsHeight( ) - ImageHeight(Sprites[Int(Frame)]))
		
		MoveMouse(X, Y)
	End Method
	
	Method Update( )
		Frame = 0
		
		dX = MouseX( )
		dY = MouseY( )
		
		If (MouseDown(1) And (FireTimer &lt;= 0)) FireBullet( )
		
		If (dX &lt; X) Frame = 2
		If (dX &gt; X) Frame = 1
		
		If (FireTimer &gt; 0) FireTimer :- 1
		
		X = X + ((dX - X) / SPEED)
		Y = Y + ((dY - Y) / SPEED)		
		
		If (X &lt; 0) X = 0
		If (Y &lt; 0) Y = 0
		
		If ((X + Width) &gt; GraphicsWidth( )) X = (GraphicsWidth( ) - Width)
		If ((Y + Height) &gt; GraphicsHeight( )) Y = (GraphicsHeight( ) - Height)
	End Method
	
	Method FireBullet( )
		Local Entity:GameEntity = New GEBullet
		
		Entity.X = X
		Entity.Y = Y
		
		GameEngine.AddEntity(Entity)
		
		FireTimer = 10
	End Method
	
	Method ToString:String( )
		Return( "GEPlayer" )
	End Method
End Type

Type GEBullet Extends GameEntity
	Const SPEED								= 15
	
	Method New( )
		AddFrame("Data/Bullet_1.png")
	End Method
	
	Method Update( )
		Y:- SPEED
		
		If (Y &lt; 0) Destructed = True
	End Method
	
	Method ToString:String( )
		Return( "GEBullet" )
	End Method
End Type

Type GEEnemy Extends GameEntity
	Const Speed								= 4
	
	Field Armor:Int							= 10
	
	Method New( )
		AddFrame("Data/Ennemy_3_1.PNG")
		AddFrame("Data/Ennemy_3_2.PNG")
		AddFrame("Data/Ennemy_3_3.PNG")
		AddFrame("Data/Ennemy_3_4.PNG")
		AddFrame("Data/Ennemy_3_5.PNG")
		AddFrame("Data/Ennemy_3_6.PNG")
	End Method
	
	Method Update( )
		Y :+ SPEED	
		
		If (Frame &lt; (Sprites.Length - 1))
			Frame :+ 0.15
		Else
			Frame = 0
		EndIf
		
		'
		For Entity:GameEntity = EachIn GameEngine.EntityList
			Select (Entity.ToString( ))
			Case "GEPlayer"
				If (Collided(Entity))
					DebugLog("Collision With Player")

					Armor :- 10
				EndIf
			
			Case "GEBullet"
				If (Collided(Entity))
					DebugLog("Collision With Bullet")

					Armor :- 5
					Entity.Destructed = True
				EndIf
						
			End Select
		Next
		
		If (Armor &lt;= 0)
			Explo:GameEntity = New GEExplosion
			Explo.X = X
			Explo.Y = Y
			GameEngine.AddEntity( Explo )
					
			Destructed = True
		EndIf
		'
		If (Y &gt; GraphicsHeight( ))
			Destructed = True
		EndIf
		
		DrawText("Armor: " + Armor, X - 4, Y - 12)
	End Method
	
	Method ToString:String( )
		Return( "GEEnemy" )
	End Method
End Type

Type GEExplosion Extends GameEntity
	Method New( )
		AddFrame("Data/Explosion_1.PNG")
		AddFrame("Data/Explosion_2.PNG")
		AddFrame("Data/Explosion_3.PNG")
		AddFrame("Data/Explosion_4.PNG")
		AddFrame("Data/Explosion_5.PNG")
		AddFrame("Data/Explosion_6.PNG")
		AddFrame("Data/Explosion_7.PNG")
		AddFrame("Data/Explosion_8.PNG")
	End Method
	
	Method Update( )
		If (Frame &lt; (Sprites.Length - 1))
			Frame :+ 0.15
		Else
			Destructed = True
		EndIf
	End Method
	
	Method ToString:String( )
		Return( "GEExplosion" )
	End Method
End Type

Type GameEngine
	Global EntityList:TList					= CreateList( )

	Field IsRunning:Int						= True
	Field SpawnCounter:Int					= 50
		
	Method Init( )	
		'
		AppTitle = "BMWaponez II"
		Graphics(640, 480, 32)

	End Method
	
	Method Run( )
		Init( )
	
		AddEntity( New GEPlayer )
		
		While ((Not AppTerminate( )) And IsRunning)
			' Clear screen.
			Cls( )
			
			' 
			For Entity:GameEntity = EachIn EntityList
				If (Entity.Destructed)
					DeleteEntity( Entity )
				Else
					Entity.Update( )
					Entity.Render( )
				EndIf
			Next
			
			' 
			If (SpawnCounter &gt; 0)
				SpawnCounter :- 1
			Else
				SpawnCounter = 50
				
				Enemy:GameEntity = New GEEnemy
		
				Enemy.X = Rand(0, GraphicsWidth( ))
				
				AddEntity(Enemy)
				DebugLog("Spawning Enemy")

			EndIf
			'
			GCCollect( )
			DrawText("Memory Useage: " + GCMemAlloced( ), 5, 5)
			DrawText("Spawn Counter: " + SpawnCounter, 5, 20)
			
			' 
			Flip( True )
		End While
	End Method
	
	Function AddEntity( Entity:GameEntity )
		EntityList.AddLast( Entity )
	End Function
	
	Function DeleteEntity( Entity:GameEntity )
		EntityList.Remove( Entity )
		Entity = Null
	End Function
End Type

Global Engine:GameEngine = New GameEngine

Engine.Run( )

</textarea><br><br><br><br>The game will never be completed (atleast this version perhaps), it is just for show.<br><br>Comments are welcome, Thanks.<br><br>:: Watch the game in action ::<br><a href="http://www.youtube.com/watch?v=Avou-CyTlFE" target="_blank">Video @ YouTube </a> <br><br></td></tr></table><br>
<a name="820100"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >itsdanreed</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> No comments? <br><br></td></tr></table><br>
<a name="820108"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >chwaga</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> make it .exe, as many of us don't have bmax <br><br></td></tr></table><br>
<a name="820115"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >itsdanreed</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I posted video :p<br>did you watch that? <br><br></td></tr></table><br>
<a name="820174"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Geehawk</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> What comments do you expect based on a small video clip in which the sprites are unclear, and the text unreadable. <br><br></td></tr></table><br>
<a name="820180"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Game looks ok in the Video but as chwaga says post a link to an exe.  As you haven't included the media no-one can run it anyway. <br><br></td></tr></table><br>
<a name="820404"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >itsdanreed</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> The flame ^^, sorry. <br><br>I appologize for the RapidShare upload, I don't yet have a website to host my own files, so sorry.<br><br>Download link<br><a href="http://rapidshare.com/files/64482929/BMWII.zip.html" target="_blank"> Download @ RapidShare </a> <br><br></td></tr></table><br>
<a name="820888"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Neuro</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice little shooter. I had some trouble exiting the game though. Reminded me of my little shooter game i did a while back... <br><br></td></tr></table><br>
<a name="821247"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >itsdanreed</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for the comment neuro, I forgot to mention... use alt-f4, because I didn't implement escape :( <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
