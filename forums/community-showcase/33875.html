<!DOCTYPE html><html lang="en" ><head ><title >3d lightning like test</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >3d lightning like test</h1><a href="forums.php" >Community Forums</a>/<a href="topics.php?forum=61" >Showcase</a>/<a href="#bottom" >3d lightning like test</a><br><br>
<a name="365157"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey everyone. I've created some code to create lightning, sort of. Involves deforming a mesh in real-time. You can use these function for whatever. Could a get a fps on the code running as it is though, please? Press space bar to get the TRUE fps reading, thanks :)<br><br>It's better when viewed moving. Remember the below code is simply just to rotate them. The functions do the work of creating and deforming the meshes. Hopefully is useful to someone other than me :)<br><br><img src="http://www.rosscrooks.pwp.blueyonder.co.uk/screenlight.JPG"><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800,600
SetBuffer BackBuffer()
;WireFrame True

Type bolt
	Field x#,y#,z#
	Field mesh
	Field surf
	Field segments ; number of segments the cylinder has |===|===| = 2 segments
	Field divisions ; how smooth the cylinder looks
	Field seg_length#
End Type 



Global piv=CreatePivot()
Global cam=CreateCamera(piv)
PositionEntity cam,0,5,-11
RotateEntity cam,30,0,0
CameraRange cam,0.1,100

Global light=CreateLight()
TurnEntity light,45,45,45

For loop=0 To 13
	l.bolt=New bolt
		l\x=0
		l\y=0
		l\z=0
		l\mesh=CreateMesh()
		l\surf=CreateSurface(l\mesh)
		l\segments=Rand(3,5)
		l\seg_length=Rand(5,7)
		l\divisions=3
	
	create_rod(l\mesh,l\surf,l\segments,l\divisions,l\seg_length)
	
	UpdateNormals(l\mesh)
	EntityFX l\mesh,32+2
	;PositionEntity l\mesh,0,0,-(l\seg_length*l\segments)*0.5
	ScaleEntity l\mesh,0.2,0.2,1

Next

Global sync=1 ; for v-sync

Dim random#(7,2)




While Not KeyHit(1)
	
	If KeyDown(203) Then RotateEntity piv,EntityPitch(piv),EntityYaw(piv)+1,EntityRoll(piv)
	If KeyDown(205) Then RotateEntity piv,EntityPitch(piv),EntityYaw(piv)-1,EntityRoll(piv)
	If KeyDown(200) Then RotateEntity piv,EntityPitch(piv)-1,EntityYaw(piv),EntityRoll(piv)
	If KeyDown(208) Then RotateEntity piv,EntityPitch(piv)+1,EntityYaw(piv),EntityRoll(piv)
	If KeyHit(57) Then sync=1-sync
	For l.bolt=Each bolt
		reset_rod(l\mesh,l\surf,l\segments,l\divisions,l\seg_length)
		randomise_rod(l\mesh,l\surf,l\segments,l\divisions)
		TurnEntity l\mesh,0,Rnd(0.1,2),0
	Next

	
	If MilliSecs()&lt;timer+1000 Then
								frame=frame+1
	Else
								fps=frame
								frame=0
								timer=MilliSecs()
	End If
	UpdateWorld
	RenderWorld
	Text 0,0,fps
	;Text 0,10," Press SPACE BAR to disable/enable v-sync"
	Flip sync
	;Delay 6
Wend
End


Function create_rod(mesh,surf,segments,divisions,segment_length)

	count=0

	For loop=0 To segments
	
		For loop1=1 To divisions

			AddVertex(surf,  Cos(loop1*(360/divisions))  ,  Sin(loop1*(360/divisions))  ,  loop*segment_length  )
			VertexColor surf,count,70,130,250,0.3
			count=count+1

		Next
	
	Next
	
	
	For loop=0 To segments
	
		For loop1=1 To divisions

			temp=AddVertex(surf,  (Cos(loop1*(360/divisions))/4)  ,  (Sin(loop1*(360/divisions))/4)  ,  loop*segment_length  )
			VertexColor surf,temp,70,130,250,0.7

		Next
	
	Next
		
	
	For loop=0 To segments-1
	
		For loop1=0 To divisions-1; must always be 1 less than divisions, because vertex index starts at 0
	
			If loop1&lt;divisions-1 Then
				tri=AddTriangle(surf,loop*divisions+loop1,(loop+1)*divisions+loop1+1,(loop+1)*divisions+loop1)
				tri1=AddTriangle(surf,loop*divisions+loop1,loop*divisions+loop1+1,(loop+1)*divisions+loop1+1)
			Else
				tri=AddTriangle(surf,loop*divisions+loop1,(loop+1)*divisions+0,(loop+1)*divisions+loop1)
				tri1=AddTriangle(surf,loop*divisions+loop1,loop*divisions+0,(loop+1)*divisions+0)
			End If
			
		Next
	Next
	
	For loop=0 To segments-1
	
		For loop1=0 To divisions-1; must always be 1 less than divisions, because vertex index starts at 0
	
			If loop1&lt;divisions-1 Then
				tri = AddTriangle(surf , count+ loop*divisions+loop1, count+ (loop+1)*divisions+loop1+1,	count+ (loop+1)*divisions+loop1)
				tri1= AddTriangle(surf , count+ loop*divisions+loop1, count+ loop*divisions+loop1+1,		count+ (loop+1)*divisions+loop1+1)
			Else
				tri = AddTriangle(surf , count+ loop*divisions+loop1, count+ (loop+1)*divisions+0,			count+ (loop+1)*divisions+loop1)
				tri1= AddTriangle(surf , count+ loop*divisions+loop1, count+ loop*divisions+0,				count+ (loop+1)*divisions+0)
			End If
	
		Next
	Next

	
	UpdateNormals(mesh)
	;ScaleMesh mesh,0.2,0.2,1
End Function

Function reset_rod(mesh,surf,segments,divisions,segment_length)

	count=0
	For loop=0 To segments
		For loop1=1 To divisions
			VertexCoords(surf,count, Cos(loop1*(360/divisions))  ,  Sin(loop1*(360/divisions))  ,  loop*segment_length  )
			count=count+1
		Next
	Next
	
	count1=0
	For loop=0 To segments
		For loop1=1 To divisions
			VertexCoords(surf,count+count1, (Cos(loop1*(360/divisions))/4)  ,  (Sin(loop1*(360/divisions))/4)  ,  loop*segment_length  )
			count1=count1+1
		Next
	Next


End Function

Function randomise_rod(mesh,surf,segments,divisions,spread=5)

	count=0
	For loop#=0 To segments
		
		random(loop,0)=Rnd(-spread,spread)
		random(loop,0)=random(loop,0) *(loop/segments) ; FOR X VERT. after the * sign, cause the tapering effect.
		
		random(loop,1)=Rnd(-spread,spread) ; FOR Y VERT
		random(loop,1)=random(loop,1) *(loop/segments)
		
		random(loop,2)=Rnd(-spread,spread) ; FOR Z VERT
		random(loop,2)=random(loop,2) *(loop/segments)
		
		For loop1=0 To divisions-1
			VertexCoords( surf,count,VertexX(surf,count)+(random(loop,0)), VertexY(surf,count)+(random(loop,1)), VertexZ(surf,count)+(random(loop,2)) ) 
			count=count+1
		Next
	Next
	
	count1=0
	For loop#=0 To segments
		
	
		For loop1=0 To divisions-1
			VertexCoords( surf,count+count1,VertexX(surf,count+count1)+(random(loop,0)), VertexY(surf,count+count1)+(random(loop,1)), VertexZ(surf,count+count1)+(random(loop,2)) ) 
			count1=count1+1
		Next
	Next


End Function
</textarea> <br><br></td></tr></table><br>
<a name="365185"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >aCiD2</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> any chance of a screenie ross boi :) <br><br></td></tr></table><br>
<a name="365191"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Done :) <br><br></td></tr></table><br>
<a name="365193"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >eBusiness</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Almost 700 fps at it's highest (gf3 ti 200, p4 1,8). But for a lightning, your effect could do with a retouch. <br><br></td></tr></table><br>
<a name="365198"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Explain please, i'm open to ideas to improve it :) <br><br></td></tr></table><br>
<a name="365201"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >eBusiness</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Most lightnings have forks, and unless it's a longlasting one (like those globes ya' know), it will not move, also normal lightnings usually have more kinks. But you seem to have the base, if you can add forks etc it will be awesome :) <br><br></td></tr></table><br>
<a name="365205"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I could find the end of a segment and generate a new bolt from that point... Thanks, i'll be back soon :) <br><br></td></tr></table><br>
<a name="365263"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Picklesworth</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> that looks good for electrical effects. <br><br></td></tr></table><br>
<a name="365279"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey again. I've made this so it now looks like real(ish) lightning. You can use this for whatever you want.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800,600
SetBuffer BackBuffer()
WireFrame True

Type bolt
	Field ID ; which bolt it belongs to
	Field x#,y#,z#
	Field mesh
	Field surf
	Field segments ; number of segments the cylinder has |===|===| = 2 segments
	Field divisions ; how smooth the cylinder looks
	Field seg_length#
	Field status; has been checked for possiblity of creating a branch
	Field timer
	Field time
End Type 

Type lightning
	Field ID
	Field number
	Field max_number
	Field time
	Field timer
End Type


Global piv=CreatePivot()
Global cam=CreateCamera(piv)
PositionEntity cam,0,5,-71
RotateEntity cam,30,0,0
CameraRange cam,0.1,100

Global light=CreateLight()
TurnEntity light,45,45,45


Global sync=1 ; for v-sync

Dim random#(7,2)


Global id=0
Global ltime=Rand(100,1000)
Global ltimer=MilliSecs()


While Not KeyHit(1)
	
	If KeyDown(203) Then RotateEntity piv,EntityPitch(piv),EntityYaw(piv)+1,EntityRoll(piv)
	If KeyDown(205) Then RotateEntity piv,EntityPitch(piv),EntityYaw(piv)-1,EntityRoll(piv)
	If KeyDown(200) Then RotateEntity piv,EntityPitch(piv)-1,EntityYaw(piv),EntityRoll(piv)
	If KeyDown(208) Then RotateEntity piv,EntityPitch(piv)+1,EntityYaw(piv),EntityRoll(piv)
	If KeyHit(57) Then sync=1-sync


	If KeyHit(2) Then generate_bolt()
	If MilliSecs()&gt;ltime+ltimer Then
		generate_bolt()
		ltimer=MilliSecs()
		ltime=Rand(100,1000)
	End If
	
	
	If MilliSecs()&lt;timer+1000 Then
								frame=frame+1
	Else
								fps=frame
								frame=0
								timer=MilliSecs()
	End If
	update_bolt()
	UpdateWorld
	RenderWorld
	Text 0,0,fps
	Text 0,10," Press SPACE BAR to disable/enable v-sync"
	Flip sync
	Delay 6
Wend
End

Function generate_bolt()
	b.lightning=New lightning
		id=id+1
		b\ID=id
		b\max_number=Rand(5,10)
		b\number=0
		b\time=10
		b\timer=MilliSecs()
			l.bolt=New bolt
				l\ID=b\ID
				l\x=Rand(-15,15)
				l\y=6
				l\z=Rand(-15,15)
				l\mesh=CreateMesh()
				l\surf=CreateSurface(l\mesh)
				l\segments=Rand(3,5)
				l\seg_length=Rand(7,7)
				l\divisions=3
				l\time=20
				l\timer=MilliSecs()

				create_rod(l\mesh,l\surf,l\segments,l\divisions,l\seg_length)
				;randomise_rod(l\mesh,l\surf,l\segments,l\divisions)

				EntityFX l\mesh,32+2
				PositionEntity l\mesh,l\x,l\y,l\z
				RotateMesh l\mesh,Rnd(70,110),0,0
				ScaleEntity l\mesh,0.2,1,0.2
				;EntityColor l\mesh,100,255,0

				temp_x=find_coord(l\mesh,l\surf,l\segments,l\divisions,l\seg_length,0)
				temp_y=find_coord(l\mesh,l\surf,l\segments,l\divisions,l\seg_length,1)
				temp_z=find_coord(l\mesh,l\surf,l\segments,l\divisions,l\seg_length,2)


End Function


Function update_bolt()

	temp_status=0
	
	For b.lightning= Each lightning
		If MilliSecs()&gt;b\time+b\timer Then
			If b\number &lt; b\max_number Then
			
				b\timer=MilliSecs()
				b\number=b\number+1; increase the number of bolts generated

				For l.bolt=Each bolt
					If l\id=b\id Then

						temp.bolt=l.bolt
						If l\status=1 Then temp_status=1
						If l\status=0 Then ; bolt has not been checked to generate more bolts
						
							If Rand(0,9)=&gt;0 Then ; random if any bolts are generated
								temp_bolt=0
								temp_status=0
								
								For loop=1 To Rand(2,3) ; how many are generated
									temp_bolt=temp_bolt+1

									l.bolt=temp.bolt ; make sure the type handle is the same is it started
									temp_x=find_coord(l\mesh,l\surf,l\segments,l\divisions,l\seg_length,0)
									temp_y=find_coord(l\mesh,l\surf,l\segments,l\divisions,l\seg_length,1)
									temp_z=find_coord(l\mesh,l\surf,l\segments,l\divisions,l\seg_length,2)

									l.bolt=New bolt
										l\ID=b\ID
										l\x=temp_x
										l\y=temp_y
										l\z=temp_z
										l\mesh=CreateMesh()
										l\surf=CreateSurface(l\mesh)
										l\segments=Rand(3,5)
										l\seg_length=Rand(5,7)
										l\divisions=3
										l\time=0
										l\timer=MilliSecs()
									
									create_rod(l\mesh,l\surf,l\segments,l\divisions,l\seg_length)
									;randomise_rod(l\mesh,l\surf,l\segments,l\divisions)

									EntityFX l\mesh,32+2
									PositionEntity l\mesh,l\x,l\y,l\z
									ScaleEntity l\mesh,0.2,1,0.2
									RotateMesh l\mesh,Rnd(30,150),Rnd(30,150),Rnd(30,150)
								Next
								
								DebugLog(" no. of bolts="+temp_bolt)
						
							End If
							
							l.bolt=temp.bolt
							l\status=1 ; has been checked
						End If
					End If
					If l\status=1 And temp_status=0 Then Exit ; if lightning bolts have been generated then exit the loop
				Next
							
				
			ElseIf b\number =&gt; b\max_number Then
			
				delete_rods(b\ID)
				Delete b.lightning
				
			End If
		End If
	Next
End Function

Function find_coord(mesh,surf,segments,divisions,segment_length,coord)
	find_seg=segments-1
	count=0
	For loop=0 To segments
		For loop1=1 To divisions
			VertexCoords( surf,count,VertexX(surf,count)+(random(loop,0)), VertexY(surf,count)+(random(loop,1)), VertexZ(surf,count)+(random(loop,2)) ) 
			count=count+1
		Next
	Next

	count1=0
	For loop=0 To segments
		For loop1=1 To divisions
			VertexCoords( surf,count+count1,VertexX(surf,count+count1)+(random(loop,0)), VertexY(surf,count+count1)+(random(loop,1)), VertexZ(surf,count+count1)+(random(loop,2)) ) 
			count1=count1+1
		Next
		If find_seg=loop Then
			TFormPoint VertexX(surf,count+count1), VertexY(surf,count+count1), VertexZ(surf,count+count1), mesh, 0
			If coord=0 Then
				Return TFormedX() ; Point x#, y#, z#, source_entity, dest_entity(EntityX(mesh)+VertexX(surf,count+count1))
			ElseIf coord=1 Then
				Return TFormedY() ; (EntityY(mesh)+VertexY(surf,count+count1))
			ElseIf coord=2 Then
				Return TFormedZ() ; (EntityZ(mesh)+VertexZ(surf,count+count1))
			End If
		End If
	Next
End Function

Function delete_rods(id)
	For l.bolt=Each bolt
		If l\ID=id Then
			FreeEntity l\mesh
			Delete l.bolt
		End If
	Next
End Function

Function create_rod(mesh,surf,segments,divisions,segment_length)

	count=0

	For loop=0 To segments
	
		For loop1=1 To divisions

			AddVertex(surf,  Cos(loop1*(360/divisions))  ,  Sin(loop1*(360/divisions))  ,  loop*segment_length  )
			VertexColor surf,count,255,255,255,0.2;70,130,250,0.3
			count=count+1

		Next
	
	Next
	
	
	For loop=0 To segments
	
		For loop1=1 To divisions

			temp=AddVertex(surf,  (Cos(loop1*(360/divisions))/6)  ,  (Sin(loop1*(360/divisions))/6)  ,  loop*segment_length  )
			VertexColor surf,temp,255,255,255,0.5;70,130,250,0.7

		Next
	
	Next
		
	
	For loop=0 To segments-1
	
		For loop1=0 To divisions-1; must always be 1 less than divisions, because vertex index starts at 0
	
			If loop1&lt;divisions-1 Then
				tri=AddTriangle(surf,loop*divisions+loop1,(loop+1)*divisions+loop1+1,(loop+1)*divisions+loop1)
				tri1=AddTriangle(surf,loop*divisions+loop1,loop*divisions+loop1+1,(loop+1)*divisions+loop1+1)
			Else
				tri=AddTriangle(surf,loop*divisions+loop1,(loop+1)*divisions+0,(loop+1)*divisions+loop1)
				tri1=AddTriangle(surf,loop*divisions+loop1,loop*divisions+0,(loop+1)*divisions+0)
			End If
			
		Next
	Next
	
	For loop=0 To segments-1
	
		For loop1=0 To divisions-1; must always be 1 less than divisions, because vertex index starts at 0
	
			If loop1&lt;divisions-1 Then
				tri = AddTriangle(surf , count+ loop*divisions+loop1, count+ (loop+1)*divisions+loop1+1,	count+ (loop+1)*divisions+loop1)
				tri1= AddTriangle(surf , count+ loop*divisions+loop1, count+ loop*divisions+loop1+1,		count+ (loop+1)*divisions+loop1+1)
			Else
				tri = AddTriangle(surf , count+ loop*divisions+loop1, count+ (loop+1)*divisions+0,			count+ (loop+1)*divisions+loop1)
				tri1= AddTriangle(surf , count+ loop*divisions+loop1, count+ loop*divisions+0,				count+ (loop+1)*divisions+0)
			End If
	
		Next
	Next

	
	UpdateNormals(mesh)
	;ScaleMesh mesh,0.2,0.2,1
End Function

Function reset_rod(mesh,surf,segments,divisions,segment_length)

	count=0
	For loop=0 To segments
		For loop1=1 To divisions
			VertexCoords(surf,count, Cos(loop1*(360/divisions))  ,  Sin(loop1*(360/divisions))  ,  loop*segment_length  )
			count=count+1
		Next
	Next
	
	count1=0
	For loop=0 To segments
		For loop1=1 To divisions
			VertexCoords(surf,count+count1, (Cos(loop1*(360/divisions))/4)  ,  (Sin(loop1*(360/divisions))/4)  ,  loop*segment_length  )
			count1=count1+1
		Next
	Next


End Function

Function randomise_rod(mesh,surf,segments,divisions,spread=5)

	count=0
	For loop#=0 To segments
		
		random(loop,0)=Rnd(-spread,spread)
		random(loop,0)=random(loop,0) *(loop/segments) ; FOR X VERT. after the * sign, cause the tapering effect.
		
		random(loop,1)=Rnd(-spread,spread) ; FOR Y VERT
		random(loop,1)=random(loop,1) *(loop/segments)
		
		random(loop,2)=Rnd(-spread,spread) ; FOR Z VERT
		random(loop,2)=random(loop,2) *(loop/segments)
		
		For loop1=0 To divisions-1
			VertexCoords( surf,count,VertexX(surf,count)+(random(loop,0)), VertexY(surf,count)+(random(loop,1)), VertexZ(surf,count)+(random(loop,2)) ) 
			count=count+1
		Next
	Next
	
	count1=0
	For loop#=0 To segments
		
	
		For loop1=0 To divisions-1
			VertexCoords( surf,count+count1,VertexX(surf,count+count1)+(random(loop,0)), VertexY(surf,count+count1)+(random(loop,1)), VertexZ(surf,count+count1)+(random(loop,2)) ) 
			count1=count1+1
		Next
	Next


End Function
</textarea> <br><br></td></tr></table><br>
<a name="365285"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >eBusiness</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Lighnings usually just appear and disapear, they don't build up this way (yes, he can continue complaining for an eternity ;) <br><br></td></tr></table><br>
<a name="365319"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah, i've got a few things too clean up :) <br><br></td></tr></table><br>
<a name="365358"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Paolo</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> SWEET.<br><br>Actually I add a EntityBblend l\mesh,3 and I like it more ;)<br><br>Paolo. <br><br></td></tr></table><br>
<a name="365431"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >aCiD2</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> awesome mann..... perfect for when i stop time in my game :) <br><br></td></tr></table><br>
<a name="365444"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SopiSoft</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> very cool!!! :-) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
