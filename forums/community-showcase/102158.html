<!DOCTYPE html><html lang="en" ><head ><title >ByteBuster8k (8k code)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >ByteBuster8k (8k code)</h1><a href="forums.php" >Community Forums</a>/<a href="topics.php?forum=61" >Showcase</a>/<a href="#bottom" >ByteBuster8k (8k code)</a><br><br>
<a name="1218393"></a>

<a name="1218488"></a>

<a name="1218489"></a>

<a name="1218491"></a>

<a name="1218494"></a>

<a name="1218495"></a>

<a name="1218496"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mr. Goober</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> <img src="http://dl.dropboxusercontent.com/u/14221897/images/ByteBuster8k.png"><br><br>PLAYABLE BUILD:<br><a href="https://dl.dropboxusercontent.com/u/14221897/files/Experiments/ByteBuster8k.exe" target="_blank">https://dl.dropboxusercontent.com/u/14221897/files/Experiments/ByteBuster8k.exe</a><br><br>SOURCE CODE:<br><a href="https://dl.dropboxusercontent.com/u/14221897/files/Experiments/ByteBuster8k.bmx" target="_blank">https://dl.dropboxusercontent.com/u/14221897/files/Experiments/ByteBuster8k.bmx</a><br><br><img src="http://i.creativecommons.org/l/by/3.0/88x31.png"> Licensed under Creative Commons. Feel free to play with the source.<br><br>EDIT: There's no way I'm gonna get this under 4096 bytes. After taking out the unfinished sound part, the source sits at 8043 bytes, under 8196, which beats the 8k challenge. There's still two more things I want to add though: enemies shooting back, and scaling difficulty. <br><br></td></tr></table><br>
<a name="1218394"></a>

<a name="1218395"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mr. Goober</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Source code at the time I made this topic. No external images or media were used in creating this. You can plug this straight into BlitzMax and it plays:<br><pre class=code>
SuperStrict
'-----------------------------
SetGraphicsDriver(GLMax2DDriver())
AppTitle = "ByteDestroyer"
Graphics(640,480)
SetVirtualResolution(160,120)


Global _colors		:Int[]			= [0,64,128,255]
Global _sprites		:t_sprite[]		= New t_sprite[10]
Global _spcounter	:Byte			= 0
Global _pship		:t_entity		= New t_entity
Global _sound		:TSound			= Null
Global _soundtime	:Byte			= 10
Global _sndchannel	:TChannel		= Null
Global _starscroll	:Float			= 0.00
Global _key_move	:Byte			= False
Global _key_space	:Byte			= False
Global _bullet		:t_entity		= New t_entity
Global _ebullets	:t_entity[]		= New t_entity[3]
Global _enemies		:t_entity[]		= New t_entity[10]
Global _score		:Float			= 0

_Init()
_MainLoop()

Type t_sprite
	Field data :Byte[,] = New Byte[10,10]
	Method Draw(X:Float=0,Y:Float=0)
		Local c:Byte=0
		For Local i:Byte=0 To 9
			For Local j:Byte=0 To 9
				c=_colors[data[i,j]]
				If (c&gt;0)
					SetColor(c,c,c)
					DrawRect(i+X,j+Y,1,1)
				EndIf
			Next
		Next
	EndMethod
	Method SetData()
		For Local j:Byte=0 To 9
			For Local i:Byte=0 To 9
				ReadData data[i,j]
			Next
		Next
	EndMethod
EndType

Type t_entity
	Field pos_x 	:Float 	= 0.00
	Field pos_y 	:Float 	= 0.00
	Field sprite	:Byte	= 0
	Field life		:Byte	= 3
	Field bullets	:Byte	= 3
	Field flag_alive:Byte	= False
	Method MovePos(X:Float=0,Y:Float=0)
		pos_x :+ X
		pos_y :+ Y
	EndMethod
	Method Draw()
		If (flag_alive)
			_sprites[sprite].Draw(Floor(pos_x),Floor(pos_y))
		EndIf
	EndMethod
EndType

Function _DrawSprites()
	For Local i:Byte = 0 To _spcounter-1
		_sprites[i].Draw(0,0)
	Next
EndFunction

Function _CreateSprite()
	Local r:t_sprite = New t_sprite
	r.SetData()
	_sprites[_spcounter] = r
	_spcounter :+ 1
EndFunction

Function _DrawSprite(N:Byte=0,X:Float=0.00,Y:Float=0.00)
	_sprites[N].Draw(X,Y)
EndFunction

Function _Init()
	RestoreData G_Ship
	_CreateSprite()
	RestoreData G_Stars
	_CreateSprite()
	RestoreData G_Heart1
	_CreateSprite()
	RestoreData G_Enemy
	_CreateSprite()
	RestoreData G_Bullet
	_CreateSprite()
	_MakeSound()
	_pship.sprite = 0
	_pship.MovePos(75,100)
	_pship.life	= 3
	_pship.flag_alive = True
	_bullet.sprite = 4
	SetClsColor(16,16,16)
	SetVirtualResolution(160,120)
EndFunction

Function _MainLoop()
	While (KeyDown(KEY_ESCAPE) = False)
		Cls
		_UpdateShip()
		_UpdateBullet()
		_starscroll :+ 0.12
		If (_starscroll &gt;= 10)
			_starscroll :- 10
		EndIf
		_DrawStars(_starscroll)
		_bullet.Draw()
		_pship.Draw()
		SetColor(0,0,0)
		DrawRect(0,110,160,10)
		SetColor(255,255,255)
		For Local i:Byte = 0 To _pship.life-1
			_DrawSprite(2,i*10,110)
		Next
		SetColor(255,255,255)
		_score :+ 0.03
		DrawText(RSet(String(Int(_score)),6),110,108)
		_DrawScanLines()
		Flip
	Wend
EndFunction

Function _DrawStars(X:Float)
	For Local i:Byte = 0 To 15
		For Local j:Byte = 0 To 11
			_DrawSprite(1,i*10,Floor(X+(j*10)-10))
		Next
	Next
EndFunction

Function _MakeSound()
	Local _s :TAudioSample = CreateAudioSample(64,16640,SF_MONO8)
	For Local i:Byte = 0 To 63
		If (i &lt;= 31)
			_s.samples[i] = 160
		Else
			_s.samples[i] = 96
		EndIf
	Next
	_sound = LoadSound(_s,True)
EndFunction

Function _DrawScanLines()
	SetVirtualResolution(640,480)
	SetColor(0,0,0)
	For Local i:Byte = 0 To 159
		DrawLine(i*4,0,i*4,480)
		If (i &lt;= 119)
			DrawLine(0,i*4,640,i*4)
		EndIf
	Next
	SetColor(255,255,255)
	SetVirtualResolution(160,120)
EndFunction

Function _CheckKeys()
	_key_move = False
	If (KeyDown(KEY_RIGHT))
		_key_move = 2
	Else If (KeyDown(KEY_LEFT))
		_key_move = 1
	EndIf
	_key_space = False
	If (KeyDown(KEY_SPACE))
		_key_space = True
	EndIf
EndFunction

Function _UpdateShip()
	_CheckKeys()
	If (_key_move &gt; 0)
		If (_key_move = 2)
			If (_pship.pos_x &lt; 150)
				_pship.MovePos(1,0)
			EndIf
		Else If (_key_move = 1)
			If (_pship.pos_x &gt; 0)
				_pship.MovePos(-1,0)
			EndIf
		EndIf
	EndIf
	If (_key_space)
		If (_bullet.flag_alive = False)
			_bullet.flag_alive = True
			_bullet.pos_x = _pship.pos_x
			_bullet.pos_y = _pship.pos_y
		EndIf
	EndIf
EndFunction

Function _UpdateBullet()
	If (_bullet.flag_alive)
		_bullet.MovePos(0,-3)
		If (_bullet.pos_y &lt; -10)
			_bullet.flag_alive = False
		EndIf
	EndIf
EndFunction

#G_Ship
DefData 0,0,0,0,2,2,0,0,0,0
DefData 0,0,0,0,2,2,0,0,0,0
DefData 0,0,0,2,3,3,2,0,0,0
DefData 0,0,0,2,3,3,2,0,0,0
DefData 0,0,2,3,0,0,3,2,0,0
DefData 0,0,2,3,1,1,3,2,0,0
DefData 2,2,2,2,3,3,2,2,2,2
DefData 2,0,2,2,2,2,2,2,0,2
DefData 2,1,2,0,2,2,0,2,1,2
DefData 0,1,0,0,0,0,0,0,1,0
#G_Stars
DefData 0,1,0,0,0,0,0,0,0,0
DefData 0,0,0,0,0,0,0,1,0,0
DefData 0,0,0,1,0,0,0,0,0,0
DefData 0,0,0,0,0,0,0,0,0,0
DefData 0,0,0,0,0,0,0,0,1,0
DefData 0,1,0,0,1,0,0,0,0,0
DefData 0,0,0,0,0,0,0,0,0,0
DefData 0,0,0,1,0,0,0,0,0,0
DefData 1,0,0,0,0,0,0,1,0,0
DefData 0,0,0,0,0,0,0,0,0,0
#G_Heart1
DefData 0,0,0,0,0,0,0,0,0,0
DefData 0,0,1,1,0,0,1,1,0,0
DefData 0,1,2,2,1,1,2,2,1,0
DefData 0,1,2,2,2,2,3,2,1,0
DefData 0,1,2,2,2,2,3,2,1,0
DefData 0,1,1,2,2,2,2,1,1,0
DefData 0,0,1,1,2,2,1,1,0,0
DefData 0,0,0,1,1,1,1,0,0,0
DefData 0,0,0,0,1,1,0,0,0,0
DefData 0,0,0,0,0,0,0,0,0,0
#G_Enemy
DefData 0,0,0,0,0,0,0,0,0,0
DefData 0,0,2,2,0,0,2,2,0,0
DefData 0,0,2,1,2,2,1,2,0,0
DefData 0,0,2,3,2,2,3,2,0,0
DefData 0,1,2,2,1,1,2,2,1,0
DefData 0,2,2,0,2,2,0,2,2,0
DefData 0,2,1,0,0,0,0,1,2,0
DefData 0,2,0,0,0,0,0,0,2,0
DefData 0,2,1,0,0,0,0,1,2,0
DefData 0,0,2,0,0,0,0,2,0,0
#G_Bullet
DefData 0,0,0,0,2,2,0,0,0,0
DefData 0,0,0,2,3,3,2,0,0,0
DefData 0,0,0,2,3,3,2,0,0,0
DefData 0,0,0,2,3,3,2,0,0,0
DefData 0,0,1,2,3,3,2,1,0,0
DefData 0,0,1,2,3,3,2,1,0,0
DefData 0,0,0,2,3,3,2,0,0,0
DefData 0,0,0,2,3,3,2,0,0,0
DefData 0,0,0,2,3,3,2,0,0,0
DefData 0,0,0,0,2,2,0,0,0,0
</pre> <br><br></td></tr></table><br>
<a name="1218401"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> What you can do:<br><br>- shorten variable names (flag_alive -&gt; alive, _bullet = b, ...)<br>- replace ":byte" ":int" ":string" with their shortcuts ($ etc.)<br>- remove "for local i:int =" with "for i =" and declare 2 array-walking-variables as global<br>- replace TRUE/FALSE with 1/0<br>- shorten function/method names<br>- replace const KEY_SOMETHING with their value<br>- replace multiple occurencies:<br><br>RestoreData VAR<br>_CreateSprite()<br>...<br>with a function _CreateSprite(somethingToAccess#Label), dunno if this is possible.<br><br><br>Hope you do not mind if you already knew this things. Just wanted to assist.<br>BTW: I like that "pixelated" look of the "sprites".<br><br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="1218402"></a>

<a name="1218403"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >big10p</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I like the style. Good programming exercise, too. You've set yourself a pretty tough goal at only 4K, or is it for a compo?<br><br>There are a lot of repeated numbers in your data (especially zeros) so I guess you could grab a few bytes back by employing simple data compression, such as RLE?<br><br>[edit]<br>@Derron: None of those things will make a difference since I assume he's talking about a 4K exe, not source file. <br><br></td></tr></table><br>
<a name="1218404"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GaryV</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Can anybody repost the screenshot (dropbox doesn't work for me)? <br><br></td></tr></table><br>
<a name="1218405"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >big10p</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Can anybody repost the screenshot (dropbox doesn't work for me)? <br></div>No probs:<br><img src="http://s20.postimg.org/j5e0ao7al/Byte_Buster.gif"> <br><br></td></tr></table><br>
<a name="1218407"></a>

<a name="1218408"></a>

<a name="1218410"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> @Derron: None of those things will make a difference since I assume he's talking about a 4K exe, not source file. <br></div><br><br><div class="quote"> I'm trying to make a game in under <b>4096 bytes (chars) of code</b> <br></div><br><br><br>BTW: I thought about compressing that defdata too - but I assume that further added "sprites" might be with less zeros. Coding the compress/uncompress might add more overhead than without.<br><br><br><pre class=code>
	If (_key_move &gt; 0)
		If (_key_move = 2)
			If (_pship.pos_x &lt; 150)
				_pship.MovePos(1,0)
			EndIf
		Else If (_key_move = 1)
			If (_pship.pos_x &gt; 0)
				_pship.MovePos(-1,0)
			EndIf
		EndIf
	EndIf
</pre><br><br>Maybe shorten this:<br><pre class=code>
If (_key_move = 2 &amp;&amp; _pship.pos_x &lt; 150) then _pship.MovePos(1,0)

If (_key_move = 1) &amp;&amp; _pship.pos_x &gt; 0) then _pship.MovePos(-1,0)
</pre><br><br>or shorten it more with assigning the direction to _key_move (1 or -1)<br>and clamping the x-position within MovePos() using pos_x = Min(Max(0,pos_x),150)<br><br>EDIT:<br><pre class=code>
Type t_entity
...
	Method MovePos(X:Float=0,Y:Float=0)
		pos_x :+ X
		pos_y :+ Y
		pos_x = Min(Max(0,pos_x),150)
	EndMethod
...
End Type

Function _CheckKeys()
	_key_move = KeyDown(KEY_RIGHT) '0 or 1
	_key_move :- KeyDown(KEY_LEFT)  '0 if both pressed, -1 if only left, 1 if only right

	_key_space = KeyDown(KEY_SPACE)
EndFunction

Function _UpdateShip()
	_CheckKeys()
	_pship.MovePos(_key_move)
...
End function
</pre><br><br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="1218409"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >big10p</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Derron, exe's contain code! These exercises usually refer to compiled code as there is no benefit to just shortening variable names etc., making the source just less understandable.<br><br><div class="quote"> BTW: I thought about compressing that defdata too - but I assume that further added "sprites" might be with less zeros. Coding the compress/uncompress might add more overhead than without. <br></div>Since the zeros represent transparent pixels, other sprites are likely to contain just as many. Also, there's no need for a compression routine as you'd just enter the ready compressed data into the data statements. <br><br></td></tr></table><br>
<a name="1218411"></a>

<a name="1218412"></a>

<a name="1218413"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> If the exercise is to build a binary of a specific size I assume writing in pure ASM is more efficient - or shortening the output of the "pre-compilate".<br><br>As the sourcecode is ~5.8kb and the author states a code size of 5844 I am quite sure that his own "task" is to code something with less than 4096 characters in source code (means he should use unix line endings :D).<br><br>Concerning compression: I did not get what you want to say - could you explain a bit more? Somewhere the decompression has to take place - coding this means adding code which adds bytes to the sourcecode/binaryfile.<br><br><br>Another "shortening" possibility:<br><pre class=code>
		If (_bullet.pos_y &lt; -10)
			_bullet.flag_alive = False
		EndIf
</pre><br><br>Like with KeyDown/KeyHit you can use the "boolean" not just for multiplying (1,0) but also as a simple return value:<br><pre class=code>
		_bullet.flag_alive = (_bullet.pos_y &lt; -10)
</pre><br><br>EDIT: you use "SetVirtualResolution" 2 times. Think there is a lot of space to free for the longthy awaited enemies.<br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="1218414"></a>

<a name="1218449"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >big10p</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Concerning compression: I did not get what you want to say - could you explain a bit more? Somewhere the decompression has to take place - coding this means adding code which adds bytes to the sourcecode/binaryfile. <br></div>It needs a function to decompress the data, but not one to compress it - that can be done separately and the resulting data added by hand into the data statements.<br><br>RLE compression is very basic so the function to decompress it is short. Part of the exercise is to see if the overhead of a decompression function is outweighed by the bytes saved by using the data compression. <br><br></td></tr></table><br>
<a name="1218415"></a>

<a name="1218416"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ahh Okay... you got me :D<br>Just ignored that "compression" part, yeah you are absolutely right, this can be handmade/precalculated.<br><br>I never used that defdata-part so I do not know if you somehow could fill defdata with other functions (output of decompression).<br>Else you would end up having to: a) decode the datastream b) fill the useData-array.<br><br>Maybe it is even shorter to Draw directly from the compressed stream. If you assume a "fixed" width of a sprite you could "mod" the y-coordinate.<br><br>Edit: Using the "per sprite"-array includes the possibility for "explosion"-effects (dissolving aka "reset pixel to black") should be easily done as it does not include new properties (vectors, positions...).<br><br><br>There are plenty of optimizations possible: "DrawRect" -&gt; "Plot" (-4 Bytes :D).<br><br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="1218421"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Steve Elliott</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Interesting concept and nice graphic style :) <br><br></td></tr></table><br>
<a name="1218442"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GaryV</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Love the screenshot, nice style.<br><br>Thanks, big10p! <br><br></td></tr></table><br>
<a name="1218448"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hotshot2005</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> this is really excellent pixel style and good learn from code you made too :) <br><br></td></tr></table><br>
<a name="1218453"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Love the look of this! <br><br></td></tr></table><br>
<a name="1218464"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> You could do pseudo random for the stars and simple formulas for the look of some of the sprites to avoid the "images" in the code. Could save up size and offer addition to even more elements with calls to the same formulas with different parameters.<br><br>Love the look, too. Very convincing! 8D ...and I'm a total geek for keeping compiles small. But it's VERY tough with Blitzmax, because you kinda have to use mods without going crazy, I think, and they sure are "wasteful" beyond what you may need. <br><br></td></tr></table><br>
<a name="1218474"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mr. Goober</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> Wow, I didn't think that this would pick up as much attention as it did. Thanks for the positive feedback everyone.<br><br>@Derron:<br>Yeah, there's a lot of optimization I can do in regards to your post about shortening variable names, etc. I'll have to do that once I finish the game. Right now I want to make sure that the entire project is completed before I make source code a bit more compressed.<br><br>@big10p:<br>Nah, this isn't for a compo. It's just something that I wanted to do to challenge myself. The goal isn't about a smaller EXE. It's simply limiting myself to 4096 characters of source code. I've added some new stuff since the screenshot, and I'm already at a huge 7,544 bytes (8192 is the next limit). The last thing I need to program in is the sound, and collision from enemies so you lose lives, and eventually, a game over.<br><br>@Derron (post #7):<br>I'm probably going to keep the DefData the same. It would be too much of a mess for me to do it another way, unless it's more useful to use array pairs ([0,0,3] would put a bright dot on coordinate 0,0), but this would be debatable if the sprite happened to have a lot of nonzero pixels.<br><br>It would be beneficial to implement a Clamp() function in the source code to shorten up the rest of the code, indeed. I'll mark that as something to do. I'll also take out SuperStrict mode after it's all done and resort to using sigils for data types instead.<br><br>I also tried to use Plot over DrawRect, but apparently Plot is not affected by the VirtualResolution. Plot seems to always plot just one pixel and not scale either way.<br><br>@Hotshot2005/BlitzSupport:<br>I appreciate it. I'm glad to see that staff from Blitz Research are also intrigued by my progress. Hotshot, feel free to use the source however you like. There's some unfinished sound code which produces a square wave which I intend to use for most of the game's sound, but currently it isn't implemented.<br><br>@Taron:<br>Yeah, I'd need to tell BlitzMax not to include all of the modules by default. When the game is completed, I can focus more on keeping the EXE file small. I thought about Incbin'ing some external files, but that would sort of take away the beauty of just plugging in the code and playing it. Generally modules will take up about 2-3MB of space on an empty project depending on what sort of build you use.<br><br><br>PROGRESS REPORT:<br><br><img src="http://puu.sh/75sv0"><br><br><pre class=code>
SuperStrict
'-----------------------------
SetGraphicsDriver(GLMax2DDriver())
AppTitle = "ByteBuster 4k"
Graphics(640,480)
SetVirtualResolution(160,120)

Const  ENMOVE_AMT	:Byte			= 1

Global _colors		:Int[]			= [0,64,128,255]
Global _sprites		:t_sprite[]		= New t_sprite[10]
Global _spcounter	:Byte			= 0
Global _pship		:t_entity		= New t_entity
Global _sound		:TSound			= Null
Global _soundtime	:Byte			= 10
Global _sndchannel	:TChannel		= Null
Global _starscroll	:Float			= 0.00
Global _key_move	:Byte			= False
Global _key_space	:Byte			= False
Global _bullet		:t_entity		= New t_entity
Global _ebullets	:t_entity[]		= New t_entity[3]
Global _enemies		:t_entity[]		= New t_entity[10]
Global _encounter	:Byte			= 0
Global _score		:Float			= 0
Global _lastlife	:Float			= 10000


_Init()
_MainLoop()

Type t_sprite
	Field data :Byte[,] = New Byte[10,10]
	Method Draw(X:Float=0,Y:Float=0)
		Local c:Byte=0
		For Local i:Byte=0 To 9
			For Local j:Byte=0 To 9
				c=_colors[data[i,j]]
				If (c&gt;0)
					SetColor(c,c,c)
					DrawRect(i+X,j+Y,1,1)
				EndIf
			Next
		Next
	EndMethod
	Method SetData()
		For Local j:Byte=0 To 9
			For Local i:Byte=0 To 9
				ReadData data[i,j]
			Next
		Next
	EndMethod
EndType

Type t_entity
	Field pos_x 	:Float 	= 0.00
	Field pos_y 	:Float 	= 0.00
	Field sprite	:Byte	= 0
	Field life		:Byte	= 3
	Field bullets	:Byte	= 3
	Field flag_alive:Byte	= False
	Field enmove	:Float	= 0
	Method MovePos(X:Float=0,Y:Float=0)
		pos_x :+ X
		pos_y :+ Y
	EndMethod
	Method Draw()
		If (flag_alive)
			_sprites[sprite].Draw(Floor(pos_x),Floor(pos_y))
		EndIf
	EndMethod
EndType

Function _DrawSprites()
	For Local i:Byte = 0 To _spcounter-1
		_sprites[i].Draw(0,0)
	Next
EndFunction

Function _CreateSprite()
	Local r:t_sprite = New t_sprite
	r.SetData()
	_sprites[_spcounter] = r
	_spcounter :+ 1
EndFunction

Function _DrawSprite(N:Byte=0,X:Float=0.00,Y:Float=0.00)
	_sprites[N].Draw(X,Y)
EndFunction

Function _Init()
	RestoreData G_Ship
	_CreateSprite()
	RestoreData G_Stars
	_CreateSprite()
	RestoreData G_Heart1
	_CreateSprite()
	RestoreData G_Enemy
	_CreateSprite()
	RestoreData G_Bullet
	_CreateSprite()
	_MakeSound()
	For Local i:Byte = 0 To 9
		_enemies[i] = New t_entity
	Next
	_pship.sprite = 0
	_pship.MovePos(75,100)
	_pship.life	= 3
	_pship.flag_alive = True
	_bullet.sprite = 4
	SetClsColor(16,16,16)
	SetVirtualResolution(160,120)
EndFunction

Function _MainLoop()
	While (KeyDown(KEY_ESCAPE) = False) And (AppTerminate() = False)
		Cls
		_UpdateShip()
		_UpdateEnemies()
		_UpdateBullet()
		_starscroll :+ 0.12
		If (_starscroll &gt;= 10)
			_starscroll :- 10
		EndIf
		If (Rand(1,100) &gt; 95)
			_CreateEnemy()
		EndIf
		_DrawStars(_starscroll)
		For Local i:Byte = 0 To 9
			_enemies[i].Draw()
		Next
		_bullet.Draw()
		_pship.Draw()
		SetColor(0,0,0)
		DrawRect(0,110,160,10)
		SetColor(255,255,255)
		For Local i:Byte = 0 To _pship.life-1
			_DrawSprite(2,i*10,110)
		Next
		SetColor(255,255,255)
		_score :+ 0.03
		If (_score &gt;= _lastlife)
			_lastlife :+ 10000
			_pship.life :+ 1
		EndIf
		DrawText(RSet(String(Int(_score)),6),110,108)
		_DrawScanLines()
		Flip
	Wend
EndFunction

Function _DrawStars(X:Float)
	For Local i:Byte = 0 To 15
		For Local j:Byte = 0 To 11
			_DrawSprite(1,i*10,Floor(X+(j*10)-10))
		Next
	Next
EndFunction

Function _MakeSound()
	Local _s :TAudioSample = CreateAudioSample(64,16640,SF_MONO8)
	For Local i:Byte = 0 To 63
		If (i &lt;= 31)
			_s.samples[i] = 160
		Else
			_s.samples[i] = 96
		EndIf
	Next
	_sound = LoadSound(_s,True)
EndFunction

Function _DrawScanLines()
	SetVirtualResolution(640,480)
	SetColor(0,0,0)
	For Local i:Byte = 0 To 159
		DrawLine(i*4,0,i*4,480)
		If (i &lt;= 119)
			DrawLine(0,i*4,640,i*4)
		EndIf
	Next
	SetColor(255,255,255)
	SetVirtualResolution(160,120)
EndFunction

Function _CheckKeys()
	_key_move = False
	If (KeyDown(KEY_RIGHT))
		_key_move = 2
	Else If (KeyDown(KEY_LEFT))
		_key_move = 1
	EndIf
	_key_space = False
	If (KeyDown(KEY_SPACE))
		_key_space = True
	EndIf
EndFunction

Function _UpdateShip()
	_CheckKeys()
	If (_key_move &gt; 0)
		If (_key_move = 2)
			If (_pship.pos_x &lt; 150)
				_pship.MovePos(1,0)
			EndIf
		Else If (_key_move = 1)
			If (_pship.pos_x &gt; 0)
				_pship.MovePos(-1,0)
			EndIf
		EndIf
	EndIf
	If (_key_space)
		If (_bullet.flag_alive = False)
			_bullet.flag_alive = True
			_bullet.pos_x = _pship.pos_x
			_bullet.pos_y = _pship.pos_y
		EndIf
	EndIf
EndFunction

Function _UpdateBullet()
	If (_bullet.flag_alive)
		_bullet.MovePos(0,-3)
		For Local i:Byte = 0 To 9
			If (_enemies[i].flag_alive)
				If (_PointInRect(_bullet.pos_x+5,_bullet.pos_y,_enemies[i].pos_x,_enemies[i].pos_y,_enemies[i].pos_x+10,_enemies[i].pos_y+10))
					_enemies[i].flag_alive = False
					_bullet.flag_alive = False
					_score :+ 100
				EndIf
			EndIf
		Next
		If (_bullet.pos_y &lt; -10)
			_bullet.flag_alive = False
		EndIf
	EndIf
EndFunction

Function _UpdateEnemies()
	For Local i:Byte = 0 To 9
		If (_enemies[i].flag_alive)
			_enemies[i].MovePos(0,1)
			_enemies[i].enmove :+ 1
			If (_enemies[i].enmove &gt;= 360)
				_enemies[i].enmove = 0
			EndIf
			_enemies[i].MovePos(ENMOVE_AMT*Sin(_enemies[i].enmove),0)
			If (_enemies[i].pos_y &gt; 110)
				_enemies[i].flag_alive = False
			EndIf
		EndIf
	Next
EndFunction

Function _CreateEnemy()
	If (_enemies[_encounter].flag_alive = False)
		_enemies[_encounter].flag_alive = True
		_enemies[_encounter].pos_x		= Rand(0,150)
		_enemies[_encounter].pos_y		= -10
		_enemies[_encounter].sprite		= 3
		_enemies[_encounter].enmove		= Rand(0,360)
		_encounter :+ 1
		If (_encounter &gt; 9)
			_encounter = 0
		EndIf
	EndIf
EndFunction

Function _PointInRect :Int(X:Float, Y:Float, X1:Float, Y1:Float, X2:Float, Y2:Float)
	If (X &gt;= X1) And (X &lt;= X2) And (Y &gt;= Y1) And (Y &lt;= Y2)
		Return True
	EndIf
	Return False
EndFunction

#G_Ship
DefData 0,0,0,0,2,2,0,0,0,0
DefData 0,0,0,0,2,2,0,0,0,0
DefData 0,0,0,2,3,3,2,0,0,0
DefData 0,0,0,2,3,3,2,0,0,0
DefData 0,0,2,3,0,0,3,2,0,0
DefData 0,0,2,3,1,1,3,2,0,0
DefData 2,2,2,2,3,3,2,2,2,2
DefData 2,0,2,2,2,2,2,2,0,2
DefData 2,1,2,0,2,2,0,2,1,2
DefData 0,1,0,0,0,0,0,0,1,0
#G_Stars
DefData 0,1,0,0,0,0,0,0,0,0
DefData 0,0,0,0,0,0,0,1,0,0
DefData 0,0,0,1,0,0,0,0,0,0
DefData 0,0,0,0,0,0,0,0,0,0
DefData 0,0,0,0,0,0,0,0,1,0
DefData 0,1,0,0,1,0,0,0,0,0
DefData 0,0,0,0,0,0,0,0,0,0
DefData 0,0,0,1,0,0,0,0,0,0
DefData 1,0,0,0,0,0,0,1,0,0
DefData 0,0,0,0,0,0,0,0,0,0
#G_Heart1
DefData 0,0,0,0,0,0,0,0,0,0
DefData 0,0,1,1,0,0,1,1,0,0
DefData 0,1,2,2,1,1,2,2,1,0
DefData 0,1,2,2,2,2,3,2,1,0
DefData 0,1,2,2,2,2,3,2,1,0
DefData 0,1,1,2,2,2,2,1,1,0
DefData 0,0,1,1,2,2,1,1,0,0
DefData 0,0,0,1,1,1,1,0,0,0
DefData 0,0,0,0,1,1,0,0,0,0
DefData 0,0,0,0,0,0,0,0,0,0
#G_Enemy
DefData 0,0,0,0,0,0,0,0,0,0
DefData 0,0,2,2,0,0,2,2,0,0
DefData 0,0,2,1,2,2,1,2,0,0
DefData 0,0,2,3,2,2,3,2,0,0
DefData 0,1,2,2,1,1,2,2,1,0
DefData 0,2,2,0,2,2,0,2,2,0
DefData 0,2,1,0,0,0,0,1,2,0
DefData 0,2,0,0,0,0,0,0,2,0
DefData 0,2,1,0,0,0,0,1,2,0
DefData 0,0,2,0,0,0,0,2,0,0
#G_Bullet
DefData 0,0,0,0,2,2,0,0,0,0
DefData 0,0,0,2,3,3,2,0,0,0
DefData 0,0,0,2,3,3,2,0,0,0
DefData 0,0,0,2,3,3,2,0,0,0
DefData 0,0,1,2,3,3,2,1,0,0
DefData 0,0,1,2,3,3,2,1,0,0
DefData 0,0,0,2,3,3,2,0,0,0
DefData 0,0,0,2,3,3,2,0,0,0
DefData 0,0,0,2,3,3,2,0,0,0
DefData 0,0,0,0,2,2,0,0,0,0
</pre> <br><br></td></tr></table><br>
<a name="1218483"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mr. Goober</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> TI-83 anyone?<br><img src="http://dl.dropboxusercontent.com/u/14221897/gifs/ByteBuster_3.gif"> <br><br></td></tr></table><br>
<a name="1218487"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mr. Goober</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> I added a completed build at the top post.<br>The source code is still not optimized, but the game is fully playable.<br>(extra lives per 10,000 points). <br><br></td></tr></table><br>
<a name="1218499"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamStrange</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's a new version, with added game play, colour and proper multitasking :)<br><br><pre class=code>
SuperStrict
'-----------------------------
SetGraphicsDriver(GLMax2DDriver())
AppTitle = "intBuster 4k"
Graphics(640,480)
SetVirtualResolution(160,120)

Const  ENMOVE_AMT	:int			= 1

Global _red				:Int[]			= [0,64,128,255,255,192,0  ,0  ,0  ,0]
Global _green			:Int[]			= [0,64,128,255,0  ,0  ,255,192,0  ,0]
Global _blue			:Int[]			= [0,64,128,255,0  ,0  ,0  ,0  ,255,192]
Global _sprites		:t_sprite[]		= New t_sprite[10]
Global _spcounter	:int			= 0
Global _pship			:t_entity		= New t_entity
Global _sound			:TSound			= Null
Global _soundtime	:int			= 10
Global _sndchannel	:TChannel		= Null
Global _starscroll	:Float			= 0.00
Global _key_move	:int			= False
Global _key_space	:int			= False
Global _bullet		:t_entity		= New t_entity
Global _ebullets	:t_entity[]		= New t_entity[3]
Global _enemies		:t_entity[]		= New t_entity[10]
Global _encounter	:int			= 0
Global _score			:Float			= 0
Global _hiscore		:Float			= 0
Global _lastlife	:Float			= 10000


_Init()
_MainLoop()

Type t_sprite
	Field data :Int[,] = New int[10,10]
	Method Draw(X:Float=0,Y:Float=0)
		Local c:int=0
		For Local i:int=0 To 9
			For Local j:int=0 To 9
				c=data[i,j]
				If (c&gt;0)
					SetColor(_red[c],_green[c],_blue[c])
					DrawRect(i+X,j+Y,1,1)
				EndIf
			Next
		Next
	EndMethod
	Method SetData()
		For Local j:int=0 To 9
			For Local i:int=0 To 9
				ReadData data[i,j]
			Next
		Next
	EndMethod
EndType

Type t_entity
	Field pos_x 	:Float 	= 0.00
	Field pos_y 	:Float 	= 0.00
	Field sprite	:int	= 0
	Field life		:int	= 3
	Field bullets	:int	= 3
	Field flag_alive:int	= False
	Field enmove	:Float	= 0
	Method SetPos(X:Float=0,Y:Float=0)
		pos_x = X
		pos_y = Y
	EndMethod
	Method MovePos(X:Float=0,Y:Float=0)
		pos_x :+ X
		pos_y :+ Y
	EndMethod
	Method Draw()
		If (flag_alive)
			_sprites[sprite].Draw(Floor(pos_x),Floor(pos_y))
		EndIf
	EndMethod
EndType

Function _DrawSprites()
	For Local i:int = 0 To _spcounter-1
		_sprites[i].Draw(0,0)
	Next
EndFunction

Function _CreateSprite()
	Local r:t_sprite = New t_sprite
	r.SetData()
	_sprites[_spcounter] = r
	_spcounter :+ 1
EndFunction

Function _DrawSprite(N:int=0,X:Float=0.00,Y:Float=0.00)
	_sprites[N].Draw(X,Y)
EndFunction

Function _Init()
	RestoreData G_Ship
	_CreateSprite()
	RestoreData G_Stars
	_CreateSprite()
	RestoreData G_Heart1
	_CreateSprite()
	RestoreData G_Enemy
	_CreateSprite()
	RestoreData G_Bullet
	_CreateSprite()
	_MakeSound()
	
	For Local i:int = 0 To 9
		_enemies[i] = New t_entity
	Next

	_pship.sprite = 0
	_bullet.sprite = 4

	SetClsColor(16,16,16)
	SetVirtualResolution(160,120)

	_pship.life	 = 0

	'create a timer to play nicely at 60 ticks a second
	CreateTimer(60)
End Function


Function _Restart()	
	For Local i:int = 0 To 9
		_enemies[i].flag_alive = False
	Next

	_pship.sprite = 0
	_pship.SetPos(75,110)
	_pship.flag_alive = True

	_bullet.flag_alive = False

	Cls
	For Local i:Int = 0 To _pship.life-1
		_DrawSprite(2,i*10,0)
	Next

	SetColor(255,255,255)
	DrawText("READY",60,50)
	SetColor(255,255,0)
	DrawText(RSet(String(Int(_score)),6),110,0)
	SetColor(0,128,255)
	DrawText("HI "+String(Int(_hiscore)),50,0)
	_DrawScanLines()
	Flip
	Delay(1500)

EndFunction


Function _MainLoop()
	EnablePolledInput()
		
	While (KeyDown(KEY_ESCAPE) = False) And (AppTerminate() = False)
		Local AppQueue:Int = PollEvent()
		If AppQueue = 0 Then
			Delay(2)
		Else
			Select EventID()
				Case EVENT_TIMERTICK
'					Print _pship.life+" "+_pship.pos_x+" "+_pship.pos_y
					Cls
					If _pship.life &lt; 1 Then
						_score = 0
						SetColor(255,255,255)
						DrawText("INVADAR",50,5)
						DrawText("move......ARROWS",15,60)
						DrawText("fire......SPACE",15,75)
						DrawText("FIRE to play",30,100)
						_DrawScanLines()
						Flip
						If KeyDown(KEY_SPACE) Then
							_pship.life = 3
							_Restart()
						End if
					Else
						If(_UpdateEnemies()) Then
							_UpdateShip()
							_UpdateBullet()
							_starscroll :+ 0.12
							If (_starscroll &gt;= 10)
								_starscroll :- 10
							EndIf
							If (Rand(1,100) &gt; 95)
								_CreateEnemy()
							EndIf
							_DrawStars(_starscroll)
							For Local i:int = 0 To 9
								_enemies[i].Draw()
							Next
							_bullet.Draw()
							_pship.Draw()
						
							For Local i:Int = 0 To _pship.life-2
								_DrawSprite(2,i*10,0)
							Next
						
							If _score &gt;= _lastlife Then
								_lastlife :+ 10000
								_pship.life :+ 1
							EndIf
							
							If _score &gt; _hiscore Then _hiscore = _score
							SetColor(0,128,255)
							DrawText("HI "+String(Int(_hiscore)),50,0)

							SetColor(255,255,0)
							DrawText(RSet(String(Int(_score)),6),110,0)
							_DrawScanLines()
							Flip
						End If
					End if	
			End Select
		End If
	Wend 		

EndFunction

Function _DrawStars(X:Float)
	For Local i:int = 0 To 15
		For Local j:int = 0 To 12
			_DrawSprite(1,i*10,Floor(X+(j*10)-10))
		Next
	Next
EndFunction

Function _MakeSound()
	Local _s :TAudioSample = CreateAudioSample(64,16640,SF_MONO8)
	For Local i:int = 0 To 63
		If (i &lt;= 31)
			_s.samples[i] = 160
		Else
			_s.samples[i] = 96
		EndIf
	Next
	_sound = LoadSound(_s,True)
EndFunction

Function _DrawScanLines()
	SetVirtualResolution(640,480)
	SetColor(0,0,0)
	For Local i:int = 0 To 159
		DrawLine(i*4,0,i*4,480)
		If (i &lt;= 119)
			DrawLine(0,i*4,640,i*4)
		EndIf
	Next
	SetColor(255,255,255)
	SetVirtualResolution(160,120)
EndFunction

Function _CheckKeys()
	_key_move = False
	If (KeyDown(KEY_RIGHT))
		_key_move = 2
	Else If (KeyDown(KEY_LEFT))
		_key_move = 1
	EndIf
	_key_space = False
	If (KeyDown(KEY_SPACE))
		_key_space = True
	EndIf
EndFunction

Function _UpdateShip()
	_CheckKeys()
	If (_key_move &gt; 0)
		If (_key_move = 2)
			If (_pship.pos_x &lt; 150)
				_pship.MovePos(1,0)
			EndIf
		Else If (_key_move = 1)
			If (_pship.pos_x &gt; 0)
				_pship.MovePos(-1,0)
			EndIf
		EndIf
	EndIf
	If (_key_space)
		If (_bullet.flag_alive = False)
			_bullet.flag_alive = True
			_bullet.pos_x = _pship.pos_x
			_bullet.pos_y = _pship.pos_y
		EndIf
	EndIf
EndFunction

Function _UpdateBullet()
	If (_bullet.flag_alive)
		_bullet.MovePos(0,-3)
		For Local i:int = 0 To 9
			If (_enemies[i].flag_alive)
				If (_PointInRect(_bullet.pos_x+5,_bullet.pos_y, _enemies[i].pos_x,_enemies[i].pos_y,_enemies[i].pos_x+10,_enemies[i].pos_y+10))
					_enemies[i].flag_alive = False
					_bullet.flag_alive = False
					_score :+ 100
				EndIf
			EndIf
		Next
		If (_bullet.pos_y &lt; -10)
			_bullet.flag_alive = False
		EndIf
	EndIf
EndFunction

Function _UpdateEnemies:int()
	For Local i:int = 0 To 9
		If (_enemies[i].flag_alive)

			If _RectInRect(_pship.pos_x, _pship.pos_y, _pship.pos_x+10, _pship.pos_y+10, _enemies[i].pos_x, _enemies[i].pos_y, _enemies[i].pos_x+10, _enemies[i].pos_y+10)
				_pship.life :- 1
				If _pship.life &gt; 0 Then
					_Restart()
				End if	
				Return false
			End If
			
			_enemies[i].MovePos(0,1)
			_enemies[i].enmove :+ 1
			If (_enemies[i].enmove &gt;= 360)
				_enemies[i].enmove = 0
			EndIf
			_enemies[i].MovePos(ENMOVE_AMT*Sin(_enemies[i].enmove),0)
			If (_enemies[i].pos_y &gt; 120)
				_enemies[i].flag_alive = False
			EndIf
		EndIf
	Next
	Return true
EndFunction

Function _CreateEnemy()
	If (_enemies[_encounter].flag_alive = False)
		_enemies[_encounter].flag_alive = True
		_enemies[_encounter].pos_x		= Rand(0,150)
		_enemies[_encounter].pos_y		= -10
		_enemies[_encounter].sprite		= 3
		_enemies[_encounter].enmove		= Rand(0,360)
		_encounter :+ 1
		If (_encounter &gt; 9)
			_encounter = 0
		EndIf
	EndIf
EndFunction

Function _PointInRect :Int(X:Float, Y:Float, X1:Float, Y1:Float, X2:Float, Y2:Float)
	If (X &gt;= X1) And (X &lt;= X2) And (Y &gt;= Y1) And (Y &lt;= Y2)
		Return True
	EndIf
	Return False
EndFunction

Function _RectInRect :Int(X:Float, Y:Float, xx:Float, yy:float, X1:Float, Y1:Float, X2:Float, Y2:Float)
	If x &lt; x2 And xx &gt; x1 And y &lt; y2 And yy &gt; y1 Then
		Return True
	End If	
	Return False
EndFunction

#G_Ship
DefData 0,0,0,0,8,8,0,0,0,0
DefData 0,0,0,0,8,8,0,0,0,0
DefData 0,0,0,8,9,9,8,0,0,0
DefData 0,0,0,8,9,9,8,0,0,0
DefData 0,0,8,9,3,3,9,8,0,0
DefData 0,0,8,9,2,2,9,8,0,0
DefData 8,8,8,8,9,9,8,8,8,8
DefData 8,0,8,8,8,8,8,8,0,8
DefData 8,2,8,0,8,8,0,8,2,8
DefData 0,4,0,0,4,4,0,0,4,0
#G_Stars
DefData 0,1,0,0,0,0,0,0,0,0
DefData 0,0,0,0,0,0,0,1,0,0
DefData 0,0,0,1,0,0,0,0,0,0
DefData 0,0,0,0,0,0,0,0,0,1
DefData 0,0,0,0,0,0,0,0,0,0
DefData 0,1,0,0,1,0,0,0,0,0
DefData 0,0,0,0,0,0,0,0,0,0
DefData 0,0,0,0,0,0,0,0,0,0
DefData 0,0,0,0,0,0,1,0,0,0
DefData 0,0,0,0,0,0,0,0,0,0
#G_Heart1
DefData 0,0,0,0,0,0,0,0,0,0
DefData 0,0,4,4,0,0,4,4,0,0
DefData 0,4,3,5,4,4,3,5,4,0
DefData 0,4,5,5,5,5,5,5,4,0
DefData 0,4,5,5,5,5,5,5,4,0
DefData 0,4,5,5,5,5,5,5,4,0
DefData 0,0,4,5,5,5,5,4,0,0
DefData 0,0,0,4,5,5,4,0,0,0
DefData 0,0,0,0,4,4,0,0,0,0
DefData 0,0,0,0,0,0,0,0,0,0
#G_Enemy
DefData 0,0,0,0,0,0,0,0,0,0
DefData 0,0,6,6,0,0,6,6,0,0
DefData 0,0,6,7,6,6,7,6,0,0
DefData 0,0,6,3,6,6,3,6,0,0
DefData 0,7,6,6,7,7,6,6,7,0
DefData 0,6,6,0,6,6,0,6,6,0
DefData 0,6,7,0,0,0,0,7,6,0
DefData 0,6,0,0,0,0,0,0,6,0
DefData 0,6,7,0,0,0,0,7,6,0
DefData 0,0,6,0,0,0,0,6,0,0
#G_Bullet
DefData 0,0,0,0,2,2,0,0,0,0
DefData 0,0,0,2,3,3,2,0,0,0
DefData 0,0,0,2,3,3,2,0,0,0
DefData 0,0,0,2,3,3,2,0,0,0
DefData 0,0,1,2,3,3,2,1,0,0
DefData 0,0,1,2,3,3,2,1,0,0
DefData 0,0,0,2,3,3,2,0,0,0
DefData 0,0,0,2,3,3,2,0,0,0
DefData 0,0,0,2,3,3,2,0,0,0
DefData 0,0,0,0,2,2,0,0,0,0</pre> <br><br></td></tr></table><br>
<a name="1218511"></a>

<a name="1218512"></a>

<a name="1218513"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> Shaped off 1.8k of code.<br><br>There is more possible but I wanted to keep the "readability".<br><br>PS: the game loop isn't that proper (pressing ESC does not work "snappy").<br><br>Pay attention that certain variables changed type (move-key was byte is now int - so it can get negative), some things changed to be more dynamic ("for x = eachin array ; x.bla"  VS "for x = 0 to 9 ; enemy[x].bla").<br><br>You could "compress" color definitons (instead of "0,64,92,.." you use "0,4,6" and multiply them with 16 - so your definitions stay between 0-9.<br><br>Most useable part will be my "init"-method as it does the same as the DATA-part (maintaining "readability"). But only as long as you do not allow more than 10 color definitions (you can use A-Z,a-Z,0-9 as "codes" too... without much hassle)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
'-----------------------------
SetGraphicsDriver(GLMax2DDriver())
AppTitle = "intBuster 4k"
Graphics(640,480)
SetVirtualResolution(160,120)

Const  ENMOVE_AMT%		= 1

Global _cr%[]			= [0,64,128,255,255,192,0  ,0  ,0  ,0]
Global _cg%[]			= [0,64,128,255,0  ,0  ,255,192,0  ,0]
Global _cb%[]			= [0,64,128,255,0  ,0  ,0  ,0  ,255,192]

Global _sprites:t_sprite[]	= New t_sprite[10]
Global _spcounter@			= 0
Global _pship:t_entity		= New t_entity
Global _sound:TSound		= Null
Global _soundtime%			= 10
Global _sndchannel:TChannel	= Null
Global _starscroll#			= .0
Global _key_move%			= 0
Global _key_space@			= 0
Global _bullet:t_entity		= New t_entity
Global _ebullets:t_entity[]	= New t_entity[3]
Global _enemies:t_entity[]	= New t_entity[10]
Global _encounter%			= 0
Global _score#				= 0
Global _hiscore#			= 0
Global _lastlife#			= 10000
Global _e:t_entity, c@, s$


_Init()
_MainLoop()

Type t_sprite
	Field data%[,] = New Int[10,10]

	Method Init:t_sprite(sa$="")
		local j%
		for s=eachin sa.split("|")
			for local i% = 0 until s.length
				data[i,j]= int(chr(s[i]))
			next
			j:+1
		Next
		return self
	End Method

	Method Draw(X#,Y#)
		For local i% = 0 To 9
			For local j% = 0 To 9
				c=data[i,j]
				if c&gt;0
					SetColor(_cr[c],_cg[c],_cb[c])
					DrawRect(i+X,j+Y,1,1)
				endif
			Next
		Next
	EndMethod

EndType


Type t_entity
	Field x# 		= 0
	Field y#		= 0
	Field sprite@	= 0
	Field life@		= 3
	Field bullets@	= 3
	Field alive@	= 0
	Field enmove#	= 0

	Method SetPos(_X#=0,_Y#=0)
		x = _X
		y = _Y
	EndMethod

	Method MovePos(_X#=0,_Y#=0)
		x :+ _X
		y :+ _Y
		x = Min(Max(0,x),150)
	EndMethod

	Method Draw()
		If alive then _sprites[sprite].Draw(int(x),int(y))
	EndMethod
EndType

Function _DrawSprites()
	For local i% = 0 To _spcounter-1
		_sprites[i].Draw(0,0)
	Next
EndFunction

Function _CreateSprite(t$)
	_sprites[_spcounter] = New t_sprite.Init(GetSprite(t))
	_spcounter :+ 1
EndFunction

Function _DrawSprite(N:Byte=0,X:Float=0.00,Y:Float=0.00)
	_sprites[N].Draw(X,Y)
EndFunction



Function _Init()
	_CreateSprite("ship")
	_CreateSprite("stars")
	_CreateSprite("heart1")
	_CreateSprite("enemy")
	_CreateSprite("bullet")

	_MakeSound()

	For local i% = 0 To 9
		_enemies[i] = New t_entity
	Next
	_bullet.sprite = 4
	_pship.life	 = 0

	SetClsColor(16,16,16)
	SetVirtualResolution(160,120)

	CreateTimer(60)
EndFunction





Function _Restart()
	for _e = eachin _enemies
		_e.alive = 0
	Next

	_pship.sprite = 0
	_pship.SetPos(75,110)
	_pship.alive = 1

	_bullet.alive = 0

	Cls
	For local i% = 0 To _pship.life-1
		_DrawSprite(2,i*10,0)
	Next

	SetColor(255,255,255)
	DrawText("READY",60,50)
	SetColor(255,255,0)
	DrawText(RSet(Int(_score),6),110,0)
	SetColor(0,128,255)
	DrawText("HI "+Int(_hiscore),50,0)
	_DrawScanLines()
	Flip
	Delay(1500)
EndFunction

Function _MainLoop()
	EnablePolledInput()

	While (KeyDown(KEY_ESCAPE) = 0) And (AppTerminate() = 0)
		If PollEvent() = 0
			Delay(2)
		Else
			Select EventID()
				Case EVENT_TIMERTICK
					Cls
					If _pship.life &lt; 1
						_score = 0
						SetColor(255,255,255)
						DrawText("INVADAR",50,5)
						DrawText("move......ARROWS",15,60)
						DrawText("fire......SPACE",15,75)
						DrawText("FIRE to play",30,100)
						_DrawScanLines()
						Flip
						If KeyDown(KEY_SPACE) Then
							_pship.life = 3
							_Restart()
						Endif
					Else
						If(_UpdateEnemies())
							_UpdateShip()
							_UpdateBullet(_bullet)
							_starscroll :+ 0.12
							_starscroll :mod 10

							If (Rand(1,100) &gt; 95) then _CreateEnemy()
							_DrawStars(_starscroll)
							For _e = eachin _enemies
								_e.Draw()
							Next
							_bullet.Draw()
							_pship.Draw()

							For Local i% = 0 To _pship.life-2
								_DrawSprite(2,i*10,0)
							Next

							If _score &gt;= _lastlife
								_lastlife :+ 10000
								_pship.life :+ 1
							EndIf

							If _score &gt; _hiscore Then _hiscore = _score
							SetColor(0,128,255)
							DrawText("HI "+Int(_hiscore),50,0)

							SetColor(255,255,0)
							DrawText(RSet(Int(_score),6),110,0)
							_DrawScanLines()
							Flip
						End If
					End if
			End Select
		End If
	Wend

EndFunction


Function _DrawStars(X:Float)
	For local i@ = 0 To 15
		For local j@ = 0 To 11
			_DrawSprite(1,i*10,Floor(X+(j*10)-10))
		Next
	Next
EndFunction

Function _MakeSound()
	Local _s :TAudioSample = CreateAudioSample(64,16640,SF_MONO8)
	For Local i% = 0 To 63
		_s.samples[i] = 96 + 64 * (i &lt;= 31)
	Next
	_sound = LoadSound(_s,True)
EndFunction

Function _DrawScanLines()
	SetVirtualResolution(640,480)
	SetColor(0,0,0)
	For Local i@ = 0 To 159
		DrawLine(i*4,0,i*4,480)
		If i &lt;= 119 then DrawLine(0,i*4,640,i*4)
	Next
	SetColor(255,255,255)
	SetVirtualResolution(160,120)
EndFunction

Function _CheckKeys()
	_key_move = KeyDown(KEY_RIGHT)
	_key_move :- KeyDown(KEY_LEFT)

	_key_space = KeyDown(KEY_SPACE)
EndFunction


Function _UpdateShip()
	_CheckKeys()
	_pship.MovePos(_key_move,0)
	If _key_space and not _bullet.alive
		_bullet.alive = 1
		_bullet.x = _pship.x
		_bullet.y = _pship.y
	EndIf
EndFunction

Function _UpdateBullet(_b:t_entity)
	If _b.alive
		_b.MovePos(0,-3)
		For _e = eachin _enemies
			If _e.alive and _PointInRect(_b.x+5,_b.y,_e.x,_e.y,_e.x+10,_e.y+10)
				_e.alive = 0
				_b.alive = 0
				_score :+ 100
			EndIf
		Next
		If _b.y &lt; -10 then _b.alive = 0
	EndIf
EndFunction

Function _UpdateEnemies%()
	For _e = eachin _enemies
		If _e.alive
			If _RectInRect(_pship.x, _pship.y, _pship.x+10, _pship.y+10, _e.x, _e.y, _e.x+10, _e.y+10)
				_pship.life :- 1
				If _pship.life &gt; 0 Then _Restart()
				Return 0
			End If

			_e.MovePos(0,1)
			_e.enmove :+ 1
			If _e.enmove &gt;= 360 then _e.enmove = 0
			_e.MovePos(ENMOVE_AMT*Sin(_e.enmove),0)
			If _e.y &gt; 120 _e.alive = 0
		EndIf
	Next
	return 1
EndFunction

Function _CreateEnemy()
	If not _enemies[_encounter].alive
		_enemies[_encounter].alive	= 1
		_enemies[_encounter].x		= Rand(0,150)
		_enemies[_encounter].y		= -10
		_enemies[_encounter].sprite	= 3
		_enemies[_encounter].enmove	= Rand(0,360)
		_encounter :+ 1
		If _encounter &gt; 9 then _encounter = 0
	EndIf
EndFunction

Function _PointInRect@(X#, Y#, X1#, Y1#, X2#, Y2#)
	return (X &gt;= X1) And (X &lt;= X2) And (Y &gt;= Y1) And (Y &lt;= Y2)
EndFunction

Function _RectInRect@(X#, Y#, xx#, yy#, X1#, Y1#, X2#, Y2#)
	return (x &lt; x2 And xx &gt; x1 And y &lt; y2 And yy &gt; y1)
EndFunction


Function GetSprite$(n$)
	Select n
		case "ship"
			s ="0000880000|"
			s:+"0000880000|"
			s:+"0008998000|"
			s:+"0008998000|"
			s:+"0089339800|"
			s:+"0089229800|"
			s:+"8888998888|"
			s:+"8088888808|"
			s:+"8280880828|"
			s:+"0400440040"
		case "stars"
			s ="0100000000|"
			s:+"0000000100|"
			s:+"0001000000|"
			s:+"0000000001|"
			s:+"0000000000|"
			s:+"0100100000|"
			s:+"0000000000|"
			s:+"0000000000|"
			s:+"0000001000|"
			s:+"0000000000"
		case "heart1"
			s ="0000000000|"
			s:+"0044004400|"
			s:+"0435443540|"
			s:+"0455555540|"
			s:+"0455555540|"
			s:+"0455555540|"
			s:+"0045555400|"
			s:+"0004554000|"
			s:+"0000440000|"
			s:+"0000000000"
		case "enemy"
			s ="0000000000|"
			s:+"0066006600|"
			s:+"0067667600|"
			s:+"0063663600|"
			s:+"0766776670|"
			s:+"0660660660|"
			s:+"0670000760|"
			s:+"0600000060|"
			s:+"0670000760|"
			s:+"0060000600"
		case "bullet"
			s ="0000220000|"
			s:+"0002332000|"
			s:+"0002332000|"
			s:+"0002332000|"
			s:+"0012332100|"
			s:+"0012332100|"
			s:+"0002332000|"
			s:+"0002332000|"
			s:+"0002332000|"
			s:+"0000220000"
	End Select
	return s
EndFunction
</textarea><br><br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="1218571"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hardcoal</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> cool <br><br></td></tr></table><br>
<a name="1218573"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mr. Goober</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#23">[#23]</a></td></tr></table></td></tr><tr ><td class="posttext"> @AdamStrange:<br>I like it. I never really used timers much, but I suppose they're good for that sort of thing. <br><br></td></tr></table><br>
<a name="1218593"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamStrange</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#24">[#24]</a></td></tr></table></td></tr><tr ><td class="posttext"> the other once thing is the game now runs at the same speed on any machine, any cpu.<br><br>You could have different timers, one for input one for the enemies, etc :) <br><br></td></tr></table><br>
<a name="1218620"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mr. Goober</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#25">[#25]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oh uh... I just relied on Flip so that it is timed on the refresh rate. That method works too. What does it do? Does it wait until x milliseconds have passed? <br><br></td></tr></table><br>
<a name="1218621"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#26">[#26]</a></td></tr></table></td></tr><tr ><td class="posttext"> It depends on how you created your graphics object.<br><br>According to the blitzmax sources that "flip -1/0/1" work differently - and one of them uses already a kind of "timer".<br><br>So all in all there is no real need for an timer if you plan to update and draw simultaneously (at XX fps).<br><br>Else (200 updates vs 20 draw /second) you have to rely on timers or other methods of a game loop.<br><br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="1218622"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Steve Elliott</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#27">[#27]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>I just relied on Flip so that it is timed on the refresh rate. That method works too.<br> <br></div><br><br>So what happens if the vsync is turned off in drivers?  It'll run at full speed - not at the refresh rate.  Or what if the monitor refresh rate varies?  Again it will vary in speed from computer to computer. <br><br></td></tr></table><br>
<a name="1218633"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >zzz</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#28">[#28]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is pretty neat. You should consider encoding your sprites as others have mentioned. You are using only 4 different values in your code, needing only 2 bits to be represented. Simply encoding this into a hexadecimal string (4 values per byte/2 hex characters) would give something like 70ish bytes per sprite instead of the 300ish you are using now. A decoding routine would add some, but it should be well worth it. <br><br></td></tr></table><br>
<a name="1218636"></a>

<a name="1218637"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#29">[#29]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Steve Elliot<br><br>brl.mod/graphics.mod/graphics.bmx:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Rem
bbdoc: Flip current graphics object
about:
#Flip swap the front and back buffers of the current graphics objects.

If @sync is 0, then the flip occurs as soon as possible. If @sync is 1, then the flip occurs
on the next vertical blank.

If @sync is -1 and the current graphics object was created with the #Graphics command,
then flips will occur at the graphics object's refresh rate, unless the graphics object was 
created with a refresh rate of 0 in which case flip occurs immediately.

If @sync is -1 and the current graphics object was NOT created with the #Graphics command,
then the flip will occur on the next vertical blank.
End Rem
Function Flip( sync=-1 )
	RunHooks FlipHook,Null

	If sync&lt;&gt;-1
		_driver.Flip sync
		Return
	EndIf
	
	If _graphics&lt;&gt;_exGraphics
		_driver.Flip True
		Return
	EndIf

	If _softSync
		_syncTime:+_syncPeriod
		_syncAccum:+_syncFrac
		If _syncAccum&gt;=_syncRate
			_syncAccum:-_syncRate
			_syncTime:+1
		EndIf
		Local dt=_syncTime-MilliSecs()
		If dt&gt;0
			Delay dt
		Else
			_syncTime:-dt
		EndIf
		_driver.Flip False
	Else If _hardSync
		_driver.Flip True
	Else
		_driver.Flip False
	EndIf

End Function
</textarea><br><br><br>Concerning sprite encoding:<br>If you use mine, you are already "char-ready" and so if using A-Za-z0-9+some others you could "mod" them with your "maxColor-const * index" and get back various information.<br><br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="1218649"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mr. Goober</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#30">[#30]</a></td></tr></table></td></tr><tr ><td class="posttext"> I can indeed compress the image data into hex values and then decompress, which shouldn't be too much data to create. After trying one time to simply compress all of the source, I got it to around 5,500-ish bytes. I encountered some problems while trying to compress it further, and ended up making the program unusable. I'll try doing it again later.<br><br>@Steve Eliott<br>Flip in this sense is using vsync (sync is 0, and created with Graphics()). Though this does mean that the refresh rate of monitors will change how the game is played (we're depending on 60 hz monitors in this sense), but I didn't really care to change it because it wasn't too much of a problem. <br><br></td></tr></table><br>
<a name="1218659"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mr. Goober</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#31">[#31]</a></td></tr></table></td></tr><tr ><td class="posttext"> So I made a little function to turn the data into hex. I'll probably convert the sprites to hex and then import those into the game.<br><br>an example<br><pre class=code>
Global _hex		:String			= "0123456789ABCDEF"

RestoreData G_Ship
Global _sprite :String = _MakeSpriteCode()
Print _sprite

Function _MakeSpriteCode :String()
	Local u:Byte[] = New Byte[100]
	For Local i:Byte = 0 To 99
		ReadData u[i]
	Next
	Return _ToHex(u)
EndFunction

Function _ToHex :String(B:Byte[])
	Local u:Byte 	= 0
	Local r:String	= ""
	For Local i:Byte = 0 To Len(B)-1 Step 2
		u = 0
		u :| B[i]
		u = u Shl 2
		u :| B[i+1]
		r :+ Chr(_hex[u])
	Next
	Return r
EndFunction


#G_Ship
DefData 0,0,0,0,2,2,0,0,0,0
DefData 0,0,0,0,2,2,0,0,0,0
DefData 0,0,0,2,3,3,2,0,0,0
DefData 0,0,0,2,3,3,2,0,0,0
DefData 0,0,2,3,0,0,3,2,0,0
DefData 0,0,2,3,1,1,3,2,0,0
DefData 2,2,2,2,3,3,2,2,2,2
DefData 2,0,2,2,2,2,2,2,0,2
DefData 2,1,2,0,2,2,0,2,1,2
DefData 0,1,0,0,0,0,0,0,1,0
</pre> <br><br></td></tr></table><br>
<a name="1218670"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#32">[#32]</a></td></tr></table></td></tr><tr ><td class="posttext"> Again you can shorten your functions:<br><br>If you have a array and you are looping through ALL of them... you can replace "For local i:Byte = 0 to 99 ... u[i]" with "For local i@ = eachin ... i" ... this even gains benefits when "i" is a more complex variable and you call methods/properties of it.<br><br><br>@flip:<br>Don't know why I search for codes if my posts get ignored: use flip -1 and create the graphics with the "Graphics()"-command. Limit the refreshrate to the one you want (60) and BlitzMax will take care itself that the loop gets a delay if needed.<br><br>Only exception is: computer is to slow to handle XX refreshrate. But that problem arises with timers too.<br><br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="1218799"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mr. Goober</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#33">[#33]</a></td></tr></table></td></tr><tr ><td class="posttext"> I didn't ignore it, but my focus changed. I am thinking on redoing the source and porting it to Monkey X and then sell the game on Android for like 99 cents. It would be my very first product in my game design career. Gotta start somewhere. <br><br></td></tr></table><br>
<a name="1218847"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#34">[#34]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just a tip: arcade games are much harder to play without proper input types (gamepads, keyboards).<br><br>So to avoid that virtual "dpad"-approach you might have a "left" and a "right" area (both sides of the display) and might think of auto-firing.<br><br>All in all this game might be a bit "to simple" for a &gt;0ct game. So you end up improving the game - with powerups and varying enemies.<br><br>Welcome to a vertical scrolling Galaga game :D<br><br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="1218880"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mr. Goober</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#35">[#35]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah, I was thinking on making a bunch of improvements to the original idea, with perhaps powerups and other types of enemies that behave differently. I was also thinking of the way touchpads would use the left and right sides of the screen to move. I was also thinking on having the ship simply autofire to avoid needing to press a third button (maybe when the player is not moving it will fire).<br><br>We'll see how it goes. I changed the look of the game so that it actually looks like it was made on a gameboy or calculator. Take a look:<br><br><img src="http://puu.sh/7adMy.png"> <br><br></td></tr></table><br>
<a name="1218886"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#36">[#36]</a></td></tr></table></td></tr><tr ><td class="posttext"> Pay attention to the smaller screensizes on smartphones.<br><br>So while we see those small lines between the "pixels" - they will be up to invisible on a small smartphone (high dpi vs lower dpi).<br><br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
