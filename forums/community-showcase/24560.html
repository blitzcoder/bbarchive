<!DOCTYPE html><html lang="en" ><head ><title >Vector Rendering Library</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Vector Rendering Library</h1><a href="forums.php" >Community Forums</a>/<a href="topics.php?forum=61" >Showcase</a>/<a href="#bottom" >Vector Rendering Library</a><br><br>
<a name="254969"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Michael Reitzenstein</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Today in Physics I was challanged to write a software renderer on my graphics calculator, and this is a (more sophisticated) version of the result. Because the calculator didn't have shades of colours pretty much the only thing you could do on the thing is draw vectors. <br><br>The calculator takes 5 seconds to render each frame of a cube rotating.<br><br>The code is really unfinished, and since I have had no experience of writing any kind of 3D engine I just made everything up as I went along. Use left and right keys to move the object. It's pitch and yaw is modified every frame aleardy.<br><br><br><pre class=code>
Graphics 1024, 768, 32, 1
SetBuffer BackBuffer( )

Global CameraX#, CameraY#, CameraZ# = -5
Global FOV# = 30.0
Global Graphics_Width = GraphicsWidth( ), Graphics_Height = GraphicsHeight( )

Global Null_Vector.Vector = New Vector

entity.Entity = CreateCube( )
entity2.Entity = CreateObject( )

While Not KeyHit( 1 )

	Render( entity )
	Render( entity2 )
	
	entity\rotation\x# = entity\rotation\x# + 5
	entity2\rotation\x# = entity2\rotation\x# - 1 
	entity\rotation\y# = entity\rotation\y# + 2.5
	entity2\rotation\y# = entity2\rotation\y# - 2.5

	VWait( )
	Flip
	Cls
	
Wend

Type Entity

	Field Position.Vector
	Field Rotation.Vector
	
	Field Vertex_Count
	Field Link_Count

	Field Vertices
	Field Link1, Link2
	
End Type

Type Vertex

	Field Vector.Vector
	Field TempPosition.Vector
	
	Field ProjectedCoords.Vector
	
End Type

Type Vector
	
	Field X#, Y#, Z#
	
End Type

Type Vertex_Stack
	
	Field Vector.Vector
	
End Type

Type Vertex_Link
	
	Field Vertex0, Vertex1
	
End Type

Function CreateObject.Entity( )

	Add_To_Vertex_Stack( -1,  1, -1 )
	Add_To_Vertex_Stack(  1,  1, -1 )
	Add_To_Vertex_Stack( -1,  1,  1 )
	Add_To_Vertex_Stack(  1,  1,  1 )
	
	Add_To_Vertex_Stack( -0.8, -1, -0.8 )
	Add_To_Vertex_Stack(  0.8, -1, -0.8 )
	Add_To_Vertex_Stack( -0.8, -1,  0.8 )
	Add_To_Vertex_Stack(  0.8, -1,  0.8 )
	
	Add_To_Vertex_Stack(  0,  2,  0 )
	
	Vertex_Link( 1, 2 )
	Vertex_Link( 1, 3 )
	Vertex_Link( 3, 4 )
	Vertex_Link( 2, 4 )
	
	Vertex_Link( 5, 6 )
	Vertex_Link( 5, 7 )
	Vertex_Link( 7, 8 )
	Vertex_Link( 6, 8 )
	
	Vertex_Link( 1, 5 )
	Vertex_Link( 2, 6 )
	Vertex_Link( 3, 7 )
	Vertex_Link( 4, 8 )
	
	Vertex_Link( 1, 9 )
	Vertex_Link( 2, 9 )
	Vertex_Link( 3, 9 )
	Vertex_Link( 4, 9 )
	
	
	Return Build_Mesh( )
	
End Function

Function CreateCube.Entity( )

	Add_To_Vertex_Stack( -1,  1, -1 )
	Add_To_Vertex_Stack(  1,  1, -1 )
	Add_To_Vertex_Stack( -1,  1,  1 )
	Add_To_Vertex_Stack(  1,  1,  1 )
	
	Add_To_Vertex_Stack( -1, -1, -1 )
	Add_To_Vertex_Stack(  1, -1, -1 )
	Add_To_Vertex_Stack( -1, -1,  1 )
	Add_To_Vertex_Stack(  1, -1,  1 )
		
	Vertex_Link( 1, 2 )
	Vertex_Link( 1, 3 )
	Vertex_Link( 3, 4 )
	Vertex_Link( 2, 4 )
	
	Vertex_Link( 5, 6 )
	Vertex_Link( 5, 7 )
	Vertex_Link( 7, 8 )
	Vertex_Link( 6, 8 )
	
	Vertex_Link( 1, 5 )
	Vertex_Link( 2, 6 )
	Vertex_Link( 3, 7 )
	Vertex_Link( 4, 8 )	
	
	Return Build_Mesh( )
	
End Function



Function Add_To_Vertex_Stack( X#, Y#, Z# )
	
	Local vs.Vertex_Stack = New Vertex_Stack
	
	vs\Vector = New Vector
	
	vs\Vector\X# = X#
	vs\Vector\Y# = Y#
	vs\Vector\Z# = Z#
	
End Function
	
Function Vertex_Link( v0, v1 )

	Local link.Vertex_Link = New Vertex_Link
	
	link\Vertex0 = v0
	link\Vertex1 = v1
	
End Function

Function Build_Mesh.Entity( )

	Local Entity.Entity = New Entity
	Entity\Position = New Vector
	Entity\Rotation = New Vector
	
	Local Vertex.Vertex
	
	Local Vertex_Count
	Local Vertex_Link_Count
	
	For Vertex_Stack.Vertex_Stack = Each Vertex_Stack
		
		Vertex_Count = Vertex_Count + 1
		
	Next
	
	For Vertex_Link.Vertex_Link = Each Vertex_Link
		
		Vertex_Link_Count = Vertex_Link_Count + 1
		
	Next
	
	Entity\Vertices = CreateBank( 4 * Vertex_Count )
	
	Local Count = 0
	
	For Vertex_Stack.Vertex_Stack = Each Vertex_Stack
		
		Vertex.Vertex = New Vertex
		Vertex\Vector = Vertex_Stack\Vector
		Vertex\ProjectedCoords = New Vector
		Vertex\TempPosition = New Vector
		
		PokeInt( Entity\Vertices, Count * 4, Handle( Vertex ) )
		
		Delete Vertex_Stack
		
		Count = Count + 1
		
	Next
	
	Count = 0
	
	Entity\Link1 = CreateBank( 4 * Vertex_Link_Count )
	Entity\Link2 = CreateBank( 4 * Vertex_Link_Count )
	
	For Vertex_Link.Vertex_Link = Each Vertex_Link
		
		PokeInt( Entity\Link1, Count * 4, PeekInt( Entity\Vertices, ( Vertex_Link\Vertex0 - 1 ) * 4 ) )
		PokeInt( Entity\Link2, Count * 4, PeekInt( Entity\Vertices, ( Vertex_Link\Vertex1 - 1 ) * 4 ) )
		
		Delete Vertex_Link
		
		Count = Count + 1
		
	Next
	
	Entity\Vertex_Count = Vertex_Count
	Entity\Link_Count = Vertex_Link_Count
	
	Return Entity
		
End Function

Function Render( Entity.Entity )

	Local Count
	Local Vertex.Vertex
	
	For Count = 0 To ( Entity\Vertex_Count - 1 )
	
		Vertex.Vertex = Object.Vertex( PeekInt( Entity\Vertices, Count * 4 ) )
		
		Copy_Vector( Vertex\Vector, Vertex\TempPosition )
		
		Rotate_Vector( Vertex\TempPosition, Null, Entity\Rotation )
		
		Add_Vector( Entity\Position, Vertex\TempPosition )
		
		Project( Vertex\TempPosition, Vertex\ProjectedCoords )
	
	Next
	
	For Count = 0 To ( Entity\Link_Count - 1 )
	
		Vertex1.Vertex = Object.Vertex( PeekInt( Entity\Link1, Count * 4 ) )
		Vertex2.Vertex = Object.Vertex( PeekInt( Entity\Link2, Count * 4 ) )
		
		If Vertex1 &lt;&gt; Null And Vertex2 &lt;&gt; Null
		
		;	If Vertex1\Vector\Z# = 1.0 And Vertex2\Vector\Z# = 1.0
			
				Line Vertex1\ProjectedCoords\X#, Vertex1\ProjectedCoords\Y#, Vertex2\ProjectedCoords\X#, Vertex2\ProjectedCoords\Y#
		;		
		;	EndIf
			
		Else
		
			RuntimeError "Link is Null"
		
		EndIf
	
	Next

End Function

Function Project( Vector1.Vector, Vector2.Vector )

	Local X#, Y#, Z#
	
	X# = Vector1\X# - CameraX#
	Y# = Vector1\Y# - CameraY#
	Z# = Vector1\Z# - CameraZ#
	
	Local AngleX# = ATan2( X#, Z# )
	Local AngleY# = ATan2( Y#, Z# )

	Vector2\X# = ( ( AngleX# / FOV ) + 1.0 ) / 2.0 * Float( Graphics_Width )
	Vector2\Y# = ( ( AngleY# / FOV ) + 1.0 ) / 2.0 * Float( Graphics_Height )
	
	Plot Vector2\X#, Vector2\Y#

End Function

Function Copy_Vector( Vector1.Vector, Vector2.Vector )
	
	Vector2\X# = Vector1\X#
	Vector2\Y# = Vector1\Y#
	Vector2\Z# = Vector1\Z#
	
End Function

Function Add_Vector( Vector1.Vector, Vector2.Vector )
	
	Vector2\X# = Vector2\X# + Vector1\X#
	Vector2\Y# = Vector2\Y# + Vector1\Y#
	Vector2\Z# = Vector2\Z# + Vector1\Z#
	
End Function

;rotates vector1 about vector2 by vector3's pitch,yaw,roll
Function Rotate_Vector( Vector1.Vector, Vector2.Vector, Vector3.Vector )
	
	Local PitchAngle#, YawAngle#, RollAngle#
	Local aDist#
	Local bDist#
	
	Local Dist#
	
	PitchAngle# = FindAngle#( Vector1\Y#, Vector1\Z# )
	PitchAngle# = PitchAngle# + Vector3\X#
	
	aDist# = ( Vector1\Y# )
	bDist# = ( Vector1\Z# )
	
	Dist# = Sqr( aDist# * aDist# + bDist# * bDist# )
	
	Vector1\Y# = Dist# * Sin( PitchAngle# )
	Vector1\Z# = Dist# * Cos( PitchAngle# )
	
	
	YawAngle# = FindAngle#( Vector1\X#, Vector1\Z# )
	YawAngle# = YawAngle# + Vector3\Y#
	
	aDist# = ( Vector1\X# )
	bDist# = ( Vector1\Z# )
	
	Dist# = Sqr( aDist# * aDist# + bDist# * bDist# )
	
	Vector1\X# = Dist# * Sin( YawAngle# )
	Vector1\Z# = Dist# * Cos( YawAngle# )
	
End Function

Function FindAngle#( X#, Y# )

	Return ATan2( X#, Y# )
	
End Function
</pre> <br><br></td></tr></table><br>
<a name="255116"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Works nicely here, though it looks like you took the left/right code out! <br><br></td></tr></table><br>
<a name="255176"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Michael Reitzenstein</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oops!<br><br><pre class=code>
If KeyDown( 203 ) entity\position\x# = entity\position\x# - 0.05
</pre><br><br>etc... <br><br></td></tr></table><br>
<a name="255642"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jfk EO-11110</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> well done! have a look at my simple 3d engine in the code archives (2D :) ). <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
