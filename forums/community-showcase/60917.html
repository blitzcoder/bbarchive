<!DOCTYPE html><html lang="en" ><head ><title >alphaballs</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >alphaballs</h1><a href="forums.php" >Community Forums</a>/<a href="topics.php?forum=61" >Showcase</a>/<a href="#bottom" >alphaballs</a><br><br>
<a name="679840"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JBR</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just messing around with metaballs(i think).<br><br><br><pre class=code>
Graphics3D 1024,768,32,0
SetBuffer BackBuffer()
ClearTextureFilters
;-----------------------------------------------------------------------------------------
camera = CreateCamera()					; camera
CameraRange camera, 512-128, 512+128			; set nicely, as 16 bit color is not as accurate
AmbientLight 255,255,255						; full bright
RotateEntity camera, 90+90, 0, 0				; looking down z
PositionEntity camera, 0, 0, 512				; raise above
CameraClsMode camera, True, True				; cls &amp; z-cls


Global Back_Texture% = CreateTexture( 1024, 1024, 1+16+32+256)
SetBuffer TextureBuffer(Back_Texture%)
	For a% = 1 To 1000
		Color Rnd(64,255),Rnd(64,255),Rnd(64,255)
		Oval Rnd(0,1023), Rnd(0,1023), 128,128, True
	Next


Global Back_Mesh% = CreateMesh()
EntityTexture Back_Mesh%, Back_Texture%
Global Back_Surf% = CreateSurface( Back_Mesh% )
v0% = AddVertex( Back_Surf%, -512, -384, 0,     0.0, 0.0 )
v1% = AddVertex( Back_Surf%, +512, -384, 0,     1.0, 0.0 )
v2% = AddVertex( Back_Surf%, +512, +384, 0,     1.0, .75 )
v3% = AddVertex( Back_Surf%, -512, +384, 0,     0.0, .75 )
AddTriangle Back_Surf%, v0,v1,v2
AddTriangle Back_Surf%, v0,v2,v3

Const Overlay_Width% = 1024/8
Const Overlay_Depth% =  768/8

Global Overlay_Texture% = CreateTexture( Overlay_Width%, Overlay_Width%, 2+16+32+256 )

Global Overlay_Mesh% = CreateMesh()
EntityTexture Overlay_Mesh%, Overlay_Texture%
Global Overlay_Surf% = CreateSurface( Overlay_Mesh% )
v0% = AddVertex( Overlay_Surf%, -512, -384, 0.1,     0.0, 0.0 )
v1% = AddVertex( Overlay_Surf%, +512, -384, 0.1,     1.0, 0.0 )
v2% = AddVertex( Overlay_Surf%, +512, +384, 0.1,     1.0, .75 )
v3% = AddVertex( Overlay_Surf%, -512, +384, 0.1,     0.0, .75 )
AddTriangle Overlay_Surf%, v0,v1,v2
AddTriangle Overlay_Surf%, v0,v2,v3




Overlay_Colour% = $ff0000			; $000000 for black

Dim Overlay_Array%( Overlay_Width%*Overlay_Depth%-1 )



Global blob_a#=0

While Not KeyHit(1) 
	For b% = 0 To Overlay_Width%*Overlay_Depth%-1		; set overlay to full colour (or a little getting through)
		Overlay_Array%( b% ) = 255 Shl 8
	Next

	For i%= 0 To 15
		xxx# = 200.0 * Cos(i%*(360/16.0)+blob_a#)
		yyy# = 200.0 * Sin(i%*(360/16.0)+blob_a#)
		overlay_blob( xxx#  , yyy#, 20/2, 15.0 )
	Next
	For i%= 0 To 15
		xxx# = 275.0 * Cos(i%*(360/16.0)+blob_a#)
		yyy# = 275.0 * Sin(i%*(360/16.0)+blob_a#)
		overlay_blob( xxx#  , yyy#, 20/2, 15.0 )
	Next
	For i%= 0 To 15
		xxx# = 375.0 * Cos(i%*(360/16.0)+blob_a#)
		yyy# = 375.0 * Sin(i%*(360/16.0)+blob_a#)
		overlay_blob( xxx#  , yyy#, 16, 15.0 )
	Next
	For i%= 0 To 15
		xxx# = 475.0 * Cos(i%*(360/16.0)+blob_a#)
		yyy# = 475.0 * Sin(i%*(360/16.0)+blob_a#)
		overlay_blob( xxx#  , yyy#, 16, 15.0 )
	Next
	overlay_blob( 0  , 0, 40/2, 15.0 )

	blob_a# = blob_a# + 1

	SetBuffer TextureBuffer(Overlay_Texture%)
	LockBuffer TextureBuffer(Overlay_Texture%)
	For y%=0 To Overlay_Depth%-1
		row% = y% * Overlay_Width%
		For x%=0 To Overlay_Width%-1
			WritePixelFast x%, y%, ((Overlay_Array%(row%+x%) Shr 8) Shl 24) + Overlay_Colour%
		Next
	Next
	UnlockBuffer TextureBuffer(Overlay_Texture%)
	SetBuffer BackBuffer()


	UpdateWorld()
	RenderWorld()
	Flip
Wend
End

Function overlay_blob( base_x#, base_y#, spread%, amp# )
	base_x# = (base_x# + 512.0)/8.0
	base_y# = (base_y# + 384.0)/8.0
	
	fx# = base_x# - Floor(base_x#)
	fy# = base_y# - Floor(base_y#)
	
	centre_x% = base_x#-fx#
	centre_y% = base_y#-fy#

	For y% = -spread% To spread%
		ycomp# = (y-fy)*(y-fy)			; done here for each row
		yy% = centre_y% + y%			; ditto

		If (yy%&gt;=0) And (yy%&lt;Overlay_Depth%) Then			; weed out the off the top 'n' bottom

			For x% = -spread% To spread%
				xx% = centre_x% + x%
				
				If (xx%&gt;=0) And (xx%&lt;Overlay_Width%) Then		; weed out the off left 'n' right
				
					d# = Sqr( (x-fx)*(x-fx) + (ycomp#) ) / spread%
			
					If (d# &lt; 0.707) Then

						hh% = Overlay_Array%( xx% + yy%*Overlay_Width% )
					
						now% = hh% - (d*d*d*d - d*d + 0.25)*amp#*255*256		; minus the height scaled
					
						If now% &lt; 0 Then now% = 0		; alpha of 0.0 is smallest
						
						Overlay_Array%( xx% + yy%*Overlay_Width% ) = now%

					EndIf
				EndIf
			Next
		EndIf
	Next
End Function
</pre><br><br>Jim <br><br></td></tr></table><br>
<a name="679842"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >puki</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Interesting.  However, what are you going to do with this? <br><br></td></tr></table><br>
<a name="679858"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JBR</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Was going to use it in a game but it didn't look as good as i'd hoped; so I thought i'd post it here.....<br><br>Jim <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
