<!DOCTYPE html><html lang="en" ><head ><title >Water Invaders</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Water Invaders</h1><a href="forums.php" >Community Forums</a>/<a href="topics.php?forum=61" >Showcase</a>/<a href="#bottom" >Water Invaders</a><br><br>
<a name="1030333"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GIB3D</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's a fun little game I made in Blitz3D.<br><br>Choose which format to download it in<br><br>Rar<br><a href="http://www.filefront.com/16657295/Water-Invaders.rar/" target="_blank">http://www.filefront.com/16657295/Water-Invaders.rar/</a><br><br>Zip<br><a href="http://www.filefront.com/16657283/Water-Invaders.zip/" target="_blank">http://www.filefront.com/16657283/Water-Invaders.zip/</a><br><br><img src="http://img413.imageshack.us/img413/1251/waterinvaders.png"><br><br>Source Code:<br>You must have FastExt and SpriteCandy to run this.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Made by Steve Wigley (GIB3D)
;Started May 15, 2010
;Ended June 4, 2010

;This game uses...
;FastExt
;SpriteCandy

;* Story:
;The player was thrown back in time &lt;enter reason here&gt;
;and must now bring his abilities back to full before he can
;defeat the one who threw him back in time (level 50)

;Once all the abilities have been upgraded completely
;the player will get a special Nuke ability that will
;kill "the one" once and for all... unless the player
;decides to start back from the beginning for some unknown reason

;When the player loses all his lives, time goes back to level 1
;but he keeps all his money

;Boss levels
;1. Level 5 : Easy         : Reward $5,000
;2. Level 15: Medium       : Reward $10,000
;3. Level 30: Hard         : Reward $25,000
;4. Level 50: Final battle : Reward $100,000


;[Block] Includes
Include "sprite candy.bb"
Include "FastExt.bb"
;[End]

;[Block] Globals
Dim ImageUpgradePos(6,1) ; Positions for upgrade icons
Dim Scene(4,1) ; scene,variable
;Scene0 = First Boss
;Scene1 = Second Boss
;Scene2 = Third Boss
;Scene3 = Final Boss
;Scene4 = Final Cutscene
;Variable0 = lines of text left
;Variable1 = this scene has been played

Global InScene,InBossLevel

Global LastMouse[1]
Global Camera
Global Menu

Global Sound_Shoot,Sound_Shoot_Volume

Global CAND_Hud
Global CAND_Resource_Boss
Global CAND_Resource_Enemy
Global CAND_Resource_Other
Global CAND_Layer_Enemy,CAND_Layer_Player,CAND_Layer_Boss,CAND_Layer_Projectile,CAND_Layer_HUD
Global HUD_Texture

Global Image_Upgrades
Global AmmoBar

Global Money
Global Difficulty
Global Score,LifeScore,Lives,Level
Global MoveTime,MoveTime2
Global StartTime,Now

Global BlackJacks
Global LevelSpeed,ProjectileSpeed#,CostSpeed
Global LevelSpread,CostSpread
Global LevelReload,ReloadSpeed#,CostReload
Global LevelAmmo,CostAmmo
Global RapidFire,CostRapidFire,MouseAim,CostMouseAim
Global CostMultiplier#
Global FullyUpgraded

Global Actor[2],FireEvents
;[End]

InitGame
MainLoop


Function MainLoop()
	While Not (Menu And KeyHit(21))
		UpdateGame
		Delay(10)
	Wend
	End
End Function

Function UpdateGame()
	VWait 60
	
	If KeyHit(1) EnableMenu
	If (KeyHit(2) Or KeyHit(79)) ToggleSound
	
	Select Menu
		Case True UpdateMenuUpgrade
		Case False UpdateMenuGame
	End Select
	
	Text GraphicsWidth()*.5,0,"Score("+Score+") Lives("+Lives+") Level("+Level+") "+GetDifficulty(),1
	Text GraphicsWidth()*.5,12,"$"+Money,1
	If InBossLevel Text GraphicsWidth()*.5,24,BossGetHealth(),1
	
	Flip 0
End Function
Function UpdateMenuUpgrade()
	Local Texts$[2],ShowText,Mouse,Highlight,CheckPointInRect=True
	Color 0,0,0
	Rect 0,0,GraphicsWidth(),GraphicsHeight()
	
	Mouse = MouseHit(1)
	
	For n = 0 To 6
		Highlight = False
		DrawImage Image_Upgrades,ImageUpgradePos(n,0),ImageUpgradePos(n,1),n
		
		Select n
			Case 0 Highlight = ReturnIfTrue(CostAmmo &gt; 0,Money =&gt; CostAmmo,2)
			Case 1 Highlight = ReturnIfTrue(CostSpeed &gt; 0,Money =&gt; CostSpeed,2)
			Case 2 Highlight = ReturnIfTrue(CostSpread &gt; 0,Money =&gt; CostSpread,2)
			Case 3 Highlight = ReturnIfTrue(CostReload &gt; 0,Money =&gt; CostReload,2)
			Case 4 Highlight = ReturnIfTrue(CostRapidFire &gt; 0,Money =&gt; CostRapidFire,2)
			Case 5 Highlight = ReturnIfTrue(CostMouseAim &gt; 0,Money =&gt; CostMouseAim,2)
			Case 6 Highlight = ReturnIfTrue(FullyUpgraded,2,0)
		End Select
		
		If CheckPointInRect
			If PointInRect(ImageUpgradePos(n,0),ImageUpgradePos(n,1),32,32,MouseX(),MouseY())
				Select n
					Case 0 ; Ammo
						Texts[0] = "Ammo Limit("+LevelAmmo+"/50"+") " + PlayerGetAmmo()
						Texts[1] = "$"+CostAmmo
						
					Case 1 ; Speed
						Texts[0] = "Projectile Speed("+LevelSpeed+"/25"+") " + ProjectileSpeed
						Texts[1] = "$"+CostSpeed
						
					Case 2 ; Projectile
						Texts[0] = "Projectile Type("+LevelSpread+"/20"+")"
						Texts[1] = "$"+CostSpread
						Texts[2] = GetPlayerProjectileType()
						
					Case 3 ; Reload
						Texts[0] = "Reload Speed("+LevelReload+"/50"+") " + ReloadSpeed
						Texts[1] = "$"+CostReload
						
					Case 4 ; Rapid Fire
						Texts[0] = "Rapid Fire = " + RapidFire
						Texts[1] = "$"+CostRapidFire
						
					Case 5 ; Mouse Aim
						Texts[0] = "Mouse Aim = " + MouseAim
						Texts[1] = "$"+CostMouseAim
						
					Case 6 ; BlackJack
						Texts[0] = "Projectile21 a.k.a. (BlackJack)  = " + BlackJacks
						
						If BlackJacks
							Texts[1] = "Press Enter to fire a BlackJack."
						Else
							Texts[1] = "Upgrade all abilities to unlock this"
						EndIf
						
				End Select
				
				ShowText = n+1
				CheckPointInRect = False
			EndIf
		EndIf
		
		Select Highlight
			Case 0 Color 128,128,128
			Case 1 Color 0,255,0
			Case 2 Color 0,0,255
		End Select
		
		Rect ImageUpgradePos(n,0)-1,ImageUpgradePos(n,1)-1,34,34,0
	Next
	
	Color 255,255,255
	If ShowText
		If Mouse
			Select ShowText
				Case 1 SetAmmoLevel
				Case 2 SetSpeedLevel
				Case 3 SetProjectileLevel
				Case 4 SetReloadLevel
				Case 5 SetRapidFire
				Case 6 SetMouseAim
			End Select
			
			FlushKeys
		EndIf
		
		Text GraphicsWidth()*.5,GraphicsHeight()-124,Texts[0],1
		Text GraphicsWidth()*.5,GraphicsHeight()-112,Texts[1],1
		Text GraphicsWidth()*.5,GraphicsHeight()-100,Texts[2],1
	EndIf
	
	Text GraphicsWidth()*.5,GraphicsHeight()-20,"Y Quit",1
End Function
Function UpdateMenuGame()
	If InScene
		HUD_Update
		RenderWorld
		UpdateScenes
		Render
	Else
		If Not Rand(0,500/Difficulty) CreateEnemy(Rand(0,1)*GraphicsWidth(),30,4)
		MoveTime2 = MoveTime2 + 1
		
		UpdatePlayer
		
		Select InBossLevel
			Case True MoveTime = MoveTime - 2 : UpdateBoss
			Default MoveTime = MoveTime - 1
		End Select
		
		UpdateEnemy
		UpdateProjectile
		
		If MoveTime &lt; 0 MoveTime = Rand(100,350)
		If MoveTime2 &gt; 180 MoveTime2 = -180
		
		Select InBossLevel
			Case False
				If CountEnemy() = 0
					InitLevel:Return
				EndIf
				
			Case True
				If CountBoss() = 0
					InitLevel:Return
				EndIf
				
		End Select
		
		If Lives =&lt; 0
			Lives = 3
			Score = 0
			LifeScore = 0
			Level = 0
			InitLevel:Return
		EndIf
		
		HUD_Update
		RenderWorld
		Render
	EndIf
	
	AppTitle "Water Invaders FPS("+SC_FPS+")"
End Function
Function Render()
	CustomPostprocessContrast(.4,5)
	RenderPostprocess(FE_Contrast)
	CustomPostprocessContrast(.8,2)
	RenderPostprocess(FE_Contrast)
End Function

Function UpdateScenes()
	Color 255,255,255
	Local Continue = KeyHit(57)
	Local X=GraphicsWidth()*.5,Y=GraphicsHeight()
	
	For n = 0 To 4
		If n+1 = InScene
			If Scene(n,0) &gt; 0
				If Continue
					FireEvents = False
					Scene(n,0) = Scene(n,0) - 1
					FlushKeys
					Exit
				EndIf
			Else
				If InScene = 5 ResetGame
				InScene = 0
			EndIf
			
			Select n
				Case 0 UpdateScene1 X,Y
				Case 1 UpdateScene2 X,Y
				Case 2 UpdateScene3 X,Y
				Case 3 UpdateScene4 X,Y
				Case 4 UpdateScene5 X,Y
			End Select
		EndIf
	Next
End Function
	Function UpdateScene1(x,y)
		Select Scene(0,0)
			Case 5
				Text x,y-60,"Boss: Why are you here?",1
			Case 4
				Text x,y-60,"Boss: I have a feeling like ",1
				Text x,y-40,"you're not supposed to be here...",1
			Case 3
				Text x,y-60,"*Message from B4*",1
			Case 2
				Text x,y-60,"Boss: Oh I see.. that's why.",1
				Text x,y-40,"You need to be dead right now",1
			Case 1
				Text x,y-60,"Boss: You don't belong... here",1
				Text x,y-40,"... now!",1
		End Select
	End Function
	Function UpdateScene2(x,y)
		Select Scene(1,0)
			Case 5
				Text x,y-60,"Boss: Hey I've seen you before!",1
			Case 4
				Text x,y-60,"Boss: Aren't you the guy that got...",1
			Case 3
				Text x,y-60,"*Message from B4*",1
			Case 2
				Text x,y-60,"Boss: Pfft, it seems so.",1
			Case 1
				Text x,y-60,"Boss: You'll never make it past me.",1
		End Select
	End Function
	Function UpdateScene3(x,y)
		Select Scene(2,0)
			Case 5
				Text x,y-60,"Boss: Hey it's you, FireMan.",1
			Case 4
				Text x,y-60,"Boss: You're the guy who",1
				Text x,y-40,"tried to take over TimeCo.",1
			Case 3
				Text x,y-60,"*Message from B4*",1
			Case 2
				Text x,y-60,"Boss: Oh well, I've got orders from",1
				Text x,y-40,"the big man himself to stop you.",1
			Case 1
				Text x,y-60,"Boss: Apparently this isn't",1
				Text x,y-40,"the first time.",1
		End Select
	End Function
	Function UpdateScene4(x,y)
		Select Scene(3,0)
			Case 5
				Text x,y-60,"B4: You've come to try it again!",1
				Text x,y-40,"Why do you still want to see",1
				Text x,y-20,"my invention? ",1
			Case 4
				Text x,y-60,"B4: How about you diggy on out",1
				Text x,y-40,"of here and you won't be hurt.",1
			Case 3
				Text x,y-60,"FireMan: I'm gonna diggy this fire",1
				Text x,y-40,"straight into you're eye!",1
			Case 2
				Text x,y-60,"B4: HAHAHA! Like that'll stop me...",1
			Case 1
				Text x,y-60,"FireMan: Huh!?",1
		End Select
	End Function
	Function UpdateScene5(x,y)
		Select Scene(4,0)
			Case 5
				Text x,y-60,"Long ago or sometime in the ",1
				Text x,y-40,"future, FireMan saw that the owner of",1
				Text x,y-20,"TimeCo, B4, had built a time machine.",1
			Case 4
				If Not FireEvents
					HUD_ClearEffects(Actor[1])
					HUD_PositionObject(Actor[1],180,GraphicsHeight()*.5+40)
					FireEvents = True
				EndIf
				Text x,y-60,"He saw that it ran completely",1
				Text x,y-40,"using water that came from many ",1
				Text x,y-20,"tubes and tunnels.",1
			Case 3
				If Not FireEvents
					HUD_FX_AlphaFade(Actor[0],1,1000)
					
					For n = 0 To 1
						HUD_FX_Shake(Actor[n],3,200,2000,1000)
					Next
					
					HUD_FX_MoveTo(Actor[0],120,GraphicsHeight()*.5+80,1000,0,0,2000)
					HUD_FX_MoveTo(Actor[0],180,GraphicsHeight()*.5+40,1000,0,10,3000)
					HUD_FX_MoveTo(Actor[0],180,GraphicsHeight()*.5+80,1000,0,0,4000)
					HUD_FX_MoveTo(Actor[1],180,GraphicsHeight()*.5,1000,0,15,3500)
					HUD_FX_AlphaFade(Actor[2],.8,1000,3500)
					FireEvents = True
				EndIf
				
				Text x,y-60,"B4 caught FireMan peeking at his ",1
				Text x,y-40,"time machine so they fought.",1
			Case 2
				If Not FireEvents
					For n = 0 To 2
						HUD_ClearEffects(Actor[n])
					Next
					
					Now = MilliSecs()-StartTime
					
					HUD_SetObjectAlpha(Actor[0],1)
					HUD_SetObjectAlpha(Actor[2],.8)
					HUD_FX_AlphaFade(Actor[0],.3,1000)
					HUD_PositionObject(Actor[0],180,GraphicsHeight()*.5+80)
					HUD_PositionObject(Actor[1],180,GraphicsHeight()*.5)
					HUD_FX_Blink(Actor[1],.5,1,2000)
					FireEvents = True
				EndIf
				
				Text x,y-80,"FireMan was stripped of his abilities",1
				Text x,y-60,"and thrown into the machine. He was",1
				Text x,y-40,"sent back to exactly",1
				Text x,y-20,MashTime(Now) + " ago.",1
			Case 1
				Text x,y-40,"And now it's happening all over again...",1
				Text x,y-20,"Water Invaders - The Neverending Battle",1
		End Select
	End Function
	
Function InitGame()
	InitOther
	InitUpgrades
	InitScenes
	InitPlayer
	InitEnemies
	InitBoss
	InitProjectile
	
	;vvv Cheating vvv
;	Level = 30
;	Money = 10000000
;	For n = 1 To 20 : SetProjectileLevel : Next
;	For n = 1 To 25 : SetSpeedLevel : Next
;	For n = 1 To 50 : SetReloadLevel : SetAmmoLevel : Next
;	SetRapidFire:SetMouseAim
	;^^^          ^^^
	InitLevel
	
	HUD_LayerToFront CAND_Layer_Boss
	HUD_LayerToFront CAND_Layer_Enemy
	HUD_LayerToFront CAND_Layer_Projectile
	HUD_LayerToFront CAND_Layer_Player
	HUD_LayerToFront CAND_Layer_HUD
	
	StartTime = MilliSecs()
End Function
Function InitOther()
	Graphics3D 800,600,32,2
	Graphics3D 320,240,32,3
	AppTitle "Water Invaders"
	InitExt
	SetBuffer BackBuffer()
	SeedRnd MilliSecs()
	
	AmbientLight 255,255,255
	Camera = CreateCamera()
	
	CAND_Hud = HUD_Create(Camera)
	CAND_Resource_Boss = HUD_LoadImageResource("Boss.png",2+256)
	CAND_Resource_Enemy = HUD_LoadImageResource("Enemies.png")
	CAND_Resource_Other = HUD_LoadImageResource("Other.png")
	
	Image_Upgrades = LoadAnimImage("Upgrades.png",32,32,0,7)
	
	
	Lives=3
	MoveTime=Rand(100,350)
	Difficulty = Clamp(Input("(1 Easy) (2 Medium) (3 Hard):"),1,3)
	DebugLog Difficulty
	
	FlushKeys:FlushMouse
	
	ImageUpgradePos(0,0) = 20:ImageUpgradePos(0,1) = 60
	ImageUpgradePos(1,0) = 60:ImageUpgradePos(1,1) = 60
	ImageUpgradePos(2,0) = 100:ImageUpgradePos(2,1) = 60
	ImageUpgradePos(3,0) = 140:ImageUpgradePos(3,1) = 60
	ImageUpgradePos(4,0) = 180:ImageUpgradePos(4,1) = 60
	ImageUpgradePos(5,0) = 220:ImageUpgradePos(5,1) = 60
	ImageUpgradePos(6,0) = 260:ImageUpgradePos(6,1) = 60
	
	CAND_Layer_HUD = HUD_CreateLayer(CAND_Hud,CAND_Resource_Other)
	AmmoBar = HUD_CreateShape(CAND_Layer_HUD,"PERCENTBAR", 100,1, 7,100, 197,10)
	HUD_SetObjectRotation AmmoBar,-90
	HUD_SetObjectGradient AmmoBar,200,150,0,0,0,0
	
	HUD_Texture = HUD_GetTextureHandle(HUD_GetLayerTexture(CAND_Layer_HUD))
End Function
Function InitScenes()
	For n = 0 To 4
		Scene(n,0) = 5
		Scene(n,1) = False
	Next
End Function
Function InitLevel()
	Level = Level + 1
	
	If FullyUpgraded BlackJacks = 3
	InBossLevel = False
	DeleteBossAll
	DeleteEnemyAll
	DeleteProjectileAll
	PlayerSetAmmo(PlayerGetMaxAmmo(),False)
	
	Select Level
		Case 5,15,30,50
			InBossLevel = True
			
			Select Level
				Case 5
					If Not Scene(0,1) InScene = 1 : Scene(0,1) = True
					CreateBoss(GraphicsWidth()*.5,80,1)
					
				Case 15
					If Not Scene(1,1) InScene = 2 : Scene(1,1) = True
					CreateBoss(GraphicsWidth()*.5,80,2)
					
				Case 30
					If Not Scene(2,1) InScene = 3 : Scene(2,1) = True
					CreateBoss(GraphicsWidth()*.5,80,3)
					
				Case 50
					If Not Scene(3,1) InScene = 4 : Scene(3,1) = True
					CreateBoss(GraphicsWidth()*.5,80,4)
					
			End Select
			
		Case 51
			InScene = 5 : Scene(3,1) = True
			
			RemoveActors
			
			Actor[0] = HUD_CreateImage(CAND_Layer_Boss,60,GraphicsHeight()*.5,0,0,0,0)
			HUD_SetObjectScale Actor[0],.25,.25
			HUD_SetObjectAlpha Actor[0],0
			
			Actor[1] = PlayerGetObject()
			HUD_FX_MoveTo Actor[1],180,GraphicsHeight()*.5+40,1000
			
			Actor[2] = HUD_CreateImage(CAND_Layer_Player,180,GraphicsHeight()*.5,11,20,8,11)
			HUD_SetObjectScale Actor[2],2,2
			HUD_SetObjectGradient Actor[2],150,150,150,50,50,50,2
		Default LoadEnemyData
	End Select
End Function
Function InitEnemies()
	CAND_Layer_Enemy = HUD_CreateLayer(CAND_Hud,CAND_Resource_Enemy)
End Function
Function InitBoss()
	CAND_Layer_Boss = HUD_CreateLayer(CAND_Hud,CAND_Resource_Boss)
End Function
Function InitPlayer()
	CAND_Layer_Player = HUD_CreateLayer(CAND_Hud,CAND_Resource_Other)
	Sound_Shoot = LoadSound("Sounds\Shoot.wav")
	Sound_Shoot_Volume = 1
	CreatePlayer(160,200)
End Function
Function InitProjectile()
	CAND_Layer_Projectile = HUD_CreateLayer(CAND_Hud,CAND_Resource_Other)
End Function
Function InitUpgrades()
	CostMultiplier = (Difficulty-1)*.03
	
	CostSpeed=100*(1+CostMultiplier)
	CostSpread=500*(1+CostMultiplier)
	CostReload=75*(1+CostMultiplier)
	CostAmmo=100*(1+CostMultiplier)
	CostRapidFire=2500*(1+CostMultiplier)
	CostMouseAim=7500*(1+CostMultiplier)
	
	LevelSpeed=1:ProjectileSpeed#=(8+LevelSpeed)*.12
	LevelSpread=1
	LevelReload=1:ReloadSpeed#=LevelReload*.01
	LevelAmmo=1
	RapidFire = 0
	MouseAim = 0
	FullyUpgraded = False
	BlackJacks = 0
End Function
Function ResetGame()
	Level = 0
	InScene = 0
	InBossLevel = False
	StartTime = MilliSecs()
	
	HUD_ClearEffects(CAND_Layer_Player)
	RemoveActors
	InitScenes
	InitUpgrades
	InitLevel
	PlayerReset
End Function
Function RemoveActors()
	For n = 0 To 2 Step 2
		If Actor[n]
			HUD_RemoveObject(Actor[n])
			Actor[n] = 0
		EndIf
	Next
End Function
Function LoadEnemyData()
	Local x,y,n
	
	Restore EnemyData
	Read x,y
	
	For h = 0 To y-1
		For w = 0 To x-1
			Read n
			CreateEnemy((w*20)+60,(h*20)+40,n)
		Next
	Next
End Function
Function GetDifficulty$()
	Select Difficulty
		Case 1 Return "Easy"
		Case 2 Return "Medium"
		Case 3 Return "Hard"
	End Select
End Function
Function GetPlayerProjectileType$()
	Select LevelSpread
		Case 1 Return "Fire"
		Case 2 Return "Fire X2"
		Case 3 Return "Fire X2 Spread"
		Case 4 Return "Fire X3"
		Case 5 Return "Fire X3 Spread"
		Case 6 Return "Fire X4"
		Case 7 Return "Fire X4 Spread"
		Case 8 Return "Fire X5"
		Case 9 Return "Fire X5 Spread"
		Case 10 Return "Plague"
		Case 11 Return "Plague X2"
		Case 12 Return "Plague X3"
		Case 13 Return "Plague X4"
		Case 14 Return "Plague X5"
		Case 15 Return "Auto Plague"
		Case 16 Return "Auto Plague X2 + Fire X2"
		Case 17 Return "Auto Plague X3 + Fire X3"
		Case 18 Return "Auto Plague X4 + Fire X4"
		Case 19 Return "Auto Plague X5 + Fire X5"
		Case 20 Return "Fast Auto Plague X5 + Fire X5"
	End Select
End Function
Function ToggleSound()
	Sound_Shoot_Volume = Not Sound_Shoot_Volume
	SoundVolume Sound_Shoot,Sound_Shoot_Volume
	FlushKeys
End Function


Function SetMoney(value,relative=True)
	Select relative
		Case True Money = Money + value
		Case False Money = value
	End Select
End Function
Function SetScore(value,relative=True)
	Select relative
		Case True Score = Score + value
		Case False Score = value
	End Select
	
	While Score-LifeScore &gt; 10000
		SetLife(1)
		LifeScore = LifeScore + 10000
	Wend
End Function
Function SetLife(value,relative=True)
	Select relative
		Case True Lives = Lives + value
		Case False Lives = value
	End Select
End Function
Function SetSpeedLevel()
	If LevelSpeed &lt; 25
		If Money =&gt; CostSpeed
			SetMoney(-CostSpeed)
			CostSpeed=CostSpeed*(1.25+CostMultiplier)
			LevelSpeed = LevelSpeed + 1
			ProjectileSpeed = (8+LevelSpeed)*.12
			
			If LevelSpeed =&gt; 25 CostSpeed = 0 : HasAllUpgrades
		EndIf
	EndIf
End Function
Function SetProjectileLevel()
	If LevelSpread &lt; 20
		If Money =&gt; CostSpread
			SetMoney(-CostSpread)
			CostSpread=CostSpread*(1.13+CostMultiplier)
			LevelSpread = LevelSpread + 1
			
			If LevelSpread =&gt; 20 CostSpread = 0 : HasAllUpgrades
		EndIf
	EndIf
End Function
Function SetReloadLevel()
	If LevelReload &lt; 50
		If Money =&gt; CostReload
			SetMoney(-CostReload)
			CostReload=CostReload*(1.14+CostMultiplier)
			LevelReload = LevelReload + 1
			ReloadSpeed#=LevelReload*.01
			
			If LevelReload =&gt; 50 CostReload = 0 : HasAllUpgrades
		EndIf
	EndIf
End Function
Function SetAmmoLevel()
	If LevelAmmo &lt; 50
		If Money =&gt; CostAmmo
			SetMoney(-CostAmmo)
			PlayerSetAmmoLimit(1)
			CostAmmo=CostAmmo*(1.12+CostMultiplier)
			LevelAmmo = LevelAmmo + 1
			
			If LevelAmmo =&gt; 50 CostAmmo = 0 : PlayerSetAmmoLimit(51) : HasAllUpgrades
		EndIf
	EndIf
End Function
Function SetRapidFire()
	If Not RapidFire
		If Money =&gt; CostRapidFire
			SetMoney(-CostRapidFire)
			RapidFire = True
			CostRapidFire = 0
			HasAllUpgrades
		EndIf
	EndIf
End Function
Function SetMouseAim()
	If Not MouseAim
		If Money =&gt; CostMouseAim
			SetMoney(-CostMouseAim)
			MouseAim = True
			CostMouseAim = 0
			HasAllUpgrades
		EndIf
	EndIf
End Function
Function EnableMenu()
	Menu = Not Menu:FlushKeys
	
	If Menu
		LastMouse[0] = MouseX()
		LastMouse[1] = MouseY()
	Else
		MoveMouse LastMouse[0],LastMouse[1]
	EndIf
End Function
Function HasAllUpgrades()
	If CostMouseAim Or CostRapidFire Or CostAmmo Or CostReload Or CostSpread Or CostSpeed
		Return
	EndIf
	
	FullyUpgraded = True
	BlackJacks = 3
End Function



Type Player
	Field Point
	Field X#,Y#
	
	Field AmmoLimit,Ammo#
	
	Field AutoAmmoTime
	
	Field Control
End Type
	Function CreatePlayer(x#,y#)
		Local a.Player = New Player
			a\X = x:a\Y = y
			a\AmmoLimit = 10:a\Ammo = a\AmmoLimit
			LoadPlayer(a)
		Return Handle(a)
	End Function
	Function LoadPlayer(a.Player)
	;	Local clip_x#=1,clip_y#=1,clip_width#=30,clip_height#=20
		Local clip_x#=3,clip_y#=3,clip_width#=15,clip_height#=17
		
		a\Point = HUD_CreateImage(CAND_Layer_Player,a\X,a\Y,clip_x,clip_y,clip_width,clip_height)
		HUD_AnimateImage a\Point,1,1,2,2,"PENDULUM",1,150
		HUD_SetObjectGradient a\Point,100,100,100,50,50,50
	End Function
	Function UpdatePlayer()
		Local a.Player
		
		For a = Each Player
			If MouseAim
				If MouseXSpeed() Or GetMouse()
					a\Control = True
				Else
					a\Control = False
				EndIf
			EndIf
			
			Select a\Control
				Case True If MouseAim a\X = MouseX()
				Case False a\X=a\X+(((KeyDown(32) Or KeyDown(205))-(KeyDown(30) Or KeyDown(203)))*((KeyDown(42) Or KeyDown(54))+1))
			End Select
			
			If a\X &gt; 280
				a\X = 280
			ElseIf a\X &lt; 40
				a\X = 40
			EndIf
			
			PlayerSetPosition a\X,a\Y
			
			If Not (MouseAim And MouseDown(2)) Or ((KeyDown(42) Or KeyDown(54)) And KeyDown(57))
				If a\Ammo &lt; a\AmmoLimit PlayerSetAmmo(ReloadSpeed)
			EndIf
			
			If (MouseAim And MouseHit(1)) Or KeyHit(57)
				If PlayerCanShoot() PlayerShoot(-Difficulty,LevelSpread)
			EndIf
			
			If RapidFire
				If (MouseAim And MouseDown(2)) Or ((KeyDown(42) Or KeyDown(54)) And KeyDown(57))
					If PlayerCanShoot() PlayerShoot(-Difficulty,LevelSpread)
				EndIf
			EndIf
			
			If KeyHit(28)
				PlayerShoot(-110,21)
			EndIf
			
			If LevelSpread &gt; 14
				a\AutoAmmoTime = a\AutoAmmoTime - 1
				
				If a\AutoAmmoTime &lt;= 0
					If LevelSpread = 20
						a\AutoAmmoTime = 30
					Else
						a\AutoAmmoTime = 60
					EndIf
					
					Select LevelSpread
						Case 15
							CreateProjectile(a\X,a\Y,0,3)
						Case 16;||
							CreateProjectile(a\X-2,a\Y,0,3)
							CreateProjectile(a\X+2,a\Y,0,3)
						Case 17;|||
							CreateProjectile(a\X-4,a\Y,0,3)
							CreateProjectile(a\X,a\Y,0,3)
							CreateProjectile(a\X+4,a\Y,0,3)
						Case 18;||||
							CreateProjectile(a\X-4,a\Y,0,3)
							CreateProjectile(a\X-2,a\Y,0,3)
							CreateProjectile(a\X+2,a\Y,0,3)
							CreateProjectile(a\X+4,a\Y,0,3)
						Case 19,20;|||||
							CreateProjectile(a\X-4,a\Y,0,3)
							CreateProjectile(a\X-2,a\Y,0,3)
							CreateProjectile(a\X,a\Y,0,3)
							CreateProjectile(a\X+2,a\Y,0,3)
							CreateProjectile(a\X+4,a\Y,0,3)
					End Select
				EndIf
			EndIf
		Next
	End Function
	Function PlayerShoot(ammo,Projectile_type)
		Local a.Player = First Player
		
		If Handle(a)
			Select Projectile_type
				Case 10,15,11,16,12,17,13,18,14,19
					ammo=ammo-2
			End Select
			
			If a\Ammo+ammo =&gt; 0
				Select Projectile_type
					Case 1;|
						CreateProjectile(a\X,a\Y,180,1)
					Case 2;||
						CreateProjectile(a\X-2,a\Y,180,1)
						CreateProjectile(a\X+2,a\Y,180,1)
					Case 3;\/
						CreateProjectile(a\X-2,a\Y,175,1)
						CreateProjectile(a\X+2,a\Y,185,1)
					Case 4;|||
						CreateProjectile(a\X-4,a\Y,180,1)
						CreateProjectile(a\X,a\Y,180,1)
						CreateProjectile(a\X+4,a\Y,180,1)
					Case 5;\|/
						CreateProjectile(a\X-4,a\Y,175,1)
						CreateProjectile(a\X,a\Y,180,1)
						CreateProjectile(a\X+4,a\Y,185,1)
					Case 6;||||
						CreateProjectile(a\X-4,a\Y,180,1)
						CreateProjectile(a\X-2,a\Y,180,1)
						CreateProjectile(a\X+2,a\Y,180,1)
						CreateProjectile(a\X+4,a\Y,180,1)
					Case 7;\||/
						CreateProjectile(a\X-4,a\Y,175,1)
						CreateProjectile(a\X-2,a\Y,180,1)
						CreateProjectile(a\X+2,a\Y,180,1)
						CreateProjectile(a\X+4,a\Y,185,1)
					Case 8;|||||
						CreateProjectile(a\X-4,a\Y,180,1)
						CreateProjectile(a\X-2,a\Y,180,1)
						CreateProjectile(a\X,a\Y,180,1)
						CreateProjectile(a\X+2,a\Y,180,1)
						CreateProjectile(a\X+4,a\Y,180,1)
					Case 9;\|||/
						CreateProjectile(a\X-4,a\Y,170,1)
						CreateProjectile(a\X-2,a\Y,175,1)
						CreateProjectile(a\X,a\Y,180,1)
						CreateProjectile(a\X+2,a\Y,185,1)
						CreateProjectile(a\X+4,a\Y,190,1)
						
					;Plague
					Case 10,15
						CreateProjectile(a\X,a\Y,0,3)
					Case 11,16;||
						CreateProjectile(a\X-2,a\Y,0,3)
						CreateProjectile(a\X+2,a\Y,0,3)
						
						If Projectile_type = 16
							CreateProjectile(a\X-2,a\Y,180,1)
							CreateProjectile(a\X+2,a\Y,180,1)
						EndIf
					Case 12,17;|||
						CreateProjectile(a\X-4,a\Y,0,3)
						CreateProjectile(a\X,a\Y,0,3)
						CreateProjectile(a\X+4,a\Y,0,3)
						
						If Projectile_type = 17
							CreateProjectile(a\X-4,a\Y,180,1)
							CreateProjectile(a\X,a\Y,180,1)
							CreateProjectile(a\X+4,a\Y,180,1)
						EndIf
					Case 13,18;||||
						CreateProjectile(a\X-4,a\Y,0,3)
						CreateProjectile(a\X-2,a\Y,0,3)
						CreateProjectile(a\X+2,a\Y,0,3)
						CreateProjectile(a\X+4,a\Y,0,3)
						
						If Projectile_type = 18
							CreateProjectile(a\X-4,a\Y,180,1)
							CreateProjectile(a\X-2,a\Y,180,1)
							CreateProjectile(a\X+2,a\Y,180,1)
							CreateProjectile(a\X+4,a\Y,180,1)
						EndIf
					Case 14,19,20;|||||
						CreateProjectile(a\X-4,a\Y,0,3)
						CreateProjectile(a\X-2,a\Y,0,3)
						CreateProjectile(a\X,a\Y,0,3)
						CreateProjectile(a\X+2,a\Y,0,3)
						CreateProjectile(a\X+4,a\Y,0,3)
						
						If Projectile_type = 19 Or Projectile_type = 20
							CreateProjectile(a\X-4,a\Y,180,1)
							CreateProjectile(a\X-2,a\Y,180,1)
							CreateProjectile(a\X,a\Y,180,1)
							CreateProjectile(a\X+2,a\Y,180,1)
							CreateProjectile(a\X+4,a\Y,180,1)
						EndIf
					Case 21
						If BlackJacks &gt; 0
							CreateProjectile(a\X,a\Y,180,4)
							BlackJacks = BlackJacks - 1
						Else
							ammo = 0
						EndIf
				End Select
				
				PlayerSetAmmo(ammo)
				
				Select Projectile_type
					Case 21
						If BlackJacks &gt; 0 HUD_PlayImageAnimations a\Point
					Default
						HUD_PlayImageAnimations a\Point
						PlaySound Sound_Shoot
				End Select
			EndIf
		EndIf
	End Function
	Function PlayerCanShoot()
		Local a.Player = First Player
		
		If Handle(a)
			If a\Ammo &lt;= a\AmmoLimit And a\Ammo &gt; 0 Return True
		EndIf
	End Function
	Function PlayerGetAmmo$()
		Local a.Player = First Player
		
		If Handle(a)
			Return Int(a\Ammo)+"/"+a\AmmoLimit
		EndIf
	End Function
	Function PlayerGetMaxAmmo()
		Local a.Player = First Player
		
		If Handle(a)
			Return a\AmmoLimit
		EndIf
	End Function
	Function PlayerSetAmmo(value#,relative=True)
		Local a.Player = First Player
		
		If Handle(a)
			Select relative
				Case True a\Ammo = a\Ammo + value
				Case False a\Ammo = value
			End Select
			
			If a\Ammo &gt; a\AmmoLimit a\Ammo = a\AmmoLimit
			
			HUD_UpdateShape AmmoBar, "PERCENTBAR" , (a\Ammo/a\AmmoLimit)*100,2
		EndIf
	End Function
	Function PlayerSetAmmoLimit(value#,relative=True)
		Local a.Player = First Player
		
		If Handle(a)
			Select relative
				Case True a\AmmoLimit = a\AmmoLimit + value
				Case False a\AmmoLimit = value
			End Select
			
			PlayerSetAmmo(value)
		EndIf
	End Function
	Function PlayerKill()
		SetLife(-1)
	End Function
	Function PlayerSetPosition(x#,y#)
		Local a.Player = First Player
		
		If Handle(a)
			HUD_PositionObject a\Point,x,y
		EndIf
	End Function
	Function PlayerGetObject()
		Local a.Player = First Player
		
		If Handle(a)
			Return a\Point
		EndIf
	End Function
	Function PlayerReset()
		PlayerSetAmmoLimit(10,False)
		PlayerSetAmmo(10,False)
	End Function
	
Type Enemy
	Field Point
	Field Health
	Field Dead
	
	Field FireTime,FireTimeDefault
	
	Field X#,Y#
	Field StartX#,StartY#
	Field EnemyType
End Type
	Function CreateEnemy(x#,y#,enemy_type)
		Local a.Enemy = New Enemy
			a\X = x:a\Y = y
			a\StartX = x:a\StartY = y
			a\EnemyType = enemy_type
			
			
			
			Select a\EnemyType
				Case 4
					a\Health = 1
					a\FireTimeDefault = Rand(10,50/Difficulty)
				Default
					a\Health = Level
					a\FireTimeDefault = (3000/Difficulty)/a\EnemyType
			End Select
			
			a\FireTime = a\FireTimeDefault
			
			LoadEnemy(a)
		Return Handle(a)
	End Function
	Function LoadEnemy(a.Enemy)
		Local clip_x#=1,clip_y#=1,clip_width#,clip_height#,removetime#
		
		Select a\EnemyType
			Case 1 clip_width = 8:clip_height = 8
			Case 2 clip_width = 11:clip_height = 8:clip_y = 10
			Case 3 clip_width = 12:clip_height = 8:clip_y = 19
			Case 4 clip_width = 15:clip_height = 10:clip_y = 28
		End Select
		
		a\Point = HUD_CreateImage(CAND_Layer_Enemy,a\X,a\Y,clip_x,clip_y,clip_width,clip_height)
		
		Select a\EnemyType
			Case 1 HUD_SetObjectGradient a\Point,80,10,50,200,100,10:HUD_AnimateImage a\Point,1,clip_y,2,2,"NORMAL",0,500
			Case 2 HUD_SetObjectGradient a\Point,10,200,10,50,10,50:HUD_AnimateImage a\Point,1,clip_y,2,2,"NORMAL",0,400
			Case 3 HUD_SetObjectGradient a\Point,10,100,200,20,10,20,2:HUD_AnimateImage a\Point,1,clip_y,2,2,"NORMAL",0,600
			Case 4 HUD_SetObjectGradient a\Point,255,255,255, 55,55,255
			
			If GraphicsWidth()-a\X = GraphicsWidth() HUD_MirrorImage a\Point,True
			
			removetime = (5000*(Difficulty*.85))
			HUD_FX_MoveTo a\Point, GraphicsWidth()-a\X,a\Y,removetime
			HUD_FX_AutoRemove a\Point,removetime
		End Select
	End Function
	Function UpdateEnemy()
		Local a.Enemy
		Local Time# = MoveTime2
		Local X# = Int(((Cos(Time)*20))/10.0)*10
		Local Y# = Int(((Sin(Time)*20))/10.0)*10
		
		For a = Each Enemy
			Select a\Dead
				Case 0
					If HUD_ObjectExists(a\Point)
						Select a\EnemyType
							Case 1,2,3
								If MoveTime =&lt; 0
									HUD_FX_MoveTo a\Point,a\StartX+X,a\StartY+Y, 1000,10,3
								EndIf
						End Select
						
						a\X = HUD_GetObjectX(a\Point)
						a\Y = HUD_GetObjectY(a\Point)
						
						Select a\EnemyType
							Case 1,2,3
								If Not Rand(0,a\FireTimeDefault)
									CreateProjectile(a\X,a\Y,0,2)
								EndIf
								
							Case 4
								a\FireTime = a\FireTime - 1
								DebugLog a\FireTime
								If a\FireTime =&lt; 0
									CreateProjectile(a\X,a\Y,0,2)
									a\FireTime = a\FireTimeDefault
								EndIf
						End Select
					Else
						DeleteEnemy(a)
					EndIf
				Case 1
					If Not HUD_ObjectExists(a\Point)
						DeleteEnemy(a)
					EndIf
			End Select
		Next
	End Function
	Function CountEnemy()
		Local a.Enemy,Number
		
		For a = Each Enemy
			Number = Number + 1
		Next
		
		Return Number
	End Function
	Function GetEnemyDead(ObjectID)
		Local a.Enemy
		
		For a = Each Enemy
			If a\Point = ObjectID
				Return a\Dead
			EndIf
		Next
	End Function
	Function EnemyKill(a.Enemy,health=-1)
		If a\Dead = 0
			a\Health = a\Health + health
			If a\Health =&lt; 0
				Select a\EnemyType
					Case 1 SetScore(Rand(200,500)*(1+((Level*.025)+CostMultiplier))):PlayerSetAmmo(.8):SetMoney(Rand(10,20)*(Level*1.2))
					Case 2 SetScore(Rand(50,100)*(1+((Level*.025)+CostMultiplier))):PlayerSetAmmo(.7):SetMoney(Rand(5,9)*(Level*1.2))
					Case 3 SetScore(Rand(10,25)*(1+((Level*.025)+CostMultiplier))):PlayerSetAmmo(.5):SetMoney(Rand(1,4)*(Level*1.2))
					Case 4 SetScore(Rand(500,1000)*(1+((Level*.025)+CostMultiplier))):PlayerSetAmmo(.5):SetMoney(Rand(50,100)*(Level*1.2))
				End Select
				
				a\Dead = 1
				HUD_FX_Ripple a\Point,10,10,100,1000,0,Rand(0,4)
				HUD_FX_AlphaFade a\Point,0,1000
				HUD_FX_AutoRemove a\Point,1100
			EndIf
		EndIf
	End Function
	Function DeleteEnemy(a.Enemy)
		If HUD_ObjectExists(a\Point) HUD_RemoveObject(a\Point)
		Delete a
	End Function
	Function DeleteEnemyAll()
		Local a.Enemy
		
		For a = Each Enemy
			DeleteEnemy(a)
		Next
	End Function


Type Boss
	Field Point
	Field Health
	
	Field X#,Y#
	Field StartX#,StartY#
	Field BossType
End Type
	Function CreateBoss(x#,y#,boss_type)
		Local a.Boss = New Boss
			a\X = x:a\Y = y
			a\StartX = x:a\StartY = y
			a\BossType = boss_type
			
			Select a\BossType
				Case 1 a\Health = 30
				Case 2 a\Health = 300
				Case 3 a\Health = 3000
				Case 4 a\Health = 99999999
			End Select
			
			LoadBoss(a)
		Return Handle(a)
	End Function
	Function LoadBoss(a.Boss)
		a\Point = HUD_CreateImage(CAND_Layer_Boss,a\X,a\Y)
		HUD_SetObjectScale a\Point,.25,.25
		HUD_SetObjectAlpha a\Point,.4
		
;		Select a\BossType
;			Case 1 HUD_SetObjectGradient a\Point,100,100,100,100,100,100;HUD_AnimateImage a\Point,1,clip_y,2,2,"NORMAL",0,500
;			Case 1 HUD_SetObjectGradient a\Point,10,200,10,50,10,50;HUD_AnimateImage a\Point,1,clip_y,2,2,"NORMAL",0,400
;			Case 1 HUD_SetObjectGradient a\Point,10,100,200,20,10,20,2;HUD_AnimateImage a\Point,1,clip_y,2,2,"NORMAL",0,600
;		End Select
	End Function
	Function UpdateBoss()
		Local a.Boss
		Local Time# = MoveTime2,RandX
		Local X# = Int(((Cos(Time)*20))/10.0)*10
		Local Y# = Int(((Sin(Time)*20))/10.0)*10
		
		For a = Each Boss
			If MoveTime =&lt; 0
				HUD_FX_MoveTo a\Point,a\StartX+X,a\StartY+Y, 500,10,10
			EndIf
			
			a\X = HUD_GetObjectX(a\Point)
			a\Y = HUD_GetObjectY(a\Point)
			
			If Not Rand(0,(30/Difficulty)/a\BossType)
				RandX = Rand(32,-32)
				
				Select Rand(0,2)
					Case 0
						CreateProjectile(RandX+a\X+8,a\Y-6,0,2)
						CreateProjectile(RandX+a\X+6,a\Y-4,0,2)
						CreateProjectile(RandX+a\X+4,a\Y-2,0,2)
						CreateProjectile(RandX+a\X+2,a\Y,0,2)
						CreateProjectile(RandX+a\X,a\Y+2,0,2)
						CreateProjectile(RandX+a\X-2,a\Y,0,2)
						CreateProjectile(RandX+a\X-4,a\Y-2,0,2)
						CreateProjectile(RandX+a\X-6,a\Y-4,0,2)
						CreateProjectile(RandX+a\X-8,a\Y-6,0,2)
						
					Case 1
						CreateProjectile(RandX+a\X+8,a\Y,0,2)
						CreateProjectile(RandX+a\X+6,a\Y,0,2)
						CreateProjectile(RandX+a\X+4,a\Y,0,2)
						CreateProjectile(RandX+a\X+2,a\Y,0,2)
						CreateProjectile(RandX+a\X,a\Y+2,0,2)
						CreateProjectile(RandX+a\X-2,a\Y,0,2)
						CreateProjectile(RandX+a\X-4,a\Y,0,2)
						CreateProjectile(RandX+a\X-6,a\Y,0,2)
						CreateProjectile(RandX+a\X-8,a\Y,0,2)
						
					Case 2
						CreateProjectile(RandX+a\X+16,a\Y,0,2)
						CreateProjectile(RandX+a\X+12,a\Y,0,2)
						CreateProjectile(RandX+a\X+8,a\Y,0,2)
						CreateProjectile(RandX+a\X+4,a\Y,0,2)
						CreateProjectile(RandX+a\X,a\Y+2,0,2)
						CreateProjectile(RandX+a\X-4,a\Y,0,2)
						CreateProjectile(RandX+a\X-8,a\Y,0,2)
						CreateProjectile(RandX+a\X-12,a\Y,0,2)
						CreateProjectile(RandX+a\X-16,a\Y,0,2)
						
				End Select
			EndIf
		Next
	End Function
	Function BossGetHealth()
		Local a.Boss = First Boss
		
		If Handle(a)
			Return a\Health
		EndIf
	End Function
	Function CountBoss()
		Local a.Boss,Number
		
		For a = Each Boss
			Number = Number + 1
		Next
		
		Return Number
	End Function
	Function BossKill(a.Boss,health=-1)
		a\Health = a\Health + health
		If a\Health =&lt; 0
			Select a\BossType
				Case 1 SetMoney(5000)
				Case 2 SetMoney(10000)
				Case 3 SetMoney(25000)
				Case 4 SetMoney(100000)
			End Select
			
			DeleteBoss(a)
		EndIf
	End Function
	Function DeleteBoss(a.Boss)
		HUD_RemoveObject(a\Point)
		Delete a
	End Function
	Function DeleteBossAll()
		Local a.Boss
		
		For a = Each Boss
			DeleteBoss(a)
		Next
	End Function


Type Projectile
	Field Point
	Field X#,Y#
	Field ProjectileType
	
	Field Target
End Type
	Function CreateProjectile(x#,y#,angle#,projectile_type)
		Local a.Projectile = New Projectile
			a\X = x:a\Y = y
			a\ProjectileType = projectile_type
			LoadProjectile(a,angle)
			If projectile_type = 3 ProjectileFindEnemy(a)
			If projectile_type = 4 HUD_FX_MoveTo a\Point,GraphicsWidth()*.5,100,1000
		Return Handle(a)
	End Function
	Function LoadProjectile(a.Projectile,angle#)
		Local clip_x#,clip_y#,clip_width#,clip_height#
		
		Select a\ProjectileType
			Case 1,3,4 clip_width = 5:clip_height = 5:clip_x = 1:clip_y = 26
			Case 2 clip_width = 3:clip_height = 5:clip_x = 7:clip_y = 26
		End Select
		
		a\Point = HUD_CreateImage(CAND_Layer_Projectile,a\X,a\Y,clip_x,clip_y,clip_width,clip_height)
		
		Select a\ProjectileType
			Case 1 HUD_SetObjectGradient a\Point,10,200,10,50,80,50,2
			Case 2 HUD_SetObjectGradient a\Point,200,200,200,50,50,50
			Case 3 HUD_SetObjectGradient a\Point,255,10,10,255,50,50,2
			Case 4 HUD_SetObjectGradient a\Point,255,200,10,255,240,50,2
		End Select
		
		HUD_SetObjectRotation a\Point,angle
	End Function
	Function UpdateProjectile()
		Local a.Projectile,DeleteMe,Search
		
		For a = Each Projectile
			DeleteMe = False:Search = False
			
			Select a\ProjectileType
				Case 1 HUD_MoveObject a\Point,0,ProjectileSpeed,True ;Player Missile
				Case 2 HUD_MoveObject a\Point,0,Difficulty+(Level*.1),True ; Enemy Missile
				Case 3 ; Homing Missile
					If HUD_ObjectExists(a\Target)
						If GetEnemyDead(a\Target)
							Search = True
						Else
							HUD_MoveObject a\Point,0,-Clamp(Distance(a\X,a\Y,HUD_GetObjectX(a\Target),HUD_GetObjectY(a\Target)),.5,10)*.1,True
						EndIf
					Else
						Search = True
					EndIf
					
					If Search
						HUD_MoveObject a\Point,0,-ProjectileSpeed,True
						ProjectileFindEnemy(a)
					EndIf
			End Select
			
			a\X = HUD_GetObjectX(a\Point)
			a\Y = HUD_GetObjectY(a\Point)
			
			If a\Y &lt; 0 Or a\Y &gt; GraphicsHeight() Or a\X &lt; 0 Or a\X &gt; GraphicsWidth()
				DeleteMe = True
			Else
				DeleteMe = ProjectileHit(a)
			EndIf
			
			If DeleteMe DeleteProjectile(a)
		Next
	End Function
	Function ProjectileFindEnemy(a.Projectile)
		Local b.Enemy
		Local D#,D2#
		
		For b = Each Enemy
			If Not b\Dead
				D = Distance(a\X,a\Y,b\X,b\Y)
				
				If D &lt;= 40
					a\Target = b\Point
					Exit
				EndIf
			EndIf
		Next
		
		If HUD_ObjectExists(a\Target)
			For b = Each Enemy
				If Not b\Dead
					D2 = Distance(a\X,a\Y,b\X,b\Y)
					
					If D2 &lt;= 40
						If D2 &lt; D
							a\Target = b\Point
						EndIf
					EndIf
				EndIf
			Next
			
			HUD_FX_FaceObject a\Point, a\Target,2
		EndIf
	End Function
	Function ProjectileHit(a.Projectile)
		Local b.Enemy,c.Player,d.Boss,DeleteMe,Shape
		
		Select a\ProjectileType
			Case 1,3
				
				For b = Each Enemy
					If Not b\Dead
						If Distance(a\X,a\Y,b\X,b\Y) &lt; 7
							If HUD_ObjectHitsObject(a\Point,b\Point)
								EnemyKill(b)
								ProjectileRedetermineTarget
								DeleteMe = True
								Exit
							EndIf
						EndIf
					EndIf
				Next
					
				If InBossLevel
					If Not DeleteMe
						For d = Each Boss
							If Distance(a\X,a\Y,d\X,d\Y) &lt; 32
								If HUD_ObjectHitsObject(a\Point,d\Point)
									BossKill(d)
									DeleteMe = True
									Exit
								EndIf
							EndIf
						Next
					EndIf
				EndIf
				
			Case 2
				
				For c = Each Player
					If Distance(a\X,a\Y,c\X,c\Y) &lt; 6
						If HUD_ObjectHitsObject(a\Point,c\Point)
							PlayerKill
							DeleteMe = True
							Exit
						EndIf
					EndIf
				Next
				
			Case 4
				If Distance(a\X,a\Y,GraphicsWidth()*.5,100) =&lt; 1
					DeleteMe = True
					
					Shape = HUD_CreateShape(CAND_Layer_Projectile,"RECTANGLE",1,0, 0,0,GraphicsWidth(),120, "CENTER","CENTER-20")
					HUD_SetObjectGradient Shape,200,150,0, 120,110,110
					HUD_SetObjectAlpha Shape,.4
					HUD_FX_Ripple Shape,1,5,10,0
					HUD_FX_AutoRemove Shape,3000
					
					d = First Boss
					
					If Handle(d)
						BossKill(d,-33333333)
					EndIf
					
					For b = Each Enemy
						If Not b\Dead
							EnemyKill(b,-33333333)
						EndIf
					Next
				EndIf
				
		End Select
		
		Return DeleteMe
	End Function
	Function ProjectileRedetermineTarget()
		Local a.Projectile
		
		For a = Each Projectile
			If a\ProjectileType = 3
				If Not HUD_ObjectExists(a\Target)
					ProjectileFindEnemy(a)
				EndIf
			EndIf
		Next
	End Function
	Function DeleteProjectile(a.Projectile)
		HUD_RemoveObject(a\Point)
		Delete a
	End Function
	Function DeleteProjectileAll()
		Local a.Projectile
		
		For a = Each Projectile
			DeleteProjectile(a)
		Next
		
		HUD_ClearLayer CAND_Layer_Projectile
	End Function


Function PointInRect(x1, y1, w, h, x2, y2)
	If x2 &lt; x1 Or x2 &gt; (x1+w) Return False
	If y2 &lt; y1 Or y2 &gt; (y1+h) Return False
	Return True
End Function
Function Clamp#(value#,low#,high#)
	If value&lt;low Then Return low
	If value&gt;high Then Return high
	Return value
End Function
Function Distance#(x1#,y1#,x2#,y2#)
	Return Sqr((x1-x2)*(x1-x2)+(y1-y2)*(y1-y2))
End Function
Function ReturnIfTrue$(boolean,true_value$,false_value$="")
	If boolean
		Return true_value
	Else
		Return false_value
	EndIf
End Function
Function GetTime(ret$,time)
	;ret = Replace(ret,"day",Replace(LSet(time/1000/60/60/24,2)," ","0"))
	;ret = Replace(ret,"hour",Replace(RSet(time/1000/60/60-(time/1000/60/60/24*24),2)," ","0"))
	ret = Replace(ret,"hour",Replace(RSet(time/1000/60/60-(time/1000/60/60/24),2)," ","0"))
	ret = Replace(ret,"min",Replace(RSet(time/1000/60-(time/1000/60/60*60),2)," ","0"))
	ret = Replace(ret,"sec",Replace(RSet(time/1000-(time/1000/60*60),2)," ","0"))
	Return ret
End Function
Function MashTime$(time)
	Local Rtrn$,Hour,Min,Sec
		Hour = GetTime("hour",time)
		Min = GetTime("min",time)
		Sec = GetTime("sec",time)
		
		If Hour &gt; 0
			Rtrn = Rtrn + Hour + " hour" + ReturnIfTrue(Hour&gt;1,"s")
		EndIf
		
		If Min &gt; 0
			Rtrn = Rtrn + ReturnIfTrue(Rtrn&lt;&gt;"",", ") + Min + " minute" + ReturnIfTrue(Min&gt;1,"s")
		EndIf
		
		If Sec &gt; 0
			Rtrn = Rtrn +  ReturnIfTrue(Rtrn&lt;&gt;""," and ") + Sec + " second" + ReturnIfTrue(Sec&gt;1,"s")
		EndIf
	Return Rtrn
End Function

.EnemyData
Data 11,5
Data 1,1,1,1,1,1,1,1,1,1,1
Data 2,2,2,2,2,2,2,2,2,2,2
Data 2,2,2,2,2,2,2,2,2,2,2
Data 3,3,3,3,3,3,3,3,3,3,3
Data 3,3,3,3,3,3,3,3,3,3,3

</textarea> <br><br></td></tr></table><br>
<a name="1030393"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >WERDNA</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Pure awesome :)<br><br>Very cool upgrade system.<br><br>Plus the boss character is funny, lol. <br><br></td></tr></table><br>
<a name="1030408"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> NICE ENDING!!!<br><br>very creative as well :)  Good job on the scripted scenes those are really a lot harder than people give credit for. <br><br></td></tr></table><br>
<a name="1030410"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Oiduts Studios</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> That was quite fun. I play for about 20 mins and decided to just let my guy sit there for about 3 hours and came back and had 700,000 credits. So I got blackjack and beat it. Nice upgrades <br><br></td></tr></table><br>
<a name="1030416"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GIB3D</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm glad you guys like it ;) <br><br></td></tr></table><br>
<a name="1030575"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chugs40</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Original for a invaders shoot 'em up. I like the upgrades. <br><br></td></tr></table><br>
<a name="1030763"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >6</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cool game for such a small package.  Nice one. <br><br></td></tr></table><br>
<a name="1031412"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> That's really one heck of a fun little game! Congrats! ;D <br><br></td></tr></table><br>
<a name="1031447"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GIB3D</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> I bet you all are wanting to see more. How about I give you all a little more...<br>Who wants me to post the source code!? <br><br></td></tr></table><br>
<a name="1031457"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >stanrol</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> me <br><br></td></tr></table><br>
<a name="1031505"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GIB3D</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Source Code posted! <br><br></td></tr></table><br>
<a name="1031653"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Terry B.</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is awesome I just played it for an hour and a half. <br>...Now to do something productive :( <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
