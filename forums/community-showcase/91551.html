<!DOCTYPE html><html lang="en" ><head ><title >Realtime Cloth Simulation in Blitz3D</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Realtime Cloth Simulation in Blitz3D</h1><a href="forums.php" >Community Forums</a>/<a href="topics.php?forum=61" >Showcase</a>/<a href="#bottom" >Realtime Cloth Simulation in Blitz3D</a><br><br>
<a name="1041636"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Noobody</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just a quick demo of uses of verlet integration in cloth simulation - nothing too fancy.<br><br>Screenshot:<br><img src="http://www.noobody.org/BBP/ClothSimulation_2.png"><br><br>With CLOTH_SIZE = 128 (rather slow):<br><img src="http://www.noobody.org/BBP/ClothSimulation_3.png"><br><br><b>Download (Win exe):</b> <a href="http://www.noobody.org/BBP/ClothSimulation.exe" target="_blank">Link</a><br><br>Code (Blitz3D):<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Const CLOTH_SIZE = 128

Const GRAVITY_X# = 0.0
Const GRAVITY_Y# = -0.001
Const GRAVITY_Z# = 0.0

Const ITERATIONS = 16

Type TCloth
	Field Mesh
	Field Surface
	
	Field GridSpacing#
	
	Field NodeX#[ CLOTH_SIZE*CLOTH_SIZE ]
	Field NodeY#[ CLOTH_SIZE*CLOTH_SIZE ]
	Field NodeZ#[ CLOTH_SIZE*CLOTH_SIZE ]
	
	Field OldX# [ CLOTH_SIZE*CLOTH_SIZE ]
	Field OldY# [ CLOTH_SIZE*CLOTH_SIZE ]
	Field OldZ# [ CLOTH_SIZE*CLOTH_SIZE ]
End Type


Graphics3D 800, 600, 0, 2

;--------------------------- Scene setup
Local Cam = CreateCamera()
PositionEntity Cam, 1.0, 3.0, -4.0

Local Light = CreateLight( 2 )
PositionEntity Light, 3.0, 4.0, -5.0
LightRange Light, 2.5

Local Cube = CreateCube()
EntityBox Cube, -1.0, -0.2, -1.0, 2.0, 0.6, 2.0
PositionEntity Cube, 1.0, 0.0, 0.0
ScaleEntity Cube, 0.9, 0.2, 0.9
EntityPickMode Cube, 3

Local Cylinder = CreateCylinder( 16 )
ScaleMesh Cylinder, 1.2, 1.2, 1.2
EntityAlpha Cylinder, 0.0
CreateCylinder( 32, True, Cylinder )
ScaleEntity Cylinder, 0.15, 3.0, 0.15
PositionEntity Cylinder, 0.0, 1.0, 0.2
TurnEntity Cylinder, 85.0, 90.0, 0.0
EntityPickMode Cylinder, 2


PointEntity Cam, Cube
;--------------------------- /Scene setup

Local Cloth.TCloth = CreateCloth( -1.5, 1.5, -2.2, 3.0/CLOTH_SIZE )
EntityColor Cloth\Mesh, 255, 128, 128
EntityAlpha Cloth\Mesh, 0.999 ;Kludgy way of disabling Z-Buffering

Local Timer = CreateTimer( 60 )

While Not KeyHit( 1 )
	RenderWorld
	
	UpdateCloth( Cloth )
	
	WireFrame MouseDown( 1 )
	
	Local I = CLOTH_SIZE*( CLOTH_SIZE - 1 )/2
	Local Pivot = CreatePivot()
	PositionEntity Pivot, Cloth\NodeX[ I ], Cloth\NodeY[ I ], Cloth\NodeZ[ I ]
	PointEntity Cam, Pivot
	
	Text 0, 0, "Left mouse button: Wireframe"
	
	Flip 0
	WaitTimer Timer
Wend
End

Function CreateCloth.TCloth( X#, Y#, Z#, GridSpacing# )
	Local Cloth.TCloth = New TCloth
	
	Cloth\Mesh    = CreateMesh()
	Cloth\Surface = CreateSurface( Cloth\Mesh )
	Cloth\GridSpacing# = GridSpacing#
	
	EntityFX Cloth\Mesh, 16
	
	For GridZ = 0 To CLOTH_SIZE - 1
		For GridX = 0 To CLOTH_SIZE - 1
			AddVertex Cloth\Surface, X# + GridX*GridSpacing#, Y#, Z# + GridZ*GridSpacing#, GridX/Float( CLOTH_SIZE ), GridZ/Float( CLOTH_SIZE )
			
			Cloth\NodeX[ GridX + GridZ*CLOTH_SIZE ] = X# + GridX*GridSpacing#
			Cloth\NodeY[ GridX + GridZ*CLOTH_SIZE ] = Y#
			Cloth\NodeZ[ GridX + GridZ*CLOTH_SIZE ] = Z# + GridZ*GridSpacing#
			
			Cloth\OldX[ GridX + GridZ*CLOTH_SIZE ] = Cloth\NodeX[ GridX + GridZ*CLOTH_SIZE ]
			Cloth\OldY[ GridX + GridZ*CLOTH_SIZE ] = Cloth\NodeY[ GridX + GridZ*CLOTH_SIZE ]
			Cloth\OldZ[ GridX + GridZ*CLOTH_SIZE ] = Cloth\NodeZ[ GridX + GridZ*CLOTH_SIZE ]
		Next
	Next
	
	For Z = 0 To CLOTH_SIZE - 2
		For X = 0 To CLOTH_SIZE - 2
			Local Index = X + Z*CLOTH_SIZE
			
			AddTriangle Cloth\Surface, Index + 1, Index, Index + CLOTH_SIZE
			AddTriangle Cloth\Surface, Index + 1, Index + CLOTH_SIZE, Index + CLOTH_SIZE + 1
		Next
	Next
	
	Return Cloth
End Function

Function UpdateCloth( Cloth.TCloth )
	For Z = 0 To CLOTH_SIZE - 1
		For X = 0 To CLOTH_SIZE - 1
			Local Index = X + Z*CLOTH_SIZE
			Local OldX# = Cloth\NodeX[ Index ]
			Local OldY# = Cloth\NodeY[ Index ]
			Local OldZ# = Cloth\NodeZ[ Index ]
			
			Cloth\NodeX[ Index ] = Cloth\NodeX[ Index ]*1.99 - Cloth\OldX[ Index ]*0.99 + GRAVITY_X#
			Cloth\NodeY[ Index ] = Cloth\NodeY[ Index ]*1.99 - Cloth\OldY[ Index ]*0.99 + GRAVITY_Y#
			Cloth\NodeZ[ Index ] = Cloth\NodeZ[ Index ]*1.99 - Cloth\OldZ[ Index ]*0.99 + GRAVITY_Z#
			
			Cloth\OldX[ Index ] = OldX#
			Cloth\OldY[ Index ] = OldY#
			Cloth\OldZ[ Index ] = OldZ#
		Next
	Next
	
	For I = 1 To ITERATIONS
		For Z = 0 To CLOTH_SIZE - 1
			For X = 0 To CLOTH_SIZE - 1
				Index  = X + Z*CLOTH_SIZE
				
				If X Then
					Local IndexL = Index - 1
					
					Local Diff1X# = Cloth\NodeX[ Index ] - Cloth\NodeX[ IndexL ]
					Local Diff1Y# = Cloth\NodeY[ Index ] - Cloth\NodeY[ IndexL ]
					Local Diff1Z# = Cloth\NodeZ[ Index ] - Cloth\NodeZ[ IndexL ]
					Local Length1# = Sqr( Diff1X#*Diff1X# + Diff1Y#*Diff1Y# + Diff1Z#*Diff1Z# )
					
					Local LengthD1# = ( Length1# - Cloth\GridSpacing# )*0.5
					
					LengthD1# = LengthD1#/Length1#
					
					Cloth\NodeX[ Index  ] = Cloth\NodeX[ Index  ] - Diff1X#*LengthD1#
					Cloth\NodeY[ Index  ] = Cloth\NodeY[ Index  ] - Diff1Y#*LengthD1#
					Cloth\NodeZ[ Index  ] = Cloth\NodeZ[ Index  ] - Diff1Z#*LengthD1#
					Cloth\NodeX[ IndexL ] = Cloth\NodeX[ IndexL ] + Diff1X#*LengthD1#
					Cloth\NodeY[ IndexL ] = Cloth\NodeY[ IndexL ] + Diff1Y#*LengthD1#
					Cloth\NodeZ[ IndexL ] = Cloth\NodeZ[ IndexL ] + Diff1Z#*LengthD1#
				EndIf
				
				
				If Z Then
					Local IndexD = Index - CLOTH_SIZE
					
					Local Diff2X# = Cloth\NodeX[ Index ] - Cloth\NodeX[ IndexD ]
					Local Diff2Y# = Cloth\NodeY[ Index ] - Cloth\NodeY[ IndexD ]
					Local Diff2Z# = Cloth\NodeZ[ Index ] - Cloth\NodeZ[ IndexD ]
					
					Local Length2# = Sqr( Diff2X#*Diff2X# + Diff2Y#*Diff2Y# + Diff2Z#*Diff2Z# )
					
					Local LengthD2# = ( Length2# - Cloth\GridSpacing# )*0.5
					
					LengthD2# = LengthD2#/Length2#
					
					Cloth\NodeX[ IndexD ] = Cloth\NodeX[ IndexD ] + Diff2X#*LengthD2#
					Cloth\NodeY[ IndexD ] = Cloth\NodeY[ IndexD ] + Diff2Y#*LengthD2#
					Cloth\NodeZ[ IndexD ] = Cloth\NodeZ[ IndexD ] + Diff2Z#*LengthD2#
					Cloth\NodeX[ Index  ] = Cloth\NodeX[ Index  ] - Diff2X#*LengthD2#
					Cloth\NodeY[ Index  ] = Cloth\NodeY[ Index  ] - Diff2Y#*LengthD2#
					Cloth\NodeZ[ Index  ] = Cloth\NodeZ[ Index  ] - Diff2Z#*LengthD2#
				EndIf
				
			Next
		Next
	Next
	
	For Z = 0 To CLOTH_SIZE - 1
		For X = 0 To CLOTH_SIZE - 1
			Index = X + Z*CLOTH_SIZE
			
			Local VX# = Cloth\NodeX[ Index ] - Cloth\OldX[ Index ]
			Local VY# = Cloth\NodeY[ Index ] - Cloth\OldY[ Index ]
			Local VZ# = Cloth\NodeZ[ Index ] - Cloth\OldZ[ Index ]
			
			If LinePick( Cloth\OldX[ Index ], Cloth\OldY[ Index ], Cloth\OldZ[ Index ], VX#, VY#, VZ# ) Then
				Local NX# = PickedNX()
				Local NY# = PickedNY()
				Local NZ# = PickedNZ()
				
				Local D# = -( PickedX()*NX# + PickedY()*NY# + PickedZ()*NZ# )
				
				Local Dist# = Cloth\NodeX[ Index ]*NX# + Cloth\NodeY[ Index ]*NY# + Cloth\NodeZ[ Index ]*NZ# + D#
				
				Cloth\NodeX[ Index ] = Cloth\NodeX[ Index ] - Dist#*NX#
				Cloth\NodeY[ Index ] = Cloth\NodeY[ Index ] - Dist#*NY#
				Cloth\NodeZ[ Index ] = Cloth\NodeZ[ Index ] - Dist#*NZ#
			EndIf
			
			VertexCoords Cloth\Surface, Index, Cloth\NodeX[ Index ], Cloth\NodeY[ Index ], Cloth\NodeZ[ Index ]
		Next
	Next
	
	UpdateNormalsEx Cloth\Surface
End Function

Function UpdateNormalsEx( Surface )
	TriangleCount = CountTriangles( Surface )
	For Triangle = 0 To TriangleCount - 1
		V1 = TriangleVertex( Surface, Triangle, 0 )
		V2 = TriangleVertex( Surface, Triangle, 1 )
		V3 = TriangleVertex( Surface, Triangle, 2 )
		
		VX1# = VertexX( Surface, V2 ) - VertexX( Surface, V1 )
		VY1# = VertexY( Surface, V2 ) - VertexY( Surface, V1 )
		VZ1# = VertexZ( Surface, V2 ) - VertexZ( Surface, V1 )
		
		VX2# = VertexX( Surface, V3 ) - VertexX( Surface, V1 )
		VY2# = VertexY( Surface, V3 ) - VertexY( Surface, V1 )
		VZ2# = VertexZ( Surface, V3 ) - VertexZ( Surface, V1 )
		
		VX3# = VY1*VZ2 - VZ1*VY2
		VY3# = VZ1*VX2 - VX1*VZ2
		VZ3# = VX1*VY2 - VY1*VX2
		
		TFormNormal VX3, VY3, VZ3, 0, 0
		
		VertexNormal Surface, V1, TFormedX(), TFormedY(), TFormedZ()
		VertexNormal Surface, V2, TFormedX(), TFormedY(), TFormedZ()
		VertexNormal Surface, V3, TFormedX(), TFormedY(), TFormedZ()
	Next
End Function</textarea> <br><br></td></tr></table><br>
<a name="1041641"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ked</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oooh. Looks nice! <br><br></td></tr></table><br>
<a name="1041643"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> nice job<br><br>on a side note if you use a collision system that didnt use picking (a self made one) then it could run much faster :p 128*128 line picks per frame is a little much for any pc. <br><br></td></tr></table><br>
<a name="1041644"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Noobody</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> on a side note if you use a collision system that didnt use picking (a self made one) then it could run much faster :p 128*128 line picks per frame is a little much for any pc. <br></div><br>Writing a picking system in B3D itself would most probably not be faster than the native picking system. The native one uses some kind of octree for culling (hard to implement in B3D) and is written in C++, which is superior to B3D speedwise. <br><br></td></tr></table><br>
<a name="1041646"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >puki</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Very sexual.<br><br>Setting the cloth as low as 16 still looks fine. <br><br></td></tr></table><br>
<a name="1041647"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> well I mean make each object a type and sense collisions based on the shape like the cube and cylinder.. idk if that would be faster, its still a lot of math for b3d to execute <br><br></td></tr></table><br>
<a name="1041651"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Noobody</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well, B3D has Pickmodes for Spheres and Boxes too, so you could set these (the box one was actually used in the demo). But either way, picking in a complex scene is still pretty time consuming.<br><br><br><div class="quote"> Very sexual. <br></div><br>I'm glad you enjoyed... I guess. <br><br></td></tr></table><br>
<a name="1041657"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tachyon</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Really, really nice! Let's see a MinB3D version! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
