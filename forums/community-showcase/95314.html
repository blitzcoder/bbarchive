<!DOCTYPE html><html lang="en" ><head ><title >Psychadelic Screensaver Progress</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Psychadelic Screensaver Progress</h1><a href="forums.php" >Community Forums</a>/<a href="topics.php?forum=61" >Showcase</a>/<a href="#bottom" >Psychadelic Screensaver Progress</a><br><br>
<a name="1096902"></a>

<a name="1096903"></a>

<a name="1096904"></a>

<a name="1096906"></a>

<a name="1096907"></a>

<a name="1096919"></a>

<a name="1096920"></a>

<a name="1096921"></a>

<a name="1096922"></a>

<a name="1096923"></a>

<a name="1096924"></a>

<a name="1096925"></a>

<a name="1096926"></a>

<a name="1096928"></a>

<a name="1096930"></a>

<a name="1096931"></a>

<a name="1097473"></a>

<a name="1097476"></a>

<a name="1097477"></a>

<a name="1097479"></a>

<a name="1097480"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dubious Drewski</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> EDIT: Updated this post (and the code) with new features on the 22nd of July<br><br>The pictures don't look like much. It's the animation that I worked hard on. The animation and the flocking particle AI. <br><br><img src="http://i.imgur.com/4OQrY.jpg"><br>I actually "finished" this project a couple of years ago, and even showed <br>it to you guys. I didn't program for two years after that. Well today I <br>got an itch, dug up this old code and GREATLY improved it. I made it way cooler to <br>watch it run. I didn't touch the particle AI much - it's pretty good as it <br>is, really.<br><br><a href="http://www.andysuderman.com/programs/" target="_blank">Exe available here.</a><br><br>And for the folks uncomfortable with internet Exes, here's the code to <br>copy paste, if you like:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
Framework BRL.Max2D
Import BRL.Random
Import brl.d3d7max2d

AppTitle = "Move mouse to Exit"
Global particleNum:Int = 2900
'Global sparkNum:Int = 30

SeedRnd MilliSecs()
Graphics 800,600,1

Global debug:Int = False

Global Theme:Int, themeCount:Int = 5
theme = Rand(themeCount+1)
Global poof:TImage = drawpoof()
Global poofHighRes:TImage = drawpoof(64,64,0)
Global effectsLayerActive:Int
Global colourVolitility:Int = 2
Global initialWaited:Int = False

Type vectorType
	Field x:Float, y:Float
EndType

Type lazyParticleSpyType
	'This code follows a single particle around and checks if it is staying still for too long.
	'If it is, an explosion is created at the location of the spy particle.  This makes the dormant blobs
	'explode sometimes, which looks cool.
	Field movementPoints:Int
	Method update()
		If particles[10].x1&lt; 1 And particles[10].y1&lt;1 Then 
			movementPoints:-1
		Else
			movementPoints:+1
			If movementPoints&gt;100 Then movementPoints = 100
		EndIf
		If movementPoints&lt;0 Then explode()
	EndMethod
	Method explode()
		Local angle:Int, power:Float, base:Int
		base = Rand(360)
		For Local a:Int = 0 To particleNum
			If within(particles[10].x, particles[10].y, particles[a].x, particles[a].y, 40, 40) Then
				angle = Rand(360)
				power = RndFloat()*10
				angle:*.2
				If Rand(2)=1 Then angle:+180
				angle:+base
				If Rand(5) = 1 Then angle = Rand(360)
				If Rand(6)=2 Then power:*2
				particles[a].x = particles[10].x
				particles[a].y = particles[10].y
				particles[a].x1=Sin(angle)*power
				particles[a].y1=Cos(angle)*power
				particles[a].r:*10.6
				particles[a].g:*10.6
				particles[a].b:*2.6
			EndIf
		Next
		movementPoints = 100
		particles[10].x:+400
		If particles[10].x&gt;800 Then particles[10].x:-800
	EndMethod
	Method New()
		movementPoints = 170
	EndMethod
EndType

Global lazyParticleSpy:lazyParticleSpyType[50]
For Local a:Int = 0 To 49
	lazyParticleSpy[a] = New lazyParticleSpyType
Next

Type effectsLayerType
	Field r:Int, g:Int, b:Int, size:Float
	Field xOff:Float, yOff:Float, alpha:Float
	Field xOff2:Int, yOff2:Int, doAlpha:Int
	Method draw(xx:Int, yy:Int, mode:Int)
		If Not effectsLayerActive Then Return
		SetColor r, g, b
		xx:*20
		yy:*20
		alpha = (Abs(xOff)+Abs(yOff))/10
		If alpha &gt;1 Then alpha = 1
'		mode = 1
		Select mode
		Case 1
			SetAlpha alpha
			SetColor r*2, g*2, b*2
			drawl xx, yy, 18, 18,,CORNERS
			SetRotation (xOff*yOff)*1
			
'			Local tempX:Float, tempY:Float
'			GetHandle (tempX, tempY)
			SetHandle 7,7
			drawl xx+9, yy+9, 14, 14,,FILL
'			DrawRect xx+2, yy+2,xx+2, yy+2,
			SetHandle 0,0
			SetRotation 0
'			alpha:*.07
			'SetAlpha alpha
			'drawl (xx-xOff*.5+xOff2, yy-yOff*.5+yOff2, -xOff, -yOff, Abs(xOff*yOff), LINE)
		Case 2
			SetAlpha alpha'1-RndFloat()*.5'alpha
			If doAlpha Then SetBlend alphablend
			SetRotation xx+yy
			DrawImage poof, xx, yy
			SetRotation 0
		Case 3
			SetAlpha alpha'1-RndFloat()*.5'alpha
'			SetBlend alphablend
			SetScale xOff*.2, yOff*.2
			DrawImage poofHighRes, xx, yy
			SetScale 1, 1
		Case 4
'			SetAlpha alpha*.3
			SetColor r*2, g*2, b*2
'			SetBlend alphablend
			SetScale xOff*.1, yOff*.1
			DrawImage poofHighRes, xx, yy
			SetScale 1, 1
		Case 5
			SetAlpha alpha
'			SetColor r*2, g*2, b*2
'			drawl xx, yy, 18, 18,,CORNERS
			alpha:*.07
			SetAlpha alpha
			drawl (xx-xOff*.5+xOff2, yy-yOff*.5+yOff2, -xOff, -yOff, Abs(xOff*yOff), LINE)
		Case 6
			SetAlpha alpha*.3
			SetColor r*2, g*2, b*2
'			SetBlend alphablend
			SetScale xOff*.4, yOff*.4
			DrawImage poof, xx, yy
			SetScale 1, 1
		Default
			alpha:*.07
			SetAlpha alpha
			drawl (xx-xOff*.5+xOff2, yy-yOff*.5+yOff2, -xOff, -yOff, Abs(xOff*yOff), LINE)
		EndSelect

		SetAlpha 1
		If timer.t10Flag Then
			r:*.95
			g:*.95
			b:*.95
			xOff:*.995
			yOff:*.995
			'size= (size-4) *.9
		EndIf
	EndMethod
	Method New()
		'r = Rand(55)
		'g = Rand(55)
		'b = Rand(55)
		xOff2 = Rand(20)-10
		yOff2 = Rand(20)-10
		size = 4+Rand(6)
	EndMethod
EndType

Global effectsLayer:effectsLayerType[41, 31]
For Local yy:Int = 0 To 30
	For Local xx:Int = 0 To 40
		effectsLayer[xx,yy] = New effectsLayerType
	Next
Next

Type gridType
	Field show:Int
	Field width:Int = 800, height:Int = 600
	Field resolution:Int = 30
	Field xPoints:Int  = width/resolution+3
	Field yPoints:Int = height/resolution+2
	Field vector:vectorType[xPoints+1,yPoints+1]
	Field vectorTarget:vectorType[xPoints+1,yPoints+1]
	Field xCenter:Int = 400-width*.5, yCenter:Int = 300-height*.5
	Field transition:Int
	Method update()
		For Local y:Int = 0 To yPoints
			For Local x:Int = 0 To xPoints
				If vector[x,y].x&lt; vectorTarget[x,y].x Then vector[x,y].x:+.01
				If vector[x,y].y&lt; vectorTarget[x,y].y Then vector[x,y].y:+.01
				If vector[x,y].x&gt; vectorTarget[x,y].x Then vector[x,y].x:-.01
				If vector[x,y].y&gt; vectorTarget[x,y].y Then vector[x,y].y:-.01
			Next
		Next
		For Local y:Int = 0 To yPoints
			For Local x:Int = 0 To xPoints
				grid.vectorTarget[x,y].x:+(RndFloat()-.5)*.08
				grid.vectorTarget[x,y].y:+(RndFloat()-.5)*.08
				grid.vectorTarget[x,y].x:*.996
				grid.vectorTarget[x,y].y:*.996
			Next
		Next
	EndMethod
	Method New()
		For Local yy:Int = 0 To yPoints
			For Local xx:Int = 0 To xPoints
				vector[xx,yy] = New vectorType
				vectorTarget[xx,yy] = New vectorType
			Next
		Next
	EndMethod
	Method circles(event:Int=0)
		Local times:Int = 1, centerXpixels:Int = 400, centerYpixels:Int = 300
		If Not event event = Rand(7)
'		event = 6
		Select event
			Case 1
			Case 2, 3
				times = 20
			Case 4
				times = 80
			Case 5
				times = 10
			Case 6

			Default
				'change nothing. Keep default values
		EndSelect
		For Local b:Int = 1 To times
			Select event
				Case 1
					createCircle(Rand(800), Rand(600))
				Case 2, 3
					createCircle(Rand(800), Rand(600), 90+Rand(140))
				Case 4
					createCircle(Rand(800), Rand(600), 90+Rand(20))
				Case 5
					createCircle(Rand(800), Rand(600), 240+Rand(120))
				Case 6,7
					Local offset:Int = Rand(360)
					For Local y:Int = 500 To -100 Step -200
						For Local x:Int = 0 To 800 Step 200
							createCircle(x, y, 150, False, offset-80, offset+80)
						Next
					Next
					For Local y:Int = 400 To -100 Step -200
						For Local x:Int = 100 To 800 Step 200
							createCircle(x, y, 150, False, offset-80, offset+80)
						Next
					Next
					For Local a:Int = 0 To 2
						createCircle(Rand(800), Rand(600), 60+Rand(90),0,90+Rand(140))
					Next
				Default
					'change nothing. Keep default values
			EndSelect
		Next
	EndMethod
	Method createCircle(centerXpixels:Int = 400, centerYpixels:Int = 300, radiusTarget:Float = 300, fill:Int = True,..
				start:Int =0, stop:Int = 360)
		Local radius:Float = 0, angle:Int
		Local xGrid:Int, yGrid:Int, centerXgrid:Int, centerYGrid:Int, r:Int, times:Int = 1
		If fill Then
			Local gimpedAngle:Int = Rand(360)
			Repeat
				angle = start
				Repeat
					xGrid = (centerXpixels+radius*Sin(angle))/resolution
					yGrid = (centerYpixels+radius*Cos(angle))/resolution
					If xGrid &gt; xPoints Then xGrid:-xPoints
					If yGrid &gt; yPoints Then yGrid:-yPoints
					If xGrid &lt; 0 Then xGrid:+xPoints
					If yGrid &lt; 0 Then yGrid:+yPoints
					grid.vectorTarget[xGrid,yGrid].x = Sin(angle+90)
					grid.vectorTarget[xGrid,yGrid].y = Cos(angle+90)
					Local completed:Float = radius/radiusTarget
					If completed &lt; .8 And completed &gt; .4 Then 				' This is to make the center of each circle pull some particles inward sometimes
						grid.vectorTarget[xGrid,yGrid].x = Sin(angle+100)
						grid.vectorTarget[xGrid,yGrid].y = Cos(angle+100)
					EndIf
					If Abs(angle-gimpedAngle)&lt; 40 Then
						grid.vectorTarget[xGrid,yGrid].x = Sin(angle-60)
						grid.vectorTarget[xGrid,yGrid].y = Cos(angle-60)
					EndIf
					angle:+10
				Until angle&gt;stop
				radius:+10
			Until radius &gt; radiusTarget
		EndIf
		angle = start
		Repeat
			xGrid = (centerXpixels+radiusTarget*Sin(angle))/resolution
			yGrid = (centerYpixels+radiusTarget*Cos(angle))/resolution
			If xGrid &gt; xPoints Then xGrid:-xPoints
			If yGrid &gt; yPoints Then yGrid:-yPoints
			If xGrid &lt; 0 Then xGrid:+xPoints
			If yGrid &lt; 0 Then yGrid:+yPoints
			grid.vectorTarget[xGrid,yGrid].x = Sin(angle+140)*2
			grid.vectorTarget[xGrid,yGrid].y = Cos(angle+140)*2
			angle:+10
		Until angle&gt; stop
	EndMethod
	Method sineSmear()
		Local angle:Int = 0
		Local gg:Int = Rand(3)
		Select gg
		Case 1
			For Local x:Int = 0 To xPoints
				For Local y:Int = 0 To yPoints
					grid.vectorTarget[x,y].x = Cos(angle*10)*1
					grid.vectorTarget[x,y].y = Sin(angle)*1
					angle:+1
					If angle &gt;360 Then angle:-360
				Next
			Next
		Default
			For Local y:Int = 0 To yPoints
				For Local x:Int = 0 To xPoints
					grid.vectorTarget[x,y].y = Cos(angle*5)*1
					grid.vectorTarget[x,y].x = Sin(angle)*1
					angle:+1
					If angle &gt;360 Then angle:-360
				Next
			Next
		EndSelect
	EndMethod
	Method waterSmear()
		For Local y:Int = 0 To yPoints
			For Local x:Int = 0 To xPoints
				grid.vectorTarget[x,y].x = .2
				grid.vectorTarget[x,y].y = Sin(x*3)+1
			Next
		Next
		Local aa:Int = Rand (4, yPoints)
		For Local y:Int = aa-3 To aa
			For Local x:Int = 0 To xPoints
				grid.vectorTarget[x,y].x = (RndFloat()-.2)
				grid.vectorTarget[x,y].y = (RndFloat()-.5)
			Next
		Next
	EndMethod
	Method explode()
		For Local y:Int = 0 To yPoints
			For Local x:Int = 0 To xPoints
				If Rnd()&lt;.2
					grid.vectorTarget[x,y].x:+(RndFloat()-.5)*15
					grid.vectorTarget[x,y].y:+(RndFloat()-.5)*15
				EndIf
			Next
		Next
	EndMethod
	Method kill()
		For Local y:Int = 0 To yPoints
			For Local x:Int = 0 To xPoints
				grid.vectorTarget[x,y].x=0
				grid.vectorTarget[x,y].y=0
			Next
		Next
	EndMethod
'	Method drawGradient()
'		glbegin Gl_Quads
'			glcolor3f	 0.9, 0.4, 0.1
'			glvertex3f 	2, 2, 0
'			glvertex3f  80, 2, 0
'			glvertex3f  80, 72, 0
'			glvertex3f  2, 52, 0
'		glend 
'	EndMethod
	Method drawVectors()
		Local xPos:Int, yPos:Int
		For Local yarray:Int = 0 To yPoints-2
			For Local xarray:Int = 0 To xPoints-2
				xPos = xCenter + xArray * resolution
				yPos = yCenter + yArray * resolution
				SetColor 250,200,100
				Drawl xPos,yPos, vector[xArray, yArray].x*20, vector[xArray, yArray].y*20,1,LINE
			Next
		Next
	EndMethod
EndType
Global grid:gridType = New gridType

Type timerType
	Field t10Flag:Int, t10:Int
	Field t50Flag:Int, t50:Int
	Field t22000Flag:Int, t22000:Int
	Method update()
		t10Flag = 0
		t50Flag = 0
		t22000Flag = 0 
		If MilliSecs()-t10 &gt;10 t10Flag = 1; t10 = MilliSecs()
		If MilliSecs()-t50 &gt;50 t50Flag = 1; t50 = MilliSecs()
		If MilliSecs()-t22000&gt;22000 t22000Flag = 1; t22000 = MilliSecs()
	EndMethod
	Method New()
		t10 = MilliSecs()
		t50 = MilliSecs()
		t22000 = MilliSecs()-10000
	EndMethod
EndType

Type colourSchemeType
	'colour[which particle, which overall scheme]
	Field redCls:Float, greenCls:Float, blueCls:Float
	Field redClsTarget:Float, greenClsTarget:Float, blueClsTarget:Float
	Field r:Int[4,6], g:Int[4,6], b:Int[4,6]
	Field Current:Int, totalSchemes:Int = 5
	Method update()
		If timer.t22000Flag Then
			For Local a:Int = 0 To particlenum
				particles[a].blendModeLife = Rand(3,30) 'debug
			Next
			theme:+1
'			effectsLayer.doAlpha = False
'			If Rand(100)&lt;50 effectsLayer.doAlpha = True
			If theme&gt;themeCount Then theme = 1
			effectsLayerActive = True
			Current:+1
			If Current&gt; totalSchemes Then Current = 0
			changecolour()
			For Local a:Int = 0 To particleNum -1 
				particles[a].gravity=0
			Next
			Select Rand(6)
				Case 1
					grid.waterSmear()
				Case 2
					grid.sineSmear()
				Case 3
					grid.kill()
					For Local a:Int = 0 To particleNum -1 Step 2 		'this makes half of the particles have reverse gravity
						particles[a].gravity=.08
					Next
				Case 4
					grid.kill()
					Local temp:Float
					For Local a:Int = 0 To particleNum -1 
						particles[a].gravity=temp
						temp:+.0002
					Next
				Default
					grid.circles()
					If initialWaited = True And Rand(2) = 1 Then particleType.reset(4)
			EndSelect
			If initialWaited = False grid.circles()
			initialWaited = True
		EndIf
		If timer.t50Flag Then
			For Local a:Int = 0 To 49
				lazyParticleSpy[a].update()
			Next
			
			If redCls&lt;redClsTarget redCls:+colourVolitility*.2
			If redCls&gt;redClsTarget redCls:-colourVolitility*.2

			If GreenCls&lt;GreenClsTarget GreenCls:+colourVolitility*.2
			If GreenCls&gt;GreenClsTarget GreenCls:-colourVolitility*.2

			If BlueCls&lt;BlueClsTarget BlueCls:+colourVolitility*.2
			If BlueCls&gt;BlueClsTarget BlueCls:-colourVolitility*.2
			
			SetClsColor redCls, greenCls, blueCls
		EndIf
	EndMethod
	Method changeColour()
		redClsTarget=r[2, Current]*.6
		greenClsTarget=g[2, Current]*.6
		blueClsTarget=b[2, Current]*.6
		
		For Local a:Int = 0 To 3
			particles[a].r = r[a, Current]
			particles[a].g = g[a, Current]
			particles[a].b = b[a, Current]
		Next
	EndMethod
	Method debug()
		For Local y:Int = 0 To totalSchemes
			For Local x: Int = 0 To 3
				SetColor r[x, y], g[x, y], b[x, y]
				DrawRect 40+x*60, 40+y*70, 57, 57
			Next
		Next
		Flip
		WaitKey
		End
	EndMethod
	
	Method New()
		'Colour Scheme 0
		r[0,0]=000
		g[0,0]=155
		b[0,0]=100
			
		r[1,0]=140
		g[1,0]=196
		b[1,0]=100
	
		r[2,0]=058
		g[2,0]=000
		b[2,0]=101
		
		r[3,0]=146
		g[3,0]=155
		b[3,0]=000

		'Colour Scheme 1
		r[0,1]=100
		g[0,1]=55
		b[0,1]=100
			
		r[1,1]=40
		g[1,1]=46
		b[1,1]=100
	
		r[2,1]=158
		g[2,1]=000
		b[2,1]=101
		
		r[3,1]=86
		g[3,1]=145
		b[3,1]=190

		'Colour Scheme 2
		r[0,2]=6
		g[0,2]=55
		b[0,2]=100
			
		r[1,2]=40
		g[1,2]=46
		b[1,2]=255
	
		r[2,2]=158
		g[2,2]=000
		b[2,2]=201
		
		r[3,2]=86
		g[3,2]=235
		b[3,2]=200

		'Colour Scheme 3
		r[0,3]=156
		g[0,3]=25
		b[0,3]=000
			
		r[1,3]=240
		g[1,3]=146
		b[1,3]=010
	
		r[2,3]=258
		g[2,3]=000
		b[2,3]=101
		
		r[3,3]=186
		g[3,3]=235
		b[3,3]=100

		'Colour Scheme 4
		r[0,4]=156
		g[0,4]=225
		b[0,4]=000
					
		r[1,4]=240
		g[1,4]=246
		b[1,4]=010
					
		r[2,4]=258
		g[2,4]=200
		b[2,4]=101
						
		r[3,4]=156
		g[3,4]=095
		b[3,4]=010

		'Colour Scheme 5
		r[0,5]=219
		g[0,5]=05
		b[0,5]=000
					
		r[1,5]=240
		g[1,5]=106
		b[1,5]=010
					
		r[2,5]=255
		g[2,5]=010
		b[2,5]=101
						
		r[3,5]=156
		g[3,5]=005
		b[3,5]=040
	EndMethod
EndType

Global colourScheme:colourSchemeType = New colourSchemeType
colourScheme.Current = Rand(colourScheme.totalSchemes)
'colourScheme.debug()


Type particleType
	Field x:Float, y:Float
	Field x1:Float, y1:Float
	Field r:Int, g:Int, b:Int
	Field xGrid:Int, yGrid:Int
	Field gravity:Float, rotation:Float, rotation1:Float
	Field blendMode:Int, blendModeLife:Int
	Method do(ID:Int)
			If blendModeLife&gt;1 Then blendModeLife:-1
			If blendModeLife = 2 Then 
				blendMode:+1
				If blendMode&gt; 3 Then blendMode = 1
			EndIf
		If timer.t50Flag Then
'			If Abs(x1) &gt; 1 And Abs(y1) &gt; 1 Then 
'				For Local a:Int = 0 To sparkNum
'					If Not spark[a].life Then spark[a].launch(x,y,x1,y1,200,1,r*2,g*2,b*2)
'				Next
'			EndIf
			If rotation &gt; 360 Then rotation:-360
			If rotation&lt;0 Then rotation:+360
			If ID &gt; 3 Then
				For Local a:Int = 0 To 3
					If within(x, y, particles[a].x, particles[a].y, 50,50) Then
						If r &lt; particles[a].r Then 
							r:+colourVolitility
							If r&gt;255 Then r = 255
						EndIf
						If g &lt; particles[a].g Then 
							g:+colourVolitility
							If g&gt;255 Then g = 255
						EndIf
						If b &lt; particles[a].b Then 
							b:+colourVolitility
							If b&gt;255 Then b = 255
						EndIf

						If r &gt; particles[a].r Then 
							r:-colourVolitility
							If r&lt;0 Then r = 0
						EndIf
						If g &gt; particles[a].g Then 
							g:-colourVolitility
							If g&lt;0 Then g = 0
						EndIf
						If b &gt; particles[a].b Then 
							b:-colourVolitility
							If b&lt;0 Then b = 0
						EndIf
					EndIf
				Next
			ElseIf initialWaited Then
				For Local a:Int = 0 To 3
					If ID &lt;&gt; a Then
						If within (x, y, particles[a].x, particles[a].y, 60, 60) Then
							x1:+(x-particles[a].x)*.05
							y1:+(y-particles[a].y)*.05
						EndIf
					EndIf
				Next
			EndIf
		EndIf
		rotation:+rotation1
		x1:+(RndFloat()-.5)*.1
		y1:+(RndFloat()-.5)*.1
		x:+x1
		y:+y1
		y1:+gravity
		x1:*.9
		y1:*.9
		If x&gt; grid.width+4 x = -4
		If x&lt; -4 x = grid.width+4
		If y&gt; grid.height+4 y = -4
		If y&lt; -4 y = grid.height+4
		xGrid = x/grid.resolution
		yGrid = y/grid.resolution
		x1:+(grid.vector[xGrid, yGrid].x+grid.vector[xGrid+1, yGrid].x)*.5
		y1:+(grid.vector[xGrid, yGrid].y+grid.vector[xGrid, yGrid+1].y)*.5
		If ID &lt; 50 Then
			Local xeffectsLayer:Int = x/20
			Local yeffectsLayer:Int = y/20
			If xeffectsLayer &lt;0 Then xeffectsLayer =0
			If yeffectsLayer &lt;0 Then yeffectsLayer =0
			If xeffectsLayer &gt;40 Then xeffectsLayer =40
			If yeffectsLayer &gt;30 Then yeffectsLayer =30
'			effectsLayer[xeffectsLayer, yeffectsLayer].r:+ r*.2
'			effectsLayer[xeffectsLayer, yeffectsLayer].g:+ g*.2
'			effectsLayer[xeffectsLayer, yeffectsLayer].b:+ b*.2
			Select theme
			Case 1
				effectsLayer[xeffectsLayer, yeffectsLayer].r:+Abs(r - effectsLayer[xeffectsLayer, yeffectsLayer].r)*.2
				effectsLayer[xeffectsLayer, yeffectsLayer].g:+Abs(g - effectsLayer[xeffectsLayer, yeffectsLayer].g)*.2
				effectsLayer[xeffectsLayer, yeffectsLayer].b:+Abs(b - effectsLayer[xeffectsLayer, yeffectsLayer].b)*.2
			Default
				effectsLayer[xeffectsLayer, yeffectsLayer].r:+Abs(r - effectsLayer[xeffectsLayer, yeffectsLayer].r)*.06
				effectsLayer[xeffectsLayer, yeffectsLayer].g:+Abs(g - effectsLayer[xeffectsLayer, yeffectsLayer].g)*.06
				effectsLayer[xeffectsLayer, yeffectsLayer].b:+Abs(b - effectsLayer[xeffectsLayer, yeffectsLayer].b)*.06
			EndSelect

			effectsLayer[xeffectsLayer, yeffectsLayer].xOff:+x1*.2
			effectsLayer[xeffectsLayer, yeffectsLayer].yOff:+y1*.2
		EndIf
		If ID = 10 And debug Then 
			SetColor 200, 200, 200
			drawl(x-25,y-25, 50, 50, 1, CORNERS)
'			drawl(x-25,y-30, lazyParticleSpy.movementPoints*.5, 5,1, FILL)
			SetAlpha .2
			DrawText "Keys: 1-reposition, 2-kill movement, 3-rand movement, 4-smear movement, 5-radial paths, 6-next", 5, 5
			SetAlpha 1
		EndIf
		SetColor r,g,b
'		DrawImage poof,x+grid.xCenter, y+grid.yCenter
		'If blend Then SetBlend alphablend Else SetBlend lightblend
		Select blendMode
		Case 1
			SetBlend lightblend
		Case 2
			SetBlend alphablend
		Default
			SetBlend lightblend
		EndSelect
'		For Local a:Int = 0 To 5 Step 3
'			DrawImage poof,x+grid.xCenter+x1*a, y+grid.yCenter+y1*a
'		Next
		SetRotation rotation
		DrawImage poof,x+grid.xCenter+x1, y+grid.yCenter+y1
		SetRotation 0
	EndMethod
	Function reset(which:Int = 0)
		If Not which Then which = Rand(4)
'		which = 4
		Select which
		Case 1
			For Local a:Int = 0 To particles.length-1
				particles[a].x = grid.width*.5
				particles[a].y = grid.height*.8
			Next
		Case 2
		For Local a:Int = 0 To particles.length-1
			Local aa:Int = Rnd(360)
			particles[a].x = grid.width*.5+Rnd(grid.width*.4)*Sin(aa)
			particles[a].y = grid.width*.5+Rnd(grid.height*.4)*Cos(aa)
		Next
		Default
			For Local a:Int = 0 To particles.length-1
				particles[a].x = Rnd(grid.width)
				particles[a].y = Rnd(grid.height)
			Next
		EndSelect
	EndFunction
	Method New()
		rotation1 = (RndFloat()-.5)*4
		Local temp:Int = Rand(3)
		Select temp
		Case 1
			x = grid.width*.5
			y = grid.height*.9
			colourVolitility = 3
			r = 55+Rand(20)
			g = 55+Rand(20)
			b = 55+Rand(20)
			gravity=-.06
		Default
			x = Rand(grid.width)
			y = Rand(grid.height)
			r = 25+Rand(120)
			g = 25+Rand(170)
			b = 25+Rand(50)
			gravity=-.06
		EndSelect
	EndMethod
EndType
Global particles:particleType[particleNum+1]
Global timer:timerType = New timerType
Global whichParticle:Int
For Local a:Int = 0 To particleNum
	whichParticle = a
	particles[a] = New particleType
Next
Type mouseType
	Field x:Int, y:Int
	Field x1:Int, y1:Int
	Method update()
		x1 = x-MouseX()
		y1 = y-MouseY()
		If Abs(x1)&gt;5 Or Abs(y1)&gt; 5	Then
			If Abs(x1)&lt; 50 And Abs(y1)&lt;50 Then End
		EndIf
		x = MouseX()
		y = MouseY()
	EndMethod
	Method New()
		x=MouseX()
		y=MouseY()
		x1=0
		y1=0
	EndMethod
EndType
Global mouse:mouseType = New mouseType
mouse.update()

HideMouse()
Repeat
	timer.update()
	colourScheme.update()
	If timer.t50Flag Then grid.update()
	Select theme
	Case 2
		For Local a:Int = 0 To particles.length-1
			particles[a].do(a)
		Next
		For Local yy:Int = 0 To 30
			For Local xx:Int = 0 To 40
				effectsLayer[xx,yy].draw(xx, yy, theme)
			Next
		Next
	Default
		For Local yy:Int = 0 To 30
			For Local xx:Int = 0 To 40
				effectsLayer[xx,yy].draw(xx, yy, theme)
			Next
		Next
		For Local a:Int = 0 To particles.length-1
			particles[a].do(a)
		Next
	EndSelect
'	For Local a:Int = 0 To sparkNum
'		spark[a].update()
'	Next
	mouse.update()
	If KeyHit(key_1) particleType.reset()
	If KeyHit(key_2) grid.kill()
	If KeyHit(key_3) grid.explode()
	If KeyHit(key_4) grid.waterSmear()
	If KeyHit(key_5) grid.circles()
	If KeyHit(key_6) timer.t22000 = 0
	If KeyHit(key_l) grid.show = 1 - grid.show
	If grid.show grid.drawVectors()
	
'	DrawText spark[2].life,20,60
	SetAlpha .05
	SetColor 255, 255, 255
	DrawText "2011 'Dubious Drewski'", 620, 585
	SetAlpha 1
	Flip;Cls
Until KeyDown(key_escape) Or AppTerminate() Or AppSuspended()
End

Function drawPoof:TImage(xsize:Int=32, ysize:Int=32, noise:Float = 0.3)
	Local image:TImage = CreateImage (xsize,ysize,,DYNAMICIMAGE)
	Local argb:Int,a:Int, r:Int, g:Int, b:Int
	Local xmid:Int = image.width*.5
	Local yMid:Int = image.height*.5
	Local temp:TPixmap = LockImage(image)
	For Local y:Int = 0 To image.height-1
		For Local x:Int = 0 To image.width-1
			Local scale:Float = 255.0/xMid
			argb=0
			Local dist:Float = Sqr((x-xMid)^2+(y-yMid)^2)
			a=(Xmid-dist)*scale*(1+(RndFloat()-.5)*noise)
			If a&lt;0 Then a=0
			If a&gt;255 Then a=255
			argb:|a
			a = a Shl 8
			argb:|a
			a = a Shl 8
			argb:|a
			a = a Shl 8
			argb:|a
			If dist&gt; xMid argb=0
			WritePixel(temp,x,y,argb)
		Next
	Next
	UnlockImage(image)
	MidHandleImage(image)
'	DrawImage(image, 40, 40);Flip;WaitKey';End
	Return image
EndFunction

Function within:Int(x1:Float,y1:Float,x2:Float,y2:Float,xsize:Int,ysize:Int)
	If x1&gt;x2-xSize And x1&lt; x2+xSize Then
		If y1&gt;y2-ySize And y1&lt; y2+ySize Return True
	EndIf
	Return False
EndFunction

Const NORMAL:Int = 0
Const DOTTED:Int = 1
Function drawEllipse(x:Int,y:Int,xSize:Int,ySize:Int,StepSize:Int = 15, width:Int=1, style:Int = NORMAL)
	Local oldlinewidth:Int = GetLineWidth(), dot:Int = 1
	SetLineWidth width
	Local a:Int = stepSize
	Repeat
		If dot DrawLine x+xSize*Sin(a), y+ySize*Cos(a),x+xSize*Sin(a-stepSize), y+ySize*Cos(a-stepSize)
		a:+stepSize
		If style dot = 1-dot
	Until a&gt; 360
	SetLineWidth oldlinewidth
EndFunction

Const FILL:Int = 3
Const LINE:Int = 2
Const CORNERS:Int = 1
Const BOX:Int = 0

Function drawl(x1:Int,y1:Int,x2:Int,y2:Int,width:Int=1,style:Int = BOX)
	Local oldlinewidth:Int = GetLineWidth()
	SetLineWidth width
	Select style
	Case 3
		DrawRect x1,y1,x2,y2
	Case 2
		x2:+x1
		y2:+y1
		DrawLine x1,y1,x2,y2
	Case 1
		x2:+x1
		y2:+y1
		DrawLine (x1,y1,x1+(x2-x1)*.1,y1)
		DrawLine (x1,y1,x1,y1+(y2-y1)*.1)
		DrawLine (x1,y2-(y2-y1)*.1,x1,y2)
		DrawLine (x1,y2,x1+(x2-x1)*.1,y2)
		
		DrawLine (x2,y1,x2-(x2-x1)*.1,y1)
		DrawLine (x2,y1,x2,y1+(y2-y1)*.1)
		DrawLine (x2,y2-(y2-y1)*.1,x2,y2)
		DrawLine (x2,y2,x2-(x2-x1)*.1,y2)
	Default
		x2:+x1
		y2:+y1
		DrawLine (x1,y1,x2,y1)
		DrawLine (x1,y1,x1,y2)
		DrawLine (x1,y2,x2,y2)
		DrawLine (x2,y1,x2,y2)
	EndSelect
	SetLineWidth oldlinewidth
End Function

</textarea><br>What do you guys think? Any ideas to make it cooler?<br><br>EDIT: And check out this one it just randomly came up with:<br><img src="http://www.andysuderman.com/programs/screensaver5.jpg"><br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1096981"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Wow, dude. This is some beautiful stuff! <br>You should actually focus on making this bacteria under a microscope and see how far you can go with a sense of realism with it. I think this would totally rock.<br><br>It can also feel excellently like a bloodstream through some x-ray or however that would normally be filmed in action. So, yeah, focus on microscopic moments of all kinds and you could make a super successful screensaver with it, I'm sure!<br><br>However, it's all beautiful very often and you've done a great job! <br><br>ROCK ON! And thanks a whole lot for sharing, by the way. It's precious, if a code drop can produce such fun moments. :) <br><br></td></tr></table><br>
<a name="1097372"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gladclef - Ben B</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> The link didn't work for me, which makes me sad that I'm missing out on something that looks very "pretty." :'( <br><br></td></tr></table><br>
<a name="1097397"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >xlsior</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> ...The full source is included above, just copy/paste and run it yourself. <br><br></td></tr></table><br>
<a name="1097422"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Very nice! I also thought of microscopic bacteria, etc. Some really cool effects in there. <br><br></td></tr></table><br>
<a name="1097474"></a>

<a name="1097475"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dubious Drewski</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you for the ideas, guys. Bacteria, eh? I don't know why I never thought of that theme. I just never thought of it that looking way.<br><br>I've fixed the link, and made some progress on the program. Transitions are nicer and I added a nicer flaring effect - it looks fantastic now; like glowing, molten steel being poured in a zero G tornado.<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1097497"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Steve Elliott</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cool. <br><br></td></tr></table><br>
<a name="1097541"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Must be nice to look at while you're <strike>tripping</strike>   listening to some music! <br><br></td></tr></table><br>
<a name="1097547"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>Any ideas to make it cooler?<br> <br></div><br>I should have said this yesterday, but on trying to tweak it just now (rather briefly!) to fit my 1920 x 1080 display, I failed. It'd be much better if it could be set to any display size, reading GraphicsWidth and GraphicsHeight rather than being hardcoded to 800 x 600 and using 400/300 for the middle of the display, etc.<br><br>Still, I've no idea how people come up with this kind of thing and it looks great regardless. <br><br></td></tr></table><br>
<a name="1097587"></a>

<a name="1097588"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dubious Drewski</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> That's actually a good bit of criticism, thank you. When I began this little <br>pet project, my only expected audience was myself. Now that I've built it up <br>so much and added some polish, I really should start making it more user-friendly.<br><br>I wonder how far I can go with this project. I have so many visual things I <br>want to try and even basic outlines on how to implement them. I have ideas <br>for alternative ways of using the Gelmap I've implemented here.<br><br>I'm moving 540km away in a few days, so I'm going to be a bit preoccupied. <br>We'll see what I get done!<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1097599"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jsp</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice and some cool photos on your homepage! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
