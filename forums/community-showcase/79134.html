<!DOCTYPE html><html lang="en" ><head ><title >L-system fractals!</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >L-system fractals!</h1><a href="forums.php" >Community Forums</a>/<a href="topics.php?forum=61" >Showcase</a>/<a href="#bottom" >L-system fractals!</a><br><br>
<a name="888580"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >chwaga</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi, I just thought I'd share with you some Lindenmayer system fractals a made a month back.  The code is sloppy and probably uncommented, but you can make some pretty neat stuff with it.<br><br>I only made 2: a (fake) Serpinski triangle, and a Dragon's curve.<br><br>Find them <a href="http://chwaga.googlepages.com/Fractal1.exe" target="_blank">here</a> and <a href="http://chwaga.googlepages.com/Fractal2.exe" target="_blank">here</a><br><br>And for the source...<br><br>Fractal1.exe:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 1024, 768, 32, 2


Const Rule$ = "F+F-F-F+F"
Const Angle# = 120
Global Length# = 10
Global a# = 0
Global iterations# = 0

Global LString$ = "F"
DrawFractal()
Repeat

	If KeyHit(57) Then KochIt()
	If MouseHit(2) Then Stop 
	
Until KeyHit(1)

End


Function DrawFractal()
	Cls
	RenderWorld()
	Local x1#, y1#, x2#, y2#
	x1 = 0 : y1 = 768
	tmpstr$ = LString$
	
	;Scale the triangle by iteration count
	Length = -7*iterations + 54
	
	For i = 1 To Len(tmpstr)
		If Mid$(tmpstr, i, 1) = "F" Then 
			x2# = x1# + Length# * Cos(a)
			y2# = y1# + Length# * Sin(a)
			Line x1, y1, x2, y2
			x1 = x2 : y1 = y2
		EndIf
		If Mid$(tmpstr, i, 1) = "+" Then 
			a = a - Angle
		EndIf
		If Mid$(tmpstr, i, 1) = "-" Then 
			a = a + Angle
		EndIf
	Next
	
	Flip()
End Function 


Function KochIt()
	tmpstr$ = LString$
	Local x1, y1, x2, y2, a#
	x2 = 0 : y2 = 384
	LString$ = ""
	For i = 1 To Len(tmpstr)
		tmpchr$ = Mid$(tmpstr, i, 1)
		If tmpchr$ = "F" Then 
			LString = LString + Rule$
		ElseIf tmpchr$ = "+"
			LString = LString + "+"
		ElseIf tmpchr$ = "-"
			LString = LString + "-"
		EndIf
	Next
	iterations = iterations+1
	DrawFractal()
End Function
</textarea><br><br>And Fractal2.exe:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 1024, 768, 32, 2


Const Rule$ = "X+YF+"
Const Rule2$ = "-FX-Y"
Const Angle# = 90
Global Length# = 10
Global a# = 0
Global iterations# = 0
Global playerscale# = 1
Global LString$ = "FX"
DrawFractal()
Repeat

	If KeyHit(57) Then KochIt()
	If MouseHit(2) Then Stop 
	If KeyHit(200) Then ScUp()
	If KeyHit(208) Then ScDown()
	
Until KeyHit(1)

End


Function DrawFractal()
	Cls
	RenderWorld()
	Local x1#, y1#, x2#, y2#
	x1 = 640 : y1 = 525
	tmpstr$ = LString$
	

	;Scale it by iteration count
	Length = 100-(iterations*8)*playerscale
	If length =&lt; 0 Then length = 1
	
	For i = 1 To Len(tmpstr)
		If Mid$(tmpstr, i, 1) = "F" Then 
			;COLORS!!!!!!
			c# = (255*i)/Len(tmpstr)
			Color c, -c, c
			


			x2# = x1# + Length# * Cos(a)
			y2# = y1# + Length# * Sin(a)
			Line x1, y1, x2, y2
			x1 = x2 : y1 = y2
		EndIf
		If Mid$(tmpstr, i, 1) = "+" Then 
			a = a - Angle
		EndIf
		If Mid$(tmpstr, i, 1) = "-" Then 
			a = a + Angle
		EndIf
	Next
	
	Text 0, 0, "Iteration " + iterations
	Text 0, 24, "Zoom = " + playerscale
	Flip()
End Function 


Function ScUp()
	playerscale = playerscale - 0.05
	DrawFractal()
End Function

Function ScDown()
	playerscale = playerscale + 0.05
	DrawFractal()
End Function


Function KochIt()
	tmpstr$ = LString$
	LString$ = ""
	For i = 1 To Len(tmpstr)
		tmpchr$ = Mid$(tmpstr, i, 1)
		If tmpchr$ = "X" Then 
			LString = LString + Rule$
		ElseIf tmpchr$ = "Y"
			LString = LString + Rule2$
		ElseIf tmpchr$ = "+"
			LString = LString + "+"
		ElseIf tmpchr$ = "-"
			LString = LString + "-"
		EndIf
	Next
	iterations = iterations+1
	DrawFractal()
End Function
</textarea><br><br>Anyone who wants to modify this so it's more flexible is more than welcome!  Try changing the angle values, you get some weird results.  Check out the dragon (fractal2) at 75 degrees <br><br></td></tr></table><br>
<a name="888692"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >chwaga</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> bah, it's cool and you know it! somebody comment on my day's work!!!!!! <br><br></td></tr></table><br>
<a name="888694"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Xzider</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Neat <br><br></td></tr></table><br>
<a name="888835"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Torrente</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> It's cool, but it's also been done 2 or 3 times on these forums before ;)<br><br>It'd be much neater if you made an .exe that could read a file containing the information about the L-system (rules, line-length, angles, etc...), and then saves that image. A lot easier to show your work that way. Also, make support for an arbitrary amount of rules, and then you'll be on a roll. <br><br></td></tr></table><br>
<a name="888859"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >chwaga</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> nah, I'm busy :P <br><br></td></tr></table><br>
<a name="888897"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I actually made something like that.  It would read the commands from a .txt file, then compute it twice.  First time without rendering, to figure out the extreme coordinates, then second time, scaling so it fits perfectly within the window.<br>Unfortunately it is on my old drive so I don't have it available to show at the moment.<br><br>Edit:  Found it.  Here is the source to the one I wrote.<br>also it is a Blitz3D program and I never commented the code.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Const TOKEN_EOL = 0
Const TOKEN_COMMAND = 1
Const TOKEN_STRING = 2
Const TOKEN_NUMBER = 3
Const STATE_NEWLINE = 0
Const STATE_INLINE = 1

Type turtle
 Field x#,y#
 Field theta#
 Field pendown
 Field penstuck
End Type
Global turtle.turtle = New turtle
turtle\pendown = True

Type turtlestack
 Field x#,y#
 Field theta#
 Field thetastep#
 Field movestep#
End Type


Type callstack
 Field editline$
 Field position
 Field order
End Type


Type opstack
 Field op
End Type

Global thetastep#
Global movestep#
Global token$
Global level
Global callstack.callstack
Global minx#,miny#,maxx#,maxy#
Dim linedef$(255)
For t = 0 To 255
 linedef$(t) = "default"
Next

Function TForward(distance#)
DebugLog "forward"

 x# = turtle\x + Cos(turtle\theta) * distance
 y# = turtle\y + Sin(turtle\theta) * distance
 If turtle\pendown = True Then Line turtle\x,turtle\y,x,y

 turtle\x = x
 turtle\y = y
 If x &lt; minx Then minx = x
 If x &gt; maxx Then maxx = x
 If y &lt; miny Then miny = y
 If y &gt; maxy Then maxy = y
End Function

Function TRight(angle#)
 turtle\theta = turtle\theta + angle
 While turtle\theta &gt; 180
  turtle\theta = turtle\theta - 360
 Wend
 While turtle\theta &lt; -179
  turtle\theta = turtle\theta + 360
 Wend
End Function

Function TLeft(angle#)
 turtle\theta = turtle\theta - angle
 While turtle\theta &lt; -179
  turtle\theta = turtle\theta + 360
 Wend
 While turtle\theta &gt; 180
  turtle\theta = turtle\theta - 360
 Wend
End Function

Function TPenUp()
 If turtle\penstuck = False Then turtle\pendown = False
 DebugLog turtle\pendown
End Function

Function TPenDown()
 If turtle\penstuck = False Then turtle\pendown = True
DebugLog turtle\pendown
End Function

Function TPush()
 turtlestack.turtlestack = New turtlestack
 turtlestack\x = turtle\x
 turtlestack\y = turtle\y
 turtlestack\theta = turtle\theta
 turtlestack\thetastep = thetastep
 turtlestack\movestep = movestep
End Function

Function TPop()
 turtlestack.turtlestack = Last turtlestack
 If turtlestack = Null Then RuntimeError "Turtle stack underflow error!!"
 turtle\x = turtlestack\x
 turtle\y = turtlestack\y
 turtle\theta = turtlestack\theta
 thetastep = turtlestack\thetastep
 movestep = turtlestack\movestep
 Delete turtlestack
End Function

Function parse()
callstack.callstack = New callstack
callstack\editline = linedef(0)
callstack\position = 1
callstack\order = 0
Repeat
 While callstack\position &gt; Len(callstack\editline)
  Delete callstack
  callstack.callstack = Last callstack
  If callstack = Null Then Return
  DebugLog "returning to "+callstack\editline+" position "+callstack\position
 Wend
 c$ = Mid(callstack\editline,callstack\position,1)
 DebugLog "testing "+c$
 callstack\position = callstack\position + 1
 If Asc(c$) &lt;&gt; - 1
  If linedef(Asc(c$)) = "default" Or callstack\order = 1
   Select(c$)
   Case "f"
    DebugLog "f"
    TForward(movestep)
   Case "g"
    DebugLog "g"
    TPenUp()
    TForward(movestep)
    TPenDown()
   Case "["
    TPush()
   Case "]"
    TPop()
   Case "!"
    thetastep = -thetastep
   Case "-"
    DebugLog "-"
    TRight(thetastep)
   Case "+"
    DebugLog "+"
    TLeft(thetastep)
   Case "@"
    numvalue# = 0.0
    position2 = callstack\position
    tc$ = Mid(callstack\editline,position2,1)
    While ((tc$ &gt;= "0" And tc &lt;= "9") Or tc = ".") And position2 &lt;= Len(callstack\editline)
     position2 = position2 + 1
     tc$ = Mid(callstack\editline,position2,1)
    Wend
    numvalue = Mid(callstack\editline,callstack\position,position2-callstack\position)
    movestep = movestep * numvalue
   End Select
  Else
   DebugLog "calling "+c$
   order = callstack\order
   callstack.callstack = New callstack
   callstack\editline = linedef(Asc(c$))
   callstack\position = 1 
   If order = 0
    callstack\order = level
   Else
    callstack\order = order - 1
   End If
  End If
 End If
Forever
End Function

Graphics 1024,768,32,0

Cls
com$ = CommandLine()
If com$ &lt;&gt; ""
 filename$ = Mid(com,2,Len(com)-2)
Else
 filename$ = Input("Input file To parse: ")
End If
lineno = 0

filein = ReadFile(filename)
If filein = 0 Then RuntimeError "error opening file "+filename
While Not Eof(filein)
 c$ = Lower(ReadLine(filein))
 lineno = lineno + 1
 position = 0
 Repeat
  If position &gt; Len(c$) Then Exit
  position = position + 1
 Until Mid(c$,position,1) &lt;&gt; " "
 If position &lt;= Len(c$)
  c$ = Right(c$,(Len(c$)-position)+1)
  Select Left(c$,5)
  Case "angle"
   numvalue# = 0
   position = 7
   position2 = 7
   If Len(c$) &gt; 6 
    tc$ = Mid(c$,position2,1)
    While ((tc$ &gt;= "0" And tc &lt;= "9") Or tc = ".") And position2 &lt;= Len(c$)
     position2 = position2 + 1
     tc$ = Mid(c$,position2,1)
    Wend
    numvalue = Mid(c$,position,position2-position)
   End If
   DebugLog "Angle " + numvalue +" "+ position+" " + position2
   If numvalue = 0 Or numvalue &gt;= 360 Then RuntimeError "line "+lineno+": Angle must be &gt;0 And &lt;360"
   thetastep = numvalue
  Case "axiom"
   If linedef(0) = "default" Then linedef(0) = ""
   position = 6
   Repeat
    tc$ = Mid(c$,position,1)
    position = position + 1
   Until (tc$ &lt;&gt; " " And tc$ &lt;&gt; "=") Or position &gt; Len(c$)
   position = position - 1
   If position &lt;= Len(c$)
    While position &lt;= Len(c$) And Mid(c$,position,1) &lt;&gt; " " And Mid(c$,position,1) &lt;&gt; ";"
     linedef(0) = linedef(0) + Mid(c$,position,1)
     position = position + 1
    Wend
   Else
    RuntimeError "Axiom not defined"
   End If
   DebugLog linedef(0)
  Default
   DebugLog "here"
   tc$ = Left(c$,1)
   If tc &lt;&gt; ";"
    position = 2
    While Mid(c$,position,1) = " " And position &lt;= Len(c$)
     position = position + 1
    Wend
    If Mid(c$,position,1) &lt;&gt; "=" Or position &gt; Len(c$) Then RuntimeError "line "+lineno+": missing ="
    position = position + 1
    If linedef(Asc(tc)) = "default" Then linedef(Asc(tc)) = ""
    While position &lt;= Len(c$)
     While Mid(c$,position,1) = " " And position &lt;= Len(c$)
      position = position + 1
     Wend
     If position &lt;= Len(c$)
      If Mid(c$,position,1) &lt;&gt; ";"
       linedef(Asc(tc)) = linedef(Asc(tc)) + Mid(c$,position,1)
       position = position + 1
      Else
       Exit
      End If
     End If
    Wend
    DebugLog tc+"="+linedef(Asc(tc))
   End If
    
  End Select
  
 End If
   
Wend

Repeat
 Cls
 level = Input("Level? ")
 If level = 0 Then Exit
 Cls
 Print "Thinking......"

 turtle\x = 0
 turtle\y = 0
 turtle\theta = -90
 movestep = 10
 TPenUp()
; turtle\penstuck = True
 minx# = 0
 miny# = 0
 maxx# = 0
 maxy# = 0
 parse()
 Cls
 maxx = maxx - minx
 maxy = maxy - miny

 If maxx/maxy &gt; 1.333333333333
  scale# = 1023.0/maxx
 Else
  scale# = 767.0/maxy
 End If

 turtle\x = -minx*scale
 turtle\y = -miny*scale
 turtle\theta = -90
 movestep = 10*scale
 turtle\penstuck = False
 TPenDown()
 parse()
WaitKey()
Forever</textarea><br><br>Here is hilbert.ls<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">angle 90
axiom=x
x=-yf+xfx+fy-
y=+xf-yfy-fx+

</textarea> <br><br></td></tr></table><br>
<a name="889533"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Axel Wheeler</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Very cool.  Even if it's been done before, nothing stretches your brain like implementing something like this.  <br><br>I would add a percent complete indicator and a cancel option during processing.<br><br>Well done Chwaga! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
