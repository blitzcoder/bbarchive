<!DOCTYPE html><html lang="en" ><head ><title >Singularity prototype</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Singularity prototype</h1><a href="forums.php" >Community Forums</a>/<a href="topics.php?forum=61" >Showcase</a>/<a href="#bottom" >Singularity prototype</a><br><br>
<a name="1044037"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tesuji</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Not really much of anything yet but here's something small I started playing around with today. It's just a couple of gravity wells + particles but has a nice proceduel field render. Thinking it could be turned into a simple game...<br><br><img src="http://snafoo.net/downloads/singularityProtoScreenshot1.png"><br><br>For those without blitzmax :<br><br><b> Windows Download </b><br><a href="http://snafoo.net/downloads/SingularityProto_Windows.zip" target="_blank">http://snafoo.net/downloads/SingularityProto_Windows.zip</a><br><br><b> Mac OSX (Intel) Download</b><br><a href="http://snafoo.net/downloads/SingularityProto_OSX.zip" target="_blank">http://snafoo.net/downloads/SingularityProto_OSX.zip</a><br> <br>Press &lt;Space&gt; or mouse button to generate a new random level.<br><br>Here's the (somewhat messy) source for those interested :<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

SuperStrict

Framework BRL.GlMax2D

Import BRL.System
Import BRL.Random


Type TVector

	Field x:Float, y:Float

End Type

Type TLevel

	Field forces:TList = New TList
	Field particles:TList = New TList
	Field targets:TList = New TList
	Field lagrangePoints:TList = New TList
	
	Field fieldImage:TImage

	Method update()
	
		For Local p:TParticle = EachIn particles
			p.update(Self)
		Next
		
		For Local t:TTarget = EachIn targets
			If t.active Then t.update(Self)
		Next
	
	End Method
	
	Method render()
	
		SetScale GraphicsWidth()/Float(ImageWidth(fieldImage)),GraphicsWidth()/Float(ImageHeight(fieldImage))
		SetAlpha .25
		SetColor 96,96,128
		SetBlend ALPHABLEND
		DrawImage fieldImage,0,0
		SetScale 1,1

		For Local p:TParticle = EachIn particles
			p.render()
		Next
		
		For Local force:TForce = EachIn forces
			force.render()
		Next

		For Local target:TTarget = EachIn targets
			If target.active Then target.render()
		Next


	End Method
	
	Method addForce(x:Float,y:Float,s:Float)
		Local force:TForce = New TForce
		force.x = x
		force.y = y
		force.strength = s
		forces.addLast(force)
	End Method

	Method addParticle(x:Float,y:Float,sx:Float,sy:Float)

		Local particle:TParticle = New TParticle
		particle.sx = sx
		particle.sy = sy
		particle.x = x
		particle.y = y
		particles.addLast(particle)
		
	End Method
	
	Method addParticleExplosion(x:Float,y:Float,force:Float=.0002, radius:Float=0.0)
	
		Local angleInc:Float = 45*.5
		Local angle:Float = 0 
	
		While (angle &lt; 360)
			Local particle:TParticle = New TParticle
			particle.sx = Sin(angle)*force
			particle.sy = Cos(angle)*force
			particle.x = x + (Sin(angle)*radius) 			
			particle.y = y + (Cos(angle)*radius)
			particle.life = 255
			particle.size = 2
			particle.mass = False 			
			particles.addLast(particle)
			
			angle :+ angleInc
		Wend
	
	End Method
	
	Method addTarget(x:Float,y:Float,sx:Float,sy:Float)

		Local target:TTarget = New TTarget
		target.sx = sx
		target.sy = sy
		target.x = x
		target.y = y
		targets.addLast(target)
	
	End Method
	
	Method addLagrangePoint(x:Float,y:Float)
	
		' make sure there are no other points located nearby
		Local distant:Int = True
		For Local p:TLagrangePoint = EachIn lagrangePoints
			Local dx:Float = p.x - x
			Local dy:Float = p.y - y
			Local d:Float = Sqr((dx*dx)+(dy*dy))
			If Abs(d) &lt; .1 Then distant = False
		Next
		
		If distant ' only add if it's .1 or more far away from other points
			Local point:TLagrangePoint = New TLagrangePoint
			point.x = x
			point.y = y
			lagrangePoints.addLast(point)
		End If
	
	End Method
	
	Method getVector:TVector(x:Float,y:Float)
		Local v:TVector = New TVector
		
		For Local f:TForce = EachIn forces
			Local dx:Float = f.x-x
			Local dy:Float = f.y-y
			Local d:Float = (dx*dx)+(dy*dy)
			If d &lt;&gt; 0
				v.x :+ (1/(10*d))*dx*f.strength
				v.y :+ (1/(10*d))*dy*f.strength 
			End If
		Next
		
		Return v
		
	End Method

	Method generateForceImage()
	
		Local lagrange:Int = 0
	
		SetScale 1,1
		SetAlpha 1.0
		SetBlend SOLIDBLEND
	
		Local x:Float = 0
		Local y:Float = 0
		
		Local width:Int = 512
		Local height:Int = 512
		
		Local xi:Float = 1.0/Float(width)
		Local yi:Float = 1.0/Float(height)
		
		While (y &lt; 1.0)
					
			Local v:TVector = getVector(x,y)
			
			Local r:Int,g:Int,b:Int

			r = Abs(v.x*.5)*255	
			g = Abs(v.y*.5)*Abs(v.x*.5)*512	
			b = Abs(v.y*.5)*255
			
			'If r &gt;= 255 And b &gt;= 255 And g &gt;= 255
			'	r = y*255
			'	g = x * 255
			'End If	
		
			' la grange
			If Abs(v.x) &lt;= 0.045 And Abs(v.y) &lt;= 0.045 
				'r = 0 ; g = 255 ; b = 0
				addLagrangePoint(x,y)
				lagrange :+ 1
			End If
			
			SetColor 255-r,255-g,255-b
			Plot x*width,y*height
			
			x :+ xi
			If x &gt; 1 
				x = 0 
				y :+ yi
			End If
		
	Wend	

		'Print "LaGrange Count " + lagrange
	
		fieldImage = LoadImage(GrabPixmap(0,0,width-1,height-1))
	End Method

End Type

Type TForce

	Field x:Float,y:Float
	Field strength:Float
	
	Method render()

		SetBlend LIGHTBLEND
		SetColor 0,0,255
		SetAlpha .15
		SetScale .6,.6
		DrawImage sphereImage, x*GraphicsWidth(),y*GraphicsWidth() 
		SetScale .3,.3
		SetColor 0,128,0
		DrawImage sphereImage, x*GraphicsWidth(),y*GraphicsWidth() 
	
		SetBlend ALPHABLEND
		SetColor 0,0,0
		SetAlpha .5		
		SetScale 1,1
		DrawOval x*GraphicsWidth()-8,y*GraphicsWidth()-8,16,16

		SetScale 1,1
	
	End Method

End Type


Type TParticle

	Field x:Float,y:Float
	Field sx:Float,sy:Float
	Field size:Float=8.0
	Field life:Int = -1
	Field active:Int = True
	Field mass:Int = True

	Method update(level:TLevel)

		If active
			
			If mass
				Local v:TVector = level.getVector(x,y)
				sx :+ v.x*.00001
				sy :+ v.y*.00001
			End If

			x :+ sx
			y :+ sy
	
			If x &lt; 0.0 Or x &gt; 1.0 Or y &lt; 0.0 Or y &gt; 1.0 Then active = False
			If life &gt; 0 Then life :-1
			If life = 0 Then active = False

		End If
	
	End Method
	
	Method render()

		If active

			SetBlend LIGHTBLEND	
			SetColor 255,0,0
			If life &gt; 0 Then SetColor life*2,life,life
	
			Local scale:Float = size*.125*.25		
			SetScale scale,scale
			SetAlpha .2
			DrawImage sphereImage,x*GraphicsWidth(),y*GraphicsWidth()
			SetScale scale*.5,scale*.5
			SetColor 255,128,128
			DrawImage sphereImage,x*GraphicsWidth(),y*GraphicsWidth()
			SetScale scale*.25,scale*.25
			SetColor 255,255,255
			DrawImage sphereImage,x*GraphicsWidth(),y*GraphicsWidth()
			 
			SetScale 1,1

		End If
	
	End Method

End Type


Type TTarget

	Field x:Float,y:Float
	Field sx:Float,sy:Float
	Field size:Float = .0625
	Field red:Int,green:Int=255,blue:Int=128
	Field active:Int = True
	
	Method update(level:TLevel)
			
		Local v:TVector = level.getVector(x,y)
		x :+ sx
		y :+ sy
		'sx :- (v.x)*.00001
		'sy :- (v.y)*.00001
		
		sx :* .9
		sy :* .9
		
		Local collision:Int = checkCollisions(level.particles)
		If collision 
			red = 255 ;	green = 0 ; blue = 0
			level.addParticleExplosion(x,y,.0006,size*.5)
			level.addParticleExplosion(x,y,.0003,size*.25)
			level.addParticleExplosion(x,y,.000125,size*.125)
			active = False
		End If

	End Method
	
	Method checkCollisions:Int(particleList:TList)
		
		' haz I collision?		
		Local collision:Int = False
		For Local p:TParticle = EachIn particleList
		
			Local dx:Float = p.x - x
			Local dy:Float = p.y - y
			Local d:Float = Sqr((dx*dx)+(dy*dy))
			If Abs(d) &lt; (size*.5)
				collision = True
				Exit
			End If
		Next
		
		Return collision
				
	End Method
	
	Method render()
	
		Local ximageScale:Float = GraphicsWidth() / Float(ImageWidth(sphereImage)) 
		Local xscale:Float = ximageScale*size
		Local yimageScale:Float = GraphicsWidth() / Float(ImageHeight(sphereImage)) 
		Local yscale:Float = yimageScale*size

		SetColor red,green,blue
		SetScale xscale,yscale
		SetBlend LIGHTBLEND
		SetAlpha .2
		DrawImage sphereImage,x*GraphicsWidth(),y*GraphicsWidth()
		SetColor green,red,blue
		SetScale xscale*.5,yscale*.5
		DrawImage sphereImage,x*GraphicsWidth(),y*GraphicsWidth()
				
		SetScale 1,1
	
	End Method
	
End Type

Type TLagrangePoint 

	Field x:Float,y:Float

End Type


'------------------------------------------------------
Graphics 1024,768,32

Global sphereImage:TImage = createSphereImage()
SetImageHandle sphereImage,ImageWidth(sphereImage)*.5, ImageHeight(sphereImage)*.5

Local level:TLevel = New TLevel
level.addForce(.25,.5,3)
level.addForce(.75,.75,4)
level.addForce(.5,.25,5)
level.addParticle(.5,.5,-.0001,-.0002)
level.generateForceImage()
Cls

Local frame:Int = 0

While Not KeyDown(KEY_ESCAPE)

	level.update()	
	level.render()
		
	Flip
	
	frame :+ 1
	If KeyHit(KEY_SPACE) Or MouseHit(1)
		level = initRandomLevel()
		Cls
	End If
Wend

End

Function initRandomLevel:TLevel()

	Local millis:Int = MilliSecs()
	SeedRnd millis
	'Print "seed "+millis

	Local level:TLevel = New TLevel
	Local iterations:Int = Rand(3,25)
	
	For Local i:Int = 0 To iterations

		level.addForce(Rnd(0.0,1.0),Rnd(0.0,1.0),Rnd(0.0,5.0))
		level.addParticle(0.5,0.5,Rnd(0,1)*.0001,Rnd(0,1)*.0001)
	Next
	
	level.generateForceImage()
	
	For Local lg:TLagrangePoint = EachIn level.lagrangePoints
		level.addTarget(lg.x,lg.y,0,0)
	Next

	Return level

End Function

Function createSphereImage:TImage()

	Local width:Int = 128
	Local height:Int = 128
	
	SetBlend ALPHABLEND
	SetScale 1,1
	SetAlpha 0.0
	SetColor 255,255,255
	DrawRect 0,0,width,height
		
	Local level:Float = 0	
	For Local i:Float = 1 To 127 Step .5

		SetAlpha i/128.0
		DrawOval i/2,i/2,128-i,128-i
	
	Next

	Return LoadImage(GrabPixmap(0,0,width-1,height-1))

End Function
</textarea> <br><br></td></tr></table><br>
<a name="1044064"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warpy</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> oooooooooo, very nice!<br><br>The field visualisation isn't massively informative when the particles are moving though - if they left trails maybe it would be more obvious what relation the field has to their movement. <br><br></td></tr></table><br>
<a name="1045852"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >taumel</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is interesting.<br><br>Due to the black colour i had black holes in mind and was surprised that the particles could escape this easily. <br><br></td></tr></table><br>
<a name="1045892"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dynaman</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Looks very nice, you might tempt me enough to reload max just to try it. <br><br></td></tr></table><br>
<a name="1046390"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Taron</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Very elegant looking! It's really somehow exciting, actually, well done! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
