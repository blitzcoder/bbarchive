<!DOCTYPE html><html lang="en" ><head ><title >MonkeyMax - The Return</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >MonkeyMax - The Return</h1><a href="forums.php" >Monkey Targets Forums</a>/<a href="topics.php?forum=523" >User Targets</a>/<a href="#bottom" >MonkeyMax - The Return</a><br><br>
<a name="2025336"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Karja</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I had a look at the excellent work on the MonkeyMax target that's been done in Diddy, and decided to see how hard it would be to get it running again. Some blood, sweat and tears later, and here's a patched version that's compatible with Monkey v0.54c:<br><br><a href="http://www.karjasoft.com/temp/MonkeyMax_v0.54c.zip" target="_blank">http://www.karjasoft.com/temp/MonkeyMax_v0.54c.zip</a><br><br>Note: this is a work in progress! To install this, simple copy the files into a Monkey Pro directory. Note again: this will overwrite a couple of files! Most notably, it patches trans and mojo, and your config.winnt.txt, etc. So keep backups, and do some manual patching if needed.<br><br>Some simple samples are available under bananas.<br><br>What works:<br>* Normal monkey as well as mojo and diddy can be used<br>* Loading text and graphics<br>* Compiling everything that I've used in my own game, including interfaces, graphics, etc etc<br><br>What doesn't work:<br>* Sound and input - I'm just using placeholders<br>* Advanced rendering - scaling, alpha, rotation etc isn't working yet<br>* Most likely, reflection!<br>* Lots of stuff that I'll run into when I start fixing the obvious things<br><br>Comments:<br>* BlitzMax doesn't have interfaces, so an interface is just an abstract class for now. It compiles at least...<br>* Most of the necessary modifications remaining are probably just in the native mojo parts, but some Trans work is probably needed<br>* This is a work in progress! And I'll leave it for a while now - this was a proof of concept for me, and I'll look into it more later<br><br>If you do mess around with this, let me know how it works and what else is needed! Samah, therevills, if this is useful for Diddy, feel free to use and update as you see fit. <br><br></td></tr></table><br>
<a name="2025351"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Why0Why</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is great!  I bought Monkey and Jungle and after playing with it, I decided to go back to Max for now as my main target is PC/Mac and I don't feel Monkey is there yet for these platforms.  I do foresee getting to the point where I am using Monkey again when it has matured some, but this might move me back more quickly. <br><br></td></tr></table><br>
<a name="2025385"></a>

<a name="2025386"></a>

<a name="2025387"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cool! Now this is what Open Source coding is about!<br><br>Reflection "should" work, as Monkey just adds more code around the existing files to act like reflection at runtime...<br><br>Quick Q: How did you get around this issue: <a href="http://www.monkeycoder.co.nz/Community/post.php?topic=1647&amp;post=15463" target="_blank">http://www.monkeycoder.co.nz/Community/post.php?topic=1647&amp;post=15463</a> As it looks like you have just changed mojo to accept BlitzMax as a target? <br><br></td></tr></table><br>
<a name="2025389"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Karja</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Haha... I actually had to do a nasty hack to get around it. I renamed bbMain to bbMain2 during translation, and added a custom bbMain that calls bbMain2 as well as Setup if it's a mojo app. It's at the end of bmaxtranslate. <br><br></td></tr></table><br>
<a name="2025397"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah crafty! :)<br><br>Also beware of the issue with case sensitivity:  <a href="http://www.monkeycoder.co.nz/Community/post.php?topic=1573&amp;post=14615" target="_blank">http://www.monkeycoder.co.nz/Community/post.php?topic=1573&amp;post=14615</a><br><br>I'm pretty sure Trans could be changed to deal with this somehow...<br><br>With your interface change, I was think about doing to same (using Abstract) but it still doesnt really work if you want to extend and implement at the same time, but as you say at least it compiles ;)<br><br>I'm pretty busy with my current project, but if you need help let me know :) <br><br></td></tr></table><br>
<a name="2025396"></a>

<a name="2025412"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Karja</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah yes - I already ran into one issue with case sensitivity when it concerned global variables and functions. But I modified trans to have a "fg_" prefix for functions and just "g_" for variables. For the case above I guess it could be solved by, for example, assigning a steadily increasing number to each variable. X -&gt; l_x_1, x -&gt; l_x_2, etc. Or something similar.<br><br>I really should work on my project instead of messing around with this, so I'll see how much time I put into it at the moment... I'm confident that it can be done now at least! :) <br><br></td></tr></table><br>
<a name="2025413"></a>

<a name="2025414"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> I'm confident that it can be done now at least! :)  <br></div><br><br>Yeah me too, thats why I started it but I get bored of things after the inital challenge is done ;)<br><br>I've just commited your changes to the Diddy's repository :) <br><br></td></tr></table><br>
<a name="2025418"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Karja</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Coolness! I SHOULD work on the game instead, but I like this kind of tinkering... We'll see if I do some more work on it after all in the near future. :) <br><br></td></tr></table><br>
<a name="2025429"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Why0Why</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> These types of projects are why I have loved the Blitz community for years.  Looking at how robust Max is now, there is a module for almost everything.  Monkey is headed in that direction and I am sure in a year or so will be well down that road. <br><br></td></tr></table><br>
<a name="2025443"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Karja</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Okay, a small addition that ought to make things a bit more usable with Diddy and Angelfont:<br><br>mojo.bmax.bmx:<br><pre class=code>	Method SetMatrix:Int(ix:float,iy:float,jx:float,jy:float,tx:float,ty:float)
		Local sx:Float = Sqr( (ix*ix) + (jx*jx) )
		Local sy:Float = Sqr( (iy*iy) + (jy*jy) )
		Local rot:Float = Atan2( jx, ix )
		SetTransform( rot, sx, sy )
		SetOrigin( tx, ty )
		Return 0
	EndMethod</pre><br><br>Now Diddy's drawing with rotation/scaling seems to work as expected. This also makes font rendering work. I guess I couldn't keep my paws off this... <br><br></td></tr></table><br>
<a name="2025446"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> I guess I couldn't keep my paws off this...  <br></div><br>LOL! <br><br></td></tr></table><br>
<a name="2025509"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Karja</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Alright then, time for an update! Here are some additional files to make things a bit more stable. (These are just the files that differ compared "v0.54c.zip" above. It'll be easier to add them to Diddy that way.)<br><br><a href="http://www.karjasoft.com/temp/MonkeyMax_v0.54c_2.zip" target="_blank">http://www.karjasoft.com/temp/MonkeyMax_v0.54c_2.zip</a><br><br>My game is almost completely working in BlitzMax now! Stuff that works include:<br> - Mouse and keyboard input<br> - Loading graphics/text<br> - Drawing alpha/scaled bitmaps<br> - Angelfont<br> - Diddy, what I use from it (IComparable interfaces, tweens, graphics, etc)<br> - Pretty much everything I have in my game, except...<br><br>What's not working:<br> - Rotation. I just extract the scale and the translation from the matrix right now - messing with the rotation was too bothersome at the moment.<br> - Sound<br> - Config and environment variables for the BlitzMax target - I'll add some for the screen size and such later<br><br>But it's -working-! I can actually play my game on BlitzMax now. <br><br></td></tr></table><br>
<a name="2025508"></a>

<a name="2025507"></a>

<a name="2025514"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> I havent actually havent tested your new stuff yet...<br><br>I thought blending worked okay before (its been awhile since I used this):<br><br><pre class=code>Function BlitzMaxSetBlend(blend:Int)
	SetBlend(blend)
EndFunction
</pre><br><br><strike>Isnt this enough, as ALPHABLEND is just an int defined as zero in Monkey and .... </strike> Ahhh I've just checked BlitzMax's consts for blends:<br><br>'modes for SetBlend<br>Const MASKBLEND=1<br>Const SOLIDBLEND=2<br>Const ALPHABLEND=3<br>Const LIGHTBLEND=4<br>Const SHADEBLEND=5<br><br>And Monkey's<br>Const AlphaBlend=0<br>Const LightenBlend=1	'deprecated<br>Const AdditiveBlend=1<br><br>Good catch! <br><br></td></tr></table><br>
<a name="2025518"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Karja</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah, I got some strange results at first - and it didn't help that there were a bug in the casting to float as well, that had to be fixed before the correct values were even passed into the SetAlpha function. But it's all working fine now!<br><br>Most of the work was probably in the bmxtranslator file. There were many little issues with castings, binary operations, slices and so on. A single line changed there was the difference between my game locking up completely, and running smooth as a baby's bottom. <br><br></td></tr></table><br>
<a name="2025520"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just had a quick play.<br><br>The exit screen in Diddy uses:<br><pre class=code>Error ""</pre> <br>To terminate the app, but it crashes in BlitzMax.<br><br>Also Mouse Hits doesnt seem to work...<br><br>I'm off to bed now, but good job finishing off MonkeyMax so far :) <br><br></td></tr></table><br>
<a name="2025521"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Karja</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Bah, who cares - it works for me! ;) Nah, just kidding. Strange. I also use 'Error ""' but my app exited cleanly... And I just use MouseDown myself, and keep internal states; haven't tried MouseHit yet, unfortunately.<br><br>Well, there's still stuff to do then! <br><br></td></tr></table><br>
<a name="2025531"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Why0Why</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> This may drive me back over to Monkey from Max!  Especially reading the numbers for New Star Soccer and how the Flash version may have pushed purchases to other platforms. <br><br></td></tr></table><br>
<a name="2025551"></a>

<a name="2025553"></a>

<a name="2025554"></a>

<a name="2025559"></a>

<a name="2025561"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Strange. I also use 'Error ""' but my app exited cleanly... And I just use MouseDown myself, and keep internal states; haven't tried MouseHit yet, unfortunately. <br></div><br><br>That's cool, I only did a quick test. I'll have a look today since I was nearly falling asleep at the keyboard last night (long week at work).<br><br>Edit1:<br>Ah with 'Error ""', it "crashes" when you compile in debug mode, which is correct for BlitzMax it just looks strange as it just says: "Unhandled Exception" and hangs the game (Monk cannot stop it)... if you run it in release mode, its fine.<br><br>Edit2:<br>I've changed error in lang.bmx to this:<br><pre class=code>Function error( err:String )
	If err = "" Then
		End
	Else
		RuntimeError err;
	EndIf
EndFunction</pre><br>So it exits cleaner in debug mode.<br><br>With MouseHit, I think its a Diddy issue - I tested with the "pure" mojo example and it works fine...<br><br>Edit3:<br>The MouseHit issue is due to the non case-sensitive code in BlitzMax, the framework of Diddy has a variable called mouseHit:Int, which is messing it up and the InputCache stuff is messing it up too... <br><br></td></tr></table><br>
<a name="2025563"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> Damn you Karja!! You've got me interested in this again!!! :P<br><br>Just added some sound commands to MonkeyMax...<br><br>Must get back to my current project!!!! <br><br></td></tr></table><br>
<a name="2025569"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> Samah thinks we should be able to do interfaces this way:<br><br>Monkey:<pre class=code>
Interface Foo
	Method Bar:Void()
End

Class SomeClass Implements Foo
	Method Bar:Void()
		Print "hello world"
	End
End

Function Main()
	Local sc:SomeClass = New SomeClass
	Local f:Foo = sc
	f.Bar()
End
</pre><br>Blitz:<pre class=code>
Type Foo Abstract
	Global implementations:TList = New TList
	Method Bar() Abstract
EndType

Type SomeClass
	Method Bar()
		Print "hello world"
	EndMethod
EndType

Foo.implementations.AddLast(TTypeId.ForName("SomeClass"))

Local sc:SomeClass = New SomeClass
Local f:Object = sc
If Foo.implementations.Contains(TTypeId.ForObject(sc)) Then
	SomeClass(f).Bar()
EndIf
</pre><br>It looks good, but I've got no idea how to add this to Trans... O_o <br><br></td></tr></table><br>
<a name="2025564"></a>

<a name="2025565"></a>

<a name="2025566"></a>

<a name="2025567"></a>

<a name="2025582"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just going thru the Monkey examples and most of them work!! O_O<br><br>Along the way I have added more stub methods (eg SetKeyboardEnabled)<br><br>These are the ones which currently have major issues:<br><br>* Richard_Betson\mirror_fx_transform<br>- the right hand lines are in the wrong place<br><br>* hitoro\autofitdemo and hitoro\simpledemo<br>- looks wrong<br><br>* hitoro\rockout<br>- plays okay until you change the VR<br><br>* hitoro\rotationsickness<br>- the green background moves incorrectly<br><br><strike><br>* mak\audiotest<br>- crashes</strike> The code doesnt take in any extra targets when added an else it worked, apart from the music and looping sounds<br><br>* mak\bouncyaliens<br>- fails compilation - array resizing (Unable to convert from 'Object Array' to 'bb_bouncyaliens_Sprite_Array')<br><br><strike><br>* mak\firepaint<br>- TouchDown doesnt translate to MouseDown<br></strike> Fixed<br><br>* mak\mojotest<br>- the vertical lines arnt right<br><br>* mak\reflectiontest<br>- Duplicate identifier<br><br><strike><br>* mak\unicodetest<br>- "Joing" not found D'oh! LOL<br></strike> Fixed<br><br><strike><br>* warpy\bezier<br>- Exits straight away<br></strike> Fixed<br><br><strike><br>* warpy\horsey<br>- Crash<br></strike> BlitzMax doesnt load mp3s - added RuntimeError to help debug this<br><br>* warpy\monkeypuzzle<br>- the mouse isnt in the middle of the monkey head<br><br>* warpy\pegs<br><strike>- Crash</strike> Fixed the crash, but still doesnt display correctly<br><br>* warpy\transform and warpy\transform_mouse<br>-Transformations not quite right...<br><br>So overall, its nearly there...<br><br>Main issues are:<br><br>* Sound<br>* Matrix Transformations<br>* Arrays<br>* Duplicate Identifiers<br><br>And of course Interfaces... <br><br></td></tr></table><br>
<a name="2025578"></a>

<a name="2025577"></a>

<a name="2025576"></a>

<a name="2025575"></a>

<a name="2025574"></a>

<a name="2025572"></a>

<a name="2025571"></a>

<a name="2025570"></a>

<a name="2025584"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Karja</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> Wow, so cool! Haha... I love the fact that my meddling has not only messed up my own project, but others' too. ;) No, but seriously - this is really close to being quite useful now. It compiles fast, it runs in a stable way on Windows and Mac - the BlitzMax target is going to be awesome.<br><br>I won't have time to do anything for a couple of days (friends coming over, promised my better half to go shopping tomorrow, etc), but some spontaneous thoughts about the interfaces:<br><br><pre class=code>'----- No problem for this part ---
Type Foo Abstract
	Global implementations:TList = New TList
	Method Bar() Abstract
EndType

'----- Easy as pie, no issues here ---
Type SomeClass
	Method Bar()
		Print "hello world"
	EndMethod
EndType

'----- Again, this can be done - but since this will be lower than the
' calls to bbMain and so on, this won't be called early enough. So it needs 
' to be inserted further up...or we can simply move the TRANSCODE block
' further up in the template. That should work ---
Foo.implementations.AddLast(TTypeId.ForName("SomeClass"))

Local sc:SomeClass = New SomeClass
Local f:Object = sc
'----- This is where the tricky part is. To do this we have to keep extra
' info in bmxtranslate, that has to be checked against each method call... ------
If Foo.implementations.Contains(TTypeId.ForObject(sc)) Then
	SomeClass(f).Bar()
EndIf</pre><br><br>So... Basically, when an interface is noted in EmitFuncDecl, info about it has to be stored in a global list, and each method call in TransFunc (I think) has to check against that list... Wouldn't it be more efficient to just add an extra call each time we know that an object belongs to an interface? I.e., that the actual BMax code would be:<br><br><pre class=code>Type Foo Abstract
	Method Bar() Abstract
EndType

Type SomeClass
	Method Bar()
		Print "hello world"
	EndMethod
EndType

Local sc:SomeClass = New SomeClass
Local f:Object = sc
SomeClass(f).Bar()</pre><br><br>Since we could check whether or not f belongs to an interface at compile time rather at runtime. <br><br></td></tr></table><br>
<a name="2025583"></a>

<a name="2025622"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#23">[#23]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> I love the fact that my meddling has not only messed up my own project, but others' too. ;)  <br></div><br>LOL!<br><br><div class="quote"> It compiles fast, it runs in a stable way on Windows and Mac - the BlitzMax target is going to be awesome. <br></div><br>Yeah, I agree. When I first tested this I was pretty impressed with the speed.<br><br><div class="quote"> spontaneous thoughts about the interfaces <br></div><br>Sounds good... I think we should leave interfaces alone for now and get the other stuff working correctly, as its going to be hardest issue to deal with...<br><br>In other news I was bored this morning and came up with a MonkeyMax logo:<br><br><img src="http://diddy.googlecode.com/svn/trunk/monkeymax/MonkeyMaxLogo.png"><br><br>(Using the SVG logo from here: <a href="http://www.blitzbasic.com/Community/posts.php?topic=88846" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=88846</a> ) <br><br></td></tr></table><br>
<a name="2025621"></a>

<a name="2025620"></a>

<a name="2025624"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#24">[#24]</a></td></tr></table></td></tr><tr ><td class="posttext"> matrix transforms are easy if you use this opengldriver I made<br><a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=2612" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=2612</a><br><br>just use the opengl command that sets the matrix <br><br></td></tr></table><br>
<a name="2025625"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#25">[#25]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> matrix transforms are easy if you use this opengldriver I made <br></div><br>Thanks Slenkar, but again I want to be able to use DirectX and not limit the BlitzMax target. <br><br></td></tr></table><br>
<a name="2025627"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >marksibly</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#26">[#26]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>Interfaces are tricky...<br><br>I don't think the above approaches will work, because they assume that the translator knows the real type of the interface object in order to perform the downcast, eg: how would this be translated?<br><br><pre class=code>
Function DoSomething( f:Foo )
  f.Bar()
End
</pre><br><br>Without resorting to reflection, the only way I can think of doing interfaces efficiently would be to get the translator to generate 'thunk' classes that forward interface method calls to 'real' object calls, eg:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'----- monkey -----

Interface Foo
	Method Bar:Void()
End

Class SomeClass Implements Foo
	Method Bar:Void()
		Print "hello world"
	End
End

Function Main()
	Local sc:SomeClass = New SomeClass
	Local f:Foo = sc
	f.Bar()
End

'----- bmx -----

Type Foo Abstract

	Method Bar() Abstract

EndType

Type SomeClass

	Method Bar()
		Print "hello world"
	EndMethod

	Method _ToFoo:Foo()
		'create on demand, or we could pre-create in New
		If Not _Foo
			_foo=New _SomeClass_Foo 'could pool these I guess.
			_foo.p=Self
		Endif
		Return _Foo
	EndMethod
	
	'one of these for each 'implements'
	Field _Foo:_SomeClass_Foo
	
EndType

'generated by compiler for each class/implements combo...
'
Type _SomeClass_Foo Extends Foo
	
	Field p:SomeClass
	
	Method Bar:Void()
		p.Bar
	EndMethod
	
EndType

Function Main()

	Local sc:SomeClass=New SomeClass
	Local f:Foo=sc._ToFoo()
	f.Bar()

EndFunction
</textarea><br><br>This is kinda complicated though: The translator would need to generate a whole new class for each combination of class/implemented interface; TransCastExpr() would need to generate _ToBlah() cast calls when casting to interfaces; Downcasting to interfaces gets tricky - a _ToBlah() method for *every* interface could perhaps be added to a root object? Not pretty, but should work and would be fast. <br><br></td></tr></table><br>
<a name="2025639"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >marksibly</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#27">[#27]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>Another approach...<br><br>Incredibly lazy, but you could just add *all* interface methods to an internal root object, eg:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'----- monkey -----

Interface Foo
	Method Bar:Void()
End

Class SomeClass Implements Foo
	Method Bar:Void()
		Print "hello world"
	End
End

Function Main()
	Local sc:SomeClass = New SomeClass
	Local f:Foo = sc
	f.Bar()
End

'----- bmx -----

Type _Object

	'Foo interface
	'
	Method _ToFoo:_Object()
		Return Null
	EndMethod
	
	Method Bar()
		Error "Internal Error!"
	EndMethod

EndType

Type SomeClass Extends _Object

	Method Bar()
		Print "hello world"
	EndMethod

	Method _ToFoo:_Object()
		Return Self
	EndMethod
	
EndType

Function Main()

	Local sc:SomeClass=New SomeClass
	Local f:_Object=sc._ToFoo()
	f.Bar()

EndFunction
</textarea><br><br>The compiler's already type checked everything, so you should never get 'Internal Error' (yeah, right!).<br><br>You'd have to check for duplicates when adding interface methods to the 'root' object; you'd still need to use _toBlah() in TransCastExpr when casting to interfaces; there may be 'munging' issues, ie: you may have to set the 'overrides' field of FuncDecls that implement interface methods; but it should work OK and is probably the easiest way to do it I can think of.<br><br>I fact, I'll have a tinker with this myself and see if I can come up with something quick 'n' nasty - no promises though! <br><br></td></tr></table><br>
<a name="2025638"></a>

<a name="2025637"></a>

<a name="2025633"></a>

<a name="2025634"></a>

<a name="2025635"></a>

<a name="2025636"></a>

<a name="2025632"></a>

<a name="2025631"></a>

<a name="2025641"></a>

<a name="2025642"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#28">[#28]</a></td></tr></table></td></tr><tr ><td class="posttext"> Interesting Mark...<br><br>With the "lazy" way, how would it handle a class which is extended and implements at the same time?<br><br><pre class=code>Interface Foo
	Method Bar:Void()
End

Class Test
	Method TestMethod()
		Print "Testing..."
	End
End

Class SomeClass Extends Test Implements Foo
	Method Bar:Void()
		Print "hello world"
	End
End

Function Main()
	Local sc:SomeClass = New SomeClass
	Local f:Foo = sc
	f.Bar()
End</pre><br><br><div class="quote"> I fact, I'll have a tinker with this myself and see if I can come up with something quick 'n' nasty - no promises though! <br></div><br>That would be fantastic if you could, as I think it is beyond me.<br><br>Whilst you are looking Mark, could you see why bouncyaliens doesnt work please? <br><br></td></tr></table><br>
<a name="2025640"></a>

<a name="2025703"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >marksibly</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#29">[#29]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>Ok, not pretty but this sort of works.<br><br>You'll also need to add this line to the end of the InsertDecl method in decl.monkey in the trans module (line 420 here):<br><br><pre class=code>
		If decl.IsSemanted() semanted.AddLast decl
</pre><br><br>I can't actually run very much (firepaint gives an array index error; mojotest can't find drawpoint; keyboardtest can't find setkeyboardenabled) but this does at least successfully run ifacetest and ifacetest2 (if you comment out the import mojo in facetest2).<br><br>Hope it's useful...<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

' Module trans.astranslator
'
' Placed into the public domain 24/02/2011.
' No warranty implied; use at your own risk.

Import trans

Const KLUDGE_INTERFACES:=True

'To be used after reflection/semant immediately before translate
'
Function KludgeInterfaces( app:AppDecl )

	'find Object decl
	'
	Local odecl:ClassDecl
	For Local decl:=Eachin app.allSemantedDecls
		Local cdecl:=ClassDecl( decl )
		If cdecl And cdecl.munged="Object"
			odecl=cdecl
			Exit
		Endif
	Next
	
	'create Kludge class
	'
	Local idecl:=New ClassDecl
	idecl.ident="_Object"
	idecl.munged="_Object"
	idecl.attrs=DECL_SEMANTED
	idecl.superTy=Type.objectType
	idecl.superClass=odecl
	idecl.objectType=New ObjectType( idecl )
	
	'find all interfaces/methods
	'
	Local ifaces:=New List&lt;ClassDecl&gt;
	Local icasts:=New StringMap&lt;FuncDecl&gt;
	Local imethods:=New StringMap&lt;List&lt;FuncDecl&gt;&gt;

	For Local decl:=Eachin app.Semanted
	
		Local cdecl:=ClassDecl( decl )
		
		If cdecl And cdecl.IsInterface()

			'create 'To' casting method...
			'
			Local fdecl:=New FuncDecl
			fdecl.ident="_To"+cdecl.ident
			fdecl.munged="_To"+cdecl.ident
			fdecl.attrs=DECL_SEMANTED|FUNC_METHOD
			fdecl.retType=idecl.objectType
			idecl.InsertDecl fdecl
			icasts.Set fdecl.munged,fdecl
				
			For Local decl:=Eachin cdecl.Semanted
				Local fdecl:=FuncDecl( decl )
				If fdecl
					Local list:=imethods.Get( fdecl.ident )
					If list
						Local found:=False
						For Local fdecl2:=Eachin list
							If fdecl.EqualsFunc( fdecl2 )
								found=True
								Exit
							Endif
						Next
						If found Continue
					Else
						list=New List&lt;FuncDecl&gt;
						imethods.Set fdecl.ident,list
					Endif
					'
					list.AddLast fdecl
					'					
					'move decl to Kludge object...
					'
					fdecl.scope=Null
					idecl.InsertDecl fdecl
					fdecl.attrs&amp;=~DECL_ABSTRACT
					'
				Endif
			Next
		Endif
	Next
	
	'update method overrides
	'
	For Local decl:=Eachin app.Semanted
	
		Local cdecl:=ClassDecl( decl )
		
		If cdecl And Not cdecl.IsInterface()
		
			'change super class to kludge class
			'
			If cdecl.superClass=odecl
				cdecl.superClass=idecl
			Endif
			
			'create cast ops for implemented interfaces
			'
			Local mdone:=New StringSet
			For Local iface:=Eachin cdecl.implmentsAll
				If mdone.Contains( iface.ident ) Continue
				mdone.Insert iface.ident
				Local fdecl:=New FuncDecl
				fdecl.ident="_To"+iface.ident
				fdecl.munged="_To"+iface.ident
				fdecl.attrs=DECL_SEMANTED|FUNC_METHOD
				fdecl.retType=idecl.objectType
				fdecl.overrides=icasts.Get( fdecl.munged )
				cdecl.InsertDecl fdecl
				_env=fdecl
				fdecl.AddStmt New ReturnStmt( New SelfExpr().Semant() )
				_env=Null
			Next
			
			'fix method overrides
			'
			For Local decl:=Eachin cdecl.Semanted
				Local fdecl:=FuncDecl( decl )
				If fdecl And fdecl.IsMethod() And Not fdecl.overrides
					Local list:=imethods.Get( fdecl.ident )
					If list
						For Local fdecl2:=Eachin list
							If fdecl.EqualsFunc( fdecl2 )
								fdecl.overrides=fdecl2
								Exit
							Endif
						Next
					Endif
				Endif
			Next
		Endif
	Next
	
	app.mainModule.InsertDecl idecl

	app.Semanted.AddFirst idecl
	app.allSemantedDecls.AddFirst idecl
	
End

Class BmxTranslator Extends CTranslator

	Method TransType$( ty:Type )
		If VoidType( ty ) Return "Int" ' no Void type in BlitzMax
		If BoolType( ty ) Return "Int"
		If IntType( ty ) Return "Int"
		If FloatType( ty ) Return "Float"
		If StringType( ty ) Return "String"
		If ArrayType( ty ) Return TransType( ArrayType(ty).elemType )+"[]"
		If ObjectType( ty )
			Local cdecl:=ty.GetClass()
			If KLUDGE_INTERFACES
				If cdecl.IsInterface() Or cdecl.munged="Object" Return "_Object"
			Endif
			Return cdecl.munged
		Endif
		
		InternalErr
	End
	
	Method TransValue$( ty:Type,value$ )
'		Print "value = "+value
		If value
			If IntType( ty ) And value.StartsWith( "$" ) Return "0x"+value[1..]
			If BoolType( ty ) Return "true"
			If NumericType( ty ) Return value
			If StringType( ty ) Return Enquote( value )
		Else
			If BoolType( ty ) Return "false"
			If NumericType( ty ) Return "0"
			If StringType( ty ) Return "~q~q"
			If ArrayType( ty ) 
				Local elemTy:=ArrayType( ty ).elemType
				Local t$="[0]"
				Local arrayOfArrays?=False
				Local count%=0
				While ArrayType( elemTy )
					count+=1
					elemTy=ArrayType( elemTy ).elemType
					t+=",[0]"
					arrayOfArrays=True
				Wend
				If arrayOfArrays
					'Return "["+t+"]"
					t = "["+t+"]"
				Else
					'Return t
					t = t
				End
'				Print "elemTy = "+elemTy
				If elemTy="Int" Or elemTy="Bool" Then
					Return t
				Else
					Return "New "+TransType( elemTy )+t
				Endif
				
			Endif
'			If ObjectType( ty ) Return "null"
			If ObjectType( ty ) Return TransType( ty ) + "(null)"
		Endif
		InternalErr
	End
	
	Method TransValDecl$( decl:ValDecl )
		Return decl.munged+":"+TransType( decl.type )
	End
	
	Method TransArgs$( args:Expr[] )
		Local t$
		For Local arg:=Eachin args
			If t t+=","
			t+=arg.Trans()
		Next
		Return Bra(t)
	End
	
	'***** Utility *****

	Method TransLocalDecl$( munged$,init:Expr )
		Return "local "+munged+":"+TransType( init.exprType )+"="+init.Trans()
	End

	Method EmitEnter( func$ )
		Emit "pushErr();"
	End
	
	Method EmitSetErr( info$ )
		Emit "_errInfo=~q"+info.Replace( "\","/" )+"~q;"
	End
	
	Method EmitLeave()
		Emit "popErr();"
	End

	'***** Declarations *****

	Method TransStatic$( decl:Decl )
		If decl.IsExtern()
			Return decl.munged
		Else If _env And decl.scope And decl.scope=_env.ClassScope
			Return decl.munged
		Else If ClassDecl( decl.scope )
			Return decl.scope.munged+"."+decl.munged
		Else If ModuleDecl( decl.scope )
			Return decl.munged
		Endif
		InternalErr
	End

	Method TransTemplateCast$( ty:Type,src:Type,expr$ )
		If ty.ActualType().EqualsType( src.ActualType() ) Return expr
		
		If Not ObjectType( src ) Err "Can't convert from "+src.ToString()+" to "+ty.ToString()

		'Return Bra( expr+" as "+TransType(ty) )
		Return TransType(ty)+Bra(expr)
	End

	Method TransGlobal$( decl:GlobalDecl )
		Return TransStatic( decl )
	End
	
	Method TransField$( decl:FieldDecl,lhs:Expr )
		Local t_lhs$="self"
		If lhs
			t_lhs=TransSubExpr( lhs )
		Endif
		Return t_lhs+"."+decl.munged
	End
		
	Method TransFunc$( decl:FuncDecl,args:Expr[],lhs:Expr )
		If decl.IsMethod()
			Local t_lhs$="self"
			If lhs
				t_lhs=TransSubExpr( lhs )
			Endif
			Return t_lhs+"."+decl.munged+TransArgs( args )
		Endif
		Return TransStatic( decl )+TransArgs( args )
	End
	
	Method TransSuperFunc$( decl:FuncDecl,args:Expr[] )
		Return "super."+decl.munged+TransArgs( args )
	End
	
	'***** Expressions *****

	Method TransConstExpr$( expr:ConstExpr )
		Return TransValue( expr.exprType,expr.value )
	End

	Method TransNewObjectExpr$( expr:NewObjectExpr )
		Local t$
		If expr.classDecl.munged&lt;&gt;"Object"
			t="(new "+expr.classDecl.munged+")"
		Else
			t="(new "+expr.classDecl.munged+"[0])"
		End
		If expr.ctor t+="."+expr.ctor.munged+TransArgs( expr.args )
		Return t

	End

	Method TransNewArrayExpr$( expr:NewArrayExpr )
		Local texpr$=expr.expr.Trans()
'		Print "TransNewArrayExpr texpr="+texpr
		Local elemTy:=ArrayType( expr.exprType ).elemType
		'
	'	If StringType( elemTy ) Return "bb_std_lang.stringArray"+Bra(texpr)
		'		
		Local t$="["+texpr+"]"
		Local ma?=False
		While ArrayType( elemTy )
			elemTy=ArrayType( elemTy ).elemType
			t+=",new "+TransType( elemTy )+"[0]"
			ma = True
		Wend
		' TEST THIS BIT LATER!!!!!!!!!!!!!!!!!!!!!!!!&lt;!&lt;!&gt;!&lt;!&gt;&lt;!

		If ma
			Return "["+"new "+TransType( elemTy )+t+"]"
		Else
			Return "new "+TransType( elemTy )+t
		End
		
	End
		

	Method TransSelfExpr$( expr:SelfExpr )
		Return "self"
	End
	
	Method TransCastExpr$( expr:CastExpr )
		Local dst:=expr.exprType
		Local src:=expr.expr.exprType
		Local texpr$=Bra( expr.expr.Trans() )
		
		If BoolType( dst )
			If BoolType( src ) Return texpr
			If IntType( src ) Return Bra( texpr )
			If FloatType( src ) Return Bra( texpr )
			If StringType( src ) Return Bra( texpr )
			If ArrayType( src ) Return Bra( texpr)
			If ObjectType( src ) Return Bra( texpr )
		Else If IntType( dst )
			If BoolType( src ) Return Bra( texpr )
			If IntType( src ) Return texpr
			If FloatType( src ) Return "Int("+Bra( texpr )+")"
'			If StringType( src ) Return Bra( texpr )
			If StringType( src ) Return "Int("+Bra( texpr )+")"
		Else If FloatType( dst )
'			If NumericType( src ) Return texpr
			If NumericType( src ) Return "Float("+texpr+")"
			If StringType( src ) 	Return texpr
		Else If StringType( dst )
			If NumericType( src ) Return texpr
			If StringType( src )  Return texpr
		Else If ObjectType( dst ) And ObjectType( src )
			If src.GetClass().ExtendsClass( dst.GetClass() )
				Return texpr
			Else If KLUDGE_INTERFACES And dst.GetClass().IsInterface()
				Return texpr+"._To"+dst.GetClass().ident+"()"
			Else
				Return Bra( "(" + TransType( dst ) + ")(" + texpr + ")" )
			Endif
		
		Endif
#rem		
		If src.GetClass().ExtendsClass( dst.GetClass() )
			Return texpr
		Else If dst.GetClass().ExtendsClass( src.GetClass() )
			'
			If KLUDGE_INTERFACES And dst.GetClass().IsInterface()
				Return texpr+"._To"+dst.GetClass().ident+"()"
			Endif
			'
'			Return Bra( texpr + " as "+TransType( dst ) )
			Return Bra( "(" + TransType( dst ) + ")(" + texpr + ")" )
		Endif
#end
		Err "BMax translator can't convert "+src.ToString()+" to "+dst.ToString()
	End
	
	Method TransUnaryExpr$( expr:UnaryExpr )
		Local pri=ExprPri( expr )
		Local t_expr$=TransSubExpr( expr.expr,pri )
'		Return TransUnaryOp( expr.op )+t_expr
		If( expr.op = "not" )
			Return "(" + TransUnaryOp( expr.op )+t_expr + ")"
		Else
			Return TransUnaryOp( expr.op )+t_expr
		Endif
	End
	
	Method TransBinaryExpr$( expr:BinaryExpr )
		Local pri=ExprPri( expr )
		Local t_lhs$=TransSubExpr( expr.lhs,pri )
		Local t_rhs$=TransSubExpr( expr.rhs,pri-1 )
		Local t_expr$=t_lhs+TransBinaryOp( expr.op,t_rhs )+t_rhs
		If expr.op="/" And IntType( expr.exprType ) t_expr=Bra( Bra(t_expr)+"|0" )
'		Return t_expr
		If( expr.op="and" Or expr.op="or" )
			Return "(" + t_expr + ")"
		Else
			Return t_expr
		Endif
	End
	
	Method TransIndexExpr$( expr:IndexExpr )
		Local t_expr:=TransSubExpr( expr.expr )
		If StringType( expr.expr.exprType ) 
			Local t_index:=expr.index.Trans()
			'Return t_expr+".charCodeAt("+t_index+")"
			'Return t_expr+".Find("+t_expr+","+t_index+")"
			Return t_expr+"["+t_index+"]"
'			Return "Int("+t_expr+"["+t_index+"])"
		Else If ENV_CONFIG="debug"
			Local t_index:=TransExprNS( expr.index )
			Return t_expr+"["+t_index+"]"
		Else
			Local t_index:=expr.index.Trans()
			Return t_expr+"["+t_index+"]"
		Endif
	End
	
	Method TransSliceExpr$( expr:SliceExpr )
		Local t_expr$=TransSubExpr( expr.expr )
		Local t_args$="0"
		If Not expr.from And Not expr.term
			Return t_expr+"[..]"
		Endif
		If expr.from t_args=expr.from.Trans()
'		If expr.term t_args+=","+expr.term.Trans()
		t_args+=".."
		If expr.term t_args+="("+expr.term.Trans()+")"
'		Return t_expr+".slice("+t_args+")"
		Return t_expr+"["+t_args+"]"
	End
	
	Method TransArrayExpr$( expr:ArrayExpr )
		Local t$
		For Local elem:=Eachin expr.exprs
			If t t+=","
			t+=elem.Trans()
		Next
		Return "["+t+"]"
	End
	
	Method TransIntrinsicExpr$( decl:Decl,expr:Expr,args:Expr[] )
	
		Local texpr$,arg0$,arg1$,arg2$
		
		If expr texpr=TransSubExpr( expr )
		
		If args.Length&gt;0 And args[0] arg0=args[0].Trans()
		If args.Length&gt;1 And args[1] arg1=args[1].Trans()
		If args.Length&gt;2 And args[2] arg2=args[2].Trans()
		
		Local id$=decl.munged[1..]
		
		Select id

		'global functions
		Case "print" Return "print("+arg0+")" ' done
		Case "error" Return "error("+arg0+")"
		
		'string/array methods
		Case "length" Return texpr+".length" ' tested and works
		
		'array methods ' TODO!!!
		Case "resize"
			Local ty:=ArrayType( expr.exprType ).elemType
			If BoolType( ty ) Return "resize_int_array"+Bra( texpr+","+arg0 ) ' bools are ints in BlitzMax!
			If IntType( ty ) Return "resize_int_array"+Bra( texpr+","+arg0 ) ' done
			If FloatType( ty ) Return "resize_float_array"+Bra( texpr+","+arg0 ) ' done
			If StringType( ty ) Return "resize_string_array"+Bra( texpr+","+arg0 ) ' done
			If ArrayType( ty ) Return "resize_array_array_"+ArrayType( ty ).elemType+Bra( texpr+","+arg0 )
			If ObjectType( ty ) Return "resize_object_array"+Bra( texpr+","+arg0 )
			InternalErr

		'string methods
		' Done all for BMax
		Case "compare" Return texpr+".Compare"+Bra( arg0 )
		Case "find" Return texpr+".Find"+Bra( arg0+","+arg1 )
		Case "findlast" Return texpr+".FindLast"+Bra( arg0 )
		Case "findlast2" Return texpr+".FindLast"+Bra( arg0+","+arg1 )
		Case "trim" Return texpr+".Trim()"
		Case "join" Return texpr+".Joing"+Bra(arg0)
		Case "split" Return texpr+".Split"+Bra( arg0 )
		Case "replace" Return texpr+".Replace"+Bra(arg0+","+arg1)
		Case "tolower" Return texpr+".ToLower()"
		Case "toupper" Return texpr+".ToUpper()"
		Case "contains" Return texpr+".Contains"+Bra( arg0 )
		Case "startswith" Return texpr+".StartsWith"+Bra( arg0 )
		Case "endswith" Return texpr+".EndsWith"+Bra( arg0 )

		'string functions
'		Case "fromchar" Return "String.fromCharCode"+Bra( arg0 )
		Case "fromchar" Return "Chr"+Bra( arg0 )

		' ** TO-DO **
		' BlitzMax angle stuff isnt proper, need to minus 90 I think from the angle!
		
		'trig functions - degrees
		' Done all for BMax
		Case "sin","cos","tan" Return id+Bra(arg0)' "Math."+id+Bra( Bra( arg0 )+"*D2R" )
		Case "asin","acos","atan" Return id+Bra(arg0)' Bra( "Math."+id+Bra( arg0 )+"*R2D" )
		Case "atan2" Return id+Bra(arg0+","+arg1) 'Bra( "Math."+id+Bra( arg0+","+arg1 )+"*R2D" )
		
		'trig functions - radians
		' Done all for BMax
		Case "sinr" Return id[..-1]+Bra(arg0)+"*D2R" 
		Case "cosr" Return id[..-1]+Bra(arg0)+"*D2R" 
		Case "tanr" Return id[..-1]+Bra(arg0)+"*D2R" 
		Case "asinr" Return id[..-1]+Bra(arg0)+"*D2R" 
		Case "acosr" Return id[..-1]+Bra(arg0)+"*D2R" 
		Case "atanr" Return id[..-1]+Bra(arg0)+"*D2R" 
		Case "atan2r" Return id[..-1]+Bra(arg0)+"*D2R" 

		'misc math functions
		' Done all for BMax
		Case "floor","ceil","log","exp" Return id+Bra( arg0 )
		Case "sqrt" Return "Sqr"+Bra(arg0)
		Case "pow" Return arg0+"^"+arg1

		End Select

		InternalErr
	End
	
	'***** Statements *****

	Method TransAssignStmt$( stmt:AssignStmt )
	#rem
		If ENV_CONFIG="debug"
			Local ie:=IndexExpr( stmt.lhs )
			If ie
				Local t_rhs:=stmt.rhs.Trans()
				Local t_expr:=ie.expr.Trans()
				Local t_index:=TransExprNS( ie.index )
				Emit "dbg_array("+t_expr+","+t_index+")["+t_index+"]"+stmt.op+t_rhs
				Return
			Endif
		Endif
	#End
		Return Super.TransAssignStmt( stmt )
	End
	
	
	'***** Declarations *****
	
	Method EmitFuncDecl( decl:FuncDecl )
		BeginLocalScope
'		PushMungScope

		'Find decl we override
		Local odecl:=decl
		While odecl.overrides
			odecl=odecl.overrides
		Wend

		'Generate 'args' string and arg casts
		Local args$
		Local argCasts:=New StringStack
		For Local i=0 Until decl.argDecls.Length
			Local arg:=decl.argDecls[i]
			Local oarg:=odecl.argDecls[i]
			MungDecl arg
			If args args+=","
			args+=arg.munged+":"+TransType( oarg.type )
			If arg.type.EqualsType( oarg.type ) Continue
			Local t$=arg.munged
			arg.munged=""
			MungDecl arg
			'argCasts.Push "Local "+arg.munged+":"+TransType(arg.ty)+"="+t+" new "+TransType(arg.ty)+";"
			argCasts.Push "Local "+arg.munged+":"+TransType(arg.type)+"="+TransType(arg.type)+Bra(t)
		Next

'		Local t$=" "+decl.munged+":"+TransType( odecl.retType )+Bra( args )
		Local t$
		If VoidType( odecl.retType )
			t=" "+decl.munged+Bra( args )
		Else
			t=" "+decl.munged+":"+TransType( odecl.retType )+Bra( args )
		Endif
		Local funcType$ = "Function"
		Local cdecl:=decl.ClassScope()
		If cdecl And cdecl.IsInterface()
'			Emit funcType+" "+t+";"

			Local q$
			funcType = "Method"
			q+=funcType+" "

			If decl.IsAbstract()
				Emit q+t+" Abstract"
			Else
				Emit q+t+""
			End
			
			If decl.IsAbstract()
				If VoidType( decl.retType )
				'	Emit "return 0;" ' return 0 as Void = Int for BlitzMax ;)
				Else
				'	Emit "return "+TransValue( decl.retType,"" )+";"
				Endif
			Else
				For Local t$=Eachin argCasts
					Emit t
				Next
				EmitBlock decl
			Endif
			If decl.IsAbstract()
				indent=indent[..indent.Length-1]
			Else
				Emit "End"+funcType
			End

		Else
			Local q$'="internal "
			If cdecl
			'	q="public "
				If decl.IsStatic()
					q+=funcType+" "
				Else
					funcType = "Method"
					q+=funcType+" "
				Endif
			'	If decl.overrides q+="override "
			Else
				q+=funcType+" "
			Endif
			If decl.IsAbstract()
				Emit q+t+" Abstract"
			Else
				Emit q+t+""
			End
			
			If decl.IsAbstract()
				If VoidType( decl.retType )
				'	Emit "return 0;" ' return 0 as Void = Int for BlitzMax ;)
				Else
				'	Emit "return "+TransValue( decl.retType,"" )+";"
				Endif
			Else
				For Local t$=Eachin argCasts
					Emit t
				Next
				EmitBlock decl
			Endif
			If decl.IsAbstract()
				indent=indent[..indent.Length-1]
			Else
				Emit "End"+funcType
			End
			
		Endif

		EndLocalScope		
'		PopMungScope
	End
	
	'returns unreachable status!
	Method EmitBlock( block:BlockDecl )
	
		PushEnv block
		
		Local func:=FuncDecl( block )
		
		If func 
			If func.IsAbstract() InternalErr
			If ENV_CONFIG&lt;&gt;"release" EmitEnter func.scope.ident+"."+func.ident
		Endif

		For Local stmt:Stmt=Eachin block.stmts
		
			_errInfo=stmt.errInfo
			
			If unreachable	' And ENV_LANG&lt;&gt;"as"
				'If stmt.errInfo Print "Unreachable:"+stmt.errInfo
				Exit
			Endif

			If ENV_CONFIG&lt;&gt;"release"
				Local rs:=ReturnStmt( stmt )
				If rs
					If rs.expr
						EmitSetErr stmt.errInfo
						Local t_expr:=TransExprNS( rs.expr )
						EmitLeave
						Emit "return "+t_expr+";"
					Else
						EmitLeave
						Emit "return;"
					Endif
					unreachable=True
					Continue
				Endif
				EmitSetErr stmt.errInfo
			Endif
			
			Local t$=stmt.Trans()
			If t Emit t+";"
			
		Next
		
		Local r=unreachable
		unreachable=False
		
		If func And Not r

			If ENV_CONFIG&lt;&gt;"release" EmitLeave
			
			If Not VoidType( func.retType )
				If func.IsCtor()
					Emit "return self;"
				Else
					If func.ModuleScope().IsStrict()
						_errInfo=func.errInfo
						Err "Missing return statement."
					Endif
					Emit "return "+TransValue( func.retType,"" )+";"
				Endif
			Endif

		Endif
		
		PopEnv
		
		Return r
	End
	
Method Enquote$( str$ )
'	str=str.Replace( "\","\\" )
	str=str.Replace( "~q","~~q" )
	str=str.Replace( "~n","~~n" )
	str=str.Replace( "~r","~~r" )
	str=str.Replace( "~t","~~t" )
	For Local i=0 Until str.Length
		If str[i]&gt;=32 And str[i]&lt;128 Continue
		Local t$,n=str[i]
		While n
			Local c=(n&amp;15)+48
			If c&gt;=58 c+=97-58
			t=String.FromChar( c )+t
			n=(n Shr 4) &amp; $0fffffff
		Wend
		If Not t t="0"
		Select ENV_LANG
		Case "cpp"
			t="~qL~q\x"+t+"~qL~q"
		Default
			t="\u"+("0000"+t)[-4..]
		End
		str=str[..i]+t+str[i+1..]
		i+=t.Length-1
	Next
	Select ENV_LANG
	Case "cpp"
		str="L~q"+str+"~q"
	Default
		str="~q"+str+"~q"
	End
	Return str
End

	Method Emit( t$ )
		If Not t Return
		
		't=t.Replace( "\n","~~n" )
		
		If t.StartsWith( "EndIf" ) Or t.StartsWith( "EndFunction" ) Or t.StartsWith( "EndMethod" ) Or t.StartsWith( "EndType" ) Or t.StartsWith("Next") Or t.StartsWith("Else") Or t.StartsWith( "Wend" )
			indent=indent[..indent.Length-1]
		Endif
		lines.AddLast indent+t
		'code+=indent+t+"~n"
		If t.EndsWith( "Then" ) Or t.StartsWith( "Function" ) Or t.StartsWith( "Method" ) Or t.StartsWith( "Type" ) Or t.StartsWith("For") Or t.StartsWith("Else") Or t.StartsWith("While")
			indent+="~t"
		Endif
	End
	
	Method TransUnaryOp$( op$ )
'		Print "TransUnaryOp op="+op
		Select op
		Case "+" Return "+"
		Case "-" Return "-"
		Case "~~" Return op
		Case "not" Return op+" "
		End Select
		InternalErr
	End
	
	Method TransAssignOp$( op$ )
'		Print "TransAssignOp op="+op
		Select op
			Case "mod=" Return " :Mod "
			Case "shl=" Return " :shl "
			Case "shr=" Return " :shr "
			Case "+=" Return " :+"
			Case "-=" Return " :-"
			Case "*=" Return " :*"
			Case "/=" Return " :/"
			Case "|=" Return " :|"
		End
		Return op
	End
	
	Method TransBinaryOp$( op$,rhs$ )
	#rem
		Select op
		Case "+","-"
			If rhs.StartsWith( op ) Return op+" "
			Return op
		Case "*","/" Return op
		Case "shl" Return op
		Case "shr" Return op
		Case "mod" Return op
		Case "and" Return op
		Case "or" Return op
		Case "=" Return op
		Case "&lt;&gt;" Return op
		Case "&lt;","&lt;=","&gt;","&gt;=" Return op
		Case "&amp;","|" Return op
		Case "~~" Return "^"
		End Select
	#end
	Return " " + op + " "
		InternalErr
	End
	
	Method TransIfStmt$( stmt:IfStmt )
		
		If ConstExpr( stmt.expr )
			If ConstExpr( stmt.expr ).value
				If EmitBlock( stmt.thenBlock ) unreachable=True
			Else If Not stmt.elseBlock.stmts.IsEmpty()
				If EmitBlock( stmt.elseBlock ) unreachable=True
			Endif
		Else If Not stmt.elseBlock.stmts.IsEmpty()
			Emit "If"+Bra( stmt.expr.Trans() )+" Then"
			Local unr=EmitBlock( stmt.thenBlock )
			Emit "Else"
			Local unr2=EmitBlock( stmt.elseBlock )
			Emit "EndIf"
			If unr And unr2 unreachable=True
		Else
			Emit "If"+Bra( stmt.expr.Trans() )+" Then"
			Local unr=EmitBlock( stmt.thenBlock )
			Emit "EndIf"
		Endif
	End
	
	Method EmitClassDecl( classDecl:ClassDecl )

		If KLUDGE_INTERFACES
			If classDecl.IsInterface() Return
		Endif
		
'		If classDecl.IsTemplateInst()
'			InternalErr
'		Endif
	
		Local classid$=classDecl.munged
		Local superid$=classDecl.superClass.munged

'		Print "superid = "+superid + " " + classid
		
		If classDecl.IsInterface() 

			Local bases$
			
			If Not KLUDGE_INTERFACES
				For Local iface:=Eachin classDecl.implments
					If bases bases+="," Else bases=" extends "
					bases+=iface.munged
				Next
			Endif

			Local ab$=""
			If classDecl.IsAbstract()
				ab="Abstract"
			End

			If superid&lt;&gt;"Object"
				Emit "Type "+classid+" extends "+superid+bases + " " + ab
			Else
				Emit "Type "+classid + " " + ab
			Endif
'			Emit "interface "+classid+bases+"{"
			
			For Local decl:=Eachin classDecl.Semanted
				Local fdecl:=FuncDecl( decl )
				If Not fdecl Continue
				EmitFuncDecl fdecl
			Next
			Emit "EndType"
'			Emit "}"
			Return
		Endif

		Local bases$
		
		If Not KLUDGE_INTERFACES
			For Local iface:=Eachin classDecl.implments
	'			If bases bases+="," Else bases=" implements "
				If bases bases+="," Else bases=" extends "
				bases+=iface.munged
			Next
		Endif

		Local ab$=""
		If classDecl.IsAbstract()
			ab="Abstract"
		End
		
		If superid&lt;&gt;"Object"
			Emit "Type "+classid+" extends "+superid+bases + " " + ab
		Elseif bases&lt;&gt;""
			Emit "Type "+classid+bases + " " + ab
		Else
			Emit "Type "+classid + " " + ab
		Endif
		
		'members...
		For Local decl:=Eachin classDecl.Semanted
			Local tdecl:=FieldDecl( decl )
			If tdecl
				Emit "Field "+TransValDecl( tdecl )+"="+tdecl.init.Trans()+";"
				Continue
			Endif
			
			Local gdecl:=GlobalDecl( decl )
			If gdecl
				Emit "Global "+TransValDecl( gdecl )+";"
				Continue
			Endif
			
			Local fdecl:=FuncDecl( decl )
			If fdecl
				EmitFuncDecl fdecl
				Continue
			Endif
		Next
		
		Emit "EndType"
	End
	
	Method TransWhileStmt$( stmt:WhileStmt )
		Local nbroken=broken
		
		Emit "While"+Bra( stmt.expr.Trans() )+""
		Local unr=EmitBlock( stmt.block )
		Emit "Wend"
		
		If broken=nbroken And ConstExpr( stmt.expr ) And ConstExpr( stmt.expr ).value unreachable=True
		broken=nbroken
	End
	
	Method TransRepeatStmt$( stmt:RepeatStmt )
		Local nbroken=broken

		Emit "Repeat"
		Local unr=EmitBlock( stmt.block )
		Emit "Until ("+Bra( stmt.expr.Trans() )+");"

		If broken=nbroken And ConstExpr( stmt.expr ) And Not ConstExpr( stmt.expr ).value unreachable=True
		broken=nbroken
	End
	
	Method TransForStmt$( stmt:ForStmt )
		Local nbroken=broken

		Local init$=stmt.init.Trans()
		Local expr$=stmt.expr.Trans()
		Local incr$=stmt.incr.Trans()

		Local splitExpr:= expr.Split(" ")
		Local splitIncr:= incr.Split(" ")
		Local bmaxExpr$ = splitExpr[1]
		Local bmaxIncr$ = splitIncr[2]

		If bmaxExpr="&lt;=" Or bmaxExpr="&gt;="
			bmaxExpr = "To"
		Else If bmaxExpr="&lt;" Or bmaxExpr="&gt;"
			bmaxExpr = "Until"
		End

'		If( splitExpr.Length &gt; 3 )
			Local out$="For "+init+" "+bmaxExpr +" "
			Local sc:Int=2
			Local first:Bool = True
			While sc &lt; splitExpr.Length
				If first
					first=False
				Else
					out+=" "
				Endif
				out+=splitExpr[sc]
				sc+=1
			Wend
			out+=" Step "+ bmaxIncr
			Emit out
'		Else
'			Emit "For "+init+" "+bmaxExpr +" "+splitExpr[2]+ " Step "+ bmaxIncr
'		Endif
		Local unr=EmitBlock( stmt.block )
		Emit "Next"

		If broken=nbroken And ConstExpr( stmt.expr ) And ConstExpr( stmt.expr ).value unreachable=True
		broken=nbroken
	End
	
	Method TransBreakStmt$( stmt:BreakStmt )
		unreachable=True
		broken+=1
		Return "Exit"
	End

	Method TransApp$( app:AppDecl )

		If KLUDGE_INTERFACES	
			KludgeInterfaces app
		Endif
		
'		app.mainFunc.munged="bbMain"
		app.mainFunc.munged="bbMain2"
		
		For Local decl:=Eachin app.imported.Values()
			MungDecl decl
		Next
		
		For Local decl:=Eachin app.Semanted

			MungDecl decl

			Local cdecl:=ClassDecl( decl )
			If Not cdecl Continue

			BeginLocalScope			
'			PushMungScope

			For Local decl:=Eachin cdecl.Semanted
				If FuncDecl( decl ) And FuncDecl( decl ).IsCtor()
					decl.ident=cdecl.ident+"_"+decl.ident
				Endif
				MungDecl decl
			Next
			
			EndLocalScope
'			PopMungScope
		Next
	
		'decls
		'
		For Local decl:=Eachin app.Semanted

			Local gdecl:=GlobalDecl( decl )
			If gdecl
				Emit "Global "+TransValDecl( gdecl )+";"
				Continue
			Endif
			
			Local fdecl:=FuncDecl( decl )
			If fdecl
				EmitFuncDecl fdecl
				Continue
			Endif
			
			Local cdecl:=ClassDecl( decl )
			If cdecl
				EmitClassDecl cdecl
				Continue
			Endif
		Next
		
		Emit "Function bbInit()"
		For Local decl:=Eachin app.semantedGlobals
			Emit TransGlobal( decl )+"="+decl.init.Trans()+";"
		Next
		Emit "EndFunction"

		Emit "Function bbMain:Int()"
		Emit "~tLocal r:Int=bbMain2();"
		Local found:Bool = False
		For Local s:String = Eachin lines
			If s.Contains( "bb_app_device" )
				found = True
				Exit
			Endif
		Next
		If found
			Emit "~tbb_app_device.Setup();"
		Endif
		Emit "~treturn r;"
		Emit "EndFunction"
		
		Return JoinLines()
	End
	
End
</textarea> <br><br></td></tr></table><br>
<a name="2025702"></a>

<a name="2025704"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#30">[#30]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cool! I'll have a look tonight - thanks Mark!<br><br><div class="quote"> (firepaint gives an array index error; mojotest can't find drawpoint; keyboardtest can't find setkeyboardenabled)  <br></div><br><br>I've fixed these issues already, if you grab the zip file from here you should have more success: <a href="http://diddy.googlecode.com/files/monkeymax_r365.zip" target="_blank">http://diddy.googlecode.com/files/monkeymax_r365.zip</a> <br><br></td></tr></table><br>
<a name="2025713"></a>

<a name="2025715"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#31">[#31]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey Mark, just been testing and it looks good - but some of the Diddy examples now don't compile, the error is this:<br><pre class=code>Compile Error
Missing return statement.</pre><br>I tested with Tile/testTile.monkey and Particle/testParticle.monkey.<br><br>Also the IDE puts the cursor right at the bottom of the file.<br><pre class=code>TRANS monkey compiler V1.30
Parsing...
Semanting...

Process Complete</pre><br>Any ideas?<br><br>I think its the new line in decl.monkey:<br><pre class=code>	Method InsertDecl( decl:Decl )
		If decl.scope InternalErr
		
		Local ident$=decl.ident
		If Not ident Return
		
		decl.scope=Self
		decls.AddLast decl
		
		Local decls:StringMap&lt;Object&gt;
		Local tdecl:=declsMap.Get( ident )
		
		If FuncDecl( decl )
			Local funcs:=FuncDeclList( tdecl )
			If funcs Or Not tdecl
				If Not funcs
					funcs=New FuncDeclList
					declsMap.Insert ident,funcs
				Endif
				funcs.AddLast FuncDecl( decl )
			Else
				Err "Duplicate identifier '"+ident+"'."
			Endif
		Else If Not tdecl
			declsMap.Insert ident,decl
		Else
			Err "Duplicate identifier '"+ident+"'."
		Endif

		If decl.IsSemanted() semanted.AddLast decl ' &lt;---- Added for MonkeyMax
	End</pre><br>When I comment it out and rebuild trans, the above examples compile but don't work.<br><br><br>Heres a simple example of the error:<br><pre class=code>Strict

Import mojo

Function Main:Int()
	New Game()
	Return 0
End

Class Game Extends App
	Field circleShape:Shape = New Circle()
	
	Method OnCreate:Int()
		SetUpdateRate 60
		Return 0
	End
	
	Method OnUpdate:Int()
		Return 0
	End
	
	Method OnRender:Int()
		Cls
		DrawText "TEST", 100, 100
		circleShape.Draw()
		Return 0
	End
End

Interface Shape
	Method Draw:Void()
End

Class Circle Implements Shape
	Method Draw:Void()
		DrawOval 200, 200, 50, 50
	End
End</pre> <br><br></td></tr></table><br>
<a name="2025711"></a>

<a name="2025712"></a>

<a name="2025709"></a>

<a name="2025710"></a>

<a name="2025716"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Karja</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#32">[#32]</a></td></tr></table></td></tr><tr ><td class="posttext"> @therevills:<br>I just downloaded monkeymax_r365 and applied it to a Monkey Pro dir on a separate computer, added the line in decl.monkey, and made a new trans executable with it.<br><br>Every example I'm testing with is working as expected - including the test you posted. Could you try again with a clean environment and see if it works there? <br><br></td></tr></table><br>
<a name="2025722"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#33">[#33]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Karja - did you also apply the KludgeInterfaces change to the bmx translator?<br><br>Just tried a total clean install:<br><br>1. Downloaded 54c and copy monkeymax_r365 on top of monkey and ran the above sample - No problem<br>2. Changed decl.monkey and compiled trans and ran the sample - No problem<br>3. Added KludgeInterfaces change and recompiled trans and ran the same - Error!<br>4. Removed decl.monkey change, recompiled trans and tried again - the code compiles but it is missing functions for BlitzMax to compile it successfully (Identifier 'm_Draw' not found) <br><br></td></tr></table><br>
<a name="2025719"></a>

<a name="2025720"></a>

<a name="2025721"></a>

<a name="2025717"></a>

<a name="2025718"></a>

<a name="2025733"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Karja</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#34">[#34]</a></td></tr></table></td></tr><tr ><td class="posttext"> Haha, didn't get that far. I was just trying the line change first. But now I have, and I've noted some things:<br><br>Inside EmitBlock in bmxtranslator:<br><pre class=code>		If func And Not r
		
			Print( "...Dealing with " + func )

			If ENV_CONFIG&lt;&gt;"release" EmitLeave
			
			If Not VoidType( func.retType )
				If func.IsCtor()
					Emit "return self;"
				Else
					If func.ModuleScope().IsStrict()
						_errInfo=func.errInfo
'						Err "Missing return statement."
						Print "Missing return statement."
					Endif
					Emit "return "+TransValue( func.retType,"" )+";"
				Endif
			Endif
		Endif</pre><br><br>If you change the Err to a Print, your test code compiles and works. Also, the printout I added shows that there's a problem when "...Dealing with Method _Object._ToShape:_Object()". Which seems to be this generated BlitzMax code:<br><br><pre class=code>Type _Object 
	Method  _ToShape:_Object()
		return _Object(null);
	EndMethod
	Method  m_Draw()
	EndMethod
EndType</pre><br><br>...But I didn't have time to look further right now. <br><br></td></tr></table><br>
<a name="2025768"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#35">[#35]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah its a "Strict" issue with the KludgeInterfaces...<br><br>If you run this code:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict

Import mojo

Function Main:Int()
	New Game()
	Return 0
End

Class Game Extends App
	'Field circleShape:Shape = New Circle()
	
	Method OnCreate:Int()
		SetUpdateRate 60
		Return 0
	End
	
	Method OnUpdate:Int()
		Return 0
	End
	
	Method OnRender:Int()
		Cls
		DrawText "TEST", 100, 100
		'circleShape.Draw()
		Return 0
	End
End
#rem
Interface Shape
	Method Draw:Void()
End

Class Circle Implements Shape
	Method Draw:Void()
		DrawOval 200, 200, 50, 50
	End
End
#end</textarea><br><br>Its fine, but as soon as you uncomment the commented lines you get the "Missing return statement" error. If you then remove "Strict" it works fine. <br><br></td></tr></table><br>
<a name="2025777"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >marksibly</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#36">[#36]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>This hopefully fixes strict mode...<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Function KludgeInterfaces( app:AppDecl )

	'find Object decl
	'
	Local odecl:ClassDecl
	For Local decl:=Eachin app.allSemantedDecls
		Local cdecl:=ClassDecl( decl )
		If cdecl And cdecl.munged="Object"
			odecl=cdecl
			Exit
		Endif
	Next
	
	'create Kludge class
	'
	Local idecl:=New ClassDecl
	idecl.ident="_Object"
	idecl.munged="_Object"
	idecl.attrs=DECL_SEMANTED
	idecl.superTy=Type.objectType
	idecl.superClass=odecl
	idecl.objectType=New ObjectType( idecl )

	Local inull:=New ConstExpr
	inull.exprType=idecl.objectType
	
	'find all interfaces/methods
	'
	Local ifaces:=New List&lt;ClassDecl&gt;
	Local icasts:=New StringMap&lt;FuncDecl&gt;
	Local imethods:=New StringMap&lt;List&lt;FuncDecl&gt;&gt;

	For Local decl:=Eachin app.Semanted
	
		Local cdecl:=ClassDecl( decl )
		
		If cdecl And cdecl.IsInterface()

			'create 'To' casting method...
			'
			Local fdecl:=New FuncDecl
			fdecl.ident="_To"+cdecl.ident
			fdecl.munged="_To"+cdecl.ident
			fdecl.attrs=DECL_SEMANTED|FUNC_METHOD
			fdecl.retType=idecl.objectType
			idecl.InsertDecl fdecl
			icasts.Set fdecl.munged,fdecl
			fdecl.AddStmt New ReturnStmt( inull )
				
			For Local decl:=Eachin cdecl.Semanted
				Local fdecl:=FuncDecl( decl )
				If fdecl
					Local list:=imethods.Get( fdecl.ident )
					If list
						Local found:=False
						For Local fdecl2:=Eachin list
							If fdecl.EqualsFunc( fdecl2 )
								found=True
								Exit
							Endif
						Next
						If found Continue
					Else
						list=New List&lt;FuncDecl&gt;
						imethods.Set fdecl.ident,list
					Endif
					'
					list.AddLast fdecl
					'					
					'move decl to Kludge object...
					'
					fdecl.scope=Null
					idecl.InsertDecl fdecl
					fdecl.attrs&amp;=~DECL_ABSTRACT
					If Not VoidType( fdecl.retType )
						Local expr:=New ConstExpr
						expr.exprType=fdecl.retType
						fdecl.AddStmt New ReturnStmt( expr )
					Endif
					'
				Endif
			Next
		Endif
	Next
	
	'update method overrides
	'
	For Local decl:=Eachin app.Semanted
	
		Local cdecl:=ClassDecl( decl )
		
		If cdecl And Not cdecl.IsInterface()
		
			'change super class to kludge class
			'
			If cdecl.superClass=odecl
				cdecl.superClass=idecl
			Endif
			
			'create cast ops for implemented interfaces
			'
			Local mdone:=New StringSet
			For Local iface:=Eachin cdecl.implmentsAll
				If mdone.Contains( iface.ident ) Continue
				mdone.Insert iface.ident
				Local fdecl:=New FuncDecl
				fdecl.ident="_To"+iface.ident
				fdecl.munged="_To"+iface.ident
				fdecl.attrs=DECL_SEMANTED|FUNC_METHOD
				fdecl.retType=idecl.objectType
				fdecl.overrides=icasts.Get( fdecl.munged )
				cdecl.InsertDecl fdecl
				Local expr:=New SelfExpr
				expr.exprType=cdecl.objectType
				fdecl.AddStmt New ReturnStmt( expr )
			Next
			
			'fix method overrides
			'
			For Local decl:=Eachin cdecl.Semanted
				Local fdecl:=FuncDecl( decl )
				If fdecl And fdecl.IsMethod() And Not fdecl.overrides
					Local list:=imethods.Get( fdecl.ident )
					If list
						For Local fdecl2:=Eachin list
							If fdecl.EqualsFunc( fdecl2 )
								fdecl.overrides=fdecl2
								Exit
							Endif
						Next
					Endif
				Endif
			Next
		Endif
	Next
	
	app.mainModule.InsertDecl idecl

	app.Semanted.AddFirst idecl
	app.allSemantedDecls.AddFirst idecl
	
End
</textarea> <br><br></td></tr></table><br>
<a name="2025779"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#37">[#37]</a></td></tr></table></td></tr><tr ><td class="posttext"> Looks good - Thanks again Mark!<br><br>With that change in decl.monkey, is it "safe" for the other targets? <br><br></td></tr></table><br>
<a name="2025780"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >marksibly</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#38">[#38]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>&gt; With that change in decl.monkey, is it "safe" for the other targets? <br><br>Should be... <br><br></td></tr></table><br>
<a name="2025781"></a>

<a name="2025782"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#39">[#39]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Should be... <br></div><br>Famous last works! ;)<br><br>So back to the other current issues with MonkeyMax:<br><br>* Sound<br>* Matrix Transformations<br>* Arrays<br>* Duplicate Identifiers<br><strike>* Interfaces</strike><br><br>I've updated the zip file for MonkeyMax on the GoogleCode site (MonkeyMax_r370.zip). <br><br></td></tr></table><br>
<a name="2025790"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Karja</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#40">[#40]</a></td></tr></table></td></tr><tr ><td class="posttext"> Regarding the matrix transformations, I'm thinking that there are a couple of different ways to do this:<br><br>1) Since we know the order in which rotation, scaling and translation is performed, we can reverse these operations from the matrix we get in SetMatrix. However, SetMatrix is called many times. Ineffective to do ATan operations and such things each time.<br><br>2) Use a BlitzMax OpenGL module that allows setting the matrix directly. But we want to use DX as well.<br><br>3) Mess around with Max2D to set the matrix directly. I don't like this option - we don't want to change the target, and I don't think we have public access to all necessary methods and fields.<br><br>4) Modify the GraphicsDevice in mojo to add a couple of methods:<br><br><pre class=code>Class GraphicsDevice="gxtkGraphics"

	Method Mode()
	Method Width()
	Method Height()
	
	Method LoadSurface:Surface( path$ )
	
	Method Cls( r#,g#,b# )
	Method SetAlpha( alpha# )
	Method SetColor( r#,g#,b# )
	Method SetMatrix( ix#,iy#,jx#,jy#,tx#,ty# )
	Method SetScissor( x,y,width,height )
	Method SetBlend( blend )

	Method SetTransform( tx#, ty# )
	Method SetScale( sx#, sy# )
	Method SetRotation( a# )
	
	Method DrawPoint( x#,y# )
	Method DrawRect( x#,y#,w#,h# )
	Method DrawLine( x1#,y1#,x2#,y2# )
	Method DrawOval( x#,y#,w#,h# )
	Method DrawPoly( verts#[] )
	Method DrawSurface( surface:Surface,x#,y# )
	Method DrawSurface2( surface:Surface,x#,y#,srcx,srcy,srcw,srch )
End

Function Translate( x#,y# )
	Transform 1,0,0,1,x,y
	context.device.SetTranslate( x, y )
End Function

Function Scale( x#,y# )
	Transform x,0,0,y,0,0
	context.device.SetScale( x, y )
End Function

Function Rotate( angle# )
	Transform Cos(angle),-Sin(angle),Sin(angle),Cos(angle),0,0
	context.device.SetRotation( angle )
End Function</pre><br><br>The downside is that all mojo targets need to be modified to have dummy implementations of these methods. Also, SetMatrix is called directly sometimes, so we still need to check out for a call to set an identity matrix, and reset everything then.<br><br>Any thoughts? I see 1 or 4 as the real options. 1 is most non-intrusive but a bit inefficient. I like that method even though I'll need to read up on matrix math for it. :P <br><br></td></tr></table><br>
<a name="2025794"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#41">[#41]</a></td></tr></table></td></tr><tr ><td class="posttext"> Number 1 is our only real option... we have nearly got it, only a few of the demos mess up.<br><br>Yeah Atan is slow, could we use a lookup table to increase the speed? <br><br></td></tr></table><br>
<a name="2026601"></a>

<a name="2026602"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#42">[#42]</a></td></tr></table></td></tr><tr ><td class="posttext"> Samah's back from Japan and he has fixed the matrix stuff by messing around with it for 30 mins or so :)<br><br><a href="http://code.google.com/p/diddy/source/detail?r=373" target="_blank">http://code.google.com/p/diddy/source/detail?r=373</a><br><br>* Sounds (to finish)<br><strike>* Matrix Transformations</strike><br>* Arrays (to fix resize_object_array)<br>* Duplicate Identifiers (todo... any ideas??)<br><strike>* Interfaces</strike> <br><br></td></tr></table><br>
<a name="2026587"></a>

<a name="2026578"></a>

<a name="2026579"></a>

<a name="2026615"></a>

<a name="2026616"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#43">[#43]</a></td></tr></table></td></tr><tr ><td class="posttext"> Again Samah suggested a way to find with Duplicate Identifiers...<br><br>We now append "_1" in front of any uppercase letters, this makes the output code a bit more messy but it works ;)<br><br><a href="http://code.google.com/p/diddy/source/detail?r=374" target="_blank">http://code.google.com/p/diddy/source/detail?r=374</a><br><br>* Sounds (to finish)<br><strike>* Matrix Transformations</strike><br>* Arrays (to fix resize_object_array)<br><strike>* Duplicate Identifiers</strike><br><strike>* Interfaces </strike><br><br>The list is getting smaller :) <br><br></td></tr></table><br>
<a name="2026663"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#44">[#44]</a></td></tr></table></td></tr><tr ><td class="posttext"> After banging my head against the keyboard I've thought of a way to fix the Array resize object issue... BlitzMax can use Slicing to resize arrays!!! Pretty simple why didnt I think of it earlier!<br><br>So now its only really sounds to finish and a big testing phase...<br><br>Oh and the CONFIG variables to add :)<br><br><a href="http://code.google.com/p/diddy/source/detail?r=376" target="_blank">http://code.google.com/p/diddy/source/detail?r=376</a> <br><br></td></tr></table><br>
<a name="2026686"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Shinkiro1</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#45">[#45]</a></td></tr></table></td></tr><tr ><td class="posttext"> Awesome guys. <br><br></td></tr></table><br>
<a name="2026696"></a>

<a name="2026697"></a>

<a name="2026698"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#46">[#46]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just updated MonkeyMax to be up-to-date with Monkey v56b and uploaded a new zip file which includes various fixes:<br><br><a href="http://diddy.googlecode.com/files/monkeymax_r379.zip" target="_blank">http://diddy.googlecode.com/files/monkeymax_r379.zip</a> <br><br></td></tr></table><br>
<a name="2026754"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#47">[#47]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oh BTW Mark, if you are reading this, I managed to test the Diddy's GUI example which is very heavy on interfaces and it compiled and ran, although the mouse input didnt work, but it displayed the GUI fine!! So you Interface Kludge worked great! <br><br></td></tr></table><br>
<a name="2026763"></a>

<a name="2026765"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#48">[#48]</a></td></tr></table></td></tr><tr ><td class="posttext"> I tried to compile a box2d demo for a laugh, <br><br>got an error on this line of the generated code:<br>t_b.f_m_flags&amp;=~bb_b2body_b2_1Body.g_e_island_1Flag;<br><br>I think its the &amp; symbol? or maybe the ~ symbol?<br><br>the error message mentions the &amp;<br><br>also in my font class I have this "~" which doesnt compile because its an escape character in blitzmax <br><br></td></tr></table><br>
<a name="2026766"></a>

<a name="2026767"></a>

<a name="2026768"></a>

<a name="2026769"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#49">[#49]</a></td></tr></table></td></tr><tr ><td class="posttext"> Good testing!!<br><br>Its the &amp;= operator, I forgot to include that one, and I've just fixed it (so it gets translated to :&amp; for BlitzMax).<br><br>I've checked it in so if you want can you try it again...<br><br><div class="quote"> also in my font class I have this "~" which doesnt compile because its an escape character in blitzmax  <br></div> Yeah I've seen this before, <strike> I need to fix this one later...</strike> ...Fixed...  :) <br><br></td></tr></table><br>
<a name="2026787"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Karja</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#50">[#50]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is awesome - you've done great stuff lately! :) I feel a bit bad for digging this up and then letting you do all the nasty work, but I blame a new job, family visits over Easter...and the fact that I bought a PS3 and Disgaea 3... <br><br></td></tr></table><br>
<a name="2026789"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#51">[#51]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> I feel a bit bad for digging this up and then letting you do all the nasty work <br></div><br>LOL! Well you can help by doing some testing ;)<br><br>@slenkar, the fix above does get Box2D compiled, but it looks like there is a calculation error somewhere as the bodies do not draw correctly. The best example which works is the buoyancy one. <br><br></td></tr></table><br>
<a name="2026833"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#52">[#52]</a></td></tr></table></td></tr><tr ><td class="posttext"> thanks for the fixes<br><br><br>hey do you think this might be a good idea?<br><br>Put the code that is common to all blitzmax programs in a blitzmax MODULE<br>then when compiling, you are only compiling game logic stuff.<br><br>Also I have a GUI module in monkey,(code that is rarely changed)  maybe there could be a way of excluding this from the main blitzmax file as I would put it in a module.<br><br>the best way of doing that could be excluding stuff that is imported, and allowing users to convert that code to blitzmax another way e.g. when someone alters their gui module<br><br>re:box2d drawing<br>blitzmax and monkey are based on the way opengl works<br>directx works differently e.g. when scaling something negatively it doesnt mirror, it merely disappears from the screen, mark wrote code to fix these differences in blitzmax.&lt;-just in case you didnt already know.<br><br>I found another error BTW:<br>In my monkey file the line looks like this:<br><pre class=code>
If String(world_map.inst.minutes).Length=1
</pre><br>but in blitzmax the line doesnt convert the 'minutes' float to a string, it just puts the float in extra brackets<br>it looks like this:<br><pre class=code>
If((bb_worldmapclass_world_map.g_inst.f_minutes).length = 1) Then
</pre> <br><br></td></tr></table><br>
<a name="2026832"></a>

<a name="2026831"></a>

<a name="2026818"></a>

<a name="2026819"></a>

<a name="2026846"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#53">[#53]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah that sounds like a good idea with the module, once this is solid I'll have a lot at that.<br><br>re:box2d - I believe its a calculation error as when I output the vertices they are different on BlitzMax compared to the other targets.<br><br>re: another error - Thanks I'll look at this one soon :) <br><br></td></tr></table><br>
<a name="2026853"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#54">[#54]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> but in blitzmax the line doesnt convert the 'minutes' float to a string, it just puts the float in extra brackets <br></div><br><br>Just fixed this one... but in your example you talk about a float which isnt going to be the same for all targets (HTML5 will be 1, C++ will be 3, for BlitzMax it will be 10). <br><br></td></tr></table><br>
<a name="2026877"></a>

<a name="2026878"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#55">[#55]</a></td></tr></table></td></tr><tr ><td class="posttext"> You can now use the Box2D module in MonkeyMax :)<br><br>(I had the CosR, SinR etc commands wrong)<br><br><a href="http://code.google.com/p/diddy/source/detail?r=384" target="_blank">http://code.google.com/p/diddy/source/detail?r=384</a><br><br>And heres an updated zip: <a href="http://diddy.googlecode.com/files/monkeymax_r384.zip" target="_blank">http://diddy.googlecode.com/files/monkeymax_r384.zip</a> <br><br></td></tr></table><br>
<a name="2026880"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Skn3</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#56">[#56]</a></td></tr></table></td></tr><tr ><td class="posttext"> wow! this has really progressed fast! Good work :D.. should soooo become an official target ;) <br><br></td></tr></table><br>
<a name="2026898"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Soap</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#57">[#57]</a></td></tr></table></td></tr><tr ><td class="posttext"> +1 to official target! <br><br></td></tr></table><br>
<a name="2026907"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#58">[#58]</a></td></tr></table></td></tr><tr ><td class="posttext"> Heres a video of MonkeyMax in action with Box2d:<br><br><iframe width="560" height="345" src="http://www.youtube.com/embed/nGChzUETzOc" frameborder="0" allowfullscreen></iframe> <br><br></td></tr></table><br>
<a name="2026927"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DruggedBunny</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#59">[#59]</a></td></tr></table></td></tr><tr ><td class="posttext"> Wow, amazing work! My first test (a reasonably complex demo with changing autofit scaling) worked perfectly on the first attempt. Brilliant stuff!<br><br>I did run into a minor problem you might want to look at, using <a href="http://www.hi-toro.com/monkey/rockout.zip" target="_blank">this crappy example</a>: on all other platforms, reducing the delta-timed framerate below the default 60 (in-game via the down cursor) keeps the game at the same speed, but on BMAX the game speeds up. (Yet increasing the framerate via the up cursor works as per the other platforms, ie. stays steady.) <br><br></td></tr></table><br>
<a name="2026932"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#60">[#60]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey James, yeah I've seen that before too, but did'nt really look into it till now.<br><br>I've just committed a fix I hope works, its based off the HTML5 and Android Mojo which adds a delay after render based on the nextupdate-now... MonkeyMax's Mojo is based off Flash's which doesnt have this delay.<br><br>Can you please try it, by just replacing the mojo.bmax.bmx file with this one: <a href="http://code.google.com/p/diddy/source/detail?r=385" target="_blank">http://code.google.com/p/diddy/source/detail?r=385</a> <br><br></td></tr></table><br>
<a name="2026971"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Neuro</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#61">[#61]</a></td></tr></table></td></tr><tr ><td class="posttext"> I got a linking error : "D:/BlitzMax/bin/ld.exe: cannot find" even though its clearly there. Any ideas? <br><br></td></tr></table><br>
<a name="2026988"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DruggedBunny</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#62">[#62]</a></td></tr></table></td></tr><tr ><td class="posttext"> @therevills: Sorry for the delay in getting back, but that certainly seems to do the trick! <br><br></td></tr></table><br>
<a name="2026994"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#63">[#63]</a></td></tr></table></td></tr><tr ><td class="posttext"> @James, no worries - I was asleep anyway :P Glad it worked :)<br><br>@Neuro - No ideas, sorry, but a quick google might ;) Try creating a little program in BlitzMax and see if that works, if not try reinstalling BlitzMax. <br><br></td></tr></table><br>
<a name="2026997"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >zoqfotpik</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#64">[#64]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks very much, this must have been crazy difficult. <br><br></td></tr></table><br>
<a name="2026999"></a>

<a name="2027000"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ErikT</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#65">[#65]</a></td></tr></table></td></tr><tr ><td class="posttext"> Found a minor snag with translated DrawImage functionality. Drawing images with scale x or y set to -1 doesn't flip them as in monkey; instead they get rotated or something. <br><br>This is super though, great going :D <br><br></td></tr></table><br>
<a name="2027003"></a>

<a name="2027004"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Goodlookinguy</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#66">[#66]</a></td></tr></table></td></tr><tr ><td class="posttext"> I started running through and testing this on several tests I've made as well as other stuff I have sitting around. I noticed that <a href="http://www.charliesgames.com/wordpress/?p=543" target="_blank">Monkey Blob Monster</a> kind-of freaks out on BlitzMax target while every other target is fine. I don't know if it's their code or yours. Hope this helps with something. <br><br></td></tr></table><br>
<a name="2027023"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#67">[#67]</a></td></tr></table></td></tr><tr ><td class="posttext"> @ErikT... that minor snag is quite a major headache to fix!! LOL<br><br>@GLG - thanks for that, it might actually be related to Erik's issue, and it is our code which is the issue ;) <br><br></td></tr></table><br>
<a name="2027130"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Samah</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#68">[#68]</a></td></tr></table></td></tr><tr ><td class="posttext"> Scaling stuff fixed and checked in. See this post: <a href="http://www.monkeycoder.co.nz/Community/post.php?topic=2683&amp;post=27129" target="_blank">http://www.monkeycoder.co.nz/Community/post.php?topic=2683&amp;post=27129</a> <br><br></td></tr></table><br>
<a name="2027152"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#69">[#69]</a></td></tr></table></td></tr><tr ><td class="posttext"> Top work, Samah!<br><br>That was very hard to fix!!<br><br>I've just uploaded a new zip with the matrix fix and the logic rate fix:<br><br><a href="http://diddy.googlecode.com/files/monkeymax_r387.zip" target="_blank">http://diddy.googlecode.com/files/monkeymax_r387.zip</a><br><br>And the matrix fix also did fixed the Monkey Blob Monster too :D <br><br></td></tr></table><br>
<a name="2027154"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#70">[#70]</a></td></tr></table></td></tr><tr ><td class="posttext"> Great job guys this is a very interesting project and something that will definitely come in handy. I need to write editors for my projects and I was dreading having to rewrite my framework in BlitzMax first. This should allow me to do it with ease. I'll be testing this out very soon, I'll let you know how I get on. <br><br></td></tr></table><br>
<a name="2027155"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Samah</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#71">[#71]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Top work, Samah!<br><br>That was very hard to fix!! <br></div><br>Bloody matrices... my head hurts.<br><br>orz <br><br></td></tr></table><br>
<a name="2027162"></a>

<a name="2027163"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Goodlookinguy</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#72">[#72]</a></td></tr></table></td></tr><tr ><td class="posttext"> Good job on figuring that out. Now all of my newer code seems to work. A bit of my old stuff keeps compiling and, only in the bmax target, quits with an error that says it's attempting to access a method/field of a null object. Which is bull crap in some of them and I'm not sure what's causing that. In fact, at least one of Challenger GUI's examples quits with that as well. <br><br></td></tr></table><br>
<a name="2027187"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#73">[#73]</a></td></tr></table></td></tr><tr ><td class="posttext"> I tried the latest version, the matrix handling is a big improvement with one small difference, when I use the transform command the results dont look the same,<br>this is the code needed to see the difference:<br><a href="http://monkeycoder.co.nz/Community/posts.php?topic=503" target="_blank">http://monkeycoder.co.nz/Community/posts.php?topic=503</a><br>youll need an image 64x64 or 128x128<br><br>The only other difference is the default screen size of blitzmax is larger than 640x480<br>every other target has 640x480 default <br><br></td></tr></table><br>
<a name="2027185"></a>

<a name="2027186"></a>

<a name="2027226"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#74">[#74]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> when I use the transform command the results dont look the same <br></div><br>You are making us cry now :'(<br><br><div class="quote"> every other target has 640x480 default  <br></div><br>Fixed! <br><br></td></tr></table><br>
<a name="2027222"></a>

<a name="2027223"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#75">[#75]</a></td></tr></table></td></tr><tr ><td class="posttext"> sorry :'{ <br><br></td></tr></table><br>
<a name="2027227"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#76">[#76]</a></td></tr></table></td></tr><tr ><td class="posttext"> Actually, this might be a limitation on BlitzMax... I'm not sure it can shear images... <br><br></td></tr></table><br>
<a name="2027245"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#77">[#77]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> quits with an error that says it's attempting to access a method/field of a null object. <br></div><br>@GLG - Have you got a small of example of that error? <br><br></td></tr></table><br>
<a name="2027282"></a>

<a name="2027283"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ErikT</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#78">[#78]</a></td></tr></table></td></tr><tr ><td class="posttext"> Works great!<br><br>Except now another bug has popped up. Nothing to worry about though, minor snag :) <br><br>The framerate in max always ends up running at half the speed I set it to in Monkey. If I set SetUpdateRate to 60 then the bmax version runs at 30 fps, 120 gives 60 fps etc. This didn't happen in r384. <br><br></td></tr></table><br>
<a name="2027285"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#79">[#79]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> If I set SetUpdateRate to 60 then the bmax version runs at 30 fps, 120 gives 60 fps etc <br></div><br>Can you show my your timing methods and where you are calculating it?<br><br>I added a delay function on r385 (http://www.monkeycoder.co.nz/Community/post.php?topic=2560&amp;post=26932) to try to match the other targets.<br><br>When I set it SetUpdateRate to 60 and use this crappy fps counter:<br><pre class=code>
		frames+=1
		Local e:=Millisecs()-time
		If e&gt;=1000
			fps=frames
			frames=0
			time+=e
		Endif
</pre><br><br>In OnRender it does output 60FPS... but if I set it the update rate to 30 it does 15FPS.<br><br>I think it might be due to how BlitzMax locks itself to the monitor refresh rate (or something)... <br><br></td></tr></table><br>
<a name="2027284"></a>

<a name="2027286"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ErikT</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#80">[#80]</a></td></tr></table></td></tr><tr ><td class="posttext"> Apart from SetUpdateRate which is called in OnCreate() I don't use any timing methods at all. Everything runs within OnRender() though, don't know if that makes any difference... <br><br></td></tr></table><br>
<a name="2027287"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#81">[#81]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah sorry, I was talking about your FPS calculations (how do you know its running at half the speed?). <br><br></td></tr></table><br>
<a name="2027288"></a>

<a name="2027289"></a>

<a name="2027290"></a>

<a name="2027291"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ErikT</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#82">[#82]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oh right. Er, I just eyeball it :] It runs consistently at half the speed of whatever I set it to so I know it's not because the computer's struggling or anything.<br><br>EDIT: Gimme a sec, I'll check the fps with fraps.<br><br>EDIT2: <br><br>SetUpdateRate 120 = 59-60 fps<br>SetUpdateRate 60 = 30-31 fps<br>SetUpdateRate 30 = 30-31 fps<br><br>Hmm... yeah sorry, was wrong about SetUpdateRate 30. <br><br></td></tr></table><br>
<a name="2027309"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#83">[#83]</a></td></tr></table></td></tr><tr ><td class="posttext"> No worries ErikT, I'll look at it later. Which example or code were you running?<br><br>@GLG - I've managed to get ChallengerGUI compiling and running on MonkeyMax... stupid backwards String slicing (Oi! ZIGGY!!!!) <br><br></td></tr></table><br>
<a name="2027352"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Shinkiro1</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#84">[#84]</a></td></tr></table></td></tr><tr ><td class="posttext"> What about setting the update rate in BMax with Graphics (...., hertz)? <br><br></td></tr></table><br>
<a name="2027354"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ErikT</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#85">[#85]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've been running my own stuff and yeah; Graphics 800,600,32,60 gives me the correct speed/update rate. <br><br></td></tr></table><br>
<a name="2027383"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#86">[#86]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hmmm, that command (Graphics 800,600,32,60) will make the game run in full-screen mode... and I'm pretty sure if we do this Graphics 800,600,0,60 BlitzMax will ignore the 60hz. <br><br></td></tr></table><br>
<a name="2027387"></a>

<a name="2027388"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ErikT</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#87">[#87]</a></td></tr></table></td></tr><tr ><td class="posttext"> It works fine now. I'm unable to reproduce the issue. Windowed mode, fullscreen mode, with or without setting 60hz in Graphics, it all works. Seems like maybe my system has been acting up and then just stopped for some reason. So yeah, false alarm I guess. Sorry :X <br><br></td></tr></table><br>
<a name="2027602"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Playniax</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#88">[#88]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>The BMAX target for Monkey is great! :)<br><br>However, I was testing your BMAX target And I found a problem with LoadString.<br><br>Problem is that all 'official' targets return a NULL object when the file was not found and I use this as a trick sometimes to stop loading when LoadString returns NULL.<br><br>But the BMAX target stops and returns error 'Error reading from stream'<br><br>Of course I can find some work arounds For this but I can imagine more programmers use this trick so I was hoping you can have BMAX to behave in exacly the same way as the other targets.<br><br>I tested my framework and this is the only part that causes a problem translating to the BMAX target.<br><br>But all in all a good job on your part :) I am really impressed with how good this works!<br><br>Thanks!<br><br>Btw, does Mark gave any plans to make this official? <br><br></td></tr></table><br>
<a name="2027610"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#89">[#89]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> But the BMAX target stops and returns error 'Error reading from stream' <br></div><br><br>Fixed, once I learnt how BlitzMax's Try/Catch works (it would have been nice to have a list of the exceptions that can be caught!!!) :)<br><br><div class="quote"> but I can imagine more programmers use this trick so I was hoping you can have BMAX to behave in exacly the same way as the other targets. <br></div><br><br>Well thats the plan, to get MonkeyMax as close as possible to the other targets... currently the only blocking is the shearing stuff. <br><br></td></tr></table><br>
<a name="2027619"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Playniax</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#90">[#90]</a></td></tr></table></td></tr><tr ><td class="posttext"> Great! When is the new version up? I don't have SVN installed :( <br><br></td></tr></table><br>
<a name="2027623"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#91">[#91]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just apply the change to mojo.bmax.bmx:<br><br><a href="http://code.google.com/p/diddy/source/detail?r=395" target="_blank">http://code.google.com/p/diddy/source/detail?r=395</a><br><br><pre class=code>	Method LoadString:String( path:String )
		path = "data/" + path
		Try
			Return LoadText( path )
		Catch ReadFail:Object
			Return Null
		EndTry
	EndMethod
</pre><br><br>You dont need to recompile trans for changes to mojo. <br><br></td></tr></table><br>
<a name="2027625"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Playniax</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#92">[#92]</a></td></tr></table></td></tr><tr ><td class="posttext"> Tested it and works, thanks! :) <br><br></td></tr></table><br>
<a name="2028031"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >visionastral</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#93">[#93]</a></td></tr></table></td></tr><tr ><td class="posttext"> - I finally found the good thread... -<br><br>Hello therevills, monkeymax is really an excellent work!<br>I just installed and tested it, and I must say I didn't expected it to work so flawlessly!<br>I compiled a 2.5D engine I made for xna and it ran at full speed without any show stopper. Only some 2D glitches are present related to the blend mode in BlitzMax, wich is MASK instead of ALPHA by default, and thus gives a different output for some graphics.<br>It's a shame the code becomes so much difficult to read, but I suppose it can't be better!<br>The only things that had prevented me from compiling are:<br><br>- open gl<br>- SaveString (wich I use a lot for writing to disk in proprietary format)<br><br>I thought BMX has native support for OpenGL (I'm a beginner in bmx so I can be wrong) I wonder if you plan to support it? It would be nice to make MonkeyMax compatible with miniB3D or any other OpenGL based module.<br>The SaveString thing, however, should be easy to fix, doesn't it?<br><br>Anyway, a really powerful work there Therevills, congratulations! <br><br></td></tr></table><br>
<a name="2028075"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#94">[#94]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey visionastral, glad you like it :)<br><br>This project has been very much a collaboration of the minds, yes I did start this and got the basics working, but Karja and Samah has helped alot and even Mark Sibly helped heaps with the interfaces :)<br><br><div class="quote"> Only some 2D glitches are present related to the blend mode in BlitzMax, wich is MASK instead of ALPHA by default <br></div><br>Could you give us an example?<br><br><div class="quote"> It's a shame the code becomes so much difficult to read <br></div><br>Yeah, but I've got use to it now. LOL! Most of the BlitzMax code at the top of the file is really just Mojo stuff, your Monkey code will be near the bottom of the file.<br><br><div class="quote"> I thought BMX has native support for OpenGL  <br></div><br>Yes it does... I might look into it in the future.<br><br><div class="quote"> The SaveString thing, however, should be easy to fix <br></div><br>Yeah, it should be pretty easy to add, same with LoadState and SaveState. <br><br></td></tr></table><br>
<a name="2028166"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matt</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#95">[#95]</a></td></tr></table></td></tr><tr ><td class="posttext"> Awesome work guys! <br><br></td></tr></table><br>
<a name="2028253"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Neuro</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#96">[#96]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok...so i finally got this to work. And it sure as hell works awesome :)!! <br><br></td></tr></table><br>
<a name="2028255"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#97">[#97]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Ok...so i finally got this to work <br></div><br>Cool! How did you fix your linking error? <br><br></td></tr></table><br>
<a name="2028256"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Raz</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#98">[#98]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've got nothing too worthwhile to add to this apart from the fact I really appreciate seeing so many updates to MonkeyMax :) <br><br></td></tr></table><br>
<a name="2028272"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Neuro</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#99">[#99]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Cool! How did you fix your linking error?  <br></div><br>Administrator issues with the D: drive. I just reinstalled Max back onto the C: drive, updated the links, and it just smoothly "worked" :). <br><br>But is it me or does running the target on Max seems to run faster than GFLW?? Although executable file size is a lot bigger though :). <br><br></td></tr></table><br>
<a name="2028320"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Neuro</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#100">[#100]</a></td></tr></table></td></tr><tr ><td class="posttext"> Also, is it possible yet to use external BMax functions in monkey too? Like the Hidemouse function? <br><br></td></tr></table><br>
<a name="2028322"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#101">[#101]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Like the Hidemouse function?  <br></div><br>We added HideMouse ages ago in Diddy and we have added it to Diddy for MonkeyMax too.<br><br><a href="http://code.google.com/p/diddy/source/browse/trunk/monkeymax/modules/diddy/native/diddy.bmax.bmx" target="_blank">http://code.google.com/p/diddy/source/browse/trunk/monkeymax/modules/diddy/native/diddy.bmax.bmx</a> <br><br></td></tr></table><br>
<a name="2028359"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Neuro</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#102">[#102]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ahh thanks, exactly what i was looking for! <br><br></td></tr></table><br>
<a name="2029339"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#103">[#103]</a></td></tr></table></td></tr><tr ><td class="posttext"> Theres a thingy called titaniumGL it works with any OpenGL game and intercepts opengl calls and uses DirectX because its faster,<br><br>So, pure opengl commands could be used for monkeymax and just let people install titaniumgl DLL into their game folder if they want it to be as fast as directx<br><br><a href="http://titaniumgl.tk/" target="_blank">http://titaniumgl.tk/</a> <br><br></td></tr></table><br>
<a name="2029890"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#104">[#104]</a></td></tr></table></td></tr><tr ><td class="posttext"> ok I converted the blitzmax mojo to pure opengl<br>so now its 100% compatible with monkey<br><br>here is BlitzMax\mod\keef.mod\gldraw.mod\gldraw.bmx<br><br>you just compile this in blitzmax with Ctrl-D<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict
Module keef.gldraw
'Import bah.libxml
Import brl.pngloader
Import brl.tgaloader
Import brl.bmploader
Import brl.jpgloader
Import Pub.OpenGL
Import brl.GLGraphics
Import pub.glew

Type TImage
Field num,height#,width#
Field path$
Field vertex#[9]
EndType



Type gldraw_stats 
Field state_blend
Field state_boundtex
Field state_texenabled
Field rotation
Field scalex:Float
Field scaley:Float
Field colorr
Field colorg
Field colorb
Field maskcolorr
Field maskcolorg
Field maskcolorb
Field globalalpha#
Field glzoom#
EndType



Global gld:gldraw_stats=New gldraw_stats

'gld.state_blend
'gld.state_boundtex
'gld.state_texenabled
'gld.imagefont
gld.rotation=0
gld.scalex=1.0
gld.scaley=1.0
gld.colorr=255
gld.colorg=255
gld.colorb=255
gld.maskcolorr=0
gld.maskcolorg=0
gld.maskcolorb=0
gld.globalalpha#=1
'gld.imagefont:Timagefont
'gld.glzoom#

Const MASKBLEND=1
Const SOLIDBLEND=2
Const ALPHABLEND=3
Const LIGHTBLEND=4
Const SHADEBLEND=5

Function BindTex( name )
	If name=gld.state_boundtex Return
	glBindTexture GL_TEXTURE_2D,name
	gld.state_boundtex=name
End Function

Function EnableTex( name )
	BindTex name
	If gld.state_texenabled Return
	glEnable GL_TEXTURE_2D
	gld.state_texenabled=True
End Function

Function DisableTex()
	If Not gld.state_texenabled Return
	glDisable GL_TEXTURE_2D
	gld.state_texenabled=False
End Function

Function drawimage2:Int(img:TImage,x:Float,y:Float, srcx:Int, srcy:Int, srcw:Int, srch:Int )
EnableTex img.num
gltranslatef(x,y,0)


Local x_pos#=srcx/img.width
'DebugLog x_pos+" xpos"

Local y_pos#=srcy/img.height
'DebugLog y_pos+" ypos"

Local width#=srcw/img.width
'DebugLog width+" width"
Local height#=srch/img.height
'DebugLog height+" height"

glBegin(GL_QUADS)
glTexCoord2f(x_pos,y_pos)
glVertex2f(0,0 )
glTexCoord2f(x_pos+width,y_pos)
 glVertex2f(srcw, 0)
glTexCoord2f(x_pos+width,y_pos+height)
glVertex2f(srcw, srch)
glTexCoord2f(x_pos,y_pos+height)
glVertex2f(0, srch)
glend
gltranslatef(-x,-y,0)
End Function 

Function DrawImage(img:TImage,x,y,frame=0)
EnableTex img.num
gltranslatef(x,y,0)

glBegin(GL_QUADS)
glTexCoord2f(0,0)
glVertex2f(img.vertex[0],img.vertex[1] )
glTexCoord2f(1,0)
 glVertex2f(img.vertex[2], img.vertex[3])
glTexCoord2f(1,1)
 glVertex2f(img.vertex[4], img.vertex[5])
glTexCoord2f(0,1)
 glVertex2f(img.vertex[6], img.vertex[7])
glend
gltranslatef(-x,-y,0)
EndFunction

Function DrawPoly(v#[])
glBegin(GL_POLYGON)
Local length=v.length
For Local iter:Int=0 To length-2 Step 2
glVertex2f(v[iter],v[iter+1])
Next
glEnd()

End Function

	Function  GrabPixmap:TPixmap( x,y,w,h )
		Local blend= gld.state_blend
		SetBlend SOLIDBLEND
		Local p:TPixmap=CreatePixmap( w,h,PF_RGBA8888 )
		glReadPixels x,GraphicsHeight()-h-y,w,h,GL_RGBA,GL_UNSIGNED_BYTE,p.pixels
		p=YFlipPixmap( p )
		SetBlend blend
		Return p
	End Function 

Function DrawOval(x,y,w#,h#)

EndFunction

Function DrawLine(x1,y1,x2,y2) 
DisableTex
glLineWidth(1) 
glBegin(GL_LINES)
glVertex2f(x1, y1)
glVertex2f(x2, y2)
glEnd()
EndFunction

Function DrawRect( posx#,posy#,width#,height#)
DisableTex

glBegin(GL_QUADS)

		glVertex2f posx,posy
		glVertex2f posx+width,posy
		glVertex2f posx+width,posy+height
		glVertex2f posx,posy+height
glend	
End Function

Function GrabImage:TImage(x,y,width,height)
Local i:TImage=CreateImage(width,height)
glBindTexture GL_TEXTURE_2D,i.num
glCopyTexSubImage2D GL_TEXTURE_2D,0,0,0,x,GraphicsHeight()-i.height-y,width,height
Return i
End Function


Function CreateImage:TImage(width,height)
		
Local i:TImage=New TImage
i.width=Width
i.height=Height
i.vertex[0]=0
i.vertex[1]=0
i.vertex[2]=i.width
i.vertex[3]=0
i.vertex[4]=i.width
i.vertex[5]=i.height
i.vertex[6]=0
i.vertex[7]=i.height
	
	glGenTextures 1,Varptr i.num
	BindTex i.num
	
	'set texture parameters
glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_MIN_FILTER,GL_LINEAR
	glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_MAG_FILTER,GL_LINEAR
	

	Local mip_level
	glTexImage2D GL_TEXTURE_2D,mip_level,GL_RGBA8,width,height,0,GL_RGBA,GL_UNSIGNED_BYTE,Null
	Return i
EndFunction


Function ImageWidth(i:TImage)
'Local width=glGetTexParameteriv(i.num,,GL_TEXTURE_WIDTH)
'DebugLog "get gl texture width"+width
Return i.width

EndFunction

Function ImageHeight(i:TImage)
Return i.height
EndFunction

Function LoadImage:TImage(url:String)
If FileType(url)=1
Local pix:TPixmap=LoadPixmap(url)
If pix=Null
RuntimeError "pixmap is null: "+url
EndIf
Local tex=GLTexFromPixmap(pix)
Local i:TImage=New TImage
i.num=tex
i.path=url
i.width=PixmapWidth(pix)
i.height=PixmapHeight(pix)



i.vertex[0]=0
i.vertex[1]=0

i.vertex[2]=i.width
i.vertex[3]=0

i.vertex[4]=i.width
i.vertex[5]=i.height

i.vertex[6]=0
i.vertex[7]=i.height

Return i
Else
RuntimeError url+" image not found"
EndIf
EndFunction




	
	Function SetAlpha( alpha# )
	Local red#=gld.colorr/255.0
		Local green#=gld.colorg/255.0
		Local blue#=gld.colorb/255.0
			gld.globalalpha=alpha
		glColor4f(red,green,blue,alpha)
	End Function

	Function SetLineWidth( width# )
		glLineWidth width
	End Function
	

	
	Function SetColor( red#,green#,blue#)
		gld.colorr=red
		gld.colorg=green
		gld.colorb=blue
		red=red/255
		green=green/255
		blue=blue/255
		'alpha=alpha/255

		glColor4f(red,green,blue,gld.globalalpha#)
	End Function

	Function SetClsColor( red,green,blue )
		red=Min(Max(red,0),255)
		green=Min(Max(green,0),255)
		blue=Min(Max(blue,0),255)
		glClearColor red/255.0,green/255.0,blue/255.0,1.0
	End Function
	

Function SetMaskColor(r,g,b)
gld.maskcolorr=r
gld.maskcolorg=g
gld.maskcolorb=b
EndFunction

Function SetBlend( blend )
		If blend=gld.state_blend Return
		gld.state_blend=blend
		Select blend
		Case MASKBLEND
			glDisable GL_BLEND
			glEnable GL_ALPHA_TEST
			glAlphaFunc GL_GEQUAL,.5
		Case SOLIDBLEND
			glDisable GL_BLEND
			glDisable GL_ALPHA_TEST
		Case ALPHABLEND
			glEnable GL_BLEND
			glBlendFunc GL_SRC_ALPHA,GL_ONE_MINUS_SRC_ALPHA
			glDisable GL_ALPHA_TEST
		Case LIGHTBLEND
			glEnable GL_BLEND
			glBlendFunc GL_SRC_ALPHA,GL_ONE
			glDisable GL_ALPHA_TEST
		Case SHADEBLEND
			glEnable GL_BLEND
			glBlendFunc GL_DST_COLOR,GL_ZERO
			glDisable GL_ALPHA_TEST
		Default
			glDisable GL_BLEND
			glDisable GL_ALPHA_TEST
		End Select
EndFunction



Function Plot(x1,y1)
DisableTex
glBegin(GL_POINTS)
glVertex2f(x1+0.5, y1+0.5)
glEnd()
EndFunction
 




Function SetViewport(x,y,width,height)
glviewport(x,y,width,height)
EndFunction


Function initgl(xsize,ysize,screendepth,fps,virtualx=0,virtualy=0)
If virtualx=0 And virtualy=0
virtualx=xsize
virtualy=ysize
EndIf
GLGraphics(xsize,ysize,ScreenDepth,fps,GRAPHICS_BACKBUFFER|GRAPHICS_DEPTHBUFFER) '| BGL_FULLSCREEN

glMatrixMode (GL_PROJECTION)
glLoadIdentity ()
glClearColor(0, 0, 0, 0)
glOrtho (0, virtualx,virtualy , 0, 0, 1)
glDisable(GL_DEPTH_TEST)
glMatrixMode (GL_MODELVIEW)
glLoadIdentity()
glTranslatef(0.375, 0.375, 0)
glEnable(GL_BLEND)
glEnable(GL_TEXTURE_2D)
';glenable(GL_SCISSOR_TEST)
';glscissor(0,ysize,xsize,ysize)
glblendfunc(GL_SRC_ALPHA,GL_ONE_MINUS_SRC_ALPHA)
SetColor 255,255,255
SetAlpha 1
SetBlend alphablend
EndFunction
	

Function Cls()
glClear(GL_COLOR_BUFFER_BIT)
EndFunction


Function setimagevertices(i:TImage,x0#,y0#,x1#,y1#,x2#,y2#,x3#,y3#)
i.vertex=[x0,y0,x1,y1,x2,y2,x3,y3]
EndFunction</textarea><br><br>here is mojo.bmax.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' ***** Start mojo.bmax.bmx ******

Global app:gxtkApp

Type gxtkApp
	Field ginput:gxtkInput
	Field gaudio:gxtkAudio
	Field ggraphics:gxtkGraphics

	Field dead:Int=0
	Field suspended:Int=0
	Field vloading:Int=0
	Field maxloading:Int=0
	Field updateRate:Int=0
	Field nextUpdate:Float=0
	Field updatePeriod:Float=0
	Field startMillis:Float=0
	
	Method New()' gxtkApp()
		app=Self
		ggraphics=New gxtkGraphics
	EndMethod
	
	Method Setup()
		ginput=New gxtkInput
		gaudio=New gxtkAudio

		bb_input__1Set_1Input_1Device(ginput)
		bb_audio__1Set_1Audio_1Device(gaudio)
		
		startMillis=BlitzMaxMillisecs()
		
		SetFrameRate( 0 )
		
		InvokeOnCreate()
		InvokeOnRender()
		Update()
	EndMethod
	
	Method Update()
		While Not AppTerminate()
			If Not updatePeriod Return
			Local updates:Int = 0
			
			Repeat
				nextUpdate:+updatePeriod
				InvokeOnUpdate()
				If Not updatePeriod Then Exit
				If nextUpdate&gt;BlitzMaxMillisecs() Then Exit
				updates:+1
				If updates = 7 Then
					nextUpdate = BlitzMaxMillisecs()
					Exit
				EndIf
			Forever
			InvokeOnRender()
			Local del:Int = nextUpdate - BlitzMaxMillisecs()
			If del &lt; 1 Then del = 1
			Delay(del)
		Wend
	EndMethod
	
	Method InvokeOnCreate()
		If dead Return
		dead = 1
		OnCreate()
		dead = 0
	EndMethod

	Method InvokeOnUpdate()
		If dead Or suspended Or Not updateRate Or vloading Return
		OnUpdate()
	EndMethod
	
	Method InvokeOnRender()
		If dead Or suspended Return
		ggraphics.BeginRender()
		If vloading
			OnLoading()
		Else
			OnRender()
		EndIf
		ggraphics.EndRender()
	EndMethod
	
	
	Method SetFrameRate( fps:Int )
		If fps
			updatePeriod=1000.0/fps
			nextUpdate=BlitzMaxMillisecs() +updatePeriod
		Else
			updatePeriod=0
		EndIf
	EndMethod

	Method LoadState:String()
'		var file:SharedObject=SharedObject.getLocal( "gxtkapp" );
'		var state:String=file.data.state;
'		file.close();
'		if( state ) return state;
		Return ""
	EndMethod
	
	Method SaveState:Int( state:String )
'		var file:SharedObject=SharedObject.getLocal( "gxtkapp" );
'		file.data.state=state;
'		file.close();
		Return 0
	EndMethod
	
	Method LoadString:String( path:String )
		path = "data/" + path
		Return LoadText( path )
	EndMethod
	
	' ***** GXTK API *****
	
	Method GraphicsDevice:gxtkGraphics()
		Return ggraphics
	EndMethod

	Method InputDevice:gxtkInput()
		Return ginput
	EndMethod

	Method AudioDevice:gxtkAudio()
		Return gaudio
	EndMethod

	Method SetUpdateRate:Int(hertz:Int)
		updateRate = hertz
		If Not vloading Then SetFrameRate(updateRate)
		Return 0
	EndMethod
	
	Method MilliSecs:Int()
		Return BlitzMaxMillisecs() - startMillis
	EndMethod
	
	Method Loading:Int()
		Return vloading
	EndMethod

	Method OnCreate:Int()
		Return 0
	EndMethod

	Method OnUpdate:Int()
		Return 0
	EndMethod
	
	Method OnSuspend:Int()
		Return 0
	EndMethod
	
	Method OnResume:Int()
		Return 0
	EndMethod
	
	Method OnRender:Int()
		Return 0
	EndMethod
	
	Method OnLoading:Int()
		Return 0
	EndMethod
EndType

Type gxtkGraphics
	Field gmode:Int = 1
	Field ix:Float=1,iy:Float,jx:Float,jy:Float=1,tx:Float,ty:Float
	Field sx:Float=1,sy:Float=1,rot:Float=0
	
	Method Mode:Int()
		Return gmode
	EndMethod
	
	Method Cls:Int(r:Int = 0, g:Int = 0, b:Int = 0)
		BlitzMaxCls(r, g, b)
		Return 0
	EndMethod
	
	Method DrawPoint:Int(x:Float, y:Float)
		Plot x, y
	EndMethod
	
	Method LoadSurface:gxtkSurface(path:String)
		Local image:TImage = LoadImage("data/"+path)
		If image Then
			Local gs:gxtkSurface = New gxtkSurface
			gs.setImage(image)
			Return gs
		EndIf
		Return Null
	EndMethod
	
	Method BeginRender()
	EndMethod
	
	Method EndRender()
		Flip
	EndMethod
	
	Method SetColor:Int(r:Int, g:Int, b:Int)
		BlitzMaxSetColor(r, g, b)
		Return 0
	EndMethod
	
	Method SetAlpha:Int(a:Float)
		BlitzMaxSetAlpha(a)
		Return 0
	EndMethod
	
	Method SetBlend:Int(blend:Int)
		BlitzMaxSetBlend(blend)
		Return 0
	EndMethod
	
	Method Width:Int()
		Return GraphicsWidth()
	EndMethod
	
	Method Height:Int()
		Return GraphicsHeight()
	EndMethod
	
	Method SetScissor:Int(x:Int, y:Int, w:Int, h:Int)
		SetViewport(x, y, w, h) ' NOT TESTED!
		Return 0
	EndMethod

	Method SetMatrix:Int(scalex:Float,shearx:Float,sheary:Float,scaley:Float,tx:Float,ty:Float)
	Local m#[16]
	
	m[0]=scalex
	m[5]=scaley
	m[10]=1
	m[15]=1
	
	m[12]=tx
	m[13]=ty
	
	m[1]=shearx
	m[4]=sheary
	
	glLoadMatrixf(m)
		Return 0
	EndMethod
	
	Method DrawSurface:Int(surface:gxtkSurface,x:Float,y:Float)
		DrawImage(surface.image, x, y, 0)
		Return 0
	EndMethod
	
	Method DrawSurface2:Int(surface:gxtkSurface,x:Float,y:Float, srcx:Int, srcy:Int, srcw:Int, srch:Int )
		DrawImage2(surface.image, x, y,srcx, srcy, srcw, srch)		
		Return 0
	EndMethod
	
	Method DrawLine:Int( x1:Float,y1:Float,x2:Float,y2:Float )
		' Need to reset transform so that BlitzMax doesn't try to apply rotation
		BlitzMaxDrawLine(x1, y1, x2, y2)
		Return 0
	EndMethod
	
	Method DrawOval:Int( x:Float, y:Float, w:Float, h:Float )
		BlitzMaxDrawOval(x, y, w, h)
		Return 0
	EndMethod
	
	Method DrawPoly:Int(vertices:Float[])
		' setting the origin to use the current rotation translation
		BlitzMaxDrawPoly( vertices )

		Return 0
	EndMethod
	
	Method DrawRect:Int( x:Float, y:Float, w:Float, h:Float )
		BlitzMaxDrawRect(x, y, w, h)
		Return 0
	EndMethod
EndType

Type gxtkInput
	Method MouseX:Float()
		Return BRL.PolledInput.MouseX()
	EndMethod

	Method MouseY:Float()
		Return BRL.PolledInput.MouseY()
	EndMethod
	
	Method KeyDown:Int( key:Int )
		If( key = 384 ) key = 1 ' change TOUCH0 to MOUSE_LMB

		If( key &gt;= 1 And key &lt;= 3 )
			Return BRL.PolledInput.MouseDown( key )
		EndIf
		If key &lt; 256
			Return BRL.PolledInput.KeyDown( key )
		Else
			Return 0
		EndIf
	EndMethod

	Method KeyHit:Int( key:Int )
		If( key = 384 ) key = 1 ' change TOUCH0 to MOUSE_LMB
		If( key &gt;= 1 And key &lt;= 3 )
			Return BRL.PolledInput.MouseHit( key )
		EndIf
		If key &lt; 256
			Return BRL.PolledInput.KeyHit( key )
		Else
			Return 0
		EndIf
	EndMethod

	Method GetChar:Int()
		Return BRL.PolledInput.GetChar()
	EndMethod

	Method JoyX:Int( index:Int )
		Return Pub.FreeJoy.JoyX( index )
	EndMethod

	Method JoyY:Int( index:Int )
		Return Pub.FreeJoy.JoyY( index )
	EndMethod

	Method JoyZ:Int( index:Int )
		Return Pub.FreeJoy.JoyZ( index )
	EndMethod

	Method TouchX:Float( index:Int )
		Return BRL.PolledInput.MouseX()
	EndMethod

	Method TouchY:Float( index:Int )
		Return BRL.PolledInput.MouseY()
	EndMethod
	
	Method AccelX:Int()
		Return 0
	EndMethod
	
	Method AccelY:Int()
		Return 0
	EndMethod
	
	Method AccelZ:Int()
		Return 0
	EndMethod
	
	Method SetKeyboardEnabled:Int( enabled:Int )
		Return 0
	EndMethod
EndType

Type gxtkChannel
	Field channel:TChannel	' null then not playing
	Field sample:gxtkSample
	Field loops:Int
	'Field transform:SoundTransform=new SoundTransform()
	'Field pausepos:Number
	Field state:Int
	
	Method New()
		channel = New TChannel
	EndMethod
EndType

Type gxtkAudio
	Field amusicState:Int = 0
	Field channels:gxtkChannel[] = New gxtkChannel[33]
	
	Method New()
		For Local i:Int = 0 To 33 - 1
			channels[i] = New gxtkChannel
		Next
	EndMethod
	
	Method MusicState:Int()
'		If( musicState = 1 And music.isPlaying() = False )
'			musicState = 0;
'		Endif
		Return amusicState
	EndMethod
	
	Method PlayMusic:Int( path:String, flags:Int )
		Return 0
	EndMethod
	
	Method ChannelState:Int( channel:Int )
		Return -1
	EndMethod
	
	Method StopMusic()
	EndMethod
	
	Method StopChannel( channel:Int )
	EndMethod

	Method PlaySample( sound:gxtkSample, channel:Int, flags:Int )
		Local chan:gxtkChannel = channels[channel]
		
		'If chan.state &lt;&gt; 0 Then chan.channel.Stop() &lt;-- this crashes after the first play!?
		
		chan.sample = sound
		'chan.loops = flags ? 0x7fffffff : 0;
		'chan.channel = sample.sound.play( 0,chan.loops,chan.transform );
		'chan.channel = sound
		chan.state=1
	
		PlaySound( sound.sound, chan.channel )
	EndMethod

	Method SetPan( channel:Int, pan:Float )
		Local chan:gxtkChannel = channels[channel]
		chan.channel.SetPan(pan)
	EndMethod

	Method SetRate( channel:Int, rate:Float )
		Local chan:gxtkChannel = channels[channel]
		chan.channel.SetRate(rate)
	EndMethod

	Method PauseMusic:Int()
		Return 0
	EndMethod

	Method ResumeMusic:Int()
		Return 0
	EndMethod

	Method SetMusicVolume:Int( volume:Float )
		Return 0
	EndMethod

	Method SetVolume:Int( channel:Int, volume:Float )
		Local chan:gxtkChannel = channels[channel]
		chan.channel.SetVolume(volume)
		Return 0
	EndMethod
	
	Method LoadSample:gxtkSample( path:String )
		Local extension:String = ExtractExt( path)
		If extension = "ogg" Or extension = "wav" Then
			Local sound:TSound = LoadSound("data/"+path)
			If sound Then
				Local gs:gxtkSample = New gxtkSample
				gs.setSound(sound)
				Return gs
			EndIf
		Else
			RuntimeError "BlitzMax can only use ogg and wav file formats"
		EndIf
		Return Null
	EndMethod
EndType

Type gxtkSample
	Field sound:TSound
	
	Method setSound(sound:TSound)
		Self.sound = sound
	EndMethod
	
	Method Discard()
		Self.sound = Null
	EndMethod
EndType

Function BlitzMaxDrawPoly:Int(vertices:Float[])
	DrawPoly vertices
	Return 0
EndFunction

Function BlitzMaxDrawRect:Int( x:Float, y:Float, w:Float, h:Float )
	DrawRect x, y, w, h
	Return 0
EndFunction

Function BlitzMaxDrawOval:Int( x:Float, y:Float, w:Float, h:Float )
	DrawOval x, y, w, h
	Return 0
EndFunction

Function BlitzMaxDrawLine:Int( x1:Float,y1:Float,x2:Float,y2:Float )
	DrawLine x1, y1, x2, y2
	Return 0
EndFunction

Function BlitzMaxMillisecs:Int()
	Return MilliSecs()
EndFunction

Function BlitzMaxSetColor(r:Int, g:Int, b:Int)
	SetColor(r, g, b)
EndFunction

Function BlitzMaxSetBlend(blend:Int)
	Select blend
		Case 0
			SetBlend( ALPHABLEND )
		Case 1
			SetBlend( LIGHTBLEND )
	End Select
EndFunction

Function BlitzMaxSetAlpha(a:Float)
	SetAlpha(a)
EndFunction

Function BlitzMaxCls(r:Int = 0, g:Int = 0, b:Int = 0)
SetClsColor( r,g,b )
	
	Cls
EndFunction

Type gxtkSurface
	Field w:Int, h:Int
	Field image:TImage
	
	Method setImage(image:TImage)
		Self.image = image
	EndMethod
	
	Method Width:Int()
		Return image.Width
	EndMethod
	
	Method Height:Int()
		Return image.Height
	EndMethod
	
	Method Discard()
	EndMethod
EndType

' ***** End mojo.bmax.bmx ******

</textarea><br><br>here is Monkey/targets/bmax/monkeygame.bmx<br><br>[bbcode]<br>SuperStrict<br>Framework keef.gldraw<br>Import brl.linkedlist<br>Import brl.audio<br>import brl.standardio<br>Import brl.textstream<br>Import Pub.FreeJoy<br>Import BRL.PolledInput<br>Global _errStack:TList = New TList;<br>Global _errInfo:String="";<br><br>initgl(640,480,0,30)<br><br>bbInit();<br>bbMain();[/bbcode]<br><br><br>The only command missing is DrawOval cos I dont know how to do that in OpenGL<br><br><br>Theres only 3 things left now,<br>DrawOval<br><br>Drawimage with extra arguments (as used in horsey demo)<br>This can easily be done with gltranslate,glscale,glrotate<br><br><br>Some kind of module system where you can compile modules seperately so compilation doesnt take as long<br>Like not including blitzmax imports into the MonkeyGame.bmx and being able to compile them seperately.<br><br>Moving monkey boilerplate code like mojo to a blitzmax module would also help. <br><br></td></tr></table><br>
<a name="2029889"></a>

<a name="2029888"></a>

<a name="2029576"></a>

<a name="2029508"></a>

<a name="2029489"></a>

<a name="2029488"></a>

<a name="2029427"></a>

<a name="2029426"></a>

<a name="2029414"></a>

<a name="2029413"></a>

<a name="2029853"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Samah</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#105">[#105]</a></td></tr></table></td></tr><tr ><td class="posttext"> Unfortunately that makes the MonkeyMax target OpenGL dependent.  We were sorta hoping to allow DirectX support for Max2D, so unless you want to do that too... ;) <br><br></td></tr></table><br>
<a name="2029858"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#106">[#106]</a></td></tr></table></td></tr><tr ><td class="posttext"> <a href="http://titaniumgl.tk/" target="_blank">http://titaniumgl.tk/</a> &lt;- this makes your PC use DirectX even for OpenGL games <br><br></td></tr></table><br>
<a name="2029859"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#107">[#107]</a></td></tr></table></td></tr><tr ><td class="posttext"> Malwarebytes blocks that link Slenkar... <br><br></td></tr></table><br>
<a name="2029860"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#108">[#108]</a></td></tr></table></td></tr><tr ><td class="posttext"> I tried it on marks bouncy alien demo and it said 'unable to calculate tex size' and didnt work :( <br><br></td></tr></table><br>
<a name="2030637"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ErikT</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#109">[#109]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hullo.<br><br>Converting this:<br><pre class=code>Field map:tlQuadTreeNode[][]</pre><br>produces an error in the translated code:<br><pre class=code>Field f_map:bb_rigzcollision_tl_1Quad_1Tree_1Node[][]=New bb_rigzcollision_tl_1Quad_1Tree_1Node[[0],[0]];</pre><br>(Unable to convert from 'Int Array' to 'Int')<br><br>... which goes away by commenting out: <pre class=code>=New bb_rigzcollision_tl_1Quad_1Tree_1Node[[0],[0]];</pre><br><br>Here's the whole class:<br><pre class=code>
Class tlQuadTree

	Field maxlevels:Int
	Field Box:tlBox
	Field rootnode:tlQuadTreeNode
	Field maxpernode:Int
	Field areacheckcount:Int
	
	Field objectsfound:Int
	Field objectsupdated:Int
	Field totalobjectsintree:Int
	
	Field min_nodewidth:Float
	Field min_nodeheight:Float
	
'	Field map:tlQuadTreeNode[,]
	Field map:tlQuadTreeNode[][]
	
	Field dimension:Int

	#rem
		bbdoc: Create a New tlQuadTree
		returns: A New quadtree
		about: Creates a New quad tree with the coordinates And dimensions given. _maxlevels determines how many times the quadtree can be sub divided. A
		quadtreenode is only subdivided when a certain amount of objects have been added, which is set by passing _maxpernode. There's no optimum values for
		these, it largely depends on your specific needs, so you will probably do well To experiment.
	#End
	Method Create:tlQuadTree(x:Float, y:Float, w:Float, h:Float, _maxlevels:Int = 4, _maxpernode:Int = 4)
		Box = New tlBox.Create(x, y, w, h)
		maxlevels = _maxlevels
		maxpernode = _maxpernode
		rootnode = New tlQuadTreeNode.Create(x, y, w, h, Self)
		dimension = Pow(2,(maxlevels-1))'2 ^ (maxlevels - 1)
		min_nodewidth = w / dimension
		min_nodeheight = h / dimension
'		map = New tlQuadTreeNode[dimension, dimension]
		map = New tlQuadTreeNode[dimension][dimension]
		For Local x:Int = 0 To dimension - 1
			For Local y:Int = 0 To dimension - 1
'				map[x, y] = rootnode
				map[x][y] = rootnode
			Next
		Next
		rootnode.dimension = dimension
		Return Self
	End Method
	
	#rem
		bbdoc: Add a New bounding box To the Quadtree
		returns: False If the box doesn't overlap the qaudtree, otherwise True.
		about: A quadtree isn't much use without any objects. Use this to add a #tlBox to the quadtree. If the bounding box does not overlap the 
		quadtree Then Null is returned.
	#End
	Method AddBox:Int(r:tlBox)
		If Box.BoundingBoxOverlap(r)
			r.quadtree = Self
			rootnode.AddBox(r)
			totalobjectsintree+=1
			Return True
		Else
			Return False
		End If
	End Method

	#rem
		bbdoc: Query the Quadtree To find objects with an area
		about: When you want To find objects within a particular area of the quadtree you can use this Method.  Pass the area coordinates And dimensions
		that you want to check, an object that can be anything that you want To pass through To the callback Function, And the Function callback that you want 
		To perform whatever tasks you need on the objects that are found within the area.
		The callback Function you create needs To have 2 parameters: ReturnedObject:Object which will be the tlBox/circle/poly, And Data:Object which can be
		and object you want to pass through to the call back Function.
	#End
	
	#rem
		bbdoc: Find out how many objects are currently in the quadtree
		returns: Number of Total Objects in Tree
		about: Use this To retrieve the total amount of objects that are stored in the quadtree.
	#End
	Method GetTotalObjects:Int()
		Return totalobjectsintree
	End Method
	
	#rem
		bbdoc: Perform some house keeping on the quadtree
		about: This will search the quadtree tree For any empty #tlQuadTreeNodes And unpartition them If necessary. 
	#End
	Method RunMaintenance()
		rootnode.UnpartitionEmptyQuads()
	End Method
	
	#rem
		bbdoc: Draw the quadtree
		about: This can be used For debugging purposes. *Warning: This will be very slow If the quadtree has more Then 6 Or 7 levels!*
	#End
	Method Draw(offsetx:Float = 0, offsety:Float = 0)
		rootnode.Draw(offsetx, offsety)
	End Method
	
	'Internal Stuff-----------------------------------
	Method UpdateRect(r:tlBox)
		'This is run automatically when a tlBox decides it needs to be moved within the quadtree
		r.RemoveFromQuadTree()
		objectsupdated+=1
		AddBox(r)
	End Method

	Method GetQuadNode:tlQuadTreeNode(x:Float, y:Float)
		Local tx:Int = x / min_nodewidth
		Local ty:Int = y / min_nodeheight
		If tx &gt;= 0 And tx &lt; dimension And ty &gt;= 0 And ty &lt; dimension
'			Return map[tx, ty]
			Return map[tx][ty]
		End If
		Return Null
	End Method
End


</pre> <br><br></td></tr></table><br>
<a name="2030682"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#110">[#110]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'll have a look tonight, thanks for the code :) <br><br></td></tr></table><br>
<a name="2030699"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#111">[#111]</a></td></tr></table></td></tr><tr ><td class="posttext"> Okay, think I've fixed it:<br><br><a href="http://code.google.com/p/diddy/source/detail?r=409" target="_blank">http://code.google.com/p/diddy/source/detail?r=409</a> <br><br></td></tr></table><br>
<a name="2030706"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ErikT</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#112">[#112]</a></td></tr></table></td></tr><tr ><td class="posttext"> Great, thanks a lot :)<br><br>The new trans_winnt.exe gives me an empty build rollout though? Tried it with 56b and 58.<br><br>EDIT:<br>Nevermind, my firewall blocked it :P The array code compiles and runs without any problems. Good going :) <br><br></td></tr></table><br>
<a name="2030705"></a>

<a name="2030701"></a>

<a name="2030718"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#113">[#113]</a></td></tr></table></td></tr><tr ><td class="posttext"> Glad it worked :)<br><br>This thread is getting pretty long, so I've created a new one here:<br><a href="http://www.monkeycoder.co.nz/Community/posts.php?topic=3018#30717" target="_blank">http://www.monkeycoder.co.nz/Community/posts.php?topic=3018#30717</a><br><br>Mark, Simon or James, could you please lock this thread :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
