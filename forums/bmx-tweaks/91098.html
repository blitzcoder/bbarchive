<!DOCTYPE html><html lang="en" ><head ><title >brl.linkedlist docs</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >brl.linkedlist docs</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=110" >BlitzMax Module Tweaks</a>/<a href="#bottom" >brl.linkedlist docs</a><br><br>
<a name="1036819"></a>

<a name="1221949"></a>

<a name="1222187"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is an update to the brl.linkedlist docs. Most of the examples are based on code by Psyced (Plash).<br><br>There are code examples for Tlink and Tlist methods too but <strike>I've left them out as</strike> that appears to break makedocs... Perhaps code examples for methods is overkill anyway?? <br><br>Edit: I fixed this, see <a href="/posts.php?topic=91195" target="_blank">makedocs tweak</a>.<br><br>The files are zipped in a doc folder (but not the module source due to the BSSC license). I've also added the TLink and TList folders.<br>linkedlist-doc.zip (expired)<br><br>I'm posting the module source (from v 1.41) and the patch.<br><br>Last edit: 16 Aug 2010<br><br>linkedlist.diff<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">--- linkedlist-old.bmx	2010-07-15 03:07:18.145005443 +0100
+++ linkedlist.bmx	2010-08-16 20:10:44.301518425 +0100
@@ -24,7 +24,7 @@
 End Function
 
 Rem
-bbdoc: Link Object used by TList
+bbdoc: Link object used by TList
 End Rem
 Type TLink
 
@@ -32,28 +32,28 @@
 	Field _succ:TLink,_pred:TLink
 	
 	Rem
-	bbdoc: Returns the Object associated with this Link.
+	bbdoc: Return the object associated with this link
 	End Rem
 	Method Value:Object()
 		Return _value
 	End Method
 
 	Rem
-	bbdoc: Returns the next link in the List.
+	bbdoc: Return the next link in the list
 	End Rem
 	Method NextLink:TLink()
 		If _succ._value&lt;&gt;_succ Return _succ
 	End Method
 
 	Rem
-	bbdoc: Returns the previous link in the List.
+	bbdoc: Return the previous link in the list
 	End Rem
 	Method PrevLink:TLink()
 		If _pred._value&lt;&gt;_pred Return _pred
 	End Method
 
 	Rem
-	bbdoc: Removes the link from the List.
+	bbdoc: Remove the link from the list
 	End Rem
 	Method Remove()
 		_value=Null
@@ -64,7 +64,7 @@
 End Type
 
 Rem
-bbdoc: Enumerator Object use by TList in order to implement Eachin support. 
+Enumerator object used by TList in order to implement EachIn support
 End Rem
 Type TListEnum
 
@@ -84,7 +84,7 @@
 End Type
 
 Rem
-bbdoc: Linked List 
+bbdoc: Linked list
 End Rem
 Type TList
 
@@ -110,7 +110,8 @@
 ?
 	Rem
 	bbdoc: Clear a linked list
-	about: Removes all objects from @list.
+	about:
+	Removes all objects from list.
 	End Rem
 	Method Clear()
 		While _head._succ&lt;&gt;_head
@@ -121,7 +122,7 @@
 	Rem
 	bbdoc: Check if list is empty
 	returns: True if list is empty, else false
-	end rem
+	End Rem
 	Method IsEmpty()
 		Return _head._succ=_head
 	End Method
@@ -129,7 +130,7 @@
 	Rem
 	bbdoc: Check if list contains a value
 	returns: True if list contains @value, else false
-	end rem
+	End Rem
 	Method Contains( value:Object )
 		Local link:TLink=_head._succ
 		While link&lt;&gt;_head
@@ -158,8 +159,8 @@
 	End Method
 
 	Rem
-	bbdoc: Returns the first object in the list
-	about: Returns Null if the list is empty.
+	bbdoc: Return the first object in the list
+	returns: Null if the list is empty
 	End Rem
 	Method First:Object()
 		If IsEmpty() Return
@@ -167,8 +168,8 @@
 	End Method
 
 	Rem
-	bbdoc: Returns the last object in the list
-	about: Returns Null if the list is empty.
+	bbdoc: Return the last object in the list
+	returns: Null if the list is empty
 	End Rem
 	Method Last:Object()
 		If IsEmpty() Return
@@ -176,8 +177,8 @@
 	End Method
 
 	Rem
-	bbdoc: Removes and returns the first object in the list.
-	about: Returns Null if the list is empty.
+	bbdoc: Remove and return the first object in the list
+	returns: Null if the list is empty
 	End Rem
 	Method RemoveFirst:Object()
 		If IsEmpty() Return
@@ -187,8 +188,8 @@
 	End Method
 
 	Rem
-	bbdoc: Removes and returns the last object in the list.
-	about: Returns Null if the list is empty.
+	bbdoc: Remove and return the last object in the list
+	returns: Null if the list is empty
 	End Rem
 	Method RemoveLast:Object()
 		If IsEmpty() Return
@@ -198,21 +199,23 @@
 	End Method
 
 	Rem
-	bbdoc: Returns the first link the list or null if the list is empty.
+	bbdoc: Return the first link in the list
+	returns: Null if the list is empty
 	End Rem
 	Method FirstLink:TLink()
 		If _head._succ&lt;&gt;_head Return _head._succ
 	End Method
 
 	Rem
-	bbdoc: Returns the last link the list or null if the list is empty.
+	bbdoc: Return the last link in the list
+	returns: Null if the list is empty
 	End Rem
 	Method LastLink:TLink()
 		If _head._pred&lt;&gt;_head Return _head._pred
 	End Method
 
 	Rem
-	bbdoc: Inserts an object before the specified list link.
+	bbdoc: Insert an object before the specified list link
 	End Rem
 	Method InsertBeforeLink:TLink( value:Object,succ:TLink )
 		Assert value Else "Can't insert Null object into list"
@@ -226,7 +229,7 @@
 	End Method
 
 	Rem
-	bbdoc: Inserts an object after the specified list link.
+	bbdoc: Insert an object after the specified list link
 	End Rem
 	Method InsertAfterLink:TLink( value:Object,pred:TLink )
 		Assert value Else "Can't insert Null object into list"
@@ -240,7 +243,8 @@
 	End Method
 
 	Rem
-	bbdoc: Returns the first link in the list with the given value, or null if none found.
+	bbdoc: Return the first link in the list with the given value
+	returns: Null if @value not found
 	End Rem
 	Method FindLink:TLink( value:Object )
 		Local link:TLink=_head._succ
@@ -251,8 +255,9 @@
 	End Method
 
 	Rem
-	bbdoc: Returns the value of the link at the given index.
-	about: Throws an exception if the index is out of range (must be 0..list.Count()-1 inclusive).
+	bbdoc: Return the value of the link at the given index
+	about:
+	Throws an exception if @index is out of range (must be 0..list.Count()-1 inclusive).
 	End Rem
 	Method ValueAtIndex:Object( index )
 		Assert index&gt;=0 Else "Object index must be positive"
@@ -267,8 +272,8 @@
 
 	Rem
 	bbdoc: Count list length
-	returns: The numbers of objects in @list.
-	end rem
+	returns: The number of objects in the list
+	End Rem
 	Method Count()
 		Local link:TLink=_head._succ,count
 		While link&lt;&gt;_head
@@ -280,7 +285,8 @@
 
 	Rem
 	bbdoc: Remove an object from a linked list
-	about: Remove scans a list for the specified value and removes its link.
+	about:
+	Scans a list for the specified value and removes its link.
 	End Rem
 	Method Remove( value:Object )
 		Local link:TLink=FindLink( value )
@@ -290,7 +296,7 @@
 	End Method
 	
 	Rem
-	bbdoc: Swap contents with the list specified.
+	bbdoc: Swap contents with the list specified
 	End Rem
 	Method Swap( list:TList )
 		Local head:TLink=_head
@@ -299,7 +305,7 @@
 	End Method
 	
 	Rem
-	bbdoc: Creates an identical copy of the list.
+	bbdoc: Create an identical copy of the list
 	End Rem
 	Method Copy:TList()
 		Local list:TList=New TList
@@ -312,7 +318,7 @@
 	End Method
 
 	Rem
-	bbdoc: Reverse the order of the list.
+	bbdoc: Reverse the order of the list
 	End Rem
 	Method Reverse()
 		Local pred:TLink=_head,succ:TLink=pred._succ
@@ -326,7 +332,7 @@
 	End Method
 	
 	Rem
-	bbdoc: Creates a new list that is the reversed version of this list.
+	bbdoc: Create a new list that is the reversed version of this list
 	End Rem
 	Method Reversed:TList()
 		Local list:TList=New TList
@@ -339,8 +345,9 @@
 	End Method
 
 	Rem
-	bbdoc: Sort a list in either ascending (default) or decending order.
-	about: User types should implement a Compare method in order to be sorted.
+	bbdoc: Sort a list in either ascending (default) or decending order
+	about:
+	User types should implement a Compare method in order to be sorted.
 	End Rem
 	Method Sort( ascending=True,compareFunc( o1:Object,o2:Object )=CompareObjects )
 		Local ccsgn=-1
@@ -407,9 +414,9 @@
 	End Method
 
 	Rem
-	bbdoc: convert a list to an array
+	bbdoc: Convert a list to an array
 	returns: An array of objects
-	end rem
+	End Rem
 	Method ToArray:Object[]()
 		Local arr:Object[Count()],i
 		Local link:TLink=_head._succ
@@ -424,7 +431,7 @@
 	Rem
 	bbdoc: Create a list from an array
 	returns: A new linked list
-	end rem
+	End Rem
 	Function FromArray:TList( arr:Object[] )
 		Local list:TList=New TList
 		For Local i=0 Until arr.length
@@ -438,46 +445,49 @@
 Rem
 bbdoc: Create a linked list
 returns: A new linked list object
-end rem
+End Rem
 Function CreateList:TList()
 	Return New TList
 End Function
 
 Rem
 bbdoc: Clear a linked list
-about: Removes all objects from @list.
-end rem
+about:
+Removes all objects from @list.
+End Rem
 Function ClearList( list:TList )
 	list.Clear
 End Function
 
 Rem
 bbdoc: Count list length
-returns: The numbers of objects in @list.
-end rem
+returns: The number of objects in @list
+End Rem
 Function CountList( list:TList )
 	Return list.Count()
 End Function
 
 Rem
 bbdoc: Check if list is empty
-returns: True if list is empty, else false
-end rem
+returns: True if @list is empty, else false
+End Rem
 Function ListIsEmpty( list:TList )
 	Return list.IsEmpty()
 End Function
 
 Rem
 bbdoc: Check if list contains a value
-returns: True if list contains @value, else false
-end rem
+returns: True if @list contains @value, else false
+End Rem
 Function ListContains( list:TList,value:Object )
 	Return list.Contains( value )
 End Function
 
 Rem
-bbdoc: Sort a list
-end rem
+bbdoc: Sort a list in either ascending (default) or decending order
+about:
+User types should implement a Compare method in order to be sorted.
+End Rem
 Function SortList( list:TList,ascending=True,compareFunc( o1:Object,o2:Object )=CompareObjects )
 	list.Sort ascending,compareFunc
 End Function
@@ -485,69 +495,71 @@
 Rem
 bbdoc: Create a list from an array
 returns: A new linked list
-end rem
+End Rem
 Function ListFromArray:TList( arr:Object[] )
 	Return TList.FromArray( arr )
 End Function
 
 Rem
-bbdoc: convert a list to an array
+bbdoc: Convert a list to an array
 returns: An array of objects
-end rem
+End Rem
 Function ListToArray:Object[]( list:TList )
 	Return list.ToArray()
 End Function
 
 Rem
 bbdoc: Swap the contents of 2 lists
-end rem
+End Rem
 Function SwapLists( list_x:TList,list_y:TList )
 	list_x.Swap list_y
 End Function
 
 Rem
-bbdoc: Reverse the order of elements of a list
-end rem
+bbdoc: Reverse the order of elements in a list
+End Rem
 Function ReverseList( list:TList )
 	list.Reverse
 End Function
 
 Rem
 bbdoc: Find a link in a list
-returns: The link containting @value
-end rem
+returns: The first link in the list containing @value, or Null if not found
+End Rem
 Function ListFindLink:TLink( list:TList,value:Object )
 	Return list.FindLink( value )
 End Function
 
 Rem
-bbdoc: Add an object to a linked list
+bbdoc: Add an object to the end of a linked list
 returns: A link object
-end rem
+End Rem
 Function ListAddLast:TLink( list:TList,value:Object )
 	Return list.AddLast( value )
 End Function
 
 Rem
-bbdoc: Add an object to a linked list
+bbdoc: Add an object to the start of a linked list
 returns: A link object
-end rem
+End Rem
 Function ListAddFirst:TLink( list:TList,value:Object )
 	Return list.AddFirst( value )
 End Function
 
 Rem
-bbdoc: Remove an object from a linked list
-about: #ListRemove scans a list for the specified value and removes its link.
-end rem
+bbdoc: Find and remove an object from a linked list
+about:
+Scans a list for the specified value and removes its link.
+End Rem
 Function ListRemove( list:TList,value:Object )
 	list.Remove value
 End Function
 
 Rem
 bbdoc: Remove an object from a linked list
-about: #RemoveLink is more efficient than #ListRemove.
-end rem
+about:
+More efficient than #ListRemove.
+End Rem
 Function RemoveLink( link:TLink )
 	link.Remove
 End Function
</textarea><br><br>linkedlist.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Rem
bbdoc: Data structures/Linked lists
End Rem
Module BRL.LinkedList

ModuleInfo "Version: 1.07"
ModuleInfo "Author: Mark Sibly"
ModuleInfo "License: Blitz Shared Source Code"
ModuleInfo "Copyright: Blitz Research Ltd"
ModuleInfo "Modserver: BRL"

ModuleInfo "History: 1.07 Release"
ModuleInfo "History: Changed Reverse to maintain TLink stability"
ModuleInfo "History: 1.06 Release"
ModuleInfo "History: Added optional CompareFunc parameter to Sort"
ModuleInfo "History: 1.05 Release"
ModuleInfo "History: Sort now swaps links instead of values"

Function CompareObjects( o1:Object,o2:Object )
	Return o1.Compare( o2 )
End Function

Rem
bbdoc: Link object used by TList
End Rem
Type TLink

	Field _value:Object
	Field _succ:TLink,_pred:TLink
	
	Rem
	bbdoc: Return the object associated with this link
	End Rem
	Method Value:Object()
		Return _value
	End Method

	Rem
	bbdoc: Return the next link in the list
	End Rem
	Method NextLink:TLink()
		If _succ._value&lt;&gt;_succ Return _succ
	End Method

	Rem
	bbdoc: Return the previous link in the list
	End Rem
	Method PrevLink:TLink()
		If _pred._value&lt;&gt;_pred Return _pred
	End Method

	Rem
	bbdoc: Remove the link from the list
	End Rem
	Method Remove()
		_value=Null
		_succ._pred=_pred
		_pred._succ=_succ
	End Method

End Type

Rem
Enumerator object used by TList in order to implement EachIn support
End Rem
Type TListEnum

	Field _link:TLink

	Method HasNext()
		Return _link._value&lt;&gt;_link
	End Method

	Method NextObject:Object()
		Local value:Object=_link._value
		Assert value&lt;&gt;_link
		_link=_link._succ
		Return value
	End Method

End Type

Rem
bbdoc: Linked list
End Rem
Type TList

	Field _head:TLink
	
	Method _pad()
	End Method

	Method New()
		_head=New TLink
		_head._succ=_head
		_head._pred=_head
		_head._value=_head
	End Method
	
?Not Threaded
	Method Delete()
		Clear
		_head._value=Null
		_head._succ=Null
		_head._pred=Null
	End Method
?
	Rem
	bbdoc: Clear a linked list
	about:
	Removes all objects from list.
	End Rem
	Method Clear()
		While _head._succ&lt;&gt;_head
			_head._succ.Remove
		Wend
	End Method

	Rem
	bbdoc: Check if list is empty
	returns: True if list is empty, else false
	End Rem
	Method IsEmpty()
		Return _head._succ=_head
	End Method
	
	Rem
	bbdoc: Check if list contains a value
	returns: True if list contains @value, else false
	End Rem
	Method Contains( value:Object )
		Local link:TLink=_head._succ
		While link&lt;&gt;_head
			If link._value.Compare( value )=0 Return True
			link=link._succ
		Wend
		Return False
	End Method

	Rem
	bbdoc: Add an object to the start of the list
	returns: A link object
	End Rem
	Method AddFirst:TLink( value:Object )
		Assert value Else "Can't insert Null object into list"
		Return InsertAfterLink( value,_head )
	End Method

	Rem
	bbdoc: Add an object to the end of the list
	returns: A link object
	End Rem
	Method AddLast:TLink( value:Object )
		Assert value Else "Can't insert Null object into list"
		Return InsertBeforeLink( value,_head )
	End Method

	Rem
	bbdoc: Return the first object in the list
	returns: Null if the list is empty
	End Rem
	Method First:Object()
		If IsEmpty() Return
		Return _head._succ._value
	End Method

	Rem
	bbdoc: Return the last object in the list
	returns: Null if the list is empty
	End Rem
	Method Last:Object()
		If IsEmpty() Return
		Return _head._pred._value
	End Method

	Rem
	bbdoc: Remove and return the first object in the list
	returns: Null if the list is empty
	End Rem
	Method RemoveFirst:Object()
		If IsEmpty() Return
		Local value:Object=_head._succ._value
		_head._succ.remove
		Return value
	End Method

	Rem
	bbdoc: Remove and return the last object in the list
	returns: Null if the list is empty
	End Rem
	Method RemoveLast:Object()
		If IsEmpty() Return
		Local value:Object=_head._pred._value
		_head._pred.remove
		Return value
	End Method

	Rem
	bbdoc: Return the first link in the list
	returns: Null if the list is empty
	End Rem
	Method FirstLink:TLink()
		If _head._succ&lt;&gt;_head Return _head._succ
	End Method

	Rem
	bbdoc: Return the last link in the list
	returns: Null if the list is empty
	End Rem
	Method LastLink:TLink()
		If _head._pred&lt;&gt;_head Return _head._pred
	End Method

	Rem
	bbdoc: Insert an object before the specified list link
	End Rem
	Method InsertBeforeLink:TLink( value:Object,succ:TLink )
		Assert value Else "Can't insert Null object into list"
		Local link:TLink=New TLink
		link._value=value
		link._succ=succ
		link._pred=succ._pred
		link._pred._succ=link
		succ._pred=link
		Return link
	End Method

	Rem
	bbdoc: Insert an object after the specified list link
	End Rem
	Method InsertAfterLink:TLink( value:Object,pred:TLink )
		Assert value Else "Can't insert Null object into list"
		Local link:TLink=New TLink
		link._value=value
		link._pred=pred
		link._succ=pred._succ
		link._succ._pred=link
		pred._succ=link
		Return link
	End Method

	Rem
	bbdoc: Return the first link in the list with the given value
	returns: Null if @value not found
	End Rem
	Method FindLink:TLink( value:Object )
		Local link:TLink=_head._succ
		While link&lt;&gt;_head
			If link._value.Compare( value )=0 Return link
			link=link._succ
		Wend
	End Method

	Rem
	bbdoc: Return the value of the link at the given index
	about:
	Throws an exception if @index is out of range (must be 0..list.Count()-1 inclusive).
	End Rem
	Method ValueAtIndex:Object( index )
		Assert index&gt;=0 Else "Object index must be positive"
		Local link:TLink=_head._succ
		While link&lt;&gt;_head
			If Not index Return link._value
			link=link._succ
			index:-1
		Wend
		RuntimeError "List index out of range"
	End Method

	Rem
	bbdoc: Count list length
	returns: The number of objects in the list
	End Rem
	Method Count()
		Local link:TLink=_head._succ,count
		While link&lt;&gt;_head
			count:+1
			link=link._succ
		Wend
		Return count
	End Method

	Rem
	bbdoc: Remove an object from a linked list
	about:
	Scans a list for the specified value and removes its link.
	End Rem
	Method Remove( value:Object )
		Local link:TLink=FindLink( value )
		If Not link Return False
		link.Remove
		Return True
	End Method
	
	Rem
	bbdoc: Swap contents with the list specified
	End Rem
	Method Swap( list:TList )
		Local head:TLink=_head
		_head=list._head
		list._head=head
	End Method
	
	Rem
	bbdoc: Create an identical copy of the list
	End Rem
	Method Copy:TList()
		Local list:TList=New TList
		Local link:TLink=_head._succ
		While link&lt;&gt;_head
			list.AddLast link._value
			link=link._succ
		Wend
		Return list
	End Method

	Rem
	bbdoc: Reverse the order of the list
	End Rem
	Method Reverse()
		Local pred:TLink=_head,succ:TLink=pred._succ
		Repeat
			Local link:TLink=succ._succ
			pred._pred=succ
			succ._succ=pred
			pred=succ
			succ=link
		Until pred=_head
	End Method
	
	Rem
	bbdoc: Create a new list that is the reversed version of this list
	End Rem
	Method Reversed:TList()
		Local list:TList=New TList
		Local link:TLink=_head._succ
		While link&lt;&gt;_head
			list.AddFirst link._value
			link=link._succ
		Wend
		Return list
	End Method

	Rem
	bbdoc: Sort a list in either ascending (default) or decending order
	about:
	User types should implement a Compare method in order to be sorted.
	End Rem
	Method Sort( ascending=True,compareFunc( o1:Object,o2:Object )=CompareObjects )
		Local ccsgn=-1
		If ascending ccsgn=1
		
		Local insize=1
		Repeat
			Local merges
			Local tail:TLink=_head
			Local p:TLink=_head._succ

			While p&lt;&gt;_head
				merges:+1
				Local q:TLink=p._succ,qsize=insize,psize=1
				
				While psize&lt;insize And q&lt;&gt;_head
					psize:+1
					q=q._succ
				Wend

				Repeat
					Local t:TLink
					If psize And qsize And q&lt;&gt;_head
						Local cc=CompareFunc( p._value,q._value ) * ccsgn
						If cc&lt;=0
							t=p
							p=p._succ
							psize:-1
						Else
							t=q
							q=q._succ
							qsize:-1
						EndIf
					Else If psize
						t=p
						p=p._succ
						psize:-1
					Else If qsize And q&lt;&gt;_head
						t=q
						q=q._succ
						qsize:-1
					Else
						Exit
					EndIf
					t._pred=tail
					tail._succ=t
					tail=t
				Forever
				p=q
			Wend
			tail._succ=_head
			_head._pred=tail

			If merges&lt;=1 Return

			insize:*2
		Forever
	End Method
		
	Method ObjectEnumerator:TListEnum()
		Local enum:TListEnum=New TListEnum
		enum._link=_head._succ
		Return enum
	End Method

	Rem
	bbdoc: Convert a list to an array
	returns: An array of objects
	End Rem
	Method ToArray:Object[]()
		Local arr:Object[Count()],i
		Local link:TLink=_head._succ
		While link&lt;&gt;_head
			arr[i]=link._value
			link=link._succ
			i:+1
		Wend
		Return arr
	End Method

	Rem
	bbdoc: Create a list from an array
	returns: A new linked list
	End Rem
	Function FromArray:TList( arr:Object[] )
		Local list:TList=New TList
		For Local i=0 Until arr.length
			list.AddLast arr[i]
		Next
		Return list
	End Function

End Type

Rem
bbdoc: Create a linked list
returns: A new linked list object
End Rem
Function CreateList:TList()
	Return New TList
End Function

Rem
bbdoc: Clear a linked list
about:
Removes all objects from @list.
End Rem
Function ClearList( list:TList )
	list.Clear
End Function

Rem
bbdoc: Count list length
returns: The number of objects in @list
End Rem
Function CountList( list:TList )
	Return list.Count()
End Function

Rem
bbdoc: Check if list is empty
returns: True if @list is empty, else false
End Rem
Function ListIsEmpty( list:TList )
	Return list.IsEmpty()
End Function

Rem
bbdoc: Check if list contains a value
returns: True if @list contains @value, else false
End Rem
Function ListContains( list:TList,value:Object )
	Return list.Contains( value )
End Function

Rem
bbdoc: Sort a list in either ascending (default) or decending order
about:
User types should implement a Compare method in order to be sorted.
End Rem
Function SortList( list:TList,ascending=True,compareFunc( o1:Object,o2:Object )=CompareObjects )
	list.Sort ascending,compareFunc
End Function

Rem
bbdoc: Create a list from an array
returns: A new linked list
End Rem
Function ListFromArray:TList( arr:Object[] )
	Return TList.FromArray( arr )
End Function

Rem
bbdoc: Convert a list to an array
returns: An array of objects
End Rem
Function ListToArray:Object[]( list:TList )
	Return list.ToArray()
End Function

Rem
bbdoc: Swap the contents of 2 lists
End Rem
Function SwapLists( list_x:TList,list_y:TList )
	list_x.Swap list_y
End Function

Rem
bbdoc: Reverse the order of elements in a list
End Rem
Function ReverseList( list:TList )
	list.Reverse
End Function

Rem
bbdoc: Find a link in a list
returns: The first link in the list containing @value, or Null if not found
End Rem
Function ListFindLink:TLink( list:TList,value:Object )
	Return list.FindLink( value )
End Function

Rem
bbdoc: Add an object to the end of a linked list
returns: A link object
End Rem
Function ListAddLast:TLink( list:TList,value:Object )
	Return list.AddLast( value )
End Function

Rem
bbdoc: Add an object to the start of a linked list
returns: A link object
End Rem
Function ListAddFirst:TLink( list:TList,value:Object )
	Return list.AddFirst( value )
End Function

Rem
bbdoc: Find and remove an object from a linked list
about:
Scans a list for the specified value and removes its link.
End Rem
Function ListRemove( list:TList,value:Object )
	list.Remove value
End Function

Rem
bbdoc: Remove an object from a linked list
about:
More efficient than #ListRemove.
End Rem
Function RemoveLink( link:TLink )
	link.Remove
End Function
</textarea><br><br>And the zip contents below.<br><br>intro.bbdoc (added "Lists vs. arrays")<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
A linked list allows you to efficiently add and remove objects to and from a collection of objects.

To create a linked list, use the #CreateList command.

Add objects to a linked list using #ListAddFirst or #ListAddLast. Both commands return a link object which can be used to later remove the object with the #RemoveLink command. You can also remove objects with the #ListRemove command. However this is not as efficient as using #RemoveLink because the list must first be searched for the object to be removed.

To visit all the objects in a linked list, you can use an #EachIn loop.

+Lists vs. arrays

Index based access in a linked list is much slower than in an array, since a list must iterate through internal links to find the specific index whereas an array can directly access any index.

"So why use linked lists at all?" you might ask. Inserting a value into the middle of an array is an 'expensive' operation, we must move all items after the insert position down in the array by one, then insert the new value in the space created. In a list however, all we have to do is redirect the pointers between links.

So when you need to make many changes to data use a list and when you need to access data randomly, or if speed is critical, use an array. To make many insertions and deletions to an array you can convert it to a list with #ListFromArray and then back to an array with #ListToArray.
</textarea><br><br>clearlist.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' clearlist.bmx

list:TList=CreateList()

ListAddFirst( list,"item 1" )
ListAddFirst( list,"item 2" )

ClearList( list )

For item$=EachIn list
	Print item
Next
</textarea><br><br>countlist.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' countlist.bmx

list:TList=CreateList()

ListAddFirst( list,"item 1" )
ListAddFirst( list,"item 2" )

Print CountList( list )
</textarea><br><br>createlist.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' createlist.bmx

' create a list to hold some objects

list:TList=CreateList()

' add some string objects to the list

ListAddLast( list,"one" )
ListAddLast( list,"two" )
ListAddLast( list,"three" )

' enumerate all the strings in the list

For item$=EachIn list
	Print item
Next
</textarea><br><br>listaddfirst.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' listaddfirst.bmx

list:TList=CreateList()

ListAddFirst( list,"item 1" )
ListAddFirst( list,"item 2" )

For item$=EachIn list
	Print item
Next
</textarea><br><br>listaddlast.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' listaddlast.bmx

list:TList=CreateList()

ListAddLast( list,"item 1" )
ListAddLast( list,"item 2" )

For item$=EachIn list
	Print item
Next
</textarea><br><br>listcontains.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' listcontains.bmx

list:TList=CreateList()

ListAddLast( list,"item 1" )
ListAddLast( list,"item 2" )

Print ListContains( list,"item 2" )
Print ListContains( list,"item 3" )
</textarea><br><br>listfromarray.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' listfromarray.bmx

Local array:String[]=["item 1","item 2","item 3"]

list:TList=ListFromArray( array )

For item$=EachIn list
	Print item
Next
</textarea><br><br>listisempty.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' listisempty.bmx

list:TList=CreateList()

Print ListIsEmpty( list )

ListAddFirst( list,"item 1" )

Print ListIsEmpty( list )
</textarea><br><br>listremove.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' listremove.bmx

list:TList=CreateList()

ListAddLast( list,"item 1" )
ListAddLast( list,"item 2" )
ListAddLast( list,"item 3" )

ListRemove( list,"item 2" )

For item$=EachIn list
	Print item
Next
</textarea><br><br>listtoarray.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' listtoarray.bmx

Local array:Object[]

list:TList=CreateList()

ListAddLast( list,"item 1" )
ListAddLast( list,"item 2" )
ListAddLast( list,"item 3" )

array=ListToArray( list )

For index=0 To array.length-1
	Print String( array[index] )
Next
</textarea><br><br>removelink.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' removelink.bmx

list:TList=CreateList()

ListAddLast( list,"item 1" )
ListAddLast( list,"item 2" )
ListAddLast( list,"item 3" )

link:TLink=ListFindLink( list,"item 2" )

If link&lt;&gt;Null Then RemoveLink( link )

For item$=EachIn list
	Print item
Next
</textarea><br><br>reverselist.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' reverselist.bmx

list:TList=CreateList()

ListAddLast( list,"item 1" )
ListAddLast( list,"item 2" )
ListAddLast( list,"item 3" )

ReverseList( list )

For item$=EachIn list
	Print item
Next
</textarea><br><br>sortlist.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' sortlist.bmx

list:TList=CreateList()

ListAddLast( list,"item 3" )
ListAddLast( list,"item 1" )
ListAddLast( list,"item 4" )
ListAddLast( list,"item 2" )

SortList( list )

For item$=EachIn list
	Print item
Next
</textarea><br><br>swaplists.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' swaplists.bmx

list:TList=ListFromArray( ["item 1","item 2","item 3"] )

swap:TList=ListFromArray( ["one","two","three"] )

SwapLists( list,swap )

For item$=EachIn list
	Print item
Next

For item=EachIn swap
	Print item
Next
</textarea><br><br>TLink folder<br><br>nextlink.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' nextlink.bmx

list:TList=New TList

list.AddLast( "item 1" )
list.AddLast( "item 2" )
list.AddLast( "item 3" )

link:TLink=list.FindLink( "item 2" )

Print String( link.NextLink().Value() )
</textarea><br><br>prevlink.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' prevlink.bmx

list:TList=New TList

list.AddLast( "item 1" )
list.AddLast( "item 2" )
list.AddLast( "item 3" )

link:TLink=list.FindLink( "item 2" )

Print String( link.PrevLink().Value() )
</textarea><br><br>value.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' value.bmx

list:TList=New TList

list.AddLast( "item 1" )
list.AddLast( "item 2" )
list.AddLast( "item 3" )

link:TLink=list.FindLink( "item 2" )

Print String( link.Value() )
</textarea><br><br>TList folder<br><br>copy.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' copy.bmx

list:TList=New TList

list.AddLast( "item 1" )
list.AddLast( "item 2" )
list.AddLast( "item 3" )

clone:TList=list.Copy()

For item$=EachIn clone
	Print item
Next
</textarea><br><br>first.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' first.bmx

list:TList=New TList

list.AddLast( "item 1" )
list.AddLast( "item 2" )
list.AddLast( "item 3" )

Print String( list.First() )
</textarea><br><br>firstlink.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' firstlink.bmx

list:TList=New TList

list.AddLast( "item 1" )
list.AddLast( "item 2" )
list.AddLast( "item 3" )

link:TLink=list.FirstLink()

Print String( link.Value() )
</textarea><br><br>insertafterlink.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' insertafterlink.bmx

list:TList=New TList

list.AddLast( "item 1" )
list.AddLast( "item 2" )
list.AddLast( "item 3" )
list.AddLast( "item 4" )

list.Remove( "item 3" )

link:TLink=list.FindLink( "item 2" )

If link&lt;&gt;Null Then list.InsertAfterLink( "new item 3!",link )

For item$=EachIn list
	Print item
Next
</textarea><br><br>last.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' last.bmx

list:TList=New TList

list.AddLast( "item 1" )
list.AddLast( "item 2" )
list.AddLast( "item 3" )

Print String( list.Last() )
</textarea><br><br>lastlink.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' lastlink.bmx

list:TList=New TList

list.AddLast( "item 1" )
list.AddLast( "item 2" )
list.AddLast( "item 3" )

link:TLink=list.LastLink()

Print String( link.Value() )
</textarea><br><br>removefirst.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' removefirst.bmx

list:TList=New TList

list.AddLast( "item 1" )
list.AddLast( "item 2" )
list.AddLast( "item 3" )

list.RemoveFirst()

For item$=EachIn list
	Print item
Next
</textarea><br><br>removelast.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' removelast.bmx

list:TList=New TList

list.AddLast( "item 1" )
list.AddLast( "item 2" )
list.AddLast( "item 3" )

list.RemoveLast()

For item$=EachIn list
	Print item
Next
</textarea><br><br>reversed.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' reversed.bmx

list:TList=New TList

list.AddLast( "item 1" )
list.AddLast( "item 2" )
list.AddLast( "item 3" )

reversed:TList=list.Reversed()

For item$=EachIn reversed
	Print item
Next
</textarea><br><br>valueatindex.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' valueatindex.bmx

list:TList=New TList

list.AddLast( "item 1" )
list.AddLast( "item 2" )
list.AddLast( "item 3" )

Print String( list.ValueAtIndex( 1 ) )
</textarea> <br><br></td></tr></table><br>
<a name="1036820"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >plash</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> There are code examples for the Tlink and Tlist methods too but I've left them out as that appears to break makedocs... Perhaps code examples for methods is overkill anyway?? <br></div>Personally, I don't think one can be too exhaustive when it comes to documentation - hence why I added examples to each method on the wiki. <br><br></td></tr></table><br>
<a name="1036822"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> So Psyced is Plash then? These are great code examples by the way.<br><br>I'm not sure but perhaps you're right. I'll let Mark Sibly decide then. :) <br><br>Edit: moved TList and Tlink examples to first post. <br><br></td></tr></table><br>
<a name="1036824"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >plash</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> So Psyced is Plash then? <br></div>Yes.<br><br>I should also note that the <a href="/posts.php?topic=85388" target="_blank">last time</a> we tried to properly format and document the BRL modules, <a href="/posts.php?topic=85412" target="_blank">it was ignored</a>.<br><br>I seriously want this to work out, but it's just not going to happen with Mark not adding the changes. We clearly need an organized solution - as suggested previously, an official repository where users could make changes would be ideal.<br>Though, again, lack of response from Mark/BRL isn't exactly helping. <br><br></td></tr></table><br>
<a name="1036899"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> I should also note that the last time we tried to properly format and document the BRL modules, it was ignored. <br></div><br>Well actually that made it into v 1.32, see <a href="/posts.php?topic=83917#947406" target="_blank">here</a>. So you can get changes added if you go about it the right way. Sadly there is no advice given on how to contribute so we have to figure it out ourselves. <br><br></td></tr></table><br>
<a name="1036974"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Why dont' use the 'manual' section of the site to add examples and comments?<br>It's the only 'stable' solution at the moment and it doesnt' require hacks/changes to the modules source.<br>I added some examples to the CreateMap command more than 1 year ago (related to another markcw's thread) - not the best implementation probably... <br><br></td></tr></table><br>
<a name="1036977"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >plash</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> @degac: That's not a real solution, as the main text is not changeable by users and the content on the online manual is <i>only</i> online. <br><br></td></tr></table><br>
<a name="1037009"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, but the 'bad' thing is you need to *manually* change some source code... not everyone can do this without problems - unless these changes become officials. <br><br></td></tr></table><br>
<a name="1037037"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >plash</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Ok, but the 'bad' thing is you need to *manually* change some source code... not everyone can do this without problems - unless these changes become officials.  <br></div>I would gladly do this if there was a better (guaranteed) avenue to have them added to a release.<br><br>Writing docs in code is far easier than using WikiBooks or the online manual. <br><br></td></tr></table><br>
<a name="1038113"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just did a major revision of everything today. I'm finished this module now (well... maybe). <br><br></td></tr></table><br>
<a name="1038129"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Who was John Galt?</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> I seriously hope these mods make it into a release. Many thanks for your efforts, guys. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
