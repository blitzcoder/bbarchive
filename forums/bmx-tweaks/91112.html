<!DOCTYPE html><html lang="en" ><head ><title >brl.math docs</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >brl.math docs</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=110" >BlitzMax Module Tweaks</a>/<a href="#bottom" >brl.math docs</a><br><br>
<a name="1036979"></a>

<a name="1221947"></a>

<a name="1222185"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is an update to the brl.math docs.<br><br>The files are zipped in a doc folder (module source not included according to the BSSC license).<br>math-doc.zip (expired)<br><br>I'm posting the module source (from v 1.41) and the patch.<br><br>Last edit: 14 Aug 2010<br><br>math.diff<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">--- math-old.bmx	2010-07-13 03:55:01.000000000 +0100
+++ math.bmx	2010-08-14 06:00:07.500720046 +0100
@@ -20,94 +20,160 @@
 Extern
 
 Rem
-bbdoc: Check if a value is NAN
-returns: True if @x is 'not a number' (eg: Sqr(-1))
+bbdoc: Check if a value is 'not a number'
+returns: True if @x is NaN, otherwise false.
+about:
+Examples of NaN are: the square root or logarithm of a negative number, 
+the inverse sine or cosine of a number less than -1 or greater than 1.
 End Rem
 Function IsNan( x:Double )="bbIsNan"
 
 Rem
-bbdoc: Check if a value is infinite (eg: 1.0/0.0)
-returns: True if @x is infinite
+bbdoc: Check if a value is infinite
+returns: True if @x is infinite, otherwise false.
+about:
+Examples of infinite are: division by zero, arithmetic overflow.
 End Rem
 Function IsInf( x:Double )="bbIsInf"
 
 Rem
 bbdoc: Square root of @x
+returns: NaN if @x is negative.
 End Rem
 Function Sqr:Double( x:Double )="bbSqr"
 
 Rem
 bbdoc: Sine of @x degrees
+returns: A value in the range -1..1.
+about:
+In trigonometry, sine can determine the remaining side of a right triangle 
+when an angle and the length of the opposite or hypotenuse is known.
 End Rem
 Function Sin:Double( x:Double )="bbSin"
 
 Rem
 bbdoc: Cosine of @x degrees
+returns: A value in the range -1..1.
+about:
+In trigonometry, cosine can determine the remaining side of a right triangle 
+when an angle and the length of the adjacent or hypotenuse is known.
 End Rem
 Function Cos:Double( x:Double )="bbCos"
 
 Rem
 bbdoc: Tangent of @x degrees
+returns: A value in the range -1..1.
+about:
+In trigonometry, tangent can determine the remaining side of a right triangle 
+when an angle and the length of the opposite or adjacent is known.
 End Rem
 Function Tan:Double( x:Double )="bbTan"
 
 Rem
-bbdoc: Inverse Sine of @x 
+bbdoc: Inverse sine of @x
+returns: A value in the range -90..90 degrees, or NaN if @x is not in the range -1..1.
+about:
+In trigonometry, arc sine can determine the remaining angles of a right triangle 
+when the lengths of the opposite and hypotenuse are known.
 End Rem
 Function ASin:Double( x:Double )="bbASin"
 
 Rem
-bbdoc: Inverse Cosine of @x
+bbdoc: Inverse cosine of @x
+returns: A value in the range 0..180 degrees, or NaN if @x is not in the range -1..1.
+about:
+In trigonometry, arc cosine can determine the remaining angles of a right triangle 
+when the lengths of the adjacent and hypotenuse are known.
 End Rem
 Function ACos:Double( x:Double )="bbACos"
 
 Rem
-bbdoc: Inverse Tangent of @x
+bbdoc: Inverse tangent of @x
+returns: A value in the range -90..90 degrees.
+about:
+In trigonometry, arc tangent can determine the remaining angles of a right triangle 
+when the lengths of the opposite and adjacent are known.
 End Rem
 Function ATan:Double( x:Double )="bbATan"
 
 Rem
-bbdoc: Inverse Tangent of two variables @x , @y
+bbdoc: Inverse tangent of two variables, @y and @x
+returns: A value in the range -180..180 degrees.
+about:
+Arc tangent 2 is similar to computing the arc tangent of @y / @x except that the 
+signs of the arguments are used to determine the quadrant. Therefore it can be used 
+to work out a spacial vector i.e. where both an angle and length are known.
 End Rem
 Function ATan2:Double( y:Double,x:Double )="bbATan2"
 
 Rem
 bbdoc: Hyperbolic sine of @x
+about:
+The hyperbolic sine and cosine get their name from the fact that they 
+define the parametric form for the hyperbola, where: x=sinh(t), y=cosh(t). 
+
+Similarly, the sine and cosine are sometimes called the circular functions 
+as they provide the parametric form for the circle, where: x=sin(t), y=cos(t).
 End Rem
 Function Sinh:Double( x:Double )="bbSinh"
 
 Rem
 bbdoc: Hyperbolic cosine of @x
+about:
+The hyperbolic sine and cosine get their name from the fact that they 
+define the parametric form for the hyperbola, where: x=sinh(t), y=cosh(t). 
+
+Similarly, the sine and cosine are sometimes called the circular functions 
+as they provide the parametric form for the circle, where: x=sin(t), y=cos(t).
 End Rem
 Function Cosh:Double( x:Double )="bbCosh"
 
 Rem
 bbdoc: Hyperbolic tangent of @x
+about:
+Just as the tangent is derived from the sine and cosine, so is the 
+hyperbolic tangent derived from the hyperbolic sine and cosine.
 End Rem
 Function Tanh:Double( x:Double )="bbTanh"
 
 Rem
-bbdoc: Exponential function
-end rem
+bbdoc: Exponential function of @x
+about:
+The exponential function is the result of the base e to the power of @x, 
+where e is approximately 2.718.
+End Rem
 Function Exp:Double( x:Double )="bbExp"
 
 Rem
-bbdoc: Natural logarithm
+bbdoc: Natural logarithm of @x
+returns: NaN if @x is negative.
+about:
+The natural logarithm, also called the base e logarithm, is the power to which the 
+base e must be raised to produce @x, where e is approximately 2.718. 
+It is the inverse of the exponential function.
 End Rem
 Function Log:Double( x:Double )="bbLog"
 
 Rem
-bbdoc: Base 10 logarithm
+bbdoc: Base 10 logarithm of @x
+returns: NaN if @x is negative.
+about:
+The base 10 logarithm, also called the common logarithm, is the power to which the 
+base 10 must be raised to produce @x.
 End Rem
 Function Log10:Double( x:Double )="bbLog10"
 
 Rem
 bbdoc: Smallest integral value not less than @x
+about:
+Rounds a floating point value up.
 End Rem
 Function Ceil:Double( x:Double )="bbCeil"
 
 Rem
 bbdoc: Largest integral value not greater than @x
+about:
+Rounds a floating point value down.
 End Rem
 Function Floor:Double( x:Double )="bbFloor"
 
</textarea><br><br>math.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Rem
bbdoc: Math/Math
End Rem
Module BRL.Math

ModuleInfo "Version: 1.05"
ModuleInfo "Author: Mark Sibly"
ModuleInfo "License: Blitz Shared Source Code"
ModuleInfo "Copyright: Blitz Research Ltd"
ModuleInfo "Modserver: BRL"

ModuleInfo "History: 1.05 Release"
ModuleInfo "History: Added IsNan and IsInf"

Import "math.c"

Extern

Rem
bbdoc: Check if a value is 'not a number'
returns: True if @x is NaN, otherwise false.
about:
Examples of NaN are: the square root or logarithm of a negative number, 
the inverse sine or cosine of a number less than -1 or greater than 1.
End Rem
Function IsNan( x:Double )="bbIsNan"

Rem
bbdoc: Check if a value is infinite
returns: True if @x is infinite, otherwise false.
about:
Examples of infinite are: division by zero, arithmetic overflow.
End Rem
Function IsInf( x:Double )="bbIsInf"

Rem
bbdoc: Square root of @x
returns: NaN if @x is negative.
End Rem
Function Sqr:Double( x:Double )="bbSqr"

Rem
bbdoc: Sine of @x degrees
returns: A value in the range -1..1.
about:
In trigonometry, sine can determine the remaining side of a right triangle 
when an angle and the length of the opposite or hypotenuse is known.
End Rem
Function Sin:Double( x:Double )="bbSin"

Rem
bbdoc: Cosine of @x degrees
returns: A value in the range -1..1.
about:
In trigonometry, cosine can determine the remaining side of a right triangle 
when an angle and the length of the adjacent or hypotenuse is known.
End Rem
Function Cos:Double( x:Double )="bbCos"

Rem
bbdoc: Tangent of @x degrees
returns: A value in the range -1..1.
about:
In trigonometry, tangent can determine the remaining side of a right triangle 
when an angle and the length of the opposite or adjacent is known.
End Rem
Function Tan:Double( x:Double )="bbTan"

Rem
bbdoc: Inverse sine of @x
returns: A value in the range -90..90 degrees, or NaN if @x is not in the range -1..1.
about:
In trigonometry, arc sine can determine the remaining angles of a right triangle 
when the lengths of the opposite and hypotenuse are known.
End Rem
Function ASin:Double( x:Double )="bbASin"

Rem
bbdoc: Inverse cosine of @x
returns: A value in the range 0..180 degrees, or NaN if @x is not in the range -1..1.
about:
In trigonometry, arc cosine can determine the remaining angles of a right triangle 
when the lengths of the adjacent and hypotenuse are known.
End Rem
Function ACos:Double( x:Double )="bbACos"

Rem
bbdoc: Inverse tangent of @x
returns: A value in the range -90..90 degrees.
about:
In trigonometry, arc tangent can determine the remaining angles of a right triangle 
when the lengths of the opposite and adjacent are known.
End Rem
Function ATan:Double( x:Double )="bbATan"

Rem
bbdoc: Inverse tangent of two variables, @y and @x
returns: A value in the range -180..180 degrees.
about:
Arc tangent 2 is similar to computing the arc tangent of @y / @x except that the 
signs of the arguments are used to determine the quadrant. Therefore it can be used 
to work out a spacial vector i.e. where both an angle and length are known.
End Rem
Function ATan2:Double( y:Double,x:Double )="bbATan2"

Rem
bbdoc: Hyperbolic sine of @x
about:
The hyperbolic sine and cosine get their name from the fact that they 
define the parametric form for the hyperbola, where: x=sinh(t), y=cosh(t). 

Similarly, the sine and cosine are sometimes called the circular functions 
as they provide the parametric form for the circle, where: x=sin(t), y=cos(t).
End Rem
Function Sinh:Double( x:Double )="bbSinh"

Rem
bbdoc: Hyperbolic cosine of @x
about:
The hyperbolic sine and cosine get their name from the fact that they 
define the parametric form for the hyperbola, where: x=sinh(t), y=cosh(t). 

Similarly, the sine and cosine are sometimes called the circular functions 
as they provide the parametric form for the circle, where: x=sin(t), y=cos(t).
End Rem
Function Cosh:Double( x:Double )="bbCosh"

Rem
bbdoc: Hyperbolic tangent of @x
about:
Just as the tangent is derived from the sine and cosine, so is the 
hyperbolic tangent derived from the hyperbolic sine and cosine.
End Rem
Function Tanh:Double( x:Double )="bbTanh"

Rem
bbdoc: Exponential function of @x
about:
The exponential function is the result of the base e to the power of @x, 
where e is approximately 2.718.
End Rem
Function Exp:Double( x:Double )="bbExp"

Rem
bbdoc: Natural logarithm of @x
returns: NaN if @x is negative.
about:
The natural logarithm, also called the base e logarithm, is the power to which the 
base e must be raised to produce @x, where e is approximately 2.718. 
It is the inverse of the exponential function.
End Rem
Function Log:Double( x:Double )="bbLog"

Rem
bbdoc: Base 10 logarithm of @x
returns: NaN if @x is negative.
about:
The base 10 logarithm, also called the common logarithm, is the power to which the 
base 10 must be raised to produce @x.
End Rem
Function Log10:Double( x:Double )="bbLog10"

Rem
bbdoc: Smallest integral value not less than @x
about:
Rounds a floating point value up.
End Rem
Function Ceil:Double( x:Double )="bbCeil"

Rem
bbdoc: Largest integral value not greater than @x
about:
Rounds a floating point value down.
End Rem
Function Floor:Double( x:Double )="bbFloor"

End Extern
</textarea><br><br>And the zip contents below.<br><br>acos.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' acos.bmx

For x#=-1.0 To 1.1 Step 0.1
	r#=ACos( x )
	
	graph$=""
	For n=0 To 60
		If n=Int( r/3 ) Then graph:+"." Else graph:+" "
		If IsNan( r ) And graph.length=1 Then graph="."
	Next
	
	Print graph+"   "+x+"~t"+r
Next
</textarea><br><br>asin.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' asin.bmx

For x#=-1.0 To 1.1 Step 0.1
	r#=ASin( x )
	
	graph$=""
	For n=-30 To 30
		If n=Int( r/3 ) Then graph:+"." Else graph:+" "
		If IsNan( r ) And graph.length=60 Then graph=graph[..graph.length-1]+"."
	Next
	
	Print graph+"   "+x+"~t"+r
Next
</textarea><br><br>atan.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' atan.bmx

For x#=-3.0 To 3.2 Step 0.2
	r#=ATan( x )
	
	graph$=""
	For n=-30 To 30
		If n=Int( r/3 ) Then graph:+"." Else graph:+" "
	Next
	
	Print graph+"   "+x+"~t"+r
Next
</textarea><br><br>atan2.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' atan2.bmx

ax#=2.0
ay#=-1.0
bx#=-1.0
by#=2.0

Print ATan2( by-ay,bx-ax )

Print ATan( (by-ay)/(bx-ax) )
</textarea><br><br>ceil.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' ceil.bmx

Print Ceil( 3.5 )
</textarea><br><br>cos.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' cos.bmx

For x#=-180.0 To 180.0 Step 15.0
	r#=Cos( x )
	
	graph$=""
	For n=-30 To 30
		If n=Int( r*30 ) Then graph:+"." Else graph:+" "
	Next
	
	Print graph+"   "+x+"~t"+r
Next
</textarea><br><br>cosh.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' cosh.bmx

For x#=-2.0 To 2.2 Step 0.2
	r#=Cosh( x )
	
	graph$=""
	For n=-30 To 30
		If n=Int( r*8 ) Then graph:+"." Else graph:+" "
	Next
	
	Print graph+"   "+x+"~t"+r
Next
</textarea><br><br>exp.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' exp.bmx

For x#=-3.0 To 3.2 Step 0.2
	r#=Exp( x )
	
	graph$=""
	For n=0 To 60
		If n=Int( r*8 ) Then graph:+"." Else graph:+" "
	Next
	
	Print graph+"   "+x+"~t"+r
Next
</textarea><br><br>floor.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' floor.bmx

Print Floor( 3.5 )
</textarea><br><br>isinf.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' isinf.bmx

Print IsInf( 1.0/0.0 )

Print IsInf( 3.40283e+38 )
</textarea><br><br>isnan.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' isnan.bmx

Print IsNan( Sqr( -1.0 ) )

Print IsNan( Log( -1.0 ) )

Print IsNan( ASin( 1.1 ) )

Print IsNan( ACos( -1.1 ) )
</textarea><br><br>log.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' log.bmx

For x#=0.0 To 30.0
	r#=Log( x )
	
	graph$=""
	For n=-30 To 30
		If n=Int( r*8 ) Then graph:+"." Else graph:+" "
		If IsInf( r ) And graph.length=1 Then graph="."
	Next
	
	Print graph+"   "+x+"~t"+r
Next
</textarea><br><br>log10.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' log10.bmx

For x#=0.0 To 30.0
	r#=Log10( x )
	
	graph$=""
	For n=-30 To 30
		If n=Int( r*8 ) Then graph:+"." Else graph:+" "
		If IsInf( r ) And graph.length=1 Then graph="."
	Next
	
	Print graph+"   "+x+"~t"+r
Next
</textarea><br><br>sin.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' sin.bmx

For x#=-180.0 To 180.0 Step 15.0
	r#=Sin( x )
	
	graph$=""
	For n=-30 To 30
		If n=Int( r*30 ) Then graph:+"." Else graph:+" "
	Next
	
	Print graph+"   "+x+"~t"+r
Next
</textarea><br><br>sinh.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' sinh.bmx

For x#=-2.0 To 2.2 Step 0.2
	r#=Sinh( x )
	
	graph$=""
	For n=-30 To 30
		If n=Int( r*8 ) Then graph:+"." Else graph:+" "
	Next
	
	Print graph+"   "+x+"~t"+r
Next
</textarea><br><br>sqr.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' sqr.bmx

Print Sqr( 9.0 )
</textarea><br><br>tan.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' tan.bmx

For x#=-60.0 To 60.0 Step 5.0
	r#=Tan( x )
	
	graph$=""
	For n=-30 To 30
		If n=Int( r*30 ) Then graph:+"." Else graph:+" "
	Next
	
	Print graph+"   "+x+"~t"+r
Next
</textarea><br><br>tanh.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' tanh.bmx

For x#=-2.0 To 2.2 Step 0.2
	r#=Tanh( x )
	
	graph$=""
	For n=-30 To 30
		If n=Int( r*8 ) Then graph:+"." Else graph:+" "
	Next
	
	Print graph+"   "+x+"~t"+r
Next
</textarea> <br><br></td></tr></table><br>
<a name="1041408"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well that's a major update... I've finished this module now.<br><br>I'm going to zip up the html output for all my tweaked modules soon as a quick way for people to patch the docs. <br><br></td></tr></table><br>
<a name="1041851"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Okay, here it is, a html only copy of all my docs work so far.<br><a href="http://blitzmirror.ueuo.com/docs-html-patch.zip" target="_blank">http://blitzmirror.ueuo.com/docs-html-patch.zip</a><br><br>Extract to your <b>docs</b> folder. To remove just rebuild the docs in MaxIDE.<br><br>Last update: 18 Aug 2010 <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
