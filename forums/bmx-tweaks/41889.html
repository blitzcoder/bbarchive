<!DOCTYPE html><html lang="en" ><head ><title >TPath</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >TPath</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=110" >BlitzMax Module Tweaks</a>/<a href="#bottom" >TPath</a><br><br>
<a name="470069"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >xacto</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've put together an example of how paths could be abstracted into a type.  The type would eliminate the need to manually tweak strings and handle any cross platform issues automatically in one place.  This would also clean up a lot of the modules where path$ = path$.Replace( "\", "/" ) is constantly repeated.  <br><br>Here's the code:<br><br><pre class=code>
Strict

Import BRL.Stream
Import BRL.FileSystem

Type TInvalidPathException
	Method ToString:String()
		Return "The specified path does not exist."
	End Method
End Type

Type TPath 
	Function FromString:TPath( path:String )
		Local aPath:TPath = New TPath
		aPath.SetPath( path )
		Return aPath
	End Function
	
	Method GetFileName:String()
		Local index:Int = m_path.FindLast( "/" )
		Local result:String = Null
		If index &gt; -1 
			result = m_path[index + 1..m_path.length]
		End If
		Return result
	End Method

	Method GetFileNameWithoutExtension:String()
		Local temp:String = GetFileName()
		If temp &lt;&gt; Null
			Local index:Int = temp.FindLast( "." )
			If index &gt; -1
				temp = temp[..index]
			End If
		End If
		Return temp
	End Method
	
	Method GetExtension:String()
		Local index:Int = m_path.FindLast( "." )
		Local result:String = Null
		If index &gt; -1
			result = m_path[index + 1..m_path.length]
		End If
		Return result
	End Method
	
	Method Exists:Int()
		Return FileType( m_path ) &lt;&gt; FILETYPE_NONE 
	End Method
	
	Method ChangeExtension( extension:String )
		Local index:Int = m_path.FindLast( "." )
		If index &gt; -1
			m_path = m_path[..index] + extension
		End If
	End Method
	
	Method GetDirectoryName:String()
		Local index:Int = m_path.FindLast( "/" )
		Local result:String = Null
		If index &gt; -1
			result = m_path[..index]
		Else
			If FileType( m_path ) = FILETYPE_DIR
				result = m_path
			End If				
		End If
		Return result
	End Method
					
	Method SetPath( path:String )
		m_path = path.Replace( "\", "/" )	
		If FileType( m_path ) = FILETYPE_NONE
			Throw New TInvalidPathException
		ElseIf FileType( m_path ) = FILETYPE_DIR
			m_path:+"/"
		End If
?Linux
?MacOS
		Local cased:String = CasedFileName( m_path )
		If cased
			m_path = cased
		End If
		Local index:Int = m_path.Find( ":" )
		If index &gt; -1 
			m_path = m_path[index + 1..m_path.length]
		End If
?
	End Method

	Method Append( path:String )
		Local temp:String = m_path
		If temp[temp.length - 1] &lt;&gt; "/"
			temp:+"/"
		End If
		temp:+path
		SetPath( temp )
	End Method
	
	Method ToString:String()
		Return m_path
	End Method
		
	Field m_path:String
End Type 

TestTPath()
Function TestTPath()
	Local testPath:TPath = TPath.FromString( "c:\temp" )
	testPath.Append( "form.dat" )
	Print testPath.ToString()
	Print testPath.GetDirectoryName()
	Print testPath.GetFileName()
	Print testPath.GetFileNameWithoutExtension()
	Print testPath.GetExtension()
	Print testPath.Exists()
End Function
</pre><br><br>Everything that accepts a String now for a path would instead accept a TPath.  To make the impact on client code almost seamless BRL could add a ToPath() method on String, which would simply create a new instance of TPath via TPath.FromString.<br><br>An example might look like this:<br><br><pre class=code>
Global anImage:TImage = LoadImage( "C:\gfx\balloon.png".ToPath() )
</pre><br><br>NOTES:<br><br>Since this is really the first class I've implemented in BlitzMax I'd like to pass on some comments/suggestions to BRL:<br><br>1. Private/Public should work within a Type.  Without being able to set members to Private we lose true encapsulation.  In the TPath example above, m_path is still publically accessible so a beginner might accidentially access it directly which would render the abstraction worthless.  An alternative here would be to support encapsulated fields a.k.a properties a la .NET, Ruby.<br><br>2. The ability to add extensions to the base types would be wonderful.  In the TPath example I would have loved to add a ToPath method on String.  This is different from inheritence as it changes an existing type.  This is similar but not quite the same as partial types.<br><br>3. Wishful thinking, I'm sure, but some form of simple reflection would be wonderful. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
