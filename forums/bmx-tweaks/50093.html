<!DOCTYPE html><html lang="en" ><head ><title >SendTo() RecvFrom()</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >SendTo() RecvFrom()</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=110" >BlitzMax Module Tweaks</a>/<a href="#bottom" >SendTo() RecvFrom()</a><br><br>
<a name="557065"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tom</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Not yet implemented as of v1.10, these 2 Methods extend the send &amp; recieve capability of sockets.<br><br>SendTo() works similar to .Send(), except with .Send() you're supposed to connect to a socket before using it. With .SendTo() you do not have to connect a socket first, you simply specify the destination IP &amp; Port.<br><br>RecvFrom() works just as .Recv() does, but you also pass the method two Integer variables (srcIP &amp; srcPort). After the call to RecvFrom() (if there was data in the socket), the srcIP &amp; srcPort variables will be filled with the IP &amp; Port of the sender.<br><br>Add this code in '\mod\brl.mod\socket.mod\socket.bmx' somewhere after 'Type TSocket', compile with command line 'bmk makemods brl.socket'<br><br>Tested &amp; working in Win32, they *should* work in Linux/Mac<br><br>Cya!<br>Tom<br><br><pre class=code>  Method SendTo( buf:Byte Ptr,count,destIP,destPort,flags=0 )
    Local n=sendto_( _socket,buf,count,flags,destIP,destPort )
    If n&lt;0 Return 0
    Return n
  End Method

  Method RecvFrom( buf:Byte Ptr,count,srcIP Var,srcPort Var,flags=0 )
    Local n=recvfrom_( _socket,buf,count,flags,srcIP,srcPort )
    If n&lt;0 Return 0
    Return n
  End Method</pre> <br><br></td></tr></table><br>
<a name="563971"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Vertex</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi!<br>Problem if you use recv or recvfrom with an udp socket is that you must read the complete size of the data buffer. If you don't do this, then the message is cut to specific size and the rest where trashed.<br><br>Here the example for the UDPStream:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict

Import Brl.Stream
Import Pub.StdC

Private

Type TSockAddr
	Field SinFamily : Short
	Field SinPort   : Short
	Field SinAddr   : Int
	Field SinZero   : Long
End Type

Extern "OS"
	Const INVALID_SOCKET_ = -1

	?Win32
		Const FIONREAD = $4004667F
		Function ioctl_:Int(Socket:Int, Command:Int, Arguments:Byte Ptr) = "ioctlsocket@12"
		Function inet_addr_:Int(Address$z) = "inet_addr@4"
		Function inet_ntoa_:Byte Ptr(Adress:Int) = "inet_ntoa@4"
		Function getsockname_:Int(Socket:Int, Name:Byte Ptr, NameLen:Int Ptr) = "getsockname@12"
		Function err:Int() = "WSAGetLastError@0"

	?MacOS
		Const FIONREAD = $4004667F
		Function ioctl_(Socket:Int, Command:Int, Arguments:Byte Ptr) = "ioctl"
		Function inet_addr_:Int(Address$z) = "inet_addr"
		Function inet_ntoa_:Byte Ptr(Adress:Int) = "inet_ntoa"
		Function getsockname_:Int(Socket:Int, Name:Byte Ptr, NameLen:Int Ptr) = "getsockname"

	?Linux
		Const FIONREAD = $0000541B
		Function ioctl_(Socket:Int, Command:Int, Arguments:Byte Ptr) = "ioctl"
		Function inet_addr_:Int(Address$z) = "inet_addr"
		Function inet_ntoa_:Byte Ptr(Adress:Int) = "inet_ntoa"
		Function getsockname_:Int(Socket:Int, Name:Byte Ptr, NameLen:Int Ptr) = "getsockname"
	?
End Extern

Public

Type TNetwork
	Function DottedIP:String(IP:Int)
		Return String.FromCString(inet_ntoa_(htonl_(IP)))
	End Function
	
	Function IntIP:Int(IP:String)
		Return htonl_(inet_addr_(IP))
	End Function
End Type

Type TUDPStream Extends TStream
	Field Socket      : Int
	Field LocalPort   : Short
	Field RemotePort  : Short
	Field RemoteIP    : Int
	Field MessageIP   : Int
	Field MessagePort : Short
	Field ReadTimeout : Int

	Method New()
		Self.Socket      = socket_(AF_INET_, SOCK_DGRAM_, 0)
		Self.LocalPort   = 0
		Self.RemotePort  = 0
		Self.RemoteIP    = 0
		Self.ReadTimeout = 0
	End Method

	Method Delete()
		Self.Close()
	End Method

	Method SetLocalPort:Int(Port:Short=0)
		Local Address:TSockAddr, NameLen:Int

		If Self.Socket = INVALID_SOCKET_ Then Return False

		If bind_(Self.Socket, AF_INET_, Port) = SOCKET_ERROR_ Then
			Return False
		Else
			Address = New TSockAddr
			NameLen = 16
			If getsockname_(Self.Socket, Address, Varptr(NameLen)) = SOCKET_ERROR_ Then
				Return False
			Else
				Self.LocalPort = ntohs_(Address.SinPort)
				Return True
			EndIf
		EndIf
	End Method
	
	Method GetLocalPort:Short()
		Return Self.LocalPort
	End Method
	
	Method SetRemotePort(Port:Short)
		Self.RemotePort = Port
	End Method
	
	Method GetRemotePort:Short()
		Return Self.RemotePort
	End Method
	
	Method SetRemoteIP(IP:Int)
		Self.RemoteIP = IP
	End Method
	
	Method GetRemoteIP:Int()
		Return Self.RemoteIP
	End Method

	Method GetMessagePort:Short()
		Return Self.MessagePort
	End Method

	Method GetMessageIP:Int()
		Return Self.MessageIP
	End Method

	Method SetReadTimeout(ReadMillisecs:Int)
		Self.ReadTimeout = ReadMillisecs
	End Method

	Method GetReadTimeout:Int()
		Return Self.ReadTimeout
	End Method

	Method Read:Int(Buffer:Byte Ptr, Size:Int)
		Local Result:Int, MessagePort:Int

		' If Socket does not exist
		If Self.Socket = INVALID_SOCKET_ Then Return 0

		' Try to receive
		Result = recvfrom_(Self.Socket, Buffer, Size, 0, Self.MessageIP, ..
		                   MessagePort)
		
		' Mark, Port is Short not Int!!!!
		Self.MessagePort = Short(MessagePort)

		If Result = SOCKET_ERROR_ Then
			Return Size
		Else
			Return Result
		EndIf
	End Method

	Method Write:Int(Buffer:Byte Ptr, Size:Int)
		Local Result:Int

		' If Socket does not exist
		If Self.Socket = INVALID_SOCKET_ Then Return 0
		
		' Try to send
		Result = sendto_(Self.Socket, Buffer, Size, 0, Self.RemoteIP, Self.RemotePort)
		If Result = SOCKET_ERROR_ Then
			Return 0
		Else
			Return Result
		EndIf
	End Method

	Method Eof:Int()
		Local Read:Int

		' If socket does not exist
		If Self.Socket = INVALID_SOCKET_ Then
			Return True
		Else
			' Data can read?
			Read = Self.Socket
			If (select_(1, Varptr(Read), 0, Null, 0, Null, Self.ReadTimeout) ..
			   = SOCKET_ERROR_) Or (Self.ReadAvail() = 0) Then

				Return True
			Else
				Return False
			EndIf
		EndIf
	End Method

	Method Size:Int()
		Return Self.ReadAvail()
	End Method
	
	Method Close()
		If Self.Socket &lt;&gt; INVALID_SOCKET_ Then
			shutdown_(Self.Socket, 2)
			closesocket_(Self.Socket)
			
			Self.Socket = INVALID_SOCKET_
		EndIf
	End Method

	Method ReadAvail:Int()
		Local Size:Int
		
		If Self.Socket = INVALID_SOCKET_ Then Return 0

		If ioctl_(Self.Socket, FIONREAD, Varptr(Size)) = SOCKET_ERROR_ Then
			Return 0
		Else
			Return Size
		EndIf
	End Method
End Type
</textarea><br><br>you would for example send a simple message to a specific ip, you can do this with:<br><pre class=code>Global Stream:TUDPStream

Stream = New TUDPStream
Stream.SetLocalPort(4321)

Stream.SetRemotePort(1234)
Stream.SetRemoteIP(TNetwork.IntIP("127.0.0.1"))

Stream.WriteLine("Hello World")

Stream.Close()
End</pre><br><br>But if you would receive a message like:<br><pre class=code>Global Stream:TUDPStream

Stream = New TUDPStream
Stream.SetLocalPort(1234)

While Not KeyDown(KEY_ESCAPE)
   If Stream.ReadAvail() Then
      While Not Stream.Eof()
         Print "&gt;"+Stream.ReadLine()
      Wend

      Stream.SetRemoteIP(Stream.GetMessageIP())
      Stream.SetRemotePort(Stream.GetMessagePort())
      Stream.WriteLine("OK")
   EndIf
Wend

Stream.Close()
End</pre><br><br>It is not work, becouse ReadLine reads byte by byte and not the full databuffer. GetMessageIP/Port does work, but only one byte are analysted complete.<br><br>Any idea how do solve this?<br><br>cu olli <br><br></td></tr></table><br>
<a name="564241"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gehhilfe</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Vertex,<br>i have a idea using banks to buffer the data<br>and the sending the bank with a extra Method.<br><br>See you Tim <br><br></td></tr></table><br>
<a name="564289"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Vertex</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Gehilfe: Hmm, yes, that was what Blitz3D do with RecvUDPMsg. RecvUDPMsg reads all I think in a internal Blitz buffer and ReadLine and so on reads out of this buffer. But it must can do with recvfrom only. A TCP Socket can do this. I must test setsockop with a peek parameter, but I think, this does only work with windows.<br><br>cu olli <br><br></td></tr></table><br>
<a name="564384"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tibit</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm sorry but I don't get the problem. As far as I know, RecvFrom works for me? And I don't buffer. With UDP you get a message or not? <br><br>I think I've missed something but why would you ever, want to not read the whole UDP message?<br><br>Please explain more, I can probably help you then. <br><br></td></tr></table><br>
<a name="564433"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Vertex</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> "Hello World" consist of 13(String+Chr(13)+Chr(10) ) Bytes.<br><br>From brl.mod\stream.mod\stream.bmx:<br><pre class=code>	Method ReadLine$()
		Local str$,buf:Byte[1024],p
		Repeat
			Local n:Byte
			If Read( Varptr n,1 )&lt;&gt;1 Exit
			If n=0 Exit
			If n=10 Exit
			If n=13 Continue
			buf[p]=n ; p:+1
			If p&lt;&gt;buf.length Continue
			str:+String.FromBytes(buf,p)
			p=0
		Forever
		If p str:+String.FromBytes(buf,p)
		Return str
	End Method</pre><br><br>You see, ReadLine want to read on this example 13 Bytes individually. One Byte need 1 call of recvfrom.<br><br>The networkbuffer consist of the 13 bytes. If you call recvfrom with 1 Byte size, than recvfrom returns SOCKET_ERROR and WSAGetlastError returns 10040 -&gt; WSAEMSGSIZE.<br><br><a href="http://msdn.microsoft.com/library/default.asp?url=/library/en-us/winsock/winsock/recvfrom_2.asp" target="_blank">http://msdn.microsoft.com/library/default.asp?url=/library/en-us/winsock/winsock/recvfrom_2.asp</a><br><br>WSAEMSGSIZE:<br><div class="quote"> The message was too large to fit into the specified buffer and was truncated. <br></div><br><br><div class="quote"> A message sent on a datagram socket was larger than the internal message buffer or some other network limit, or the buffer used to receive a datagram was smaller than the datagram itself. <br></div><br><br>Datagram Socket = UDP Socket<br><br>So 1 Byte is read, and the other 12 Bytes are deleted from the networkbuffer. A second call of recvfrom will fall.<br><br>cu olli <br><br></td></tr></table><br>
<a name="564529"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gehhilfe</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Vertex my Version with Buffering is running well.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Private

Type TSockAddr
   Field SinFamily : Short
   Field SinPort   : Short
   Field SinAddr   : Int
   Field SinZero   : Long
End Type

Extern "OS"
   Const INVALID_SOCKET_ = -1

   ?Win32
      Const FIONREAD = $4004667F
      Function ioctl_:Int(Socket:Int, Command:Int, Arguments:Byte Ptr) = "ioctlsocket@12"
      Function inet_addr_:Int(Address$z) = "inet_addr@4"
      Function inet_ntoa_:Byte Ptr(Adress:Int) = "inet_ntoa@4"
      Function getsockname_:Int(Socket:Int, Name:Byte Ptr, NameLen:Int Ptr) = "getsockname@12"

   ?MacOS
      Const FIONREAD = $4004667F
      Function ioctl_(Socket:Int, Command:Int, Arguments:Byte Ptr) = "ioctl"
      Function inet_addr_:Int(Address$z) = "inet_addr"
      Function inet_ntoa_:Byte Ptr(Adress:Int) = "inet_ntoa"
      Function getsockname_:Int(Socket:Int, Name:Byte Ptr, NameLen:Int Ptr) = "getsockname"

   ?Linux
      Const FIONREAD = $0000541B
      Function ioctl_(Socket:Int, Command:Int, Arguments:Byte Ptr) = "ioctl"
      Function inet_addr_:Int(Address$z) = "inet_addr"
      Function inet_ntoa_:Byte Ptr(Adress:Int) = "inet_ntoa"
      Function getsockname_:Int(Socket:Int, Name:Byte Ptr, NameLen:Int Ptr) = "getsockname"
   ?
End Extern

Public

Type TNetwork
   Function DottedIP:String(IP:Int)
      Return String.FromCString(inet_ntoa_(htonl_(IP)))
   End Function
   
   Function IntIP:Int(IP:String)
      Return htonl_(inet_addr_(IP))
   End Function
End Type

Type TUDPStream Extends TStream
   Field Socket      : Int
   Field LocalPort   : Short
   Field RemotePort  : Short
   Field RemoteIP    : Int
   Field MessageIP   : Int
   Field MessagePort : Short
   Field ReadTimeout : Int
   Field RecvBuffer  : TBank
   Field SendBuffer  : TBank
   Field SendBufferSize : Short
   Field RecvBufferSize : Short
   Field ReadBufferSize : Short

   Method New()
      Self.Socket      = socket_(AF_INET_, SOCK_DGRAM_, 0)
      Self.LocalPort   = 0
      Self.RemotePort  = 0
      Self.RemoteIP    = 0
      Self.ReadTimeout = 0
	  Self.RecvBuffer = CreateBank ( 1024 )
	  Self.SendBuffer = CreateBank ( 1024 )
   End Method

   Method Delete()
      Self.Close()
   End Method

   Method SetLocalPort:Int(Port:Short=0)
      Local Address:TSockAddr, NameLen:Int

      If Self.Socket = INVALID_SOCKET_ Then Return False

      If bind_(Self.Socket, AF_INET_, Port) = SOCKET_ERROR_ Then
         Return False
      Else
         Address = New TSockAddr
         NameLen = 16
         If getsockname_(Self.Socket, Address, Varptr(NameLen)) = SOCKET_ERROR_ Then
            Return False
         Else
            Self.LocalPort = ntohs_(Address.SinPort)
            Return True
         EndIf
      EndIf
   End Method
   
   Method GetLocalPort:Short()
      Return Self.LocalPort
   End Method
   
   Method SetRemotePort(Port:Short)
      Self.RemotePort = Port
   End Method
   
   Method GetRemotePort:Short()
      Return Self.RemotePort
   End Method
   
   Method SetRemoteIP(IP:Int)
      Self.RemoteIP = IP
   End Method
   
   Method GetRemoteIP:Int()
      Return Self.RemoteIP
   End Method

   Method GetMessagePort:Short()
      Return Self.MessagePort
   End Method

   Method GetMessageIP:Int()
      Return Self.MessageIP
   End Method

   Method SetReadTimeout(ReadMillisecs:Int)
      Self.ReadTimeout = ReadMillisecs
   End Method

   Method GetReadTimeout:Int()
      Return Self.ReadTimeout
   End Method

	Method Send:Int()
		'Bank aus lessen und Senden und danach lerren.
		Local Result:Int
		
		Result = sendto_ ( Self.Socket,  Self.SendBuffer.buf(), Self.SendBufferSize, 0, Self.RemoteIP, Self.RemotePort )
		
		If Result = SOCKET_ERROR_ Then Return False Else Return True
		
		Self.SendBufferSize = 0
		
	End Method

	Method Recv:Int()

		Local size:Int = Self.ReadAvail()
		Local Result:Int, MessagePort:Int
		
		If size = 0 Then Return False

		Result = recvfrom_ ( Self.Socket, Self.RecvBuffer.buf(), 1024, 0, Self.MessageIP, MessagePort ) 
		
		Self.MessagePort = Short(MessagePort)
		
		Self.RecvBufferSize = 0
		
		Self.ReadBufferSize = Size
		
		Return True

	End Method
	
	Method Read:Int (Buffer:Byte Ptr, Size:Int)

		Print "Reading"

		Local BBuffer:Byte Ptr = Self.RecvBuffer.buf()
		
		If Self.Socket = INVALID_SOCKET_ Then Return False
		
		'Set Offset in Bank
		BBuffer:+ Self.RecvBufferSize
		
		'Copy Data
		MemCopy Buffer, BBuffer, Size
		
		Self.RecvBufferSize:+ Size
		
		Return True
	
	End Method
	
	Method Write:Int (Buffer:Byte Ptr, Size:Int)
		
		Print "Writing"
		
		Local BBuffer:Byte Ptr = Self.SendBuffer.buf()
		
		If Self.Socket = INVALID_SOCKET_ Then Return False

		BBuffer:+ Self.SendBufferSize
		
		MemCopy BBuffer, Buffer, Size
		
		Self.SendBufferSize:+ Size
		
		Return True

	End Method
   
	Method Eof:Int()
      Local Read:Int

      ' If socket does not exist
      	If Self.Socket = INVALID_SOCKET_ Then
         	Return True
		End If
		
		If RecvBufferSize &gt;= ReadBufferSize Then 
			Return True
		Else
			Return False
		End If

  	 End Method

   Method Size:Int()
      Return Self.ReadAvail()
   End Method
   
   Method Close()
      If Self.Socket &lt;&gt; INVALID_SOCKET_ Then
         shutdown_(Self.Socket, 2)
         closesocket_(Self.Socket)
         
         Self.Socket = INVALID_SOCKET_
      EndIf
   End Method

   Method ReadAvail:Int()
      Local Size:Int
      
      If Self.Socket = INVALID_SOCKET_ Then Return 0

      If ioctl_(Self.Socket, FIONREAD, Varptr(Size)) = SOCKET_ERROR_ Then
         Return 0
      Else
         Return Size
      EndIf
   End Method
End Type
</textarea><br><br>And some Sampels<br><br><pre class=code>
Const Port = 1234

Global Stream:TUDPStream

Stream = New TUDPStream
If Not Stream.SetLocalPort(Port) Then RuntimeError "Server can not bind to port: "+ Port

Repeat
	
	Delay 200
		
	If Stream.Recv() 
		While Not Stream.eof()
			Print "New Byte:" +	Stream.ReadByte()
		Wend
	EndIf
	
	FlushMem()	

Forever
</pre><br><br><pre class=code>
Const Port = 5200

Global Stream:TUDPStream

Stream = New TUDPStream
If Not Stream.SetLocalPort(Port) Then RuntimeError "Server can not bind to port: "+ Port

Stream.SetRemoteIP TNetwork.IntIP("127.0.0.1")
Stream.SetRemotePort 1234

Stream.WriteByte(33)
Stream.WriteByte(33)
Stream.WriteByte(33)
Stream.WriteByte(33)
Stream.WriteByte(33)
Stream.WriteByte(33)
Stream.WriteByte(33)

Stream.Send()
</pre> <br><br></td></tr></table><br>
<a name="566369"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tom</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I still don't get what you're on about. I'm not using streams, and as Wave said, sendTo() &amp; recvFrom() are working fine for me too.<br><br>If there's a problem with sockets when using the additional code I posted, can you show us a simple example?<br><br>Thanks<br>Tom <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
