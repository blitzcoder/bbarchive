<!DOCTYPE html><html lang="en" ><head ><title >brl.retro docs</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >brl.retro docs</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=110" >BlitzMax Module Tweaks</a>/<a href="#bottom" >brl.retro docs</a><br><br>
<a name="1037450"></a>

<a name="1221945"></a>

<a name="1222183"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is an update to the brl.retro docs. The examples are based on the Blitz3D docs.<br><br>The files are zipped in a doc folder (module source not included according to the BSSC license).<br>retro-doc.zip (expired)<br><br>I'm posting the module source (from v 1.41) and the patch.<br><br>Last edit: 16 Aug 2010<br><br>retro.diff<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">--- retro-old.bmx	2010-07-13 03:55:00.000000000 +0100
+++ retro.bmx	2010-08-16 05:33:03.392421104 +0100
@@ -23,12 +23,10 @@
 bbdoc: Extract substring from a string
 returns: A sequence of characters from @str starting at position @pos and of length @size
 about:
-The Mid$ command returns a substring of a String.
-
-Given an existing string, a @position from the start of the string and
-an optional @size, #Mid creates a new string equal to the section specified.
-If no size if given, #Mid returns the characters in the existing string from
-@position to the end of the string.
+Given an existing string, a position from the start of the string and 
+an optional @size, #Mid creates a new string equal to the section specified. 
+If no size is given, #Mid returns the characters in the existing string from 
+@pos to the end of the string.
 
 For compatibility with classic BASIC, the @pos parameter is 'one based'.
 End Rem
@@ -45,22 +43,20 @@
 bbdoc: Find a string within a string
 returns: The position within @str of the first matching occurance of @sub
 about:
-The @start parameter allows you to specifying a starting index for the search.
+The @start parameter allows you to specify a start index for the search.
 
-For compatiblity with classic BASIC, the @start parameter and returned position
-are both 'one based'.
+For compatibility with classic BASIC, @start and the returned position are both 'one based'.
 End Rem
 Function Instr( str$,sub$,start=1 )
 	Return str.Find( sub,start-1 )+1
 End Function
 
 Rem
-bbdoc: Extract characters from the beginning of a string
-returns: @size leftmost characers of @str
+bbdoc: Extract substring from the start of a string
+returns: The @n leftmost characters of @str
 about:
-The Left$ command returns a substring of a String.
-Given an existing String and a @size, Left$ returns the first @size
-characters from the start of the String in a new String.
+Given an existing string and a size, #Left returns a new string containing the first @n 
+characters from the start of the string.
 End Rem
 Function Left$( str$,n )
 	If n&gt;Len(str) n=Len(str)
@@ -68,12 +64,11 @@
 End Function
 
 Rem
-bbdoc: Extract characters from the end of a string
-returns: @size rightmost characters of @str
+bbdoc: Extract substring from the end of a string
+returns: The @n rightmost characters of @str
 about:
-The Right$ command returns a substring of a String.
-Given an existing String and a @size, Right$ returns the last @size
-characters from the end of the String.
+Given an existing string and a size, #Right returns a new string containing the last @n 
+characters from the end of the string.
 End Rem
 Function Right$( str$,n )
 	If n&gt;Len(str) n=Len(str)
@@ -83,7 +78,7 @@
 Rem
 bbdoc: Left justify string
 returns: A string of length @n, padded with spaces
-endrem
+EndRem
 Function LSet$( str$,n )
 	Return str[..n]
 End Function
@@ -91,24 +86,24 @@
 Rem
 bbdoc: Right justify string
 returns: A string of length @n, padded with spaces
-endrem
+EndRem
 Function RSet$( str$,n )
 	Return str[Len(str)-n..]
 End Function
 
 Rem
-bbdoc: Performs a search and replace function
-returns: A string with all instances of @sub$ replaced by @replace$
+bbdoc: Perform a search and replace function
+returns: A string with all instances of @sub replaced by @replaceWith
 about:
-The Replace$ command replaces all instances of one string with another.
+Replaces all instances of one string with another.
 End Rem
 Function Replace$( str$,sub$,replaceWith$ )
 	Return str.Replace( sub,replaceWith )
 End Function
 
 Rem
-bbdoc: Remove unprintable characters from ends a string
-returns: @str with leading and trailing unprintable characters removed
+bbdoc: Remove unprintable characters from both ends of a string
+returns: A string with leading and trailing unprintable characters removed
 End Rem
 Function Trim$( str$ )
 	Return str.Trim()
</textarea><br><br>retro.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Rem
bbdoc: BASIC/BASIC compatibility
End Rem
Module BRL.Retro

ModuleInfo "Version: 1.09"
ModuleInfo "Author: Mark Sibly"
ModuleInfo "License: Blitz Shared Source Code"
ModuleInfo "Copyright: Blitz Research Ltd"
ModuleInfo "Modserver: BRL"

ModuleInfo "History: 1.09 Release"
ModuleInfo "History: Cleaned up Mid$"
ModuleInfo "History: 1.08 Release"
ModuleInfo "History: Deleted network stuff"

Import BRL.Basic

Rem
bbdoc: Extract substring from a string
returns: A sequence of characters from @str starting at position @pos and of length @size
about:
Given an existing string, a position from the start of the string and 
an optional @size, #Mid creates a new string equal to the section specified. 
If no size is given, #Mid returns the characters in the existing string from 
@pos to the end of the string.

For compatibility with classic BASIC, the @pos parameter is 'one based'.
End Rem
Function Mid$( str$,pos,size=-1 )
	If pos&gt;Len( str ) Return Null
	pos:-1
	If( size&lt;0 ) Return str[pos..]
	If pos&lt;0 size=size+pos;pos=0
	If pos+size&gt;Len( str ) size=Len( str )-pos
	Return str[pos..pos+size]
End Function

Rem
bbdoc: Find a string within a string
returns: The position within @str of the first matching occurance of @sub
about:
The @start parameter allows you to specify a start index for the search.

For compatibility with classic BASIC, @start and the returned position are both 'one based'.
End Rem
Function Instr( str$,sub$,start=1 )
	Return str.Find( sub,start-1 )+1
End Function

Rem
bbdoc: Extract substring from the start of a string
returns: The @n leftmost characters of @str
about:
Given an existing string and a size, #Left returns a new string containing the first @n 
characters from the start of the string.
End Rem
Function Left$( str$,n )
	If n&gt;Len(str) n=Len(str)
	Return str[..n]
End Function

Rem
bbdoc: Extract substring from the end of a string
returns: The @n rightmost characters of @str
about:
Given an existing string and a size, #Right returns a new string containing the last @n 
characters from the end of the string.
End Rem
Function Right$( str$,n )
	If n&gt;Len(str) n=Len(str)
	Return str[Len(str)-n..]
End Function

Rem
bbdoc: Left justify string
returns: A string of length @n, padded with spaces
EndRem
Function LSet$( str$,n )
	Return str[..n]
End Function

Rem
bbdoc: Right justify string
returns: A string of length @n, padded with spaces
EndRem
Function RSet$( str$,n )
	Return str[Len(str)-n..]
End Function

Rem
bbdoc: Perform a search and replace function
returns: A string with all instances of @sub replaced by @replaceWith
about:
Replaces all instances of one string with another.
End Rem
Function Replace$( str$,sub$,replaceWith$ )
	Return str.Replace( sub,replaceWith )
End Function

Rem
bbdoc: Remove unprintable characters from both ends of a string
returns: A string with leading and trailing unprintable characters removed
End Rem
Function Trim$( str$ )
	Return str.Trim()
End Function

Rem
bbdoc: Convert string to lowercase
returns: Lowercase equivalent of @str
End Rem
Function Lower$( str$ )
	Return str.ToLower()
End Function

Rem
bbdoc: Convert string to uppercase
returns: Uppercase equivalent of @str
End Rem
Function Upper$( str$ )
	Return str.ToUpper()
End Function

Rem
bbdoc: Convert an integer value to a hexadecimal string
returns: The hexadecimal string representation of @val
End Rem
Function Hex$( val )
	Local buf:Short[8]
	For Local k=7 To 0 Step -1
		Local n=(val&amp;15)+Asc("0")
		If n&gt;Asc("9") n=n+(Asc("A")-Asc("9")-1)
		buf[k]=n
		val:Shr 4
	Next
	Return String.FromShorts( buf,8 )
End Function

Rem
bbdoc: Convert an integer value to a binary string
returns: The binary string representation of @val
End Rem
Function Bin$( val )
	Local buf:Short[32]
	For Local k=31 To 0 Step -1
		buf[k]=(val&amp;1)+Asc("0")
		val:Shr 1
	Next
	Return String.FromShorts( buf,32 )
End Function

Rem 
bbdoc: Convert a 64 bit long integer value to a hexadecimal string 
returns: The hexadecimal string representation of @val 
End Rem 
Function LongHex$( val:Long ) 
	Return Hex$( val Shr 32 )+Hex$( val ) 
End Function 

Rem 
bbdoc: Convert a 64 bit long integer value to a binary string 
returns: The binary string representation of @val 
End Rem 
Function LongBin$( val:Long ) 
	Return Bin$( val Shr 32 )+Bin$( val ) 
End Function 
</textarea><br><br>And the zip contents below.<br><br>intro.bbdoc<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
The BASIC compatibility module provides miscellaneous functions that emulate the behaviour
of 'classic' BASIC.

The functions in this module have largely been superceded by BlitzMax features such as 
'string slicing', and the Find, Replace, Trim, ToLower and ToUpper string methods.

However, for programmers from a classic BASIC background, these utility functions should make
the transition to BlitzMax a little easier.

NOTE: Strings in classic BASIC are '1 based'. This means that the first character within a 
string is at index 1, the second at index 2 and so on. However, BlitzMax strings are '0 based',
meaning the first character is at index 0, the second at index 1 and so on. The #Instr and
#Mid functions in this module retain the '1 based' behaviour of classic BASIC.
</textarea><br><br>bin.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' bin.bmx

num=64738

Print "binary string of "+num+" is: "+Bin( num )
</textarea><br><br>hex.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' hex.bmx

num=64738

Print "hexidecimal string of "+num+" is: "+Hex( num )
</textarea><br><br>instr.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' instr.bmx

str$="Hello World!"

Print "contains 'Wo' at position: "+Instr( str,"Wo",1 )
</textarea><br><br>left.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' left.bmx

str$="Hello World!"

Print "the left 5 letters are: '"+Left( str,5 )+"'"
</textarea><br><br>lower.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' lower.bmx

str$="Hello World!"

Print "to lowercase: '"+Lower( str )+"'"
</textarea><br><br>lset.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' lset.bmx

str$="Hello World!"

Print "left padded: '"+LSet( str,Len( str )+5 )+"'"
</textarea><br><br>mid.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' mid.bmx

str$="Hello World!"

For n=1 To Len( str )
	Print Mid( str,n,Len( str )-n+1 )
Next
</textarea><br><br>replace.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' replace.bmx

str$="Hello World!"

Print "with 'll' replaced: '"+Replace( str,"ll","dd" )+"'"
</textarea><br><br>right.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' right.bmx

str$="Hello World!"

Print "the right 6 letters are: '"+Right( str,6 )+"'"
</textarea><br><br>rset.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' rset.bmx

str$="Hello World!"

Print "right padded: '"+RSet( str,Len( str )+5 )+"'"
</textarea><br><br>trim.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' trim.bmx

str$="  Hello World!  "

Print "trimmed: '"+Trim( str )+"'"
</textarea><br><br>upper.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' upper.bmx

str$="Hello World!"

Print "to uppercase: '"+Upper( str )+"'"
</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
