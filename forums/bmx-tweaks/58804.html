<!DOCTYPE html><html lang="en" ><head ><title >[MAXGUI] "About" window module?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >[MAXGUI] "About" window module?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=110" >BlitzMax Module Tweaks</a>/<a href="#bottom" >[MAXGUI] "About" window module?</a><br><br>
<a name="654295"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi!<br><br>Has anyone written a proper "about" window module yet?<br>Such as the ones used in Firefox or Thunderbird:<br><br><img src="http://img100.imageshack.us/img100/5226/d4zp.png"><br><img src="http://img112.imageshack.us/img112/3568/bild21yc.png"><br><br>Version, homepage link ect on the one "side".<br>Scrolling credits on the other "side".<br><br>Anything that is better than this when I click "About" in the BMX GUI:<br><img src="http://img112.imageshack.us/img112/3342/bild49mh.png"> <br><br></td></tr></table><br>
<a name="654508"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >klepto2</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Do you mean something like this? <br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Type TAboutBox
	Field Window:Tgadget
	Field Buttons:TAboutButton[2]
	Field RealButtons:TGadget[2]
	Field ActiveButton:TAboutButton
	Field ExitButton:TGadget
	Field Canvas:TGadget
	Field Timer:TTimer
	
	Field Name:String
	Field PosX:Int
	Field PosY:Int 
	Field Width:Int
	Field Height:Int
	
	Field AlwaysOnTop:Byte = False
	
	Function eventhook:Object(id:Int,data:Object,context:Object)
		If TAboutBox(context) TAboutBox(context).ev TEvent(data);Return data	
	EndFunction
	
	Method New()
		AddHook EmitEventHook,eventhook,Self
	End Method
	
	Function Create:TAboutBox(Name:String , X:Int , Y:Int , Width:Int , Height:Int)
		Local A:TAboutBox = New TAboutBox
		A.Name = Name
		A.PosX = X
		A.PosY = Y
		A.Width = Width
		A.Height = Height
		
		A.Window = CreateWindow(Name , X , Y , Width , Height,,WINDOW_TITLEBAR)
		A.Canvas = CreateCanvas(0 , 0 , ClientWidth(A.Window) , ClientHeight(A.Window) - 50 , A.Window)
		A.ExitButton = CreateButton("Exit",ClientWidth(A.Window)-120 , ClientHeight(A.Window) - 40 , 80 , 30 , A.Window)
		
		A.Timer = CreateTimer(60)
		HideGadget(A.Window)
		
		Return A
	End Function
	
	Method AddButton(Name:String,Style:TTextStyle , Index:Int)
		If Index &gt; 2 And Index &lt; 1 Then 
			Throw "Index must be in 1 or 2"
		Else
			Local B:TAboutButton = TaboutButton.Create(Name,Style)
			B.Canvas = Canvas
			B.Index = Index
			Style.Parent = B
			Buttons[Index-1] = B
			RealButtons[Index - 1] = CreateButton(B.Name , 40 , ClientHeight(Window) - 40 , 80 , 30 , Window) 
			Select Index
				Case 1
					ShowGadget(RealButtons[Index - 1])
					ActiveButton = Buttons[Index-1]
				Default
					HideGadget(RealButtons[Index - 1])
			End Select
			
		EndIf
	End Method
	
	Method ev(event:TEvent)
			If event.id = EVENT_GADGETPAINT Then 
				If ActiveButton Then
					ActiveButton.Style.Update()
				EndIf
			EndIf
			
			If event.id = EVENT_TimerTick And Event.source = Timer Then
				If Canvas.parent &lt;&gt; Null Then RedrawGadget canvas
			EndIf
			
			If event.id = EVENT_WINDOWCLOSE And event.Source = Window
					Kill()
			End If			
			If event.id = EVENT_GADGETACTION
				If event.source = ExitButton Then 
					Kill()
				EndIf
				If Event.Source = RealButtons[0] Then
					ActiveButton = Buttons[1]
					HideGadget(RealButtons[0])
					ShowGadget(RealButtons[1])
				ElseIf Event.Source = RealButtons[1] Then
					ActiveButton = Buttons[0]
					HideGadget(RealButtons[1])
					ShowGadget(RealButtons[0])
				EndIf
			EndIf

	End Method
	
	Method Kill()
		HideGadget(Window)
		ShowGadget(RealButtons[0])
		ActiveButton = Buttons[0]
		HideGadget(RealButtons[1])

	End Method
	
	Method Show()
		ShowGadget(Window)
	End Method

End Type

Type TTextStyle
	Field Index
	Field Text:Tlist = New TList
	Field Background:Timage
	Field isBAckground:Byte = False
	Field CenterAllign:Byte = True
	Field LeftAllign:Byte = False
	Field RightAllign:Byte = False
	Field Parent:TAboutButton
	
	Method AddText(Text:String) 
		self.Text.Addlast(Text)
	End Method
	
	Method SetBackGround(Path:String)
		Background = LoadImage(Path)
		If Background Then isBackground = True
	End Method
	
	Method SetAllign(Center:Byte = True , _Left:Byte = False , _Right:Byte = False)
			If Center = True Then 
				CenterAllign = True
				LeftAllign = False
				RightAllign = False
				Return
			EndIf
			If _Left = True Then 
				CenterAllign = False
				LeftAllign = True
				RightAllign = False
				Return
			EndIf	
			If _Right = True Then 
				CenterAllign = False
				LeftAllign = False
				RightAllign = True
				Return
			EndIf
	End Method
	
	Method Update()
	End Method
End Type

Type TStaticText Extends TTextStyle
	
	Function Create:TStaticText()
		Local S:TStaticText = New TStaticText
		Return S
	End Function
	
	Method Update()
		SetGraphics CanvasGraphics(parent.canvas)
		Cls
		If CenterAllign = True Then
			If isBackground = True Then
				DrawImageRect BAckGround , 0 , 0 , ClientWidth(Parent.Canvas) , ClientHeight(Parent.Canvas)
			EndIf
			
			Local Row:Int = 0
			Local Width:Int = ClientWidth(Parent.Canvas) 
			
			For Local T:String = EachIn Text
				DrawText T , (Float(Width)/2.0)-(TextWidth(T)/2) , 20 * Row
				Row:+ 1
			Next
			
		EndIf
		
		If LeftAllign = True Then
			If isBackground = True Then
				DrawImageRect BackGround , 0 , 0 , ClientWidth(Parent.Canvas) , ClientHeight(Parent.Canvas)
			EndIf
			
			Local Row:Int = 0
			Local Width:Int = ClientWidth(Parent.Canvas) 
			
			For Local T:String = EachIn Text
				DrawText T ,0 , 20 * Row
				Row:+ 1
			Next
			
		EndIf
		
		If RightAllign = True Then
			If isBackground = True Then
				DrawImageRect BAckGround , 0 , 0 , ClientWidth(Parent.Canvas) , ClientHeight(Parent.Canvas)
			EndIf
			
			Local Row:Int = 0
			Local Width:Int = ClientWidth(Parent.Canvas) 
			
			For Local T:String = EachIn Text
				DrawText T , Width-TextWidth(T) , 20 * Row
				Row:+ 1
			Next
			
		EndIf


		
		'Print "Test"
		
		Flip
	End Method
	
End Type

Type TAboutButton
	Field Canvas:TGadget
	Field Name:String
	Field Style:TTextStyle
	Field Index:Int
	
	Function Create:TAboutButton(Name:String , Style:TTextStyle )
		
		Local B:TAboutButton = New TAboutButton
		B.Name = Name
		B.Style = Style
		  
		
		Return B
	End Function
	
End Type



' createmenu.bmx
' Creating the About Box

Local About:TAboutBox = TAboutBox.Create("About" , 20 , 20 , 400 , 400)

Local TS:TStaticText = TStaticText.Create()
TS.AddText("Test1")
TS.AddText("Test1")
TS.SetAllign(False,False,True)

About.AddButton("&lt;&lt; Test2" , TS , 1)

Local TS2:TStaticText = TStaticText.Create()
TS2.SetBackGround("D:\Back.JPG")
TS2.AddText("Test2")
TS2.AddText("Test2")
TS2.SetAllign(True , False , False)

About.AddButton("&gt;&gt; Test1" , TS2 , 2)

					


Local window:TGadget
Local filemenu:TGadget
Local editmenu:TGadget
Local helpmenu:TGadget

Const MENU_NEW=101
Const MENU_OPEN=102
Const MENU_SAVE=103
Const MENU_CLOSE=104
Const MENU_EXIT=105

Const MENU_CUT=106
Const MENU_COPY=107
Const MENU_PASTE=108

Const MENU_ABOUT=109

window=CreateWindow("My Window",40,40,320,240)

filemenu=CreateMenu("&amp;File",0,WindowMenu(window))
CreateMenu"&amp;New",MENU_NEW,filemenu,KEY_N,MODIFIER_COMMAND
CreateMenu"&amp;Open",MENU_OPEN,filemenu,KEY_O,MODIFIER_COMMAND
CreateMenu"&amp;Close",MENU_CLOSE,filemenu,KEY_W,MODIFIER_COMMAND
CreateMenu"",0,filemenu
CreateMenu"&amp;Save",MENU_SAVE,filemenu,KEY_S,MODIFIER_COMMAND
CreateMenu"",0,filemenu
CreateMenu"E&amp;xit",MENU_EXIT,filemenu,KEY_F4,MODIFIER_COMMAND

editmenu=CreateMenu("&amp;Edit",0,WindowMenu(window))
CreateMenu "Cu&amp;t",MENU_CUT,editmenu,KEY_X,MODIFIER_COMMAND
CreateMenu "&amp;Copy",MENU_COPY,editmenu,KEY_C,MODIFIER_COMMAND
CreateMenu "&amp;Paste",MENU_PASTE,editmenu,KEY_V,MODIFIER_COMMAND

helpmenu=CreateMenu("&amp;Help",0,WindowMenu(window))
CreateMenu "&amp;About",MENU_ABOUT,helpmenu

UpdateWindowMenu window

While True
	WaitEvent 
	Select EventID()
		Case EVENT_WINDOWCLOSE
			End
		Case EVENT_MENUACTION
			Select EventData()
				Case MENU_EXIT
					End
				Case MENU_ABOUT
					About.Show()
					
			End Select
	End Select
Wend
</textarea><br><br>The TTextStyle is extendable as you see by the TStaticText<br>So it should be easy to add different Effects like scrolling, fading etc.<br>It is a short write and bug maybe included. but it should give you a hint how to do it. Also things like textColor etc. are not implemented yet.<br><br>Enjoy ;) <br><br></td></tr></table><br>
<a name="654867"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Klepto.<br><br>Will have a look.<br><br>P.S.: Is this EmitEventHook stuff portable to MAC+Linux? <br><br></td></tr></table><br>
<a name="655502"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dreamora</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yepp thats BMs own event code stuff from brl.event etc <br><br></td></tr></table><br>
<a name="655578"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Another problem with the example.<br>When I click the close button, not only the about window shuts down, but also the WHOLE application.<br>How can I prevent that?<br><br>Also, can I set a window to ALWAYSONTOP with MAXGUI commands?<br>So the about window stays over the apps window... <br><br></td></tr></table><br>
<a name="655579"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Change the <pre class=code>		Case EVENT_WINDOWCLOSE
			End</pre> to this: <pre class=code>		Case EVENT_WINDOWCLOSE
			If EventSource() = About.Window Then About.Kill() Else End</pre>*EDIT: Just to explain: this will check to see if the event was triggered by the About Box window, if it was then the about box is "Killed", otherwise (i.e. it was triggered by the main window) it ends the program.<br><br>Also I think there was an "AlwaysOnTop" flag in the Type declaration but I don't think it works. :(<br><br><br>Seb <br><br></td></tr></table><br>
<a name="655598"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Seb! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
