<!DOCTYPE html><html lang="en" ><head ><title >[BRL.GLMax2D] Heavy modifications, more open</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >[BRL.GLMax2D] Heavy modifications, more open</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=110" >BlitzMax Module Tweaks</a>/<a href="#bottom" >[BRL.GLMax2D] Heavy modifications, more open</a><br><br>
<a name="964673"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >plash</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> <i>(Proposed release)</i><br><br>The module has been turned to the awesome side of SuperStrict, and all the private code has been stuffed into a type (_TGLMax2DAssistant), so that any variables (states, transform variables) or functions (specifically DisableTex and EnableTex - previously, I had to use a hack to get around the resetting of the last bound texture) are open for use outside of the module.<br><br>There never was an official response to <a href="/posts.php?topic=83506" target="_blank"><u>my thread on Private access</u></a>, so I went ahead and modified this.<br>Obviously, I wouldn't care if these changes were made or not if I can <a href="/posts.php?topic=83506#942119" target="_blank"><u>legally</u></a> *rename* and claim rights on the GLMax2D module (which would actually make things easier.. and harder).<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Rem
bbdoc: Graphics/OpenGL Max2D
about:
The OpenGL Max2D module provides an OpenGL driver for #Max2D.
End Rem
Module brl.GLMax2D

ModuleInfo "Version: 1.13"
ModuleInfo "Author: Mark Sibly"
ModuleInfo "License: Blitz Shared Source Code"
ModuleInfo "Copyright: Blitz Research Ltd"
ModuleInfo "Modserver: BRL"

ModuleInfo "History: 1.14 Proposed Release"
ModuleInfo "History: [Plash] SuperStrict'd the module and wrapped all Private code into _TGLMax2DAssistant"
ModuleInfo "History: 1.13 Release"
ModuleInfo "History: Cleaned up SetGraphics"
ModuleInfo "History: 1.12 Release"
ModuleInfo "History: Fixed filtered image min filters"
ModuleInfo "History: 1.11 Release"
ModuleInfo "History: Fixed texture delete logic"
ModuleInfo "History: 1.10 Release"
ModuleInfo "History: Add SetColor/SetClsColor clamping"
ModuleInfo "History: 1.09 Release"
ModuleInfo "History: Fixed DrawPixmap using current blend mode - now always uses SOLIDBLEND"
ModuleInfo "History: 1.08 Release"
ModuleInfo "History: Added MIPMAPPEDIMAGE support"
ModuleInfo "History: 1.07 Release"
ModuleInfo "History: Now default driver for MacOS/Linux only (D3D7 for windows)"
ModuleInfo "History: 1.06 Release"
ModuleInfo "History: Ripped out a bunch of dead code"
ModuleInfo "History: 1.05 Release"
ModuleInfo "History: Added checks to prevent invalid textures deletes"

Import brl.Max2D
Import brl.GLGraphics

Private

' The code would be too massively ugly (and hard to work with) if
' _TGLMax2DAssistant was prepended to all the uses of these variables
Global ix:Float, iy:Float, jx:Float, jy:Float

Public

Type _TGLMax2DAssistant
	
	Global GLGraphics:TGLGraphics
	
	' Naughty!
	Const GL_BGR:Int = $80E0
	Const GL_BGRA:Int = $80E1
	Const GL_CLAMP_TO_EDGE:Int = $812F
	Const GL_CLAMP_TO_BORDER:Int = $812D
	
	' These parameters are simply for direct access (instead of calling GetTransformVars,
	' you can just access these pointers)
	Global p_ix:Float Ptr, p_iy:Float Ptr, p_jx:Float Ptr, p_jy:Float Ptr
	Global color4ub:Byte[4]
	
	Global state_blend:Int
	Global state_boundtex:Int
	Global state_texenabled:Int
	
	Function BindTex(name:Int)
		
		If name &lt;&gt; state_boundtex
			glBindTexture(GL_TEXTURE_2D, name)
			state_boundtex = name
		End If
		
	End Function
	
	Function EnableTex(name:Int)
		
		BindTex(name)
		
		If state_texenabled = False
			
			glEnable(GL_TEXTURE_2D)
			state_texenabled = True
			
		End If
		
	End Function
	
	Function DisableTex()
		
		If state_texenabled = True
			
			glDisable(GL_TEXTURE_2D)
			state_texenabled = False
			
		End If
		
	End Function
	
	Function Pow2Size:Int(n:Int)
		Local t:Int = 1
		
		While t &lt; n
			
			t:*2
			
		Wend
		
		Return t
		
	End Function
	
	Function CreateTex:Int(width:Int, height:Int, flags:Int)
		Local name:Int
		
		glGenTextures(1, Varptr name)
		
		glBindTexture(GL_TEXTURE_2D, name)
		
		'set texture parameters
		glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_WRAP_S, GL_CLAMP_TO_EDGE)
		glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_WRAP_T, GL_CLAMP_TO_EDGE)
		
		If flags &amp; FILTEREDIMAGE
			
			glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER, GL_LINEAR)
			
			If flags &amp; MIPMAPPEDIMAGE
				glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_LINEAR_MIPMAP_LINEAR)
			Else
				glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_LINEAR)
			End If
			
		Else
			
			glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER, GL_NEAREST)
			
			If flags &amp; MIPMAPPEDIMAGE
				glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_NEAREST_MIPMAP_NEAREST)
			Else
				glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_NEAREST)
			End If
			
		End If
		
		Local mip_level:Int
		
		Repeat
			
			glTexImage2D(GL_TEXTURE_2D, mip_level, GL_RGBA8, width, height, 0, GL_RGBA, GL_UNSIGNED_BYTE, Null)
			
			If Not (flags &amp; MIPMAPPEDIMAGE) Exit
			If width = 1 And height = 1 Then Exit
			If width &gt; 1 Then width:/2
			If height &gt; 1 Then height:/2
			
			mip_level:+1
			
		Forever
		
		Return name
		
	End Function
	
	Function UploadTex(pixmap:TPixmap, flags:Int)
		Local mip_level:Int
		
		Repeat
			
			glPixelStorei(GL_UNPACK_ROW_LENGTH, pixmap.pitch / BytesPerPixel[pixmap.format])
			glTexSubImage2D(GL_TEXTURE_2D, mip_level, 0, 0, pixmap.width, pixmap.height, GL_RGBA, GL_UNSIGNED_BYTE, pixmap.pixels)
			
			If Not (flags &amp; MIPMAPPEDIMAGE) Exit
			If pixmap.width &gt; 1 And pixmap.height &gt; 1
				pixmap = ResizePixmap(pixmap, pixmap.width / 2, pixmap.height / 2)
			Else If pixmap.width &gt; 1
				pixmap = ResizePixmap(pixmap, pixmap.width / 2, pixmap.height)
			Else If pixmap.height &gt; 1
				pixmap = ResizePixmap(pixmap, pixmap.width, pixmap.height / 2)
			Else
				Exit
			End If
			
			mip_level:+1
			
		Forever
		
		glPixelStorei(GL_UNPACK_ROW_LENGTH, 0)
		
	End Function
	
	Function AdjustTexSize(width:Int Var, height:Int Var)
		Local t:Int
		
		'calc texture size
		width = Pow2Size(width)
		height = Pow2Size(height)
		
		Repeat
			
			glTexImage2D(GL_PROXY_TEXTURE_2D, 0, 4, width, height, 0, GL_RGBA, GL_UNSIGNED_BYTE, Null)
			glGetTexLevelParameteriv(GL_PROXY_TEXTURE_2D, 0, GL_TEXTURE_WIDTH, Varptr t)
			
			If t &lt;&gt; 0 Then Exit
			If width = 1 And height = 1 Then RuntimeError("Unable to calculate tex size")
			If width &gt; 1 Then width:/2
			If height &gt; 1 Then height:/2
			
		Forever
		
	End Function
	
	Function ResetGLContext()
		
		state_blend = 0
		state_boundtex = 0
		state_texenabled = 0
		
	End Function
	
	Function SetBlend(blend:Int)
		
		If blend &lt;&gt; state_blend
			
			state_blend = blend
			
			Select blend
				Case MASKBLEND
					glDisable(GL_BLEND)
					glEnable(GL_ALPHA_TEST)
					glAlphaFunc(GL_GEQUAL, 0.5)
					
				Case SOLIDBLEND
					glDisable(GL_BLEND)
					glDisable(GL_ALPHA_TEST)
					
				Case ALPHABLEND
					glEnable(GL_BLEND)
					glBlendFunc(GL_SRC_ALPHA, GL_ONE_MINUS_SRC_ALPHA)
					glDisable(GL_ALPHA_TEST)
					
				Case LIGHTBLEND
					glEnable(GL_BLEND)
					glBlendFunc(GL_SRC_ALPHA, GL_ONE)
					glDisable(GL_ALPHA_TEST)
					
				Case SHADEBLEND
					glEnable(GL_BLEND)
					glBlendFunc(GL_DST_COLOR, GL_ZERO)
					glDisable(GL_ALPHA_TEST)
					
				Default
					glDisable(GL_BLEND)
					glDisable(GL_ALPHA_TEST)
					
			End Select
			
		End If
		
	End Function
	
	Function SetAlpha(alpha:Float)
		
		If alpha &gt; 1.0 alpha = 1.0
		If alpha &lt; 0.0 alpha = 0.0
		color4ub[3] = alpha * 255
		
		glColor4ubv(color4ub)
		
	End Function
	
	Function SetColor(red:Int, green:Int, blue:Int)
		
		color4ub[0] = Min(Max(red, 0), 255)
		color4ub[1] = Min(Max(green, 0), 255)
		color4ub[2] = Min(Max(blue, 0), 255)
		
		glColor4ubv(color4ub)
		
	End Function
	
	Function GetTransformVars(_ix:Float, _iy:Float, _jx:Float, _jy:Float)
		
		_ix = ix
		_iy = iy
		_jx = jx
		_jy = jy
		
	End Function
	
End Type

Type TGLImageFrame Extends TImageFrame
	
	Field u0:Float, v0:Float, u1:Float, v1:Float
	Field name:Int, seq:Int
	
	Method New()
		
		seq = GraphicsSeq
		
	End Method
	
	Method Delete()
		
		If seq &lt;&gt; Null
			
			If seq = GraphicsSeq
				glDeleteTextures(1, Varptr name)
			End If
			
			seq = 0
			
		End If
		
	End Method
	
	Method Draw(x0:Float, y0:Float, x1:Float, y1:Float, tx:Float, ty:Float)
		
		Assert seq = GraphicsSeq Else "Image does not exist"
		
		_TGLMax2DAssistant.EnableTex(name)
		glBegin(GL_QUADS)
			glTexCoord2f(u0, v0)
			glVertex2f(x0 * ix + y0 * iy + tx, x0 * jx + y0 * jy + ty)
			glTexCoord2f(u1, v0)
			glVertex2f(x1 * ix + y0 * iy + tx, x1 * jx + y0 * jy + ty)
			glTexCoord2f(u1, v1)
			glVertex2f(x1 * ix + y1 * iy + tx, x1 * jx + y1 * jy + ty)
			glTexCoord2f(u0, v1)
			glVertex2f(x0 * ix + y1 * iy + tx, x0 * jx + y1 * jy + ty)
		glEnd()
		
	End Method
	
	Function CreateFromPixmap:TGLImageFrame(src:TPixmap, flags:Int)
		Local tex_w:Int = src.width
		Local tex_h:Int = src.height
		
		'determine tex size
		_TGLMax2DAssistant.AdjustTexSize(tex_w, tex_h)
		
		'make sure pixmap fits texture
		Local width:Int = Min(src.width, tex_w)
		Local height:Int = Min(src.height, tex_h)
		If src.width &lt;&gt; width Or src.height &lt;&gt; height
			src = ResizePixmap(src, width, height)
		End If
		
		'create texture pixmap
		Local tex:TPixmap = src
		
		'"smear" right/bottom edges if necessary
		If width &lt; tex_w Or height &lt; tex_h
			
			tex = TPixmap.Create(tex_w, tex_h, PF_RGBA8888)
			
			tex.Paste(src, 0, 0)
			
			If width &lt; tex_w
				
				tex.Paste(src.Window(width - 1, 0, 1, height), width, 0)
				
			End If
			
			If height &lt; tex_h
				
				tex.Paste(src.Window(0, height - 1, width, 1), 0, height)
				
				If width &lt; tex_w
					tex.Paste(src.Window(width - 1, height - 1, 1, 1), width, height)
				End If
				
			End If
			
		Else
			
			If tex.format &lt;&gt; PF_RGBA8888
				tex = tex.Convert(PF_RGBA8888)
			End If
			
		End If
		
		'create tex
		Local name:Int = _TGLMax2DAssistant.CreateTex(tex_w, tex_h, flags)
		
		'upload it
		_TGLMax2DAssistant.UploadTex(tex, flags)
		
		'done!
		Local frame:TGLImageFrame = New TGLImageFrame
		frame.name = name
		frame.u1 = Float(width) / Float(tex_w)
		frame.v1 = Float(height) / Float(tex_h)
		
		Return frame
		
	End Function
	
End Type


Type TGLMax2DDriver Extends TMax2DDriver
	
	'graphics driver overrides
	Method GraphicsModes:TGraphicsMode[] ()
		
		Return GLGraphicsDriver().GraphicsModes()
		
	End Method
	
	Method AttachGraphics:TMax2DGraphics(widget:Int, flags:Int)
		Local g:TGLGraphics = GLGraphicsDriver().AttachGraphics(widget, flags)
		
		If g &lt;&gt; Null
			Return TMax2DGraphics.Create(g, Self)
		End If
		
		Return Null
		
	End Method
	
	Method CreateGraphics:TMax2DGraphics(width:Int, height:Int, depth:Int, hertz:Int, flags:Int)
		Local g:TGLGraphics = GLGraphicsDriver().CreateGraphics(width, height, depth, hertz, flags)
		
		If g &lt;&gt; Null
			Return TMax2DGraphics.Create(g, Self)
		End If
		
		Return Null
		
	End Method
	
	Method SetGraphics(g:TGraphics)
		
		If g = Null
			
			TMax2DGraphics.ClearCurrent()
			GLGraphicsDriver().SetGraphics(Null)
			
		Else
			
			Local t:TMax2DGraphics = TMax2DGraphics(g)
			Assert t And TGLGraphics(t._graphics)
			
			GLGraphicsDriver().SetGraphics(t._graphics)
			
			ResetGLContext(t)
			
			t.MakeCurrent()
			
		End If
		
	End Method
	
	Method ResetGLContext(g:TGraphics)
		Local gw:Int, gh:Int, gd:Int, gr:Int, gf:Int
		
		g.GetSettings(gw, gh, gd, gr, gf)
		
		_TGLMax2DAssistant.ResetGLContext()
		
		glDisable(GL_TEXTURE_2D)
		glMatrixMode(GL_PROJECTION)
		glLoadIdentity()
		glOrtho(0, gw, gh, 0, - 1, 1)
		glMatrixMode(GL_MODELVIEW)
		glViewport(0, 0, gw, gh)
		
	End Method
	
	Method Flip(sync:Int)
		
		GLGraphicsDriver().Flip(sync)
		
	End Method
	
	Method ToString:String()
		
		Return "OpenGL"
		
	End Method
	
	Method CreateFrameFromPixmap:TGLImageFrame(pixmap:TPixmap, flags:Int)
		Local frame:TGLImageFrame
		
		frame = TGLImageFrame.CreateFromPixmap(pixmap, flags)
		
		Return frame
		
	End Method
	
	Method SetBlend(blend:Int)
		
		_TGLMax2DAssistant.SetBlend(blend)
		
	End Method
	
	Method SetAlpha(alpha:Float)
		
		_TGLMax2DAssistant.SetAlpha(alpha)
		
	End Method
	
	Method SetLineWidth(width:Float)
		
		glLineWidth(width)
		
	End Method
	
	Method SetColor(red:Int, green:Int, blue:Int)
		
		_TGLMax2DAssistant.SetColor(red, green, blue)
		
	End Method
	
	Method SetClsColor(red:Int, green:Int, blue:Int)
		
		red = Min(Max(red, 0), 255)
		green = Min(Max(green, 0), 255)
		blue = Min(Max(blue, 0), 255)
		
		glClearColor(red / 255.0, green / 255.0, blue / 255.0, 1.0)
		
	End Method
	
	Method SetViewport(x:Int, y:Int, w:Int, h:Int)
		
		If x = 0 And y = 0 And w = GraphicsWidth() And h = GraphicsHeight()
			
			glDisable(GL_SCISSOR_TEST)
			
		Else
			
			glEnable(GL_SCISSOR_TEST)
			glScissor(x, GraphicsHeight() - y - h, w, h)
			
		End If
		
	End Method
	
	Method SetTransform(xx:Float, xy:Float, yx:Float, yy:Float)
		
		ix = xx
		iy = xy
		jx = yx
		jy = yy
		
	End Method
	
	Method Cls()
		
		glClear(GL_COLOR_BUFFER_BIT)
		
	End Method
	
	Method Plot(x:Float, y:Float)
		
		_TGLMax2DAssistant.DisableTex()
		glBegin(GL_POINTS)
			glVertex2f(x + 0.5, y + 0.5)
		glEnd()
		
	End Method
	
	Method DrawLine(x0:Float, y0:Float, x1:Float, y1:Float, tx:Float, ty:Float)
		
		_TGLMax2DAssistant.DisableTex()
		glBegin(GL_LINES)
			glVertex2f(x0 * ix + y0 * iy + tx + 0.5, x0 * jx + y0 * jy + ty + 0.5)
			glVertex2f(x1 * ix + y1 * iy + tx + 0.5, x1 * jx + y1 * jy + ty + 0.5)
		glEnd()
		
	End Method
	
	Method DrawRect(x0:Float, y0:Float, x1:Float, y1:Float, tx:Float, ty:Float)
		
		_TGLMax2DAssistant.DisableTex()
		glBegin(GL_QUADS)
			glVertex2f(x0 * ix + y0 * iy + tx, x0 * jx + y0 * jy + ty)
			glVertex2f(x1 * ix + y0 * iy + tx, x1 * jx + y0 * jy + ty)
			glVertex2f(x1 * ix + y1 * iy + tx, x1 * jx + y1 * jy + ty)
			glVertex2f(x0 * ix + y1 * iy + tx, x0 * jx + y1 * jy + ty)
		glEnd()
		
	End Method
	
	Method DrawOval(x0:Float, y0:Float, x1:Float, y1:Float, tx:Float, ty:Float)
		Local th:Float, x:Float, y:Float
		Local xr:Float = (x1 - x0) * 0.5
		Local yr:Float = (y1 - y0) * 0.5
		Local segs:Int = Abs(xr) + Abs(yr)
		
		segs = Max(segs, 12) &amp; ~3
		
		x0:+xr
		y0:+yr
		
		_TGLMax2DAssistant.DisableTex()
		glBegin(GL_POLYGON)
		
		For Local i:Int = 0 Until segs
			th = i * 360.0 / segs
			x = x0 + Cos(th) * xr
			y = y0 - Sin(th) * yr
			
			glVertex2f(x * ix + y * iy + tx, x * jx + y * jy + ty)
			
		Next
		
		glEnd()
		
	End Method
	
	Method DrawPoly(xy:Float[], handle_x:Float, handle_y:Float, origin_x:Float, origin_y:Float)
		Local x:Float, y:Float
		
		If xy.length &lt; 6 Or (xy.length &amp; 1) Return
		
		_TGLMax2DAssistant.DisableTex()
		glBegin(GL_POLYGON)
		
		For Local i:Int = 0 Until xy.Length Step 2
			x = xy[i + 0] + handle_x
			y = xy[i + 1] + handle_y
			
			glVertex2f(x * ix + y * iy + origin_x, x * jx + y * jy + origin_y)
			
		Next
		
		glEnd()
		
	End Method
	
	Method DrawPixmap(p:TPixmap, x:Int, y:Int)
		Local blend:Int = _TGLMax2DAssistant.state_blend
		_TGLMax2DAssistant.DisableTex()
		_TGLMax2DAssistant.SetBlend(SOLIDBLEND)
		
		Local t:TPixmap = YFlipPixmap(p)
		
		If t.format &lt;&gt; PF_RGBA8888 Then t = ConvertPixmap(t, PF_RGBA8888)
		
		glRasterPos2i(0, 0)
		glBitmap(0, 0, 0, 0, x, - y - t.height, Null)
		glDrawPixels(t.width, t.height, GL_RGBA, GL_UNSIGNED_BYTE, t.pixels)
		
		_TGLMax2DAssistant.SetBlend(blend)
		
	End Method
	
	Method GrabPixmap:TPixmap(x:Int, y:Int, w:Int, h:Int)
		Local blend:Int = _TGLMax2DAssistant.state_blend
		_TGLMax2DAssistant.SetBlend(SOLIDBLEND)
		
		Local p:TPixmap = CreatePixmap(w, h, PF_RGBA8888)
		
		glReadPixels(x, GraphicsHeight() - h - y, w, h, GL_RGBA, GL_UNSIGNED_BYTE, p.pixels)
		p = YFlipPixmap(p)
		
		_TGLMax2DAssistant.SetBlend(blend)
		
		Return p
		
	End Method
	
End Type

Rem
bbdoc: Get OpenGL Max2D Driver
about:
The returned driver can be used with #SetGraphicsDriver to enable OpenGL Max2D 
rendering.
End Rem
Function GLMax2DDriver:TGLMax2DDriver()
	Global _driver:TGLMax2DDriver = New TGLMax2DDriver
	
	Return _driver
	
End Function

_TGLMax2DAssistant.p_ix = Varptr(ix)
_TGLMax2DAssistant.p_iy = Varptr(iy)
_TGLMax2DAssistant.p_jx = Varptr(jx)
_TGLMax2DAssistant.p_jy = Varptr(jy)

SetGraphicsDriver(GLMax2DDriver())


</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
