<!DOCTYPE html><html lang="en" ><head ><title >Small addition to BRL Reflection: Get Function ptr</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Small addition to BRL Reflection: Get Function ptr</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=110" >BlitzMax Module Tweaks</a>/<a href="#bottom" >Small addition to BRL Reflection: Get Function ptr</a><br><br>
<a name="953737"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BORNtobeNAMELESS</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Could be easily build into the Module, but this way the module does not need to be modified:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Type TFunction
	Field name:String
	Field fptr:Byte Ptr
	Field params:String
EndType

Function RefrGetFunctions:TFunction[](tt:TTypeId)
	If Not tt._class Return Null

	Local debug:Int=(Int Ptr tt._class)[2]
	Local p:Int Ptr=(Int Ptr debug)+2
	Local ret:TFunction[0]
	
	While p[0]
		Local id:String=String.FromCString( Byte Ptr p[1] )
		Local ty:String=String.FromCString( Byte Ptr p[2] )
		
		Local meta:String
		Local i:Int=ty.Find( "{" )
		If i&lt;&gt;-1
			meta=ty[i+1..ty.length-1]
			ty=ty[..i]
		EndIf

		Select p[0]
			Case 7
				ret = ret[..ret.length+1]
				Local n:TFunction = New TFunction
				n.name = id
				n.params = ty
				n.fptr = Byte Ptr(Int Ptr(tt._class + p[3])[0])
				ret[ret.length-1] = n
		End Select
		p:+4
	Wend

	Return ret
EndFunction

Function RefrGetFuncPtr:Byte Ptr(tt:TTypeId, name:String)
	If Not tt._class Return Null
	name = name.toLower()

	Local debug:Int=(Int Ptr tt._class)[2]
	Local p:Int Ptr=(Int Ptr debug)+2
	
	While p[0]
		Local id:String=String.FromCString( Byte Ptr p[1] )
		Local ty:String=String.FromCString( Byte Ptr p[2] )
		
		Local meta:String
		Local i:Int=ty.Find( "{" )
		If i&lt;&gt;-1
			meta=ty[i+1..ty.length-1]
			ty=ty[..i]
		EndIf

		Select p[0]
			Case 7
				If id.ToLower() = name Then
					Return Byte Ptr(Int Ptr(tt._class + p[3])[0])
				EndIf
		End Select
		p:+4
	Wend

	Return Null
EndFunction
</textarea><br><br><br>A Small example how to use it:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Type TTestType
	Function DoSomething(n:String)
		Print n
	EndFunction

	Function DoSomethingOther:Int(a:Int, b:Int)
		Return a+b
	EndFunction
EndType

Global t:TTypeId = TTypeId.ForName("TTestType")

For Local f:TFunction = EachIn RefrGetFunctions(t)
	Print f.name
	Print f.params
Next

Global f1(n:String) = RefrGetFuncPtr(t, "DoSomething")
Global f2:Int(a:Int, b:Int) = RefrGetFuncPtr(t, "DoSomethingOther")

f1("Hello You!")
Print f2(2,3)

End
</textarea><br><br><br>I needed this to automaticaly export Types as a Python-Module, maybe someone might find this usefull, too. <br><br></td></tr></table><br>
<a name="953759"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Azathoth</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Can it be used to get method pointers? <br><br></td></tr></table><br>
<a name="953763"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BORNtobeNAMELESS</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Method pointers are not possible as Methods allways need an Object to be called. Internaly BRL.Reflection gets a method pointer whenever it invokes a method internaly, but puts the Self-Object on the stack before it calls the method.<br>See TMethod.Invoke and the private _Call function in BRL.Reflection if you want to know more about it. <br><br></td></tr></table><br>
<a name="953765"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Htbaa</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> It would be cool if the reflection module would support full function and type function support. <br><br></td></tr></table><br>
<a name="953972"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice addition :)<br><br><div class="quote"> Can it be used to get method pointers? <br></div><br>Not true method pointers, but it works. (code ripped shamelessly from BRL.Reflection)<br><pre class=code>
Function GetMethodFuncPtr:Byte Ptr( this:Object, name:String)
	If this And name Then	
		Local typ:TTypeId = TTypeId.ForObject(this)
		Local meth:TMethod = typ.FindMethod(name)	
		If meth Then
			If meth._index &lt; 65536 Then Return bbRefMethodPtr( this, meth._index)
			Return Byte Ptr meth._index
		EndIf
	EndIf
	Return Null
EndFunction
</pre><br>Example usage:<br><pre class=code>
Local stream:TStream = WriteFile("test")
Local StreamWriteLine( this:TStream, s:String) = GetMethodFuncPtr( stream, "WriteLine")
StreamWriteLine( stream, "hello")
StreamWriteLine( stream, "world")
stream.Close()
</pre><br>I have an implementation of working method pointers though, using GNU Lightning.. it even plays nicely with the old GC, keeping the object alive until the methodptr is released.<br>But since the language doesnt support them natively, they have to be created and released manually :(<br><br>Its usage looks something like this:<pre class=code>
Local stream:TStream = WriteFile("c:\test")
Local StreamWriteLine( s:String) = GetMethodPtr( stream, "WriteLine", 1)
StreamWriteLine("hello")
StreamWriteLine("world")
stream.Close()
ReleaseMethodPtr(StreamWriteLine)
</pre><br><b>EDIT:</b><br>If you want to see how it works, download <a href="http://grable0.googlepages.com/grb.lightning.mod.x86.rar" target="_blank">grb.lightning.mod.x86.rar</a> and run this source:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Import BRL.Reflection
Import GRB.lightning

Private
Extern "C"
	Function bbRefMethodPtr:Byte Ptr( this:Object, index:Int)
	Function bbGCRetain( this:Int)
	Function bbGCRelease( this:Int)
EndExtern
Public

' returns pointer to method function (this pointer must be first param of prototype)
'
' prototype example: TStream.WriteLine( s:String) =&gt; StreamWriteLine( this:TStream, s:String)
'
Function GetMethodFuncPtr:Byte Ptr( this:Object, name:String)
	If this And name Then	
		Local typ:TTypeId = TTypeId.ForObject(this)
		Local meth:TMethod = typ.FindMethod(name)	
		If meth Then
			If meth._index &lt; 65536 Then Return bbRefMethodPtr( this, meth._index)
			Return Byte Ptr meth._index
		EndIf
	EndIf
	Return Null
EndFunction

' returns pointer to trampoline function with embedded this pointer (this not needed in prototype)
' the returned function keeps the object alive until ReleaseMethodPtr() is called.
'
' prototype example: TStream.WriteLine( s:String) =&gt; StreamWriteLine( s:String)
'
Function GetMethodPtr:Byte Ptr( this:Object, name:String, numparams:Int)
	If this And name Then
		Local typ:TTypeId = TTypeId.ForObject(this)
		Local meth:TMethod = typ.FindMethod(name)
		If meth Then
			Local code:Byte Ptr = MemAlloc(256)
			Local param:Int[numparams]
			
			' retain this pointer
			Local thisp:Int Ptr = Int Ptr code
			thisp[0] = (Int Byte Ptr this) - 8
			bbGCRetain(thisp[0])
			' skip it
			code :+ 4
			
			' setup prolog &amp; parameters
			JIT_set_ip(code)
			JIT_prolog(numparams)
			For Local i:Int = 0 Until numparams
				param[i] = JIT_arg_i()
			Next
			
			' setup call to method
			JIT_prepare(numparams + 1)
			' push parameters
			For Local i:Int = numparams-1 To 0 Step -1
				JIT_getarg_i(JIT_R0, param[i])
				JIT_pusharg_p(JIT_R0)
			Next
			' push self
			JIT_movi_i(JIT_R0, thisp[0])
			JIT_pusharg_p(JIT_R0)
			
			' call actual method 
			Local mp:Byte Ptr
			If meth._index &lt; 65536 Then 
				mp = bbRefMethodPtr( this, meth._index)
			Else
				mp = Byte Ptr meth._index
			EndIf
			JIT_finish(mp)
			
			JIT_ret_bs()
			
			JIT_flush_code( code, JIT_get_ip())
			Return code
		EndIf
	EndIf
	Return Null
EndFunction

' releases the object from the method pointer, and frees the method pointer itself
Function ReleaseMethodPtr( p:Byte Ptr)
	Local thisp:Int Ptr = Int Ptr(p - 4)
	bbGCRelease( thisp[0])
	MemFree thisp
EndFunction

'
' TEST
'
Type TTest
	Field F:Int
	
	Method Set:TTest( value:Int)
		F = value
		Return Self
	EndMethod
	
	Method test:Int( a:Int, b:Int)
		Print a + " -- " + b + " -- " + F
		Return -1
	EndMethod
EndType

Local t:TTest = New TTest.Set(10)
Local mp1:Int( a:Int, b:Int) = GetMethodPtr( t, "test", 2)
Local mp2:Int( a:Int, b:Int) = GetMethodPtr( New TTest.Set(20), "test", 2)

t = Null; GCCollect()
Print mp1( 1, 2)
Print mp2( 1, 2)

ReleaseMethodPtr(mp1)
ReleaseMethodPtr(mp2)
</textarea> <br><br></td></tr></table><br>
<a name="953985"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> @grable<br><br>bbGCRetain and bbGCRelease have been deprecated, I believe. <br><br></td></tr></table><br>
<a name="954032"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >N</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Method pointers are not possible as Methods allways need an Object to be called. Internaly BRL.Reflection gets a method pointer whenever it invokes a method internaly, but puts the Self-Object on the stack before it calls the method.<br> <br></div>This is very possible.  You can also very easily replace methods. <br><br></td></tr></table><br>
<a name="954062"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> bbGCRetain and bbGCRelease have been deprecated, I believe. <br></div><br>Yeah, i havent taken the plunge to 1.32 yet ;) but isnt the old GC still in use when not using threading? <br><br></td></tr></table><br>
<a name="954112"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >N</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Yeah, i havent taken the plunge to 1.32 yet ;) but isnt the old GC still in use when not using threading?  <br></div>Yes. <br><br></td></tr></table><br>
<a name="956203"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >spacerat</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've been trying to integrate function support into brl.reflection so that it behaves exactly the same as the support for methods, and I've managed to get everything working apart from Invoke(args:Object[]). The code for the whole thing is here <a href="http://pastebin.com/f453e43b9" target="_blank">http://pastebin.com/f453e43b9</a> but if you just want to see invoke take a look here.<br><pre class=code>
        Method Invoke:Object(args:Object[] = Null)
                Local q[10], sp:Byte Ptr = q
                sp:+4
                If typeId() = LongTypeId sp:+8
        '       If args &lt;&gt; Null
                        For Local i = 0 Until args.length
                                If Int Ptr(sp)&gt;=Int Ptr(q)+8 Throw "ERROR"
                                sp = _Push(sp, argTypes()[i], args[i])
                        Next
        '       EndIf
                If Int Ptr(sp)&gt;Int Ptr(q)+8 Throw "ERROR"
                       
                Select typeId()
                        Case ByteTypeId, ShortTypeId, IntTypeId
                                Local f:Int(p0, p1, p2, p3, p4, p5, p6, p7) = _fptr
                                Return String.FromInt(f(q[0], q[1], q[2], q[3], q[4], q[5], q[6], q[7]))
                        Case LongTypeId
                                Throw "TODO"
                        Case FloatTypeId
                                Local f:Float(p0, p1, p2, p3, p4, p5, p6, p7) = _fptr
                                Return String.FromFloat(f(q[0], q[1], q[2], q[3], q[4], q[5], q[6], q[7]))
                        Case DoubleTypeId
                                Local f:Double(p0, p1, p2, p3, p4, p5, p6, p7) = _fptr
                                Return String.FromDouble(f(q[0], q[1], q[2], q[3], q[4], q[5], q[6], q[7]))
                        Default
                                Local f:Object(p0, p1, p2, p3, p4, p5, p6, p7) = _fptr
                                Return f(q[0], q[1], q[2], q[3], q[4], q[5], q[6], q[7])
                End Select     
        End Method
       
        Field _selfTypeId:TTypeId, _argTypes:TTypeId[]
        Field _fptr:Byte Ptr
</pre><br><br>Basically, Invoke only seems to work when the function takes no arguments, otherwise the program crashes (no debug information, it just fails). I think if we can fix this, we might have a worthy modification to the official brl.reflection.<br><br>EDIT: Fixed it myself. Pastebin for the whole working thing is here <a href="http://pastebin.com/f5aa00efa" target="_blank">http://pastebin.com/f5aa00efa</a> . <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
