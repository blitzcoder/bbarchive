<!DOCTYPE html><html lang="en" ><head ><title >Small DrawText addition</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Small DrawText addition</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=110" >BlitzMax Module Tweaks</a>/<a href="#bottom" >Small DrawText addition</a><br><br>
<a name="467961"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >N</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>
Rem
bbdoc: Draw text
about:
#DrawText prints strings at position @x,@y of the graphics display using
the current image font specified by the #SetImageFont command.  The text will be centered based on your use of @centerx and @centery .&lt;p&gt;
Other commands that affect #DrawText include #SetColor, #SetHandle, 
#SetScale, #SetRotation, #SetOrigin, #SetViewPort, #SetBlend and #SetAlpha.
End Rem
Function DrawText( t$,x#,y#, centerx%=0, centery%=0 )
	If centerx Then x :- TextWidth(t$)*.5
	If centery Then y :- TextHeight(t$)*.5
	image_font.Draw t,..
	x+origin_x+handle_x*tform_ix+handle_y*tform_iy,..
	y+origin_y+handle_x*tform_jx+handle_y*tform_jy,..
	tform_ix,tform_iy,tform_jx,tform_jy
End Function
</pre><br><br>You can probably figure out where to put this if you decide to use it. <br><br></td></tr></table><br>
<a name="467990"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >fredborg</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> And more from the same drawer:<pre class=code>Rem
bbdoc: Draw text
about:
#DrawText prints strings at position @x,@y of the graphics display using
the current image font specified by the #SetImageFont command.  The text will be aligned based on your use of @alignx and @aligny. Possible values for @alignx are:&lt;br&gt;
ALIGN_LEFT - Aligns the text so the left most edge is at @x (Default)&lt;br&gt;
ALIGN_CENTER - Centers the text horizontaly at @x&lt;br&gt;
ALIGN_RIGHT - Aligns the text so the right most edge is at @x&lt;br&gt;
Possible values for @aligny are:&lt;br&gt;
ALIGN_TOP - Aligns the text so the top is at @y (Default)&lt;br&gt;
ALIGN_MIDDLE - Centers the text verticaly at @y&lt;br&gt;
ALIGN_BOTTOM - Aligns the text so the bottom edge is at @y&lt;p&gt;
Other commands that affect #DrawText include #SetColor, #SetHandle, 
#SetScale, #SetRotation, #SetOrigin, #SetViewPort, #SetBlend and #SetAlpha.
End Rem
Const ALIGN_LEFT   = 0
Const ALIGN_CENTER = 1
Const ALIGN_RIGHT  = 2
Const ALIGN_TOP    = 0
Const ALIGN_MIDDLE = 1
Const ALIGN_BOTTOM = 2
Function DrawText( t$,x#,y#, alignx%=0, aligny%=0 )
	If alignx = ALIGN_CENTER
		x :- TextWidth(t$)*.5
	ElseIf alignx = ALIGN_RIGHT
		x :- TextWidth(t$)
	EndIf
	If aligny = ALIGN_MIDDLE
		y :- TextHeight(t$)*.5
	ElseIf aligny = ALIGN_BOTTOM
		y :- TextHeight(t$)
	EndIf
	image_font.Draw t,..
	x+origin_x+handle_x*tform_ix+handle_y*tform_iy,..
	y+origin_y+handle_x*tform_jx+handle_y*tform_jy,..
	tform_ix,tform_iy,tform_jx,tform_jy
End Function</pre> <br><br></td></tr></table><br>
<a name="468101"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I would prefer DrawText x,y,w=0,h=0,align=0 where align is a combination of horizontal and vertical flags and w,h where specified are a rectangle area that produce multiline input when overflow occurs or when the text includes ~n characters. <br><br></td></tr></table><br>
<a name="468115"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Difference</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sometimes you don't want multiline, even if the text is too wide to fit. (Lists and such) <br><br></td></tr></table><br>
<a name="468436"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >fredborg</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is a bit more advanced, but not perfect...<br><br>Peter, it works just like the regular DrawText if you omit the w# parameter or set it to 0.<br><br>Here's the code (place in 'max2d.bmx'):<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Const H_ALIGN_LEFT   = 0
Const H_ALIGN_CENTER = 1
Const H_ALIGN_RIGHT  = 2
Const V_ALIGN_TOP    = 0
Const V_ALIGN_MIDDLE = 4
Const V_ALIGN_BOTTOM = 8
Function DrawTextEx( t$,x#,y#,w#=0,h#=0,align%=0 )

	Local textlines:String[1]
	Local linecount:Int = 1
	textlines[0] = t$

	Local fitwidth = true
	If w&lt;=0
		' No fixed limit, if w isn't set
		w = 100000
		fitwidth = false
	End If
		
	' Try to fit inside x,y,w,h
	t$.Replace("~n",Chr(10))
		
	linecount = 1
	textlines[0] = ""
	Local index = 0
	Local oldindex = 0
	Local newline = False
	While oldindex&lt;t.length-1
		
		newline = False
			
		index = t.length-1
			
		For Local i = oldindex To t.length-1
			If t[i] = 10
				index = i
				newline = True
				Exit
			ElseIf t[i]&lt;=32
				index = i
				Exit
			EndIf
		Next
		
		If newline
			If TextWidth(textlines[linecount-1]+t[oldindex..index+1])&lt;w
				textlines[linecount-1]:+t[oldindex..index+1]
				linecount:+1
				textlines=textlines[..linecount]
				textlines[linecount-1] = " "
			Else
				linecount:+1
				textlines=textlines[..linecount]
				textlines[linecount-1] = " "+t[oldindex..index+1]
			EndIf
		ElseIf TextWidth(textlines[linecount-1]+t[oldindex..index+1])&lt;w Or textlines[linecount-1].length=0
			textlines[linecount-1]:+t[oldindex..index+1]
		Else 
			linecount:+1
			textlines=textlines[..linecount]
			textlines[linecount-1] = ""
			index = oldindex-1
		EndIf 
		oldindex = index+1
	Wend

	If fitwidth = False
		' Reset w to 0
		w = 0
	EndIf
				
	If align &amp; V_ALIGN_MIDDLE
		y = y + h*0.5 - TextHeight(t$)*linecount*.5
	ElseIf align &amp; V_ALIGN_BOTTOM
		y = y + h - TextHeight(t$)*linecount
	EndIf

	Local dx#

	For t$ = EachIn textlines
		dx = x
		If align &amp; H_ALIGN_CENTER
			dx = x + w*0.5 - TextWidth(t$)*.5
		ElseIf align &amp; H_ALIGN_RIGHT
			dx = x + w - TextWidth(t$)
		EndIf

		image_font.Draw t,..
		dx+origin_x+handle_x*tform_ix+handle_y*tform_iy,..
		y+origin_y+handle_x*tform_jx+handle_y*tform_jy,..
		tform_ix,tform_iy,tform_jx,tform_jy
		y :+ TextHeight(t$)
	Next
	
End Function</textarea><br>And a small example:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics 640,480,16

t$ = "DrawTextEx Example~n~n~nJust some text to show how it works!!!"

Repeat
	Cls
	x0 = GraphicsWidth()/2
	y0 = GraphicsHeight()/2
	x1 = MouseX()
	y1 = MouseY()
	
	w = Abs(x0-x1)
	h = Abs(y0-y1)
	
	x0 = Min(x0,x1) 
	y0 = Min(y0,y1) 

	SetColor 255,0,0
	DrawRect x0,y0,w,h
	SetColor 255,255,255

	' Possible align flags are:
	' H_ALIGN_LEFT	(default)
	' H_ALIGN_CENTER
	' H_ALIGN_RIGHT
	' V_ALIGN_TOP		(default)
	' V_ALIGN_MIDDLE
	' V_ALIGN_BOTTOM
	'
	' Combine flags using | (binary or)	
	DrawTextEx t$,x0,y0,w,h,H_ALIGN_CENTER|V_ALIGN_MIDDLE
	
	Flip
Until KeyHit(KEY_ESCAPE)

End</textarea><br><br>I really like the way you can access a string like an array, great stuff! <br><br></td></tr></table><br>
<a name="470487"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bot Builder</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cool. Comments for the module entry:<br><pre class=code>Rem
bbdoc: Draw text
about:
#DrawText prints strings at position @x,@y of the graphics display using
the current image font specified by the #SetImageFont command.  The text 
will be aligned based on applying binary ORs (| operator) of 2 flags,
one from the top 3, one from the bottom. The flags for @align are:&lt;br&gt;
ALIGN_LEFT - Aligns the text so the left most edge is at @x (Default)&lt;br&gt;
ALIGN_CENTER - Centers the text horizontaly at @x&lt;br&gt;
ALIGN_RIGHT - Aligns the text so the right most edge is at @x&lt;br&gt;
ALIGN_TOP - Aligns the text so the top is at @y (Default)&lt;br&gt;
ALIGN_MIDDLE - Centers the text verticaly at @y&lt;br&gt;
ALIGN_BOTTOM - Aligns the text so the bottom edge is at @y&lt;p&gt;
Other commands that affect #DrawText include #SetColor, #SetHandle, 
#SetScale, #SetRotation, #SetOrigin, #SetViewPort, #SetBlend and #SetAlpha.
End Rem</pre> <br><br></td></tr></table><br>
<a name="630844"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Red</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> bug fixed: SetRotation <br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Const H_ALIGN_LEFT   = 0
Const H_ALIGN_CENTER = 1
Const H_ALIGN_RIGHT  = 2
Const V_ALIGN_TOP    = 0
Const V_ALIGN_MIDDLE = 4
Const V_ALIGN_BOTTOM = 8
Rem
bbdoc: Draw wrapped text
about:
#DrawWrappedText prints wrapped texts in rectangle at position @x, @y of the graphics display using
the current image font specified by the #SetImageFont command.&lt;br&gt;&lt;br&gt;  
@x, @y, @w, @h define the rectangle area.&lt;br&gt;&lt;br&gt;
The text will be aligned based on applying binary ORs (| operator) of 2 flags,
one from the top, one from the bottom. The flags for @align are:&lt;br&gt;&lt;br&gt;
&lt;table&gt;
&lt;tr&gt;&lt;th&gt;Align values&lt;/th&gt;&lt;th&gt;Effect&lt;/th&gt;&lt;/tr&gt;
&lt;tr&gt;&lt;td&gt;ALIGN_LEFT&lt;/td&gt;&lt;td&gt;Aligns the text so the left most edge is at @x (Default)&lt;/td&gt;&lt;/tr&gt;
&lt;tr&gt;&lt;td&gt;ALIGN_CENTER&lt;/td&gt;&lt;td&gt;Centers the text horizontaly at @x&lt;/td&gt;&lt;/tr&gt;
&lt;tr&gt;&lt;td&gt;ALIGN_RIGHT&lt;/td&gt;&lt;td&gt;Aligns the text so the right most edge is at @x&lt;/td&gt;&lt;/tr&gt;
&lt;tr&gt;&lt;td&gt;ALIGN_TOP&lt;/td&gt;&lt;td&gt;Aligns the text so the top is at @y (Default)&lt;/td&gt;&lt;/tr&gt;
&lt;tr&gt;&lt;td&gt;ALIGN_MIDDLE&lt;/td&gt;&lt;td&gt;Centers the text verticaly at @y&lt;/td&gt;&lt;/tr&gt;
&lt;tr&gt;&lt;td&gt;ALIGN_BOTTOM&lt;/td&gt;&lt;td&gt;Aligns the text so the bottom edge is at @y&lt;/td&gt;&lt;/tr&gt;
&lt;/table&gt;&lt;br&gt;
Other commands that affect #DrawWrappedText include #SetColor, #SetHandle, 
#SetScale, #SetRotation, #SetOrigin, #SetViewPort, #SetBlend and #SetAlpha.&lt;br&gt;
End Rem
Function DrawWrappedText( t$,x#,y#,w#=0,h#=0,align%=0 )

	Local textlines:String[1]
	Local linecount:Int = 1
	textlines[0] = t$

	Local fitwidth = True
	If w&lt;=0
		' No fixed limit, if w isn't set
		w = 100000
		fitwidth = False
	End If
		
	' Try to fit inside x,y,w,h
	t$.Replace("~n",Chr(10))
		
	linecount = 1
	textlines[0] = ""
	Local index = 0
	Local oldindex = 0
	Local newline = False
	While oldindex&lt;t.length-1
		
		newline = False
			
		index = t.length-1
			
		For Local i = oldindex To t.length-1
			If t[i] = 10
				index = i
				newline = True
				Exit
			ElseIf t[i]&lt;=32
				index = i
				Exit
			EndIf
		Next
		
		If newline
			If TextWidth(textlines[linecount-1]+t[oldindex..index+1])&lt;w
				textlines[linecount-1]:+t[oldindex..index+1]
				linecount:+1
				textlines=textlines[..linecount]
				textlines[linecount-1] = " "
			Else
				linecount:+1
				textlines=textlines[..linecount]
				textlines[linecount-1] = " "+t[oldindex..index+1]
			EndIf
		ElseIf TextWidth(textlines[linecount-1]+t[oldindex..index+1])&lt;w Or textlines[linecount-1].length=0
			textlines[linecount-1]:+t[oldindex..index+1]
		Else 
			linecount:+1
			textlines=textlines[..linecount]
			textlines[linecount-1] = ""
			index = oldindex-1
		EndIf 
		oldindex = index+1
	Wend

	If fitwidth = False
		' Reset w to 0
		w = 0
	EndIf
			
	Local offset_x#
	Local offset_y#
	Local shift_y#
	Local dx#
	Local dy#
		
	If align &amp; V_ALIGN_MIDDLE
		offset_y=(h - TextHeight(t$)*linecount)*.5
	ElseIf align &amp; V_ALIGN_BOTTOM
		offset_y=(h - TextHeight(t$)*linecount)
	EndIf
	y:+ offset_y*gc.tform_jy
	x:+ offset_y*gc.tform_iy

	For t$ = EachIn textlines
		If align &amp; H_ALIGN_CENTER
			offset_x=(w - TextWidth(t$))*.5
		ElseIf align &amp; H_ALIGN_RIGHT
			offset_x=(w - TextWidth(t$))
		EndIf

		gc.image_font.Draw t,..
		x+gc.origin_x+(offset_x+gc.handle_x)*gc.tform_ix+gc.handle_y*gc.tform_iy,..
		y+gc.origin_y+(offset_x+gc.handle_x)*gc.tform_jx+gc.handle_y*gc.tform_jy,..
		gc.tform_ix,gc.tform_iy,gc.tform_jx,gc.tform_jy
		
		shift_y=TextHeight(t$)
		y :+ shift_y*gc.tform_jy
		x :+ shift_y*gc.tform_iy
	Next
	
End Function</textarea> <br><br></td></tr></table><br>
<a name="630846"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >N</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> It's over a year old, Ed. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
