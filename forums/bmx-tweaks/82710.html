<!DOCTYPE html><html lang="en" ><head ><title >Mouse Speed Functions Additions</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Mouse Speed Functions Additions</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=110" >BlitzMax Module Tweaks</a>/<a href="#bottom" >Mouse Speed Functions Additions</a><br><br>
<a name="932906"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ked</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey! After reading in the code archives that people were having trouble getting MouseXSpeed(), MouseYSpeed(), and MouseZSpeed() to work like the Blitz3D functions, I went into the BlitzMax source to see if I could help. I modified BRL.System and BRL.PolledInput and got everything working like it should. (I tested them with a MiniB3D example.)<br><br><b>BRL.System</b><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Rem
bbdoc: System/System
End Rem
Module BRL.System

ModuleInfo "Version: 1.27"
ModuleInfo "Author: Mark Sibly, Simon Armstrong"
ModuleInfo "License: Blitz Shared Source Code"
ModuleInfo "Copyright: Blitz Research Ltd"
ModuleInfo "Modserver: BRL"

ModuleInfo "History: 1.27 Release"
ModuleInfo "History: Add support for MouseXSpeed(),MouseYSpeed(),MouseZSpeed()"
ModuleInfo "History: 1.26 Release"
ModuleInfo "History: Moved event enums from system.h to event.mod/event.h"
ModuleInfo "Histroy: Moved keycode enums from system.h to keycodes.mod/keycodes.h"
ModuleInfo "History: 1.26 Release"
ModuleInfo "History: Fixed win32 filerequester causing activewindow dramas"
ModuleInfo "History: 1.25 Release"
ModuleInfo "History: Fixed unretained object issue with mouse tracking"
ModuleInfo "History: 1.24 Release"
ModuleInfo "History: Fixed windowed mode HideMouse issue"
ModuleInfo "History: 1.23 Release"
ModuleInfo "History: Fixed win32 requestfile default extension bug#2"
ModuleInfo "History: 1.22 Release"
ModuleInfo "History: Fixed win32 requestfile default extension bug"
ModuleInfo "History: 1.21 Release"
ModuleInfo "History: New Linux implementation of OpenURL"
ModuleInfo "History: 1.20 Release"
ModuleInfo "History: RequestFile now adds extension to filename on Windows"
ModuleInfo "History: 1.19 Release"
ModuleInfo "History: Added EVENT_GADGETLOSTFOCUS handling"
ModuleInfo "History: 1.18 Release"
ModuleInfo "History: Added EVENT_KEYREPEAT handling"
ModuleInfo "History: 1.17 Release"
ModuleInfo "History: OpenURL now attempts to fully qualify file / http url supplied"
ModuleInfo "History: 1.16 Release"
ModuleInfo "History: Fixed MacOS RequestFile to respect wild card filter"
ModuleInfo "History: 1.15 Release"
ModuleInfo "History: Fixed mouse hidden by default"
ModuleInfo "History: 1.14 Release"
ModuleInfo "History: Fixed HideMouse causing mouse to disappear when in non-client areas"
ModuleInfo "History: 1.13 Release"
ModuleInfo "History: Fixed Linux MoveMouse to be relative to the origin of the current Graphics window"
ModuleInfo "History: 1.12 Release"
ModuleInfo "History: Added Linux X11 import to remove glgraphics.mod dependency"
ModuleInfo "History: Fixed linux middle button crash"
ModuleInfo "History: 1.11 Release"
ModuleInfo "History: Fixed win32 clipboard glitches with QS_ALLINPUT bbSystemWait mod"
ModuleInfo "History: 1.11 Release"
ModuleInfo "History: CGSetLocalEventsSuppressionInterval fix for MacOS bbSystemMoveMouse"
ModuleInfo "History: 1.10 Release"
ModuleInfo "History: Ripped out input stuff and added hook"
ModuleInfo "History: 1.09 Release"
ModuleInfo "History: Tweaked MacOS GetChar()"
ModuleInfo "History: 1.07 Release"
ModuleInfo "History: Added AppTitle support for requesters"
ModuleInfo "History: 1.06 Release"
ModuleInfo "History: Fixed MacOS RequestDir ignoring initial path"
ModuleInfo "History: 1.05 Release"
ModuleInfo "History: Added RequestDir support for MacOS"
ModuleInfo "History: 1.04 Release"
ModuleInfo "History: Added mouse capture to Win32"
ModuleInfo "History: Fixed C Compiler warnings"

Import BRL.Event
Import BRL.KeyCodes
Import BRL.Hook
Import BRL.FileSystem
Import Pub.StdC

Import "driver.bmx"

Import "system.c"

?MacOS
Import "system.macos.bmx"
Driver=New TMacOSSystemDriver
?Win32
Import "system.win32.bmx"
Import "-lcomdlg32"
Driver=New TWin32SystemDriver
?Linux
Import "-lX11"
Import "system.linux.bmx"
Driver=New TLinuxSystemDriver
?

Private
Global _busy
Public

Global _lastx:Int=0,_lasty:Int=0,_lastz:Int=0

Rem
bbdoc: Poll operating system
about:
#PollSystem returns control back to the operating system, allowing such
events as keystrokes and gadget actions to be processed. Control is then
returned back to your program.

If #PollSystem encounters a key, mouse or app suspend/resume/terminate 
event, an equivalent #TEvent object event will be generated which may be intercepted using
the #EmitEventHook hook.
End Rem
Function PollSystem()
	If _busy Return
	_busy=True
	Driver.Poll
	_busy=False
End Function

Rem
bbdoc: Wait for operating system
about:
#WaitSystem returns control back to the operating system, waiting until 
an event such as a keystroke or gadget action occurs.

Note that #WaitSystem may wait indefinitely if there is no possibility
of any events occuring, so use with caution.

If #WaitSystem encounters a key, mouse or app suspend/resume/terminate 
event, an equivalent #TEvent object will be generated which may be intercepted using
the #EmitEventHook hook.
End Rem
Function WaitSystem()
	If _busy Return
	_busy=True
	Driver.Wait
	_busy=False
End Function

Rem
bbdoc: Get current date
returns: The current date as a string
about:
Returns the current date in the format: DD MON YYYY (i.e. 10 DEC 2000).
End Rem
Function CurrentDate$()
	Local	time[256],buff:Byte[256]
	time_(time)
	strftime_(buff,256,"%d %b %Y",localtime_( time ))
	Return String.FromCString(buff)
End Function

Rem
bbdoc: Get current time
returns: The current time as a string
about:
Returns the current time in the format: HH:MM:SS (i.e. 14:31:57).
End Rem
Function CurrentTime$()
	Local	time[256],buff:Byte[256]
	time_(time)
	strftime_( buff,256,"%H:%M:%S",localtime_( time ) );
	Return String.FromCString(buff)
End Function

Rem
bbdoc: Move mouse pointer
about:
#MoveMouse positions the mouse cursor at a specific location within
the current window or graphics display.
End Rem
Function MoveMouse( x,y )
	Driver.MoveMouse x,y
	_lastx=0
	_lasty=0
	_lastz=0
End Function

Rem
bbdoc: Make the mouse pointer visible
End Rem
Function ShowMouse()
	Driver.SetMouseVisible True
End Function

Rem
bbdoc: Make the mouse pointer invisible
End Rem
Function HideMouse()
	Driver.SetMouseVisible False
End Function

Rem
bbdoc: Notify user
about:
#Notify activates a simple user interface element informing the user of an event.
The optional @serious flag can be used to indicate a 'critical' event.

Note that a user interface may not be available when in graphics mode on some platforms.
End Rem
Function Notify( text$,serious=False )
	Driver.Notify text,serious
End Function

Rem
bbdoc: Request user confirmation.
returns: True or False depending on the user's selection
about:
#Confirm activates a simple user interface element requesting the user to select between
YES and NO options. If the user selects YES, then #Confirm returns True. Otherwise,
False is returned.

Note that a user interface may not be available when in graphics mode on some platforms.
End Rem
Function Confirm( text$,serious=False )
	Return Driver.Confirm( text,serious )
End Function

Rem
bbdoc: Request user confirmation or cancellation.
returns: 1, 0 or -1 depending on the user's selection
about:
#Proceed activates a simple user interface element requesting the user to select between
YES, NO and CANCEL options. If the user selects YES, then #Proceed return 1. If the user
selects NO, then #Proceed returns 0. Otherwise, #Proceed returns -1.

Note that a user interface may not be available when in graphics mode on some platforms.
End Rem
Function Proceed( text$,serious=False )
	Return Driver.Proceed( text,serious )
End Function

Rem
bbdoc: Display system file requester.
returns: The path of the selected file or an empty string if the operation was cancelled.
about:
@text is used as the title of the file requester.

The optional @extensions string can either be a comma separated list of 
file extensions or as in the following example groups of extensions
that begin with a "group:" and separated by a semicolon. 
@save_flag can be True to create a save-style requester, or False to create a load-style requester.

@initial_path is the initial path for the file requester.

Note that a user interface may not be available when in graphics mode on some platforms.
End Rem
Function RequestFile$( text$,extensions$="",save_flag=False,initial_path$="" )
	Return Driver.RequestFile( text,extensions,save_flag,initial_path )
End Function

Rem
bbdoc: Display system folder requester.
returns: The path of the selected folder or an empty string if the operation was cancelled.
about:
@text is used as the title of the file requester.

@initial_path is the initial path for the folder requester.

Note that a user interface may not be available when in graphics mode on some platforms.
End Rem
Function RequestDir$( text$,initial_path$="" )
	Return Driver.RequestDir( text,initial_path )
End Function

Rem
bbdoc: Opens a URL with the system's default web browser.
about: Note that a user interface may not be available when in graphics mode on some platforms.
End Rem
Function OpenURL( url$ )
	Local dev$,anchor$

	dev=url[..5].toLower()
	If dev&lt;&gt;"http:" And dev&lt;&gt;"file:"
		Local h=url.find("#")
		If h&gt;-1
			anchor=url[h..]
			url=url[..h]
		EndIf
		Local f$=RealPath(url)
		If FileType(f) 
			url="file:"+f +anchor
		Else
			url="http:"+url+anchor
		EndIf
	EndIf
	Return Driver.OpenURL( url )
End Function</textarea><br><br><b>BRL.PolledInput</b><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Rem
bbdoc: User input/Polled input
End Rem
Module BRL.PolledInput

ModuleInfo "Version: 1.02"
ModuleInfo "Author: Mark Sibly, Simon Armstrong"
ModuleInfo "License: Blitz Shared Source Code"
ModuleInfo "Copyright: Blitz Research Ltd"
ModuleInfo "Modserver: BRL"

ModuleInfo "History: 1.02 Release"
ModuleInfo "History: Added MouseXSpeed(), MouseYSpeed(), MouseZSpeed()"
ModuleInfo "History: 1.01 Release"
ModuleInfo "History: Fixed charQueue bug"

Import BRL.System

Private

Global enabled
Global autoPoll=True
Global inputSource:Object
Global suspended,terminate
Global keyStates[256],keyHits[256]
Global mouseStates[4],mouseHits[4],mouseLocation[4]
Global charGet,charPut,charQueue[256]

Function Hook:Object( id,data:Object,context:Object )

	Local ev:TEvent=TEvent(data)
	If Not ev Return data
	
	If inputSource And inputSource&lt;&gt;ev.source Return data
	
	Select ev.id
	Case EVENT_KEYDOWN
		If Not keyStates[ev.data]
			keyStates[ev.data]=1
			keyHits[ev.data]:+1
		EndIf
	Case EVENT_KEYUP
		keyStates[ev.data]=0
	Case EVENT_KEYCHAR
		If charPut-charGet&lt;256
			charQueue[charPut &amp; 255]=ev.data
			charPut:+1
		EndIf
	Case EVENT_MOUSEDOWN
		If Not mouseStates[ev.data]
			mouseStates[ev.data]=1
			mouseHits[ev.data]:+1
		EndIf
	Case EVENT_MOUSEUP
		mouseStates[ev.data]=0
	Case EVENT_MOUSEMOVE
		mouseLocation[0]=ev.x
		mouseLocation[1]=ev.y
	Case EVENT_MOUSEWHEEL
		mouseLocation[2]:+ev.data
	Case EVENT_APPSUSPEND
		FlushKeys
		FlushMouse
		suspended=True
	Case EVENT_APPRESUME
		FlushKeys
		FlushMouse
		suspended=False
	Case EVENT_APPTERMINATE
		terminate=True
	End Select

	Return data

End Function

Public

Rem
Currently only called by Graphics/bglCreateContext.
Private for now, as it really needs a source:object parameter.
End Rem
Function EnablePolledInput( source:Object=Null )
	If enabled Return
	inputSource=source
	FlushKeys
	FlushMouse
	AddHook EmitEventHook,Hook,Null,0
	enabled=True
End Function

Rem
Currently only called by EndGraphics/bglDeleteContext
End Rem
Function DisablePolledInput()
	If Not enabled Return
	RemoveHook EmitEventHook,Hook
	FlushKeys
	FlushMouse
	inputSource=Null
	enabled=False
End Function

Rem
bbdoc: Get app suspended state
returns: True if application is currently suspended.
End Rem
Function AppSuspended()
	If autoPoll PollSystem
	Return suspended
End Function

Rem
bbdoc: Return app terminate state
returns: True if user has requested to terminate application
End Rem
Function AppTerminate()
	If autoPoll PollSystem
	Local n=terminate
	terminate=False
	Return n
End Function

Rem
bbdoc: Check for key hit
returns: Number of times @key has been hit.
about:
The returned value represents the number of the times @key has been hit since the last
call to #KeyHit with the same @key.

See the #{key codes} module for a list of valid key codes.
End Rem
Function KeyHit( key )
	If autoPoll PollSystem
	Local n=keyHits[key]
	keyHits[key]=0
	Return n
End Function

Rem
bbdoc: Check for key state
returns: #True if @key is currently down
about:
See the #{key codes} module for a list of valid keycodes.
End Rem
Function KeyDown( key )
	If autoPoll PollSystem
	Return keyStates[key]
End Function

Rem
bbdoc: Get next character
returns: The character code of the next character.
about:
As the user hits keys on the keyboard, BlitzMax records the character codes of these 
keystrokes into an internal 'character queue'.

#GetChar removes the next character code from this queue and returns it the application.

If the character queue is empty, 0 is returned.
End Rem
Function GetChar()
	If autoPoll PollSystem
	If charGet=charPut Return 0
	Local n=charQueue[charGet &amp; 255]
	charGet:+1
	Return n
End Function

Rem
bbdoc: Flush key states and character queue.
about:
#FlushKeys resets the state of all keys to 'off', and resets the character queue
used by #GetChar.
End Rem
Function FlushKeys()
	PollSystem
	charGet=0
	charPut=0
	For Local i=0 Until 256
		keyStates[i]=0
		keyHits[i]=0
	Next
End Function

Rem
bbdoc: Get mouse x location
returns: Mouse x axis location
about:
The returned value is relative to the left of the screen.
end rem
Function MouseX()
	If autoPoll PollSystem
	Return mouseLocation[0]
End Function

Rem
bbdoc: Get mouse y location
returns: Mouse y axis location
about:
The returned value is relative to the top of the screen.
end rem
Function MouseY()
	If autoPoll PollSystem
	Return mouseLocation[1]
End Function

Rem
bbdoc: Get mouse wheel
returns: Mouse wheel value
about:
The mouse wheel value increments when the mouse wheel is rolled 'away' from the user, and
decrements when the mouse wheel is rolled 'towards' the user.
end rem
Function MouseZ()
	If autoPoll PollSystem
	Return mouseLocation[2]
End Function

Rem
bbdoc: Get mouse x speed
returns: Mouse x speed
End Rem
Function MouseXSpeed()
	Local result:Int=MouseX()-brl.system._lastx
	brl.system._lastx=MouseX()
	Return result
EndFunction

Rem
bbdoc: Get mouse y speed
returns: Mouse y speed
End Rem
Function MouseYSpeed()
	Local result:Int=MouseY()-brl.system._lasty
	brl.system._lasty=MouseY()
	Return result
EndFunction

Rem
bbdoc: Get mouse z speed
returns: Mouse z speed
End Rem
Function MouseZSpeed()
	Local result:Int=MouseZ()-brl.system._lastz
	brl.system._lastz=MouseZ()
	Return result
EndFunction

Rem
bbdoc: Flush mouse button states
about:
#FlushMouse resets the state of all mouse buttons to 'off'.
End Rem
Function FlushMouse()
	PollSystem
	For Local i=0 Until 4
		mouseStates[i]=0
		mouseHits[i]=0
	Next
	mouseLocation[2]=0
End Function

Rem
bbdoc: Check for mouse button click
returns: Number of times @button has been clicked.
about:
The returned value represents the number of the times @button has been clicked since the
last call to #MouseHit with the same @button.

@button should be 1 for the left mouse button, 2 for the right mouse button or 3 for the
middle mouse button.
End Rem
Function MouseHit( button )
	If autoPoll PollSystem
	Local n=mouseHits[button]
	mouseHits[button]=0
	Return n
End Function

Rem
bbdoc: Check for mouse button down state
returns: #True if @button is currently down
about:
@button should be 1 for the left mouse button, 2 for the right mouse button or 3 for the
middle mouse button.
end rem
Function MouseDown( button )
	If autoPoll PollSystem
	Return mouseStates[button]
End Function

Rem
bbdoc: Wait for a key press
returns: The keycode of the pressed key
about:
#WaitKey suspends program execution until a key has been hit. The keycode of this
key is then returned to the application.

See the #{key codes} module for a list of valid keycodes.
End Rem
Function WaitKey()
	FlushKeys
	Repeat
		WaitSystem
		For Local n=1 To 255
			If KeyHit(n) Return n
		Next
	Forever
End Function

Rem
bbdoc: Wait for a key press
returns: The character code of the pressed key
about:
#WaitChar suspends program execution until a character is available from #GetChar. This
character is then returned to the application.
End Rem
Function WaitChar()
	FlushKeys
	Repeat
		WaitSystem
		Local n=GetChar()
		If n Return n
	Forever
End Function

Rem
bbdoc: Wait for mouse button click
returns: The clicked button
about:
#WaitMouse suspends program execution until a mouse button is clicked.

#WaitMouse returns 1 if the left mouse button was clicked, 2 if the right mouse button was
clicked or 3 if the middle mouse button was clicked.
End Rem
Function WaitMouse()
	FlushMouse
	Repeat
		WaitSystem
		For Local n=1 To 3
			If MouseHit(n) Return n
		Next
	Forever
End Function</textarea><br><br>Since this is a private forum, I hope I'm allowed to post the source to these modules! Hopefully, these modifications can be added to SVN? <br><br></td></tr></table><br>
<a name="932911"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Does it provide the same results on all platforms? <br><br></td></tr></table><br>
<a name="932917"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ked</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I only have a Windows computer, but it should. I didn't add any platform specific code. <br><br></td></tr></table><br>
<a name="932954"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Can I have some test code for Linux then, coz I'm lazy. <br><br></td></tr></table><br>
<a name="932955"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yan</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hmmm...I obviously didn't give enough detail in the other thread, so I fixed the incorrect Z axis handling...<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict

Rem
bbdoc: System/System
End Rem
Module BRL.System

ModuleInfo "Version: 1.27"
ModuleInfo "Author: Mark Sibly, Simon Armstrong"
ModuleInfo "License: Blitz Shared Source Code"
ModuleInfo "Copyright: Blitz Research Ltd"
ModuleInfo "Modserver: BRL"

ModuleInfo "History: 1.27 Release"
ModuleInfo "History: Add support for MouseXSpeed(),MouseYSpeed(),MouseZSpeed()"
ModuleInfo "History: 1.26 Release"
ModuleInfo "History: Moved event enums from system.h to event.mod/event.h"
ModuleInfo "Histroy: Moved keycode enums from system.h to keycodes.mod/keycodes.h"
ModuleInfo "History: 1.26 Release"
ModuleInfo "History: Fixed win32 filerequester causing activewindow dramas"
ModuleInfo "History: 1.25 Release"
ModuleInfo "History: Fixed unretained object issue with mouse tracking"
ModuleInfo "History: 1.24 Release"
ModuleInfo "History: Fixed windowed mode HideMouse issue"
ModuleInfo "History: 1.23 Release"
ModuleInfo "History: Fixed win32 requestfile default extension bug#2"
ModuleInfo "History: 1.22 Release"
ModuleInfo "History: Fixed win32 requestfile default extension bug"
ModuleInfo "History: 1.21 Release"
ModuleInfo "History: New Linux implementation of OpenURL"
ModuleInfo "History: 1.20 Release"
ModuleInfo "History: RequestFile now adds extension to filename on Windows"
ModuleInfo "History: 1.19 Release"
ModuleInfo "History: Added EVENT_GADGETLOSTFOCUS handling"
ModuleInfo "History: 1.18 Release"
ModuleInfo "History: Added EVENT_KEYREPEAT handling"
ModuleInfo "History: 1.17 Release"
ModuleInfo "History: OpenURL now attempts to fully qualify file / http url supplied"
ModuleInfo "History: 1.16 Release"
ModuleInfo "History: Fixed MacOS RequestFile to respect wild card filter"
ModuleInfo "History: 1.15 Release"
ModuleInfo "History: Fixed mouse hidden by default"
ModuleInfo "History: 1.14 Release"
ModuleInfo "History: Fixed HideMouse causing mouse to disappear when in non-client areas"
ModuleInfo "History: 1.13 Release"
ModuleInfo "History: Fixed Linux MoveMouse to be relative to the origin of the current Graphics window"
ModuleInfo "History: 1.12 Release"
ModuleInfo "History: Added Linux X11 import to remove glgraphics.mod dependency"
ModuleInfo "History: Fixed linux middle button crash"
ModuleInfo "History: 1.11 Release"
ModuleInfo "History: Fixed win32 clipboard glitches with QS_ALLINPUT bbSystemWait mod"
ModuleInfo "History: 1.11 Release"
ModuleInfo "History: CGSetLocalEventsSuppressionInterval fix for MacOS bbSystemMoveMouse"
ModuleInfo "History: 1.10 Release"
ModuleInfo "History: Ripped out input stuff and added hook"
ModuleInfo "History: 1.09 Release"
ModuleInfo "History: Tweaked MacOS GetChar()"
ModuleInfo "History: 1.07 Release"
ModuleInfo "History: Added AppTitle support for requesters"
ModuleInfo "History: 1.06 Release"
ModuleInfo "History: Fixed MacOS RequestDir ignoring initial path"
ModuleInfo "History: 1.05 Release"
ModuleInfo "History: Added RequestDir support for MacOS"
ModuleInfo "History: 1.04 Release"
ModuleInfo "History: Added mouse capture to Win32"
ModuleInfo "History: Fixed C Compiler warnings"

Import BRL.Event
Import BRL.KeyCodes
Import BRL.Hook
Import BRL.FileSystem
Import Pub.StdC

Import "driver.bmx"

Import "system.c"

?MacOS
Import "system.macos.bmx"
Driver=New TMacOSSystemDriver
?Win32
Import "system.win32.bmx"
Import "-lcomdlg32"
Driver=New TWin32SystemDriver
?Linux
Import "-lX11"
Import "system.linux.bmx"
Driver=New TLinuxSystemDriver
?

Private
Global _busy
Public

Global _lastx:Int=0,_lasty:Int=0,_lastz:Int=0

Rem
bbdoc: Poll operating system
about:
#PollSystem returns control back to the operating system, allowing such
events as keystrokes and gadget actions to be processed. Control is then
returned back to your program.

If #PollSystem encounters a key, mouse or app suspend/resume/terminate 
event, an equivalent #TEvent object event will be generated which may be intercepted using
the #EmitEventHook hook.
End Rem
Function PollSystem()
	If _busy Return
	_busy=True
	Driver.Poll
	_busy=False
End Function

Rem
bbdoc: Wait for operating system
about:
#WaitSystem returns control back to the operating system, waiting until 
an event such as a keystroke or gadget action occurs.

Note that #WaitSystem may wait indefinitely if there is no possibility
of any events occuring, so use with caution.

If #WaitSystem encounters a key, mouse or app suspend/resume/terminate 
event, an equivalent #TEvent object will be generated which may be intercepted using
the #EmitEventHook hook.
End Rem
Function WaitSystem()
	If _busy Return
	_busy=True
	Driver.Wait
	_busy=False
End Function

Rem
bbdoc: Get current date
returns: The current date as a string
about:
Returns the current date in the format: DD MON YYYY (i.e. 10 DEC 2000).
End Rem
Function CurrentDate$()
	Local	time[256],buff:Byte[256]
	time_(time)
	strftime_(buff,256,"%d %b %Y",localtime_( time ))
	Return String.FromCString(buff)
End Function

Rem
bbdoc: Get current time
returns: The current time as a string
about:
Returns the current time in the format: HH:MM:SS (i.e. 14:31:57).
End Rem
Function CurrentTime$()
	Local	time[256],buff:Byte[256]
	time_(time)
	strftime_( buff,256,"%H:%M:%S",localtime_( time ) );
	Return String.FromCString(buff)
End Function

Rem
bbdoc: Move mouse pointer
about:
#MoveMouse positions the mouse cursor at a specific location within
the current window or graphics display.
End Rem
Function MoveMouse( x,y )
	Driver.MoveMouse x,y
	_lastx=0
	_lasty=0
End Function

Rem
bbdoc: Make the mouse pointer visible
End Rem
Function ShowMouse()
	Driver.SetMouseVisible True
End Function

Rem
bbdoc: Make the mouse pointer invisible
End Rem
Function HideMouse()
	Driver.SetMouseVisible False
End Function

Rem
bbdoc: Notify user
about:
#Notify activates a simple user interface element informing the user of an event.
The optional @serious flag can be used to indicate a 'critical' event.

Note that a user interface may not be available when in graphics mode on some platforms.
End Rem
Function Notify( text$,serious=False )
	Driver.Notify text,serious
End Function

Rem
bbdoc: Request user confirmation.
returns: True or False depending on the user's selection
about:
#Confirm activates a simple user interface element requesting the user to select between
YES and NO options. If the user selects YES, then #Confirm returns True. Otherwise,
False is returned.

Note that a user interface may not be available when in graphics mode on some platforms.
End Rem
Function Confirm( text$,serious=False )
	Return Driver.Confirm( text,serious )
End Function

Rem
bbdoc: Request user confirmation or cancellation.
returns: 1, 0 or -1 depending on the user's selection
about:
#Proceed activates a simple user interface element requesting the user to select between
YES, NO and CANCEL options. If the user selects YES, then #Proceed return 1. If the user
selects NO, then #Proceed returns 0. Otherwise, #Proceed returns -1.

Note that a user interface may not be available when in graphics mode on some platforms.
End Rem
Function Proceed( text$,serious=False )
	Return Driver.Proceed( text,serious )
End Function

Rem
bbdoc: Display system file requester.
returns: The path of the selected file or an empty string if the operation was cancelled.
about:
@text is used as the title of the file requester.

The optional @extensions string can either be a comma separated list of 
file extensions or as in the following example groups of extensions
that begin with a "group:" and separated by a semicolon. 
@save_flag can be True to create a save-style requester, or False to create a load-style requester.

@initial_path is the initial path for the file requester.

Note that a user interface may not be available when in graphics mode on some platforms.
End Rem
Function RequestFile$( text$,extensions$="",save_flag=False,initial_path$="" )
	Return Driver.RequestFile( text,extensions,save_flag,initial_path )
End Function

Rem
bbdoc: Display system folder requester.
returns: The path of the selected folder or an empty string if the operation was cancelled.
about:
@text is used as the title of the file requester.

@initial_path is the initial path for the folder requester.

Note that a user interface may not be available when in graphics mode on some platforms.
End Rem
Function RequestDir$( text$,initial_path$="" )
	Return Driver.RequestDir( text,initial_path )
End Function

Rem
bbdoc: Opens a URL with the system's default web browser.
about: Note that a user interface may not be available when in graphics mode on some platforms.
End Rem
Function OpenURL( url$ )
	Local dev$,anchor$

	dev=url[..5].toLower()
	If dev&lt;&gt;"http:" And dev&lt;&gt;"file:"
		Local h=url.find("#")
		If h&gt;-1
			anchor=url[h..]
			url=url[..h]
		EndIf
		Local f$=RealPath(url)
		If FileType(f) 
			url="file:"+f +anchor
		Else
			url="http:"+url+anchor
		EndIf
	EndIf
	Return Driver.OpenURL( url )
End Function</textarea><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict

Rem
bbdoc: User input/Polled input
End Rem
Module BRL.PolledInput

ModuleInfo "Version: 1.02"
ModuleInfo "Author: Mark Sibly, Simon Armstrong"
ModuleInfo "License: Blitz Shared Source Code"
ModuleInfo "Copyright: Blitz Research Ltd"
ModuleInfo "Modserver: BRL"

ModuleInfo "History: 1.02 Release"
ModuleInfo "History: Added MouseXSpeed(), MouseYSpeed(), MouseZSpeed()"
ModuleInfo "History: 1.01 Release"
ModuleInfo "History: Fixed charQueue bug"

Import BRL.System

Private

Global enabled
Global autoPoll=True
Global inputSource:Object
Global suspended,terminate
Global keyStates[256],keyHits[256]
Global mouseStates[4],mouseHits[4],mouseLocation[4]
Global charGet,charPut,charQueue[256]

Function Hook:Object( id,data:Object,context:Object )

	Local ev:TEvent=TEvent(data)
	If Not ev Return data
	
	If inputSource And inputSource&lt;&gt;ev.source Return data
	
	Select ev.id
	Case EVENT_KEYDOWN
		If Not keyStates[ev.data]
			keyStates[ev.data]=1
			keyHits[ev.data]:+1
		EndIf
	Case EVENT_KEYUP
		keyStates[ev.data]=0
	Case EVENT_KEYCHAR
		If charPut-charGet&lt;256
			charQueue[charPut &amp; 255]=ev.data
			charPut:+1
		EndIf
	Case EVENT_MOUSEDOWN
		If Not mouseStates[ev.data]
			mouseStates[ev.data]=1
			mouseHits[ev.data]:+1
		EndIf
	Case EVENT_MOUSEUP
		mouseStates[ev.data]=0
	Case EVENT_MOUSEMOVE
		mouseLocation[0]=ev.x
		mouseLocation[1]=ev.y
	Case EVENT_MOUSEWHEEL
		mouseLocation[2]:+ev.data
	Case EVENT_APPSUSPEND
		FlushKeys
		FlushMouse
		suspended=True
	Case EVENT_APPRESUME
		FlushKeys
		FlushMouse
		suspended=False
	Case EVENT_APPTERMINATE
		terminate=True
	End Select

	Return data

End Function

Public

Rem
Currently only called by Graphics/bglCreateContext.
Private for now, as it really needs a source:object parameter.
End Rem
Function EnablePolledInput( source:Object=Null )
	If enabled Return
	inputSource=source
	FlushKeys
	FlushMouse
	AddHook EmitEventHook,Hook,Null,0
	enabled=True
End Function

Rem
Currently only called by EndGraphics/bglDeleteContext
End Rem
Function DisablePolledInput()
	If Not enabled Return
	RemoveHook EmitEventHook,Hook
	FlushKeys
	FlushMouse
	inputSource=Null
	enabled=False
End Function

Rem
bbdoc: Get app suspended state
returns: True if application is currently suspended.
End Rem
Function AppSuspended()
	If autoPoll PollSystem
	Return suspended
End Function

Rem
bbdoc: Return app terminate state
returns: True if user has requested to terminate application
End Rem
Function AppTerminate()
	If autoPoll PollSystem
	Local n=terminate
	terminate=False
	Return n
End Function

Rem
bbdoc: Check for key hit
returns: Number of times @key has been hit.
about:
The returned value represents the number of the times @key has been hit since the last
call to #KeyHit with the same @key.

See the #{key codes} module for a list of valid key codes.
End Rem
Function KeyHit( key )
	If autoPoll PollSystem
	Local n=keyHits[key]
	keyHits[key]=0
	Return n
End Function

Rem
bbdoc: Check for key state
returns: #True if @key is currently down
about:
See the #{key codes} module for a list of valid keycodes.
End Rem
Function KeyDown( key )
	If autoPoll PollSystem
	Return keyStates[key]
End Function

Rem
bbdoc: Get next character
returns: The character code of the next character.
about:
As the user hits keys on the keyboard, BlitzMax records the character codes of these 
keystrokes into an internal 'character queue'.


#GetChar removes the next character code from this queue and returns it the application.

If the character queue is empty, 0 is returned.
End Rem
Function GetChar()
	If autoPoll PollSystem
	If charGet=charPut Return 0
	Local n=charQueue[charGet &amp; 255]
	charGet:+1
	Return n
End Function

Rem
bbdoc: Flush key states and character queue.
about:
#FlushKeys resets the state of all keys to 'off', and resets the character queue
used by #GetChar.
End Rem
Function FlushKeys()
	PollSystem
	charGet=0
	charPut=0
	For Local i=0 Until 256
		keyStates[i]=0
		keyHits[i]=0
	Next
End Function

Rem
bbdoc: Get mouse x location
returns: Mouse x axis location
about:
The returned value is relative to the left of the screen.
end rem
Function MouseX()
	If autoPoll PollSystem
	Return mouseLocation[0]
End Function

Rem
bbdoc: Get mouse y location
returns: Mouse y axis location
about:
The returned value is relative to the top of the screen.
end rem
Function MouseY()
	If autoPoll PollSystem
	Return mouseLocation[1]
End Function

Rem
bbdoc: Get mouse wheel
returns: Mouse wheel value
about:
The mouse wheel value increments when the mouse wheel is rolled 'away' from the user, and
decrements when the mouse wheel is rolled 'towards' the user.
end rem
Function MouseZ()
	If autoPoll PollSystem
	Return mouseLocation[2]
End Function

Rem
bbdoc: Get mouse x speed
returns: Mouse x speed
End Rem
Function MouseXSpeed()
	Local result:Int=MouseX()-brl.system._lastx
	brl.system._lastx=MouseX()
	Return result
EndFunction

Rem
bbdoc: Get mouse y speed
returns: Mouse y speed
End Rem
Function MouseYSpeed()
	Local result:Int=MouseY()-brl.system._lasty
	brl.system._lasty=MouseY()
	Return result
EndFunction

Rem
bbdoc: Get mouse z speed
returns: Mouse z speed
End Rem
Function MouseZSpeed()
	Local result:Int=MouseZ()-brl.system._lastz
	brl.system._lastz=MouseZ()
	Return result
EndFunction

Rem
bbdoc: Flush mouse button states
about:
#FlushMouse resets the state of all mouse buttons to 'off'.
End Rem
Function FlushMouse()
	PollSystem
	For Local i=0 Until 4
		mouseStates[i]=0
		mouseHits[i]=0
	Next
	mouseLocation[2]=0
	brl.system._lastz = 0
End Function

Rem
bbdoc: Check for mouse button click
returns: Number of times @button has been clicked.
about:
The returned value represents the number of the times @button has been clicked since the
last call to #MouseHit with the same @button.

@button should be 1 for the left mouse button, 2 for the right mouse button or 3 for the
middle mouse button.
End Rem
Function MouseHit( button )
	If autoPoll PollSystem
	Local n=mouseHits[button]
	mouseHits[button]=0
	Return n
End Function

Rem
bbdoc: Check for mouse button down state
returns: #True if @button is currently down
about:
@button should be 1 for the left mouse button, 2 for the right mouse button or 3 for the
middle mouse button.
end rem
Function MouseDown( button )
	If autoPoll PollSystem
	Return mouseStates[button]
End Function

Rem
bbdoc: Wait for a key press
returns: The keycode of the pressed key
about:
#WaitKey suspends program execution until a key has been hit. The keycode of this
key is then returned to the application.

See the #{key codes} module for a list of valid keycodes.
End Rem
Function WaitKey()
	FlushKeys
	Repeat
		WaitSystem
		For Local n=1 To 255
			If KeyHit(n) Return n
		Next
	Forever
End Function

Rem
bbdoc: Wait for a key press
returns: The character code of the pressed key
about:
#WaitChar suspends program execution until a character is available from #GetChar. This
character is then returned to the application.
End Rem
Function WaitChar()
	FlushKeys
	Repeat
		WaitSystem
		Local n=GetChar()
		If n Return n
	Forever
End Function

Rem
bbdoc: Wait for mouse button click
returns: The clicked button
about:
#WaitMouse suspends program execution until a mouse button is clicked.

#WaitMouse returns 1 if the left mouse button was clicked, 2 if the right mouse button was
clicked or 3 if the middle mouse button was clicked.
End Rem
Function WaitMouse()
	FlushMouse
	Repeat
		WaitSystem
		For Local n=1 To 3
			If MouseHit(n) Return n
		Next
	Forever
End Function</textarea> <br><br></td></tr></table><br>
<a name="932975"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ked</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Can I have some test code for Linux then, coz I'm lazy. <br></div><br>Sure.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict
Framework BRL.Max2D
Import BRL.GLMax2D

SetGraphicsDriver GLMax2DDriver()

Graphics 800,600

Repeat
	If KeyHit(KEY_ESCAPE)
		End
	EndIf
	If AppTerminate()
		End
	EndIf
	
	Cls
	
	Local mxs:Int=MouseXSpeed()
	Local mys:Int=MouseYSpeed()
	Local mzs:Int=MouseZSpeed()
	
	DrawText "Mouse X Speed: "+mxs,5,5
	DrawText "Mouse Y Speed: "+mys,5,15
	DrawText "Mouse Z Speed: "+mzs,5,25
	DrawText "[M] to reset mouse position",5,35
	
	If KeyHit(KEY_M)
		MoveMouse 400,300
	EndIf
	
	Flip
Forever</textarea><br><br>Here's the MiniB3D example I used:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Import sidesign.minib3d

Local width=640,height=480,depth=32,mode=2

Graphics3D width,height,depth,mode

Local cam=CreateCamera()
PositionEntity cam,0,0,-5

Local light=CreateLight(1)

'Local tex=LoadTexture("media/test.png")

Local cube=CreateCube()
Local sphere=CreateSphere()
Local cylinder=CreateCylinder()
Local cone=CreateCone() 

PositionEntity cube,-3,0,0
PositionEntity sphere,-1,0,0
PositionEntity cylinder,1,0,0
PositionEntity cone,3,0,0

'EntityTexture cube,tex
'EntityTexture sphere,tex
'EntityTexture cylinder,tex
'EntityTexture cone,tex

;' used by camera code
Local mxs#=0
Local mys#=0
Local move#=0.5
MouseXSpeed() ;' flush
MouseYSpeed() ;' flush

;' used by fps code
Local old_ms=MilliSecs()
Local renders
Local fps

Local aa

While Not KeyDown(KEY_ESCAPE)		

	If KeyHit(KEY_ENTER) Then DebugStop

	;'' control camera
	
	;' mouse look
	
	If KeyDown(KEY_SPACE)=False
	
		mxs#=mxs#+(MouseXSpeed()/5.0)
		mys#=mys#+(MouseYSpeed()/5.0)
	
		RotateEntity cam,mys#,-mxs#,0
	
		MoveMouse width/2,height/2
	
	EndIf
	
	MouseXSpeed() ;' flush
	MouseYSpeed() ;' flush

	;' move camera forwards/backwards/left/right with cursor keys
	
	If KeyDown(KEY_UP)=True Then MoveEntity cam,0,0,move# ;' move camera forward
	If KeyDown(KEY_DOWN)=True Then MoveEntity cam,0,0,-move# ;' move camera back

	If KeyDown(KEY_LEFT)=True Then MoveEntity cam,-move#,0,0 ;' move camera left
	If KeyDown(KEY_RIGHT)=True Then MoveEntity cam,move#,0,0 ;' move camera right
	
	;''
		
	If KeyHit(KEY_MINUS)
	
		aa=aa-1
		If aa=-1 Then aa=16
		If aa=15 Then aa=12
		If aa=11 Then aa=9
		If aa=7 Then aa=6
		If aa=1 Then aa=0
		AntiAlias aa
	
	EndIf
	
	If KeyHit(KEY_EQUALS)
	
		aa=aa+1
		If aa=1 Then aa=2
		If aa=7 Then aa=9
		If aa=10 Then aa=12
		If aa=13 Then aa=16
		If aa=17 Then aa=0
		AntiAlias aa
	
	EndIf
	
	TurnEntity cube,0,1,0

	RenderWorld
	renders=renders+1

	;' calculate fps
	If MilliSecs()-old_ms&gt;=1000
		old_ms=MilliSecs()
		fps=renders
		renders=0
	EndIf
	
	Text 0,0,"FPS: "+fps
	Text 0,20,"+/- to change AA value"
	Text 0,40,"AntiAlias "+aa

	Flip

Wend
End</textarea> <br><br></td></tr></table><br>
<a name="933119"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Tested in window/fullscreen on ubuntu hardy, also tested Yan's version. Both work fine.<br><br><div class="quote"> Fixed the incorrect Z axis handling... <br></div><br>I'm not sure those two additions are necessary since mouse z state resets itself. <br><br></td></tr></table><br>
<a name="933143"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yan</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm afraid, you're mistaken. ;op<br><br>Try this with the original version...<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict
Framework BRL.Max2D
Import BRL.GLMax2D

'SetGraphicsDriver GLMax2DDriver()

Graphics 800,600

Local scale# = 1

Repeat
	Local mxs:Int=MouseXSpeed()
	Local mys:Int=MouseYSpeed()
	
	scale# :+ (MouseZSpeed() * 0.25)
	
	If KeyHit(KEY_M)
		MoveMouse 400,300
	EndIf
	
	Cls
		
	SetScale scale#, scale#
	SetHandle 50, 50
	DrawRect 400, 300, 100, 100		
	
	SetScale 1, 1
	SetHandle 0, 0
	DrawText "Mouse X Speed: "+mxs, 12, 12
	DrawText "Mouse Y Speed: "+mys, 12, 24

	DrawText "Mousewheel to zoom", 12, 48
	DrawText "[M] to reset mouse position", 12, 60
	
	Flip
Until KeyHit(KEY_ESCAPE) Or AppTerminate()

End</textarea><br>...Play around with the zoom and then press 'M'...Oops... ;o)<br><br>The same thing will happen when focus is lost too.<br><br><br>Hence the *necessary* modifications. :o)<br><br>[edit]<br>You may have to increase the scale multiplier to see the effect clearly as I haven't actually tried either version, TBH.<br>[/edit] <br><br></td></tr></table><br>
<a name="933169"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, thanks. I see what you mean. <br><br></td></tr></table><br>
<a name="940667"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ked</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Can someone with a Mac test this? <br><br></td></tr></table><br>
<a name="943620"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ked</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> It would be very helpful if someone with a Mac could test this. I really would like for this to be included in the next update. <br><br></td></tr></table><br>
<a name="962372"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Ked, works as expected on my panther g3 ppc laptop.<br><br>I only tested mousex and y because of the trackpad though. I'll try to get a usb mouse to test mouse z. I used Yan's tweaked code and your basic test code as I don't have minib3d installed.<br><br>Also, could you wrap your code in codebox tags? Would be nice. <br><br></td></tr></table><br>
<a name="962432"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ked</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks, markcw! Let me know what you get with the usb mouse.<br><br>Updated top. <br><br></td></tr></table><br>
<a name="962590"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, I got a usb mouse this morning and mouse z also works fine in your basic test code.<br><br>Cheers for the edit. <br><br></td></tr></table><br>
<a name="962790"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ked</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Awesome!<br><br>I have a question, though: What kind of results were you getting? Mark wanted to make sure it worked on Mac because:<br><div class="quote"> Mac ... seems to sample mouse at more frequent intervals... <br></div> <br><br></td></tr></table><br>
<a name="962860"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well I'm not sure what you mean. The basic test code really only prints negative or positive values and then returns to zero, as for the values I didn't notice anything unusual. <br><br></td></tr></table><br>
<a name="963232"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ked</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Well I'm not sure what you mean. The basic test code really only prints negative or positive values and then returns to zero, as for the values I didn't notice anything unusual. <br></div><br>I meant if you were getting any doubled return values or something like that.<br><br>Anyway, thanks again for your help! Emailing Mark now... <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
