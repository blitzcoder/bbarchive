<!DOCTYPE html><html lang="en" ><head ><title >brl.map docs</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >brl.map docs</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=110" >BlitzMax Module Tweaks</a>/<a href="#bottom" >brl.map docs</a><br><br>
<a name="1036944"></a>

<a name="1221948"></a>

<a name="1222186"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is an update to the brl.map docs. As in <a href="/posts.php?topic=91098" target="_blank">brl.linkedlist docs</a> Plash deserves credit for the original examples.<br><br>The files are zipped in a doc folder (module source not included according to the BSSC license).<br>map-doc.zip (expired)<br><br>I'm posting the module source (from v 1.41) and the patch.<br><br>Last edit: 16 Aug 2010<br><br>map.diff<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">--- map-old.bmx	2010-07-15 22:35:02.103676351 +0100
+++ map.bmx	2010-08-16 19:45:18.826134055 +0100
@@ -481,7 +481,7 @@
 Rem
 bbdoc: Clear a map
 about:
-#ClearMap removes all keys and values from @map
+Removes all keys and values from @map.
 End Rem
 Function ClearMap( map:TMap )
 	map.Clear
@@ -498,7 +498,7 @@
 Rem
 bbdoc: Insert a key/value pair into a map
 about:
-If @map already contained @key, it's value is overwritten with @value. 
+If @map already contained @key, its value is overwritten with @value. 
 End Rem
 Function MapInsert( map:TMap,key:Object,value:Object )
 	map.Insert key,value
@@ -508,7 +508,7 @@
 bbdoc: Find a value given a key
 returns: The value associated with @key
 about:
-If @map does not contain @key, a #Null object is returned.
+If @map does not contain @key, a Null object is returned.
 End Rem
 Function MapValueForKey:Object( map:TMap,key:Object )
 	Return map.ValueForKey( key )
@@ -516,7 +516,7 @@
 
 Rem
 bbdoc: Check if a map contains a key
-returns: True if @map contains @key
+returns: True if @map contains @key, otherwise false
 End Rem
 Function MapContains( map:TMap,key:Object )
 	Return map.Contains( key )
</textarea><br><br>map.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Rem
bbdoc: Data structures/Maps
End Rem
Module BRL.Map

ModuleInfo "Version: 1.07"
ModuleInfo "Author: Mark Sibly"
ModuleInfo "License: Blitz Shared Source Code"
ModuleInfo "Copyright: Blitz Research Ltd"
ModuleInfo "Modserver: BRL"

ModuleInfo "History: 1.07 Release"
ModuleInfo "History: Fixed MapKeys/MapValues functions to return enumerators"
ModuleInfo "History: 1.06 Release"
ModuleInfo "History: Restored KeyValue enumerator"
ModuleInfo "History: 1.05 Release"
ModuleInfo "History: Added Copy method"
ModuleInfo "History: 1.04 Release"
ModuleInfo "History: Fixed Clear memleak"
ModuleInfo "History: 1.03 Release"
ModuleInfo "History: Finally changed to red/back tree!"
ModuleInfo "History: Added procedural interface"
ModuleInfo "History: 1.02 Release"
ModuleInfo "History: Fixed TMap.Remove:TNode not returning node"

Private

Global nil:TNode=New TNode

nil._color=TMap.BLACK
nil._parent=nil
nil._left=nil
nil._right=nil

Public

Type TKeyValue

	Method Key:Object()
		Return _key
	End Method
	
	Method Value:Object()
		Return _value
	End Method
	
	'***** PRIVATE *****

	Field _key:Object,_value:Object

End Type

Type TNode Extends TKeyValue

	Method NextNode:TNode()
		Local node:TNode=Self
		If node._right&lt;&gt;nil
			node=_right
			While node._left&lt;&gt;nil
				node=node._left
			Wend
			Return node
		EndIf
		Local parent:TNode=_parent
		While node=parent._right
			node=parent
			parent=parent._parent
		Wend
		Return parent
	End Method
	
	Method PrevNode:TNode()
		Local node:TNode=Self
		If node._left&lt;&gt;nil
			node=node._left
			While node._right&lt;&gt;nil
				node=node._right
			Wend
			Return node
		EndIf
		Local parent:TNode=node._parent
		While node=parent._left
			node=parent
			parent=node._parent
		Wend
		Return parent
	End Method
	
	Method Clear()
		_parent=Null
		If _left&lt;&gt;nil _left.Clear
		If _right&lt;&gt;nil _right.Clear
	End Method
	
	Method Copy:TNode( parent:TNode )
		Local t:TNode=New TNode
		t._key=_key
		t._value=_value
		t._color=_color
		t._parent=parent
		If _left&lt;&gt;nil t._left=_left.Copy( t )
		If _right&lt;&gt;nil t._right=_right.Copy( t )
		Return t
	End Method
	
	Method Key:Object()
		Return _key
	End Method
	
	Method Value:Object()
		Return _value
	End Method

	'***** PRIVATE *****
	
	Field _color,_parent:TNode=nil,_left:TNode=nil,_right:TNode=nil

End Type

Type TNodeEnumerator
	Method HasNext()
		Return _node&lt;&gt;nil
	End Method
	
	Method NextObject:Object()
		Local node:TNode=_node
		_node=_node.NextNode()
		Return node
	End Method

	'***** PRIVATE *****
		
	Field _node:TNode	
End Type

Type TKeyEnumerator Extends TNodeEnumerator
	Method NextObject:Object()
		Local node:TNode=_node
		_node=_node.NextNode()
		Return node._key
	End Method
End Type

Type TValueEnumerator Extends TNodeEnumerator
	Method NextObject:Object()
		Local node:TNode=_node
		_node=_node.NextNode()
		Return node._value
	End Method
End Type

Type TMapEnumerator
	Method ObjectEnumerator:TNodeEnumerator()
		Return _enumerator
	End Method
	Field _enumerator:TNodeEnumerator
End Type

'***** PUBLIC *****

Type TMap

?Not Threaded
	Method Delete()
		Clear
	End Method
?
	Method Clear()
		If _root=nil Return
		_root.Clear
		_root=nil
	End Method
	
	Method IsEmpty()
		Return _root=nil
	End Method
	
	Method Insert( key:Object,value:Object )

		Assert key Else "Can't insert Null key into map"

		Local node:TNode=_root,parent:TNode=nil,cmp
		
		While node&lt;&gt;nil
			parent=node
			cmp=key.Compare( node._key )
			If cmp&gt;0
				node=node._right
			Else If cmp&lt;0
				node=node._left
			Else
				node._value=value
				Return
			EndIf
		Wend
		
		node=New TNode
		node._key=key
		node._value=value
		node._color=RED
		node._parent=parent
		
		If parent=nil
			_root=node
			Return
		EndIf
		If cmp&gt;0
			parent._right=node
		Else
			parent._left=node
		EndIf
		
		_InsertFixup node
	End Method
	
	Method Contains( key:Object )
		Return _FindNode( key )&lt;&gt;nil
	End Method

	Method ValueForKey:Object( key:Object )
		Local node:TNode=_FindNode( key )
		If node&lt;&gt;nil Return node._value
	End Method
	
	Method Remove( key:Object )
		Local node:TNode=_FindNode( key )
		If node=nil Return 0
		 _RemoveNode node
		Return 1
	End Method
	
	Method Keys:TMapEnumerator()
		Local nodeenum:TNodeEnumerator=New TKeyEnumerator
		nodeenum._node=_FirstNode()
		Local mapenum:TMapEnumerator=New TMapEnumerator
		mapenum._enumerator=nodeenum
		Return mapenum
	End Method
	
	Method Values:TMapEnumerator()
		Local nodeenum:TNodeEnumerator=New TValueEnumerator
		nodeenum._node=_FirstNode()
		Local mapenum:TMapEnumerator=New TMapEnumerator
		mapenum._enumerator=nodeenum
		Return mapenum
	End Method
	
	Method Copy:TMap()
		Local map:TMap=New TMap
		map._root=_root.Copy( nil )
		Return map
	End Method
	
	Method ObjectEnumerator:TNodeEnumerator()
		Local nodeenum:TNodeEnumerator=New TNodeEnumerator
		nodeenum._node=_FirstNode()
		Return nodeenum
	End Method
	
	'***** PRIVATE *****
	
	Method _FirstNode:TNode()
		Local node:TNode=_root
		While node._left&lt;&gt;nil
			node=node._left
		Wend
		Return node
	End Method
	
	Method _LastNode:TNode()
		Local node:TNode=_root
		While node._right&lt;&gt;nil
			node=node._right
		Wend
		Return node
	End Method
	
	Method _FindNode:TNode( key:Object )
		Local node:TNode=_root
		While node&lt;&gt;nil
			Local cmp=key.Compare( node._key )
			If cmp&gt;0
				node=node._right
			Else If cmp&lt;0
				node=node._left
			Else
				Return node
			EndIf
		Wend
		Return node
	End Method
	
	Method _RemoveNode( node:TNode )
		Local splice:TNode,child:TNode
		
		If node._left=nil
			splice=node
			child=node._right
		Else If node._right=nil
			splice=node
			child=node._left
		Else
			splice=node._left
			While splice._right&lt;&gt;nil
				splice=splice._right
			Wend
			child=splice._left
			node._key=splice._key
			node._value=splice._value
		EndIf
		Local parent:TNode=splice._parent
		If child&lt;&gt;nil
			child._parent=parent
		EndIf
		If parent=nil
			_root=child
			Return
		EndIf
		If splice=parent._left
			parent._left=child
		Else
			parent._right=child
		EndIf
		
		If splice._color=BLACK _DeleteFixup child,parent
	End Method
	
	Method _InsertFixup( node:TNode )
		While node._parent._color=RED And node._parent._parent&lt;&gt;nil
			If node._parent=node._parent._parent._left
				Local uncle:TNode=node._parent._parent._right
				If uncle._color=RED
					node._parent._color=BLACK
					uncle._color=BLACK
					uncle._parent._color=RED
					node=uncle._parent
				Else
					If node=node._parent._right
						node=node._parent
						_RotateLeft node
					EndIf
					node._parent._color=BLACK
					node._parent._parent._color=RED
					_RotateRight node._parent._parent
				EndIf
			Else
				Local uncle:TNode=node._parent._parent._left
				If uncle._color=RED
					node._parent._color=BLACK
					uncle._color=BLACK
					uncle._parent._color=RED
					node=uncle._parent
				Else
					If node=node._parent._left
						node=node._parent
						_RotateRight node
					EndIf
					node._parent._color=BLACK
					node._parent._parent._color=RED
					_RotateLeft node._parent._parent
				EndIf
			EndIf
		Wend
		_root._color=BLACK
	End Method
	
	Method _RotateLeft( node:TNode )
		Local child:TNode=node._right
		node._right=child._left
		If child._left&lt;&gt;nil
			child._left._parent=node
		EndIf
		child._parent=node._parent
		If node._parent&lt;&gt;nil
			If node=node._parent._left
				node._parent._left=child
			Else
				node._parent._right=child
			EndIf
		Else
			_root=child
		EndIf
		child._left=node
		node._parent=child
	End Method
	
	Method _RotateRight( node:TNode )
		Local child:TNode=node._left
		node._left=child._right
		If child._right&lt;&gt;nil
			child._right._parent=node
		EndIf
		child._parent=node._parent
		If node._parent&lt;&gt;nil
			If node=node._parent._right
				node._parent._right=child
			Else
				node._parent._left=child
			EndIf
		Else
			_root=child
		EndIf
		child._right=node
		node._parent=child
	End Method
	
	Method _DeleteFixup( node:TNode,parent:TNode )
		While node&lt;&gt;_root And node._color=BLACK
			If node=parent._left
				Local sib:TNode=parent._right
				If sib._color=RED
					sib._color=BLACK
					parent._color=RED
					_RotateLeft parent
					sib=parent._right
				EndIf
				If sib._left._color=BLACK And sib._right._color=BLACK
					sib._color=RED
					node=parent
					parent=parent._parent
				Else
					If sib._right._color=BLACK
						sib._left._color=BLACK
						sib._color=RED
						_RotateRight sib
						sib=parent._right
					EndIf
					sib._color=parent._color
					parent._color=BLACK
					sib._right._color=BLACK
					_RotateLeft parent
					node=_root
				EndIf
			Else	
				Local sib:TNode=parent._left
				If sib._color=RED
					sib._color=BLACK
					parent._color=RED
					_RotateRight parent
					sib=parent._left
				EndIf
				If sib._right._color=BLACK And sib._left._color=BLACK
					sib._color=RED
					node=parent
					parent=parent._parent
				Else
					If sib._left._color=BLACK
						sib._right._color=BLACK
						sib._color=RED
						_RotateLeft sib
						sib=parent._left
					EndIf
					sib._color=parent._color
					parent._color=BLACK
					sib._left._color=BLACK
					_RotateRight parent
					node=_root
				EndIf
			EndIf
		Wend
		node._color=BLACK
	End Method
	
	Const RED=-1,BLACK=1
	
	Field _root:TNode=nil
	
End Type

Rem
bbdoc: Create a map
returns: A new map object
End Rem
Function CreateMap:TMap()
	Return New TMap
End Function

Rem
bbdoc: Clear a map
about:
Removes all keys and values from @map.
End Rem
Function ClearMap( map:TMap )
	map.Clear
End Function

Rem
bbdoc: Check if a map is empty
returns: True if @map is empty, otherwise false
End Rem
Function MapIsEmpty( map:TMap )
	Return map.IsEmpty()
End Function

Rem
bbdoc: Insert a key/value pair into a map
about:
If @map already contained @key, its value is overwritten with @value. 
End Rem
Function MapInsert( map:TMap,key:Object,value:Object )
	map.Insert key,value
End Function

Rem
bbdoc: Find a value given a key
returns: The value associated with @key
about:
If @map does not contain @key, a Null object is returned.
End Rem
Function MapValueForKey:Object( map:TMap,key:Object )
	Return map.ValueForKey( key )
End Function

Rem
bbdoc: Check if a map contains a key
returns: True if @map contains @key, otherwise false
End Rem
Function MapContains( map:TMap,key:Object )
	Return map.Contains( key )
End Function

Rem
bbdoc: Remove a key/value pair from a map
End Rem
Function MapRemove( map:TMap,key:Object )
	map.Remove key
End Function

Rem
bbdoc: Get map keys
returns: An iterator object
about:
The object returned by #MapKeys can be used with #EachIn to iterate through 
the keys in @map.
End Rem
Function MapKeys:TMapEnumerator( map:TMap )
	Return map.Keys()
End Function

Rem
bbdoc: Get map values
returns: An iterator object
about:
The object returned by #MapValues can be used with #EachIn to iterate through 
the values in @map.
End Rem
Function MapValues:TMapEnumerator( map:TMap )
	Return map.Values()
End Function

Rem
bbdoc: Copy a map
returns: A copy of @map
End Rem
Function CopyMap:TMap( map:TMap )
	Return map.Copy()
End Function
</textarea><br><br>And the zip contents below.<br><br>intro.bbdoc<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
A Map is a type of fast key lookup data structure that offers a flexible means of indexing into its individual elements.

Indices into the elements of a Map are called keys. These keys, along with the data values associated with them, are stored within the Map.
Each entry of a Map contains exactly one unique key and its corresponding value.

Keys and values stored in a Map can be of any type. This includes floats, strings, arrays, objects or even other Maps.
</textarea><br><br>copymap.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' copymap.bmx

map:TMap=CreateMap()

MapInsert( map,"key 1","value 1" )

map2:TMap=CopyMap(map)

Print String( MapValueForKey( map2,"key 1" ) )
</textarea><br><br>mapcontains.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' mapcontains.bmx

map:TMap=CreateMap()

MapInsert( map,"key 1","value 1" )

Print MapContains( map,"key 1" )
Print MapContains( map,"key 2" )
</textarea><br><br>mapinsert.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' mapinsert.bmx

map:TMap=CreateMap()

MapInsert( map,"key 1","value 1" )

Print MapIsEmpty( map )

ClearMap( map )

Print MapIsEmpty( map )
</textarea><br><br>mapkeys.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' mapkeys.bmx

map:TMap=CreateMap()

MapInsert( map,"key 1","value 1" )
MapInsert( map,"key 2","value 2" )

For obj:Object=EachIn MapKeys( map )
	Print String( obj )+", "+String( MapValueForKey( map,obj ) )
Next
</textarea><br><br>mapremove.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' mapremove.bmx

map:TMap=CreateMap()

MapInsert( map,"key 1","value 1" )

Print MapContains( map,"key 1" )

MapRemove( map,"key 1" )

Print MapContains( map,"key 1" )
</textarea><br><br>mapvalueforkey.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' mapvalueforkey.bmx

map:TMap=CreateMap()

MapInsert( map,"key 1","value 1" )

Print String( MapValueForKey( map,"key 1" ) )
</textarea><br><br>mapvalues.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' mapvalues.bmx

map:TMap=CreateMap()

MapInsert( map,"key 1","value 1" )
MapInsert( map,"key 2","value 2" )

For obj:Object=EachIn MapValues( map )
	Print String( obj )
Next
</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
