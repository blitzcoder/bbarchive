<!DOCTYPE html><html lang="en" ><head ><title >Pub.FreeJoy additions</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Pub.FreeJoy additions</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=110" >BlitzMax Module Tweaks</a>/<a href="#bottom" >Pub.FreeJoy additions</a><br><br>
<a name="932872"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yan</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Rediscovered this forgotten version of FreeJoy today. Nothing mind blowing, but I thought someone might find it useful.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict

Rem
bbdoc: User input/Joystick
End Rem
Module Pub.FreeJoy

ModuleInfo "Version: 1.09"
ModuleInfo "Author: Simon Armstrong"
ModuleInfo "License: Public Domain"
ModuleInfo "Copyright: Blitz Research Ltd"
ModuleInfo "Modserver: BRL"

ModuleInfo "History: 1.09 Release"
ModuleInfo "History: Fixed vacant ports returning port0's values in 'freejoy.win32.c'"
ModuleInfo "History: Added axis direction commands and associated functions"
ModuleInfo "History: Added FlushJoy And WaitJoy"
ModuleInfo "History: Added Assert to SampleJoy for out of range port values"
ModuleInfo "History: 1.08 Release"
ModuleInfo "History: Added JoyHit samplejoy fix, thanks to Yan"
ModuleInfo "History: 1.07 Release"
ModuleInfo "History: Added MacOSX Rx,Ry,Rz (JoyR,JoyU,JoyV) and Wheel"
ModuleInfo "History: 1.06 Release"
ModuleInfo "History: Enabled Apple Gamepad and MultiAxis HID classes"
ModuleInfo "History: 1.05 Release"
ModuleInfo "History: Fixed Linux C Compiler warnings"
ModuleInfo "History: 1.04 Release"
ModuleInfo "History: Fixed C Compiler warnings"

?MacOS
Import "freejoy.macosx.c"
Import "-framework IOKit"
?Win32
Import "freejoy.win32.c"
?Linux
Import "freejoy.linux.c"
?

Extern

Rem
bbdoc: Counts the number of joysticks.
returns: The number of joysticks and gamecontrollers connected to the system.
end rem
Function JoyCount()

Function JoyCName:Byte Ptr(port)

Rem
bbdoc: Available buttons (on/off controls) on a joystick.
returns: A bitfield representing which buttons are present.
end rem
Function JoyButtonCaps(port)

Rem
bbdoc: Available axis (proportional controls) on a joystick.
returns: A bitfield representing which axis are available.
about:
The bit positions of the returned value correspond to the following constants defined
in the FreeJoy module:
[
* Const JOY_X=0
* Const JOY_Y=1
* Const JOY_Z=2
* Const JOY_R=3
* Const JOY_U=4
* Const JOY_V=5
* Const JOY_YAW=6
* Const JOY_PITCH=7
* Const JOY_ROLL=8
* Const JOY_HAT=9
* Const JOY_WHEEL=10
]
End Rem
Function JoyAxisCaps(port)

Function ReadJoy(port,buttons:Int Ptr,axis:Float Ptr)
Function WriteJoy(port,channel,value#)

End Extern

Const JOY_X=0
Const JOY_Y=1
Const JOY_Z=2
Const JOY_R=3
Const JOY_U=4
Const JOY_V=5
Const JOY_YAW=6
Const JOY_PITCH=7
Const JOY_ROLL=8
Const JOY_HAT=9
Const JOY_WHEEL=10

Rem
bbdoc: Get the name of the joysticks connected to the specified port.
returns: The system name of the joystick.
end rem
Function JoyName$(port)
  Return String.FromCString(JoyCName(port))
End Function

Global joy_time[16]
Global joy_buttons[16]
Global joy_axis#[16*16]
Global joy_hits[16,16]

Function SampleJoy(port)
  Assert port &lt; 16, "Joystick port value out of range"
  Local t
  t=joy_time[port]-MilliSecs()
  If t&lt;0 Or t&gt;1
    Local old=joy_buttons[port]
    ReadJoy port,Varptr joy_buttons[port],Varptr joy_axis[port*16]
    For Local button=0 To 16
      Local b=1 Shl button
      If Not(old &amp; b) And joy_buttons[port]&amp;b joy_hits[button, port]:+1'button and port were t'other way round.
    Next
  EndIf
End Function

Rem
bbdoc: Test the status of a joystick button.
returns: True if the button is pressed.
end rem
Function JoyDown( button,port=0 )
  SampleJoy port
  If joy_buttons[port] &amp; (1 Shl button) Return True
End Function

Rem
bbdoc: Check for a joystick button press
returns: Number of times @button has been hit.
about:
The returned value represents the number of the times @button has been hit since 
the last call to #JoyHit with the same specified @button.
End Rem
Function JoyHit( button,port=0 )
  SampleJoy port
  Local n=joy_hits[button,port]
  joy_hits[button,port]=0
  Return n
End Function

Rem
bbdoc: Reports the horizontal position of the joystick.
returns: Zero if the joystick is centered, -1 if Left, 1 if Right or a value inbetween.
end rem
Function JoyX#( port=0 )
  SampleJoy port
  Return joy_axis[port*16+JOY_X]
End Function

Rem
bbdoc: Reports the vertical position of the joystick.
returns: Zero if the joystick is centered, -1.0 if Up, 1.0 if Down or a value inbetween.
end rem
Function JoyY#( port=0 )
  SampleJoy port
  Return joy_axis[port*16+JOY_Y]
End Function

Rem
bbdoc: Reports the position of the joystick's Z axis if supported.
returns: Zero if the joystick is centered, -1.0 if Up, 1.0 if Down or a value inbetween.
end rem
Function JoyZ#( port=0 )
  SampleJoy port
  Return joy_axis[port*16+JOY_Z]
End Function

Rem
bbdoc: Reports the position of the joystick's R axis if supported.
returns: Zero if the joystick is centered, -1.0 if Up, 1.0 if Down or a value inbetween.
end rem
Function JoyR#( port=0 )
  SampleJoy port
  Return joy_axis[port*16+JOY_R]
End Function

Rem
bbdoc: Reports the position of the joystick's U axis if supported.
returns: Zero if the joystick is centered, -1.0 if Up, 1.0 if Down or a value inbetween.
about:
The U value of a joystick usually corresponds to a joystick's 'slider' or 'throttle' feature, although this may vary depending on the joystick, and will not be available with all joysticks.
End Rem
Function JoyU#( port=0 )
  SampleJoy port
  Return joy_axis[port*16+JOY_U]
End Function

Rem
bbdoc: Reports the position of the joystick's V axis if supported.
returns: Zero if the joystick is centered, -1.0 if Up, 1.0 if Down or a value inbetween.
about:
The V value of a joystick usually corresponds to a joystick's 'slider' or 'throttle' feature, although this may vary depending on the joystick, and will not be available with all joysticks.
End Rem
Function JoyV#( port=0 )
  SampleJoy port
  Return joy_axis[port*16+JOY_V]
End Function

Rem
bbdoc: Reports the position of the joystick's YAW axis if supported.
returns: Zero if the joystick is centered, -1.0 if Up, 1.0 if Down or a value inbetween.
end rem
Function JoyYaw#( port=0 )
  SampleJoy port
  Return joy_axis[port*16+JOY_YAW]
End Function

Rem
bbdoc: Reports the position of the joystick's PITCH axis if supported.
returns: Zero if the joystick is centered, -1.0 if Up, 1.0 if Down or a value inbetween.
end rem
Function JoyPitch#( port=0 )
  SampleJoy port
  Return joy_axis[port*16+JOY_PITCH]
End Function

Rem
bbdoc: Reports the position of the joystick's ROLL axis if supported.
returns: Zero if the joystick is centered, -1.0 if Up, 1.0 if Down or a value inbetween.
end rem
Function JoyRoll#( port=0 )
  SampleJoy port
  Return joy_axis[port*16+JOY_ROLL]
End Function

Rem
bbdoc: Reports the position of the joystick's HAT controller if supported.
returns: -1.0 if the joystick is centered, and values between 0.0, 0.25, 0.5 and 0.75 for the directions Up, Right, Down, Left respectively.
End Rem
Function JoyHat#( port=0 )
  SampleJoy port
  Return joy_axis[port*16+JOY_HAT]
End Function

Rem
bbdoc: Reports the position of the joystick's WHEEL axis if supported.
returns: Zero if the joystick is centered, -1.0 if Left, 1.0 if Right or a value inbetween.
end rem
Function JoyWheel#( port=0 )
  SampleJoy port
  Return joy_axis[port*16+JOY_WHEEL]
End Function

'MARK: Yan's additions...
Private
Global joyCenterZone#[16]
ResetJoyCenterZone#(-1)

Public

'Undocumented: is this useful?
Function AutoJoyCenterZone( port=0, sampleAxes=%000011, samples=1000 )
  Local filteredAxisCaps = JoyAxisCaps(port) &amp; sampleAxes, joyCenter#
  
  For Local c = 1 To samples
      Local joyPos#
      
      SampleJoy(port)
      For Local a = JOY_X To JOY_V
        If filteredAxisCaps &amp; (1 Shl a)
          joyPos# = Abs(joy_axis[(port * 16) + a])
          If joyPos# &gt; joyCenter# Then joyCenter# = joyPos#
        EndIf
      Next    
  Next
  
  joyCenterZone#[port] = joyCenter# * 1.1 ' Increase by 10% of max
End Function

Rem
bbdoc: Sets the center zone used by the joystick direction commands.
about: CenterZone must be between 0.0 and 1.0.
End rem
Function SetJoyCenterZone( centerZone#, port=0 ) 
  Assert (centerZone# &gt;= 0) And (centerZone# &lt;= 1.0) Else "SetJoyCenterZone(): centerZone value must be between 0.0 and 1.0" 
  joyCenterZone#[port] = centerZone#
End Function

Rem
bbdoc: Reports the current value of a ports center zone.
returns: The current center zone value which will be between 0.0 and 1.0.
end rem
Function GetJoyCenterZone#( port=0 )

  Return joyCenterZone#[port]
End Function

Rem
bbdoc: Resets the centre zone to its default value.
about: The default value used for the center zone is ~0.333. Using a port value of -1 will reset the center zone for all joystick ports.
end rem
Function ResetJoyCenterZone#( port=0 )
  If port = -1
    For Local p = 0 To 15
      joyCenterZone#[p] = 0.333
    Next
  Else
    joyCenterZone#[port] = 0.333
  EndIf
End Function

Rem
bbdoc: Reports the horizontal direction of the joystick.
returns: Zero if the axis is centered (inside +/- JoyCenterZone), -1 if Left (less than '-JoyCenterZone') and 1 if Right (more than JoyCenterZone).
End Rem
Function JoyXDir( port=0 )
  Local JoyPos# = JoyX(port)
  
  Return (JoyPos# &gt;= joyCenterZone#[port]) - (JoyPos# &lt;= -joyCenterZone#[port])
End Function

Rem
bbdoc: Reports the vertical direction of the joystick.
returns: Zero if the axis is centered (inside +/- JoyCenterZone), -1 if Up (less than '-JoyCenterZone') and 1 if Down (more than JoyCenterZone).
End Rem
Function JoyYDir( port=0 )
  Local JoyPos# = JoyY(port)
  
  Return (JoyPos# &gt;= joyCenterZone#[port]) - (JoyPos# &lt;= -joyCenterZone#[port])
End Function

Rem
bbdoc: Reports the direction of the joystick's Z axis if supported.
returns: Zero if the axis joystick is centered (inside +/- JoyCenterZone), -1 if Up (less than '-JoyCenterZone') and 1 if Down (more than JoyCenterZone).
End Rem
Function JoyZDir( port=0 )
  Local JoyPos# = JoyZ(port)
  
  Return (JoyPos# &gt;= joyCenterZone#[port]) - (JoyPos# &lt;= -joyCenterZone#[port])
End Function

Rem
bbdoc: Reports the direction of the joystick's U axis if supported.
returns: Zero if the axis is centered (inside +/- JoyCenterZone), -1 if Up (less than '-JoyCenterZone') and 1 if Down (more than JoyCenterZone).
End Rem
Function JoyUDir( port=0 )
  Local JoyPos# = JoyU(port)
  
  Return (JoyPos# &gt;= joyCenterZone#[port]) - (JoyPos# &lt;= -joyCenterZone#[port])
End Function

Rem
bbdoc: Reports the direction of the joystick's V axis if supported.
returns: Zero if the axis is centered (inside +/- JoyCenterZone), -1 if Up (less than '-JoyCenterZone') and 1 if Down (more than JoyCenterZone).
End Rem
Function JoyVDir( port=0 )
  Local JoyPos# = JoyV(port)
  
  Return (JoyPos# &gt;= joyCenterZone#[port]) - (JoyPos# &lt;= -joyCenterZone#[port])
End Function

Rem
bbdoc: Flush all connected joysticks' button states.
End Rem
Function FlushJoy() 
  For Local p = 0 To 15
    SampleJoy(p)

    joy_buttons[p] = 0
    
    For Local i = 0 To 15
      joy_hits[i, p] = 0
    Next
  Next
End Function

Rem
bbdoc: Causes program execution to halt until a joystick button on the specified port is hit.
returns: The button hit.
End Rem 
Function WaitJoy( port=0 )
  FlushJoy()
  
  Repeat
    For Local b = 0 To 15
      If JoyHit(b, port) Return b
    Next
    
    'PollSystem
  Forever
End Function

'TODO: 
Function JoyType( port=0 )
  'Not sure what was intended here?
  'Analogue/Digital detection?
  '??
End Function</textarea> <br><br></td></tr></table><br>
<a name="932873"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yan</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Example that uses some of the new commands...<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Rem
	JoyDir example
End Rem

Strict

If JoyCount() = 0 Then RuntimeError "No controllers connected"

Graphics 800, 600, 0
HideMouse

Local screenCentreX = (GraphicsWidth() * 0.5)
Local screenCentreY = (GraphicsHeight() * 0.5)

For Local p = 0 Until JoyCount()
	AutoJoyCenterZone(p)
Next

Local joySelected, joyCentreZone# = GetJoyCenterZone(joySelected)

Repeat
	Local joyNo = JoyCount()
	
	If KeyHit(KEY_RIGHT)
		joySelected :+ 1
		joySelected = Min(joyNo - 1, joySelected)
		joyCentreZone# = GetJoyCenterZone(joySelected)
	EndIf
	 
	If KeyHit(KEY_LEFT) Then
		joySelected :- 1
		joySelected = Max(0, joySelected)
		joyCentreZone# = GetJoyCenterZone(joySelected)
	EndIf
	
	If KeyHit(KEY_UP)
		joyCentreZone# :+ 0.1
		joyCentreZone# = Min(0.9 , joyCentreZone#)
		SetJoyCenterZone(joyCentreZone#, joySelected)
	EndIf
	
	If KeyHit(KEY_DOWN)
		joyCentreZone# :- 0.1
		joyCentreZone# = Max(0.1 , joyCentreZone#)
		SetJoyCenterZone(joyCentreZone#, joySelected)
	EndIf
	
	Cls
	
	SetColor 255, 255, 255
	DrawCentredText "Controller " + (joySelected + 1) + " of " + joyNo, screenCentreX, 12
	DrawCentredText "Use Left and Right Cursor keys to select controller", screenCentreX, 570
	DrawCentredText "Use Up and Down Cursor keys to adjust centre zone", screenCentreX, 588
		
	SetColor 0, 50, 125
	DrawRect screenCentreX - 200, screenCentreY - 200, 400, 400
	
	SetColor 0, 25, 50
	DrawRect screenCentreX - 198, screenCentreY - 198, 396, 396
	
	SetColor 50, 50, 0
	Local tl = joyCentreZone# * 200, br = joyCentreZone# * 400 
	DrawRect screenCentreX - tl, screenCentreY - tl, br, br
	
	SetColor 25, 25, 0
	DrawRect screenCentreX - (tl - 2), screenCentreY - (tl - 2), br - 4, br - 4
	
	SetColor 0, 50, 125
	DrawRect screenCentreX - 15, screenCentreY - 15, 30, 30
	
	SetColor 25, 25, 0
	DrawRect screenCentreX - 13, screenCentreY - 13, 26, 26
	
	SetColor 0, 50, 125
	DrawLine screenCentreX - 50, screenCentreY, screenCentreX + 50, screenCentreY
	DrawLine screenCentreX, screenCentreY - 50, screenCentreX, screenCentreY + 50
	
	SetColor 50,50, 0
	DrawRect screenCentreX + (JoyX(joySelected) * 200) - 13,..
					screenCentreY + (JoyY(joySelected) * 200) -13, 26, 26
	
	SetColor 255, 255, 0
	DrawRect screenCentreX + (JoyXDir(joySelected) * 200) - 13,..
					screenCentreY + (JoyYDir(joySelected) * 200) -13, 26, 26

	Flip

Until KeyHit(KEY_ESCAPE) Or AppTerminate()

End


Function DrawCentredText(text$, x, y)
	DrawText text$, x - (TextWidth(text$) * 0.5), y - (TextHeight(text$) * 0.5)
End Function</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
