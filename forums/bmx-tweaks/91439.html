<!DOCTYPE html><html lang="en" ><head ><title >brl.bank docs</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >brl.bank docs</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=110" >BlitzMax Module Tweaks</a>/<a href="#bottom" >brl.bank docs</a><br><br>
<a name="1040420"></a>

<a name="1221942"></a>

<a name="1222179"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is an update to the brl.bank docs. Most of the examples are based on the Blitz3D docs.<br><br>The contents of the doc folder are zipped and posted below. No changes to intro.bbdoc so I didn't post it.<br>bank-doc.zip (expired)<br><br>I'm posting the module source (from v 1.41) and the patch.<br><br>Last edit: 16 Aug 2010<br><br>bank.diff<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">--- bank-old.bmx	2010-07-13 03:55:02.000000000 +0100
+++ bank.bmx	2010-08-16 22:57:16.784370642 +0100
@@ -21,7 +21,7 @@
 
 Rem
 bbdoc: Memory bank
-end rem
+End Rem
 Type TBank
 
 	Field _buf:Byte Ptr,_size,_capacity,_locked
@@ -79,14 +79,14 @@
 	Rem
 	bbdoc: Get capacity of bank
 	returns: The capacity, in bytes, of the bank's internal memory buffer
-	end rem
+	End Rem
 	Method Capacity()
 		Return _capacity
 	End Method
 
 	Rem
 	bbdoc: Resize a bank
-	end rem
+	End Rem
 	Method Resize( size )
 		Assert _locked=0 Else "Locked banks cannot be resize"
 		Assert _capacity&gt;=0 Else "Static banks cannot be resized"
@@ -104,7 +104,7 @@
 	
 	Rem
 	bbdoc: Read bytes from a stream into a bank
-	end rem
+	End Rem
 	Method Read( stream:TStream,offset,count )
 		Assert offset&gt;=0 And offset&lt;=_size-count Else "Illegal bank offset"
 		Return stream.Read( _buf+offset,count )
@@ -112,7 +112,7 @@
 	
 	Rem
 	bbdoc: Write bytes in a bank to a stream
-	end rem
+	End Rem
 	Method Write( stream:TStream,offset,count )
 		Assert offset&gt;=0 And offset&lt;=_size-count Else "Illegal bank offset"
 		Return stream.Write( _buf+offset,count )
@@ -121,7 +121,7 @@
 	Rem
 	bbdoc: Peek a byte from a bank
 	returns: The byte value at the specified byte offset within the bank
-	end rem
+	End Rem
 	Method PeekByte( offset )
 		Assert offset&gt;=0 And offset&lt;_size Else "Illegal bank offset"
 		Return _buf[offset]
@@ -129,7 +129,9 @@
 
 	Rem
 	bbdoc: Poke a byte into a bank
-	end rem
+	about:
+	A byte is an unsigned 8 bit value with a range of 0..255.
+	End Rem
 	Method PokeByte( offset,value )
 		Assert offset&gt;=0 And offset&lt;_size Else "Illegal bank offset"
 		_buf[offset]=value
@@ -138,7 +140,7 @@
 	Rem
 	bbdoc: Peek a short from a bank
 	returns: The short value at the specified byte offset within the bank
-	end rem
+	End Rem
 	Method PeekShort( offset )
 		Assert offset&gt;=0 And offset&lt;_size-1 Else "Illegal bank offset"
 		Return (Short Ptr(_buf+offset))[0]
@@ -146,7 +148,9 @@
 
 	Rem
 	bbdoc: Poke a short into a bank
-	end rem
+	about:
+	A short is an unsigned 16 bit (2 bytes) value with a range of 0..65535.
+	End Rem
 	Method PokeShort( offset,value )
 		Assert offset&gt;=0 And offset&lt;_size-1 Else "Illegal bank offset"
 		(Short Ptr(_buf+offset))[0]=value
@@ -155,7 +159,7 @@
 	Rem
 	bbdoc: Peek an int from a bank
 	returns: The int value at the specified byte offset within the bank
-	end rem
+	End Rem
 	Method PeekInt( offset )
 		Assert offset&gt;=0 And offset&lt;_size-3 Else "Illegal bank offset"
 		Return (Int Ptr(_buf+offset))[0]
@@ -163,7 +167,9 @@
 
 	Rem
 	bbdoc: Poke an int into a bank
-	end rem
+	about:
+	An int is a signed 32 bit integer (4 bytes).
+	End Rem
 	Method PokeInt( offset,value )
 		Assert offset&gt;=0 And offset&lt;_size-3 Else "Illegal bank offset"
 		(Int Ptr(_buf+offset))[0]=value
@@ -172,7 +178,7 @@
 	Rem
 	bbdoc: Peek a long from a bank
 	returns: The long value at the specified byte offset within the bank
-	end rem
+	End Rem
 	Method PeekLong:Long( offset )
 		Assert offset&gt;=0 And offset&lt;_size-7 Else "Illegal bank offset"
 		Return (Long Ptr(_buf+offset))[0]
@@ -180,7 +186,9 @@
 
 	Rem
 	bbdoc: Poke a long value into a bank
-	end rem
+	about:
+	A long is a signed 64 bit integer (8 bytes).
+	End Rem
 	Method PokeLong( offset,value:Long )
 		Assert offset&gt;=0 And offset&lt;_size-7 Else "Illegal bank offset"
 		(Long Ptr(_buf+offset))[0]=value
@@ -189,7 +197,7 @@
 	Rem
 	bbdoc: Peek a float from a bank
 	returns: The float value at the specified byte offset within the bank
-	end rem
+	End Rem
 	Method PeekFloat#( offset )
 		Assert offset&gt;=0 And offset&lt;_size-3 Else "Illegal bank offset"
 		Return (Float Ptr(_buf+offset))[0]
@@ -197,7 +205,9 @@
 
 	Rem
 	bbdoc: Poke a float value into a bank
-	end rem
+	about:
+	A float is a signed 32 bit floating point (4 bytes).
+	End Rem
 	Method PokeFloat( offset,value# )
 		Assert offset&gt;=0 And offset&lt;_size-3 Else "Illegal bank offset"
 		(Float Ptr(_buf+offset))[0]=value
@@ -206,7 +216,7 @@
 	Rem
 	bbdoc: Peek a double from a bank
 	returns: The double value at the specified byte offset within the bank
-	end rem
+	End Rem
 	Method PeekDouble!( offset )
 		Assert offset&gt;=0 And offset&lt;_size-7 Else "Illegal bank offset"
 		Return (Double Ptr(_buf+offset))[0]
@@ -214,7 +224,9 @@
 
 	Rem
 	bbdoc: Poke a double value into a bank
-	end rem
+	about:
+	A double is a signed 64 bit floating point (8 bytes).
+	End Rem
 	Method PokeDouble( offset,value! )
 		Assert offset&gt;=0 And offset&lt;_size-7 Else "Illegal bank offset"
 		(Double Ptr(_buf+offset))[0]=value
@@ -222,9 +234,8 @@
 	
 	Rem
 	bbdoc: Save a bank to a stream
-	about:
-	Return True if successful, otherwise False.
-	end rem
+	returns: True if successful, otherwise false
+	End Rem
 	Method Save( url:Object )
 		Local stream:TStream=WriteStream( url )
 		If Not stream Return
@@ -235,10 +246,8 @@
 
 	Rem
 	bbdoc: Load a bank from a stream
-	returns: A new TBank object
-	about:
-	Returns a new TBank object if successfull, otherwise Null.
-	end rem
+	returns: A new TBank object if successful, otherwise Null
+	End Rem
 	Function Load:TBank( url:Object )
 		Local stream:TStream=ReadStream( url )
 		If Not stream Return
@@ -251,8 +260,8 @@
 
 	Rem
 	bbdoc: Create a bank
-	returns: A new TBank object with an initial size of @size
-	end rem
+	returns: A new TBank object of the specified @size
+	End Rem
 	Function Create:TBank( size )
 		Assert size&gt;=0 Else "Illegal bank size"
 		Local bank:TBank=New TBank
@@ -264,7 +273,7 @@
 	
 	Rem
 	bbdoc: Create a bank from an existing block of memory
-	end rem
+	End Rem
 	Function CreateStatic:TBank( buf:Byte Ptr,size )
 		Assert size&gt;=0 Else "Illegal bank size"
 		Local bank:TBank=New TBank
@@ -278,11 +287,10 @@
 
 Rem
 bbdoc: Create a bank
-returns: A bank object with an initial size of @size bytes
+returns: A bank object of the specified @size in bytes
 about:
-#CreateBank creates a Bank allocating a specified amount of memory that
-can be used for storage of binary data using the various Poke and
-Peek commands. 
+Allocates a specified amount of memory that can be used for storage of 
+binary data using the various Poke and Peek commands.
 End Rem
 Function CreateBank:TBank( size=0 )
 	Return TBank.Create( size )
@@ -301,22 +309,21 @@
 
 Rem
 bbdoc: Load a bank
-returns: A bank containing the binary contents of @url, or null if @url could not be opened
+returns: A bank containing the binary contents of @url, or Null if @url could not be opened
 about:
-#LoadBank reads the entire contents of a binary file from a specified @url into a newly
+Reads the entire contents of a binary file from a specified @url into a newly
 created bank with a size matching that of the file.
-end rem
+End Rem
 Function LoadBank:TBank( url:Object )
 	Return TBank.Load( url )
 End Function
 
 Rem
 bbdoc: Save a bank
-returns: True if successful.
+returns: True if successful, otherwise false
 about:
-#SaveBank writes it's entire contents to a @url. If the @url is a file path a new
-file is created.
-end rem
+Writes its entire contents to a @url. If the @url is a file path a new file is created.
+End Rem
 Function SaveBank( bank:TBank,url:Object )
 	Return bank.Save( url )
 End Function
@@ -325,7 +332,7 @@
 bbdoc: Get bank's memory buffer
 returns: A byte pointer to the bank's internal memory buffer
 about:
-Please use #LockBank and #UnlockBank instead of this method.
+Please use #LockBank and #UnlockBank instead of this function.
 End Rem
 Function BankBuf:Byte Ptr( bank:TBank )
 	Return bank.Buf()
@@ -333,7 +340,7 @@
 
 Rem
 bbdoc: Lock a bank's memory block
-returns: A byte pointer to the memory block controlled by the bank.
+returns: A byte pointer to the memory block controlled by the bank
 about:
 While locked, a bank cannot be resized.
 
@@ -357,7 +364,7 @@
 Rem
 bbdoc: Get size of bank
 returns: The size, in bytes, of the bank's internal memory buffer
-end rem
+End Rem
 Function BankSize( bank:TBank )
 	Return bank.Size()
 End Function
@@ -370,7 +377,7 @@
 more memory due to a resize. Bank capacity may be increased due to a call
 to #ResizeBank by either 50% or the requested amount, whichever is greater.
 Capacity never decreases.
-end rem
+End Rem
 Function BankCapacity( bank:TBank )
 	Return bank.Capacity()
 End Function
@@ -378,10 +385,10 @@
 Rem
 bbdoc: Resize a bank
 about:
-#ResizeBank modifies the size limit of a bank. This may cause memory to be
-allocated if the requested size is greater than the bank's current capacity,
+Modifies the size limit of a bank. This may cause memory to be allocated 
+if the requested size is greater than the bank's current capacity, 
 see #BankCapacity for more information.
-end rem
+End Rem
 Function ResizeBank( bank:TBank,size )
 	bank.Resize size
 End Function
@@ -389,9 +396,8 @@
 Rem
 bbdoc: Copy bank contents
 about:
-#CopyBank copies @count bytes from @src_offset in @src_bank to @dst_offset
-in @dst_bank.
-end rem
+Copies @count bytes from @src_offset in @src_bank to @dst_offset in @dst_bank.
+End Rem
 Function CopyBank( src_bank:TBank,src_offset,dst_bank:TBank,dst_offset,count )
 	Assert..
 	count&gt;=0 And..
@@ -405,16 +411,16 @@
 Rem
 bbdoc: Peek a byte from a bank
 returns: The byte value at the specified byte offset within the bank
-about:
-A byte is an unsigned 8 bit value with a range of 0..255.
-end rem
+End Rem
 Function PeekByte( bank:TBank,offset )
 	Return bank.PeekByte( offset )
 End Function
 
 Rem
 bbdoc: Poke a byte into a bank
-end rem
+about:
+A byte is an unsigned 8 bit value with a range of 0..255.
+End Rem
 Function PokeByte( bank:TBank,offset,value )
 	bank.PokeByte offset,value
 End Function
@@ -422,9 +428,7 @@
 Rem
 bbdoc: Peek a short from a bank
 returns: The short value at the specified byte offset within the bank
-about:
-A short is an unsigned 16 bit (2 bytes) value with a range of 0..65535.
-end rem
+End Rem
 Function PeekShort( bank:TBank,offset )
 	Return bank.PeekShort( offset )
 End Function
@@ -432,8 +436,8 @@
 Rem
 bbdoc: Poke a short into a bank
 about:
-An short is an unsigned 16 bit value that requires 2 bytes of storage.
-end rem
+A short is an unsigned 16 bit (2 bytes) value with a range of 0..65535.
+End Rem
 Function PokeShort( bank:TBank,offset,value )
 	bank.PokeShort offset,value
 End Function
@@ -441,9 +445,7 @@
 Rem
 bbdoc: Peek an int from a bank
 returns: The int value at the specified byte offset within the bank
-about:
-An int is a signed 32 bit value (4 bytes).
-end rem
+End Rem
 Function PeekInt( bank:TBank,offset )
 	Return bank.PeekInt( offset )
 End Function
@@ -451,27 +453,25 @@
 Rem
 bbdoc: Poke an int into a bank
 about:
-An int is a signed 32 bit value that requires 4 bytes of storage.
-end rem
+An int is a signed 32 bit integer (4 bytes).
+End Rem
 Function PokeInt( bank:TBank,offset,value )
 	bank.PokeInt offset,value
 End Function
 
 Rem
-bbdoc: Peek a long integer from a bank
-returns: The long integer value at the specified byte offset within the bank
-about:
-A long is a 64 bit integer that requires 8 bytes of memory.
-end rem
+bbdoc: Peek a long from a bank
+returns: The long value at the specified byte offset within the bank
+End Rem
 Function PeekLong:Long( bank:TBank,offset )
 	Return bank.PeekLong( offset )
 End Function
 
 Rem
-bbdoc: Poke a long integer int into a bank
+bbdoc: Poke a long into a bank
 about:
-A long is a 64 bit integer that requires 8 bytes of storage.
-end rem
+A long is a signed 64 bit integer (8 bytes).
+End Rem
 Function PokeLong( bank:TBank,offset,value:Long )
 	bank.PokeLong offset,value
 End Function
@@ -479,9 +479,7 @@
 Rem
 bbdoc: Peek a float from a bank
 returns: The float value at the specified byte offset within the bank
-about:
-A float requires 4 bytes of storage
-end rem
+End Rem
 Function PeekFloat#( bank:TBank,offset )
 	Return bank.PeekFloat( offset )
 End Function
@@ -489,8 +487,8 @@
 Rem
 bbdoc: Poke a float into a bank
 about:
-A float requires 4 bytes of storage
-end rem
+A float is a signed 32 bit floating point (4 bytes).
+End Rem
 Function PokeFloat( bank:TBank,offset,value# )
 	bank.PokeFloat offset,value
 End Function
@@ -498,9 +496,7 @@
 Rem
 bbdoc: Peek a double from a bank
 returns: The double value at the specified byte offset within the bank
-about:
-A double requires 8 bytes of storage
-end rem
+End Rem
 Function PeekDouble!( bank:TBank,offset )
 	Return bank.PeekDouble( offset )
 End Function
@@ -508,24 +504,24 @@
 Rem
 bbdoc: Poke a double into a bank
 about:
-A double requires 8 bytes of storage
-end rem
+A double is a signed 64 bit floating point (8 bytes).
+End Rem
 Function PokeDouble( bank:TBank,offset,value! )
 	bank.PokeDouble offset,value
 End Function
 
 Rem
-bbdoc: Read bytes from a Stream to a Bank
-returns: The number of bytes successfully read from the Stream
-end rem
+bbdoc: Read bytes from a stream to a bank
+returns: The number of bytes successfully read from the stream
+End Rem
 Function ReadBank( bank:TBank,stream:TStream,offset,count )
 	Return bank.Read( stream,offset,count )
 End Function
 
 Rem
-bbdoc: Write bytes from a Bank to a Stream
-returns: The number of bytes successfully written to the Stream
-end rem
+bbdoc: Write bytes from a bank to a stream
+returns: The number of bytes successfully written to the stream
+End Rem
 Function WriteBank( bank:TBank,stream:TStream,offset,count )
 	Return bank.Write( stream,offset,count )
 End Function
</textarea><br><br>bank.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Rem
bbdoc: Miscellaneous/Banks
End Rem
Module BRL.Bank

ModuleInfo "Version: 1.06"
ModuleInfo "Author: Mark Sibly"
ModuleInfo "License: Blitz Shared Source Code"
ModuleInfo "Copyright: Blitz Research Ltd"
ModuleInfo "Modserver: BRL"

ModuleInfo "History: 1.06 Release"
ModuleInfo "History: Added Lock/Unlock to replace Buf"
ModuleInfo "History: 1.05 Release"
ModuleInfo "History: Fixed Read/Write using ReadBytes/WriteBytes"

Import BRL.Stream

Rem
bbdoc: Memory bank
End Rem
Type TBank

	Field _buf:Byte Ptr,_size,_capacity,_locked
	
	Method _pad()
	End Method
	
	Method Delete()
		Assert Not _locked
		If _capacity&gt;=0 MemFree _buf
	End Method

	Rem
	bbdoc: Get a bank's memory pointer
	returns: A byte pointer to the memory block controlled by the bank
	about:
	Please use #Lock and #Unlock instead of this method.
	End Rem
	Method Buf:Byte Ptr()
		Return _buf
	End Method
	
	Rem
	bbdoc: Lock a bank's memory block
	returns: A byte pointer to the memory block controlled by the bank
	about:
	While locked, a bank cannot be resized.

	After you have finished with a bank's memory block, you must use #Unlock
	to return it to the bank.
	End Rem
	Method Lock:Byte Ptr()
		_locked:+1
		Return _buf
	End Method
	
	Rem
	bbdoc: Unlock a bank's memory pointer
	about:
	After you have finished with a bank's memory block, you must use #Unlock
	to return it to the bank.
	End Rem
	Method Unlock()
		_locked:-1
	End Method

	Rem
	bbdoc: Get a bank's size
	returns: The size, in bytes, of the memory block controlled by the bank
	End Rem
	Method Size()
		Return _size
	End Method

	Rem
	bbdoc: Get capacity of bank
	returns: The capacity, in bytes, of the bank's internal memory buffer
	End Rem
	Method Capacity()
		Return _capacity
	End Method

	Rem
	bbdoc: Resize a bank
	End Rem
	Method Resize( size )
		Assert _locked=0 Else "Locked banks cannot be resize"
		Assert _capacity&gt;=0 Else "Static banks cannot be resized"
		If size&gt;_capacity
			Local n=_capacity*3/2
			If n&lt;size n=size
			Local tmp:Byte Ptr=MemAlloc(n)
			MemCopy tmp,_buf,_size
			MemFree _buf
			_capacity=n
			_buf=tmp
		EndIf
		_size=size
	End Method
	
	Rem
	bbdoc: Read bytes from a stream into a bank
	End Rem
	Method Read( stream:TStream,offset,count )
		Assert offset&gt;=0 And offset&lt;=_size-count Else "Illegal bank offset"
		Return stream.Read( _buf+offset,count )
	End Method
	
	Rem
	bbdoc: Write bytes in a bank to a stream
	End Rem
	Method Write( stream:TStream,offset,count )
		Assert offset&gt;=0 And offset&lt;=_size-count Else "Illegal bank offset"
		Return stream.Write( _buf+offset,count )
	End Method

	Rem
	bbdoc: Peek a byte from a bank
	returns: The byte value at the specified byte offset within the bank
	End Rem
	Method PeekByte( offset )
		Assert offset&gt;=0 And offset&lt;_size Else "Illegal bank offset"
		Return _buf[offset]
	End Method

	Rem
	bbdoc: Poke a byte into a bank
	about:
	A byte is an unsigned 8 bit value with a range of 0..255.
	End Rem
	Method PokeByte( offset,value )
		Assert offset&gt;=0 And offset&lt;_size Else "Illegal bank offset"
		_buf[offset]=value
	End Method

	Rem
	bbdoc: Peek a short from a bank
	returns: The short value at the specified byte offset within the bank
	End Rem
	Method PeekShort( offset )
		Assert offset&gt;=0 And offset&lt;_size-1 Else "Illegal bank offset"
		Return (Short Ptr(_buf+offset))[0]
	End Method

	Rem
	bbdoc: Poke a short into a bank
	about:
	A short is an unsigned 16 bit (2 bytes) value with a range of 0..65535.
	End Rem
	Method PokeShort( offset,value )
		Assert offset&gt;=0 And offset&lt;_size-1 Else "Illegal bank offset"
		(Short Ptr(_buf+offset))[0]=value
	End Method

	Rem
	bbdoc: Peek an int from a bank
	returns: The int value at the specified byte offset within the bank
	End Rem
	Method PeekInt( offset )
		Assert offset&gt;=0 And offset&lt;_size-3 Else "Illegal bank offset"
		Return (Int Ptr(_buf+offset))[0]
	End Method

	Rem
	bbdoc: Poke an int into a bank
	about:
	An int is a signed 32 bit integer (4 bytes).
	End Rem
	Method PokeInt( offset,value )
		Assert offset&gt;=0 And offset&lt;_size-3 Else "Illegal bank offset"
		(Int Ptr(_buf+offset))[0]=value
	End Method

	Rem
	bbdoc: Peek a long from a bank
	returns: The long value at the specified byte offset within the bank
	End Rem
	Method PeekLong:Long( offset )
		Assert offset&gt;=0 And offset&lt;_size-7 Else "Illegal bank offset"
		Return (Long Ptr(_buf+offset))[0]
	End Method

	Rem
	bbdoc: Poke a long value into a bank
	about:
	A long is a signed 64 bit integer (8 bytes).
	End Rem
	Method PokeLong( offset,value:Long )
		Assert offset&gt;=0 And offset&lt;_size-7 Else "Illegal bank offset"
		(Long Ptr(_buf+offset))[0]=value
	End Method
	
	Rem
	bbdoc: Peek a float from a bank
	returns: The float value at the specified byte offset within the bank
	End Rem
	Method PeekFloat#( offset )
		Assert offset&gt;=0 And offset&lt;_size-3 Else "Illegal bank offset"
		Return (Float Ptr(_buf+offset))[0]
	End Method

	Rem
	bbdoc: Poke a float value into a bank
	about:
	A float is a signed 32 bit floating point (4 bytes).
	End Rem
	Method PokeFloat( offset,value# )
		Assert offset&gt;=0 And offset&lt;_size-3 Else "Illegal bank offset"
		(Float Ptr(_buf+offset))[0]=value
	End Method

	Rem
	bbdoc: Peek a double from a bank
	returns: The double value at the specified byte offset within the bank
	End Rem
	Method PeekDouble!( offset )
		Assert offset&gt;=0 And offset&lt;_size-7 Else "Illegal bank offset"
		Return (Double Ptr(_buf+offset))[0]
	End Method

	Rem
	bbdoc: Poke a double value into a bank
	about:
	A double is a signed 64 bit floating point (8 bytes).
	End Rem
	Method PokeDouble( offset,value! )
		Assert offset&gt;=0 And offset&lt;_size-7 Else "Illegal bank offset"
		(Double Ptr(_buf+offset))[0]=value
	End Method
	
	Rem
	bbdoc: Save a bank to a stream
	returns: True if successful, otherwise false
	End Rem
	Method Save( url:Object )
		Local stream:TStream=WriteStream( url )
		If Not stream Return
		Local n=stream.WriteBytes( _buf,_size )
		stream.Close
		Return True
	End Method

	Rem
	bbdoc: Load a bank from a stream
	returns: A new TBank object if successful, otherwise Null
	End Rem
	Function Load:TBank( url:Object )
		Local stream:TStream=ReadStream( url )
		If Not stream Return
		Local data:Byte[]=LoadByteArray( stream )
		Local bank:TBank=Create( data.length )
		MemCopy bank.Buf(),data,data.length 
		stream.Close
		Return bank
	End Function

	Rem
	bbdoc: Create a bank
	returns: A new TBank object of the specified @size
	End Rem
	Function Create:TBank( size )
		Assert size&gt;=0 Else "Illegal bank size"
		Local bank:TBank=New TBank
		bank._buf=MemAlloc( size )
		bank._size=size
		bank._capacity=size
		Return bank
	End Function
	
	Rem
	bbdoc: Create a bank from an existing block of memory
	End Rem
	Function CreateStatic:TBank( buf:Byte Ptr,size )
		Assert size&gt;=0 Else "Illegal bank size"
		Local bank:TBank=New TBank
		bank._buf=buf
		bank._size=size
		bank._capacity=-1
		Return bank
	End Function

End Type

Rem
bbdoc: Create a bank
returns: A bank object of the specified @size in bytes
about:
Allocates a specified amount of memory that can be used for storage of 
binary data using the various Poke and Peek commands.
End Rem
Function CreateBank:TBank( size=0 )
	Return TBank.Create( size )
End Function

Rem
bbdoc: Create a bank with existing data
returns: A bank object that references an existing block of memory
about:
The memory referenced by a static bank is not released when the bank is deleted.
A static bank cannot be resized.
End Rem
Function CreateStaticBank:TBank( buf:Byte Ptr,size )
	Return TBank.CreateStatic( buf,size )
End Function

Rem
bbdoc: Load a bank
returns: A bank containing the binary contents of @url, or Null if @url could not be opened
about:
Reads the entire contents of a binary file from a specified @url into a newly
created bank with a size matching that of the file.
End Rem
Function LoadBank:TBank( url:Object )
	Return TBank.Load( url )
End Function

Rem
bbdoc: Save a bank
returns: True if successful, otherwise false
about:
Writes its entire contents to a @url. If the @url is a file path a new file is created.
End Rem
Function SaveBank( bank:TBank,url:Object )
	Return bank.Save( url )
End Function

Rem
bbdoc: Get bank's memory buffer
returns: A byte pointer to the bank's internal memory buffer
about:
Please use #LockBank and #UnlockBank instead of this function.
End Rem
Function BankBuf:Byte Ptr( bank:TBank )
	Return bank.Buf()
End Function

Rem
bbdoc: Lock a bank's memory block
returns: A byte pointer to the memory block controlled by the bank
about:
While locked, a bank cannot be resized.

After you have finished with a bank's memory block, you must use #UnlockBank
to return it to the bank.
End Rem
Function LockBank:Byte Ptr( bank:TBank )
	Return bank.Lock()
End Function

Rem
bbdoc: Unlock a bank's memory block
about:
After you have finished with a bank's memory block, you must use #UnlockBank
to return it to the bank.
End Rem
Function UnlockBank( bank:TBank )
	bank.Unlock
End Function

Rem
bbdoc: Get size of bank
returns: The size, in bytes, of the bank's internal memory buffer
End Rem
Function BankSize( bank:TBank )
	Return bank.Size()
End Function

Rem
bbdoc: Get capacity of bank
returns: The capacity, in bytes, of the bank's internal memory buffer
about:
The capacity of a bank is the size limit before a bank must allocate
more memory due to a resize. Bank capacity may be increased due to a call
to #ResizeBank by either 50% or the requested amount, whichever is greater.
Capacity never decreases.
End Rem
Function BankCapacity( bank:TBank )
	Return bank.Capacity()
End Function

Rem
bbdoc: Resize a bank
about:
Modifies the size limit of a bank. This may cause memory to be allocated 
if the requested size is greater than the bank's current capacity, 
see #BankCapacity for more information.
End Rem
Function ResizeBank( bank:TBank,size )
	bank.Resize size
End Function

Rem
bbdoc: Copy bank contents
about:
Copies @count bytes from @src_offset in @src_bank to @dst_offset in @dst_bank.
End Rem
Function CopyBank( src_bank:TBank,src_offset,dst_bank:TBank,dst_offset,count )
	Assert..
	count&gt;=0 And..
	src_offset&gt;=0 And..
	dst_offset&gt;=0 And..
	src_offset+count&lt;=src_bank.Size() And..
	dst_offset+count&lt;=dst_bank.size() Else "Illegal range for CopyBank"
	MemCopy( dst_bank.Buf()+dst_offset,src_bank.Buf()+src_offset,count )
End Function

Rem
bbdoc: Peek a byte from a bank
returns: The byte value at the specified byte offset within the bank
End Rem
Function PeekByte( bank:TBank,offset )
	Return bank.PeekByte( offset )
End Function

Rem
bbdoc: Poke a byte into a bank
about:
A byte is an unsigned 8 bit value with a range of 0..255.
End Rem
Function PokeByte( bank:TBank,offset,value )
	bank.PokeByte offset,value
End Function

Rem
bbdoc: Peek a short from a bank
returns: The short value at the specified byte offset within the bank
End Rem
Function PeekShort( bank:TBank,offset )
	Return bank.PeekShort( offset )
End Function

Rem
bbdoc: Poke a short into a bank
about:
A short is an unsigned 16 bit (2 bytes) value with a range of 0..65535.
End Rem
Function PokeShort( bank:TBank,offset,value )
	bank.PokeShort offset,value
End Function

Rem
bbdoc: Peek an int from a bank
returns: The int value at the specified byte offset within the bank
End Rem
Function PeekInt( bank:TBank,offset )
	Return bank.PeekInt( offset )
End Function

Rem
bbdoc: Poke an int into a bank
about:
An int is a signed 32 bit integer (4 bytes).
End Rem
Function PokeInt( bank:TBank,offset,value )
	bank.PokeInt offset,value
End Function

Rem
bbdoc: Peek a long from a bank
returns: The long value at the specified byte offset within the bank
End Rem
Function PeekLong:Long( bank:TBank,offset )
	Return bank.PeekLong( offset )
End Function

Rem
bbdoc: Poke a long into a bank
about:
A long is a signed 64 bit integer (8 bytes).
End Rem
Function PokeLong( bank:TBank,offset,value:Long )
	bank.PokeLong offset,value
End Function

Rem
bbdoc: Peek a float from a bank
returns: The float value at the specified byte offset within the bank
End Rem
Function PeekFloat#( bank:TBank,offset )
	Return bank.PeekFloat( offset )
End Function

Rem
bbdoc: Poke a float into a bank
about:
A float is a signed 32 bit floating point (4 bytes).
End Rem
Function PokeFloat( bank:TBank,offset,value# )
	bank.PokeFloat offset,value
End Function

Rem
bbdoc: Peek a double from a bank
returns: The double value at the specified byte offset within the bank
End Rem
Function PeekDouble!( bank:TBank,offset )
	Return bank.PeekDouble( offset )
End Function

Rem
bbdoc: Poke a double into a bank
about:
A double is a signed 64 bit floating point (8 bytes).
End Rem
Function PokeDouble( bank:TBank,offset,value! )
	bank.PokeDouble offset,value
End Function

Rem
bbdoc: Read bytes from a stream to a bank
returns: The number of bytes successfully read from the stream
End Rem
Function ReadBank( bank:TBank,stream:TStream,offset,count )
	Return bank.Read( stream,offset,count )
End Function

Rem
bbdoc: Write bytes from a bank to a stream
returns: The number of bytes successfully written to the stream
End Rem
Function WriteBank( bank:TBank,stream:TStream,offset,count )
	Return bank.Write( stream,offset,count )
End Function
</textarea><br><br>And the zip contents.<br><br>copybank.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' copybank.bmx

bank:TBank=CreateBank( 16 )

For n=0 To 15
	PokeByte( bank,n,n^2 )
Next

ResizeBank( bank,32 )

CopyBank( bank,0,bank,16,12 )

For n=0 To BankSize( bank )-1
	Print n+": "+PeekByte( bank,n )
Next

ResizeBank( bank,16 )

Print "size: "+BankSize( bank )
Print "capacity: "+BankCapacity( bank )
</textarea><br><br>createstaticbank.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' createstaticbank.bmx

bank:TBank=CreateBank( 4 )

Local buf:Byte Ptr=BankBuf( bank )

newbank:TBank=CreateStaticBank( buf,4 )

PokeInt( newbank,0,123 )

Print "bank value: "+PeekInt( bank,0 )
Print "static bank value: "+PeekInt( newbank,0 )
Print "buffer address: "+Int( buf )
</textarea><br><br>lockbank.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' lockbank.bmx

bank:TBank=CreateBank( 4 )

Local buf:Byte Ptr=LockBank( bank )

buf[0]=246

Print "bank value: "+PeekByte( bank,0 )
Print "buffer value: "+Int( buf[0] )
Print "buffer address: "+Int( buf )

UnlockBank( bank )
</textarea><br><br>pokedouble.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' pokedouble.bmx

bank:TBank=CreateBank( 12 )

PokeFloat( bank,0,-3.4028234e+38 )
PokeDouble( bank,4,-1.7976931348623157e+308 )

Print PeekFloat( bank,0 )
Print PeekDouble( bank,4 )
</textarea><br><br>pokelong.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' pokelong.bmx

bank:TBank=CreateBank( 12 )

PokeInt( bank,0,-2147483647 )
PokeLong( bank,4,-9223372036854775807 )

Print PeekInt( bank,0 )
Print PeekLong( bank,4 )
</textarea><br><br>pokeshort.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' pokeshort.bmx

bank:TBank=CreateBank( 4 )

PokeByte( bank,0,255 )
PokeShort( bank,1,65535 )

Print PeekByte( bank,0 )
Print PeekShort( bank,1 )
</textarea><br><br>savebank.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' savebank.bmx

Local array:Int[]=[72,101,108,108,111,32,87,111,114,108,100,33]

bank:TBank=CreateBank( array.length )

For n=0 To array.length-1
	PokeByte( bank,n,array[n] )
Next

SaveBank( bank,"bank.txt" )

newbank:TBank=LoadBank( "bank.txt" )

For n=0 To BankSize( newbank )-1
	Print Chr( PeekByte( newbank,n ) )
Next
</textarea><br><br>writebank.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' writebank.bmx

Local array:Int[]=[72,101,108,108,111,32,87,111,114,108,100,33]

bank:TBank=CreateBank( array.length )

For n=0 To array.length-1
	PokeByte( bank,n,array[n] )
Next

file:TStream=WriteFile( "bank.txt" )

WriteBank( bank,file,0,array.length )

CloseFile( file )

file=OpenFile( "bank.txt" )

newbank:TBank=CreateBank( StreamSize( file ) )

ReadBank( newbank,file,0,StreamSize( file ) )

CloseFile( file )

For n=0 To BankSize( newbank )-1
	Print Chr( PeekByte( newbank,n ) )
Next
</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
