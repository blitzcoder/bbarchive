<!DOCTYPE html><html lang="en" ><head ><title >2D Plasma type thingi</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >2D Plasma type thingi</h1><a href="forums.php" >BlitzPlus Forums</a>/<a href="topics.php?forum=81" >BlitzPlus Programming</a>/<a href="#bottom" >2D Plasma type thingi</a><br><br>
<a name="284521"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Paul "Taiphoz"</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> LOL its me again. Im having one of those "hay how do they do that" type of few days.<br><br>This time its one of those cool looking plasma things Iv done a little research on the net about it but cant really seem to nail anything down, so I thought stuff it ill just have a go.<br><br><pre class=code>
; Plasma Test
;
;  Yavin
;
;  Teamrebellion.co.uk
;


Graphics 200,200,16,2
SetBuffer BackBuffer()
Cls


Global temp_image
temp_image=CreateImage(100,100)

Global counter%
counter=1

Dim lCos(360)
For looper=1 To 360
	lcos(looper)=Cos(looper)
Next



While Not KeyHit(1)
Cls
	draw_plasma()
	DrawImage temp_image,0,0
	Color 0,0,0
	Text 1,1,"C"+counter
Flip False

Wend

Function draw_plasma()
	;LockBuffer 
	For looper=1 To 200
		For inner=1 To 200
			Local r%,g%,a,b,c
			
			a=-Cos(looper)*looper
			b=Cos(looper)*looper
			c=-Cos(looper)*looper
			
			a=a-Sin(inner)*inner
			b=b+Sin(inner)*inner
			c=c-Sin(inner)*inner
			
			a=a+counter
			b=b+counter
			c=c+counter
			
			Color a,b,c
			Plot looper,inner
			
		Next
	Next
	;UnlockBuffer
	;SaveBuffer (BackBuffer(),"test.bmp")
	counter=counter+2:If counter=&gt;1000 Then counter=0
	
End Function
</pre> <br><br></td></tr></table><br>
<a name="284523"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Paul "Taiphoz"</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oh yeah you will note some of that aint actually used. like the sin table. im still working away on it.<br><br>If you run it you will see it looks ok, but its not as smoth as others I have seem and some times you see a hard edge.. so any help would be cool thanks. <br><br></td></tr></table><br>
<a name="284524"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Clyde</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Very very cool indeed! I love these demo effects!! :)<br><br>And welldone for it! Lets see some people help with this! :) <br><br></td></tr></table><br>
<a name="284530"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Perturbatio</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Pretty cool :)<br>a bit faster (but also slightly wrong).<br><pre class=code>
; Plasma Test
;
;  Yavin
;
;  Teamrebellion.co.uk
;


Graphics 200,200,16,2
SetBuffer BackBuffer()
Cls


Global temp_image
temp_image=CreateImage(100,100)

Global counter%
counter=1

Dim lCos(360)
For looper=1 To 360
	lcos(looper)=Cos(looper)
Next



While Not KeyHit(1)
Cls
	draw_plasma()
	DrawImage temp_image,0,0
	Color 0,0,0
	Text 1,1,"C"+counter
Flip False

Wend

Function draw_plasma()
;LOCAL VARS
Local argb
Local NewBuffer = ImageBuffer(temp_image)

LockBuffer NewBuffer

	For looper=1 To 200
		For inner=1 To 200
			Local r%,g%,a,b,c
			
			a=-Cos(looper)*looper
			b=Cos(looper)*looper
			c=-Cos(looper)*looper
			
			a=a-Sin(inner)*inner
			b=b+Sin(inner)*inner
			c=c-Sin(inner)*inner
			
			a=a+counter
			b=b+counter
			c=c+counter
			
			;Color a,b,c
			;Plot looper,inner

			argb=(c Or (b Shl 8) Or (a Shl 16) Or ($ff000000))
			
			WritePixelFast looper,inner, argb, NewBuffer
			
		Next
	Next
	UnlockBuffer NewBuffer
	;SaveBuffer (BackBuffer(),"test.bmp")
	counter=counter+2:If counter=&gt;1000 Then counter=0
	
End Function

</pre> <br><br></td></tr></table><br>
<a name="284533"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Perturbatio</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> *EDIT*<br>figured it out, you're looping 200 times when the image is only 100 wide<br><br><pre class=code>
; Plasma Test
;
;  Yavin
;
;  Teamrebellion.co.uk
;


Graphics 200,200,16,2
SetBuffer BackBuffer()
Cls


Global temp_image
temp_image=CreateImage(100,100)

Global counter%
counter=1

Dim lCos(360)
For looper=1 To 360
	lcos(looper)=Cos(looper)
Next



While Not KeyHit(1)
Cls
	draw_plasma()
	DrawImage temp_image,0,0
	Color 0,0,0
	Text 1,1,"C"+counter
Flip False

Wend

Function draw_plasma()
;LOCAL VARS
Local argb
Local NewBuffer = ImageBuffer(temp_image)

LockBuffer NewBuffer

	For looper=1 To 100
		For inner=1 To 100
			Local r%,g%,a,b,c
			
			a=-Cos(looper)*looper
			b=Cos(looper)*looper
			c=-Cos(looper)*looper
			
			a=a-Sin(inner)*inner
			b=b+Sin(inner)*inner
			c=c-Sin(inner)*inner
			
			a=a+counter
			b=b+counter
			c=c+counter
			
			;Color a,b,c
			;Plot looper,inner

			argb=(c Or (b Shl 8) Or (a Shl 16) Or ($ff000000))
			
			WritePixelFast looper,inner, argb, NewBuffer
			
		Next
	Next
	UnlockBuffer NewBuffer
	;SaveBuffer (BackBuffer(),"test.bmp")
	counter=counter+2:If counter=&gt;1000 Then counter=0
	
End Function
</pre> <br><br></td></tr></table><br>
<a name="284534"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> my 2 eurocents: (b+)<br><br><pre class=code>
;-------------------------
; hacky wacko plasma
;
; by CS^TBL
;-------------------------

Global sw=ClientWidth(Desktop())
Global sh=ClientHeight(Desktop())

app=CreateWindow("",0,0,sw,sh,0,0)

panel=CreatePanel(0,0,sw,sh,app)
SetPanelColor panel,0,0,0
Global c=CreateCanvas(0,0,128,64,panel)

SetGadgetLayout c,1,0,1,0
SetGadgetShape c,0,192*5/8,sw,sh*5/8

Global f,f2

SetBuffer CanvasBuffer(c)

timer=CreateTimer(30)
Repeat

	WaitEvent()
	
	If EventID()=$4001
		plazma()
		FlipCanvas c
	EndIf
	
	If EventID()=$803
		quit=True
	EndIf
	
Until quit
FreeTimer timer
FreeGadget c
End

Function plazma()
	f=f+2
	f2=f2-6
	
	For x=-64 To 64
		For y=0 To 64
			px=Sin((f2+x*-5)+(f+y*5))*Sin((f2+x*-4)+(f+y*3))*16
			py=Cos((f2+x*-8)+(f+y*-4))*Cos((x*5)+(f2+y*-2))*17
			
			dx=x+px
			dy=y+py
			
			dx=dx
			dy=dy-32
			d#=Sqr((dx*dx)+(dy*dy))
	
			d#=d#*3
			d#=255-d#
	
			If d#&gt;255 Then d#=255
			If d#&lt;0 Then d#=0
	
			
			;d#=128+Sin(d#*3)*111  ; sinecolor
			
			b=(d#+px+(x*13))*.25
			g=(d#-px+py+(y*6))*.5
			r=(d#+py+(y*x/10))*1
			
			If r&lt;0 Then r=0		
			If g&lt;0 Then g=0		
			If b&lt;0 Then b=0		
			If r&gt;255 Then r=255		
			If g&gt;255 Then g=255		
			If b&gt;255 Then b=255		
			Color r,g,b
			Rect (64+x)*1,y*1,1,1
		Next
	Next

End Function
</pre> <br><br></td></tr></table><br>
<a name="284544"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Perturbatio</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Now if only it could be made tileable...<br><br><pre class=code>
; Plasma Test
;
;  Yavin
;
;  Teamrebellion.co.uk
;


Graphics 400,400,16,2
SetBuffer BackBuffer()
Cls


Global temp_image
temp_image=CreateImage(200,200)

Global counter%
counter=1

Dim lCos(360)
For looper=1 To 360
	lcos(looper)=Cos(looper)
Next



While Not KeyHit(1)
Cls
	draw_plasma()
	DrawImage temp_image,0,0
	DrawImage temp_image,199,0
	DrawImage temp_image,0,199
	DrawImage temp_image,199,199
	Color 0,0,0
	Text 1,1,"C"+counter
Flip False

Wend



Function draw_plasma()
;LOCAL VARS
Local argb
Local NewBuffer = ImageBuffer(temp_image)

LockBuffer NewBuffer

	For looper=1 To 200
		For inner=1 To 200
			Local r%,g%,a,b,c
			
			a= -Cos(looper) * looper
			b= Cos(looper)  * looper
			c= -Cos(looper) * looper
			
			a= a - Sin(inner) * inner
			b= b + Sin(inner) * inner
			c= c - Sin(inner) * inner
			
			a=a+counter
			b=b+counter
			c=c+counter
			
			Color a,b,c
			;Plot looper,inner

			argb=(c Or (b Shl 8) Or (a Shl 16) Or ($ff000000))
			;Rect looper,inner,1,1,1 
			WritePixelFast looper,inner, argb, NewBuffer
			
		Next
	Next
	UnlockBuffer NewBuffer
	;SaveBuffer (BackBuffer(),"test.bmp")
	counter=counter+2:If counter=&gt;1000 Then counter=0
	
End Function
</pre> <br><br></td></tr></table><br>
<a name="284604"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Paul "Taiphoz"</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Tile is easy.<br><br><pre class=code>
; Plasma Test 
; 
; Yavin 
; 
; Teamrebellion.co.uk 
; 

Const gfx_width=640
Const gfx_height=480

Graphics 640,480,16,0
SetBuffer BackBuffer() 
Cls 


Const size=100

Global temp_image 
temp_image=CreateImage(size,size) 

Global display_image
display_image=CreateImage(gfx_width,gfx_height)


Global counter% 
counter=1 

Dim pos(size,size)

Dim lCos(360) 
For looper=1 To 360 
lcos(looper)=Cos(looper) 
Next 



While Not KeyHit(1) 
Cls 
draw_plasma() 
render_screen()
For x=0 To gfx_width Step 198
	For y=0 To gfx_height Step 198
		DrawImage display_image,x,y
	Next
Next
Flip

Wend 


Function render_screen()

	
	DrawImage temp_image,0,0
	flip_vert() 
	DrawImage temp_image,99,0 
	flip_vert()
	flip_hor()
	DrawImage temp_image,0,99
	flip_vert()
	DrawImage temp_image,99,99

	CopyRect 0,0,200,200,0,0,BackBuffer(),ImageBuffer(display_image)
	
	
End Function


Function flip_vert()
	SetBuffer ImageBuffer(temp_image)
	LockBuffer
	For x=1 To ImageWidth(temp_image)
		For y=1 To ImageHeight(temp_image)
			pos(x,y)=ReadPixelFast(x,y,ImageBuffer(temp_image))
		Next
	Next
	For x=ImageWidth(temp_image) To 1 Step -1
		For y=ImageHeight(temp_image) To 1 Step -1
			WritePixelFast(x,y,pos(ImageWidth(temp_image)-x,y),ImageBuffer(temp_image))
		Next
	Next
	UnlockBuffer 
	SetBuffer BackBuffer()
End Function


Function flip_hor()
	SetBuffer ImageBuffer(temp_image)
	LockBuffer
	For x=1 To ImageWidth(temp_image)
		For y=1 To ImageHeight(temp_image)
			pos(x,y)=ReadPixelFast(x,y,ImageBuffer(temp_image))
		Next
	Next
	For x=ImageWidth(temp_image) To 1 Step -1
		For y=ImageHeight(temp_image) To 1 Step -1
			WritePixelFast(x,y,pos(x,ImageHeight(temp_image)-y),ImageBuffer(temp_image))
		Next
	Next
	UnlockBuffer 
	SetBuffer BackBuffer()
End Function


Function draw_plasma() 
;LOCAL VARS 
Local argb 
Local NewBuffer = ImageBuffer(temp_image) 

LockBuffer NewBuffer 

	For looper=1 To size 
		For inner=1 To size 
			Local r%,g%,a,b,c 
			
			a= -Cos(looper) * inner 
			b= Cos(looper) * inner 
			c= -Cos(looper) * inner 
			
			a= a + Sin(inner) * looper 
			b= b - Sin(inner) * looper 
			c= c + Sin(inner) * looper 
			
			a=a+counter 
			b=b+counter 
			c=c+counter 
			
			Color a,b,c 
			;Plot looper,inner 
			
			argb=(c Or (b Shl 8) Or (a Shl 16) Or ($ff000000)) 
			;Rect looper,inner,1,1,1 
			WritePixelFast looper,inner, argb, NewBuffer 
			
		Next 
	Next 
	
UnlockBuffer NewBuffer 
;SaveBuffer (BackBuffer(),"test.bmp") 
counter=counter+2:If counter=&gt;1000 Then counter=0 

End Function 
</pre><br><br>I still cant seem to get it looking right though, which has to be something to do with my sin cos maths. so anyone got any ideas  ? <br><br></td></tr></table><br>
<a name="284646"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> No fair, CS, as you were/are part of The Black Lotus, if I'm interpreting your nick correctly! Nice though! <br><br></td></tr></table><br>
<a name="284659"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> yeah, tho I just was one of the the composer there. The NL coders were all asm/c++ gurus, and asm/c++ both look kinda like abacadabra to me, so their help wasn't of much use for me. The things that I know about coding and algos, are things that I developed/discovered on my own. This plasma is fairly simple, I did it over a year ago in B3d actually, just a month ago I decided to run it in a b+ canvas that I could stretch.<br>Originally I didn't even intend to make a plasma but a spherical glow/flare.. some texgens call it an enviroment map. And I was like: hey, what if you mess some vars up here and there, and bingo, a plasma was born. :)<br><br>btw, officially I'm still @ TBL, but the PC division is pretty dead now. <br><br></td></tr></table><br>
<a name="284672"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Paul "Taiphoz"</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> CS yours is nice m8. I had to convert it back to B3D but its still not what im after.<br><br>What im looking for is one more like the one I have done, but more fluid. if you check it you will see what I mean. <br><br></td></tr></table><br>
<a name="284779"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Perturbatio</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> I don't know if it exists in BlitzPlus, but in B3D there is a 2D example called cows and bees, that uses a plasma background, you could take a look at that for inspiration<br><br>in fact, here's the code that draws the plasma from that example:<br><pre class=code>
	dy=dy-1

	; replace with sheep coords
	musx=manx	
	musy=many


	For x=0 To 640 Step 8

		For y=2 To 395 Step 8
			cv=Rnd(-2,2)
			cv=cv+Abs(x-musx)*Sin(y+(dy))
			cv=cv+Abs(y-musy)*Cos((x+dy)+Sin((x+y)))
			cv=cv+Abs(x-musx)
			bv=Abs(x-musx)*(Sin(y))
			bv=bv+Abs(y-musy)*Sin(x)
			bv=bv+(musx+musy)/12
			bv=bv+(cv/12)


			cv=255-cv
			If bv&lt;0 Then bv=0
			If bv&gt;255 Then bv=255

			If cv&gt;255 Then cv=255
			cv=cv-100
			If cv&lt;0 Then cv=9
			tv=cv+bv
			tv=tv*Sin(dy)*8
			If tv&lt;0 Then tv=0
			If tv&gt;255 Then tv=255
			Color tv,bv,cv+(tv/3)
			;Plot x,y
			Rect x,y,8,8,True
		Next
	Next
</pre> <br><br></td></tr></table><br>
<a name="284815"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Richard Betson</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> <img src="http://members.getgoin.net/~vidiot/pic.jpg"><br><br><pre class=code>
; Plasma Test
;
;  Yavin
;
;  Teamrebellion.co.uk
;

;Code modified by Richard R Betson 11/10/03
;Faster lookups added.
;Tiled with CopyRect For fullscreen FX.

Global gw=320
Global gh=240

Graphics 640,480,16,2
SetBuffer BackBuffer()
Cls


Global temp_image
temp_image=CreateImage(gw,gh)

Global counter%
counter=1

Dim lCos(360)
For looper=1 To 360
	lcos(looper)=Cos(looper)
Next
Dim aa(gw,gh)
Dim bb(gw,gh)
Dim cc(gw,gh)


plasma_lu()


Color 255,255,255


While Not KeyHit(1)

	draw_plasma()
	DrawImage temp_image,0,0
		For ii=1 To gw
			CopyRect ii-1,0,1,gh,(gw*2)-ii,0,BackBuffer(),BackBuffer()
		Next  
	
		For ii=1 To gh
			CopyRect 0,ii-1,(gw*2),1,0,(gh*2)-ii,BackBuffer(),BackBuffer()
		Next  



	fps=fps+1
	If fps_t&lt;MilliSecs()
	fp$=" "+Str$(fps)
	fps2=fps
	fps_t=1000+MilliSecs()
	fps=0
	EndIf
	Color 255,255,255
	Text 10,15,"FPS: "+fp$

Flip False

If KeyHit(2)=True
	SaveBuffer(FrontBuffer(),"pic.bmp")
EndIf


Wend







Function draw_plasma()
	
	LockBuffer ImageBuffer(temp_image)

	For looper=1 To gw-1
		For inner=1 To gh-1
			Local r%,g%,a,b,c
			
			a=aa(looper,inner)
			b=bb(looper,inner)
			c=cc(looper,inner)
			
			
			a=a+counter
			b=b+counter
			c=c+counter
			
			WritePixelFast looper,inner,((c Shl 16) Or (b Shl 8 ) Or (a )),ImageBuffer(temp_image) 
		Next
	Next
    UnlockBuffer ImageBuffer(temp_image)
	counter=counter+2:If counter=&gt;1000 Then counter=0
	
End Function

Function plasma_lu()
	For looper=1 To gw
		For inner=1 To gh
			Local r%,g%,a,b,c
			
			aa(looper,inner)=-Cos(looper)*looper
			bb(looper,inner)=Cos(looper)*looper
			cc(looper,inner)=-Cos(looper)*looper
			
			aa(looper,inner)=aa(looper,inner)-Sin(inner)*inner
			bb(looper,inner)=bb(looper,inner)+Sin(inner)*inner
			cc(looper,inner)=cc(looper,inner)-Sin(inner)*inner
			
		Next
	Next

End Function

</pre><br><br><br>&gt;Added Look-up Table<br>&gt;Tiled/flipped 640 x 480<br>&gt;FPS 58 fullscreen ---  43 Windowed<br><br><br><br>Yavin,<br><br>I modified this to run faster with a look-up table and a tiled/fliped screen using copyrect. <br><br>Did it just for fun... ;)<br>Good work Yavin :)<br><br><br>PS: This seems to crash in B+ but works fine in B3D (mmmm...)<br><br>L8r, <br><br></td></tr></table><br>
<a name="284852"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bremer</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> There are no bounds checking for writepixelfast in blitz+ so your lines that looks like this:<br><br>For looper=1 To gw<br>For inner=1 To gh<br><br>needs to be like this:<br><br>For looper=0 To gw-1<br>For inner=0 To gh-1<br><br>What you are doing is drawing pixels from 1 to 320 and from 1 to 240, but the image you have created is 0-239 and 0-319. If you use WPF outside a buffer in blitz+ you are very likely to crash it and do other strange things because you are writing data to memory that has not been reserved. Always be sure that you keep within the screen/image buffer when using WPF or use some kind of manual bounds checking. <br><br></td></tr></table><br>
<a name="284875"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Richard Betson</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> zawran,<br><br>Right! :)<br><br>Forgot...Fixed...<br><br>Runs slower in B+ - FPS 29 windowed, 33 fullscreen.<br><br>Blitting would make it faster :)<br><br>L8r, <br><br></td></tr></table><br>
<a name="285110"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bremer</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Runs at 51fps on my computer windowed, and 65fps fullscreen. I have a 2.4ghz, 512mb ram, Geforce4mx 64mb. <br><br></td></tr></table><br>
<a name="286185"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Clyde</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> Brilliant stuff &amp; Welldone guys! :) <br><br></td></tr></table><br>
<a name="332010"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Paul "Taiphoz"</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> Man I forgot all about this thread. lol glad I found it againc cos I lost my source.<br><br>Thanks to those who added to it. <br><br></td></tr></table><br>
<a name="350345"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Clyde</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> How did you get on with this mate? <br><br></td></tr></table><br>
<a name="350542"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mark Tiffany</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> FWIW, here's my plasma style effect from the title screen of my game Viper (link in sig).<br><br><pre class=code>
; Plasma effect written by Mark Tiffany for Viper menu screens.
Global Plasma_Cycle
Global Plasma_Width
Global Plasma_Height
Global Plasma_Image
Const MAXSIN=256
Global LSinB
Global Plasma_Speed=0
Global Plasma_On=True

;----------------------- TEST CODE
;
Global SCREEN_WIDTH=640, SCREEN_HEIGHT=480
Graphics SCREEN_WIDTH,SCREEN_HEIGHT,16,2
SetBuffer BackBuffer()
ClsColor 255,255,255
Cls
PlasmaSetup()
Repeat
	SetBuffer BackBuffer()
	PlasmaDraw()
	Flip False
	If KeyHit(1) Then Exit
Forever
End

;----------------------- END OF TEST CODE

Global buffer_fmt,buffer_bank,buffer_offset_x,buffer_offset_y,buffer_base
Global buffer_offset_x2,buffer_offset_x4,buffer_offset_x8
Global buffer_offset_y2,buffer_offset_y4,buffer_offset_y8
Global buffer_rowsize,buffer_rowsize2,buffer_rowsize4,mask

Function PlasmaSetup()
	Plasma_Image=CreateImage(SCREEN_WIDTH,SCREEN_HEIGHT,1,4)
	buff=ImageBuffer(Plasma_Image)
	SetBuffer buff
	ClsColor 255,255,255
	Cls	
	LockBuffer(buff)
	buffer_fmt=LockedFormat(buff)
	buffer_bank=LockedPixels(buff)
	buffer_offset_y=LockedPitch(buff)
	If buffer_fmt=1 Then 
		buffer_offset_x=4
		buffer_base=17
		mask=$40
	ElseIf buffer_fmt=1 Then 
		buffer_offset_x=4
		buffer_base=17
		mask=$20		
	ElseIf buffer_fmt=3 Then
		buffer_offset_x=3
		buffer_base = 140
		mask=$FF
	ElseIf buffer_fmt=4 Then
		buffer_offset_x=4
		buffer_base = 140
		mask=$FF
	End If	
	UnlockBuffer(buff)
	
	LSinB = CreateBank(MAXSIN+1)
	
	For n=0 To MAXSIN
		If buffer_fmt&lt;3 Then
			PokeByte LSinB,n,buffer_base + 6 * Sin(n*360.0/256.0)
		Else
			PokeByte LSinB,n, buffer_base + 50 * Sin(n*360.0/256.0)
		End If
	Next
	Plasma_Width = Ceil((SCREEN_WIDTH/8)+0.5)-2
	Plasma_Height = Ceil((SCREEN_HEIGHT/8)+0.5)-2

	buffer_offset_x2 = buffer_offset_x * 2
	buffer_offset_x4 = buffer_offset_x * 4
	buffer_offset_x8 = buffer_offset_x * 8
	buffer_offset_y2 = buffer_offset_y * 2
	buffer_offset_y4 = buffer_offset_y * 4	
	buffer_offset_y8 = buffer_offset_y * 8
	buffer_rowsize   = buffer_offset_x * SCREEN_WIDTH
	buffer_rowsize2  = buffer_rowsize * 2
	buffer_rowsize4  = buffer_rowsize * 4
	
	time=MilliSecs()
	PlasmaDraw()
	Plasma_Speed=MilliSecs()-time
	If Plasma_Speed&gt;100 Then
		Plasma_On=False
	Else
		Plasma_On=True
	End If
	
End Function

Function PlasmaDraw()
	Local rgb
	Local base_r,base_g,base_b
	Local r,g,b
	
	If Plasma_On Then
		
		; cycle
		If Plasma_Cycle &gt; 254 Then
			Plasma_Cycle = 0
		Else
			Plasma_Cycle = Plasma_Cycle + 1
		End If
	
		buff=ImageBuffer(Plasma_Image)
		SetBuffer buff
		LockBuffer(buff) 
		
		base_r=Plasma_Cycle
		base_g=192+Plasma_Cycle
		base_b=256-Plasma_Cycle
		
		For y = 0 To Plasma_Height
			offset = buffer_offset_y8 * y 
			ioffset = offset
			y2=y Shl 1
			r = base_r+y2
			g = base_g+y2
			b = base_b-y 
			For x = 0 To Plasma_Width
				rr = PeekByte(LSinB,((r - x) And mask))
				gg = PeekByte(LSinB,((g + x + x) And mask))
				bb = PeekByte(LSinB,((b - x) And mask))			
				If buffer_fmt=1 Then
					rgb=(rr Shl 11) Or (gg Shl 5) Or bb
					rgb=(rgb Shl 16) Or rgb
				ElseIf buffer_fmt=2
					rgb=(rr Shl 10) Or (gg Shl 5) Or bb
					rgb=(rgb Shl 16) Or rgb
				ElseIf buffer_fmt=3
					rgb=(rr Shl 16) Or (gg Shl 8) Or bb
				ElseIf buffer_fmt=4
					rgb=$7F000000 Or (rr Shl 16) Or (gg Shl 8) Or bb
				End If		
							
				PokeInt buffer_bank,offset,rgb
				CopyBank buffer_bank,offset,buffer_bank,offset+buffer_offset_x,buffer_offset_x
				CopyBank buffer_bank,offset,buffer_bank,offset+buffer_offset_x2,buffer_offset_x2
				CopyBank buffer_bank,offset,buffer_bank,offset+buffer_offset_x4,buffer_offset_x4				
				offset=offset+buffer_offset_x8
			Next
			CopyBank buffer_bank,ioffset,buffer_bank,ioffset+buffer_offset_y,buffer_rowsize
			CopyBank buffer_bank,ioffset,buffer_bank,ioffset+buffer_offset_y2,buffer_rowsize2
			CopyBank buffer_bank,ioffset,buffer_bank,ioffset+buffer_offset_y4,buffer_rowsize4
		Next	
		UnlockBuffer(buff)
	End If
	SetBuffer BackBuffer()
	DrawBlock Plasma_Image,0,0
End Function
</pre><br><br>Hmmmmmm, I probably ought to have put a couple of comments in there!  Ah well. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
