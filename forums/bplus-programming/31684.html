<!DOCTYPE html><html lang="en" ><head ><title >How to load tiles(as layers) for maps</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >How to load tiles(as layers) for maps</h1><a href="forums.php" >BlitzPlus Forums</a>/<a href="topics.php?forum=81" >BlitzPlus Programming</a>/<a href="#bottom" >How to load tiles(as layers) for maps</a><br><br>
<a name="338308"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Apollonius</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I forgot how to load tiles and display using data 0,0,0,0,....<br><br>its using FOR but I forgot could anyone give me an example? <br><br></td></tr></table><br>
<a name="338394"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> make a map using data ..<br><br>a minimap of 4x4 is like this:<br><br>data 1,1,1,1<br>data 1,0,0,1<br>data 1,0,0,1<br>data 1,1,1,1<br><br>read it in a 2d array, or bank (if you know how to use a bank in a 2d array-style)<br><br>when you draw your map, the number in the data-lines (1 or 0 in this example) refers to one of these: (1) an offset in a large image containing misc tiles (walls, trees, paths etc.) or (2) a unique iamge-handle with 1 tile.<br><br>then using a 'for y for x next next' loop draw your map<br><br>but also try old pages in the beginner-area (around page 18..22) I'm sure there're already plenty of examples. <br><br></td></tr></table><br>
<a name="338397"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> here ... I dug-up this old (Blitz+) example :) it's prolly at this forum already, but I can't find it anymore.<br><br>No types anywhere, I hate types ..<br><br><pre class=code>; size of your desktop
Global sw=ClientWidth(Desktop())
Global sh=ClientHeight(Desktop())

; a fullscreen window
Global APP=CreateWindow("very basic but ample MAP tutorial :)",0,0,sw,sh,0,0)

; a 640x480 canvas
Global APPcanvas=CreateCanvas(0,0,640,480,APP)
; make the canvas 'stretchable'
SetGadgetLayout APPcanvas,1,0,1,0
; make it the size of the parent-window (fullscreen)
SetGadgetShape APPcanvas,0,0,sw,sh


; make room for 4 32x32 tiles, and a 32x32 tile for the player
Global TILES=CreateImage(128,32)
Global PLAYER=CreateImage(32,32)

; make a map (32x32 tiles)
Dim MAPgfx(32,32),MAPwalk(32,32) ; MAPwalk defines where you can walk, independent of the gfx

PrepareGFX()
PrepareMAP()

Global quit=False
Global cameraX,cameraY,playerX=5,playerY=5,grid=False,coords=False


SetBuffer CanvasBuffer(APPcanvas)

If MAPwalk(playerX+cameraX,playerY+cameraY)=1
	drawmap()
	Notify "change some factors in PrepareMAP(), or change playerX and/or playerY coords"
	quit=True
EndIf



Drawmap()


timer=CreateTimer(20) ; 20 fps

;-------------------------------------------------------------
Repeat
	WaitEvent()
	If EventID()=$803 quit=True ; alt f4=quit
	If EventID()=$4001 updategame()
Until quit

; today we are not lazy as usual, and we actually FREE the images&amp;stuff :)
FreeImage TILES
FreeImage PLAYER
FreeGadget APP
End

;-------------------------------------------------------------

Function updategame()

	If KeyDown(200) ; up
		If MAPwalk(cameraX+playerX,cameraY+PlayerY-1)=0
			playerY=playerY-1
			If playerY&lt;4
				cameraY=cameraY-1
				If cameraY&lt;0
					cameraY=0
				Else
					playerY=4
				EndIf
			EndIf
		EndIf
		Drawmap()
	EndIf
	
	If KeyDown(208) ; dn
		If MAPwalk(cameraX+playerX,cameraY+PlayerY+1)=0
			playerY=playerY+1
			If playerY&gt;10
				cameraY=cameraY+1
				If cameraY&gt;17
					cameraY=17
				Else
					playerY=10
				EndIf
			EndIf
		EndIf
		Drawmap()
	EndIf
	
	If KeyDown(203) ; left
		If MAPwalk(cameraX+playerX-1,cameraY+PlayerY)=0
			playerX=playerX-1
			If playerX&lt;4
				cameraX=cameraX-1
				If cameraX&lt;0
					cameraX=0
				Else
					playerX=4
				EndIf
			EndIf
		EndIf
		Drawmap()
	EndIf
	
	If KeyDown(205) ; right
		If MAPwalk(cameraX+playerX+1,cameraY+PlayerY)=0
			playerX=playerX+1
			If playerX&gt;15
				cameraX=cameraX+1
				If cameraX&gt;12
					cameraX=12
				Else
					playerX=15
				EndIf
			EndIf
		EndIf
		Drawmap()
	EndIf
	
	If KeyHit(28) ; enter
		grid=1-grid ; toggle between 0 and 1
		Drawmap()
	EndIf

	If KeyHit(14) ; backspace
		coords=1-coords ; toggle between 0 and 1
		Drawmap()
	EndIf

End Function

Function Drawmap()
	Color 0,0,0
	For y=0 To 14 ; 15x32 = 480
		For x=0 To 19 ; 20 x 32 = 640
			DrawBlockRect TILES,x*32,y*32,MAPgfx(cameraX+x,cameraY+y)*32,0,32,32 ; draw a part of an image  [[tile1][tile2][etc][..]]
			If grid
				Rect x*32,y*32,33,33,False
			EndIf
		Next
	Next
	DrawImage PLAYER,playerX*32,playerY*32
	
	If coords
		Color 0,0,0
		Text 20,20,playerX
		Text 20,30,playerY
		Text 40,20,cameraX
		Text 40,30,cameraY
		Color 164,175,186
		Rect 58,15,24,32,True
		Color 0,0,0
		Text 60,20,cameraX+playerX
		Text 60,30,cameraY+playerY
	EndIf
	
	FlipCanvas APPcanvas
End Function


Function PrepareMAP()
	; prepare a map

	; nothing special to learn here, it's just a quick way to generate a map, usually you design your map in a map editor
	
	For y=0 To 31
		For x=0 To 31
			MAPgfx(x,y)=1 ; wall

			If y&gt;0 And y&lt;31 ; only draw inside the outer walls
				If x&gt;0 And x&lt;31
					MAPgfx(x,y)=0
					
					; plant trees
					treefactor=Rnd(0,10)
					If treefactor&gt;9
						MAPgfx(x,y)=3
					EndIf
					
					; build walls
					longwallfactor=Rnd(0,90)
					If longwallfactor&gt;89
						px=Rnd(1,30)
						py=Rnd(1,20)
						For t=0 To 6
							MAPgfx(px,py+t)=1
						Next
						px=Rnd(1,20)
						py=Rnd(1,30)
						For t=0 To 6
							MAPgfx(px+t,py)=1
						Next
					EndIf
					
					; let the elephants urinate here :)
					waterfactor=Rnd(0,50)
					If waterfactor&gt;49
						px=Rnd(1,24)
						py=Rnd(1,24)
						For tx=0 To 3
							For ty=0 To 3
								MAPgfx(px+tx,py+ty)=2
							Next
						Next
					EndIf
				EndIf
				
			EndIf
		Next
	Next
	; prepare the walkmap: 0=walk, 1=don't walk
	For y=0 To 31
		For x=0 To 31
			Select MAPgfx(x,y)
				Case 0 ; grass
					MAPwalk(x,y)=0
				Case 1 ; wall
					MAPwalk(x,y)=1
				Case 2 ; water
					MAPwalk(x,y)=1
				Case 3 ; tree
					MAPwalk(x,y)=1
			End Select
		Next
	Next
End Function

Function PrepareGFX()
	
	; nothing special to learn here, it's just a quick way to generate gfx, usually you design your gfx in a gfx editor
	
	SetBuffer ImageBuffer(TILES)

	; grass (and grass for the tree)
	For x=0 To 31
		For y=0 To 31
			Color Rnd(0,32),Rnd(160,255),Rnd(64,128)
			Plot x,y
			Plot 96+x,y
		Next
	Next
	
	; wall
	Color 192,128,80
	Rect 32,0,32,32,True
	
	Color 96,96,96
	For y=0 To 31 Step 8
		Rect 32,y,32,2,True
	Next
	For y=0 To 3
		yy= (y Mod 2)
		For x=(yy*8) To 16+(yy*8) Step 16
			Rect 32+x,y*8,2,8,True
		Next
	Next
	
	; water
	
	Color 0,0,255
	Rect 64,0,32,32,True
	For t=0 To 128
		c=Rnd(0,3)
		If c&lt;1 Color 255,255,255
		If c&gt;=1 Color 128,128,255
		x=Rnd(0,31)
		y=Rnd(0,31)
		Plot 64+x,y
	Next
	
	; tree
	
	Color 200,80,0
	Rect 96+14,24,4,8,True
	
	Color 0,128,0
	Oval 96+4,0,24,24,True
	
	; player  (well .. sortof :)
	SetBuffer ImageBuffer(PLAYER)
	
	Color 255,255,255
	Oval 8,8,16,16,True
	Oval 12,4,8,8,True
	Rect 12,16,2,16,True
	Rect 18,16,2,16,True
	Color 1,1,1
	Plot 14,6
	Plot 17,6
	
End Function</pre> <br><br></td></tr></table><br>
<a name="338402"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LarsG</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>No types anywhere, I hate types ..<br> <br></div><br><br>You must be nuts!! lol <br><br></td></tr></table><br>
<a name="338405"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> hmm no nuts today.. .. I just hate types, that's all :)<br><br>don't you hate something too? sprots? spinache? spam? :) <br><br></td></tr></table><br>
<a name="338419"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LarsG</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I hate spam.. both types.. I have to agree on that.. <br><br></td></tr></table><br>
<a name="338420"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> both 'types' :) <br><br></td></tr></table><br>
<a name="338448"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >sswift</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> CS:<br>I hated types too.  And you might nto believe me, but you will grow to love them.  The sooner you learn how to use them effectively, the better.  They make a lot of stuff much easier. <br><br></td></tr></table><br>
<a name="338453"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> I like the 'idea' of types, but I don't like the way Blitz offers them. Here's one area in which a c++ class looks cleaner to me! (prolly the only area)<br><br>I go with banks instead.., needs a little more thinking ahead,  but the end-result looks cleaner than a type. <br><br></td></tr></table><br>
<a name="338541"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Apollonius</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> dunno what a bank is or what it looks like nor what it eats in winter :| <br><br></td></tr></table><br>
<a name="338592"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> a bank is just a piece of memory..<br><br>bla=CreateBank(1000)<br><br>now you have 1000 bytes of memory for you to abuse.. (to store variables/large maps etc.)<br><br>A bank is the way to make your own commands that look like normal blitz commands, instead of types that look like types.<br><br>See the help for CreateBank, PokeByte, PeekByte, PokeShort, PeekShort, PokeInt, PeekInt, PokeFloat, PeekFloat, BankSize, ResizeBank and FreeBank.<br><br>for maps, instead of storing 1 byte per maptile in a 2d array like this: dim map(100,100)<br><br>you could use a bank, like this: map=CreateBank(100*100)<br><br>in this case:<br>memlocation 0..99 equals x:0..99 / y:0<br>memlocation 100..199 equals x:0..99 / y:1<br>memlocation 200..299 equals x:0..99 / y:2<br><br>etc.<br><br>however, only when you use bytes.. if you use shorts (=2 bytes) then you need a bank twice this size and then:<br><br>memlocation 0..199 step 2 equals x:0..99 / y:0<br>memlocation 200..399 step 2 equals x:0..99 / y:1<br><br>etc. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
