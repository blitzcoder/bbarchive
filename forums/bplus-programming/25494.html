<!DOCTYPE html><html lang="en" ><head ><title >TileMap Collision Problem</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >TileMap Collision Problem</h1><a href="forums.php" >BlitzPlus Forums</a>/<a href="topics.php?forum=81" >BlitzPlus Programming</a>/<a href="#bottom" >TileMap Collision Problem</a><br><br>
<a name="265197"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >maverick69</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have developed a TileEngine using Blitz. Each Tile can have different values. For example if a tile has a value of 1 a collision is possible, if the value is 0, no collision occurs.<br><br>Now i have a problem: I have a lot of enemy objects. They fly into one direction and if they collide with a tile they should fly in the other direction but mathematical correct (a little bit like a ball).<br><br>I have done a little exmaple programm to show you my problem.<br><br>Does anyone know how to solve this problem?<br><br><pre class=code>
Graphics 320,256
SetBuffer BackBuffer()
SeedRnd MilliSecs();

;Generate Random Map
Dim Map(10,8)
For a=0 To 10
	For b=0 To 8
		Map(a,b)=Rand(0,10)
	Next
Next

Type player
	Field x
	Field y
	Field dx
	Field dy
End Type
Global player.player = New player

player\x=100
player\y=100
player\dx=2
player\dy=2

Repeat
	ClsColor 0,0,0
	Cls
	DrawMap()
	
	;
	; Calc new player position
	;
	
	player\x=player\x+player\dx
	player\y=player\y+player\dy

	If player\x&gt;320 Or player\x&lt;0 Then player\dx=-player\dx
	If player\y&gt;256 Or player\y&lt;0 Then player\dy=-player\dy
	
	If player\x&gt;320 Then player\x=320
	If player\x&lt;0 Then player\x=0
	If player\y&gt;256 Then player\y=256
	If player\y&lt;0 Then player\y=0
	
	;
	; check for wall
	;
	
	If isWall(player\x,player\y)
		 ;
		 ; // TODO //
		 ;
		 ; player\dx=-player\dx
		 ; player\dy=-player\dy
	End If
	
	;
	; draw player
	;
	
	Color 255,0,0
	Rect player\x-1,player\y-1,2,2,1
	Color 64,64,64
	
	Flip
Forever

Function isWall(x,y)
	If Map(x/32,y/32)=8 Then Return True
	Return False
End Function

Function DrawMap()
For a=0 To 10
	For b=0 To 8
		If Map(a,b)=8 ;blocktile
		Rect a*32,b*32,32,32,1
		End If
	Next
Next						
End Function
</pre> <br><br></td></tr></table><br>
<a name="265226"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> hi! <br><br>Try moving the enemy down/up, check for a collision. If one occurs, reverse the y direction of the enemy. Then move the enemy left/right, check for a collision, if one occurs then reverse the x direction. Should keep you right.:)  Oh and take out the repeat...forever loop and replace it with a<br><pre class=code>
While Not KeyHit(1)


Wend
</pre><br><br>loop. it means that ppl can exit the game when escpe is pressed :) <br><br></td></tr></table><br>
<a name="265439"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Curtastic</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> you just need to seperate the x and y movement and collision<br>and add "if keyhit(1) then end". better than a while loop becuase you can put it wherever you want (like in handleinput())<br><br><pre class=code>
Graphics 320,256
SetBuffer BackBuffer()
SeedRnd MilliSecs();

;Generate Random Map
Dim Map(10,8)
For a=0 To 10
	For b=0 To 8
		Map(a,b)=Rand(0,10)
	Next
Next

Type player
	Field x
	Field y
	Field dx
	Field dy
End Type
Global player.player = New player

player\x=100
player\y=100
player\dx=2
player\dy=2

Repeat
	ClsColor 0,0,0
	Cls
	DrawMap()
	
	;
	; Calc new player position
	;
	
	player\x=player\x+player\dx

	If player\x&gt;320 Or player\x&lt;0 Then player\dx=-player\dx
	
	If player\x&gt;320 Then player\x=320
	If player\x&lt;0 Then player\x=0

	;
	; check for wall x
	;
	If isWall(player\x,player\y)
		player\dx=-player\dx
		player\x=player\x+player\dx
	End If

	player\y=player\y+player\dy
	If player\y&gt;256 Or player\y&lt;0 Then player\dy=-player\dy
	If player\y&gt;256 Then player\y=256
	If player\y&lt;0 Then player\y=0
	
	;
	; check for wall y
	;
	If isWall(player\x,player\y)
		player\dy=-player\dy
		player\y=player\y+player\dy
	End If
	
	
	;
	; draw player
	;
	
	Color 255,0,0
	Rect player\x-1,player\y-1,2,2,1
	Color 64,64,64
	
	Flip
	If KeyHit(1) Then End
Forever

Function isWall(x,y)
	If Map(x/32,y/32)=8 Then Return True
	Return False
End Function

Function DrawMap()
For a=0 To 10
	For b=0 To 8
		If Map(a,b)=8 ;blocktile
		Rect a*32,b*32,32,32,1
		End If
	Next
Next						
End Function</pre> <br><br></td></tr></table><br>
<a name="265712"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mark1nc</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> You might also want to use Floor() instead of using the built-in Int().  The Int() rounds anything over .5 up to 1<br>Floor() drops the fraction, which is what you want done.<br><br>Function isWall(x,y)<br>	If Map(Floor(x/32),Floor(y/32))=8 Then Return True<br>	Return False<br>End Function <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
