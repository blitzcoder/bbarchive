<!DOCTYPE html><html lang="en" ><head ><title >AlphaDrawing functions</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >AlphaDrawing functions</h1><a href="forums.php" >BlitzPlus Forums</a>/<a href="topics.php?forum=81" >BlitzPlus Programming</a>/<a href="#bottom" >AlphaDrawing functions</a><br><br>
<a name="1084181"></a>

<a name="1084185"></a>

<a name="1084186"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >InvincibleWall</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> okay from the help on my last post I think I have got this transparency thing down.... could you guys tell me how I did??<br><br>Conv (ConvR,ConvG,ConvB... ext) functions were provided by Yasha<br><br><br>these are the same thing<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Const ScreenWidth = 800
Const ScreenHeight = 600

;-------------------------------------------------------------------------------------------------
;Small Demo
Graphics ScreenWidth,ScreenHeight


While Not KeyHit(1)
   MX = MouseX()
   MY = MouseY()
   Angle = Angle + 5
   If Angle &gt; 360
       Angle = 0
   End If
   Cls
   Color 255,255,255
   Rect 200,200,100,100
   LockBuffer()
   RectAlpha(250,250,100,100,100,200,200,.5)
   FadedCircleAlpha(50 * (Cos(Angle)) + MX,50 * (Sin(Angle)) + MY,30,255,160,0,.5)
   FadedCircleAlpha(50 * (Cos(Angle + 90)) + MX,50 * (Sin(Angle + 90)) + MY,30,255,255,0,.5)
   FadedCircleAlpha(50 * (Cos(Angle + 180)) + MX,50 * (Sin(Angle + 180)) + MY,30,200,0,0,.5)
   FadedCircleAlpha(50 * (Cos(Angle + 270)) + MX,50 * (Sin(Angle + 270)) + MY,30,0,0,170,.5)
   UnlockBuffer()
   Flip
Wend

EndGraphics
End
;----------------------------------------------------------------------------------------------------


;-----------------------------------------------------------------------------------------
;uses PlotAlpha(x,y,r,g,b,a) to alter the color of a  Faded Circle on the screen accorrding to the rgb entered by the percent of Alpha (a value between 1 and 0)
;Because of PlotAlpha() this Function requires the buffer to be locked with LockBuffer()
;Because of PlotAlpha() this Function also requires the Height and Width of the screen stored to referencing
Function FadedCircleAlpha(x,y,ra,r,g,b,a#)
   Local alpha#,d
   For x1 = 0 To ra * 2
       For y1 = 0 To ra * 2
           d = (x1 - ra) * (x1 - ra) + (y1 - ra) * (y1 - ra)
           Alpha = (1 - (Float(d)/Float(ra * ra))) * a
           If Alpha &gt; 0
               If d &lt;= ra * ra
                   PlotAlpha(x1 + x - ra,y1 + y - ra,r,g,b,alpha)
               End If
           End If
       Next
   Next
End Function

;-----------------------------------------------------------------------------------------
;uses PlotAlpha(x,y,r,g,b,a) to alter the color of a Circle on the screen accorrding to the rgb entered by the percent of Alpha (a value between 1 and 0)
;Because of PlotAlpha() this Function requires the buffer to be locked with LockBuffer()
;Because of PlotAlpha() this Function also requires the Height and Width of the screen stored to referencing
Function CircleAlpha(x,y,ra,r,g,b,a#)
   Local d
   For x1 = 0 To ra * 2
       For y1 = 0 To ra * 2
           d = (x1 - ra) * (x1 - ra) + (y1 - ra) * (y1 - ra)
           If d &lt;= ra * ra
               PlotAlpha(x1 + x - ra,y1 + y - ra,r,g,b,a)
           End If
       Next
   Next
End Function

;-----------------------------------------------------------------------------------------
;uses PlotAlpha(x,y,r,g,b,a) to alter the color of a rectangle on the screen accorrding to the rgb entered by the percent of Alpha (a value between 1 and 0)
;Because of PlotAlpha() this Function requires the buffer to be locked with LockBuffer()
;Because of PlotAlpha() this Function also requires the Height and Width of the screen stored to referencing
Function RectAlpha(x,y,w,h,r,g,b,a#)
   For x1 = 0 To w
       For y1 = 0 To h
           PlotAlpha(x1 + x,y1 + y,r,g,b,a)
       Next
   Next
End Function

;-----------------------------------------------------------------------------------------
;alters the color of a pixel on the screen accorrding to the rgb entered by the percent of Alpha (a value between 1 and 0)
;This Function requires the buffer to be locked with LockBuffer()
;This Function also requires the Height and Width of the screen stored to referencing
Function PlotAlpha(x,y,r,g,b,a#)
   If x &lt; 0 Then Return False
   If x &gt;= ScreenWidth Then Return False
   If y &lt; 0 Then Return False
   If y &gt;= ScreenHeight Then Return False
   Local argb = ReadPixelFast(x,y,BackBuffer())
   Local red1 = ConvR(argb)
   Local green1 = ConvG(argb)
   Local blue1 = ConvB(argb)
   Local red2 = red1 + ((r - red1) * a)
   Local green2 = green1 + ((g - green1) * a)
   Local blue2 = blue1 + ((b - blue1) * a)
   argb = ConvRGBInt(red2,green2,blue2,255)
   WritePixelFast(x,y,argb,BackBuffer())
End Function

;-----------------------------------------------------------------------------------------
;get Red value out of color integer
Function ConvR%(argb%) 
   Return (argb And $FF0000) Shr 16
End Function

;-----------------------------------------------------------------------------------------
;get Green value out of color integer
Function ConvG%(argb%)
   Return (argb And $FF00) Shr 8
End Function

;-----------------------------------------------------------------------------------------
;get Blue value out of color integer
Function ConvB%(argb%)
   Return (argb And $FF)
End Function

;-----------------------------------------------------------------------------------------
;get Alpha value out of color integer
Function ConvA%(argb%)
   Return (argb And $FF000000) Shr 24
End Function

;-----------------------------------------------------------------------------------------
;Convert Red,Green,Blue,Alpha into argb color integer
Function ConvRGBInt(r,g,b,a)
   Return (a Shl 24) Or (r Shl 16) Or (g Shl 8) Or b
End Function

;----------------------------------------------------------------------------------------- </textarea><br><br><pre class=code>Const ScreenWidth = 800
Const ScreenHeight = 600

;-------------------------------------------------------------------------------------------------
;Small Demo
Graphics ScreenWidth,ScreenHeight


While Not KeyHit(1)
   MX = MouseX()
   MY = MouseY()
   Angle = Angle + 5
   If Angle &gt; 360
       Angle = 0
   End If
   Cls
   Color 255,255,255
   Rect 200,200,100,100
   LockBuffer()
   RectAlpha(250,250,100,100,100,200,200,.5)
   FadedCircleAlpha(50 * (Cos(Angle)) + MX,50 * (Sin(Angle)) + MY,30,255,160,0,.5)
   FadedCircleAlpha(50 * (Cos(Angle + 90)) + MX,50 * (Sin(Angle + 90)) + MY,30,255,255,0,.5)
   FadedCircleAlpha(50 * (Cos(Angle + 180)) + MX,50 * (Sin(Angle + 180)) + MY,30,200,0,0,.5)
   FadedCircleAlpha(50 * (Cos(Angle + 270)) + MX,50 * (Sin(Angle + 270)) + MY,30,0,0,170,.5)
   UnlockBuffer()
   Flip
Wend

EndGraphics
End
;----------------------------------------------------------------------------------------------------


;-----------------------------------------------------------------------------------------
;uses PlotAlpha(x,y,r,g,b,a) to alter the color of a  Faded Circle on the screen accorrding to the rgb entered by the percent of Alpha (a value between 1 and 0)
;Because of PlotAlpha() this Function requires the buffer to be locked with LockBuffer()
;Because of PlotAlpha() this Function also requires the Height and Width of the screen stored to referencing
Function FadedCircleAlpha(x,y,ra,r,g,b,a#)
   Local alpha#,d
   For x1 = 0 To ra * 2
       For y1 = 0 To ra * 2
           d = (x1 - ra) * (x1 - ra) + (y1 - ra) * (y1 - ra)
           Alpha = (1 - (Float(d)/Float(ra * ra))) * a
           If Alpha &gt; 0
               If d &lt;= ra * ra
                   PlotAlpha(x1 + x - ra,y1 + y - ra,r,g,b,alpha)
               End If
           End If
       Next
   Next
End Function

;-----------------------------------------------------------------------------------------
;uses PlotAlpha(x,y,r,g,b,a) to alter the color of a Circle on the screen accorrding to the rgb entered by the percent of Alpha (a value between 1 and 0)
;Because of PlotAlpha() this Function requires the buffer to be locked with LockBuffer()
;Because of PlotAlpha() this Function also requires the Height and Width of the screen stored to referencing
Function CircleAlpha(x,y,ra,r,g,b,a#)
   Local d
   For x1 = 0 To ra * 2
       For y1 = 0 To ra * 2
           d = (x1 - ra) * (x1 - ra) + (y1 - ra) * (y1 - ra)
           If d &lt;= ra * ra
               PlotAlpha(x1 + x - ra,y1 + y - ra,r,g,b,a)
           End If
       Next
   Next
End Function

;-----------------------------------------------------------------------------------------
;uses PlotAlpha(x,y,r,g,b,a) to alter the color of a rectangle on the screen accorrding to the rgb entered by the percent of Alpha (a value between 1 and 0)
;Because of PlotAlpha() this Function requires the buffer to be locked with LockBuffer()
;Because of PlotAlpha() this Function also requires the Height and Width of the screen stored to referencing
Function RectAlpha(x,y,w,h,r,g,b,a#)
   For x1 = 0 To w
       For y1 = 0 To h
           PlotAlpha(x1 + x,y1 + y,r,g,b,a)
       Next
   Next
End Function

;-----------------------------------------------------------------------------------------
;alters the color of a pixel on the screen accorrding to the rgb entered by the percent of Alpha (a value between 1 and 0)
;This Function requires the buffer to be locked with LockBuffer()
;This Function also requires the Height and Width of the screen stored to referencing
Function PlotAlpha(x,y,r,g,b,a#)
   If x &lt; 0 Then Return False
   If x &gt;= ScreenWidth Then Return False
   If y &lt; 0 Then Return False
   If y &gt;= ScreenHeight Then Return False
   Local argb = ReadPixelFast(x,y,BackBuffer())
   Local red1 = ConvR(argb)
   Local green1 = ConvG(argb)
   Local blue1 = ConvB(argb)
   Local red2 = red1 + ((r - red1) * a)
   Local green2 = green1 + ((g - green1) * a)
   Local blue2 = blue1 + ((b - blue1) * a)
   argb = ConvRGBInt(red2,green2,blue2,255)
   WritePixelFast(x,y,argb,BackBuffer())
End Function

;-----------------------------------------------------------------------------------------
;get Red value out of color integer
Function ConvR%(argb%) 
   Return (argb And $FF0000) Shr 16
End Function

;-----------------------------------------------------------------------------------------
;get Green value out of color integer
Function ConvG%(argb%)
   Return (argb And $FF00) Shr 8
End Function

;-----------------------------------------------------------------------------------------
;get Blue value out of color integer
Function ConvB%(argb%)
   Return (argb And $FF)
End Function

;-----------------------------------------------------------------------------------------
;get Alpha value out of color integer
Function ConvA%(argb%)
   Return (argb And $FF000000) Shr 24
End Function

;-----------------------------------------------------------------------------------------
;Convert Red,Green,Blue,Alpha into argb color integer
Function ConvRGBInt(r,g,b,a)
   Return (a Shl 24) Or (r Shl 16) Or (g Shl 8) Or b
End Function

;----------------------------------------------------------------------------------------- </pre><br><br><font class="tiny">Last edited 2011</font><br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1084247"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >InvincibleWall</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Um? Did anyone see this? <br><br></td></tr></table><br>
<a name="1084288"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Andy_A</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> When it comes to displaying graphics, faster is almost always better.<br><br>Ditching the RGB functions, and in-lining the one line statements in the PlotAlpha() function, gives a 10% speed increase.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Function PlotAlpha(x,y,r,g,b,a#)
   If x &lt; 0 Then Return False
   If x &gt;= ScreenWidth Then Return False
   If y &lt; 0 Then Return False
   If y &gt;= ScreenHeight Then Return False
   Local argb = ReadPixelFast(x,y,BackBuffer())
   Local red1 = (argb And $FF0000) Shr 16 ;ConvR(argb)
   Local green1 = (argb And $FF00) Shr 8 ;ConvG(argb)
   Local blue1 = (argb And $FF) ;ConvB(argb)
   Local red2 = red1 + ((r - red1) * a)
   Local green2 = green1 + ((g - green1) * a)
   Local blue2 = blue1 + ((b - blue1) * a)
   argb = $FF000000 Or (red2 Shl 16) Or (green2 Shl 8) Or blue2 ;ConvRGBInt(red2,green2,blue2,255)
   WritePixelFast(x,y,argb,BackBuffer())
End Function</textarea><br><br>By the way, the ConvA() function is never used.<br><br>Other than those nitpiks, the alpha-blending works fine. <br><br></td></tr></table><br>
<a name="1084327"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GregH</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> InvincibleWall,<br><br>Good job.  Alpha Blending isn't something you see every day in BlitzPlus.  Its good to see I'm not the only one still using BlitzPlus.  The only thing I changed in your demo was:<br><br>;Small Demo<br>Graphics ScreenWidth,ScreenHeight,0,2<br><br>I did this so BlitPlus would run in a window at the native screen depth.  BlitzPlus just seems to me like it should be run in a window because of what it is...BlitzBasic with a Windows GUI.<br><br>I'm still working on getting a handle of BlitzMax and OOP but I keep going back to BlitzPlus to whip out simple Windows apps. <br><br></td></tr></table><br>
<a name="1084340"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >InvincibleWall</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cool, yah faster is always better...<br>Do you think these are fast enough to be used in a game as engine exhaust, planet glow and lasers?<br><br>I made a small test with them with 3 missiles with engine exhaust and a background of 100 pulsating stars (radii of 1-3) and it worked fine on my computer (which isn't very fast) until I started using circles with a radius over 25<br><br>And I'm not quite sure how to test speed of a program (accurately)... So I don't know how much faster it is to use CircleAlpha instead of FadedCircleAlpha<br><br>Lol GregH I use BlitzPlus cuz that's all I have... I'm Using it as an elective in Highschool :)<br>Though I am thinking a great deal about getting Bmax or B3d because of B+ limitations (such as this slow alpha blending thing)<br><br>Thx for the input guys <br><br></td></tr></table><br>
<a name="1084356"></a>

<a name="1084357"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Andy_A</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> It's your demo but with timing code to see which is faster, FadeCircle or Circle (and the tweak to PlotAlpha).<br><br>L-click to toggle between Fade and Circle.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Const ScreenWidth = 800
Const ScreenHeight = 600

;-------------------------------------------------------------------------------------------------
;Small Demo
Graphics ScreenWidth,ScreenHeight,0,2


While Not KeyHit(1)
	MX = MouseX()
	MY = MouseY()
	Angle = Angle + 5
	If Angle &gt; 360
		Angle = 0
	End If
	Cls
	Color 255,255,255
	Rect 200,200,100,100

	count% = count + 1
	st% = MilliSecs()
	

	;=================================================
	If MouseHit(1) Then test% = Not test
	;=================================================
	; toggle this variable to see difference in timing
	; between FadeCircleAlpha and CircleAlpha
	;=================================================
	
	LockBuffer()

	RectAlpha(250,250,100,100,100,200,200,.5)

	If test = 0 Then
		FadedCircleAlpha(50 * (Cos(Angle)      ) + MX,50 * (Sin(Angle)      ) + MY,30,255,160,  0,.5)
		FadedCircleAlpha(50 * (Cos(Angle +  90)) + MX,50 * (Sin(Angle +  90)) + MY,30,255,255,  0,.5)
		FadedCircleAlpha(50 * (Cos(Angle + 180)) + MX,50 * (Sin(Angle + 180)) + MY,30,200,  0,  0,.5)
		FadedCircleAlpha(50 * (Cos(Angle + 270)) + MX,50 * (Sin(Angle + 270)) + MY,30,  0,  0,170,.5)
	Else
		CircleAlpha(50 * (Cos(Angle)      ) + MX,50 * (Sin(Angle)      ) + MY,30,255,160,  0,.5)
		CircleAlpha(50 * (Cos(Angle +  90)) + MX,50 * (Sin(Angle +  90)) + MY,30,255,255,  0,.5)
		CircleAlpha(50 * (Cos(Angle + 180)) + MX,50 * (Sin(Angle + 180)) + MY,30,200,  0,  0,.5)
		CircleAlpha(50 * (Cos(Angle + 270)) + MX,50 * (Sin(Angle + 270)) + MY,30,  0,  0,170,.5)
	End If
	UnlockBuffer()
	et% = MilliSecs()-st

	If count &gt; 99 Then
		rate# = Float(avg + et)/100.
		count = 0
		Text 5,540, rate#
		avg% = 0
	Else
		avg = avg + et
		Text 5,540, rate#
	End If
		
		
	Flip
Wend

EndGraphics
End
;----------------------------------------------------------------------------------------------------


;-----------------------------------------------------------------------------------------
;uses PlotAlpha(x,y,r,g,b,a) to alter the color of a  Faded Circle on the screen accorrding to the rgb entered by the percent of Alpha (a value between 1 and 0)
;Because of PlotAlpha() this Function requires the buffer to be locked with LockBuffer()
;Because of PlotAlpha() this Function also requires the Height and Width of the screen stored to referencing
Function FadedCircleAlpha(x,y,ra,r,g,b,a#)
	Local Alpha#, ra2#, d%

	ra2 = ra*ra
	For x1 = 0 To ra * 2
		For y1 = 0 To ra * 2
			d = (x1 - ra) * (x1 - ra) + (y1 - ra) * (y1 - ra)
			Alpha = (1 - (Float(d)/ra2)) * a
			If Alpha &gt; 0 Then
				If d &lt;= ra2
					PlotAlpha(x1 + x - ra,y1 + y - ra,r,g,b,Alpha)
				End If
			End If
		Next
	Next
End Function

;-----------------------------------------------------------------------------------------
;uses PlotAlpha(x,y,r,g,b,a) to alter the color of a Circle on the screen accorrding to the rgb entered by the percent of Alpha (a value between 1 and 0)
;Because of PlotAlpha() this Function requires the buffer to be locked with LockBuffer()
;Because of PlotAlpha() this Function also requires the Height and Width of the screen stored to referencing
Function CircleAlpha(x,y,ra,r,g,b,a#)
   Local d
   For x1 = 0 To ra * 2
       For y1 = 0 To ra * 2
           d = (x1 - ra) * (x1 - ra) + (y1 - ra) * (y1 - ra)
           If d &lt;= ra * ra
               PlotAlpha(x1 + x - ra,y1 + y - ra,r,g,b,a)
           End If
       Next
   Next
End Function

;-----------------------------------------------------------------------------------------
;uses PlotAlpha(x,y,r,g,b,a) to alter the color of a rectangle on the screen accorrding to the rgb entered by the percent of Alpha (a value between 1 and 0)
;Because of PlotAlpha() this Function requires the buffer to be locked with LockBuffer()
;Because of PlotAlpha() this Function also requires the Height and Width of the screen stored to referencing
Function RectAlpha(x,y,w,h,r,g,b,a#)
   For x1 = 0 To w
       For y1 = 0 To h
           PlotAlpha(x1 + x,y1 + y,r,g,b,a)
       Next
   Next
End Function

;-----------------------------------------------------------------------------------------
;alters the color of a pixel on the screen accorrding to the rgb entered by the percent of Alpha (a value between 1 and 0)
;This Function requires the buffer to be locked with LockBuffer()
;This Function also requires the Height and Width of the screen stored to referencing
Function PlotAlpha(x,y,r,g,b,a#)
   If x &lt; 0 Then Return False
   If x &gt;= ScreenWidth Then Return False
   If y &lt; 0 Then Return False
   If y &gt;= ScreenHeight Then Return False
   Local argb = ReadPixelFast(x,y,BackBuffer())
   Local red1 = (argb And $FF0000) Shr 16
   Local green1 = (argb And $FF00) Shr 8
   Local blue1 = (argb And $FF)
   Local red2 = red1 + ((r - red1) * a)
   Local green2 = green1 + ((g - green1) * a)
   Local blue2 = blue1 + ((b - blue1) * a)
   argb = $FF000000 Or (red2 Shl 16) Or (green2 Shl 8) Or blue2
   WritePixelFast(x,y,argb,BackBuffer())
End Function

</textarea><br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1084439"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >InvincibleWall</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cool thx for the code Andy_A <br><br></td></tr></table><br>
<a name="1084514"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Andy_A</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> no prob.<br><br>Now, get to work on that game!  :) <br><br></td></tr></table><br>
<a name="1084994"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >InvincibleWall</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> well The alpha functions cause exsesive amounts of lag, so they have been ditched =(<br><br>the effects they made in small amounts were amazing though =) <br><br></td></tr></table><br>
<a name="1085000"></a>

<a name="1085001"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Andres</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> is your space background black? if it is, you could for example try draw the planet glow as a plain image (no alpha) and then the stars with alpha.<br><br>this would be much faster, if it could be done like that.<br><br>edit:<br>here's my TGA functions with alpha: <a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=1814" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=1814</a><br><br>it only draws the alpha pixels with plotting and the no alpha pixels are drawn with blitzes DrawImage(), maybe it will help you too in some cases.<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
