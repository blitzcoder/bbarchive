<!DOCTYPE html><html lang="en" ><head ><title >Bank monitor .. lil present</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Bank monitor .. lil present</h1><a href="forums.php" >BlitzPlus Forums</a>/<a href="topics.php?forum=81" >BlitzPlus Programming</a>/<a href="#bottom" >Bank monitor .. lil present</a><br><br>
<a name="309519"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Once in a while you'll pull your hair out while debugging when working with banks ..<br>On one such day I decided to the stuff below .. perhaps not the cleanest code in terms of optimization or var naming.. but it works, and doesn't require globals..<br><br>Mousewheel and right mousebutton are supported.<br><br>opinions! does it suck? is it handy? If someone makes use of it, well, I guess I didn't waste my afternoon then :)<br><br><pre class=code>
Function ViewBank(bank)
	FlushEvents()

	BankMonitor=CreateWindow("Bank Monitor   (by CS^TBL)",32,32,706,584,0,1)
	
	BankHome=CreateButton("Home",656,8,40,24,BankMonitor)
	Bank16m=CreateButton("-16",656,48,40,24,BankMonitor)
	Bank16p=CreateButton("+16",656,76,40,24,BankMonitor)
	Bank512m=CreateButton("-512",656,112,40,24,BankMonitor)
	Bank512p=CreateButton("+512",656,140,40,24,BankMonitor)
	HighliteNotZero=CreateButton("&gt;0",656,170,40,24,BankMonitor,2)
	ViewtypePanel=CreatePanel(652,200,44,40,BankMonitor)
	ViewHex=CreateButton("HEX",0,0,44,16,ViewtypePanel,3):SetButtonState ViewHex,True
	ViewDec=CreateButton("DEC",0,24,44,16,ViewtypePanel,3)
	ViewVarPanel=CreatePanel(652,260,44,60,BankMonitor)
	ViewShort=CreateButton("Short",0,0,44,16,ViewVarPanel,3)
	ViewInt=CreateButton("Int",0,20,44,16,ViewVarPanel,3):SetButtonState ViewInt,True
	ViewFloat=CreateButton("Float",0,40,44,16,ViewVarPanel,3)
	
	Drawselector=False
	BankCanvas=CreateCanvas(8,8,512+130,512+32,BankMonitor)

	SetCanvas BankCanvas
		ClsColor 0,0,80:Cls
	
	size=BankSize(bank)
	offset=0
	redrawBankView=True
	quitBankMonitor=False
	
	Repeat

		If redrawBankView
		
			Cls
			
			redrawBankView=False
		
			For y=0 To 31
				For x=0 To 15

					index=x+(y*16)+(offset*16)
					If index&lt;size And index=&gt;0 ; inside bank
					
						; &gt;0 highlite
						byte=PeekByte(bank,index)
						If byte
							If ButtonState(HighliteNotZero)
								Color 0,0,160
								Rect x*32,y*16,32,16,True
								Rect 512+x*8,y*16,8,16,True
							EndIf
						EndIf

						; txt
						Color 255,255,255
						If ButtonState(ViewHex) ; hex
							Text x*32,y*16,RSet(Right$(Hex(PeekByte(bank,index)),2),3)
						Else ; dec
							Text x*32,y*16,RSet(PeekByte(bank,index),3)
						EndIf
						; right column
						Text 512+(x*8),y*16,Chr(PeekByte(bank,index))
						
					Else ; outside bank
						Color 255,255,255
						Text x*32,y*16,RSet(".",3)
						Text 512+(x*8),y*16,"."
					EndIf
					
				Next
			Next
			Color 0,0,180:Line 256,0,256,511
			Line 0,514,511,514
			
			If Drawselector
				If ButtonState(ViewShort)
					For t=0 To 1
						y=((DrawselectorValue+t) / 16)-offset
						If y&lt;32
							x=(DrawselectorValue+t) Mod 16
							Color 255,160,0
							Rect x*32,y*16,32,16,False
						EndIf
					Next
				EndIf
				If ButtonState(ViewInt)
					For t=0 To 3
						y=((DrawselectorValue+t) / 16)-offset
						If y&lt;32
							x=(DrawselectorValue+t) Mod 16
							Color 255,160,0
							Rect x*32,y*16,32,16,False
						EndIf
					Next
				EndIf
				If ButtonState(ViewFloat)
					For t=0 To 7
						y=((DrawselectorValue+t) / 16)-offset
						If y&lt;32
							x=(DrawselectorValue+t) Mod 16
							Color 255,160,0
							Rect x*32,y*16,32,16,False
						EndIf
					Next
				EndIf
			EndIf
			
			Color 255,255,255
			If DrawselectorValue&lt;size
				Text 208,520,"Mem adres: "+DrawselectorValue+" ("+Hex(DrawselectorValue)+")"
			Else
				Text 208,520,"Mem adres: out of range!"
			EndIf
			
			Text 480,520,"View: "+(offset*16)+".."+(511+offset*16)
			
			If ButtonState(ViewShort)
				If DrawselectorValue&lt;(size-1)
					Text 4,520,"Short: "+PeekShort(bank,DrawselectorValue)
				EndIf
			EndIf
			If ButtonState(ViewInt)
				If DrawselectorValue&lt;(size-3)
					Text 4,520,"Int  : "+PeekInt(bank,DrawselectorValue)
				EndIf
			EndIf
			If ButtonState(ViewFloat)
				If DrawselectorValue&lt;(size-7)
					Text 4,520,"Float: "+PeekFloat(bank,DrawselectorValue)
				EndIf
			EndIf
			
			FlipCanvas BankCanvas
			
		EndIf

		;----------------------------------------------------------------

		WaitEvent()
		
		If EventID()=$401
			If EventSource()=BankHome ; home
				offset=0:redrawBankView=True
			EndIf
			If EventSource()=Bank16m ; 16--
				offset=offset-1
				If offset&lt;0 offset=0
				redrawBankView=True
			EndIf
			If EventSource()=Bank16p ; 16++
				offset=offset+1:redrawBankView=True
			EndIf
			If EventSource()=Bank512m ; 512--
				offset=offset-32
				If offset&lt;0 offset=0
				redrawBankView=True
			EndIf
			If EventSource()=Bank512p ; 512++
				offset=offset+32:redrawBankView=True
			EndIf
			If EventSource()=HighliteNotZero ; &gt;0
				redrawBankView=True
			EndIf
			If EventSource()=ViewHex ; hex
				redrawBankView=True
			EndIf
			If EventSource()=ViewDec ; dec
				redrawBankView=True
			EndIf
		EndIf
		If EventID()=$803
			quitBankMonitor=True
		EndIf
		If EventID()=$201
			If EventData()=2
				If ButtonState(ViewFloat)
					SetButtonState(ViewFloat),False
					SetButtonState ViewShort,True
				ElseIf ButtonState(ViewInt)
					SetButtonState(ViewInt),False
					SetButtonState ViewFloat,True
				ElseIf ButtonState(ViewShort)
					SetButtonState(ViewShort),False
					SetButtonState ViewInt,True
				EndIf
				redrawBankView=True
			EndIf
		EndIf
		If EventID()=$203 ; mouse move
			If EventX()&lt;512 And EventY()&lt;512
				DrawselectorValue=(EventX()/32)+(((EventY()/16)+offset)*16)

				Drawselector=True
				; check for da boundaries when reading out short/int/float				
				If ButtonState(ViewShort)
					If DrawselectorValue&gt;(size-2)
						Drawselector=False
					EndIf
				EndIf
				If ButtonState(ViewInt)
					If DrawselectorValue&gt;(size-4)
						Drawselector=False
					EndIf
				EndIf
				If ButtonState(ViewFloat)
					If DrawselectorValue&gt;(size-8)
						Drawselector=False
					EndIf
				EndIf
				
				redrawBankView=True
			Else
				Drawselector=False:redrawBankView=True
			EndIf
		EndIf
		If EventID()=$204 ; wheel
			offset=offset-EventData()
			If offset&lt;0 offset=0
			redrawBankView=True
		EndIf
		
		If EventID()=$206 ; leave
			Drawselector=False:redrawBankView=True
		EndIf
		
	Until quitBankMonitor

	FlushEvents()	
	FreeGadget BankMonitor
End Function
</pre><br><br>my 2ct. <br><br></td></tr></table><br>
<a name="309520"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> darn I knew I forgot a part of my 'lazy.bb' :)<br><br><pre class=code>
Function SetCanvas(bla)
	SetBuffer CanvasBuffer(bla)
End Function
</pre><br><br>I guess you'll know how to modify the code if you don't want this lazy-ass function (I've tons of them lazy stuff) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
