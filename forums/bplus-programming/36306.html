<!DOCTYPE html><html lang="en" ><head ><title >Drawing Order in 2D Tile Editor</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Drawing Order in 2D Tile Editor</h1><a href="forums.php" >BlitzPlus Forums</a>/<a href="topics.php?forum=81" >BlitzPlus Programming</a>/<a href="#bottom" >Drawing Order in 2D Tile Editor</a><br><br>
<a name="397286"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Adam Bossy</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm working on a tile editor for my game and ran into the problem of the order the images are drawn.<br><br>I want to allow the user to be able to draw to different layers (4 total) and to be able to move tiles forward or backwards in the order they are drawn, as in a graphics program like Photoshop.<br><br>Right now, the technique I'm working for is 4 different types, one with a pointer for each layer. The drawing order would be determined by the order that is contained in that type object. Therefore, I would have to use "Before" and "After" to re-sort the tiles everytime the position of one is changed. Has anybody encountered this sort of problem in a tile editor? In what way did you program it in order for it to be efficient and functional?<br><br>I'm doing this all in 2D. I know many people suggest using 3D sprites instead. Is the extra work to change from 2D to 3D worth it, or necessary, even? What advantages are there to each? <br><br></td></tr></table><br>
<a name="397300"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skn3</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>
Type layers
	Field s.tiles
	Field e.tiles
End Type

Type tiles
	Field image
	Field x
	Field y
	
	Field layer.layers
End Type

Function CreateLayer.layers()
	Local layer.layers
	
	;create new layer
	layer = New layers
	
	Return layer
End Function

Function DeleteLayer(layer.layers)
	Local tile.tiles
	
	;clear tiles from layer
	If layer\s &lt;&gt; Null
		tile = layer\s
		Repeat
			If tile = layer\e
				Delete tile
				Exit
			Else
				tile = After tile
				Delete Before tile
			End If
		Forever
	End If
	
	;delete layer
	Delete layer
End Function

Function CreateTile.tiles(image,x,y,layer.layers)
	Local tile.tiles
	
	;create new tile
	tile       = New tiles
	tile\image = image
	tile\x     = x
	tile\y     = y
	tile\layer = layer
	
	;insert tile into layer
	If layer\s = Null
		;this is first tile in layer, set start pointer to tile
		layer\s = tile
	Else
		;there are other tiles in this layer, add tile to end of list
		Insert tile After layer\e
	End If
	;update the end pointer to this tile
	layer\e = tile
	
	Return tile
End Function

Function DeleteTile(tile.tiles)
	;update layers list of tiles
	If tile = tile\layer\s And tile = tile\layer\e
		;if this tile is the only tile in layer NULL start end pointers (clear the list)
		tile\layer\s = Null
		tile\layer\e = Null
	ElseIf tile = tile\layer\s
		;tile is first in list, update start pointer
		tile\layer\s = After tile
	ElseIf tile = tile\layer\e
		;tile is last in list, update end pointer
		tile\layer\e = Before tile
	End If
	
	;delete tile
	Delete tile
End Function

Function MoveLayerUp(layer.layers)
	;if the layer before this layer exists, then move this layer, before it
	If Before layer &lt;&gt; Null Insert layer Before (Before layer)
End Function

Function MoveLayerDown(layer.layers)
	;if the layer after this layer exists, then move this layer, after it
	If After layer &lt;&gt; Null Insert layer After (After layer)
End Function

Graphics 640,480,32,2

;frame timer
Global timer = CreateTimer(50)

;create 3 layers
Global layer1.layers = CreateLayer()
Global layer2.layers = CreateLayer()
Global layer3.layers = CreateLayer()

;create some tile images
Global image1 = CreateImage(32,32)
Global image2 = CreateImage(32,32)
Global image3 = CreateImage(32,32)
SetBuffer ImageBuffer(image1)
ClsColor 255,0,0
Cls
SetBuffer ImageBuffer(image2)
ClsColor 0,255,0
Cls
SetBuffer ImageBuffer(image3)
ClsColor 0,0,255
Cls

;set active layer and image
Global currentlayer.layers = layer1.layers
Global currentimage        = image1

SetBuffer BackBuffer()
ClsColor 255,255,255
Repeat
	Cls	
		;render layers
		If Last layers &lt;&gt; Null
			layer.layers = Last layers
			Repeat
				;render this layers tiles
				If layer\s &lt;&gt; Null
					tile.tiles = layer\s
					Repeat
						DrawImage tile\image,tile\x,tile\y
						
						If tile = layer\e Exit
						tile = After tile
					Forever
				End If
				
				If layer = First layers Exit
				layer = Before layer
			Forever
		End If
		
		;user interface
		x = MouseX()
		y = MouseY()
		If x &gt; 160
			DrawImage currentimage,x,y
			If MouseHit(1)
				CreateTile(currentimage,x,y,currentlayer)
			End If
		End If
		
		If KeyHit(2) currentlayer = layer1
		If KeyHit(3) currentlayer = layer2
		If KeyHit(4) currentlayer = layer3
			
		If KeyHit(200) MoveLayerUp(currentlayer)
		If KeyHit(208) MoveLayerDown(currentlayer)
			
		If KeyHit(57)
			Select currentimage
				Case image1
					currentimage = image2
				Case image2
					currentimage = image3
				Case image3
					currentimage = image1
			End Select
		End If
		
		Color 0,0,0
		Rect 0,0,160,480,1
		Color 255,255,255
		Text 2,2,"left click to place"
		Text 2,16,"a tile."
		Text 2,40,"up to move layer up"
		Text 2,64,"down to move layer"
		Text 2,78,"down"
		Text 2,102,"1,2,3 to change"
		Text 2,116,"the current layer"
		Text 2,140,"space to change"
		Text 2,154,"tile image"
		
		Text 10,178,"current tile"
		DrawImage currentimage,40,192
		
		y = 244
		For layer.layers = Each layers
			If layer = layer1
				If layer = currentlayer
					Color 0,128,0
					Rect 2,y,156,20,1
					Color 0,55,0
					Rect 2,y,156,20,0
					Color 255,255,255
					Text 4,y,"layer 1"
				Else
					Color 128,128,128
					Rect 2,y,156,20,1
					Color 55,55,55
					Rect 2,y,156,20,0
					Color 0,0,0
					Text 4,y,"layer 1"
				End If
			ElseIf layer = layer2
				If layer = currentlayer
					Color 0,128,0
					Rect 2,y,156,20,1
					Color 0,55,0
					Rect 2,y,156,20,0
					Color 255,255,255
					Text 4,y,"layer 2"
				Else
					Color 128,128,128
					Rect 2,y,156,20,1
					Color 55,55,55
					Rect 2,y,156,20,0
					Color 0,0,0
					Text 4,y,"layer 2"
				End If
			ElseIf layer = layer3
				If layer = currentlayer
					Color 0,128,0
					Rect 2,y,156,20,1
					Color 0,55,0
					Rect 2,y,156,20,0
					Color 255,255,255
					Text 4,y,"layer 3"
				Else
					Color 128,128,128
					Rect 2,y,156,20,1
					Color 55,55,55
					Rect 2,y,156,20,0
					Color 0,0,0
					Text 4,y,"layer 3"
				End If
			End If
			y = y + 20
		Next
	Flip
	WaitTimer(timer)
Until KeyDown(1)
</pre> <br><br></td></tr></table><br>
<a name="397373"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MSW</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Erm...how about a double linked list?<br><br>type tile<br><br> field image<br> field x<br> field y<br><br> field next.tile<br> field previous.tile<br><br>end type<br><br>Then your layers only need to function as "pointers" into individual groupings of tiles...and each tile "points" to both the next and previous tile with in the group.<br><br>sorta like this (trying to sorta diagram it out):<br><br><br>first these are the layers:<br><br>Layer.tile(1) -&gt; tile 707<br>Layer.tile(2) -&gt; tile 53<br><br><br>now for the tiles:<br><br>tile 707.previous -&gt; Null<br>tile 707.next -&gt; tile 811<br><br><br><br>tile 811.previous -&gt; tile 707<br>tile 811.next -&gt; tile 12<br><br><br><br>tile 12.previous -&gt; tile 811<br>tile 12.next -&gt; Null<br><br><br>tile 53.previous -&gt; Null<br>tile 53.Next -&gt; Null<br><br><br><br>(the numbers 707,811,12 are just there it identify specific tiles in the diagram, showing that the tile order is specificly controlled by the next/previous links)...notice that the first tile...the one that the layer points to...the 'previous' link value is null...and the last tile in the grouping, the 'next' link value is null...in this way you can easily check if you have reached the top or bottom of a layer grouping (and if both 'previous' and 'next' links are Null...then there it only one tile in grouping)<br><br>So useing the above...say you wanted to move tile 811 from the first to the second layer grouping...a function like this could work (assumeing that layer() is a global array )<br><br>Function movetile( me.tile, laynumber%)<br><br>   ;temp variables used to store the previous/next pointers<br>  tempprevious.tile = me.previous<br>  tempnext.tile = me.next<br>  <br>    ;swap out the previous/next pointers with the linked tiles<br>    ;effectively this removes the "me" tile from the layer group<br>   <br>     if tempprevious then tempprevious.next = tempnext<br><br>     if tempnext then tempnext.previous = tempprevious<br>  <br><br>     ;now set "me" previous to null (indicateing it is the first tile in a layer grouping)<br><br>        me.previous = null<br><br>      ;then set "me" next to the first tile in the layer grouping<br><br>         me.next = layer(laynumber)<br><br>       ;then set that tile (the first tile in the laynumber grouping)<br>        ;'previous' value to "me"<br><br>         if layer(layernumber) then layer(laynumber).previous = me<br><br>         ;and finaly set "me" at the top of the leyer grouping<br><br>          layer(laynumber) = me<br><br>end function<br><br><br>It's kinda hard to explain linked lists (and I likely screwed up the blitz programing syntax for types up there..which won't help)...but I hope that helps <br><br></td></tr></table><br>
<a name="404161"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Adam Bossy</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for the replies!<br><br>So, when drawing the tiles, should I use <br><br>For t.tile = each tile<br>  DrawImage t\image<br>Next<br><br>Or would something like this be necessary for it to draw correctly?<br><br>me.tile = First tile<br>While me &lt;&gt; Null<br>  DrawImage me\image<br>  me = me.next<br>Wend <br><br></td></tr></table><br>
<a name="404237"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MSW</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> The second option would be the way to go for the linked list method I outlined above.<br><br>The problem with conditional level checking as shown by Skn[ac] in the code above is that you end up checking all the tiles multiple times...this isn't bad when you have a small over all number of them...but say you have 10,000 (a 100 by 100 array)...and 17 of them are on the first layer, 45 are on the second, 9,000 on the third, and 54 on the fourth...you end up checking all 10,000 tiles 4 times per update...and thus in the first layer alone with only 17 tiles to draw, means you are wasteing time checking the remaining 9983 tiles...sure this is pretty fast, but you are wasteing time on checking instead of processing other things (like AI, etc..)<br><br>However there are simple ways to speed even that up...bounds checking...for each layer you would record the minimum and maximum array locations that include the specific layer in question...<br><br>for example if the map is 100 tiles wide by 100 tiles tall you could have something like this for each layer at map load:<br><br>Layer1_min_X = 100<br>Layer1_min_Y = 100<br>Layer1_max_X = 0<br>Layer1_max_y = 0<br><br>then run through the tiles in the array...if the tile is on the particular layer then check if the array X/Y location is within those bounds:<br><br>For X = 0 to 100<br>For Y = 0 to 100<br><br>;obviously do this for each layer<br>  If map.tile(x,y)\layer = 1 then<br>    If X &lt; level1_min_X then level1_min_x = x<br>    If y &lt; level1_min_y then level1_min_y = y<br>    If X &gt; level1_max_X then level1_max_x = x<br>    If y &gt; level1_max_y then level1_max_y = y<br>  end if<br><br>next <br>next<br><br><br>then when you draw, you can do it as Skn[ac] showed except for each layer you set up the for/next loops to start at minX/Y to maxX/Y...something like:<br><br>For X = Level1_min_X to Level1_max_X<br>For Y = Level1)min_Y to Level1_max_Y<br><br>  if map.tile(x,y)\layer = 1 then draw tile<br><br>next<br>next<br><br>this will only have you checking for the correct layer in an area of the map that is certain to have tiles in it...meaning you arn't wasteing time checking for the correct layer in potentialy larger areas of the map that won't have tiles to draw right now...<br><br>However the linked list method is more efficent in that it directly points to the tiles that need to be drawn. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
