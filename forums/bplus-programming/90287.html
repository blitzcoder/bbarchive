<!DOCTYPE html><html lang="en" ><head ><title >Platformer Collisions broken, cant fix *headbash*</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Platformer Collisions broken, cant fix *headbash*</h1><a href="forums.php" >BlitzPlus Forums</a>/<a href="topics.php?forum=81" >BlitzPlus Programming</a>/<a href="#bottom" >Platformer Collisions broken, cant fix *headbash*</a><br><br>
<a name="1026072"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SilentAssaSIN</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi guys, I made this platformer code, pretty basic ATM just generates a floor and some random blocks. Collisions seems to work perfectly except under one condition, moving to the left when there is a block underneath.<br><br>I've tried and tried but cannot work out what to change, if any of you kind fellows could mull this over and give advise that would be awesome.<br><br>One of the things I looked at was to possibly modify the collisions so that X and Y checks are further seperated, maybe that will fix it. I'm going to try that now I guess!<br><br>Any help will be ecstaticly recieved as I just can't work it out. I hope you find the code readable enough. It needs to following images to work:<br><br>player.png image which can be any image 32x32 (or less)<br><br>tiles.png a 32x32 tilestrip, the tiles can be blank/anything as long as the first tile is blank, colour 255,0,255! It's set to load 5 frames as standard so a pure white strip is fine.<br><br>collide.png 32x32 mostly 255,0,255 masked to show the areas the code will check for collisions in.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics 800,600,0,2
SetBuffer(BackBuffer())

SeedRnd(Rnd(0.9))

Color 255,0,0

Type playerData
	Field x#,y#,oldx#,oldy#
	Field acc#,maxspeed#,dec#,speed#
	Field gravity#
	Field maxspeedy#,speedy#
End Type

	PD.playerData=New playerData
	PD\x#=100
	PD\y#=100
	PD\acc#=0.6
	PD\maxspeed#=2
	PD\dec#=0.1
	PD\gravity#=0.6

	player=LoadImage("player.png")
	MidHandle player
	tiles=LoadAnimImage("tiles.png",32,32,0,5)
	MaskImage tiles,255,0,255
	collide=LoadImage("collide.png")
	MaskImage collide,255,0,255

Dim MapH(100,25,7)
;0 background 1
;1 background 2
;3 level layer
;4 foreground 1
;5 foreground 2
;6 mobs
;7 objects
For x=0 To 100
	MapH(x,18,3)=1
	MapH(x,19,3)=1
	MapH(Rand(0,25),Rand(5,20),3)=Rand(0,3)
Next

While Not KeyDown(1)
Cls

PD\oldx#=PD\x#
PD\oldy#=PD\y#

If KeyDown(205) ;Right
	PD\speed#=PD\speed#+PD\acc#
EndIf

If KeyDown(203) ;Left
	PD\speed#=PD\speed#-PD\acc#
EndIf

If PD\speed#&gt;0.1
	PD\speed#=PD\speed#-PD\dec#
ElseIf PD\speed#&lt;0.1
	PD\speed#=PD\speed#+PD\dec#
EndIf

If PD\speed#&gt;PD\maxspeed# Then PD\speed#=PD\maxspeed#
If PD\speed#&lt;PD\maxspeed#*-1 Then PD\speed#=PD\maxspeed#*-1
If PD\speed#&gt;-0.2 And PD\speed#&lt;0.2 Then PD\speed#=0



PD\x#=PD\x#+PD\speed#
PD\y#=PD\y#+PD\gravity#

For x=Floor(PD\x#/32)-1 To Floor(PD\x#/32)+1
	For y=Floor(PD\y#/32)-2 To Floor(PD\y#/32)+1
		DrawImage collide,x*32,y*32
		Text x*32,y*32,x+" "+y
		If ImagesCollide(player,PD\x#,PD\y#,0,tiles,x*32,y*32,mapH(x,y,3))
			If Not ImagesCollide(player,PD\oldx#,PD\y#,0,tiles,x*32,y*32,mapH(x,y,3))
				Text 10,80,"X Collide  "+x
				resetx=True
			EndIf
			If Not ImagesCollide(player,PD\x#,PD\oldy#,0,tiles,x*32,y*32,mapH(x,y,3))
				Text 10,70,"Y Collide  "+y
				resety=True
			EndIf
			If resetx=True
				PD\x#=PD\oldx#
				resetx=False
			EndIf
			If resety=True
				PD\y#=PD\oldy#
				resety=False
			EndIf
		EndIf
	Next
Next

Text 10,10,PD\x#
Text 10,20,PD\speed#

Text 10,40,(PD\x#/32)
Text 10,50,(PD\y#/32)

For x=0 To 24
	For y=0 To 18
		DrawImage tiles,x*32,(y*32),MapH(x,y,3)
	Next
Next
DrawImage player, PD\x#, PD\y#

Flip
Wend
</textarea> <br><br></td></tr></table><br>
<a name="1026175"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sauer</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I looked at your code and couldn't pinpoint the problem.  If I had to guess, it would have something to do with your collision checking does not vary depending on the direction the player is moving.<br><br>What I do when making platformers is check collisions at only the points necessary (usually the corners) depending on what direction the player is moving.  Here's a small example where the white dots around the circle show where the collisions are being checked:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Sidescroller tutorial by Sauer

AppTitle "Side Scroller"
Graphics 640,480,32,2
SeedRnd MilliSecs()

;-----------------------
;  Globals
;-----------------------
Const GRAVITY#=.1

Global px=64
Global py=64
Global vy#=0
Global jumpable=False

Global camx#=0
Global camy#=0
Dim map(60,15)

;---------------------
; IMAGES
;---------------------
Global playerimg=CreateImage(32,32,1)
SetBuffer ImageBuffer(playerimg)
Color 200,100,255
Oval 0,0,32,32,1

Global floorimg=CreateImage(32,32,1)
SetBuffer ImageBuffer(floorimg)
For y=0 To 32 Step 2
	For x=0 To 32 Step 2
		z=Rand(100,200)
		Color  0,z,0
		Rect x,y,2,2,1
	Next
Next

Global skyimg=CreateImage(32,32,1)
SetBuffer ImageBuffer(skyimg)
For y=0 To 32 Step 2
	For x=0 To 32 Step 2
		z=Rand(100,200)
		Color  0,0,z
		Rect x,y,2,2,1
	Next
Next

Global lavaimg=CreateImage(32,32,1)
SetBuffer ImageBuffer(lavaimg)
For y=0 To 32 Step 2
	For x=0 To 32 Step 2
		z=Rand(100,200)
		Color  z,0,0
		Rect x,y,2,2,1
	Next
Next

Dim tilearray(3)
tilearray(0)=skyimg
tilearray(1)=floorimg
tilearray(2)=lavaimg


;-----------------------
;  MAIN LOOP
;-----------------------
SetBuffer BackBuffer()
load_map()
While Not KeyHit(1)
	Cls
	display_map()
	update_player()
	draw_player()
	Flip
Wend

;-----------------------
; FUNCTIONS
;-----------------------


Function load_map()
	Restore leveldata
	For y=0 To 14
		For x=0 To 59
			Read num
			map(x,y)=num
		Next
	Next
End Function 

Function display_map()
	For y=camy To camy+14
		For x=camx To camx+19
			DrawImage tilearray(map(x+camx,y+camy)),(x-camx) Shl 5,(y-camy*32) Shl 5
		Next
	Next
End Function 

Function draw_player()
	DrawImage playerimg,px,py
End Function 

Function update_player()
	
	opy=(py/32)*32+31
	
	If KeyDown(203)
		Color 255,255,0
		Plot px-2,py
		Plot px-2,py+31
		If get_map(px-2,py+1)&lt;&gt;1 And get_map(px-2,py+31)&lt;&gt;1 ;assumption made that gravity is pulling down (py+1) 
			px=px-2
		EndIf
	EndIf
		
	If KeyDown(205)
		Color 255,255,0
		Plot px+34,py
		Plot px+34,py+31
		If get_map(px+34,py+1)&lt;&gt;1 And get_map(px+34,py+31)&lt;&gt;1
			px=px+2
		EndIf 
	EndIf 

	
	If KeyHit(200) And jumpable=True
		vy=-5
		jumpable=False
	EndIf
	
	Plot px,py-1
	Plot px+31,py-1
	If get_map(px,py-1)=1 Or get_map(px+31,py-1)=1
		vy=1
		jumpable=False
	EndIf
	
	vy=vy+GRAVITY
	py=py+vy
	
	
	If map(px Shr 5,(py Shr 5) + 1)=1 Or map((px+32) Shr 5,(py Shr 5) + 1)=1
		py=opy
		vy=0
		jumpable=True
	EndIf
	

	
	
	Text 0,0,vy
	Text 0,12,jumpable

End Function

Function get_map(x,y)
	x=(x+camx) Shr 5
	y=(y+camy) Shr 5
	Return map(x,y)
End Function  
	

Function cam_scroll(v)
	camx=camx+v
	If camx&gt; 20 Then camx=20
	If camy&lt;0 Then camx=0 
End Function 

;---------------------
;  DATA
;---------------------

;20x 15 per screen
;60 x 15 total
.leveldata
Data 1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1, 1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1, 1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1 
Data 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
Data 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
Data 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
Data 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
Data 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
Data 1,0,0,0,0,0,0,0,0,0,0,1,1,1,1,0,0,0,0,1, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
Data 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
Data 1,0,0,0,0,0,1,1,0,0,0,0,0,0,0,0,0,0,0,1, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
Data 1,0,0,0,0,0,0,1,0,0,0,1,0,0,0,0,0,0,0,1, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
Data 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
Data 1,0,0,0,0,0,0,0,0,1,0,0,0,0,0,0,0,0,0,1, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
Data 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1, 0,0,0,0,1,0,0,0,0,1,0,0,0,0,1,0,0,0,0,1, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
Data 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1, 0,0,0,0,1,0,0,0,0,1,0,0,0,0,1,0,0,0,0,1, 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
Data 1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1, 0,0,0,0,1,0,0,0,0,1,0,0,0,0,1,0,0,0,0,1, 1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1</textarea><br><br>Hope that helps.  I'll look at your code again tomorrow to see if I can find a specific solution. <br><br></td></tr></table><br>
<a name="1026179"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SilentAssaSIN</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks, will check it out once I get into work :) very much appreciated.<br><br>At current (sorry I should have explained a bit better) it check the 9 squares around the player. I 'fixed' it in a way by moving the player up very slightly if a y collision is detected but its ugly and occasionally still locks for a millisecond :s<br><br>Hehe must find an elegant solution hopefully your techniques will be better, I shall try and adopt them in once I've understood it!<br><br>Either way I'm quite chuffed so far hehe.<br><br><br>If you watned to see what i did, after the line<br><br>If resety=true<br> PD\y#=PD\oldy#<br><br>change it to also take 0.05 (or so) away from PD\y# ontop but if the number gets too large it bounces, too low and it doesn't work! Interesting times. Good fun either way. <br><br></td></tr></table><br>
<a name="1026556"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SilentAssaSIN</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hehe thanks for the help, made a few changes and lo and behold it is slightly cleaner. I also added a jump/doublejump feature (spacebar) which is fun. Haha this is great I'm having buckets of fun doing this!<br><br>Got myself the basics of a map editor together, going to be great adding features to the 'game' :D<br><br><pre class=code>
Graphics 800,600,0,2
SetBuffer(BackBuffer())

SeedRnd(Rnd(0.9))

Color 255,0,0

Type playerData
	Field x#,y#,oldx#,oldy#
	Field acc#,maxspeed#,dec#,speed#
	Field gravity#
	Field maxspeedy#,speedy#
	Field IsInAir,DoubleJump
End Type

	PD.playerData=New playerData
	PD\x#=100
	PD\y#=100
	PD\acc#=0.6
	PD\maxspeed#=2
	PD\dec#=0.1
	PD\gravity#=1
	PD\maxspeedy#=24
	PD\IsInAir=True

	player=LoadImage("p.png")
	MidHandle player
	MaskImage player,255,0,255
	tiles=LoadAnimImage("tiles32.png",32,32,0,5)
	MaskImage tiles,255,0,255
	collide=LoadImage("collide.png")
	MaskImage collide,255,0,255

Dim MapH(100,25,7)
;0 background 1
;1 background 2
;3 level layer
;4 foreground 1
;5 foreground 2
;6 mobs
;7 objects
For x=0 To 100
	MapH(x,18,3)=1
	MapH(x,19,3)=1
	MapH(Rand(0,25),Rand(5,20),3)=Rand(0,3)
Next

While Not KeyDown(1)
Cls

PD\oldx#=PD\x#
PD\oldy#=PD\y#

If KeyDown(205) ;Right
	PD\speed#=PD\speed#+PD\acc#
EndIf

If KeyDown(203) ;Left
	PD\speed#=PD\speed#-PD\acc#
EndIf

If KeyHit(57)
	If PD\IsInAir=False
		PD\speedy#=PD\speedy#-16
		PD\IsInAir=True
	Else If PD\DoubleJump=False
		PD\speedy#=PD\speedy#-8
		PD\DoubleJump=True
	EndIf
EndIf

If PD\speed#&gt;0.1
	PD\speed#=PD\speed#-PD\dec#
ElseIf PD\speed#&lt;0.1
	PD\speed#=PD\speed#+PD\dec#
EndIf

If PD\speed#&gt;PD\maxspeed# Then PD\speed#=PD\maxspeed#
If PD\speed#&lt;PD\maxspeed#*-1 Then PD\speed#=PD\maxspeed#*-1
If PD\speed#&gt;-0.2 And PD\speed#&lt;0.2 Then PD\speed#=0



PD\x#=PD\x#+PD\speed#
PD\speedy#=PD\speedy#+PD\gravity
If PD\speedy#&gt;PD\maxspeedy# Then PD\speedy#=PD\maxspeedy#
PD\y#=Floor(PD\y#+PD\speedy#)

For x=Floor(PD\x#/32)-1 To Floor(PD\x#/32)+1
	For y=Floor(PD\y#/32)-1 To Floor(PD\y#/32)+1
		DrawImage collide,x*32,y*32
		Text x*32,y*32,x+" "+y
		If ImagesCollide(player,PD\x#,PD\y#,0,tiles,x*32,y*32,mapH(x,y,3))
			If Not ImagesCollide(player,PD\oldx#,PD\y#,0,tiles,x*32,y*32,mapH(x,y,3))
				Text 10,80,"X Collide  "+x
				resetx=True
			EndIf
			If Not ImagesCollide(player,PD\x#,PD\oldy#,0,tiles,x*32,y*32,mapH(x,y,3))
				Text 10,70,"Y Collide  "+y
				resety=True
				PD\speedy#=0
				PD\IsInAir=False
				PD\DoubleJump=False
			EndIf
			If resetx=True
				PD\x#=PD\oldx#
				resetx=False
			EndIf
			If resety=True
				PD\y#=PD\oldy#
				resety=False
				PD\y#=PD\y#-0.05
			EndIf
		EndIf
	Next
Next

Text 10,10,PD\x#
Text 10,20,PD\speed#

Text 10,40,(PD\x#/32)
Text 10,50,(PD\y#/32)

For x=0 To 24
	For y=0 To 18
		DrawImage tiles,x*32,(y*32),MapH(x,y,3)
	Next
Next
DrawImage player, PD\x#, PD\y#

Flip
Wend
</pre> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
