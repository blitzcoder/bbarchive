<!DOCTYPE html><html lang="en" ><head ><title >Pathfinding</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Pathfinding</h1><a href="forums.php" >BlitzPlus Forums</a>/<a href="topics.php?forum=81" >BlitzPlus Programming</a>/<a href="#bottom" >Pathfinding</a><br><br>
<a name="1184499"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have been working on a pseudo-A* kind of routine, but I have a problem.<br><br>The problem seems to occur when the best route seems to take the moving object back to where it last was, thereby creating an infinite pattern of repeated steps back and forth.<br><br>Not sure how best to fix it... Any help would be greatly recommended.<br><br><br>I appreciate the following code will not run as-is, but I hope that it will be clear enough - The main A-Star routine (and where the problem occurs) is "GetAStarBest(Location,Target)"<br><br><pre class=code>
;Declarations
Global LevelDataFilePath$
Global LevelImageFilePath$

Global MapImage
Global ASTarBank

Global X_Offset
Global Y_Offset

Type Blobs
	Field Location
	Field Colour
	Field Image
End Type

Global Move.Blobs
Global Destination.Blobs
Global LastValue#

;Example
RunTime

;Functions

Function RunTime()
	Initialise
	While (Not( (KeyDown(1) + KeyHit(1))))
		Loop
	Wend
	CloseDown
End Function 

Function CloseDown()
	FreeImage MapImage
	FreeImage Move\Image
	FreeImage Destination\Image
	Delete Each Blobs
	EndGraphics
	End
End Function
	
Function Initialise()
	Graphics GadgetWidth(Desktop()),GadgetHeight(Desktop()),32,2
	SetBuffer(BackBuffer())
	
	SeedRnd MilliSecs()
	
	LevelDataFilePath$=CurrentDir()+"OutPut.dat"
	LevelImageFilePath$=CurrentDir()+"Temp.png"
	
	MapImage=SetMapImage()
	ASTarBank=SetAStarData()
	
	SetMoveBlob
	SetDestinationBlob
	
	X_Offset=(GraphicsWidth()-512)*0.5
	Y_Offset=(GraphicsHeight()-512)*0.5
	
	LastValue#=256.0*(Sqr(512+512))
	
End Function

Function Loop()
	MoveBlob
	UpdateScreen
End Function 

Function UpdateScreen()
	Cls
	DrawImage MapImage,X_Offset,Y_Offset
	DrawBlobs
	Flip
End Function

Function MoveBlob()
	If (move\location=destination\location)
		Notify"Destination reached!"
		CloseDown
	End If	
	Move\Location=GetAStarBest(Move\Location,Destination\Location)
End Function		

Function DrawBlobs()
	Local Blob.Blobs
	Local X
	Local Y
	If ((MilliSecs() Mod 1000)&gt;125)
		X=GetCoordX(Move\Location)
		X=X+X_Offset
		Y=GetCoordY(Move\Location)
		Y=Y+Y_Offset
		DrawImage Move\Image,X,Y
	End If
	X=GetCoordX(Destination\Location)
	X=X+X_Offset
	Y=GetCoordY(Destination\Location)
	Y=Y+Y_Offset
	DrawImage Destination\Image,X,Y
End Function

Function SetMoveBlob()
	Move.Blobs=New Blobs
	Move\Location=GetSafeLocation()
	Move\Colour=GetRGB(0,255,0)
	SetBlobImage(Move)
End Function

Function SetDestinationBlob()
	Destination.Blobs=New Blobs
	Destination\Location=GetSafeLocation()
	If (Destination\Location=Move\Location)
		While (Destination\Location=Move\Location)
			Destination\Location=GetSafeLocation()
		Wend
	End If
	Destination\Colour=GetRGB(255,0,0)
	SetBlobImage(Destination)
End Function	

Function SetAStarData()
	If (FileType(LevelDataFilePath)&lt;&gt;1)
		RuntimeError "Please run "+Chr(34)+CurrentDir()+"Example.bb"+Chr(34)+" To obtain Map Data"
	End If
	Local Temp=ReadFile(LevelDataFilePath)
	If (Not(Temp))
		RuntimeError("No data read from "+Chr(34)+LevelDataFilePath+Chr(34))
	End If
	Local Bank=CreateBank(512*512)
	ReadBytes(Bank,Temp,0,512*512)
	CloseFile Temp
	Return Bank
End Function

Function SetMapImage()
	If (FileType(LevelImageFilePath)&lt;&gt;1)
		RuntimeError "Please run "+Chr(34)+CurrentDir()+"Example.bb"+Chr(34)+" To obtain Map Data"
	End If
	Local Temp=LoadImage(LevelImageFilePath)
	If (Not(Temp))
		RuntimeError("No data read from "+Chr(34)+LevelImageFilePath+Chr(34))
	End If	
	Local Image=CreateImage(512,512)
	CopyRect 0,0,512,512,0,0,ImageBuffer(Temp),ImageBuffer(Image)
	FreeImage Temp
	Return Image
End Function

Function GetSafeLocation()
	Local X=Rand(0,511)
	Local Y=Rand(0,511)
	If (GetAStarValue(X,Y)&gt;=100)
		While (GetAStarValue(X,Y)&gt;=100)
			X=Rand(0,511)
			Y=Rand(0,511)
		Wend
	End If
	Return GetLocation(X,Y)
End Function

Function GetAStarBest(Location,Target)
	Local TrialLocationX
	Local TrialLocationY
	Local Angle
	Local X
	Local Y
	
	Local LocationX=GetCoordX(Location)
	Local LocationY=GetCoordY(Location)
	
	Local TargetLocationX=GetCoordX(Target)
	Local TargetLocationY=GetCoordY(Target)
	
	Local AStarValue#
	Local InitialLowest#=256.0
	Local InitialDistance#=GetDistance(LocationX,LocationY,TargetLocationX,TargetLocationY)
	Local Lowest#=InitialLowest*InitialDistance
	Local ChooseLocation=Location
	
	For Angle=0 To 315 Step 45
	X=Sin(Angle)
	Y=Cos(Angle)
	TrialLocationX=X+LocationX
	TrialLocationY=Y+LocationY
		Local Distance#=GetDistance(TrialLocationX,TrialLocationY,TargetLocationX,TargetLocationY)
				
		If (TrialLocationX&lt;0 Or TrialLocationX&gt;511)
			TrialLocationX=(TrialLocationX+(Sgn(0-TrialLocationX)))
		End If
		
		If (TrialLocationY&lt;0 Or TrialLocationY&gt;511)
			TrialLocationY=(TrialLocationY+(Sgn(0-TrialLocationY)))
		End If
		
		AStarValue=GetAStarValue(TrialLocationX,TrialLocationY)		
		Local Value#=AStarValue*Distance#
		DebugLog (X+","+Y+" : "+GetAStarValue(TrialLocationX,TrialLocationY))
		If ((Value#&lt;Lowest#));Import to have = or will not move over equal terrain
			If ((Int(TrialLocationX)&lt;&gt;LocationX) And (Int(TrialLocationY)&lt;&gt;LocationY))
				If ((Value#&lt;=LastValue#))
					Lowest=Value
					ChooseLocation=GetLocation(Int(TrialLocationX),Int(TrialLocationY))
				End If	
			End If
		End If
	Next
	If (ChooseLocation=Location)
		RuntimeError"Nowhere to move to!"
	End If


	LastValue=Value+1
	
	DebugLog ("Chosen: "+Str(GetCOordX(ChooseLocation)-LocationX)+","+Str(GetCOordY(ChooseLocation)-LocationY))
	Return ChooseLocation
End Function

Function GetDistance#(x1#,y1#,x2#,y2#)
	Return (Sqr(((x1#-x2#)*(x1#-x2#))+((y1#-y2#)*(y1#-y2#))))
End Function

Function GetLocation(X,Y)
	Return (X And 65535) + ((Y And 65535) Shl 16)
End Function

Function GetCoordX(Location)
	Return Location And 65535
End Function

Function GetCoordY(Location)
	Return (Location Shr 16) And 65535
End Function

Function GetAStarValue(X,Y)
	Local Byte#=PeekByte(ASTarBank,X+(Y*512))
	Return Byte
End Function

Function GetRGB(R%,G%,B%)
	Return (((R% And 255)Shl 16) Or ((G% And 255) Shl 8) Or (B%And 255))
End Function

Function GetRed(RGB%)
	Return ((RGB% Shr 16) And 255)
End Function

Function GetGreen(RGB)
	Return ((RGB Shr 8) And 255)
End Function

Function GetBlue(RGB%)
	Return (RGB% And 255)
End Function

Function SetBlobImage(Blob.Blobs)
	Blob\Image=CreateImage(7,7)
	MidHandle Blob\Image
	SetBuffer(ImageBuffer(Blob\Image))
	ClsColor 0,0,0
	Color GetRed(Blob\Colour),GetGreen(Blob\Colour),GetBlue 
	Oval 0.5,0.5,7,7,True
	SetBuffer(BackBuffer())
End Function
</pre> <br><br></td></tr></table><br>
<a name="1184621"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matty</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm not going to read all of that code right now...but the way I get around this sort of thing is to disallow testing squares that have already been tested...use a bank or an array that holds all the postions currently tested and compare against that. There's no point checking the same square twice...ever. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
