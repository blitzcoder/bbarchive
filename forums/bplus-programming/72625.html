<!DOCTYPE html><html lang="en" ><head ><title >Background Animation System</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Background Animation System</h1><a href="forums.php" >BlitzPlus Forums</a>/<a href="topics.php?forum=81" >BlitzPlus Programming</a>/<a href="#bottom" >Background Animation System</a><br><br>
<a name="811645"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yeshu777</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is a quick system for having randomly occuring animations on a static background in your game using image strips.<br><br>Thought it may be of use to some and hopefully improved.<br><br>Here's how it works.<br><br>First create a text file (using wordpad or similar) with the following information:<br><br>Path to Anim Strip Image<br>Id No.<br>No. of Frames<br>Frame Width<br>Frame Height<br>x_position on background<br>y_position on background<br>Animation Speed<br>Occurance Chance<br><br>If you set "Occurance Chance" as 0 it will not animate randomly - instead you can use the function ForceAnimation(id) to start off the animation.<br><br>Useful if you want to trigger an animation as a player moves past a certain part of the background.<br><br>So your config file would look like this:<br><br><pre class=code>
gfx\back\tile1.bmp
1
28
124
76
59
376
60
5

gfx\back\tile2.bmp
2
22
115
117
96
252
50
20

</pre><br>And so on, for how many different entities are to be animated. Be sure to leave a blank line between each entity.<br><br>Save this as test_cfg.txt<br><br>Here is the code that processes the text file and contains the functions that will sit in your main game code.<br><br>Save this as bg_anim.bb<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;*************************************************************
;* Description... bg_anim.bb
;* Date.......... 24.07.07
;* Author........ Yeshu777
;*************************************************************
 
;*************************************************************
; Animation Object Type Declaration
;*************************************************************

Type	anim_object

	Field	id
	Field	image
	Field	nof_frames
	Field	width
	Field	height
	Field	x_pos
	Field	y_pos

	Field	status
	
	Field	index
	Field	speed
	Field	chance
	Field	timer

End Type

;*************************************************************
; Global variables
;*************************************************************

Global	anim.anim_object;

;*************************************************************
;* Description... Initialise the animation list from the config
;* Date.......... 24.07.07
;* Author........ Yeshu777
;* Parameters.... Path to the config file
;*************************************************************

Function	ProcessConfig(config$)
			
	file = ReadFile(config$);
				
	; Read the config into a list of types
	
	While(Not Eof(file))
		
		anim.anim_object = New anim_object;
			
		path$ = ReadLine(file);

		anim\id = Int(ReadLine(file));
		anim\nof_frames = Int(ReadLine(file)) - 1;
		anim\width = Int(ReadLine(file));
		anim\height = Int(ReadLine(file));
		anim\x_pos = Int(ReadLine(file));
		anim\y_pos = Int(ReadLine(file));
		anim\speed = Int(ReadLine(file));
		anim\chance = Int(ReadLine(file));

		; Load the animation strip
		
		anim\image = LoadAnimImage(path$, anim\width, anim\height, 0, anim\nof_frames);

		; Default Mask 
		MaskImage(anim\image, 255,0,255);
		
		; Set Object Defaults
		anim\status = False;
		anim\index = 0;
		anim\timer = 0;

		; Skip blank line
		ReadLine(file);
				
	Wend
	
	CloseFile(file);

	; Required to setup Random Decision

	SeedRnd(MilliSecs());
			
	Return	

	; For Debug only
	
	For anim.anim_object = Each anim_object

		Print "Id: "+anim\id
		Print "Frames: "+anim\nof_frames;
		Print "Width: "+anim\width;
		Print "Height: "+anim\height;
		Print "X: "+anim\x_pos
		Print "Y: "+anim\y_pos
		Print "Speed: "+anim\speed;
		Print "Chance: "+anim\chance
		Print "Time: "+anim\timer;
		Print "";
		Delay(200);

	Next

End Function

;*************************************************************
;* Description... Reiterate through the animation, activate 
;* .............. update frame &amp; limit check, deactivate
;* Date.......... 24.07.07
;* Author........ Yeshu777
;* Parameter1.... Animation	List of anim_objects
;*************************************************************

Function	ProcessAnimation(list.anim_object)

	For list.anim_object = Each anim_object

		If(list\status = False) Then

			; Chance of animation Starting
			
			If(RandomDec(list\chance)) Then

				list\index = 0;				Restart Animation
				list\timer = MilliSecs();	Update Timer
				list\status = True;			Enable 

			End If

		Else		
			
			; Animation Timing
			
			If(MilliSecs() &gt; list\timer + list\speed) Then

				list\timer = MilliSecs();		Update Timer
				list\index = list\index + 1;	Increment Frame
				
			End If
								
			; Animtion Frame Limiting
			
			If(list\index &gt;= list\nof_frames) Then

				list\index = 0;
				list\status = False;
				
			End If
								
		End If
				
	Next

End Function

;*************************************************************
;* Description... Parse the list and draw any active animations
;* Date.......... 24.07.07
;* Author........ Yeshu777
;* Parameter1.... Animation	List of anim_object
;* Returns....... Nothing
;*************************************************************

Function	DrawAnimation(list.anim_object)

	For list.anim_object = Each anim_object

		If(list\status = True) Then

			DrawImage(list\image, list\x_pos, list\y_pos, list\index);
									
		End If
				
	Next

End Function

;*************************************************************
;* Description... Force a specific list item to animate
;* Date.......... 24.07.07
;* Author........ Yeshu777
;* Parameter1.... Item ID
;* Returns....... Nothing
;*************************************************************

Function	ForceAnimation(id)

	For list.anim_object = Each anim_object

		If(list\id = id) Then
                        list\index = 0;
                        list\timer = MilliSecs();
			list\status = True;
		End If
		
	Next

End Function

;*************************************************************
;* Description... Free the animation list
;* Date.......... 24.07.07
;* Author........ Yeshu777
;* Parameter1.... Animation	List of anim_object
;* Returns....... Nothing
;*************************************************************

Function	FreeAnimation(list.anim_object)

	For list.anim_object = Each anim_object
		Delete(list)				
	Next

End Function

;*************************************************************
;* Description... Random Decision
;* Date.......... 24.07.07
;* Author........ Yeshu777
;* Parameters.... Percenatge chance (1000 = 100%)
;* Returns....... True or False
;*************************************************************

Function	RandomDec(percentage)

	If(Rand(1,1000) &lt; percentage) Then
		Return(True);
	End If
	
	Return(False);

End Function
</textarea><br><br>Here is how it is implemented in the main game:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;*************************************************************
;* Description... Main.bb
;* Date.......... 24.07.07
;* Author........ Yeshu777
;*************************************************************

	Include "bg_anim.bb";			BackGround Animation System

	; Setup Graphic Mode

	Graphics(800,600,32,3);
	SetBuffer(BackBuffer());


;*************************************************************
; Global Vars
;*************************************************************

	Global 	background;

	
;*************************************************************
; Load Gfx
;*************************************************************

	background = LoadImage("backgnd.bmp")
	
	ProcessConfig("test_cfg.txt");


;*************************************************************
; Init Game
;*************************************************************



;*************************************************************
;* Description... Main Program Loop
;* Date.......... 24.07.07
;* Author........ Yeshu777
;* Parameters.... None	
;*************************************************************

	While(Not KeyHit(1))
				
		ProcessAnimation(anim);

		Cls
		
		DrawImage(background,0,0,0);

                DrawAnimation(anim);	
				
		Flip
		
	Wend

;-------------------------------------------------------------

	FreeAnimation(anim);
	
End
</textarea><br><br>If you want to add more animated entities then you can just add to the config file leaving the game code untouched. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
