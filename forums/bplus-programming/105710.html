<!DOCTYPE html><html lang="en" ><head ><title >Can't remember how to do Types within types</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Can't remember how to do Types within types</h1><a href="forums.php" >BlitzPlus Forums</a>/<a href="topics.php?forum=81" >BlitzPlus Programming</a>/<a href="#bottom" >Can't remember how to do Types within types</a><br><br>
<a name="1292011"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >okee</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm using Blitzplus to knock up a quick editor<br>I used to know how to do types within types but<br>doesn't seem to work the way I thought it did years ago<br><br>so basically if I have a type that has another type within it<br>Can i add multiple types to one instance of the main type<br>and have it associated with it<br><br>If I have a ship and i want to add a different weapon for each one<br>I thought i could add each weapon and then they would be associated with that ship<br>but it seems to output all 4 weapons for each ship<br>Not sure if the setup is incorrect or output<br><br><br><br><br><br><br><br><br><pre class=code>

; type test
Graphics 1024,768

SetBuffer BackBuffer()

fntArial=LoadFont("Arial",20,False,False,False) 

SetFont fntArial 

; ship type
Type TShip
	Field Model$
	Field Weapons.TWeapon
End Type

; weapon each ship has
Type TWeapon
	Field WType$
	Field Name$
	Field Power
End Type

SetupShips()

While Not KeyHit(1) 
	Cls
	OutputData()
	Flip
Wend

; print to screen to test
Function OutputData()
	
	Local y = 10
	; print list of ships and weapons
	For Ship.TShip = Each TShip
		Text 10,y,"Ship type: " + Ship\Model$	  	
		y = y +15
		For Ship\Weapons = Each TWeapon
			Text 30,y,"Weapon Type: " + Ship\Weapons\wtype$ + " -- " +	"Weapon Name: " + Ship\Weapons\Name$ + " -- Weapon Power: " + Ship\Weapons\Power   	
			y = y +15
		Next	
	Next
	
	; output just the last ship
	s.Tship = Last TShip
		Text 10,y,"Ship type: " + s\Model$
		y = y +15
		For s\Weapons = Each TWeapon
			Text 30,y,"Weapon Type: " + s\Weapons\wtype$ + " -- " +	"Weapon Name: " + s\Weapons\Name$ + " -- Weapon Power: " + s\Weapons\Power   	
			y = y +15
		Next

	
End Function

; create the ships and add weapons
Function SetupShips()
	
	Local x
	Restore shipData
	
	; create 4 ships
	For x = 1 To 4
		Ship.TShip = New TShip
		Read Ship\Model$
	Next
	
	Restore weaponData
	
; testing by adding 4 weapons to the last ship
;	s.TShip = Last TShip
;	For x = 1 To 4
;		s\weapons.Tweapon = New TWeapon
;		Read s\weapons\WType
;		Read s\Weapons\Name
;		Read s\Weapons\Power
;	Next	
	
	
	; add 1 weapon to each ship
	; thought it would then just show 1 weapon per ship on output
	; but it's showing 4 for each
	For Ship.Tship = Each TShip
	
		Ship\Weapons.Tweapon = New TWeapon
		Read Ship\Weapons\WType
		Read Ship\Weapons\Name
		Read Ship\Weapons\Power
	
	Next
		


End Function		

.shipData
Data "Transporter", "Fighter","Cruiser", "Supply"

.weaponData
Data "Cannon", "Big cannon", 400
Data "Blaster", "laser Blaster", 100
Data "zapper", "Ion Zapper", 200
Data "None", "none", 0


</pre> <br><br></td></tr></table><br>
<a name="1292017"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Zethrax</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Note that the code below was tested in Blitz3D. As far as I'm aware it should work the same in BlitzPlus.<br><br>If you want a single weapon or multiple weapons stored in separate fields then you'd do it like this:-<br><br><pre class=code>
; ship type
Type TShip
	Field Model$
	Field Weapons.TWeapon
End Type

; weapon each ship has
Type TWeapon
	Field WType$
	Field Name$
	Field Power
End Type

ship.TShip = New TShip

; Use either of the methods below to create a weapon object and store its pointer in the ship object
;weapon.TWeapon = New TWeapon : ship\Weapons = weapon
ship\Weapons = New TWeapon

ship\Weapons\Name$ = "Cannon"

Print ship\Weapons\Name$

WaitKey
End
</pre><br><br>If you want multiple weapons stored as an indexable array then you'd do it like this:-<br><br><pre class=code>
Const NUM_SHIP_WEAPONS = 2
Const MAX_SHIP_WEAPONS_INDEX = NUM_SHIP_WEAPONS - 1

; Ship weapon IDs.
Const SHIP_WEAPON_CANNON = 0
Const SHIP_WEAPON_HARPOON = 1


; ship type
Type TShip
	Field Model$
	Field Weapons.TWeapon[ MAX_SHIP_WEAPONS_INDEX ] ; Create a local array for the weapons.
End Type

; weapon each ship has
Type TWeapon
	Field WType$
	Field Name$
	Field Power
End Type

ship.TShip = New TShip

; Use either of the methods below to create a weapon object and store its pointer in the ship object
;weapon.TWeapon = New TWeapon : ship\Weapons[ SHIP_WEAPON_CANNON ] = weapon
;weapon.TWeapon = New TWeapon : ship\Weapons[ SHIP_WEAPON_HARPOON ] = weapon
ship\Weapons[ SHIP_WEAPON_CANNON ] = New TWeapon
ship\Weapons[ SHIP_WEAPON_HARPOON ] = New TWeapon

ship\Weapons[ SHIP_WEAPON_CANNON ]\Name$ = "Cannon"
ship\Weapons[ SHIP_WEAPON_HARPOON ]\Name$ = "Harpoon"

For i = 0 To MAX_SHIP_WEAPONS_INDEX
	Print ship\Weapons[ i ]\Name$
Next

WaitKey
End
</pre><br><br>If you want to be able to update the weapons polymorphically without having to care about the type of weapon then just use a Select Case setup:-<br><br><pre class=code>
Function UpdateAllShipWeapons( ship.TShip )
	For weapon_id = 0 To MAX_SHIP_WEAPONS_INDEX
		UpdateShipWeapon( ship.TShip, weapon_id )
	Next
End Function


Function UpdateShipWeapon( ship.TShip, weapon_id )
	Select weapon_id
		Case SHIP_WEAPON_CANNON
			; Update code for the cannon goes here.
		Case SHIP_WEAPON_HARPOON
			; Update code for the harpoon goes here.
	End Select
End Function
</pre> <br><br></td></tr></table><br>
<a name="1292037"></a>

<a name="1292038"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >okee</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> using the following code, simplified<br><br><pre class=code>
; type test
Graphics 1024,768

SetBuffer BackBuffer()

fntArial=LoadFont("Arial",20,False,False,False) 

SetFont fntArial 

; ship type
Type TShip
	Field Model$
	Field Weapons.TWeapon
End Type

; weapon each ship has
Type TWeapon

	Field Name$

End Type

; create first ship
ship.TShip = New Tship
ship\model = "Ship A"
    ;create a wepaon for the ship
ship\weapons = New TWeapon
ship\Weapons\Name$ = "Weapon A1"

; create second ship
ship.TShip = New Tship
ship\model = "Ship B"

   ;create 2 weapons for this ship
ship\weapons = New TWeapon
ship\Weapons\Name$ = "Weapon B1"

ship\weapons = New TWeapon
ship\Weapons\Name$ = "Weapon B2"


While Not KeyHit(1) 
	Cls
	OutputData()
	
	Flip
Wend

; print to screen to test
Function OutputData()
	
	Local y = 10
	; print list of ships and weapons
	For Ship.Tship = Each TShip
		; print the ship model (parent type)
		Text 10,y,"Ship type: " + Ship\Model$	  	
		y = y +15
		; print all the weapons for that particular model (should only be 1 for each
		For Ship\Weapons = Each TWeapon
			Text 30,y,"Weapon Name: " + Ship\Weapons\Name$   	
			y = y +15
		Next	
	Next
	
End Function

</pre><br><br>I get the following result<br><br><pre class=code>

Ship type: Ship A
Weapon Name: Weapon A1
Weapon Name: Weapon B1
Weapon Name: Weapon B2
Ship type: Ship B
Weapon Name: Weapon A1
Weapon Name: Weapon B1
Weapon Name: Weapon B2

</pre><br><br><br>whereas i thought it should be<br><br><pre class=code>

Ship type: Ship A
Weapon Name: Weapon A1

Ship type: Ship B
Weapon Name: Weapon B1
Weapon Name: Weapon B2

</pre> <br><br></td></tr></table><br>
<a name="1292088"></a>

<a name="1292089"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dan</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> As far as i know, you cant assign type in type directly, in blitzbasic.<br><br>To learn types, imagine you are editing a text file with notepad.<br><br>Type name is the filename.<br><br>Each line of text (in one filename) is a new type entry. <br>(each line contains the fields values, separated by comma) <br><br>So:<br>In your simplified example,you are displaying each TWeapon.txt which is NOT assigned to Tship.txt <br><br></td></tr></table><br>
<a name="1292199"></a>

<a name="1292200"></a>

<a name="1292203"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> in BlitzBasic you have no LIST of objects like in BlitzMax. There is only a common list for all members of a type. So if you use FOR/EACH you will always get all Weapons of all Ships.<br><br>If you can, change to BlitzMax. It is also free, but much more comfortable....<br><br>If you need BlitzPlus:<br>You have to invent a "LIST" type to have individual sub-list in each Ship. This could be done f.e. by an array, which contains the pointer to the corresponding weapons:<br><br><pre class=code>SetBuffer BackBuffer()

fntArial=LoadFont("Arial",20,False,False,False) 

SetFont fntArial 

; ship type
Type TShip
	Field Model$
	Field Weapon.TWeapon[9]
End Type

; weapon each ship has
Type TWeapon
	Field Name$
End Type

; create first ship
ship.TShip = New Tship
ship\model = "Ship A"

;create a wepaon for the ship
Weapon.TWeapon= New TWeapon
Weapon\Name$ = "Weapon A1"
Ship\Weapon[1]=Weapon


; create second ship
ship.TShip = New Tship
ship\model = "Ship B"

Weapon.TWeapon= New TWeapon
Weapon\Name$ = "Weapon B1"
Ship\Weapon[1]=Weapon

Weapon.TWeapon= New TWeapon
Weapon\Name$ = "Weapon B2"
Ship\Weapon[2]=Weapon


While Not KeyHit(1) 
	Cls
	OutputData()
	
	Flip
Wend

; print to screen to test
Function OutputData()
	
	Local y = 10
	; print list of ships and weapons
		For Weapon.TWeapon = Each TWeapon
			Text 30,y,"Weapon Name: " + Weapon\Name$   	
			y = y +15
		Next	

	For Ship.Tship = Each TShip
		; print the ship model (parent type)
		Text 10,y,"Ship type: " + Ship\Model$	  	
		y = y +15
		; print all the weapons for that particular model (should only be 1 for each
		For i%=0 To 8

				If Int(ship\Weapon[i])&lt;&gt;0
					Text 30,y,"Ship ARRAY Weapon Name: " + ship\Weapon[i]\Name
					y = y +15
				EndIf 
		Next
	Next
End Function</pre> <br><br></td></tr></table><br>
<a name="1292206"></a>

<a name="1292207"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bobysait</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> In it can help, here is a code using a small database for storing models to copy<br><br>Using a small linked-list stuff to manage multiple weapons per ship<br>It's both more complicated and easier at the same time.<br>It's based on MMO database, so it's how it "could have been done" for storing a big database and handling lots of objects without consuming too much RAM.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">


; - The Database - &gt; you'd better use a real database
;					sql with a library to access it, or a filestream ... etc ...

	.weaponTemplateData
	;		Guid	, Type$		, Name$				, Power%
	Data	1		, "Cannon"	, "Big cannon"		, 400
	Data	2		, "Blaster"	, "laser Blaster"	, 100
	Data	3		, "zapper"	, "Ion Zapper"		, 200
	Data	4		, "None"	, "none"			, 0
	Data	-1

	.shipTemplateData
	;		ID		, Model$		, Weapons Guids (ID Weapon 1, ID Weapon 2, ID Weapon N... until ID Weapon = -1)
	Data	1		, "Cruiser"		, 1,2,3, -1	; cruiser has blaster, zapper, cannon ... and so many stuff
	Data	2		, "Transporter"	, 2, -1		; transporter use blaster
	Data	3		, "Supply"		, 4, -1		; supply has nothing.
	Data	4		, "Fighter"		, 1, 3, -1	; fighter has a Cannon and a Ion Zapper
	Data	-1


.shipData
	;		Ship GUID	, pos (x,y)
	Data	1			, 550, 250 ; the big cruiser
	Data	2			, 500, 400 ; a Transporter
	Data	3			, 450, 260 ; supply
	Data	4			, 400, 340 ; the fighters escort
	Data	4			, 650, 340
	Data	4			, 670, 240
	Data	4			, 380, 240
	Data	4			, 500, 480
	Data	-1











; Imagine those types as a SQL Database with Tables
; @GUID is a unique integer Index that link an object in the global Arrays (the Index tables)
; TWeaponTemplate and TShipTemplate contains the prototypes for copy.
; Game Objects (below templates) are "world entities" that copies the properties of the templates.
; note that you can also stream a database to allow a bigger database without using the physical RAM
; It may be usefull for a big MMORPG which contains thousands (millions ?) of entries
; or it may be useless for a small game with only 10 different ship stypes and dozens of weapons ...


; - TEMPLATE DATABASE -
	
	; - Database Size - ; increase this to store more templates in the database.
	Const MAX_WEAPON_TEMPLATES% = 5
	Const MAX_SHIP_TEMPLATES% = 10
	
	; table for weapons
	Type TWeaponTemplate
		Field GUID%
		Field WType$
		Field Name$
		Field Power%
	End Type
	
	; table for Ship
	Type TShipTemplate
		Field GUID%
		Field model$
		Field weaponTemplates ; IDs in the list of weapon templates (bank)
	End Type
	
	; this can be replace by a Dim ... or a bank, or a stream ... or anything that can contains data
	; for bank or stream, it may be more difficult because of "strings" that can't be stored as "string-pointer"
	; but it's still possible while using a second stream that contains the string values with a unique index to them.
	; For now, I'm just using a Global Array. Easy to create and manipulate.
	Global TABLE_WEAPONS.TWeaponTemplate[MAX_WEAPON_TEMPLATES]
	Global TABLE_SHIPS.TShipTemplate[MAX_WEAPON_TEMPLATES]
	
	; &lt;weapon templates&gt;
	
	Function newWeaponTemplate(weaponGUID%, wtype$, name$, power)
		; make sure the GUID is valid !
		If weaponGUID&lt;0 Or weaponGUID&gt;=MAX_WEAPON_TEMPLATES Then Return
		; create the template
		Local weaponT.TWeaponTemplate = New TWeaponTemplate
		; set the index, and "index it"
		weaponT\GUID = weaponGUID
		TABLE_WEAPONS[weaponGUID] = weaponT
		; set the weapon parameters.
		weaponT\WType = wtype
		weaponT\Name = name
		weaponT\Power = power
	End Function
	
	
	; &lt;ship templates&gt;
	
	Function newShipTemplate(shipGUID%, model$)
		; assert valid GUID (we could add a check to assert the GUID is not already used)
		If shipGUID&lt;0 Or shipGUID&gt;=MAX_SHIP_TEMPLATES Then Return
		
		; create the ship template
		Local shipT.TShipTemplate = New TShipTemplate
		; index it
		shipT\GUID = shipGUID
		TABLE_SHIPS[shipGUID] = shipT
		; ship parameters
		shipT\model = model
		; initialize the weapon bank &gt; 0 weapon at the moment
		shipT\weaponTemplates = CreateBank(0)
	End Function
	
	; add a weapon Id to the weapon's IDs list of a ship template
	Function ShipTPL_AddWeaponID(shipGUID, weaponGUId%)
		If shipGUID&lt;0 Or shipGUID&gt;=MAX_SHIP_TEMPLATES Then Return
		If weaponGUId&lt;0 Or weaponGUId&gt;=MAX_WEAPON_TEMPLATES Then Return
		Local WSize% = BankSize(TABLE_SHIPS[shipGUID]\weaponTemplates)
		ResizeBank(TABLE_SHIPS[shipGUID]\weaponTemplates, WSize+4)
		PokeInt(TABLE_SHIPS[shipGUID]\weaponTemplates, WSize, weaponGUId)
	End Function











; - GAME OBJECTS -

; the real weapon objects
Type TWeapon
	Field GUID% ; GUID of the weapon to use as reference in the weapon database (index of the template to use)
	Field Name$ ; if customization of the weapon &gt; name the specific weapon with a different name
	Field Power% ; so as the power can be modified after creation (if enabled)
End Type

; the world ships
Type TShip
	Field GUID% ; GUID of the ship in the Ship database
	Field WeaponH.TWeapon
	Field weaponT.TWeapon
	Field countWeapons%
	Field x,y
End Type

; create a weapon.
; this function is called by newShip when generating weapons from template
; but it can be created on the fly to add a weapon to a ship that is not supposed to own it from scratch
Function newWeapon.TWeapon(GUID%)
	If GUID&lt;0 Or GUID&gt;=MAX_WEAPON_TEMPLATES Then Return Null
	Local weapon.TWeapon	=	New TWeapon
	weapon\GUID				=	GUID
	; copy template parameters by default
	weapon\Name				=	TABLE_WEAPONS[GUID]\Name
	weapon\Power			=	TABLE_WEAPONS[GUID]\Power
	Return weapon
End Function

; create a new ship
; @GUID : the ship GUID in the ship templates table
Function newShip.TShip(GUID%, x,y)
	If GUID&lt;0 Or GUID&gt;=MAX_SHIP_TEMPLATES Then Return Null
	Local ship.TShip		=	New TShip
	ship\GUID				=	GUID
	ship\x					=	x
	ship\y					=	y
	ship\countWeapons		=	0
	Local l_WeaponIds		=	TABLE_SHIPS[GUID]\weaponTemplates
	Local l_countWeaponIds	=	BankSize(l_WeaponIds)/4
	Local l_weaponId%
	For l_weaponId = 0 To l_countWeaponIds-1
		Local weapon.TWeapon = newWeapon(PeekInt(l_WeaponIds, l_weaponId*4))
		If ship\WeaponH = Null
			ship\WeaponH	=	weapon
			Insert weapon After Last TWeapon
		Else
			Insert weapon After ship\weaponT
		EndIf
		ship\weaponT		=	weapon
		ship\countWeapons	=	ship\countWeapons+1
	Next
	Return ship
End Function

; returns the count of weapons owned by the ship
Function ShipCountWeapons%(ship.TShip)
	Return ship\countWeapons
End Function

; returns the weapons of the ship by id (in Range [0, countWeapons-1])
Function ShipWeapon.TWeapon(ship.TShip, pWeaponId%)
	; out of range weapon id ?
	If pWeaponId&lt;0 Or pWeaponId&gt;=ship\countWeapons Then Return Null
	
	Local weapon.TWeapon = ship\WeaponH
	Local w%
	For w = 1 To pWeaponId
		weapon = After weapon
	Next
	Return weapon
End Function




; create the ships and add weapons
Function readDatabase()
	
	; Read weapon templates
	Restore weaponTemplateData
	Local weaponGUID%, weaponType$, weaponName$, WeaponPower%
	Repeat
		Read weaponGUID : If weaponGUID=-1 Then Exit ; end of data marked as "-1"
		Read weaponType
		Read weaponName
		Read WeaponPower
		; create the weapon template
		newWeaponTemplate(weaponGUID, weaponType, weaponName, WeaponPower)
	Forever
	
	; read ship templates
	Restore shipTemplateData
	Local ShipGUID%, ShipName$, ShipWeaponGUID%
	Repeat
		Read ShipGUID : If ShipGUID=-1 Then Exit;
		Read ShipName
		newShipTemplate(ShipGUID, ShipName)
		Repeat
			Read ShipWeaponGUID : If ShipWeaponGUID = -1 Then Exit;
			ShipTPL_AddWeaponID(ShipGUID, ShipWeaponGUID)
		Forever
		
	Forever
	
End Function


Function readGameObjects()
	
	Restore shipData
	Local ShipGUID%, ShipX%, ShipY%
	Repeat
		Read ShipGUID : If ShipGUID = -1 Then Exit;
		Read ShipX
		Read ShipY
		newShip(ShipGUID, ShipX,ShipY)
	Forever
	
End Function	



Function drawWeaponIcon(weaponType$, x,y,w=6,h=6)
	Select Lower(weaponType)
		Case "cannon"	: Color 255,000,000
		Case "blaster"	: Color 255,255,000
		Case "zapper"	: Color 000,255,000
		Case "none"		: Color 050,050,050
	End Select
	Rect x,y, w,h,1
End Function



Graphics 1024,768, 0,2
SetBuffer BackBuffer()

Local fntArial=LoadFont("Arial",20,False,False,False) 
SetFont fntArial

readDatabase()

readGameObjects()

While Not KeyHit(1) 
	Cls
	Local ship.TShip
	Local w, h
	For ship = Each TShip
		Local shipModel$ = TABLE_SHIPS[ship\GUID]\model
		Select Lower(shipModel)
			Case "transporter"
				Color 100,200,200
				w = 20 : h=40
			Case "fighter"
				Color 255,100,000
				w = 10 : h=15
			Case "cruiser"
				Color 100,255,100
				w = 50 : h=80
			Case "supply"
				Color 100,100,255
				w = 10 : h=20
		End Select
		
		Rect ship\x-w, ship\y-h, w*2,h*2,0
		Text ship\x,ship\y, shipModel, 1,1
		
		Local nWeapons% = ShipCountWeapons(ship)
		Local iw
		For iw = 0 To nWeapons-1
			Local weapon.TWeapon = ShipWeapon(ship, iw)
			drawWeaponIcon( TABLE_WEAPONS[weapon\GUID]\WType, ship\x-w+2+iw*8, ship\y-h+2, 6,6)
		Next
	Next
	
	Color 000,255,000
	Text 10,10,"Legend :"
	drawWeaponIcon("cannon"	, 10,35, 10,10) : Text 30,30, "cannon"
	drawWeaponIcon("blaster", 10,55, 10,10) : Text 30,50, "blaster"
	drawWeaponIcon("zapper"	, 10,75, 10,10) : Text 30,70, "zapper"
	drawWeaponIcon("none"	, 10,95, 10,10) : Text 30,90, "none"
	
	Flip
	
Wend

End

</textarea> <br><br></td></tr></table><br>
<a name="1292368"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >okee</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for the examples lads, will test out these methods<br><br><pre class=code> If you can, change to BlitzMax. It is also free, but much more comfortable....</pre><br><br>Believe me I'd love to but just end up banging my head against a wall with the docs <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
