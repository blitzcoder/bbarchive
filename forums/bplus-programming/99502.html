<!DOCTYPE html><html lang="en" ><head ><title >Update My Game.</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Update My Game.</h1><a href="forums.php" >BlitzPlus Forums</a>/<a href="topics.php?forum=81" >BlitzPlus Programming</a>/<a href="#bottom" >Update My Game.</a><br><br>
<a name="1168288"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >EliteKen</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>I am looking to making a way to update my game files...<br><br>Does anyone have a code that allows you to download an .EXE file from a webURL?<br><br><br>I have found one that works on other files such as .jpg, .txt, .bmp, etc... but not one for .EXE<br><br><br>or better yet, anyone got a good technique/code for updating game files.<br><br><br>Thanks. <br><br></td></tr></table><br>
<a name="1168289"></a>

<a name="1168290"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Zethrax</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> An exe file is just a file. If you can download .jpg, .txt, .bmp, etc files then you can download an exe file too.<br><br>(Note that all the code below is for Blitz3D. It will probably work in BlitzPlus, but I haven't tested it.)<br><br>The code below will download a file off the Internet and place it into a string global (I originally wrote it for downloading XML data). The comments contain usage information. It requires the 'SplitString()' function found at: <a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=2991" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=2991</a><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Function GetInternetFile( url$, dont_cache = True )
	; Downloads a file or webpage from the internet and places it into the string global 'G_internet_file$'.
	
	; = Parameters =
	; 'url$' - Should hold the full url of the file or webpage to be downloaded.
	; 'dont_cache' - A flag that controls whether browser caching is allowed.
	;	-	True = (default) The browser cache will be ignored and the file will always be sourced from the server.
	;	-	False = The file will be sourced from the browser cache if available, or from the server if not.

	; = Data Return Globals =
	; The HTTP file body (the actual file data) will be placed into 'G_internet_file$'.
	; The HTTP file header will be placed into 'G_internet_file_header$'.
	; The HTTP protocol version will be placed into 'G_internet_file_protocol_version$' (eg 'HTTP/1.0').
	; The HTTP status code will be placed into 'G_internet_file_status_code$' ('200' means no problems were encountered).
	; The HTTP status message will be placed into 'G_internet_file_status_message$' ('OK' means no problems were encountered).
	; The error codes returned by this function are also available in 'G_internet_file_error'.
	
	; = Function Return Values =
	; 0 = Success (no errors).
	; 1 = Unable to connect to server.
	; 2 = File request failed.
	; 3 = A stream error occurred.
	; 4 = The URL was badly formed and could not be parsed.
	; 5 = The file header was badly formed or corrupted.
	; 6 = Incorrectly formatted status line in header.
	; 7 = HTTP status code indicated an error.

	; = Requirements =
	; Requires that a string global named 'G_internet_file$' be declared.
	; Requires that a string global named 'G_internet_file_header$' be declared.	
	; Requires that a string global named 'G_internet_file_protocol_version$' be declared.
	; Requires that a string global named 'G_internet_file_status_code$' be declared.
	; Requires that a string global named 'G_internet_file_status_message$' be declared.
	; Requires that an integer global named 'G_internet_file_error' be declared.
	; Requires the 'SplitString()' function. (http://www.blitzbasic.com/codearcs/codearcs.php?code=2991)

	; = Reference Links =
	; <a href="http://en.wikipedia.org/wiki/List_of_HTTP_status_codes" target="_blank">http://en.wikipedia.org/wiki/List_of_HTTP_status_codes</a>
	; <a href="http://www.w3.org/Protocols/rfc2616/rfc2616-sec6.html" target="_blank">http://www.w3.org/Protocols/rfc2616/rfc2616-sec6.html</a>
	; <a href="http://www.jmarshall.com/easy/http/" target="_blank">http://www.jmarshall.com/easy/http/</a>

	Local tcp, startpos, endpos, host$, header_line$
	
	G_internet_file$ = ""
	G_internet_file_header$ = ""

	; Extract the hostname from the URL.
	startpos = Instr( url$, "://" )
	If startpos = 0 Then G_internet_file_error = 4 : Return 4
	startpos = startpos + 3
	endpos = Instr( url$, "/", startpos )
	If endpos = 0 Then endpos = Len( url$ ) + 1
	host$ = Mid( url$, startpos, endpos - startpos )
	If host$ = "" Then G_internet_file_error = 4 : Return 4
	
	tcp = OpenTCPStream( host$, 80 )
	
	If Not tcp Then G_internet_file_error = 1 : Return 1 ; Unable to connect to server.
	
	; Write the HTTP request header.
	; Note that 'WriteLine' automatically adds a linebreak (CRLF), so no need to add one.
	WriteLine tcp,"GET " + url$ + " HTTP/1.0"
	WriteLine tcp, "Host: " + host$
	If dont_cache
		WriteLine tcp, "Cache-Control: no-cache, must-revalidate"
		WriteLine tcp, "Expires: Sat, 26 Jul 1997 05:00:00 GMT" ; (Just an old date)
	EndIf
	WriteLine tcp, "User-Agent: downloader"
	WriteLine tcp, "Connection: close"
	WriteLine tcp, ""
	
	If Eof( tcp ) Then CloseTCPStream tcp : G_internet_file_error = 2 : Return 2 ; File request failed.

	header_line$ = ReadLine( tcp )
	If SplitString( header_line$, " " ) &lt;&gt; 2 Then CloseTCPStream tcp : G_internet_file_error = 6 : Return 6 ; Incorrectly formatted status line in header.
	G_internet_file_protocol_version$ = A_string$( 0 )
	G_internet_file_status_code$ = A_string$( 1 )
	G_internet_file_status_message$ = A_string$( 2 )
	If G_internet_file_status_code$ &lt;&gt; "200" Then CloseTCPStream tcp : G_internet_file_error = 7 : Return 7 ; HTTP status code indicated an error.
	G_internet_file_header$ = G_internet_file_header$ + header_line$ + G_eol$

	While Not Eof( tcp )
		header_line$ = ReadLine( tcp )
		If header_line$ = "" Then Exit ; The blank line terminating the header was found.
		G_internet_file_header$ = G_internet_file_header$ + header_line$ + G_eol$
	Wend 
	
	Select Eof( tcp )
		Case -1
			CloseTCPStream tcp
			G_internet_file_error = 3
			Return 3 ; A stream error occurred.
		Case 1
			CloseTCPStream tcp
			G_internet_file_error = 5
			Return 5 ; The file header was badly formed or corrupted.
	End Select		
	
	While Not Eof( tcp )
		G_internet_file$ = G_internet_file$ + Chr( ReadByte( tcp ) )
	Wend
	
	If Eof( tcp ) = -1 Then CloseTCPStream tcp : G_internet_file_error = 3 : Return 3 ; A stream error occurred.
	
	CloseTCPStream tcp
	
	G_internet_file_error = 0
	Return 0
End Function</textarea><br><br><br>The code below can be used to save the resulting string to a file. You may want to change it to save the data in the global string variable directly rather than passing all the data to the function via the function parameter.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Function SaveString( filepath$, stringval$ )
	; Saves the 'stringval$' to the specified 'filepath$' as a file.
	; Returns True on success or False if there was an error.
	
	Local file, i, l = Len( stringval$ )
	file = WriteFile( filepath$ )
	If file = 0 Then Return False
	For i = 1 To l
		WriteByte file, Asc( Mid( stringval$, i, 1 ) )
	Next
	CloseFile file
	Return True
End Function</textarea><br><br><br>The code below can be used to check if a new version of your program is available. The comments contain usage information.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Function CheckForNewVersion()
	; Checks for a newer version of the program.

	; Returns:-
	; 0 = There is no newer version.
	; 1 = A newer version of the program exists.
	; 2 = An error occurred when fetching the version file.
	; 3 = The version file was empty.
	; 4 = The version file was badly formatted or there was a file corruption issue.

	; = Requirements =
	; The string global 'G_version_check_url$' should hold the URL of the version file.
	; The version file should exist as a text file hosted on a web-server, and should contain the major version number, followed by a dot character, followed by the minor version number (eg. '1.2').
	; The integer constant 'MAJOR_VERSION_NUMBER' should hold the major version number of the program.
	; The integer constant 'MINOR_VERSION_NUMBER' should hold the minor version number of the program.
	; Requires the 'GetInternetFile()' function. ()
	; Requires the 'SplitString()' function. (http://www.blitzbasic.com/codearcs/codearcs.php?code=2991)

	If GetInternetFile( G_version_check_url$ ) &lt;&gt; 0 Then Return 2 ; An error occurred when fetching the version file.
	If G_internet_file$ = "" Then Return 3 ; The version file was empty.
	; Servers sometimes seem to like to add a zero to the end of text files, so let's remove it. 'Replace' seems to be bugged in this case so we need to do things the long way round.
	If Asc( Mid( G_internet_file$, Len( G_internet_file$ ), 1 ) ) = 0
		G_internet_file$ = Left( G_internet_file$, Len( G_internet_file$ ) - 1 )
	EndIf
	If SplitString( G_internet_file$, "." ) &lt;&gt; 1 Then Return 4 ; The version file was badly formatted or there was a file corruption issue.
	If Int( ( A_string$( 0 ) ) &lt;&gt; MAJOR_VERSION_NUMBER ) Or Int( ( A_string$( 1 ) ) &lt;&gt; MINOR_VERSION_NUMBER )
		Return 1 ; A newer version of the program exists.
	EndIf
	Return 0 ; There is no newer version.
End Function</textarea><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
