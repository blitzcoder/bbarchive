<!DOCTYPE html><html lang="en" ><head ><title >WriteLine fails after ReadLine</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >WriteLine fails after ReadLine</h1><a href="forums.php" >Archives Forums</a>/<a href="topics.php?forum=105" >BlitzMax Bug Reports</a>/<a href="#bottom" >WriteLine fails after ReadLine</a><br><br>
<a name="1160288"></a>

<a name="1160293"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Title says it all.<br><br>EDIT:- <a href="/posts.php?topic=99007" target="_blank">More Info is here</a><br><br>This code:-<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict
Function CreateTestFile()
	Local Stream:TStream
	
	Stream = WriteFile("Test.txt")
	
	WriteLine(Stream,"FirstLine")
	WriteLine(Stream,"SecondLine")
	WriteLine(Stream,"ThirdLine")
	
	CloseFile(Stream)
EndFunction

Function ReadThenWrite()
	Local Stream:TStream,Line$
	Local Success

	WriteStdout "Testing ReadThenWrite~n"
	
	'OpenFile
	Stream = OpenFile("Test.txt")
	WriteStdout "~tOpenFile: "
	If Stream
		WriteStdout "OK~n"
	Else
		WriteStdout "Fail~n"
		End
	EndIf

	'ReadLine
	Line = ReadLine(Stream)
	WriteStdout "~tReadLine: "
	If Line = ""
		WriteStdout "ERROR!~n"
		CloseFile(Stream)
	Else
		WriteStdout Line+"~n"
	EndIf

	'WriteLine
	Success = WriteLine(Stream,"Testing")
	WriteStdout "~tWriteLine: Writing ~qTesting~q as a line: "
	If Not Success
		WriteStdout "ERROR!~n"
	Else
		WriteStdout "OK~n"
	EndIf

	'CloseFile
	CloseFile(Stream)

	'Display the file
	Print "~n~tFileContents:"
	Stream = OpenFile("Test.txt")
	While Not Eof(Stream)
		Print "~t~t"+ReadLine(Stream)
	Wend
	
	Print
EndFunction

Function WriteOnly()
	Local Stream:TStream,Line$
	Local Success

	WriteStdout "Testing WriteOnly~n"
	
	'OpenFile
	Stream = OpenFile("Test.txt")
	WriteStdout "~tOpenFile: "
	If Stream
		WriteStdout "OK~n"
	Else
		WriteStdout "Fail~n"
		End
	EndIf

	'WriteLine
	Success = WriteLine(Stream,"Testing")
	WriteStdout "~tWriteLine: "
	If Not Success
		WriteStdout "ERROR!~n"
	Else
		WriteStdout "OK~n"
	EndIf

	'CloseFile
	CloseFile(Stream)

	'Display the file
	Print "~n~tFileContents:"
	Stream = OpenFile("Test.txt")
	While Not Eof(Stream)
		Print "~t~t"+ReadLine(Stream)
	Wend
	End
EndFunction

Print

CreateTestFile
ReadThenWrite
WriteOnly
</textarea><br><br>produces this result:-<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Testing ReadThenWrite
	OpenFile: OK
	ReadLine: FirstLine
	WriteLine: Writing "Testing" as a line: OK

	FileContents:
		FirstLine
		SecondLine
		ThirdLine

Testing WriteOnly
	OpenFile: OK
	WriteLine: Writing "Testing" as a line: OK

	FileContents:
		Testing
		
		SecondLine
		ThirdLine
</textarea><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1160328"></a>

<a name="1160330"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> To open a file for writing use WriteFile.<br><br>To open a file for reading use ReadFile.<br><br>If you want to open a file and partially overwrite the contents in a random access manner then use OpenFile.<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1160332"></a>

<a name="1160346"></a>

<a name="1160347"></a>

<a name="1160348"></a>

<a name="1160354"></a>

<a name="1160355"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi skidracer,<br><br>Just found this -<br><br>from <a href="http://pubs.opengroup.org/onlinepubs/009695399/functions/fopen.html" target="_blank">fopen</a><br><br><i>When a file is opened with update mode ( '+' as the second or third character in the mode argument), both input and output may be performed on the associated stream. However, the application shall ensure that output is not directly followed by input without an intervening call to fflush() or to a file positioning function ( fseek(), fsetpos(), or rewind()), and input is not directly followed by output without an intervening call to a file positioning function, unless the input operation encounters end-of-file.</i><br><br>BMax opens the file using the same update mode, so this explains it all.<br><br><div class="quote"> As BlitzMax has no SeekFile I don't think there is a situation where OpenFile should ever be used <br></div><br>However the OpenFile() method has separate flags for opening a file for reading and/or writing. If you're going to include such a command then it should work as expected. I can imagine an additional flag in the underlying TStream methods to know the previous operation wouldn't be too hard to incorporate to get this working.<br><br>Thanks.<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1160375"></a>

<a name="1160376"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Blitzmax does have a SeekFile().  It is called SeekStream().  Modified code, works as expected.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict
Function CreateTestFile()
	Local Stream:TStream
	
	Stream = WriteFile("Test.txt")
	
	WriteLine(Stream,"FirstLine")
	WriteLine(Stream,"SecondLine")
	WriteLine(Stream,"ThirdLine")
	
	CloseFile(Stream)
EndFunction

Function ReadThenWrite()
	Local Stream:TStream,Line$
	Local Success

	WriteStdout "Testing ReadThenWrite~n"
	
	'OpenFile
	Stream = OpenFile("Test.txt")
	WriteStdout "~tOpenFile: "
	If Stream
		WriteStdout "OK~n"
	Else
		WriteStdout "Fail~n"
		End
	EndIf

	'ReadLine
	Line = ReadLine(Stream)
	WriteStdout "~tReadLine: "
	If Line = ""
		WriteStdout "ERROR!~n"
		CloseFile(Stream)
	Else
		WriteStdout Line+"~n"
	EndIf

	'WriteLine
	SeekStream(Stream,StreamPos(stream)) '&lt;-----added this line
	Success = WriteLine(Stream,"Testing")
	WriteStdout "~tWriteLine: Writing ~qTesting~q as a line: "
	If Not Success
		WriteStdout "ERROR!~n"
	Else
		WriteStdout "OK~n"
	EndIf

	'CloseFile
	CloseFile(Stream)

	'Display the file
	Print "~n~tFileContents:"
	Stream = OpenFile("Test.txt")
	While Not Eof(Stream)
		Print "~t~t"+ReadLine(Stream)
	Wend
	
	Print
EndFunction

Function WriteOnly()
	Local Stream:TStream,Line$
	Local Success

	WriteStdout "Testing WriteOnly~n"
	
	'OpenFile
	Stream = OpenFile("Test.txt")
	WriteStdout "~tOpenFile: "
	If Stream
		WriteStdout "OK~n"
	Else
		WriteStdout "Fail~n"
		End
	EndIf

	'WriteLine
	Success = WriteLine(Stream,"Testing")
	WriteStdout "~tWriteLine: "
	If Not Success
		WriteStdout "ERROR!~n"
	Else
		WriteStdout "OK~n"
	EndIf

	'CloseFile
	CloseFile(Stream)

	'Display the file
	Print "~n~tFileContents:"
	Stream = OpenFile("Test.txt")
	While Not Eof(Stream)
		Print "~t~t"+ReadLine(Stream)
	Wend
	End
EndFunction

Print

CreateTestFile
ReadThenWrite
WriteOnly

</textarea><br>Output<br><pre class=code>Testing ReadThenWrite
	OpenFile: OK
	ReadLine: FirstLine
	WriteLine: Writing "Testing" as a line: OK

	FileContents:
		FirstLine
		Testing
		e
		ThirdLine

Testing WriteOnly
	OpenFile: OK
	WriteLine: OK

	FileContents:
		Testing
		
		Testing
		e
		ThirdLine
</pre><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
