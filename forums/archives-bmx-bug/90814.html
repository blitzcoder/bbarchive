<!DOCTYPE html><html lang="en" ><head ><title >Bug? in BRL.Stream / documentation</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Bug? in BRL.Stream / documentation</h1><a href="forums.php" >Archives Forums</a>/<a href="topics.php?forum=105" >BlitzMax Bug Reports</a>/<a href="#bottom" >Bug? in BRL.Stream / documentation</a><br><br>
<a name="1033223"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >n-Halbleiter</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey there!<br><br>I'm a bit concerned about a problem in the documentation or BRL.Stream: Under Tutorials -&gt; BlitzMax Overview there is a small text (with sample code) about TStream, TStreamWrapper and TStreamFactory. The problem is the following: <br><br>Run the code (available in the documentation (not at the location the link directs to) or at the bottom of this post) and you will see: The text will not be converted to uppercase. So this whole thing is useless.<br><br>After tracking down the problem I found out that the implementations of 'Read[thing]' (except 'Bytes' which is not implemented in TStreamWrapper at all) just redirect to the stream's one. With this you can't just derive your own wrapper and use it like this (sometimes this would be really cool). So, the solution is just to comment this section out. The modified source of the TStreamWrapper Type would then look like this:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Type TStreamWrapper Extends TStream
	Field _stream:TStream

	Rem
	bbdoc: Set underlying stream
	about:
	Sets the stream to be 'wrapped'. All calls to TIO methods of this stream will be
	redirected to @stream.
	end rem
	Method SetStream( stream:TStream )
		_stream=stream
	End Method

	Method Eof()
		Return _stream.Eof()
	End Method

	Method Pos()
		Return _stream.Pos()
	End Method

	Method Size()
		Return _stream.Size()
	End Method
	
	Method Seek( pos )
		Return _stream.Seek( pos )
	End Method

	Method Flush()
		_stream.Flush
	End Method

	Method Close()
		_stream.Close
	End Method

	Method Read( buf:Byte Ptr,count )
		Return _stream.Read( buf,count )
	End Method

	Method Write( buf:Byte Ptr,count )
		Return _stream.Write( buf,count )
	End Method

	Rem
	Method ReadByte()
		Return _stream.ReadByte()
	End Method
	
	Method WriteByte( n )
		_stream.WriteByte n
	End Method
	
	Method ReadShort()
		Return _stream.ReadShort()
	End Method
	
	Method WriteShort( n )
		_stream.WriteShort n
	End Method
	
	Method ReadInt()
		Return _stream.ReadInt()
	End Method
	
	Method WriteInt( n )
		_stream.WriteInt n
	End Method
	
	Method ReadFloat:Float()
		Return _stream.ReadFloat()
	End Method
	
	Method WriteFloat( n:Float )
		_stream.WriteFloat n
	End Method
	
	Method ReadDouble:Double()
		Return _stream.ReadDouble()
	End Method
	
	Method WriteDouble( n:Double )
		_stream.WriteDouble n
	End Method
	
	Method ReadLine$()
		Return _stream.ReadLine()
	End Method
	
	Method WriteLine( t$ )
		Return _stream.WriteLine( t )
	End Method
	
	Method ReadString$( n )
		Return _stream.ReadString( n )
	End Method
	
	Method WriteString( t$ )
		_stream.WriteString t
	End Method
	
	Method ReadObject:Object()
		Return _stream.ReadObject()
	End Method
	
	Method WriteObject( obj:Object )
		_stream.WriteObject obj
	End Method
	End Rem
	
End Type</textarea><br><br>(just copied from MaxIDE so that you can easily replace it for testing.)<br><br>The code for the example:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict

Type TUpperStream Extends TStreamWrapper

	Method Read:Int( buf:Byte Ptr,count:Int )
		'Read data from the underlying stream
		count=Super.Read( buf,count )
		'Convert the data to uppercase
		For Local i=0 Until count
			If buf[i]&gt;=Asc("a") And Buf[i]&lt;=Asc("z")
				buf[i]=buf[i]-Asc("a")+Asc("A")
			EndIf
		Next
		'Done!
		Return count
	End Method

	Method Write:Int( buf:Byte Ptr,count:Int )
		'Copy the data to a new buffer, converting to uppercase as we go
		Local tmp:Byte[count]
		For Local i=0 Until count
			If buf[i]&gt;=Asc("a") And buf[i]&lt;=Asc("z")
				tmp[i]=buf[i]-Asc("a")+Asc("A")
			Else
				tmp[i]=buf[i]
			EndIf
		Next
		'Write the data to the underlying stream
		Return Super.Write( tmp,count )
	End Method

	Function Create:TUpperStream( stream:TStream )
		Local t:TUpperStream=New TUpperStream
		'SetStream is a TStreamWrapper method that sets the underlying stream.
		t.SetStream stream
		Return t
	End Function

End Type

Type TUpperStreamFactory Extends TStreamFactory

	Method CreateStream:TUpperStream( url:Object,proto:String,path:String,readable:Int,writeable:Int )
		If proto$&lt;&gt;"uppercase" Return
		Local stream:TStream=OpenStream( path,readable,writeable )
		If stream Return TUpperStream.Create( stream )
	End Method
	
End Type

New TUpperStreamFactory

'Exmaple of manually creating a TUpperStream:

'Create a tmp file and write some text to it.
Local tmp:TStream=WriteStream( "tmp" )
tmp.WriteLine "A little example..."
tmp.WriteLine "of our cool TUpperStream!"
tmp.Close()

'Open tmp file again, and wrap it with a TUpperStream
tmp:TStream=ReadStream( "tmp" ) 
Local upperizer:TUpperStream=TUpperStream.Create( tmp )

'Dump file contents
While Not upperizer.Eof()
	Print upperizer.ReadLine()
Wend

upperizer.Close()
tmp.Close()

Print ""

'Example of automatically creating a TUpperStream:
Local tmp2:TStream=WriteStream( "uppercase::tmp" )
tmp2.WriteLine "Another little example..."
tmp2.WriteLine "of our even cooler TUpperStream!"
tmp2.Close()

tmp2:TStream=ReadStream( "tmp" )
While Not tmp2.Eof()
	Print tmp2.ReadLine()
Wend
tmp2.Close()</textarea><br><br>With this small modification everything works how it should.<br><br>Thanks and out. :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
