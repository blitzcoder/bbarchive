<!DOCTYPE html><html lang="en" ><head ><title >Random Access File Utilities</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Allows you to randomly access a file, language=bb, category=File Utilities'><meta name='author' content='TygerWulf'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=8>File Utilities</a>/Random Access File Utilities</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="994.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Random Access File Utilities by TygerWulf</td><td align="right">2004 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Allows you to randomly access a file by using identifiers to locate data in a file.  It needs an encryption code or some other such to be truly effective, though, but it operates well the way it is.  If you don't mind people being able to read the files, then go ahead and use it. </td></tr><tr ><td class="cell"><pre class="code">Type Datafile
	Field FName$
	Field Size
	Field ID
End Type

;FWriteS(File.DataFile, Key$, KeyVal$)
;File.DataFile--A file opened with FOpen
;Key$--The identifier that will be used to retrieve the data later
;KeyVal$--The Value to be stored for retrieval later
;Writes a string to a file or updates a string value in the file.

Function FWriteS(File.DataFile, Key$,KeyVal$)
	Key$ = "$" +Key$
	If File\Size = 0 Then
		X$ = Key$+"="+KeyVal
		WriteLine(File\ID,X$)
		QSFile(File)
		Return 2
	End If
	P = KeyExists(File, Key$)
	If P &lt;&gt; -1 Then

		SeekFile(File\ID,P)
		X$ = ReadLine(File\ID)
		SeekFile(File\ID,P)
		L = Len(X$)
		X$ = Key$+"="+KeyVal
		If Len(X$) &lt; L Then X$ = LSet$(X$,L)
		WriteLine(File\ID, X$)
		QSFile(File)
		Return 1 ;Updated an existing value
	Else
		SeekFile(File\ID,File\Size)
		X$ = Key$+"="+KeyVal
		WriteLine(File\ID,X$)
		QSFile(File)
		Return 2 ;Created a new value
	End If
	
	Return 0

End Function


;FReadS$(File.DataFile, Key$)
;File.DataFile--A File Opened with FOpen
;Key$--The identifier to look for in the file, where the information is stored
;Returns the string value found at identifier Key$ or "" if Key$ was not found

Function FReadS$(File.DataFile, Key$)
	Key$ = "$" +Key$
	P = KeyExists(File, Key$)
	If P &lt;&gt; -1 Then
		SeekFile(File\ID,P)
		X$ = ReadLine(File\ID)
		L = Instr(X$, "=")
		If L = 0 Then Return 0
		L = Len(X$) - (L)
		Return Right$(X$, L)

	End If
	Return ""
	
End Function


;FWriteI(File.DataFile, Key$, KeyVal%)
;File.DataFile--A file opened with FOpen
;Key$--The identifier that will be used to retrieve the data later
;KeyVal%--The Value to be stored for retrieval later
;Writes an integer to a file or updates an integer value in the file.

Function FWriteI(File.DataFile, Key$,KeyVal%)
	Key$ = "%" +Key$
	If File\Size = 0 Then
		X$ = Key$+"="+Str(KeyVal)
		WriteLine(File\ID,X$)
		QSFile(File)
		Return 2
	End If
	P = KeyExists(File, Key$)
	If P &lt;&gt; -1 Then
		SeekFile(File\ID,P)
		X$ = ReadLine(File\ID)
		SeekFile(File\ID,P)
		L = Len(X$)
		X$ = Key$+"="+Str(KeyVal)
		If Len(X$) &lt; L Then X$ = LSet$(X$,L)
		WriteLine(File\ID, X$)
		QSFile(File)
		Return 1 ;Updated an existing value
	Else
		SeekFile(File\ID,File\Size)
		X$ = Key$+"="+Str(KeyVal)
		WriteLine(File\ID,X$)
		QSFile(File)
		Return 2 ;Created a new value
	End If
	
	Return 0

End Function


;FReadI%(File.DataFile, Key$)
;File.DataFile--A File Opened with FOpen
;Key$--The identifier to look for in the file, where the information is stored
;Returns the intiger value found at identifier Key$ or 0 if Key$ was not found

Function FReadI%(File.DataFile, Key$)

	Key$ = "%" +Key$
	P = KeyExists(File, Key$)
	If P &lt;&gt; -1 Then
		SeekFile(File\ID,P)
		X$ = ReadLine(File\ID)
		L = Instr(X$, "=")
		If L = 0 Then Return 0
		L = Len(X$) - (L)
		X$ = Right$(X$, L)
		L = X$
		Return L

	End If
	Return 0
	
End Function


;FWriteF(File.DataFile, Key$, KeyVal#)
;File.DataFile--A file opened with FOpen
;Key$--The identifier that will be used to retrieve the data later
;KeyVal#--The Value to be stored for retrieval later
;Writes a float to a file or updates a float value in the file.

Function FWriteF(File.DataFile, Key$,KeyVal#)
	Key$ = "#" +Key$
	If File\Size = 0 Then
		X$ = Key$+"="+Str(KeyVal)
		WriteLine(File\ID,X$)
		QSFile(File)
		Return 2
	End If
	P = KeyExists(File, Key$)
	If P &lt;&gt; -1 Then
		SeekFile(File\ID,P)
		X$ = ReadLine(File\ID)
		SeekFile(File\ID,P)
		L = Len(X$)
		X$ = Key$+"="+Str(KeyVal)
		If Len(X$) &lt; L Then X$ = LSet$(X$,L)
		WriteLine(File\ID, X$)
		QSFile(File)
		Return 1 ;Updated an existing value
	Else
		SeekFile(File\ID,File\Size)
		X$ = Key$+"="+Str(KeyVal)
		WriteLine(File\ID,X$)
		QSFile(File)
		Return 2 ;Created a new value
	End If
	
	Return 0

End Function


;FReadF#(File.DataFile, Key#)
;File.DataFile--A File Opened with FOpen
;Key$--The identifier to look for in the file, where the information is stored
;Returns the float value found at identifier Key$ or 0.0 if Key$ was not found

Function FReadF#(File.DataFile, Key$)

	Key$ = "#" +Key$
	P = KeyExists(File, Key$)
	If P &lt;&gt; -1 Then
		SeekFile(File\ID,P)
		X$ = ReadLine(File\ID)
		L = Instr(X$, "=")
		If L = 0 Then Return 0
		L = Len(X$) - (L)
		X$ = Right$(X$, L)
		L = X$
		Return L

	End If
	Return 0
	
End Function


;KeyExists(File.DataFile, Key$)
;File.DataFile--A File opened with FOpen
;Key$--The identifier to search for.
;Returns the location in the file where Key$ is found, or -1 if Key$ was not found

Function KeyExists(File.DataFile, Key$)

	L = Len(Key$)
	SeekFile(File\ID,0)
	While Not Eof(File\ID)
		N = FilePos(File\ID)
		X$ = ReadLine(File\ID)
		If Lower$(Key$) = Lower$(Left$(X$,L)) Then
			Return N
		End If
	Wend
	Return -1

End Function


;QSFile(File.DataFile)
;File.Datafile--A file opened with FOpen
;Closes and reopens a file to save data changes to disk.

Function QSFile(File.DataFile)

	CloseFile(File\ID)
	File\ID = OpenFile(File\FName$)
	UpdateFile(File)
	Return

End Function


;UpdateFile(File.DataFile)
;File.Datafile--A file opened with FOpen
;Updates the information of a Datafile type.  Currently only updates size.

Function UpdateFile(File.DataFile)

	File\Size = FileSize(File\FName$)
	Return

End Function

;FOpen.DataFile(Filename$,RC)
;Filename$--a string containing the file you want to open
;RC--a flag indicating if the file should be created when not found
;Opens a file and returns a Datafile custom type with information and a
;pointer to the file in its fields.

Function FOpen.DataFile(Filename$,RC)

File.DataFile = New DataFile
Select RC
	Case 0 ;Read/Write, create if not found
		If ff_is_file(Filename$) &lt;&gt; True Then
			CurFile = WriteFile(FileName$)  ;Creating Unfound File
			CloseFile(CurFile)
		End If
		File\ID = OpenFile(Filename$)
		File\FName$ = Filename$
		File\Size = FileSize(Filename$)
		Return File
		
		
	Case 1 ;Read/Write, Return Error on no file found
		If ff_is_file(Filename$) &lt;&gt; True Then
			Return Null ;File not found
		End If
		File\ID = OpenFile(Filename$)
		File\FName$ = Filename$
		File\Size = FileSize(Filename$)
		Return File

	Default
		Delete File
		Return Null ;Unexpected error
	
End Select

End Function


;FClose(File.DataFile)
;File.DataFile--A File opened with FOpen
;Closes a file previously opened with FOpen

Function FClose(File.DataFile)

	CloseFile(File\ID)
	Delete File

End Function

;A function to check if a file exists
;Not mine.

Function ff_is_file(file$)
	If FileType(file$) = 1 Then Return True
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
