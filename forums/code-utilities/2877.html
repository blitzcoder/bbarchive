<!DOCTYPE html><html lang="en" ><head ><title >Shortcut</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Create a shortcut file (*.lnk), language=bb, category=File Utilities'><meta name='author' content='Subirenihil'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=8>File Utilities</a>/Shortcut</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2877.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Shortcut by Subirenihil</td><td align="right">2011 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> [UPDATED] Thank you Malice for your telling me what that mysterious blank parameter was (username) and what I was missing for getting the comment to work!<br><br>Function to create a shortcut.<br>Parameters are:<br>	LinkLocation$ - Full path to the folder to create the shortcut in<br>	Title$ - File name, not including .lnk extension<br>	Target$ - Target of the shortcut<br>	IconFile$ - Full path and filename of the file containing the icon the shortcut will use<br>	IconIndex% - which icon in the IconFile$ to use<br>	UserName$ - (optional) Name of the user who can use the shortcut. Leave blank for all users. Use GetEnv("USERNAME") for the current user.<br>	StartIn$ - (optional) Full path of the folder the Target$ will start in<br>	Description$ - (optional) Set the text displayed when the shortcut is moused over<br><br>Please note that this does leave a very small .inf file in the users temp directory every time its used.<br><br>The following lines must be in shell32.decls:<br>	.lib "shell32.dll"<br>	ShellExecute%(hwnd%,Operation$,File$,Parameters$,Directory$,ShowCmd%):"ShellExecuteA" </td></tr><tr ><td class="cell"><pre class="code">;The following lines must be in shell32.decls
;   .lib "shell32.dll"
;   ShellExecute%(hwnd%,Operation$,File$,Parameters$,Directory$,ShowCmd%):"ShellExecuteA"

Const DATALINES = 9 ;+10 ;use this if you use the readability modification below

;Load a template .inf file from data
Dim InfLines$(DATALINES)

Restore template
For a=0 To DATALINES-1
	Read InfLines$(a)
	InfLines$(a) = Replace$(InfLines$(a),"'",Chr$(34)) ;Convert single quotes into double quotes
Next

;Create a shortcut to notepad on the user's desktop
CreateShortcut "%16384%", "Testing", "%10%\NOTEPAD.EXE", "%10%\NOTEPAD.EXE", 0, "", "%16430%", "Open Notepad"
End

;Creates a shortcut called &lt;Title&gt; at &lt;Link Location&gt; that opens &lt;Target&gt;,
;  has icon &lt;Icon Index&gt; from &lt;Icon File&gt;, starts in &lt;Start Folder&gt; and has &lt;Description&gt;
;  &lt;Target&gt; also needs to contain any additional parameters.
Function CreateShortcut(LinkLocation$, Title$, Target$, IconFile$, IconIndex%, UserName$="", StartIn$="", Description$="")
	Local infstr$[DATALINES]
	
	tempdir$=SystemProperty("tempdir")
	If Right$(tempdir$,1)&lt;&gt;"\" Then tempdir$=tempdir$+"\"
	
	Repeat
		temp$=tempdir$+"Create Shortcut"+Rand(10000)+".inf"
	Until FileType(temp$)=0
	
	inf%=WriteFile(temp$)
	
	For a = 0 To DATALINES-1
		infstr[a]=InfLines$(a)
		;For easier to read *.inf files, change the %'s to &lt; and &gt;
		If Instr(InfLines$(a),"%LinkLocation%"): infstr[a]=Replace$(infstr[a],"%LinkLocation%", LinkLocation$)	:EndIf; &lt;Link Location&gt;
		If Instr(InfLines$(a),"%LinkTitle%"   ): infstr[a]=Replace$(infstr[a],"%LinkTitle%"   , Title$)			:EndIf; &lt;Title&gt;
		If Instr(InfLines$(a),"%Target%"      ): infstr[a]=Replace$(infstr[a],"%Target%"      , Target$)		:EndIf; &lt;Target&gt;
		If Instr(InfLines$(a),"%IconFile%"    ): infstr[a]=Replace$(infstr[a],"%IconFile%"    , IconFile$)		:EndIf; &lt;Icon File&gt;
		If Instr(InfLines$(a),"%IconIndex%"   ): infstr[a]=Replace$(infstr[a],"%IconIndex%"   , IconIndex%)		:EndIf; &lt;Icon Index&gt;
		If Instr(InfLines$(a),"%UserName%"    ): infstr[a]=Replace$(infstr[a],"%UserName%"    , UserName$)		:EndIf; &lt;User Name&gt;
		If Instr(InfLines$(a),"%StartFolder%" ): infstr[a]=Replace$(infstr[a],"%StartFolder%" , StartIn$)		:EndIf; &lt;Start Folder&gt;
		If Instr(InfLines$(a),"%Description%" ): infstr[a]=Replace$(infstr[a],"%Description%" , Description$)	:EndIf; &lt;Description&gt;
		WriteLine inf, infstr[a]
	Next
	
	CloseFile inf%
	ShellExecute 0,"install",Chr$(34)+temp$+Chr$(34),"","",0
End Function

.template
Data "[Version]"
Data "signature='$CHICAGO$'"
Data ""
Data "[DefaultInstall]"
Data "UpdateInis=Addlink"
Data ""
Data "[Addlink]"
Data "setup.ini, progman.groups,, 'group1=''%LinkLocation%'''"
Data "setup.ini, group1,, '''%LinkTitle%'',''%Target%'',''%IconFile%'',%IconIndex%, ''%UserName%'', ''%StartFolder%'', Comment='%Description%''"

;You can also uncomment this data section (and add 10 to the DATALINES constant),
; as well as slightly modify the CreateShortcut function to make the *.inf easier to read.
; However, if you are not using the complete file paths (i.e. *.inf directory listings, etc.) then this won't work.
	;Data ""
	;Data "[Strings]"
	;Data "LinkTitle    = '&lt;Title&gt;'"
	;Data "LinkLocation = '&lt;LinkLocation&gt;'"
	;Data "Target       = '&lt;Target&gt;'"
	;Data "IconFile     = '&lt;IconFile&gt;'"
	;Data "IconIndex    = '&lt;IconIndex&gt;'"
	;Data "UserName     = '&lt;UserName&gt;'"
	;Data "StartFolder  = '&lt;StartFolder&gt;'"
	;Data "Description  = '&lt;Description&gt;'"

; Common DIRID Listing, as used by *.inf files
; For example: if &lt;Link Location&gt; = "%16384%" then the shortcut would be created on the user's desktop.
;
; Destination Directories
;
; 01 - SourceDrive:\pathname         (the directory from which the INF file was installed) 
; 10 - Windows directory             (%SystemRoot%) 
; 11 - System directory              (%SystemRoot%\system32 -NT-, %SystemRoot%\system -Win9x/Me-) 
; 12 - Drivers directory             (%SystemRoot%\system32\drivers -NT-, %SystemRoot%\system\IoSubsys -Win9x/Me-)
; 17 - INF file directory            (%SystemRoot%\inf)
; 18 - Help directory                (%SystemRoot%\Help)
; 20 - Fonts directory               (%SystemRoot%\Fonts)
; 24 - Root directory of system disk (%SystemDrive%)
; 25 - Shared directory              (%ALLUSERSPROFILE%\Shared Documents)
; 53 - User profile directory        (%USERPROFILE%)
;
; Shell Special Folders
;
; 16419 %ALLUSERSPROFILE%\Application Data
; 16409 %ALLUSERSPROFILE%\Desktop
; 16430 %ALLUSERSPROFILE%\Documents
; 16437 %ALLUSERSPROFILE%\Documents\My Music
; 16438 %ALLUSERSPROFILE%\Documents\My Pictures
; 16439 %ALLUSERSPROFILE%\Documents\My Videos
; 16415 %ALLUSERSPROFILE%\Favorites
; 16406 %ALLUSERSPROFILE%\Start Menu
; 16407 %ALLUSERSPROFILE%\Start Menu\Programs
; 16431 %ALLUSERSPROFILE%\Start Menu\Programs\Administrative Tools
; 16408 %ALLUSERSPROFILE%\Start Menu\Programs\Startup
; 16429 %ALLUSERSPROFILE%\Templates
; 16410 %USERPROFILE%\Application Data
; 16417 %USERPROFILE%\Cookies
; 16384 %USERPROFILE%\Desktop
; 16400 %USERPROFILE%\Desktop
; 16390 %USERPROFILE%\Favorites
; 16412 %USERPROFILE%\Local Settings\Application Data
; 16443 %USERPROFILE%\Local Settings\Application Data\Microsoft\CD Burning
; 16418 %USERPROFILE%\Local Settings\History
; 16416 %USERPROFILE%\Local Settings\Temporary Internet Files
; 16389 %USERPROFILE%\My Documents
; 16397 %USERPROFILE%\My Documents\My Music
; 16423 %USERPROFILE%\My Documents\My Pictures
; 16398 %USERPROFILE%\My Documents\My Videos
; 16403 %USERPROFILE%\NetHood
; 16411 %USERPROFILE%\PrintHood
; 16392 %USERPROFILE%\Recent
; 16393 %USERPROFILE%\SendTo
; 16395 %USERPROFILE%\Start Menu
; 16386 %USERPROFILE%\Start Menu\Programs
; 16432 %USERPROFILE%\Start Menu\Programs\Administrative Tools
; 16391 %USERPROFILE%\Start Menu\Programs\Startup
; 16405 %USERPROFILE%\Templates
; 16422 %ProgramFiles%
; 16427 %ProgramFiles%\Common Files
; 16440 %SystemRoot%\Resources
; 16441 %SystemRoot%\Resources\0409</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>2012</font></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Description$ - (optional) Not sure why this doesn't seem to work currently, but should set the text displayed when the shortcut is moused over<br> <br></div><br><br>Your inf script file is incomplete.<br>There should be space for a "USER" argument (used if the shortcut is to be only eligible for certain users) But more importantly, you have forgotten to declare the Comment line.<br><br>This is necessary to populate the field with the value supplied.<br><br>Sorry I changed your original quite a lot, but it should be simple enough to see where the changes need to be made :)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

;Example
CreateShortcut("%16384%", "Testing", "%10%\NOTEPAD.EXE", "%10%\NOTEPAD.EXE", 2,GetEnv("USERNAME"), "%10%", "This works!")
End


Function CreateShortcut(Location$,Name$,Target$,Icon$,IconZBI,UserProfile$,TargetDir$,Comment$)
	
	Local InfPath$=GetEnv("TEMP")+"\"+"CreateShortcut"+CurrentDate()+".inf"
	
	Local Args_ProgGroups$="setup.ini, progman.groups,, 'group1=''"+Location+"'''"
	Local Args_Link$="setup.ini, group1,,'''"+Name+"'',''"+Target+"'',''"+Icon+"'',"+Str(IconZBI)+",''"+UserProfile+"'',"+TargetDir+", Comment='"+Comment+"''"
	
	Local inf=WriteFile(InfPath)
	
	WriteLine inf,"[Version]"
	WriteLine inf,"signature='$CHICAGO$'	
	WriteLine inf,""
	WriteLine inf,"[DefaultInstall]"
	WriteLine inf,"UpdateInis=Addlink"
	WriteLine inf,""
	WriteLine inf,"[Addlink]"
	WriteLine inf,Replace(Args_ProgGroups,"'",Chr(34))
	WriteLine inf,Replace(Args_Link,"'",Chr(34))
	
	CloseFile inf%
	
	
	ShellExecute 0,"install",Chr$(34)+InfPath$+Chr$(34),"","",0
	
End Function
</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Subirenihil</td><td align="right"><font class=tiny>2012</font></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Malice! I never could find any information on what that parameter was, good to know it's the username.<br>And thanks for figuring out what was wrong with the description parameter.<br><br>I've updated the code to reflect your insight. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
