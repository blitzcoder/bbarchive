<!DOCTYPE html><html lang="en" ><head ><title >Any File to b64 BB data statements</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Post small images with your source, language=bb, category=File Utilities'><meta name='author' content='Andy_A'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=8>File Utilities</a>/Any File to b64 BB data statements</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="3026.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Any File to b64 BB data statements by Andy_A</td><td align="right">2013 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Creates a .bb file of data statements which can be posted with your source code. The created .bb file can then be run to save the encoded image or zip to disk.<br><br><br>Pros:<br>   Encodes files in a more compact format than decimal data and hex data. <br>   Decimal data statements use ~3.57 bytes to encode 1 byte from file.<br>   Hex data statements use ~2 bytes to encode 1 byte of data from file<br>   b64 data statements use ~1.33 bytes to encode 1 byte of data from file<br>Fairly fast, I encoded a 45K jpg in 3 seconds (reading the input file, encoding the data, writing data to output file)<br><br>Cons:<br>   Large loader code adds ~950 bytes to data<br>Too slow for realtime use </td></tr><tr ><td class="cell"><pre class="code">;Any file to base64 data statements by Andy_A
;2013.01.30

AppTitle "Any file to b64 data statements"

;==============================================================================
Const sizeOfLine = 60 ;&lt;-- change number of bytes encoded per line of data here


Const loader = 1 ;&lt;-- change to zero for data output ONLY, else add loader code


BlitzPlus = 1 ;&lt;-- Change to zero if using Blitz Classic or Blitz3D


;Change 'Restore' label for data statements here
dataLabel$ = "imgData" ;&lt;-- ** DO NOT **  include leading period/stop
;==============================================================================

If BlitzPlus Then
	file$ = RequestFile("Load file to convert to data...","*",False)
	If file$ = "" Then RuntimeError "File Load aborted..."
Else
	file$ = "blocks.png" ;&lt;-- blocks from Blitzanoid, change to your file here (B3D)
End If

;Read the file to convert
filein = ReadFile(file$)
;Create and fill bank with file data	
size = FileSize(file$)

;name the output file
If BlitzPlus Then
	datafile$ = RequestFile("Save data as...","bb",True,"dataFile.bb")
	If datafile$ = "" Then RuntimeError "File save aborted."
Else
	datafile$ = "blocks_png.bb" ;&lt;-- change to your output name here (B3D)
End If
fileOut = WriteFile(datafile$)
fullLines = Floor(size/sizeOfLine )
rm = size - (fullLines * sizeOfLine )
q$ = Chr$(34)
char$="ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789+/"
Dim k(63)
For i = 0 To 63
	k(i) = Asc(Mid(char$,i+1,1))	
Next

st = MilliSecs()
If loader = 1 Then
	WriteLine(fileOut,"Restore "+dataLabel$+":Read p$,s,t:o=WriteFile(p):n=Floor(s/t):If(n*t)&lt;s Then n=n+1")
	WriteLine(fileOut,"For i=1To n:Read d$:z$=z+d64(d):For j=1To Len(z)")
	WriteLine(fileOut,"WriteByte(o,Asc(Mid(z,j,1))):Next:z="+q$+q$+":Next:CloseFile o:End")
	WriteLine(fileOut,"Function d64$(d$): s$="+q$+q$+":rm=0:L=Len(d):If Right(d,1)="+q$+"="+q$+"Then rm=2")
	WriteLine(fileOut,"r$="+q$+"ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789+/"+q$)
	WriteLine(fileOut,"If Right(d,2)="+q$+"=="+q$+"Then rm=1")
	WriteLine(fileOut,"If rm&gt;0Then m=L-5Else m=L-1")
	WriteLine(fileOut,"For i=1To m Step 4:w=Instr(r,Mid(d,i,1))-1:x=Instr(r,Mid(d,i+1,1))-1")
	WriteLine(fileOut,"y=Instr(r,Mid(d,i+2,1))-1:z=Instr(r,Mid(d,i+3,1))-1")
	WriteLine(fileOut,"a=(w Shl 2)+((x And 48)Shr 4):b=((x And 15)Shl 4)+((y And 60)Shr 2)")
	WriteLine(fileOut,"c=((y And 3)Shl 6)+z:s=s+Chr(a)+Chr(b)+Chr(c):Next:If rm=1Then")
	WriteLine(fileOut,"w=Instr(r,Mid(d,L-3,1))-1:x=Instr(r,Mid(d,L-2,1))-1")
	WriteLine(fileOut,"a=(w Shl 2)+((x And 48)Shr 4):s=s+Chr(a):EndIf:If rm=2Then")
	WriteLine(fileOut,"w=Instr(r,Mid(d,L-3,1))-1:x=Instr(r,Mid(d,L-2,1))-1")
	WriteLine(fileOut,"y=Instr(r,Mid(d,L-1,1))-1:a=(w Shl 2)+((x And 48) Shr 4)")
	WriteLine(fileOut,"b=((x And 15) Shl 4)+((y And 60)Shr 2):s=s+Chr(a)+Chr(b):EndIf")
	WriteLine(fileOut,"Return s:End Function")
End If

WriteLine(fileout,"."+dataLabel$)
WriteLine(fileout,"Data"+q$+file$+q$+","+size+","+sizeOfLine)
For i = 1 To size-rm-1 Step sizeOfLine
	a1$ = ""
	For j = 1 To sizeOfLine
		a1$ = a1$ + Chr(ReadByte(filein))
	Next
	WriteLine(fileOut, "Data" + q$ + e64(a1$) + q$ )
Next
If rm &gt; 0 Then
	a1$ = ""
	For i = 1 To rm
		a1$ = a1$ + Chr(ReadByte(filein))
	Next
	WriteLine(fileOut, "Data" + q$ + e64(a1$) + q$)
End If

CloseFile(filein)
CloseFile(fileout)
et = MilliSecs()-st
Print "ET: "+et

WaitKey()
End

Function e64$(e$)
	f$ = ""
	g = Len(e$)
	rm = g- Floor(g/3)*3
	For i = 1 To g-rm-1 Step 3
		a = Asc(Mid$(e$,i  ,1))
		b = Asc(Mid$(e$,i+1,1))
		c = Asc(Mid$(e$,i+2,1))
		w = (a And 252) Shr 2
		x = ((a And 3) Shl 4) + ( (b And 240) Shr 4)
		y = ((b And 15) Shl 2) + ( (c And 192) Shr 6)
		z = c And 63
		f$ = f$ + Chr$(k(w)) + Chr$(k(x)) +Chr$(k(y)) +Chr$(k(z))
	Next
	If rm = 1 Then
		a = Asc(Right(e$,1))
		w = (a And 252) Shr 2
		x = (a And 3) Shl 4
		f$ = f$ + Chr(k(w)) + Chr(k(x)) + "=="
	End If
	If rm = 2 Then
		a = Asc(Mid(e$,g-1,1))
		b = Asc(Right(e$,1))
		w = (a And 252) Shr 2
		x = (a And 3) Shl 4
		x = x + ( (b And 240) Shr 4)
		y = (b And 15) Shl 2
		f$ = f$ + Chr(k(w))+Chr(k(x))+Chr(k(y)) + "="
	End If
	Return f$
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Andy_A</td><td align="right"><font class=tiny>2013</font></td></tr></table></td></tr><tr ><td class="posttext"> Here's an encoded png file (2K) ready to save to disk:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">;LOADER code ================================================================
Restore imgData:Read p$,s,t:o=WriteFile(p):n=Floor(s/t):If(n*t)&lt;s Then n=n+1
For i=1To n:Read d$:z$=z+d64(d):For j=1To Len(z)
WriteByte(o,Asc(Mid(z,j,1))):Next:z="":Next:CloseFile o;:End &lt;- comment out the 'End' statement
Function d64$(d$): s$="":rm=0:L=Len(d):If Right(d,1)="="Then rm=2
r$="ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789+/"
If Right(d,2)="=="Then rm=1
If rm&gt;0Then m=L-5Else m=L-1
For i=1To m Step 4:w=Instr(r,Mid(d,i,1))-1:x=Instr(r,Mid(d,i+1,1))-1
y=Instr(r,Mid(d,i+2,1))-1:z=Instr(r,Mid(d,i+3,1))-1
a=(w Shl 2)+((x And 48)Shr 4):b=((x And 15)Shl 4)+((y And 60)Shr 2)
c=((y And 3)Shl 6)+z:s=s+Chr(a)+Chr(b)+Chr(c):Next:If rm=1Then
w=Instr(r,Mid(d,L-3,1))-1:x=Instr(r,Mid(d,L-2,1))-1
a=(w Shl 2)+((x And 48)Shr 4):s=s+Chr(a):EndIf:If rm=2Then
w=Instr(r,Mid(d,L-3,1))-1:x=Instr(r,Mid(d,L-2,1))-1
y=Instr(r,Mid(d,L-1,1))-1:a=(w Shl 2)+((x And 48) Shr 4)
b=((x And 15) Shl 4)+((y And 60)Shr 2):s=s+Chr(a)+Chr(b):EndIf
Return s:End Function
;============================================================================

;add your display code here
Graphics 640,480,32,2
SetBuffer BackBuffer()

image = LoadImage("blocks.png")
DrawImage image,(640-ImageWidth(image))/2, (480-ImageHeight(image))/2
Flip

WaitMouse()
FreeImage image
End

;generated b64 data statements
.imgData
Data "blocks.png",2216,99
Data "iVBORw0KGgoAAAANSUhEUgAAAOAAAAAgCAIAAACEp+MCAAAAAXNSR0IArs4c6QAAAARnQU1BAACxjwv8YQUAAAAgY0hSTQAAeiYAAICEAAD6AAAAgOgAAHUwAADqYAAAOpgA"
Data "ABdwnLpRPAAAAAlwSFlzAAALMgAACzIBQPVfaAAACBFJREFUeF7dmT9sHUUQxvdF0CBFAYmQPKM4doFEAQUSFFS0aSheR5UKpARRUkGTwkh5IoWpIqVEKLIQxZPS0aRxYQRK"
Data "XgGiQQJZwVYQBVYUSEjw8b039mS9Mzc373Yv/nMane729r7b2/3d3O5MrxoOA7a1tcne2Obnw9xc2NgI6+sNNXfVFjc3cfjJsMJ+3CTfnw+n5sLdjbDpkL89VfthcxH7fx9d"
Data "nJxsNz2gNx96c6HaCJXjAVO1xVcn7b/429XJC4VbDd0TXp4LpzbC3fXwe2P/rIXbqLO5OMZ++NOjif7jpu4/FuaOhY3tsL7dKB/WJpJh841J/zz46Ffs/2vqnmO73bPt6J7H"
Data "U7XXp/1/ffUr7O/cuWM368SJE8ePH793797W1lbjC5Da0tLSpCYAHYVgGHqx6verwWCy7/VwKg2XkkKULIQAuzas+mFk2JkwfrtffTiosH+lV+FUGi4lhSh5NizAqkfD0dfB"
Data "sPF3ofqnXz0cTPZ/93AqDZeSQpQsnA2wYXU1jE5aNj7dr94cVB9g36vOhvFpabiUFE5KFp6BDe9X4cuRZTfH/T+rwVaFfe+PKtwcS8OlpHBScmYB9mBYXQ8jw1bDeKtf3R9U"
Data "2P/Vq3AqDZeSQpTMhwXY6urqe+Z24cKFK1eurKysYH/p0iWcyg2XkkKUPD/dAsgrTidghSYBCvKK0wlYobkD6MNBcToBKzQJUJBXnM4JrIB+CijIK07nBFZAPwUU5BWnE7BC"
Data "kwAFeQaf7ejEXdBsBrSd7yRX6gG0ne8kV+oBtJ3vJFfqArSV79xxpR5AW/nOHVfqALSd7yRX6gG0NZ0uQHPo9ACaQ6cH0Bw6XYDm0OnxoDl0OjxoDp0eQHPobAbURWfNfNTj"
Data "QT101s1HPR7URWfNfNTlQR101s1HXR7UQWfdfNTjQT101s1HPR7UQ2fdfJQmo9YvPp9O24Pm02l70Hw6GzxoPp22B82n0/Sg+XTaHjSfTgvQInSSE1UXSUXoJCeqLpKK0ElO"
Data "VF8kFaGTVvrqIqkInbTS1xZJRegkJ6oukorQSU5UWSQVpFNdJBWkU10kFaRTXyQVpFNdJBWkU1skFaRTXSQVpFP5xbvoFPFODn8mIVIJqIdOGe/k8GcyJZWAuugU8U4OfyYh"
Data "UgVQB50y3snhz3RKKgF10CnjnRz+TKekAlAPnTLeyeHPZEoqAfXQKeOdHP5MpqQpoJl0ytB9AmgmnTJ0nwCaSacM3aeA5tGpRO8TQPPoVKL3ewHNpFOG7hNAM+mUofs9gEo6"
Data "ZcZI5ooklHFJDKikU67QDd+p5pZiQCWdMmMkc0VqSokL9wAq6JQrdMN3qrmlPYF6QadcoRu+U80txYF6SadcoRu+U80txYBKOuUK3fCdam5pD6BJJlPNW9o4yqvL0SIpyWSq"
Data "eUuVQqPwxbDMi6Qkk6nmLW0c5dXlz58skpJMpp631DKcOppUc/kFXiQlmUw9b6llOHU0qebSMi+SkkymmrdUKTQKL4dlXiQlmUw1b6lSaBSeO3duZ5FE7pMCQzA+oFO1hC/V"
Data "HYBOBpTcJwWGYHzA+U9ZYufucRV0MqDkPikwBOMDzn/KEjt3j6ugkwEFTDtEUkaeIkSxyRI7d4+roHMXUMC0QyRl5ClCFJsssXP3uAo6dwEl90mBIRgfcP5Tlti5e1wFnQwo"
Data "uU8KDGHjA85/yhI7d4+roPMJoBQM6s7Iz3VnFAzqzsjPdWjTYFB3Rn6uOyM/193WIZoEfXdoknJ3aJJyh2gS913SCfHu0CTl7tDc+cX/8vM3sO9XP7Ptx1vXUA37xpqo8O3o"
Data "POylk8/B/PohhK718YhO219cH4LJNmv7pcIhKwFAn378lmFfXH4HtBF2N1bexak0XEoKY0Cd+ui4p6DPwxM3OGl/PIT+/kkGvpR+HU9O/UOGo/pFFqcTfQdN8qB4Io7pufJB"
Data "TP9MdBbRpyZRA3iwuX9atF9FIV/fJqxO/9BzyS8A11gHaDvfSYPNAwx9RjNhNNYnVmzfHNcpog9BSWfr9qtMNOond8VjQf1jo6Z+XUeHTrxJHaA5dCaAxp3OjHLv0xjU0cl+"
Data "LqkTA3oQ9Bv9XF375Y30Oh46+V5UPlJQxi+jAuqhs26+KD1Q4qH5x0oOrI7OWF/WqQOUphMJ/Uw5DyQOius3Mhp/hPEUQt44E51HFk16MQloPp2GByXfQI82Vl0JPTMBWqev"
Data "YhojqwKk9o/afqkfo5P8ImxADeZI54hDaXjQInRSJ9IiKRngFnTykCRj3EKfXpzpT5DaF/0WqCXtNxRokXq4txiggnSqv2DPvNOYOcR+NEc/5jKG0q9fp0Azh5n0W9PDLa9T"
Data "4ChK60cciBsZUA+dMt7J3ZSAJQGK6aTK8sf91PQThviUm2S03/hxc/tV/biw9S/eA008xfLUP9B1CNBMOpOfo5yDSjoloAadHekTjvF3Ugcotz8Zy7hhsv2sL5nOAbTRdx41"
Data "QCWd8j87Ez0JoKyPcSLleBZF3b0v+uq/mKiKPWjcfulsjPbXeabWACUfanH9g+hKKYcZZzLVvKX0YXbJ++df40UMvzZxmcSWW9CJW0rpy2kGlaj66vjZ7a+7hfU9TBhfbxF9"
Data "Txv2rQ73L8eHk7Alfe4zGXo/HgDq3zp9vPlM4qhcVl82ACUzAWS3Xx3aA6W/b/B5Hkx+rjtDG2zxxgqZt3v0qaPiB/FpkT5URfyr7MbRydT3vOO+1Wl8+cwKTj5aP6Wgvirl"
Data "HBhP+51SBm2N32rOIw7mvf8D5zQ8aMiOeWUAAAAASUVORK5CYII="</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >virtlands</td><td align="right"><font class=tiny>2013</font></td></tr></table></td></tr><tr ><td class="posttext"> It's fascinating Base64 encoding.  Storing it in strings was a great idea.<br><br>Did you know that if you include too many megabytes of <br>"Data " statements that it crashes the IDE ? <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Andy_A</td><td align="right"><font class=tiny>2013</font></td></tr></table></td></tr><tr ><td class="posttext"> Yes, quite aware however, I would never recommend creating a file greater than 200K for personal use as there are better methods for including large files with your executable.<br><br>As for posting stuff on the web to avoid 404's later on down the road, I would recommend keeping to 6K or less as a max. <br><br>&lt;mini-rant&gt;<br>Remember, it's very rude and inconsiderate to waste someone else's bandwidth with huge images or encoded images for that matter. &lt;end-rant&gt;<br><br>Base 64 provided the best encoding efficiency of all the simple algo's I reviewed. There are more efficient algo's but require more code for re-assembling the original file from data statements. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
