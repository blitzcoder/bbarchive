<!DOCTYPE html><html lang="en" ><head ><title >CSV Generator</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Generate code for .csv files (Jul03 update), language=bb, category=File Utilities'><meta name='author' content='Braneloc'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=8>File Utilities</a>/CSV Generator</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="442.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >CSV Generator by Braneloc</td><td align="right">2002 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Generates nice friendly useful code for your database/spreadsheet files stored in .csv format.  (Comma Seperated Values, as exported by Excel and most spreadsheets)   Update your code instantly if you add/delete fields.  Use multiple CSV files with a single include.<br><br>Uses multiple files, please download complete files from <a href="http://www.mirex.demon.co.uk/blitz" target="_blank">http://www.mirex.demon.co.uk/blitz</a><br><br>Enjoy !<br><br><br><br>		============================<br>		   CSV Database Generator !<br>		     Lousy Documentation<br>		============================<br><br>		Copyright Keith Hill, Aug 2002<br>		    Braneloc@...<br><br>	Purpose:<br>		Nice friendly CSV data files for your custom TYPE lists<br>		and standard functions to do stuff with them.<br><br><br>	Usage:<br>		Stick all your CSV databases in a directory<br>		Have a nice database_code directory<br>		Run this program, it will create....<br><br>		"db_code/database.bb"<br>			master include for all generated databases<br>	<br>		code that does the following...  (where TYPE is the name of your csv.database)<br><br>		TYPE_load			Load the entire type list into memory<br>		TYPE_kill			Free the entire type list from memory<br>		TYPE_save			Save the entire type list to file<br>		TYPE_add			Add a TYPE to memory - requires full parameters<br>		TYPE_find			Find a type in your list indexed on the first field<br>		TYPE_count			How many of these darn things have you loaded ?<br>		TYPE_fadd			Find-and-Add - create a new one if it does not exist<br>		TYPE_sort			Sorts the memory list into order<br><br>	Licence:<br>		Use it as you see fit, free of charge for all projects !   (Though I'd<br>		appreciate an email if you use it in your projects)<br><br>		Ownership and copyright of the generator code and template remain with <br>		Braneloc@... at all times.  Please send me any updates(!)<br><br>	Distribution:<br>		You are quite free to distribute any generated code or executables based<br>		on generated code as you see fit, though the code generator/template<br>		(source or executable) may not be distributed without prior consent of<br>		Braneloc@... at all times for any use.  (Especially if you<br>		are making a profit and I am not !)<br>		<br><br>		<br>	Template:<br><br>		{	name of "type"<br>		#	first field of type<br>		¬	duplicate line for each field of type<br>		}	field in duplicated line<br>		~	entire data structure<br><br><br><br>Main program code (July03)<br>("csv-codegen.bb")<br><pre class=code>
;
;
;		CSV Database Generator !!
;
;		(c) K.Hill, Aug 2002
;
;
;
Include "misc.bb"
;Include "db_code/database.bb"

Global qq,name$,topline$
Global codedir$="db_code/"
Global datadir$="database"

;
;		Installation.
;		The two directories above, codedir$ and datadir$ should be created..
;		Put the template file into the CODEdir
;		Put any CSV files into the DATAdir
;		

;
;		Usage: 	Ok, once installation is done, you can auto-gen code to handle CSV files
;				as types by running this file.  In *your* code, just include either the
;				generated code file ( something_csv.bb) or "database.bb" (which contains
;				includes of all includes :)  It should all be fairly simple after that.
;

;
;		Lousy Documentation
;		(in templates)
;
;	{	name of "type"
;	#	first field of type
;	¬	duplicate line for each field of type
;	}	field in duplicated line
;	£	field in duplicated line - without type
;	~	entire data structure
;

master=WriteFile(codedir$+"database.bb")
	Restore base:a$=";"	
		While a$&lt;&gt;"***"
			Read a$
			If a$&lt;&gt;"***"
				WriteLine master,a$
			EndIf
		Wend
		WriteLine master,";Include "+Chr$(34)+"Misc.bb"+Chr$(34)
		WriteLine master,"Global csv_dir$:csv_dir$="+Chr$(34)+datadir$+"/"+Chr$(34)
	
	name$="_"
	dd=ReadDir(datadir$)
		While name$&lt;&gt;""
			;name$="test"
			name$=NextFile$(dd)
			;Print name$
			If name$&lt;&gt;""
				If FileType(name$)&lt;&gt;2
					name$=killext(name$)
					process(name$)
					WriteLine master,"Include "+Chr$(34)+codedir$+name$+"_csv.bb"+Chr$(34)
				EndIf
			EndIf
		Wend
	CloseDir dd

CloseFile master

Function killext$(a$)
	For n=1 To Len(a$)
		b$=Mid$(a$,n,1)
		If b$="."
			Return z$
		Else
			z$=z$+b$
		EndIf
	Next
	Return z$
End Function
Function process(name$)	
	Print "Processing..."+datadir$+"/"+name$+".csv"
	f=ReadFile(datadir$+"/"+name$+".csv")
	qq=WriteFile(codedir$+name$+"_csv.bb")
	
	topline$=ReadLine$(f)
	split_csv(topline$)
	
	For n=1 To 10
;		Print split$(n)
	Next
	
	
	;Restore start:codechunk()
	;pp( "Type "+name$)
	;template("	Field ")
	;pp("End Type")
	CloseFile f
	
	f=ReadFile(codedir$+"csv-template!.bb")
	While Not Eof(f)
		a$=ReadLine(f)
		If Mid$(a$,1,1)="¬"
			For ff=1 To WORDSNUM
				If split$(ff)&lt;&gt;""
					procline(Mid$(a$,2),ff)
				EndIf
			Next
		Else
			procline(a$)
		EndIf
	Wend
	CloseFile f
	CloseFile qq
End Function

Function procline(a$,ff=1)
	z$=""
	For n=1 To Len(a$)
		b$=Mid$(a$,n,1)
		Select b$
		Case "{"
			z$=z$+name$
		Case "#"
			z$=z$+split$(1)
		Case "}"
			z$=z$+split$(ff)
		Case "£"
			z$=z$+notype$(split$(ff))
		Case "~"
			z$=z$+topline$
		Default
			z$=z$+b$
		End Select
	Next
	pp(z$)
End Function
Function notype$(a$)
	z$=Right$(a$,1)
	If z$="#" Or z$="%" Or z$="$"
		Return Mid$(a$,1,Len(a$)-1)
	Else
		Return a$
	EndIf
End Function	
Function codechunk()
	a$=";"
	While a$&lt;&gt;"***"
		Read a$
		If a$&lt;&gt;"***"
			pp( a$)
		EndIf
	Wend
	Print ";"
End Function
Function template(a$="",b$="")
	z$=""
	For n=1 To WORDSNUM
		If split$(n)&lt;&gt;""
			pp( a$+split$(n)+b$)
		EndIf
	Next
End Function
Function pp(a$)
;	Print a$
	WriteLine qq,a$
End Function


.start
Data ";		Generated code
Data ";		CSV Database system
Data ";		By Braneloc, Aug 2002
Data ";
Data ";
Data ";		Global csv_dir$
Data "****
.base
Data ";
Data ";
Data ";		Generated database include
Data ";
Data ";		(c) K.Hill, Aug 2002
Data ";			Contains all database includes for the project
Data ";
Data ";
Data "****
</pre><br><br>TEMPLATE code (july03)<br>("db_code/csv-template!.bb")<br><pre class=code>
;
;
;		Generated code
;		CSV Database system
;		By Braneloc, Aug 2002
;
;
;		{.csv
;		~
;
;include "misc.bb"
;Global csv_dir$:csv_dir$="database/"

Type {
¬	Field }
End Type

Function {_pack$(q.{)
	a$=""
¬	a$=a$+q\}+","
	Return a$
End Function
Function {_data.{(a$)
	If a$&lt;&gt;""
		split_csv(a$)
		tag=1
		q.{=New {
¬		q\}=split$(tag):tag=tag+1
	EndIf
	Return q.{
End Function
Function {_load()
	f=ReadFile(csv_dir$+"{.csv")
	a$=ReadLine(f)	; header line
	While Not Eof(f)
		a$=ReadLine(f)
		If a$&lt;&gt;""
			split_csv(a$)
			tag=1
			q.{=New {
¬			q\}=split$(tag):tag=tag+1
		EndIf
	Wend
	CloseFile(f)
End Function
Function {_kill()
	For n.{=Each {
		Delete n
	Next
End Function
Function {_save()
	f=WriteFile(csv_dir$+"{.csv")
	WriteLine f,"~"
	For q.{=Each {
		a$=""
¬		a$=a$+q\}+","
		WriteLine f,a$
	Next
	CloseFile(f)
End Function
Function {_saveXML()
	f=WriteFile(csv_dir$+"{.XML")
	WriteLine f,"&lt;?xml version="+z$+"1.0"+z$+" encoding="+z$+"utf-8"+z$+" ?&gt;"
	WriteLine f,"&lt;{_list&gt;"
	For q.{=Each {
		WriteLine f,"&lt;{&gt;"
¬		WriteLine f,"    &lt;£&gt;"+q\£+"&lt;/£&gt;"	
		WriteLine f,"&lt;/{&gt;"	
	Next
	WriteLine f,"&lt;/{_list&gt;"
	WriteFile f
	CloseFile (f)
End Function
Function {_add(~)
	q.{=New {
¬	q\}=}
End Function
Function {_find.{(#)
	For n.{=Each {
		If n\#=#
			Return n
		EndIf
	Next
	Return Null
End Function
Function {_count()
	tag=0
	For n.{=Each {
		tag=tag+1
	Next
	Return tag
End Function
Function {_fadd.{(#)
	tag.{=Null
	For n.{=Each {
		If n\#=#
			tag=n
		EndIf
	Next
	If tag=Null
		tag=New {
	EndIf
	Return tag
End Function
Function {_sort()
	Repeat
		swapped=False
		For {.{ = Each {
			afterType.{=After {
			If (afterType&lt;&gt;Null)
				If (afterType\# &lt; {\#)
					Insert afterType Before {
					swapped=True
				EndIf
			EndIf
		Next
	Until Not swapped
End Function
</pre><br><br>("misc.bb")<br><pre class=code>
;
;
;		(edited) Miscellaneous stuff
;
;		(c) K.Hill, June 2001
;			Braneloc@...
;


Const WORDSNUM = 100
Dim split$(WORDSNUM )


Function split_csv(a$)
	;# splits up a line of text into words
	s=1
	For n=1 To WORDSNUM 
		split$(n)=""
	Next
	For n=1 To Len(a$)
		z$=Mid$(a$,n,1)
		If z$=","
			split$(s)=Trim (split$(s))
			s=s+1
		Else
			split$(s)=split$(s)+z$
		EndIf
	Next
	split$(s)=Trim (split$(s))
End Function
</pre><br><br><br><br>Sample CSV file...<br>("database/test.csv")<br><br>Note the first line contains the TYPENAMEs </td></tr><tr ><td class="cell"><pre class="code">name$,email$,comment$
Braneloc,Braneloc@mirex.demon.co.uk,Author of this stuff</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
