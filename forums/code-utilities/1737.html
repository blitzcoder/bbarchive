<!DOCTYPE html><html lang="en" ><head ><title >ProgressStream</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Hook-Functions for Streams, language=bmx, category=File Utilities'><meta name='author' content='Byteemoz'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=8>File Utilities</a>/ProgressStream</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1737.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >ProgressStream by Byteemoz</td><td align="right">2006 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> A simple demo:<br><pre class=code>Import "ProgressStream.bmx"

' get filename
file$ = RequestFile("Open big file...")
If file = "" End

' use the progress stream
file = "progress::" + file

' set the stream hook
SetProgressStreamHook Hook, 500 ' Hook is being called every 500ms

' start message
Print "Loading file..."

' a simple loading function
Local stream:TStream = ReadStream(file)
While Not stream.Eof()
	stream.ReadByte()
Wend
stream.close

' end message
Print "Done"


' hook function
Function Hook(Stream:TStream, Read:Int, Written:Int, Context:Object)
	Local size:Int = Stream.Size()
	If size = 0 Then Print "Progress ??? (" + Read + " Bytes)"
	Local percent = 100.0 * Read / size
	Print "Progress " + percent + "% (" + Read + " Bytes)"
EndFunction</pre> </td></tr><tr ><td class="cell"><pre class="code">' ProgressStream.bmx

SuperStrict

Import BRL.Stream

Type TProgressStream Extends TStreamWrapper

	Field _readcount:Int = 0
	Field _writecount:Int = 0
	Field _lasthook:Int = 0

	Method Close()
		SetStream Null
	End Method

	Function Create:TProgressStream( stream:TStream )
		Local t:TProgressStream = New TProgressStream
		t.SetStream stream
		Return t
	End Function

	Method Read:Int( buf:Byte Ptr,count:Int )
		_readcount:+ count ; Progress()
		Return _stream.Read( buf, count ) 
	End Method

	Method Write:Int( buf:Byte Ptr,count:Int )
		_writecount:+ count ; Progress()
		Return _stream.Write( buf,count )
	End Method
	
	Method ReadByte:Int()
		_readcount:+ 1 ; Progress()
		Return _stream.ReadByte()
	End Method
	
	Method WriteByte( n:Int )
		_writecount:+ 1 ; Progress()
		_stream.WriteByte n
	End Method
	
	Method ReadShort:Int()
		_readcount:+ 2 ; Progress()
		Return _stream.ReadShort()
	End Method
	
	Method WriteShort( n:Int )
		_writecount:+ 2 ; Progress()
		_stream.WriteShort n
	End Method
	
	Method ReadInt:Int()
		_readcount:+ 4 ; Progress()
		Return _stream.ReadInt()
	End Method
	
	Method WriteInt( n:Int )
		_writecount:+ 4 ; Progress()
		_stream.WriteInt n
	End Method
	
	Method ReadFloat:Float()
		_readcount:+ 4 ; Progress()
		Return _stream.ReadFloat()
	End Method
	
	Method WriteFloat( n:Float )
		_writecount:+ 4 ; Progress()
		_stream.WriteFloat n
	End Method
	
	Method ReadDouble:Double()
		_readcount:+ 8 ; Progress()
		Return _stream.ReadDouble()
	End Method
	
	Method WriteDouble( n:Double )
		_writecount:+ 8 ; Progress()
		_stream.WriteDouble n
	End Method
	
	Method ReadLong:Long()
		_readcount:+ 8 ; Progress()
		Return _stream.ReadLong()
	End Method
	
	Method WriteLong( n:Long )
		_writecount:+ 8 ; Progress()
		_stream.WriteLong n
	End Method
	
	Method ReadLine$()
		Local t$ = _stream.ReadLine()
		_readcount:+ t.Length ; Progress()
		Return t$
	End Method
	
	Method WriteLine:Int( t$ )
		_writecount:+ t.Length ; Progress()
		Return _stream.WriteLine( t )
	End Method
	
	Method ReadString$( n:Int )
		_readcount:+ n ; Progress()
		Return _stream.ReadString( n )
	End Method
	
	Method WriteString( t$ )
		_writecount:+ t.Length ; Progress()
		_stream.WriteString t
	End Method
	
	Method Progress()
		If MilliSecs() - _lasthook &lt; ProgressInterval Then Return
		ProgressHook _stream, _readcount, _writecount, ProgressContext
		_lasthook = MilliSecs()
	EndMethod
EndType

Function SetProgressStreamHook(Hook(Stream:TStream, Read:Int, Written:Int, Context:Object), Interval:Int = 100, Context:Object = Null)
	ProgressHook = Hook
	ProgressInterval = Interval
	ProgressContext = Context
EndFunction

Private
	
Type TProgressStreamFactory Extends TStreamFactory

	Method CreateStream:TStream(URL:Object, Proto:String, Path:String, Readable:Int, Writeable:Int)
		If Proto = "progress" Then
			Local stream:TStream=OpenStream(path,readable,writeable)
			If stream Return TProgressStream.Create(stream)
		EndIf
	End Method
End Type

New TProgressStreamFactory

Function NullProgressHook(Stream:TStream, Read:Int, Written:Int, Context:Object) ; EndFunction
Global ProgressHook(Stream:TStream, Read:Int, Written:Int, Context:Object) = NullProgressHook
Global ProgressInterval:Int = 100
Global ProgressContext:Object = Null</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Streaksy</td><td align="right"><font class=tiny>2011</font></td></tr></table></td></tr><tr ><td class="posttext"> woa...<br><br>would this work with things like LoadImage ? <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
