<!DOCTYPE html><html lang="en" ><head ><title >Compile Blitz3D with Blitz3D</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Compile and run source and script files in Blitz3D using Blitzcc.exe, language=bb, category=File Utilities'><meta name='author' content='Spencer'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=8>File Utilities</a>/Compile Blitz3D with Blitz3D</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="3218.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Compile Blitz3D with Blitz3D by Spencer</td><td align="right">2015 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This program stitches  files in a "source" and "script" directory together into a single output.bb file, then invokes blitzcc.exe to compile and run output.bb<br><br><b>Update AUG-5-2015</b> Switched NEW_LN from 10-13 to 13-10 Per RGR's suggestion.<br><br>Here is an example layout of how to use.  The image references "the code above" , but in this case its the code below. The image was a supplement to a Forum discussion regarding BriskVM See link.<br><br><a href="http://www.blitzbasic.co.nz/Community/posts.php?topic=103814" target="_blank">Is BriskVM dead? How do I get hold of?</a><br><br><img src="https://spencerjobe.com/Blitz3D/Blitz3D_Script_Compiler_Example.png"><br><br>I should say that this is merely a proof of concept. If you were distributing a program in this fashion you might want to do a couple of things: <br><br>1) "Scrub your Inputs" - Parse the user's script files and stop if there are any dangerous commands. File I/O is a good example. You don't want the user to read/write to files. So, you will need to parse and analyze the script files and throw an error on unwanted code. This is totally possible, but the code becomes more complicated and it would muddy up this example. <br><br>2) "Protect your Source" - Find a way to obfuscate you source files in the "source" folder.  This way users can't easily alter the Game's source code. There are many different ways to do this, but they were omitted from the example code for clarity. </td></tr><tr ><td class="cell"><pre class="code">Graphics 640, 480, 0, 2
AppTitle "Blitz3D Game Launcher"

Const SCRIPT_FOLDER$ = "C:\MyGameFolder\scripts\"
Const SOURCE_FOLDER$ = "C:\MyGameFolder\source\"
Const OUTPUT_FILEPATH$ = "C:\MyGameFolder\output.bb"

Const BLITZ_PATH_VAR_NAME$ = "BLITZPATH"
Const BLITZ_PATH_VALUE$ = "C:\MyGameFolder\"
Const BLITZ_COMPILER_PATH$ = BLITZ_PATH_VALUE + "bin\compiler.exe"

Global BLITZ_CMD_TEMPLATE$ = Replace("'" + BLITZ_COMPILER_PATH + "' '{PATH}'","'",Chr(34))
Global NEW_LN$ = Chr(13) + Chr(10)


Function CompileAndRun(CodePath$, ScriptPath$, DestPath$)

    Local FullSource$ = ConcatSource(CodePath,ScriptPath)
    Local Command$ = Replace(BLITZ_CMD_TEMPLATE, "{PATH}", DestPath)
        
    WriteAllText(DestPath,FullSource)
    SetEnv(BLITZ_PATH_VAR_NAME, BLITZ_PATH_VALUE)
    Print GetEnv(BLITZ_PATH_VAR_NAME)
    Print Command
    Input "Press ENTER to run..."
    ExecFile(Command)
    End

End Function


Function ConcatSource$(CodePath$, ScriptPath$)

    Local SourceCodeBlob$ = ConcatFiles(CodePath)
    Local ScriptBlob$ = ConcatFiles(ScriptPath)
    Local FullSource$ = ""
    
    FullSource = SourceCodeBlob
    FullSource = FullSource + String(NEW_LN,2)
    FullSource = FullSource + ";" + String("*",80) + NEW_LN
    FullSource = FullSource + "; SCRIPT SOURCE BEGIN" + NEW_LN
    FullSource = FullSource + ";" + String("*",80) + NEW_LN
    FullSource = FullSource + String(NEW_LN,2)
    FullSource = FullSource + ScriptBlob
    
    Return FullSource

End Function


Function ConcatFiles$(Folder$)

    Local DirStream = ReadDir(Folder)
    Local FileName$ = ""
    Local TextBlob$ = ""
    Local FilePath$ = ""
 
    NextFile(DirStream) ; skip .
    NextFile(DirStream) ; skip ..

    Repeat
    
        FileName = NextFile(DirStream)
    
        If FileName = "" Then
    
            Exit
        Else
            FilePath = Folder + FileName    
            TextBlob = TextBlob + ReadAllText(FilePath)
    
        EndIf
    Forever

    CloseDir(DirStream)
    Return TextBlob

End Function


Function ReadAllText$(Path$)
    
    Local Stream = ReadFile(Path)
    Local Contents$ = ""
    
    While Not Eof(Stream)
        Contents = Contents + ReadLine(Stream) + NEW_LN
    Wend
    
    CloseFile(Stream)
    Return Contents
    
End Function


Function WriteAllText(Path$, Contents$)
    
    Local Stream = WriteFile(Path)
    
    WriteLine(Stream,Contents)
    CloseFile(Stream)
    
End Function



CompileAndRun(SOURCE_FOLDER,SCRIPT_FOLDER,OUTPUT_FILEPATH)</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Blitzplotter</td><td align="right"><font class=tiny>2015</font></td></tr></table></td></tr><tr ><td class="posttext"> Very interesting post Spencer, thank you for sharing. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >xlsior</td><td align="right"><font class=tiny>2015</font></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> 2) "Protect your Source" - Find a way to obfuscate you source files in the "source" folder. This way users can't easily alter the Game's source code. There are many different ways to do this, but they were omitted from the example code for clarity. <br></div><br><br>Just be aware that since the compiler will need to be able to read the code from disk in plain text, as -some- point it will exist in human readable form. Even if you encrypt the source, then decrypt-&gt; compile -&gt; delete , a user can still intercept the code, especially if they 'end task' your program during this window and it has no chance to clean up after itself. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Spencer</td><td align="right"><font class=tiny>2015</font></td></tr></table></td></tr><tr ><td class="posttext"> Yep that's correct. Reading the source would be possible. Encoding it in some fashion, perhaps even bin-packing it, would help to ensure that "clean" source is used each time. But, to xlsior's point: There will always be some way to <i>read</i> the source. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Spencer</td><td align="right"><font class=tiny>2015</font></td></tr></table></td></tr><tr ><td class="posttext"> Here's a version that allows you to block certain keywords. The check will fail if a key word appears anywhere in the script. <br><br>These examples will fail<br><pre class=code>
   1:    fs = ReadFile("test.txt")
   2:    a = 400 ;this is a comment ReadFile
   3:    a = "This is a string ReadFile"
</pre><br><br>These examples will pass<br><pre class=code>
     1:   fx = extReadFile("test.txt")
     2:   a = 400 ;we called extReadFile() above
     3:   a = "This is a string _ReadFile" 
</pre><br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 640, 480, 0, 2
AppTitle "Blitz3D Game Launcher Pro"

Const SCRIPT_FOLDER$ = "C:\MyGameFolder\scripts\"
Const SOURCE_FOLDER$ = "C:\MyGameFolder\source\"
Const OUTPUT_FILEPATH$ = "C:\MyGameFolder\output.bb"

Const BLITZ_PATH_VAR_NAME$ = "BLITZPATH"
Const BLITZ_PATH_VALUE$ = "C:\MyGameFolder\"
Const BLITZ_COMPILER_PATH$ = BLITZ_PATH_VALUE + "bin\compiler.exe"

Global BLITZ_CMD_TEMPLATE$ = Replace("'" + BLITZ_COMPILER_PATH + "' '{PATH}'","'",Chr(34))
Global NEW_LN$ = Chr(13) + Chr(10)

Const BLOCK_MAX = 300
Dim BLOCKED$(BLOCK_MAX)
Global BLOCKED_LAST = -1

Function Block(Value$) 
    BLOCKED_LAST = BLOCKED_LAST+ 1
    BLOCKED(BLOCKED_LAST) = Value
End Function

;IF a blocked String appears anywhere in code: THROW ERROR
;**IS NOT Case sensitive.**
Block("Stop")
Block("Extern")
Block("AppTitle")
Block("CommandLine")
Block("SystemProperty")
Block("SetEnv")
Block("GetEnv")
Block("CallDLL")
Block("ExecFile")
Block("OpenFile")
Block("ReadFile")
Block("WriteFile")
Block("CloseFile")
Block("FilePos")
Block("SeekFile")
Block("ReadDir")
Block("CloseDir")
Block("NextFile")
Block("CurrentDir")
Block("ChangeDir")
Block("CreateDir")
Block("DeleteDir")
Block("FileType")
Block("FileSize")
Block("CopyFile")
Block("DeleteFile")
Block("CountHostIPs")
Block("HostIP")
Block("DottedIP")
Block("CopyStream")
Block("OpenTCPStream")
Block("CreateTCPServer")
Block("TCPStreamIP")
Block("TCPStreamPort")
Block("TCPTimeouts")
Block("CreateUDPStream")
Block("SendUDPMsg")
Block("RecvUDPMsg")
Block("UDPStreamIP")
Block("UDPStreamPort")
Block("UDPMsgIP")
Block("UDPMsgPort")
Block("UDPTimeouts")
Block("StartNetGame")
Block("HostNetGame")
Block("JoinNetGame")
Block("StopNetGame")
Block("CreateNetPlayer")
Block("DeleteNetPlayer")
Block("NetPlayerName")
Block("NetPlayerLocal")
Block("RecvNetMsg")
Block("NetMsgType")
Block("NetMsgFrom")
Block("NetMsgTo")
Block("NetMsgData")
Block("SendNetMsg")


;Called from within ConcatSource function
Function CheckScript$(ScriptSource$) 

    Local CheckSource$ = " " + Lower(ScriptSource) + " "
    Local BlockedItem$ = ""
    Local CheckItem$ = ""
    Local Index = 0
    Local BlockedPos = 0
    Local PriorChr$ = ""
    Local NextChr$ = ""
    Local AllowedChars$ = "abcdefghijklmnopqrstuvwxyz01234567890_"
    
    For Index = 0 To BLOCKED_LAST

        BlockedItem = BLOCKED(Index)
        CheckItem = Lower(BlockedItem)
        BlockedPos = Instr(CheckSource,CheckItem)

        If BlockedPos &gt; 0 Then
        
            PriorChr = Mid(CheckSource,BlockedPos-1,1)            
            NextChr = Mid(CheckSource,BlockedPos+Len(BlockedItem),1)
                        
            If Instr(AllowedChars,PriorChr) = 0 Then            
                If Instr(AllowedChars,NextChr) = 0 Then            
                    Print "Script Error"
                    Print ""
                    Print "Script Cannot contain '" + BlockedItem + "'"
                    Print ""
                    Input "Press [ENTER] to exit..."
                    End ;&lt;-- END PROGRAM            
                EndIf        
            EndIf                        
        EndIf
    Next
    
End Function


Function CompileAndRun(CodePath$, ScriptPath$, DestPath$)

    Local FullSource$ = ConcatSource(CodePath,ScriptPath)
    Local Command$ = Replace(BLITZ_CMD_TEMPLATE, "{PATH}", DestPath)
        
    WriteAllText(DestPath,FullSource)
    SetEnv(BLITZ_PATH_VAR_NAME, BLITZ_PATH_VALUE)
    Print GetEnv(BLITZ_PATH_VAR_NAME)
    Print Command
    Input "Press ENTER to run..."
    ExecFile(Command)
    End

End Function


Function ConcatSource$(CodePath$, ScriptPath$)

    Local SourceCodeBlob$ = ConcatFiles(CodePath)
    Local ScriptBlob$ = ConcatFiles(ScriptPath)
    Local FullSource$ = ""

    CheckScript(ScriptBlob)
    
    FullSource = SourceCodeBlob
    FullSource = FullSource + String(NEW_LN,2)
    FullSource = FullSource + ";" + String("*",80) + NEW_LN
    FullSource = FullSource + "; SCRIPT SOURCE BEGIN" + NEW_LN
    FullSource = FullSource + ";" + String("*",80) + NEW_LN
    FullSource = FullSource + String(NEW_LN,2)
    FullSource = FullSource + ScriptBlob
    
    Return FullSource

End Function


Function ConcatFiles$(Folder$)

    Local DirStream = ReadDir(Folder)
    Local FileName$ = ""
    Local TextBlob$ = ""
    Local FilePath$ = ""
 
    NextFile(DirStream) ; skip .
    NextFile(DirStream) ; skip ..

    Repeat
        
        FileName = NextFile(DirStream)
    
        If FileName = "" Then
            Exit
        Else
            FilePath = Folder + FileName    
            TextBlob = TextBlob + ReadAllText(FilePath)
        EndIf

    Forever

    CloseDir(DirStream)
    Return TextBlob

End Function


Function ReadAllText$(Path$)
    
    Local Stream = ReadFile(Path)
    Local Contents$ = ""
    
    While Not Eof(Stream)
        Contents = Contents + ReadLine(Stream) + NEW_LN
    Wend
    
    CloseFile(Stream)
    Return Contents
    
End Function


Function WriteAllText(Path$, Contents$)
    
    Local Stream = WriteFile(Path)

    WriteLine(Stream,Contents)
    CloseFile(Stream)
    
End Function



CompileAndRun(SOURCE_FOLDER,SCRIPT_FOLDER,OUTPUT_FILEPATH)
</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> Block("Extern")<br><br>What is this? <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
