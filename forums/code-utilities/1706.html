<!DOCTYPE html><html lang="en" ><head ><title >.decls Converter</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Convert .decls files to BlitzMax sourcecode, language=bb, category=File Utilities'><meta name='author' content='N'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=8>File Utilities</a>/.decls Converter</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1706.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >.decls Converter by N</td><td align="right">2006 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Basically, this is an application to convert Blitz3D/Plus .decls files to useable BlitzMax source code.<br><br>There is an optional flag you can pass to it -- '/u'.  This sets it up so that it calls LoadLibraryW instead of LoadLibraryA.  By default it is not enabled since Pub.Win32 doesn't have it declared.  This is by and large dependent on your code, which is why it isn't enabled by default.<br><br>To use it you just compile it and drag decls files onto it.  Simple as that, nothing hard. </td></tr><tr ><td class="cell"><pre class="code">SuperStrict

Framework Brl.Blitz
Import Brl.Filesystem
Import Brl.Stream
Import Brl.LinkedList

Global libs:TList = New TList
Global mode$="A"

For Local i:Int = 1 To AppArgs.Length-1
    If AppArgs[i].ToLower( ) = "/u" Then
        AppArgs[i] = ""
        mode = "W"
    EndIf
Next

For Local i:Int = 1 To AppArgs.Length-1
    If AppArgs[i].Length = 0 Then Continue
    ProcessDecl( AppArgs[i] )
Next

End

Type TFunc
    Field sym$
    Field decl$
    
    Method Format$( s$ )
        Return s.Replace( "$(SYM)", sym )..
                .Replace( "$(DECL)", decl )
    End Method
    
    Function Create:TFunc( decl$, symbol$ )
        Local f:TFunc = New TFunc
        f.sym = symbol
        f.decl = decl
        Return f
    End Function
End Type

Type TLib
    Method New( )
        link = libs.AddLast( Self )
        funcs = New TList
    End Method
    
    Method Dispose( )
        funcs.Clear( )
        funcs = Null
        link.Remove( )
        link = Null
        lib = Null
    End Method
    
    Field lib$
    Field funcs:TList
    Field link:TLink
End Type

Function ProcessDecl( declPath$ )
    Local s:TStream = ReadStream( declPath )
    Local lib:TLib
    While Not s.Eof( )
        Local l$ = s.ReadLine( ).Trim( )
        
        Local cf% = l.Find( ";" )
        If cf &lt;&gt; -1 Then l = l[..cf].Trim( )
        
        If l.Length = 0 Then Continue
        
        If l[0] = 46 Then ' Set lib
            If lib Then
                If lib.funcs.Count( ) = 0 Or lib.lib = "." Then
                    lib.Dispose( )
                    lib = Null
                EndIf
            EndIf
            
            Local qb% = l.Find( "~q" )+1 ' Quote begin
            If qb = 0 then Continue
            Local qe% = l.Find( "~q", qb ) ' Quote end
            If qe = -1 Then Continue
            
            lib = New TLib
            lib.lib = l[qb..qe]
        ElseIf lib Then ' Parse function
            Local s$[] = SplitString( l, ":~q" )
            lib.funcs.AddLast( TFunc.Create( s[0].Replace("*","@ Ptr").Replace("$","$z"), s[1] ) )
        EndIf
    Wend
    s.Close( )
    lib = Null
    
    s = WriteStream( StripExt(declPath)+".bmx" )
    
    s.WriteLine( "Strict" )
    s.WriteLine( "Import Pub.Win32" )
    s.WriteLine( "" )
    s.WriteLine( "Private" )
    s.WriteLine( "Local lib%" )
    s.WriteLine( "" )
    s.WriteLine( "Public" )
    
    For lib = EachIn libs
        s.WriteLine( "" )
        s.WriteLine( "lib = LoadLibrary"+mode+"(~q"+lib.lib+"~q)" )
        s.WriteLine( "" )
        For Local f:TFunc = EachIn lib.funcs
            s.WriteLine( f.Format( "Global $(DECL) = GetProcAddress(lib, ~q$(SYM)~q)" ) )
        Next
        s.WriteLine( "" )
        s.WriteLine( "lib = 0" )
    Next
    
    s.Close( )
    s = Null
    
    ClearLibs( )
End Function

Function ClearLibs( )
    For Local i:TLib = EachIn libs
        i.Dispose( )
    Next
    libs.Clear( )
End Function

Function SplitString$[]( s$, sp$ )
    Local p:Int, l:Int, o$[32], x%
    Local n:Int
    For n = 0 To s.Length - 1
        Local lx% = x
        For p = 0 To sp.Length - 1
            If s[n] = sp[p] Then
                If x = o.Length Then o = o[..o.Length*2]
                o[x] = s[l..n+(n = s.Length-1 And (Not (s[n]=sp[p])))].Trim( )
                l = n+1
                If o[x].Length=0 Then Exit
                x :+ 1
                Exit
            EndIf
        Next
        If x &lt;&gt; lx Then Continue
        If n = s.Length-1 Then
            If x = o.Length Then o = o[..o.Length*2]
            o[x] = s[l..n+(n = s.Length-1 And (Not (s[n]=sp[p])))].Trim( )
            If o[x].Length = 0 Then Exit
            x :+ 1
        EndIf
    Next
    If x = 0 Then Return Null
    Return o[0..x]
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Picklesworth</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> Excellent.<br>Thanks, Noel.<br>I was hoping for some way to get back all those old Decls files! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Blueapples</td><td align="right"><font class=tiny>2007</font></td></tr></table></td></tr><tr ><td class="posttext"> Banned or not, Noel, thank you so much - this is exactly what I've been trying to find forever. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
