<!DOCTYPE html><html lang="en" ><head ><title >Recursive CRC-32 file builder</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='CRC-32 file builder, language=bb, category=File Utilities'><meta name='author' content='ozak'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=8>File Utilities</a>/Recursive CRC-32 file builder</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1457.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Recursive CRC-32 file builder by ozak</td><td align="right">2005 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Takes a directory and recursively calculates crc-32 for all files and stores it in a file.<br>Great for online game patchers. Simply use the tool to build a list, and your patcher can download patchlist.txt and see if the CRC of any game files have changed and if so, download the new one.<br><br>NOTE: It's hardcoded to the path the exe file is in, and it skips patchlist.txt and buildcrc.exe due to obvious reasons :) </td></tr><tr ><td class="cell"><pre class="code">; Recursive directory list CRC-32 builder by Odin Jensen (www.furi.dk)

; Setup CRC stuff
Dim CRC32_table(255)
CRC32_init()

Global outfile = WriteFile("patchinfo.dat")
Print("CRC-32 list builder by Odin Jensen")
Print("----------------------------------")
Print("")

BuildList("./")

CloseFile(outfile)

Print("")
Print("Done.")

; Build list function
Function BuildList(From_$) 
  Local MyDir, MyFile$ 

  MyDir = ReadDir(From_$) 
  MyFile$ = NextFile$(MyDir) 
  MyFile$ = NextFile$(MyDir) 
  Repeat 
    MyFile$ = NextFile$(MyDir) 
    If MyFile$ = "" Then Exit 

    If FileType(From_$ + "\" + MyFile$) = 2 Then 
      BuildList(From_$ + "\" + MyFile$) 
    Else 
	  temp$ = Replace(From_$ + "\" + MyFile$, "./\", "")

	  If ((Not Right(temp, 13) = "patchinfo.dat") And (Not Right(temp, 12) = "buildcrc.exe"))
	  
		  crc32% = CRC32_FromFile(temp)
	      WriteLine(outfile, temp + "," + Str(Hex(crc32)))
		  Print("&gt; CRC32 for file '" + temp + "' is " + Str(Hex(crc32)))
	  End If

    End If 
  Forever 
  CloseDir(MyDir)  
End Function


; CRC init function
Function CRC32_Init()
  Local i
  Local j
  Local value

  For i=0 To 255
    value=i
    For j=0 To 7
      If (value And $1) Then 
        value=(value Shr 1) Xor $EDB88320
      Else
        value=(value Shr 1)
      EndIf
    Next
    CRC32_table(i)=value
  Next
End Function

; Function to get CRC-32 value from a file
Function CRC32_FromFile(name$)
  Local byte
  Local crc
  Local file

  crc=$FFFFFFFF
  file=ReadFile(name$)
  If file=0 Then Return
  While Not Eof(file)
    byte=ReadByte(file)
    crc=(crc Shr 8) Xor CRC32_table(byte Xor (crc And $FF))
  Wend
  Return ~crc
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ckob</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> it seems that after so many files the CRC starts spitting out all 0's <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ckob</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> Also here's this same thing working in Blitzmax<br><br><pre class=code>


Global CRC32_table[256]
CRC32_init()

Global outfile
outfile = WriteFile("patchinfo.dat")

BuildList(dir$)
CloseFile outfile


' Build list Function
Function BuildList(From_$) 
  Local MyDir, MyFile$ 

  MyDir = ReadDir(From_$) 
  MyFile$ = NextFile$(MyDir) 
  MyFile$ = NextFile$(MyDir) 
Repeat
	
    MyFile$ = NextFile$(MyDir) 

    If MyFile$ = "" Then Exit 

    If FileType(From_$ + "\" + MyFile$) = 2 Then 
      BuildList(From_$ + "\" + MyFile$) 
    Else 
	  temp$ = Replace(From_$ + "\" + MyFile$, "./\", "")

		  crc32% = CRC32_FromFile(temp)

		  Print "&gt; CRC32 for file '" + temp + "' is " + String(Hex(crc32))
			      WriteLine outfile, temp + "," + String(Hex(crc32))

	End If 
	
If KeyHit(KEY_ESCAPE) Then
	End
EndIf		
	
	
	
Forever 

  CloseDir(MyDir) 
 

End Function









' CRC init Function
Function CRC32_Init()
  Local i
  Local j
  Local value

  For i=0 To 255
    value=i
    For j=0 To 7
      If (value And $1) Then 
        value=(value Shr 1) ~ $EDB88320
      Else
        value=(value Shr 1)
      EndIf
    Next
    CRC32_table[i]=value
  Next
End Function

' Function To get CRC-32 value from a file
Function CRC32_FromFile(name$)
  Local Bytes
  Local crc
  Local file

  crc=$FFFFFFFF
  file=ReadFile(name$)
  If file=0 Then Return
  While Not Eof(file) Or KeyHit(1)
    Bytes=ReadByte(file)
    crc=(crc Shr 8) ~ CRC32_table(Bytes ~ (crc And $FF))
  Wend
  Return ~crc
End Function

</pre> <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
