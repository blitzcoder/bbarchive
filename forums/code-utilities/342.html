<!DOCTYPE html><html lang="en" ><head ><title >Batch Convert .3ds &amp; .x to .b3d</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Batch convert .3ds &amp;amp; .x files in a dir to .b3d, language=bb, category=File Utilities'><meta name='author' content='elias_t'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=8>File Utilities</a>/Batch Convert .3ds &amp; .x to .b3d</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="342.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Batch Convert .3ds &amp; .x to .b3d by elias_t</td><td align="right">2002 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> The only extra information retrieved is uv-mapping coords.<br><br>But it's very usefull to convert all 3ds and x models in a directory to b3d. </td></tr><tr ><td class="cell"><pre class="code">;batch convert .3ds and .x files in a directory to .b3d files
;modified Mark's code

;---------------------
Dim b3d_stack(100)
Global b3d_file,b3d_tos

;---------------------

Graphics3D 640,480,32,2


;/////////////////////////////////
rd=ReadDir(CurrentDir$())

tm$=NextFile$(rd)

While Not tm$=&quot;&quot;

If Right$(tm$,4)=&quot;.3ds&quot; Or Right$(tm$,4)=&quot;.3DS&quot; Or Right$(tm$,2)=&quot;.x&quot; Or Right$(tm$,2)=&quot;.X&quot;

mesh = LoadMesh  (tm$) 

If Right$(tm$,4)=&quot;.3ds&quot; Or Right$(tm$,4)=&quot;.3DS&quot; Then tm$=Left$(tm$,Len(tm$)-4)
If Right$(tm$,2)=&quot;.x&quot; Or Right$(tm$,2)=&quot;.X&quot; Then tm$=Left$(tm$,Len(tm$)-2)

WriteBB3D( tm$+&quot;.b3d&quot;,mesh )

FreeEntity mesh

Print tm$ + &quot;  Ok&quot;

EndIf

tm$=NextFile$(rd)
Wend


CloseDir rd

End


;/////////////////////////////////////////////////////////////////////////
;////////////////////////////////////////////////////////////////////////


Function WriteBB3D( f_name$,mesh )

	file=WriteFile( f_name$ )

	b3dSetFile( file )
	
	b3dBeginChunk( &quot;BB3D&quot; )
		b3dWriteInt( 1 )	;version
		
		b3dBeginChunk( &quot;BRUS&quot; )
			b3dWriteInt( 0 )					;0 textures per brush
			b3dWriteString( &quot;Brush&quot; )			;brush name
			b3dWriteFloat( 1 )					;red
			b3dWriteFloat( 1 )					;green
			b3dWriteFloat( 1 )					;blue
			b3dWriteFloat( 1 )					;alpha
			b3dWriteFloat( 0 )					;shininess
			b3dWriteInt( 1 )					;blend
			b3dWriteInt( 0 )					;FX
		b3dEndChunk()	;end of BRUS chunk
		
		b3dBeginChunk( &quot;NODE&quot; )
			b3dWriteString( &quot;_&quot; );entity name
			b3dWriteFloat( 0 )	;x_pos
			b3dWriteFloat( 0 )	;y_pos
			b3dWriteFloat( 0 )	;y_pos
			b3dWriteFloat( 1 )	;x_scale
			b3dWriteFloat( 1 )	;y_scale
			b3dWriteFloat( 1 )	;z_scale
			b3dWriteFloat( 1 )	;rot_w
			b3dWriteFloat( 0 )	;rot_x
			b3dWriteFloat( 0 )	;rot_y
			b3dWriteFloat( 0 )	;rot_z
			WriteMESH( mesh )
		b3dEndChunk()	;end of NODE chunk
		
	b3dEndChunk()	;end of BB3D chunk
	
	CloseFile file
End Function

Function WriteMESH( mesh )

	n_surfs=CountSurfaces( mesh )
	
	b3dBeginChunk( &quot;MESH&quot; )
		b3dWriteInt( -1 )				;no 'entity' brush
		
		b3dBeginChunk( &quot;VRTS&quot; )
			b3dWriteInt( 0 )			;flags - 0=no normal/color
			b3dWriteInt( 1 )			;1 tex_coord sets
			b3dWriteInt( 2 )			;2 coords per set
			
			For k=1 To n_surfs
				surf=GetSurface( mesh,k )
				n_verts=CountVertices( surf )-1
				
				For j=0 To n_verts
					b3dWriteFloat( VertexX( surf,j ) )
					b3dWriteFloat( VertexY( surf,j ) )
					b3dWriteFloat( VertexZ( surf,j ) )
					
					b3dWriteFloat( VertexU#( surf,j ) )
					b3dWriteFloat( VertexV#( surf,j ) )
					
				Next
			Next
		b3dEndChunk()	;end of VRTS chunk
		
		first_vert=0
		For k=1 To n_surfs
			surf=GetSurface( mesh,k )
			n_tris=CountTriangles( surf )-1
			
			b3dBeginChunk( &quot;TRIS&quot; )
				b3dWriteInt( 0 )		;brush for these triangles
				
				For j=0 To n_tris
					b3dWriteInt( first_vert+TriangleVertex( surf,j,0 ) )
					b3dWriteInt( first_vert+TriangleVertex( surf,j,1 ) )
					b3dWriteInt( first_vert+TriangleVertex( surf,j,2 ) )
				Next
				
			b3dEndChunk()	;end of TRIS chunk
			
			first_vert=first_vert+CountVertices( surf )
			
		Next
		
	b3dEndChunk()	;end of MESH chunk
	
End Function


;////////////////////////////////////////////////////////////////////////

;***** Functions for writing to B3D files *****

Function b3dWriteByte( n )
	WriteByte( b3d_file,n )
End Function

Function b3dWriteInt( n )
	WriteInt( b3d_file,n )
End Function

Function b3dWriteFloat( n# )
	WriteFloat( b3d_file,n )
End Function

Function b3dWriteString( t$ )
	For k=1 To Len( t$ )
		ch=Asc(Mid$(t$,k,1))
		b3dWriteByte(ch)
		If ch=0 Return
	Next
	b3dWriteByte( 0 )
End Function

Function b3dBeginChunk( tag$ )
	b3d_tos=b3d_tos+1
	For k=1 To 4
		b3dWriteByte(Asc(Mid$( tag$,k,1 )))
	Next
	b3dWriteInt( 0 )
	b3d_stack(b3d_tos)=FilePos( b3d_file )
End Function

Function b3dEndChunk()
	n=FilePos( b3d_file )
	SeekFile b3d_file,b3d_stack(b3d_tos)-4
	b3dWriteInt( n-b3d_stack(b3d_tos) )
	SeekFile b3d_file,n
	b3d_tos=b3d_tos-1
End Function

;------------------
Function b3dSetFile( file )
	b3d_tos=0
	b3d_file=file
End Function
;----------------------------------</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Prym</td><td align="right"><font class=tiny>2014</font></td></tr></table></td></tr><tr ><td class="posttext"> thank  you , <br>but how to use with textures ? textures in brushes ? <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
