<!DOCTYPE html><html lang="en" ><head ><title >File-Function collection</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='some function to deal with files and folders, language=bb, category=File Utilities'><meta name='author' content='Klaas'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=8>File Utilities</a>/File-Function collection</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="934.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >File-Function collection by Klaas</td><td align="right">2004 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> a small collection of file function i often use in my programms ... well, maybe someon is interessted </td></tr><tr ><td class="cell"><pre class="code">;some functions to deal with files and folders
;i use arrays for easy access, but can be changed to types easily
;
;hope that helps someone with this anoying recursive folder stuff


Dim xff_splitresult$(100)
Dim ff_results$(100)

;----------------------------------------- Functions
;ff_is_dir(dirname$)
;returns true if dirname$ is a existing directory

;------
;ff_is_file(filename$)
;returns true if filename$ is a existing file


;------
;ff_searchFile(name$,path$)
;name$ = searchpattern like "*.jpg" or "*metal*.png" ... "*" is the wildcard
;path$ = the path to search in
;returns the number of matches .. matches can then be found in "ff_results" array

;------
;ff_pathname$(filename$)
;returns the file path ... "D:\progs\file.jpg" -&gt; "D:\progs"

;------
;ff_filename$(filename$)
;returns the file name ... "D:\progs\file.jpg" -&gt; "file.jpg"

;------
;ff_dirname$(filename$)
;returns the directory name ... "D:\progs\file.jpg" -&gt; "\progs"

;------
;ff_volumename$(filename$)
;returns the volume name ... "D:\progs\file.jpg" -&gt; "D:"

;------
;ff_copy(file$,new_file$,[overwrite],[move])
;file = original file
;new_file = file to copy to
;overwrite = set to true if you wish to overwrite an existing file
;move = set to true if you wish to move the original file
;returns true on success

;------
;ff_mkpath(path$)
;creates a given filepath
;path = path to create ... "D:\progs\things\foo" .. if "D:\progs" exists then "D:\progs\things" and ""D:\progs\things\foo" wil be created
;returns true on success

;------
;ff_smartcopy(file$,new_file$,[overwrite],[move])
;file = original file or folder
;new_file = file or folder to copy to
;overwrite = set to true if you wish to overwrite an existing file's
;move = set to true if you wish to move the original file's
;returns true on success
;
;this function can copy whole directory trees with all files in it

;------
;ff_unlink(file$)
;file = file or folder to delete
;returns true on success
;
;deletes files or whole directory structures

;------

;file functions
Function ff_is_dir(folder$)
	If FileType(folder$) = 2 Then Return True
End Function

Function ff_is_file(file$)
	If FileType(file$) = 1 Then Return True
End Function

Function ff_searchFile(name$,path$,count=0)
	comp = xff_split("*",name$)

	dir = ReadDir(path$)
	If Not dir Then Return False	
	entry$ = NextFile(dir)

	While (entry$ &lt;&gt; "")
		If entry$ &lt;&gt; "." And entry$ &lt;&gt; ".."
			full_entry$ = path$+"\"+entry$
			
			Select FileType(full_entry$)
			Case 0
				Return False
			Case 1
				result$ = full_entry
				found = True
				pos = 1
				For i=0 To comp - 1
					If Not xff_splitresult(i) = ""
						pos = Instr(entry$,xff_splitresult(i),pos)
						If Not pos
							found = False
							Exit
						EndIf
					EndIf
				Next
				If found
					ff_results(count) = path$+"\"+entry
					count = count + 1
				EndIf
			Case 2
				count = ff_searchFile(name$,full_entry$,count)
			End Select
		End If
		entry$ = NextFile(dir)
	Wend
	Return count
End Function

Function ff_pathname$(txt$)
	txt$ = Replace(txt$,"/","\")
	length = xff_split("\",txt$)
	
	dir$ = xff_splitresult(0)
	For i=1 To length-2
		dir$ = dir$ + "\" + xff_splitresult(i)
	Next
	
	Return dir$
End Function

Function ff_filename$(txt$)
	txt$ = Replace(txt$,"/","\")
	length = xff_split("\",txt$)
	
	file$ = xff_splitresult(0)
	For i=0 To length - 1
		file$ = xff_splitresult(i)
	Next
	
	Return file$
End Function

Function ff_dirname$(txt$)
	txt$ = Replace(txt$,"/","\")
	xff_split(":\",txt$)

	txt$ = xff_splitresult(1)
	length = xff_split("\",txt$)
	
	For i=0 To length-2
		dir$ = dir$ + "\" + xff_splitresult(i)
	Next
	
	Return dir$
End Function

Function ff_volumename$(txt$)
	length=xff_split(":",txt$)
	
	Return xff_splitresult(0)
End Function

Function ff_mkpath(path$)
	path$=Replace(path$,"/","\")
	length=xff_split("\",path$)
	
	path_by_now$=xff_splitresult(0)
	For i=0 To length-1
		If Not FileType(path_by_now$) = 2 
			CreateDir(path_by_now$)
		End If
		If Not FileType(path_by_now$) Then Return False
		path_by_now$ = path_by_now$+"/"+xff_splitresult(i+1)
	Next
	Return True
End Function

Function ff_unlink(file$)
	Select FileType(file$)
	Case 0
		Return False
	Case 1
		DeleteFile(file$)
		If FileType(file$) &lt;&gt; 0 Then Return False
	Case 2
		dir = ReadDir(file$)
		entry$ = NextFile(dir)
		While (entry$ &lt;&gt; "")
			If entry$ &lt;&gt; "." And entry$ &lt;&gt; ".."
				full_entry$ = file$+"\"+entry$
				Select FileType(full_entry$)
				Case 0
				Case 1
					If Not ff_unlink(full_entry$) Then Return False
				Case 2
					If Not ff_unlink(full_entry$) Then Return False
				End Select
			End If
			entry$=NextFile(dir)
		Wend
		DeleteDir(file$)
	End Select
	Return True
End Function

Function ff_copy(file$,new_file$,overwrite=0,move=0)
	If FileType(file$) = 2 Then Return False
	
	If Not overwrite
		If FileType(new_file$) Return False
	End If

	CopyFile(file$,new_file$)	
	If FileType(new_file$) &lt;&gt; 1 Return False	
	
	If move
		DeleteFile(file$)
	End If
	Return True	
End Function

Function ff_smartcopy(file$,new_file$,overwrite=0,move=0)
	If Not overwrite
		If FileType(new_file$)=1 Return False
	End If

	Select FileType(file$)
	Case 0
		Return False
	Case 1
		dir$ = ff_pathname$(new_file$)
		
		If Not ff_is_dir(dir$)
			If Not ff_mkpath(dir$) Then Return False
		End If

		CopyFile(file$,new_file$)
		If Not ff_is_file(new_file$) Then Return False
		If move Then DeleteFile(file$)
	Case 2
		ff_mkpath(new_file$)
		
		dir = ReadDir(file$)
		entry$ = NextFile(dir)
		While (entry$ &lt;&gt; "")
			If entry$ &lt;&gt; "." And entry$ &lt;&gt; ".."
				full_entry$ = file$+"\"+entry$
				Select FileType(full_entry$)
				Case 0
				Case 1
					If Not ff_smartcopy(full_entry$,new_file$+"\"+entry$,overwrite,move) Then Return False
				Case 2
					If Not ff_smartcopy(full_entry$,new_file$+"\"+entry$,overwrite,move) Then Return False
				End Select
			End If
			entry$=NextFile(dir)
		Wend
		If move Then DeleteDir(file$)
	End Select
	Return True
End Function

;help functions
Function xff_split(seperator$,txt$)
	pos=Instr(txt$,seperator$,1)	
	count = 1
	While (pos)
		xff_splitresult(count-1) = Left(txt$,pos-Len(seperator))
		
		txt$=Right(txt$,Len(txt$)-pos-Len(seperator)+1)
		pos=Instr(txt$,seperator$,1)
		count=count+1
	Wend
	xff_splitresult(count-1)=txt$
	
	Return count
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >gman</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> works well.  some additional functions for this library:<br><br>ff_juststem - returns the just the filename without the extension<br>ff_justext - returns just the file extension<br>ff_addbs - adds a backslash to a path if needed<br>ff_forceext - forces the file extension To be the extension passed<br>ff_forcepath - forces the path of a file to a different path<br><br><pre class=code>
; ================================
; Author: gman
; Date: 2004_08_18
; Title: ff_juststem
; Description: - returns the just the filename without the extension
; Requirements: - File-Function collection by Klaas (ID: 934)
; ================================
Function ff_juststem$(file$)
	file$=ff_filename(file$)
	xff_split(".",file$)
	Return xff_splitresult(0)
End Function 

; ================================
; Author: gman
; Date: 2004_08_18
; Title: ff_justext
; Description: - returns just the file extension
; Requirements: - File-Function collection by Klaas (ID: 934)
; ================================
Function ff_justext$(file$)
	file$=ff_filename(file$)
	xff_split(".",file$)
	Return xff_splitresult(1)
End Function

; ================================
; Author: gman
; Date: 2004_08_18
; Title: ff_addbs
; Description: - adds a backslash to a path if needed
; Requirements: - File-Function collection by Klaas (ID: 934)
; ================================
Function ff_addbs$(path$)
	path$=Trim(path$)
	If Right(path$,1)&lt;&gt;"\"
		path$=path$+"\"
	EndIf
	Return path$
End Function

; ================================
; Author: gman
; Date: 2004_08_18
; Title: ff_forceext
; Description: - forces the file extension To be the extension passed
; Requirements: 
;		- File-Function collection by Klaas (ID: 934)
;		- ff_addbs(), ff_juststem()
; ================================
Function ff_forceext$(file$,ext$)
	path$=ff_addbs(ff_pathname(file$))
	file$=ff_juststem(file$)
	If file$&lt;&gt;""
		file$=file$+"."+ext$
	EndIf
	Return path$+file$
End Function

; ================================
; Author: gman
; Date: 2004_08_18
; Title: ff_forcepath
; Description: - forces the path of a file to a different path
; Requirements:
;		- File-Function collection by Klaas (ID: 934)
;		- ff_addbs()
; ================================
Function ff_forcepath$(file$,path$)
	path$=ff_addbs(path$)
	file$=ff_filename(file$)	
	Return path$+file$
End Function
</pre> <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
