<!DOCTYPE html><html lang="en" ><head ><title >Basic Game Ini File</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Basic Game Ini File, language=bb, category=File Utilities'><meta name='author' content='Grey Alien'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=8>File Utilities</a>/Basic Game Ini File</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1667.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Basic Game Ini File by Grey Alien</td><td align="right">2006 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This is my Ini file code from Easter Bonus.  You can change the parameters as you see fit but the basic concept is an easy way to store values in a file that you can edit in notepad. </td></tr><tr ><td class="cell"><pre class="code">;Ini Variables			
	Global IniMusic = 1
	Global IniSound = 1
	Global IniMusicVolume# = 100 ;in percent
	Global IniSoundVolume# = 100 ;in percent
	Global IniFade = 1
	Global IniFlipFalse = 0
	Global Ini16BitGraphics = 0
	Global IniWindowedMode = 1
	Global IniLockKeys = 0
	Global IniGrid = 1 ;always on
	Global IniAlphaBlend = 0 ;always off
	Global IniParticles# = 80
	Global IniInstructionsAtStart = 1	
	Global IniShowPortalSplash = 0
	
; -----------------------------------------------------------------------------
; Read Ini File
; -----------------------------------------------------------------------------
Function IniFileRead(filename$)
	;load the ini file settings into global variables
	ThePath$ = filename + ".ini"
	Local ini = ccLoadFile(ThePath)
	Local l$
	Local flag$
	Local Value$
	
	While Not Eof(ini)
		l$ = ReadLine(ini)			
		flag$ = Upper(ccIniFirstString(l$))
		Value$ = ccIniLastString(l$)
		num% = Int(Value)
	
		Select True
			Case flag$ = "MUSIC"
				IniMusic = num				
			Case flag$ = "SOUND"
				IniSound = num				
			Case flag$ = "MUSIC VOLUME"
				IniMusicVolume = num				
			Case flag$ = "SOUND VOLUME"
				IniSoundVolume = num				
			Case flag$ = "FADE"
				IniFade = num
			Case flag$ = "FLIP FALSE"
				IniFlipFalse = num
			Case flag$ = "16BIT GRAPHICS"
				Ini16BitGraphics = num
			Case flag$ = "WINDOWED MODE"
				IniWindowedMode = num
			Case flag$ = "PARTICLES"
				IniParticles = num	
			Case flag$ = "INSTRUCTIONS AT START"
				IniInstructionsAtStart = num	
			Case flag$ = "SHOW PORTAL SPLASH"
				IniShowPortalSplash = num				
			Default
		End Select		
	Wend
	CloseFile(ini)
End Function

; -----------------------------------------------------------------------------
; Write Ini File
; -----------------------------------------------------------------------------
Function IniFileWrite(filename$)
	;write the ini file settings from global variables
	ThePath$ = filename + ".ini"
	Local ini = WriteFile(ThePath)
	Local l$
	Local flag$
	Local Value$
	
	ccWriteIniNumber(ini, "MUSIC", IniMusic)
	ccWriteIniNumber(ini, "SOUND", IniSound)
	ccWriteIniNumber(ini, "MUSIC VOLUME", IniMusicVolume)
	ccWriteIniNumber(ini, "SOUND VOLUME", IniSoundVolume)
	ccWriteIniNumber(ini, "FADE", IniFade)
	ccWriteIniNumber(ini, "FLIP FALSE", IniFlipFalse)
	ccWriteIniNumber(ini, "16BIT GRAPHICS", Ini16BitGraphics)
	ccWriteIniNumber(ini, "WINDOWED MODE", IniWindowedMode)
	ccWriteIniNumber(ini, "PARTICLES", IniParticles)
	ccWriteIniNumber(ini, "INSTRUCTIONS AT START", IniInstructionsAtStart)
	ccWriteIniNumber(ini, "SHOW PORTAL SPLASH", IniShowPortalSplash)
	CloseFile(ini)
End Function


; -----------------------------------------------------------------------------
; Load a file and show error if not found
; -----------------------------------------------------------------------------
Function ccLoadFile% (ThePath$)
	pointer = ReadFile(ThePath$)
	If Not pointer Then
    	RuntimeError ("Error loading file "+ThePath$)
		End
	Else
    	Return Pointer	
  	EndIf
End Function

; -----------------------------------------------------------------------------
; IniFirst String (return first part of string up to = sign)
; -----------------------------------------------------------------------------
Function ccIniFirstString$(s$)
	;pass in a string, this will only return the first part up to, but not including, the = sign (or end)		
	Return ccFirstStringToSub(s$, "=")
End Function

; -----------------------------------------------------------------------------
; IniLast String (return last part of string from = sign)
; -----------------------------------------------------------------------------
Function ccIniLastString$(s$)
	;pass in a string, this will only return the last part from, but not including, the = sign
	Return ccLastStringToSub(s$, "=")
End Function

; -----------------------------------------------------------------------------
; ccWriteIniNumber
; -----------------------------------------------------------------------------
Function ccWriteIniNumber(ini%, flag$, value%)
	;use this to write flag=number to an ini file
	WriteLine(ini, Upper(flag)+"="+Str(value)) 
End Function

; -----------------------------------------------------------------------------
; ccWriteIniString
; -----------------------------------------------------------------------------
Function ccWriteIniString(ini%, flag$, value$)
	;use this to write flag=string to an ini file
	WriteLine(ini, Upper(flag)+"="+value$) 
End Function

; -----------------------------------------------------------------------------
; First String To Sub (return first part of string up to Substring)
; -----------------------------------------------------------------------------
Function ccFirstStringToSub$(s$, sub$)
	;pass in a string, this will only return the first part up to, but not including, the substring (or end)
	pos% = Instr(s$, sub$)
	;If pos = 0 then then end of the was reached, so return the whole thing.
	If pos = 0 Then
		Return s$
	Else
		Return Mid(s$, 1, pos-1)
	EndIf
End Function

; -----------------------------------------------------------------------------
; Last String To Sub (return last part of string from substring)
; -----------------------------------------------------------------------------
Function ccLastStringToSub$(s$, sub$)
	;pass in a string, this will only return the last part from, but not including, the substring
	pos% = Instr(s$, sub$)
	;If pos = 0 then then end of the was reached, so return nothing
	If pos = 0 Then
		Return ""
	Else
		Return Mid(s$, pos + Len(sub$), Len(s$)-pos)
	EndIf
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
