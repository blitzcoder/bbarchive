<!DOCTYPE html><html lang="en" ><head ><title >Pack file class</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Easy to use file packer, language=bmx, category=File Utilities'><meta name='author' content='JoshK'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=8>File Utilities</a>/Pack file class</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2559.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pack file class by JoshK</td><td align="right">2009 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> By itself, this offers little or no protection.  However it can be combined with any kind of compression and encryption routines by using bank streams, as the advanced example shows.  Customize to your liking. </td></tr><tr ><td class="cell"><pre class="code">SuperStrict

Framework brl.bankstream
Import brl.filesystem
Import brl.standardio
'Import "bank-utilities.bmx" 'http://blitzmax.com/codearcs/codearcs_bmx/2558.bmx

Local pak:TPackFile


'Simple example
pak=New TPackFile
pak.AddFile("packfile.bmx","testfile.txt")
pak.Save("data.pak")

pak=TPackFile.Load("data.pak")
Print pak.unpack()

Rem
'Advanced example (requires bank-utilities.bmx)
Const ENCRYPTIONKEY:String="BlitzMax is the best programming language in the world!"

pak=New TPackFile

'Pack the source code of this program
pak.AddFile("packfile.bmx")

Local bank:TBank=New TBank
Local bankstream:TBankStream=CreateBankStream(bank)
pak.save(bankstream)
bankstream.close()
bank=CompressBank(bank)
EncryptBank(bank,ENCRYPTIONKEY)
SaveBank(bank,"data.pak")

bank=LoadBank("data.pak")
DecryptBank(bank,ENCRYPTIONKEY)
bank=DecompressBank(bank)
bankstream=CreateBankStream(bank)
pak=TPackFile.Load(bankstream)
Print pak.unpack("unpack\test\stuff")
EndRem

SuperStrict

Import brl.bankstream
Import brl.filesystem

Type TPackFile
	
	Field filecount:Int
	Field filename:String[]
	Field filebank:TBank[]
	
	Method AddFile:Int(src:String,dst:String="")
		Local bank:TBank

		If Not dst dst=src
		dst=dst.Replace("\","/")
		If Chr(dst[0])+Chr(dst[1])="./"
			dst=dst[2..]
		EndIf
		
		bank=LoadBank(src)
		If Not bank
			DebugLog "Failed to load file ~q"+src+"~q."
			Return False
		EndIf
		filecount:+1
		filename=filename[..filecount]
		filename[filecount-1]=dst
		filebank=filebank[..filecount]
		filebank[filecount-1]=bank
		Return True
	EndMethod
	
	Method AddDir:Int(path:String="")
		Local n:Int
		Local files:String[]
		
		'Correct path string
		If path="" path="."
		path=path.Replace("\","/")
		If path
			If Chr(path[path.length-1])&lt;&gt;"/" path:+"/"
		EndIf
		
		files=LoadDir(path)
		If Not files
			DebugLog "Failed to load directory ~q"+path+"~q."
			Return False
		EndIf
		
		For n=0 To files.length-1
			
			Select FileType(path+files[n])
			Case 1
				DebugLog "Adding file ~q"+path+files[n]+"~q..."
				If Not AddFile(path+files[n])
					Return False
				EndIf
			Case 2
				DebugLog "Adding directory ~q"+path+files[n]+"~q..."
				If Not AddDir(path+files[n]) Return False
			EndSelect
		Next
		Return True
	EndMethod
	
	Function Load:TPackFile(url:Object)
		Local n:Int
		Local packfile:TPackFile
		Local size:Int
		Local stream:TStream
		
		stream=ReadStream(url)
		If Not stream Return Null
		packfile=New TPackFile
		packfile.filecount=stream.ReadInt()
		packfile.filename=packfile.filename[..packfile.filecount]
		packfile.filebank=packfile.filebank[..packfile.filecount]
		For n=0 To packfile.filecount-1
			packfile.filename[n]=stream.ReadLine()
			size=stream.ReadInt()
			packfile.filebank[n]=CreateBank(size)
			stream.readbytes(packfile.filebank[n].buf(),size)
		Next
		stream.close()
		Return packfile
	EndFunction
	
	Method Unpack:Int(path:String="",ShowProgress(progress:Float)=Null)
		Local n:Int
		Local stream:TStream
		Local size:Int
		Local totalsize:Int
		Local bytesunpacked:Int
		Local dir:String
		
		'Correct path string
		path=path.Replace("\","/")
		If path
			If Chr(path[path.length-1])&lt;&gt;"/" path:+"/"
		EndIf

		'Make sure unpack folder exists
		If path
			If FileType(path)&lt;&gt;2
				If Not CreateDir(path,1)
					DebugLog "Failed to create directory ~q"+path+"~q."
					Return False
				EndIf
			EndIf
		EndIf
		
		totalsize=GetDataSize()
		For n=0 To filecount-1
			dir=ExtractDir(path+filename[n])
			If FileType(dir)&lt;&gt;2
				If Not CreateDir(dir,1)
					DebugLog "Failed to create directory ~q"+dir+"~q."
					Return False
				EndIf
			EndIf
			stream=WriteFile(path+filename[n])
			If Not stream
				DebugLog "Failed to write file ~q"+path+filename[n]+"~q."
				Return False
			EndIf
			size=filebank[n].size()
			stream.writebytes(filebank[n].buf(),size)
			stream.close()
			bytesunpacked:+size
			If ShowProgress ShowProgress(Float(bytesunpacked)/Float(totalsize))
		Next
		
		Return True
	EndMethod
	
	Method GetDataSize:Int()
		Local size:Int,n:Int
		
		For n=0 To filecount-1
			size:+filebank[n].size()
		Next
		Return size
	EndMethod
	
	Method Save:Int(url:Object)
		Local n:Int
		Local size:Int
		Local ownstream:Int
		Local stream:TStream
		
		stream=TStream(url)
		If Not stream
			stream=WriteStream(url)
			ownstream=1
		EndIf
		If Not stream Return 0
		stream.WriteInt(filecount)
		For n=0 To filecount-1
			stream.WriteLine(filename[n])
			size=filebank[n].size()
			stream.WriteInt(size)
			stream.writebytes(filebank[n].buf(),size)
		Next
		If ownstream
			stream.close()
		EndIf
		Return 1
	EndMethod
	
EndType</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>2012</font></td></tr></table></td></tr><tr ><td class="posttext"> Hi<br><br>Here is some modifications to include packing and encrypting directly under the class.<br><br>PS : Thanks josh for this piece of code<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Framework brl.bankstream
Import "PackUtil.bmx" 

Local pak:TPackFile

'Simple example
pak=New TPackFile

pak.AddDir("Cadres","2111")
'pak.AddFile("pack/IMG_0242.bmp","","2111")
'pak.AddFile("pack/IMG_0265.bmp","","2111")
'pak.AddFile("pack/IMG_0267.bmp","","2111")
pak.Save("data.pak")

Print ""
pak=TPackFile.Load("data.pak")
Print "&gt;"+pak.unpack("unpack",Null,"2111")
</textarea><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Import brl.bank
Import pub.zlib
Import brl.filesystem
Import brl.standardio
Import brl.bankstream
Import brl.filesystem
Import BRL.Retro


Type TPackFile
	
	Field filecount:Int
	Field filename:String[]
	Field filebank:TBank[]
	
	Method AddFile:Int(src:String,dst:String,key:String)
		Local bank:TBank
		
		' Nettoyage des path
		If Not dst dst=src
		dst=dst.Replace("","/")
		
		If Chr(dst[0])+Chr(dst[1])="./"
			dst=dst[2..]
		EndIf
		
		' Compression et encryptage
		bank=LoadBank(src)
		
		Local originalSize:Int=BankSize(bank)
		Local compLevel:Int
		
		' On skipe le fichier
		If Not bank
			Print "Failed to load file ["+src+"]"
			Return False
		Else
			' selection du taux de compression selon les fichiers
			Select Upper(Right$(src,4))
			Case ".ZIP"
				compLevel=0
			Case ".TAR"
				compLevel=0
			Case ".RAR"
				compLevel=0
			Default 
				compLevel=9
			End Select
			
			' Compression de la banque
			bank=CompressBank(bank,compLevel)
			EncryptBank(bank,key)
			
			Local bufferSize:Int=BankSize(bank)
			Local percent:Float=0
	
			Print ""
			Print "Adding file ["+src+"]"
			
			' Prevent division by zero
			If originalSize&gt;0 Then 
				percent=(bufferSize*100)/originalSize
			EndIf

			Print "Original: "+originalSize+" bytes / Compressed: "+bufferSize+" bytes ("+Int(percent)+"%)"
		EndIf
		
		' Sinon on l'ajoute
		filecount:+1
		filename=filename[..filecount]
		filename[filecount-1]=dst
		filebank=filebank[..filecount]
		filebank[filecount-1]=bank
		
		Return True
	EndMethod
	
	' Ajoute tout les fichiers d'un repertoire
	Method AddDir:Int(path:String="",key:String)
		Local n:Int
		Local files:String[]
		
		'Correct path string
		If path="" path="."
		path=path.Replace("","/")
		
		If path
			If Chr(path[path.length-1])&lt;&gt;"/" path:+"/"
		EndIf
		
		files=LoadDir(path)
		
		For n=0 To files.length-1
			
			Select FileType(path+files[n])
			Case 1
				If Not AddFile(path+files[n],"",Key) Then
					Return False
				EndIf
			Case 2
				If Not AddDir(path+files[n],Key) Then 
					Return False
				EndIf
			EndSelect
		Next
		
		Return True
	EndMethod
	
	' Charge un fichier pack
	Function Load:TPackFile(url:Object)
		Local n:Int
		Local packfile:TPackFile
		Local size:Int
		Local stream:TStream
		
		stream=ReadStream(url)
		If Not stream Return Null
		
		packfile=New TPackFile
		packfile.filecount=stream.ReadInt()
		
		packfile.filename=packfile.filename[..packfile.filecount]
		packfile.filebank=packfile.filebank[..packfile.filecount]
		
		For n=0 To packfile.filecount-1
			packfile.filename[n]=stream.ReadLine()
			size=stream.ReadInt()
			packfile.filebank[n]=CreateBank(size)
			stream.readbytes(packfile.filebank[n].buf(),size)
		Next
		
		stream.close()
		Return packfile
		
	EndFunction
	
	' Decompresse tout un pack dans un rep
	Method Unpack:Int(path:String="",ShowProgress(progress:Float)=Null,key:String)
		Local n:Int
		Local stream:TStream
		Local size:Int
		Local totalsize:Int
		Local bytesunpacked:Int
		Local dir:String
		
		'Correct path string
		path=path.Replace("","/")
		If path
			If Chr(path[path.length-1])&lt;&gt;"/" path:+"/"
		EndIf

		'Make sure unpack folder exists
		If path
			If FileType(path)&lt;&gt;2
				If Not CreateDir(path,1)
					Print "Failed to create directory ~q"+path+"~q."
					Return False
				EndIf
			EndIf
		EndIf
		
		totalsize=GetDataSize()
		
		For n=0 To filecount-1
			dir=ExtractDir(path+filename[n])

			If FileType(dir)&lt;&gt;2
				If Not CreateDir(dir,1)
					Print "Failed to create directory ~q"+dir+"~q."
					Return False
				Else
					Print "Create directory: "+dir
				EndIf
			EndIf
			
			stream=WriteFile(path+filename[n])
			
			If Not stream
				Print "Failed to write file ~q"+path+filename[n]+"~q."
				Return False
			Else
				Print "Create file: "+path+filename[n]
			EndIf
			
			' Decompression
			DecryptBank(filebank[n],key)
			filebank[n]=DecompressBank(filebank[n])
			size=filebank[n].size()
			
			' Ecriture du fichier de sortie
			stream.writebytes(filebank[n].Buf(),size)
			
			' fermeture du fichier
			stream.close()
			bytesunpacked:+size
			
			If ShowProgress Then ShowProgress(Float(bytesunpacked)/Float(totalsize))
			
		Next
		
		Return True
	EndMethod
	
	' Recuperation de la taille de toutes les banques en ram
	Method GetDataSize:Int()
		Local size:Int,n:Int
		
		For n=0 To filecount-1
			size:+filebank[n].size()
		Next
		
		Return size
	EndMethod
	
	' Sauvegarde du pack
	Method Save:Int(url:Object)
		Local n:Int
		Local size:Int
		Local ownstream:Int
		Local stream:TStream
		
		stream=TStream(url)
		
		If Not stream
			stream=WriteStream(url)
			ownstream=1
		EndIf
		
		If Not stream Return 0
		stream.WriteInt(filecount)
		
		For n=0 To filecount-1
			
			' Si le nom contient : on clean...
			If Instr(filename[n],":")&gt;0 Then
				stream.WriteLine(Right$(filename[n],Len(filename[n])-(Instr(filename[n],":")+1)))
			Else
				stream.WriteLine(filename[n])
			EndIf
				
			size=filebank[n].size()
			stream.WriteInt(size)
			stream.writebytes(filebank[n].buf(),size)
		Next
		
		If ownstream
			stream.close()
		EndIf
		
		Return 1
	EndMethod
	
EndType

' Compress bank functions
Function CompressBank:TBank( bank:TBank, level:Int=9 )
	Local size:Int=bank.Size()
	Local out_size:Int=size+size/10+32
	Local out:TBank=TBank.Create( out_size )
	compress2 out.Buf()+4,out_size,bank.Buf(),size,level
	out.PokeByte 0,size
	out.PokeByte 1,size Shr 8
	out.PokeByte 2,size Shr 16
	out.PokeByte 3,size Shr 24
	out.Resize out_size+4
	Return out
End Function

Function DecompressBank:TBank( bank:TBank )
	Local out_size:Int
	out_size:|bank.PeekByte(0)
	out_size:|bank.PeekByte(1) Shl 8
	out_size:|bank.PeekByte(2) Shl 16
	out_size:|bank.PeekByte(3) Shl 24
	Local out:TBank=TBank.Create( out_size )
	uncompress out.Buf(),out_size,bank.Buf()+4,bank.Size()-4
	Return out
EndFunction

' Encrypting bank
Function EncryptBank(bank:TBank,key:String)
	Local c$
	Local i:Int
	
	For i = 0 To bank.size()-1
		c = Chr$(PeekByte(bank,i))
		PokeByte(bank,i,Asc(XorCrypt(c,key))) 
	Next

	Function XorCrypt:String(str$, key$)
		Local ml%, pl%, i%, result$,k:Int,c:Byte
		ml% = str.length
		pl% = key.length
		For i = 0 Until ml
			c=str$[i]
			k=key$[i Mod pl]
			c = c ~ k
			result$ :+ Chr(c)
		Next 
		Return result$
	EndFunction

EndFunction

Function DecryptBank(bank:TBank,key:String)
	EncryptBank(bank,key)
EndFunction

</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Richard Betson</td><td align="right"><font class=tiny>2014</font></td></tr></table></td></tr><tr ><td class="posttext"> This all works great! Thanks. :) <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
