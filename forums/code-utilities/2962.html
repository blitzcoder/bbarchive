<!DOCTYPE html><html lang="en" ><head ><title >XML Parser &amp; Saver</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Capable of efficiently reading and writing XML data. This is not a wrapper., language=bmx, category=File Utilities'><meta name='author' content='Pineapple'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=8>File Utilities</a>/XML Parser &amp; Saver</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2962.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >XML Parser &amp; Saver by Pineapple</td><td align="right">2012 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> The module pine.BinTree is required to run this code.<br><br><a href="http://blitzbasic.com/Community/posts.php?topic=97992" target="_blank">http://blitzbasic.com/Community/posts.php?topic=97992</a><br><br>Download it directly here:<br><br><a href="http://dl.dropbox.com/u/10116881/blitz/pine.bintree/pine.bintree.mod.zip" target="_blank">http://dl.dropbox.com/u/10116881/blitz/pine.bintree/pine.bintree.mod.zip</a><br><br>My apologies for using BinTree instead of a native module, TList would cause huge slowdowns with larger lists of nodes or attributes, and TMap doesn't allow multiple entries for the same key. </td></tr><tr ><td class="cell"><pre class="code">' 	--+-----------------------------------------------------------------------------------------+--
'	  |   This code was originally written by Sophie Kirschner (sophiek@pineapplemachine.com)   |  
' 	  | It is released as public domain. Please don't interpret that as liberty to claim credit |  
' 	  |   that isn't yours, or to sell this code when it could otherwise be obtained for free   |  
'	  |                because that would be a really shitty thing of you to do.                |
' 	--+-----------------------------------------------------------------------------------------+--

SuperStrict

Import pine.BinTree
Import brl.stream


' example program
' loads your xml file named 'example_input.xml', parses it, and then writes it as 'example_output.xml'
Rem 
Local f:TStream=ReadFile("example_input.xml")
Local n:xmlnode=xmlnode.read(f)
CloseFile f
f=WriteFile("example_output.xml")
n.write f
CloseFile f
End
EndRem 


Rem
bbdoc: XML Node type
about: children:BinTree and attributes:BinTree are two fields containing all the children nodes and the attributes, respectively.
They can be iterated through using EachIn. (For a:xmlattribute=EachIn node.attributes; For n:xmlnode=EachIn node.children)
TreeFind:Object(tree:BinTree,key$) will return the first value in the tree with the given key.
TreeFindAll:TList(tree:BinTree,key$) will return a TList containing all the values in the tree with the given tree.
See the BinTree documentation for more detailed info and how to iterate through keys, nodes containing key/value pairs, manipulation, etc.
EndRem 
Type xmlnode
	Field name$
	Field children:BinTree=CreateTree()
	Field attributes:BinTree=CreateTree()
	Rem
	bbdoc: Returns a new xmlnode.
	EndRem 
	Function Create:xmlnode(name$)
		Local n:xmlnode=New xmlnode
		n.name=name
		Return n
	End Function
	Rem
	bbdoc: Adds a new child.
	EndRem 
	Method addchild(n:xmlnode)
		Assert n,"Cannot assign a nonexistent xmlnode as a child."
		children.insert n.name,n
	End Method
	Rem
	bbdoc: Adds a new attribute.
	returns: The created xmlattribute object.
	EndRem 
	Method addattribute:xmlattribute(name$,value$)
		Local a:xmlattribute=New xmlattribute
		a.name=name
		a.value=value
		attributes.insert name,a
		Return a
	End Method
	Rem
	bbdoc: Removes a child.
	EndRem 
	Method removechild(n:xmlnode)
		Assert a,"Attempted to remove nonexistent XML node."
		children.removevalue(n,n.name)
	End Method
	Rem
	bbdoc: Removes an attribute.
	EndRem 
	Method removeattribute(a:xmlattribute)
		Assert a,"Attempted to remove nonexistent XML attribute."
		attributes.removevalue(a,a.name)
	End Method
	Rem
	bbdoc: Returns the value assocated with some attribute name.
	EndRem 
	Method getvalue$(name$)
		Local val:xmlattribute=xmlattribute(attributes.find(name))
		If Not val Return ""
		Return val.value
	End Method
	Rem
	bbdoc: Returns the attribute assocated with the given name.
	EndRem 
	Method getattribute:xmlattribute(name$)
		Return xmlattribute(attributes.find(name))
	End Method
	Rem
	bbdoc: Returns the first encountered child node with the given name.
	EndRem 
	Method getchild:xmlnode(name$)
		Return xmlnode(children.find(name))
	End Method
	Rem
	bbdoc: Returns a list of all child nodes with the given name.
	EndRem 
	Method getchildren:TList(name$)
		Return children.findall(name)
	End Method
	Rem
	bbdoc: Read an XML Node (along with any children) from a stream.
	EndRem 
	Function read:xmlnode(f:TStream)
		Local n:xmlnode=New xmlnode
		While ReadByte(f)&lt;&gt;opentag
			Assert Not Eof(f),"Encountered unexpected end-of-file."
		Wend
		Local nb%
		Local tag$=""
		Repeat
			nb=ReadByte(f)
			If nb=fintag Then Exit
			tag:+Chr(nb)
		Forever
		Assert Len(tag),"Encountered illegal tag: &lt;&gt;"
		If Asc(Right(tag,1))=closetag Then ' has no children
			tag=Left(tag,tag.length-1)
		ElseIf Asc(Left(tag,1))=closetag Then ' is a closing tag
			Return Null
		Else
			Local clist:TList=CreateList()
			Repeat
				Local c:xmlnode=read(f)
				If c Then
					clist.addfirst c
				Else
					Exit
				EndIf
			Forever
			For Local c:xmlnode=EachIn clist
				n.children.insert c.name,c
			Next
		EndIf
		n.parsetag tag
		Assert n.name,"Encountered nameless tag."
		
		Rem
		?debug
		DebugLog "Read xmlnode: "+n.name
		Local attrstr$=""
		For Local a:xmlattribute=EachIn n.attributes
			attrstr:+a.name+" = ~q"+a.value+"~q; "
		Next
		Local chldstr$=""
		For Local c:xmlnode=EachIn n.children
			chldstr:+c.name+"; "
		Next
		DebugLog "Attributes: "+attrstr
		DebugLog "Children: "+chldstr
		?
		EndRem
		
		Return n
	End Function
	Rem
	bbdoc: Write an XML Node (and all its children) to a stream.
	EndRem 
	Method write(f:TStream,prefix$="")
		Assert f,"Stream does not exist."
		WriteString f,prefix
		WriteByte f,opentag
		WriteString f,name
		For Local a:xmlattribute=EachIn attributes
			WriteByte f,space
			WriteString f,a.name
			WriteByte f,equals
			WriteByte f,quote
			WriteString f,a.value
			WriteByte f,quote
		Next
		If children.isempty() Then
			WriteByte f,space
			WriteByte f,closetag
			WriteByte f,fintag
			WriteByte f,newl
		Else
			WriteByte f,fintag
			WriteByte f,newl
			For Local n:xmlnode=EachIn children
				n.write f,prefix+Chr(tab)
			Next
			WriteString f,prefix
			WriteByte f,opentag
			WriteByte f,closetag
			WriteString f,name
			WriteByte f,fintag
			WriteByte f,newl
		EndIf
	End Method
	
	' private stuff that you shouldn't need to touch
	Const opentag%=Asc("&lt;")
	Const closetag%=Asc("/")
	Const fintag%=Asc("&gt;")
	Const space%=Asc(" ")
	Const tab%=Asc("	")
	Const newl%=Asc("~n")
	Const equals%=Asc("=")
	Const quote%=Asc("~q")
	Function iswhitespace%(c%)
		Return (c=space) Or (c=tab) Or (c=newl)
	End Function
	Method parsetag(str$)
		Local x%=0
		Local on$="",spaces%=0
		Local inquotes%=0
		Local lastwasspace%=1
		Local lasta:xmlattribute
		While x&lt;str.length
			If inquotes=0 And iswhitespace(str[x]) Then
				If Not lastwasspace
					If spaces=0 Then 
						name=on
						on=""
						spaces=1
					EndIf
					lastwasspace=1
				EndIf
			ElseIf inquotes=0 And str[x]=equals
				Local a:xmlattribute=New xmlattribute
				a.name=on
				attributes.insert a.name,a
				lasta=a
				on=""
				lastwasspace=0
			ElseIf str[x]=quote
				If inquotes
					Assert lasta,"Encountered malformed tag."
					lasta.value=on
					on=""
					lasta=Null
				EndIf
				inquotes=Not inquotes
				lastwasspace=0
			ElseIf lasta And inquotes
				on:+Chr(str[x])
				lastwasspace=0
			ElseIf Not lasta
				on:+Chr(str[x])
				lastwasspace=0
			EndIf
			x:+1
		Wend
		If Not spaces Then
			name=on
		EndIf
	End Method
	
End Type

Rem
bbdoc: XML Node Attribute type
EndRem 
Type xmlattribute
	Field name$
	Field value$
End Type</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bobysait</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>
Method removechild(n:xmlnode)
		Assert a,"Attempted to remove nonexistent XML node."
		children.removevalue(n,n.name)
	End Method
</pre><br><br>-&gt; Assert n<br><br>While it can't even compile this in SuperStrict, I'm wondering, did you tried your own code before posting it ? <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
