<!DOCTYPE html><html lang="en" ><head ><title >Controlling NPCs with Types</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Controlling NPCs with Types</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=88" >Blitz3D Tutorials</a>/<a href="#bottom" >Controlling NPCs with Types</a><br><br>
<a name="1014955"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've seen a few posts lately about how to control enemies or NPCs and simple AI.  Here's a simple way to control your NPCs using types.<br><br>All you need to do is have a status or state field in your NPC type.  Each frame, act upon the chosen state.  It's pretty simple. <br><br>First: What do we want to do? <br>We want the enemy NPC to sit idle, look for the player..<br>If they see the player, follow him, and if he gets close enough, attack!<br>Then, we want to return the enemy NPC to it's starting location.<br>When the enemy returns to it's post, it will begin searthing for the player again....<br><br>To see the difference in states, we'll change the color of the enemy NPCs so we can visually see what state they are in.<br><br>If the player gets within say 10 units of the enemy, the enemy will become aggressive.  We want the enemy to follow the player either until the player gets too far away or the enemy reaches the player, in which case it will attack the player.<br><br>That's all we need to know to get started!<br><br>Here is the commented code:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Waypoint Example By Drak, 3/8/2010

;************************************************************
;THIS PROGRAM WILL:											*
;Create a player mesh (a square)							*
;Create some enemies (some spheres)							*
;Make on-the-fly waypoints for the enemies to return to		*
;Return the enemies to the waypoints						*
;Also shows some some rudimentary AI						*
;Shows some very simple particle effects as well			*
;************************************************************

AppTitle "Drak's Waypoint Example"

;-------------------------------------------------------------------------------------------------------
;INITIALIZATION
;-------------------------------------------------------------------------------------------------------
Graphics3D 800,600
SetBuffer BackBuffer()
SeedRnd MilliSecs()

Global player = CreateCube()	;create a cube for the player
EntityColor player, 0,0,200		;we'll make the player blue

Global player_hp = 10			;give the player 10 hit points
Global player_alive = 1			;player is indeed alive

Global camera = CreateCamera()	;create a camera
PositionEntity camera, 0,40,0	;raise it up in the air
RotateEntity camera, 90,0,0		;point it down


;-------------------------------------------------------------------------------------------------------
;TYPES
;-------------------------------------------------------------------------------------------------------
Type enemy
	Field mesh				;will hold the actual mesh
	Field return_waypoint	;will hold the handle of the return waypoint
	Field status			;holds a # referring to the enemies status
	Field speed#			;this will hold the speed of the enemy
End Type

Type particle				
	Field mesh				;holds mesh
	Field alpha#			;holds alpha
	Field speed_x#			;speed x
	Field speed_y#			;same
	Field speed_z#			;same...
	Field fade_speed#		;fade_speed
End Type

;-------------------------------------------------------------------------------------------------------
;LETS CREATE SOME PRIMITIVES TO USE LATER	
;-------------------------------------------------------------------------------------------------------

Global spark = CreateCube()
	ScaleEntity spark, .25,.25,.25
	EntityColor spark, 255,168,0
	HideEntity spark

Global waypoint = CreateSphere()
	ScaleEntity waypoint, .5,.5,.5
	EntityColor waypoint, 0,200,0
	HideEntity waypoint

;-------------------------------------------------------------------------------------------------------
;CREATE SOME ENEMIES
;-------------------------------------------------------------------------------------------------------
For num = 1 To 5
	e.enemy = New enemy				;new enemy
	e\mesh = CreateSphere(16)		;create a sphere and store it in e\mesh
	e\speed = .1					;set the speed of each enemy
	e\status = 1					;set status to 1
	PositionEntity e\mesh, Rand(-20,20),0,Rand(-20,20)	;place the enemies randomly
	EntityAlpha e\mesh, .5
Next

;-------------------------------------------------------------------------------------------------------
;MAIN LOOP
;-------------------------------------------------------------------------------------------------------
While Not KeyHit(1)
	get_input()
	run_enemies()
	run_particles()
	check_hp()
	
	
	UpdateWorld
	RenderWorld
	
	gui()
	Flip
	
Wend
;-------------------------------------------------------------------------------------------------------
;END MAIN LOOP
;-------------------------------------------------------------------------------------------------------


;-----------------------------------------------------------------------------------------------------
;FUNCTION GET_INPUT &lt;--- GRABS INPUT FOR THE PLAYER
;-----------------------------------------------------------------------------------------------------
Function get_input()
	If player_alive = 1
	;W key
	If KeyDown(17) Then TranslateEntity player, 0,0,.15	;move it +z
	
	;A key
	If KeyDown(30) Then TranslateEntity player, -.15,0,0;move it -x
	
	;S key
	If KeyDown(31) Then TranslateEntity player, 0,0,-.15;move it -z
	
	;D key
	If KeyDown(32) Then TranslateEntity player, .15,0,0 ;move it +x
	End If 
End Function 

	
;-----------------------------------------------------------------------------------------------------
;FUNCTION RUN_ENEMIES   &lt;---EXOCUTES ACTIONS FOR EACH ENEMY
;-----------------------------------------------------------------------------------------------------
Function run_enemies()
	For e.enemy = Each enemy	;for each enemy
		;regardless of case, the distance to the player must be known, so we'll check it here
		Local dist# = EntityDistance(e\mesh,player)
			Select e\status			;select it's status
				
				Case 1 ;ON GUARD
					EntityColor e\mesh, 200,200,0
					If dist# &lt;= 10		;if the distance is less than 10 or equal to it
						If player_alive = 1
							e\status = 2	;switch to state 2
						End If
					End If
				
				Case 2 ;DROP NEW WAYPOINT
					Local x# = EntityX(e\mesh)	;grab the enemies' coordinates
					Local y# = EntityY(e\mesh)
					Local z# = EntityZ(e\mesh)
					
						;place the waypoint at the location
						e\return_waypoint = CopyEntity(waypoint)
						PositionEntity e\return_waypoint,x#,y#,z#
						
						e\status = 3	;then switch to case 3, which is pursue
				
				Case 3 ;PURSUE THE PLAYER
					EntityColor e\mesh, 200,0,0	;red
					If dist# &lt; 1	;then attack!
						e\status = 4;change to attack
						Exit 		;exit the loop
					End If 
					
					If dist# &lt; 12	;first, check to see if we're 12 units away or less....
						PointEntity e\mesh, player		;point enemy at the player
						MoveEntity e\mesh, 0,0,e\speed	;then make the enemy move in the pointed direction
					Else
						e\status = 5	;set to status 5, which is return to waypoint
					End If 	
		
				Case 4 ;ATTACK
					player_hp = player_hp - 1	;take 1 hp from the player
					e\status = 5				;now return to the waypoint
					Local s_x# = EntityX(player)
					Local s_y# = EntityY(player)
					Local s_z# = EntityZ(player)
					Local num = Rand(3,8)
						;create some effects
						create_particles(num,s_x,s_y,s_z)
				
				Case 5
					EntityColor e\mesh, 200,0,200
					Local dist_home# = EntityDistance(e\mesh,e\return_waypoint)	;get the distance home
						If dist_home# &gt; .2							;If we're further than .2 away...
							PointEntity e\mesh, e\return_waypoint	;point at home waypoint
							MoveEntity e\mesh, 0,0,e\speed			;move them forward
						Else If dist_home# &lt;= .2
							FreeEntity e\return_waypoint			;get rid of the waypoint mesh
							e\status = 1							;and return to idle again
						End If
				
				Default
					e\status = 1	;if somehow e\status = anything else, we'll set it to 1
			
			End Select
	Next
End Function 

;-----------------------------------------------------------------------------------------------------
;GRAPHIC READOUTS   &lt;---This function prints readouts to the screen
;-----------------------------------------------------------------------------------------------------
Function gui()
	Text 0,0, "Use WASD keys to move around."
	Text 0,10,"Don't let the spheres touch you or you'll lose hitpoints."
	Text 0,20,"Remaining HP: " + player_hp
	
	If player_alive = 0
		Text 350,290,"You are DEAD!"
	End If 
End Function 


;-----------------------------------------------------------------------------------------------------
;RUN PARTICLES	   &lt;---This function runs the particles if there are any
;-----------------------------------------------------------------------------------------------------
Function run_particles()
For p.particle = Each particle
	p\alpha = p\alpha - p\fade_speed	;take fade speed from alpha
	EntityAlpha p\mesh, p\alpha			;set alpha to p\alpha
		If p\alpha &lt;= 0					;if alpha is &lt;= 0
			FreeEntity p\mesh			;free it
			Delete p				
			Exit						;exit the loop
		Else
			p\speed_y = p\speed_y - .01							;subtract from y speed
			MoveEntity p\mesh, p\speed_x, p\speed_y,p\speed_z	;move the particle
		End If
		
		
Next
End Function  


;-----------------------------------------------------------------------------------------------------
;CREATE PARTICLES	&lt;--- THis is called when we need to create particles
;-----------------------------------------------------------------------------------------------------
Function create_particles(num,x#,y#,z#)
	For s = 1 To num
		p.particle = New particle
		p\mesh = CopyEntity(spark)
		p\speed_x# = Rnd(-.2,.2)
		p\speed_y# = Rnd(.3)
		p\speed_z# = Rnd(-.2,.2)
		p\alpha = 1
		p\fade_speed# = Rnd(.1)
		PositionEntity p\mesh, x,y,z
	Next
End Function 

;-----------------------------------------------------------------------------------------------------
;CHECK HP	&lt;---This checks the player's HEALTH each frame
;-----------------------------------------------------------------------------------------------------
Function check_hp()
If player_hp &lt;= 0
	player_hp = 0
	If player_alive = 1
	Local s_x# = EntityX(player)
	Local s_y# = EntityY(player)
	Local s_z# = EntityZ(player)
	Local num = Rand(15)
		;create some effects
		create_particles(num,s_x,s_y,s_z)
	player_alive = 0
	End If
End If 
End Function 
</textarea> <br><br></td></tr></table><br>
<a name="1066922"></a>

<a name="1066965"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Blitzplotter</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Exceptional piece of coding Drak, thanks for sharing!<br><br>Hope you do not mind, I posted a slightly modified version of your code in the following thread as an include file to implement homing missiles/spheres in some JV-ODE physics car code :)<br><br><a href="http://www.blitzbasic.com/Community/posts.php?topic=89252#1066963" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=89252#1066963</a><br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1067442"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Glad someone found it useful! <br><br></td></tr></table><br>
<a name="1067599"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Blitzplotter</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Drak, hell yeah, I feel myself being sucked back into B3D with physics(JV-ODE) at precisely the wrong time - I'm embarking on my penultimate course of study towards my degree with the OU, nevertheless I love excuses to play in B3D with physics &amp; now a fantastic piece of code that lends itself to homing missiles - I'm curious to explore whether your code homes in on the y axis as well as the x &amp; z axis. Not too hard to prove, but thats where I'm headin'.... <br><br></td></tr></table><br>
<a name="1068164"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> It <i>will</i> home in on the Y axis in this program, but since the player is viewed from above here, and not allowed to move on the Y axis it's not relevant to this program.  The "enemies" simply use the PointEntity() command so they actually <i>can</i> move and pursue in all axis. <br><br></td></tr></table><br>
<a name="1124878"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> 2/3/12 <br>Added a 6th state to the NPC enemies.  Now they will wander randomly if they don't see the player.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Waypoint Example By Drak, 3/8/2010
;EDITED 2/3/12
;************************************************************
;THIS PROGRAM WILL:											*
;Create a player mesh (a square)							*
;Create some enemies (some spheres)							*
;Make on-the-fly waypoints for the enemies to return to		*
;Return the enemies to the waypoints						*
;Also shows some some rudimentary AI						*
;Shows some very simple particle effects as well			*
;************************************************************

AppTitle "Drak's Waypoint Example"

;-------------------------------------------------------------------------------------------------------
;INITIALIZATION
;-------------------------------------------------------------------------------------------------------
Graphics3D 800,600
SetBuffer BackBuffer()
SeedRnd MilliSecs()

Global player = CreateCube()	;create a cube for the player
EntityColor player, 0,0,200		;we'll make the player blue

Global player_hp = 10			;give the player 10 hit points
Global player_alive = 1			;player is indeed alive

Global camera = CreateCamera()	;create a camera
PositionEntity camera, 0,40,0	;raise it up in the air
RotateEntity camera, 90,0,0		;point it down


;-------------------------------------------------------------------------------------------------------
;TYPES
;-------------------------------------------------------------------------------------------------------
Type enemy
	Field mesh				;will hold the actual mesh
	Field return_waypoint	;will hold the handle of the return waypoint
	Field status			;holds a # referring to the enemies status
	Field speed#			;this will hold the speed of the enemy
	Field timer				;2/3/12 This will be a counter
	Field timer_max			;2/3/12 This is the counter's maximum
	Field steer_angle#		;2/3/12 This holds the enemy's steering angle
End Type

Type particle				
	Field mesh				;holds mesh
	Field alpha#			;holds alpha
	Field speed_x#			;speed x
	Field speed_y#			;same
	Field speed_z#			;same...
	Field fade_speed#		;fade_speed
End Type

;-------------------------------------------------------------------------------------------------------
;LETS CREATE SOME PRIMITIVES TO USE LATER	
;-------------------------------------------------------------------------------------------------------

Global spark = CreateCube()
	ScaleEntity spark, .25,.25,.25
	EntityColor spark, 255,168,0
	HideEntity spark

Global waypoint = CreateSphere()
	ScaleEntity waypoint, .5,.5,.5
	EntityColor waypoint, 0,200,0
	HideEntity waypoint

;-------------------------------------------------------------------------------------------------------
;CREATE SOME ENEMIES
;-------------------------------------------------------------------------------------------------------
For num = 1 To 5
	e.enemy = New enemy				;new enemy
	e\mesh = CreateSphere(16)		;create a sphere and store it in e\mesh
	e\speed = .1					;set the speed of each enemy
	e\status = 1					;set status to 1
	e\timer = 0						;2/3/12	set timer to 0
	e\timer_max = Rand(400,1000)	;2/3/12	pick a maximum timer
	PositionEntity e\mesh, Rand(-20,20),0,Rand(-20,20)	;place the enemies randomly
	EntityAlpha e\mesh, .5
	RotateEntity e\mesh, 0,Rand(360),0
Next

;-------------------------------------------------------------------------------------------------------
;MAIN LOOP
;-------------------------------------------------------------------------------------------------------
While Not KeyHit(1)
	get_input()
	run_enemies()
	run_particles()
	check_hp()
	
	
	UpdateWorld
	RenderWorld
	
	gui()
	Flip
	
Wend
;-------------------------------------------------------------------------------------------------------
;END MAIN LOOP
;-------------------------------------------------------------------------------------------------------


;-----------------------------------------------------------------------------------------------------
;FUNCTION GET_INPUT &lt;--- GRABS INPUT FOR THE PLAYER
;-----------------------------------------------------------------------------------------------------
Function get_input()
	If player_alive = 1
	;W key
	If KeyDown(17) Then TranslateEntity player, 0,0,.15	;move it +z
	
	;A key
	If KeyDown(30) Then TranslateEntity player, -.15,0,0;move it -x
	
	;S key
	If KeyDown(31) Then TranslateEntity player, 0,0,-.15;move it -z
	
	;D key
	If KeyDown(32) Then TranslateEntity player, .15,0,0 ;move it +x
	End If 
End Function 

	
;-----------------------------------------------------------------------------------------------------
;FUNCTION RUN_ENEMIES   &lt;---EXOCUTES ACTIONS FOR EACH ENEMY
;-----------------------------------------------------------------------------------------------------
Function run_enemies()
	For e.enemy = Each enemy	;for each enemy
		;regardless of case, the distance to the player must be known, so we'll check it here
		Local dist# = EntityDistance(e\mesh,player)
			Select e\status			;select it's status
				
				Case 1 ;ON GUARD
					EntityColor e\mesh, 200,200,0
					If dist# &lt;= 10		;if the distance is less than 10 or equal to it
						If player_alive = 1
							e\status = 2	;switch to state 2
						End If
					Else e\timer = e\timer + 1
						If e\timer &gt;= e\timer_max
							e\status = 6 ;switch to quick wander
						End If
					End If 
				
				Case 2 ;DROP NEW WAYPOINT
					Local x# = EntityX(e\mesh)	;grab the enemies' coordinates
					Local y# = EntityY(e\mesh)
					Local z# = EntityZ(e\mesh)
					
						;place the waypoint at the location
						e\return_waypoint = CopyEntity(waypoint)
						PositionEntity e\return_waypoint,x#,y#,z#
						
						e\status = 3	;then switch to case 3, which is pursue
				
				Case 3 ;PURSUE THE PLAYER
					EntityColor e\mesh, 200,0,0	;red
					If dist# &lt; 1	;then attack!
						e\status = 4;change to attack
						Exit 		;exit the loop
					End If 
					
					If dist# &lt; 12	;first, check to see if we're 12 units away or less....
						PointEntity e\mesh, player		;point enemy at the player
						MoveEntity e\mesh, 0,0,e\speed	;then make the enemy move in the pointed direction
					Else
						e\status = 5	;set to status 5, which is return to waypoint
					End If 	
		
				Case 4 ;ATTACK
					player_hp = player_hp - 1	;take 1 hp from the player
					e\status = 5				;now return to the waypoint
					Local s_x# = EntityX(player)
					Local s_y# = EntityY(player)
					Local s_z# = EntityZ(player)
					Local num = Rand(3,8)
						;create some effects
						create_particles(num,s_x,s_y,s_z)
				
				Case 5	;RETURN TO WAYPOINT
					EntityColor e\mesh, 200,0,200
					Local dist_home# = EntityDistance(e\mesh,e\return_waypoint)	;get the distance home
						If dist_home# &gt; .2							;If we're further than .2 away...
							PointEntity e\mesh, e\return_waypoint	;point at home waypoint
							MoveEntity e\mesh, 0,0,e\speed			;move them forward
						Else If dist_home# &lt;= .2
							FreeEntity e\return_waypoint			;get rid of the waypoint mesh
							e\status = 1							;and return to idle again
						End If
				
				;CASE 6 ADDED ON 2/3/12
				Case 6	;WANDER
					EntityColor e\mesh, 0,200,0	;turn it green
						;KEEP THE WANDERING SIMPLE!!!! DON'T OVERCOMPLICATE THIS!!!!!
						e\steer_angle = e\steer_angle + Rnd(-.1,.1)
							If e\steer_angle &gt; 1 Then e\steer_angle = 1
							If e\steer_angle &lt; -1 Then e\steer_angle = -1
						TurnEntity e\mesh, 0,e\steer_angle,0
						MoveEntity e\mesh, 0,0,e\speed/5
					;keep checking to see if the player is close enough to follow
						If dist# &lt;= 10		;if the distance is less than 10 or equal to it
							If player_alive = 1
								e\status = 2	;switch to state 2
							End If
						End If 

						
				Default
					e\status = 1	;if somehow e\status = anything else, we'll set it to 1
			
			End Select
	Next
End Function 

;-----------------------------------------------------------------------------------------------------
;GRAPHIC READOUTS   &lt;---This function prints readouts to the screen
;-----------------------------------------------------------------------------------------------------
Function gui()
	Text 0,0, "Use WASD keys to move around."
	Text 0,10,"Don't let the spheres touch you or you'll lose hitpoints."
	Text 0,20,"Remaining HP: " + player_hp
	
	If player_alive = 0
		Text 350,290,"You are DEAD!"
	End If 
End Function 


;-----------------------------------------------------------------------------------------------------
;RUN PARTICLES	   &lt;---This function runs the particles if there are any
;-----------------------------------------------------------------------------------------------------
Function run_particles()
For p.particle = Each particle
	p\alpha = p\alpha - p\fade_speed	;take fade speed from alpha
	EntityAlpha p\mesh, p\alpha			;set alpha to p\alpha
		If p\alpha &lt;= 0					;if alpha is &lt;= 0
			FreeEntity p\mesh			;free it
			Delete p				
			Exit						;exit the loop
		Else
			p\speed_y = p\speed_y - .01							;subtract from y speed
			MoveEntity p\mesh, p\speed_x, p\speed_y,p\speed_z	;move the particle
		End If
		
		
Next
End Function  


;-----------------------------------------------------------------------------------------------------
;CREATE PARTICLES	&lt;--- THis is called when we need to create particles
;-----------------------------------------------------------------------------------------------------
Function create_particles(num,x#,y#,z#)
	For s = 1 To num
		p.particle = New particle
		p\mesh = CopyEntity(spark)
		p\speed_x# = Rnd(-.2,.2)
		p\speed_y# = Rnd(.3)
		p\speed_z# = Rnd(-.2,.2)
		p\alpha = 1
		p\fade_speed# = Rnd(.1)
		PositionEntity p\mesh, x,y,z
	Next
End Function 

;-----------------------------------------------------------------------------------------------------
;CHECK HP	&lt;---This checks the player's HEALTH each frame
;-----------------------------------------------------------------------------------------------------
Function check_hp()
If player_hp &lt;= 0
	player_hp = 0
	If player_alive = 1
	Local s_x# = EntityX(player)
	Local s_y# = EntityY(player)
	Local s_z# = EntityZ(player)
	Local num = Rand(15)
		;create some effects
		create_particles(num,s_x,s_y,s_z)
	player_alive = 0
	End If
End If 
End Function 
</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
