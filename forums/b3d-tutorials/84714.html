<!DOCTYPE html><html lang="en" ><head ><title >Verlet physics tutorial (only the basics)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Verlet physics tutorial (only the basics)</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=88" >Blitz3D Tutorials</a>/<a href="#bottom" >Verlet physics tutorial (only the basics)</a><br><br>
<a name="957288"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi<br><br>NOTE: I know this lacks timestep and collision detection etc but the point of this tutorial is to help people that don't get it, get it.<br><br>Here is a tutorial I decided to write for anyone who wants to understand verlet integration but really doesn't know how to decode all of those confusing equations on wikipedia.<br><br>So where to start?  Well verlet integration is based around point masses.  These point masses are bound together by constraints that can be rigid or flexible.  Thats about it.  Now to coding it.<br><br>The pointmass Type itself:<br><br>Each point mass has an <b>x</b> value and a <b>y</b> value as well as an <b>ox</b> (old x) and an <b>oy</b> (old y)  These ox and oy values are used to calculate the velocity of the particle after each frame.  So right now our code for the verlet type would look like this in b3d or bmax:<br><br><pre class=code>Type pointmass
	Field x#
	Field y#
	Field ox#
	Field oy#
End Type
</pre><br><br>now this doesn't do much by itself so we must come up with an update function.  The update function figures out the velocities of each verlet and moves them one step further.  This is easier than it sounds.<br><br>The equation of figuring out the velocity couldn't be simpler<br><br>dx = x - ox<br>dy = y - oy<br><br>dx is the difference between the ox and the current x<br><br>so now we put the current x and y values in our ox and oy fields like this<br><br>ox = x<br>oy = y<br><br>next we add our velocity to the current x and y to simulate a time step forward.<br><br>x = x + dx<br>y = y + dy<br><br>could it be any simpler?  This satisfies newton's first law.  Paraphrased: an object in motion stays in motion unless acted on by another force<br><br>So here is what our type and update function look like in b3d code<br><br><pre class=code>Type pointmass
	Field x#
	Field y#
	Field ox#
	Field oy#
End Type

Function updatepointmasses()

	For p.pointmass = Each pointmass
		dx# = p\x - p\ox
		dy# = p\y - p\oy
		
		p\ox = p\x
		p\oy = p\y
		
		p\x = p\x + dx
		p\y = p\y + dy
	Next

End Function</pre><br><br>now to make a short demo of this, we must add drawing functions and a simple while loop.<br><br><pre class=code>Graphics 640,480,0,2
SetBuffer BackBuffer()


createpointmass(100,100,5,1)

While Not KeyDown(1)
Cls


updatepointmasses()
Flip
Wend
End

Type pointmass
	Field x#
	Field y#
	Field ox#
	Field oy#
End Type

Function updatepointmasses()

	Color 255,255,255
	For p.pointmass = Each pointmass
		
		Oval p\x-4,p\y-4,8,8,0	;draws an oval at the verlet's position
		
		Local dx# = p\x - p\ox	;gets the velocity
		Local dy# = p\y - p\oy
		
		p\ox = p\x	;updates ox and oy
		p\oy = p\y
		
		p\x = p\x + dx#	;adds velocity to get new x and new y
		p\y = p\y + dy#
	Next

End Function


Function createpointmass.pointmass(x#,y#,vx#,vy#)	; x and y are coords for the verlet. vx and vy are velocity values for the verlet

p.pointmass = New pointmass
p\x = x
p\y = y
p\ox = x-vx	;gives the particle a starting velocity
p\oy = y-vy

Return p.pointmass

End Function</pre><br><br>so the code above simply makes a particle and gives it a velocity.  :-)<br><br>Now I will move on to constraints which I view as wooden rods between verlets.  So how do constraints work?  well constraints are satisfied in a loop by brute force pretty much.  All other methods are either too ineffiecient or just not accurate enough.  The constraint loop goes after the loop that updates velocity and works like this:  if a constraint is too short due to the particles being compressed, then stretch it to make it the right size.  If a constraint is too long because the particles were stretched apart then shrink it to make it the right size.<br><br>The constraint type will look something like this:<br><br><pre class=code>Type constraint
	Field p1.pointmass
	Field p2.pointmass
	Field length#
End Type</pre><br><br>Here are the equations we will use for constraints (not optimized but for simplicity instead)<br><br>find the distance between the constrained verlets:<br>dist = sqr((x1-x2)^2 + (y1-y2)^2)<br><br>get the differences in the x and y values:<br>dx = x1-x2<br>dy = y1-y2<br><br>dist is the distance between 2 particles and length is the length of the constraint. diff is the difference between the two<br><br>diff = dist-length<br><br>diff = diff / length<br><br>dx = dx * .5<br>dy = dy * .5<br><br>and finally the constraint is solved with:<br><br>x1 = x1 - (diff * dx)<br>y1 = y1 - (diff * dy)<br><br>x2 = x2 + (diff *dx)<br>y2 = y2 + (diff * dy)<br><br>If you really want to know how this works it is best to draw it out on graph paper, and do it step by step.<br><br>One final note on constraints before you see the code is that the constraints are updated 5-20 times every loop depending on the accuracy needed for the engine.  This is only necessary when there are many constraints but I will put it in anyway.<br><br>For now here is the update function we are going to use.  this is not for efficiency, it is made so it is easy to understand.<br><br><pre class=code>Function updateconstraints()
	
	For cnt = 0 To CONST_ITERATIONS-1	;this is necessary with many constraints to solve them correctly
		For c.constraint = Each constraint
			dist# = Sqr((c\p1\x-c\p2\x)^2 + (c\p1\y-c\p2\y)^2)	;distance formula
			
			diff# = dist#-c\length#	;shows the margin of error the update loop has created so it can be corrected
			
			dx# = c\p1\x-c.p2\x	;difference between x's and y's
			dy# = c\p1\y-c.p2\y
			
			If c\length &gt; 0 Then	;prevents a divided by 0 error that may occur
				diff = diff / c\length
			Else
				diff = 0
			EndIf
			
			dx = dx * .5
			dy = dy * .5
			
			c\p1\x = c\p1\x - (diff*dx)
			c\p1\y = c\p1\y - (diff*dy)
			
			c\p2\x = c\p2\x + (diff*dx)
			c\p2\y = c\p2\y + (diff*dy)
		Next
	Next
	
End Function</pre><br><br>Now if we put this all together, we can make a basic demo to show that constraints work.<br><br><pre class=code>Graphics 640,480,0,2
SetBuffer BackBuffer()

Global CONST_ITERATIONS = 5

p1.pointmass = createpointmass(100,100,1,0)
p2.pointmass = createpointmass(100,150,0,0)
c.constraint = createconstraint(p1,p2)

While Not KeyDown(1)
Cls


updatepointmasses()
updateconstraints()
drawconstraints()

Flip
Wend
End

Type pointmass
	Field x#
	Field y#
	Field ox#
	Field oy#
End Type

Function updatepointmasses()

	Color 255,255,255
	For p.pointmass = Each pointmass
		
		Oval p\x-4,p\y-4,8,8,0	;draws an oval at the verlet's position
		
		Local dx# = p\x - p\ox	;gets the velocity
		Local dy# = p\y - p\oy
		
		p\ox = p\x	;updates ox and oy
		p\oy = p\y
		
		p\x = p\x + dx#	;adds velocity to get new x and new y
		p\y = p\y + dy#
	Next

End Function


Function createpointmass.pointmass(x#,y#,vx#,vy#)	; x and y are coords for the verlet. vx and vy are velocity values for the verlet

p.pointmass = New pointmass
p\x = x
p\y = y
p\ox = x-vx	;gives the particle a starting velocity
p\oy = y-vy

Return p.pointmass

End Function


Type constraint
	Field p1.pointmass
	Field p2.pointmass
	Field length#
End Type

Function updateconstraints()
	
	For cnt = 0 To CONST_ITERATIONS-1	;this is necessary with many constraints to solve them correctly
		For c.constraint = Each constraint
			dist# = Sqr((c\p1\x-c\p2\x)^2 + (c\p1\y-c\p2\y)^2)	;distance formula
			
			diff# = dist#-c\length#	;shows the margin of error the update loop has created so it can be corrected
			
			dx# = c\p1\x-c\p2\x	;difference between x's and y's
			dy# = c\p1\y-c\p2\y
			
			If c\length &gt; 0 Then	;prevents a divided by 0 error that may occur
				diff = diff / c\length
			Else
				diff = 0
			EndIf
			
			dx = dx * .5
			dy = dy * .5
			
			c\p1\x = c\p1\x - (diff*dx)
			c\p1\y = c\p1\y - (diff*dy)
			
			c\p2\x = c\p2\x + (diff*dx)
			c\p2\y = c\p2\y + (diff*dy)
		Next
	Next
	
End Function



Function createconstraint.constraint(p1.pointmass,p2.pointmass)

	c.constraint = New constraint
	c\p1.pointmass = p1.pointmass
	c\p2.pointmass = p2.pointmass
	c\length# = Sqr((c\p1\x-c\p2\x)^2 + (c\p1\y-c\p2\y)^2)
	Return c.constraint

End Function

Function drawconstraints()

For c.constraint = Each constraint
	Line c\p1\x,c\p1\y,c\p2\x,c\p2\y
Next

End Function</pre><br><br><br>Ok, time for a little demo of a square falling using the same verlet engine in the above post.  I just added some verlets and constraints before the loop<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics 640,480,0,2
SetBuffer BackBuffer()

Global CONST_ITERATIONS = 5

p1.pointmass = createpointmass(100,100,0,0)
p2.pointmass = createpointmass(100,150,0,0)
p3.pointmass = createpointmass(150,150,0,0)
p4.pointmass = createpointmass(150,100,0,0)

c.constraint = createconstraint(p1,p2)
c.constraint = createconstraint(p2,p3)
c.constraint = createconstraint(p3,p4)
c.constraint = createconstraint(p1,p4)

While Not KeyDown(1)
Cls


updatepointmasses()
updateconstraints()
drawconstraints()

Flip
Wend
End

Type pointmass
	Field x#
	Field y#
	Field ox#
	Field oy#
End Type

Function updatepointmasses()

	Color 255,255,255
	For p.pointmass = Each pointmass
		
		Oval p\x-4,p\y-4,8,8,0	;draws an oval at the verlet's position
		
		Local dx# = p\x - p\ox	;gets the velocity
		Local dy# = p\y - p\oy+.1		;added gravity here
		
		p\ox = p\x	;updates ox and oy
		p\oy = p\y
		
		p\x = p\x + dx#	;adds velocity to get new x and new y
		p\y = p\y + dy#
		
		If p\y &gt; GraphicsHeight() Then
			p\y = GraphicsHeight()
			dx = p\x-p\ox
			dx = dx*.5
			p\ox = p\ox +dx	;simulates friction
		EndIf
	Next

End Function


Function createpointmass.pointmass(x#,y#,vx#,vy#)	; x and y are coords for the verlet. vx and vy are velocity values for the verlet

p.pointmass = New pointmass
p\x = x
p\y = y
p\ox = x-vx	;gives the particle a starting velocity
p\oy = y-vy

Return p.pointmass

End Function


Type constraint
	Field p1.pointmass
	Field p2.pointmass
	Field length#
End Type

Function updateconstraints()
	
	For cnt = 0 To CONST_ITERATIONS-1	;this is necessary with many constraints to solve them correctly
		For c.constraint = Each constraint
			dist# = Sqr((c\p1\x-c\p2\x)^2 + (c\p1\y-c\p2\y)^2)	;distance formula
			
			diff# = dist#-c\length#	;shows the margin of error the update loop has created so it can be corrected
			
			dx# = c\p1\x-c\p2\x	;difference between x's and y's
			dy# = c\p1\y-c\p2\y
			
			If c\length &gt; 0 Then	;prevents a divided by 0 error that may occur
				diff = diff / c\length
			Else
				diff = 0
			EndIf
			
			dx = dx * .5
			dy = dy * .5
			
			c\p1\x = c\p1\x - (diff*dx)
			c\p1\y = c\p1\y - (diff*dy)
			
			c\p2\x = c\p2\x + (diff*dx)
			c\p2\y = c\p2\y + (diff*dy)
		Next
	Next
	
End Function



Function createconstraint.constraint(p1.pointmass,p2.pointmass)

	c.constraint = New constraint
	c\p1.pointmass = p1.pointmass
	c\p2.pointmass = p2.pointmass
	c\length# = Sqr((c\p1\x-c\p2\x)^2 + (c\p1\y-c\p2\y)^2)
	Return c.constraint

End Function

Function drawconstraints()

For c.constraint = Each constraint
	Line c\p1\x,c\p1\y,c\p2\x,c\p2\y
Next

End Function</textarea><br><br>so run that and see what happens... uh It falls over! so we must support it by adding the following lines to the beginning after all other constraints are created.<br><br><pre class=code>c.constraint = createconstraint(p1,p3)
c.constraint = createconstraint(p2,p4)</pre><br><br>so the code will look like this:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics 640,480,0,2
SetBuffer BackBuffer()

Global CONST_ITERATIONS = 5

p1.pointmass = createpointmass(100,100,0,0)
p2.pointmass = createpointmass(100,150,0,0)
p3.pointmass = createpointmass(150,150,0,0)
p4.pointmass = createpointmass(150,100,0,0)

c.constraint = createconstraint(p1,p2)
c.constraint = createconstraint(p2,p3)
c.constraint = createconstraint(p3,p4)
c.constraint = createconstraint(p1,p4)

c.constraint = createconstraint(p1,p3)
c.constraint = createconstraint(p2,p4)

While Not KeyDown(1)
Cls


updatepointmasses()
updateconstraints()
drawconstraints()

Flip
Wend
End

Type pointmass
	Field x#
	Field y#
	Field ox#
	Field oy#
End Type

Function updatepointmasses()

	Color 255,255,255
	For p.pointmass = Each pointmass
		
		Oval p\x-4,p\y-4,8,8,0	;draws an oval at the verlet's position
		
		Local dx# = p\x - p\ox	;gets the velocity
		Local dy# = p\y - p\oy+.1		;added gravity here
		
		p\ox = p\x	;updates ox and oy
		p\oy = p\y
		
		p\x = p\x + dx#	;adds velocity to get new x and new y
		p\y = p\y + dy#
		
		If p\y &gt; GraphicsHeight() Then
			p\y = GraphicsHeight()
			dx = p\x-p\ox
			dx = dx*.5
			p\ox = p\ox +dx	;simulates friction
		EndIf
	Next

End Function


Function createpointmass.pointmass(x#,y#,vx#,vy#)	; x and y are coords for the verlet. vx and vy are velocity values for the verlet

p.pointmass = New pointmass
p\x = x
p\y = y
p\ox = x-vx	;gives the particle a starting velocity
p\oy = y-vy

Return p.pointmass

End Function


Type constraint
	Field p1.pointmass
	Field p2.pointmass
	Field length#
End Type

Function updateconstraints()
	
	For cnt = 0 To CONST_ITERATIONS-1	;this is necessary with many constraints to solve them correctly
		For c.constraint = Each constraint
			dist# = Sqr((c\p1\x-c\p2\x)^2 + (c\p1\y-c\p2\y)^2)	;distance formula
			
			diff# = dist#-c\length#	;shows the margin of error the update loop has created so it can be corrected
			
			dx# = c\p1\x-c\p2\x	;difference between x's and y's
			dy# = c\p1\y-c\p2\y
			
			If c\length &gt; 0 Then	;prevents a divided by 0 error that may occur
				diff = diff / c\length
			Else
				diff = 0
			EndIf
			
			dx = dx * .5
			dy = dy * .5
			
			c\p1\x = c\p1\x - (diff*dx)
			c\p1\y = c\p1\y - (diff*dy)
			
			c\p2\x = c\p2\x + (diff*dx)
			c\p2\y = c\p2\y + (diff*dy)
		Next
	Next
	
End Function



Function createconstraint.constraint(p1.pointmass,p2.pointmass)

	c.constraint = New constraint
	c\p1.pointmass = p1.pointmass
	c\p2.pointmass = p2.pointmass
	c\length# = Sqr((c\p1\x-c\p2\x)^2 + (c\p1\y-c\p2\y)^2)
	Return c.constraint

End Function

Function drawconstraints()

For c.constraint = Each constraint
	Line c\p1\x,c\p1\y,c\p2\x,c\p2\y
Next

End Function</textarea><br><br>Now the only thing that needs to be done is a collision engine... the hard part (for me at least) <br><br></td></tr></table><br>
<a name="957298"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tobo</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Impressive.<br><br>Helpful bit of reference material, this. <br><br></td></tr></table><br>
<a name="957450"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> thanks.  glad to know I helped someone understand it more :) <br><br></td></tr></table><br>
<a name="957451"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GIB3D</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Wow, that's really simple<br><br>Here's this, I added a triangle and movement controls just to see what would happen.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics 640,480,0,2
SetBuffer BackBuffer()

Global CONST_ITERATIONS = 5

Global p1.pointmass = createpointmass(100,100,0,0)
Global p2.pointmass = createpointmass(100,150,0,0)
Global p3.pointmass = createpointmass(150,150,0,0)
Global p4.pointmass = createpointmass(150,100,0,0)

Global t1.pointmass = createpointmass(200,50,0,0)
Global t2.pointmass = createpointmass(180,100,0,0)
Global t3.pointmass = createpointmass(220,100,0,0)

createconstraint(p1,p2)
createconstraint(p2,p3)
createconstraint(p3,p4)
createconstraint(p1,p4)

createconstraint(p1,p3)
createconstraint(p2,p4)

createconstraint(t1,t2)
createconstraint(t2,t3)
createconstraint(t3,t1)

While Not KeyDown(1)
Cls

updatepointmasses()
updateconstraints()
drawconstraints()

If KeyDown(200)
	t1\oy = t1\y+2
	t2\oy = t2\y+2
	t3\oy = t3\y+2
EndIf

If KeyDown(203)
	t1\ox = t1\x+2
EndIf

If KeyDown(205)
	t1\ox = t1\x-2
EndIf

Flip
Wend
End

Type pointmass
	Field x#
	Field y#
	Field ox#
	Field oy#
End Type

Function updatepointmasses()

	Color 255,255,255
	For p.pointmass = Each pointmass
		
		Oval p\x-4,p\y-4,9,9,0	;draws an oval at the verlet's position
		
		Local dx# = p\x - p\ox	;gets the velocity
		Local dy# = p\y - p\oy+.1		;added gravity here
		
		p\ox = p\x	;updates ox and oy
		p\oy = p\y
		
		p\x = p\x + dx#	;adds velocity to get new x and new y
		p\y = p\y + dy#
		
		If p\y &gt; GraphicsHeight() Then
			p\y = GraphicsHeight()
			dx = p\x-p\ox
			dx = dx*.5
			p\ox = p\ox +dx	;simulates friction
		EndIf
		
		
	Next

End Function


Function createpointmass.pointmass(x#,y#,vx#,vy#)	; x and y are coords for the verlet. vx and vy are velocity values for the verlet

p.pointmass = New pointmass
p\x = x
p\y = y
p\ox = x-vx	;gives the particle a starting velocity
p\oy = y-vy

Return p.pointmass

End Function


Type constraint
	Field p1.pointmass
	Field p2.pointmass
	Field length#
End Type

Function updateconstraints()
	
	For cnt = 0 To CONST_ITERATIONS-1	;this is necessary with many constraints to solve them correctly
		For c.constraint = Each constraint
			dist# = Sqr((c\p1\x-c\p2\x)^2 + (c\p1\y-c\p2\y)^2)	;distance formula
			
			diff# = dist#-c\length#	;shows the margin of error the update loop has created so it can be corrected
			
			dx# = c\p1\x-c\p2\x	;difference between x's and y's
			dy# = c\p1\y-c\p2\y
			
			If c\length &gt; 0 Then	;prevents a divided by 0 error that may occur
				diff = diff / c\length
			Else
				diff = 0
			EndIf
			
			dx = dx * .5
			dy = dy * .5
			
			c\p1\x = c\p1\x - (diff*dx)
			c\p1\y = c\p1\y - (diff*dy)
			
			c\p2\x = c\p2\x + (diff*dx)
			c\p2\y = c\p2\y + (diff*dy)
		Next
	Next
	
End Function



Function createconstraint.constraint(p1.pointmass,p2.pointmass)

	c.constraint = New constraint
	c\p1.pointmass = p1.pointmass
	c\p2.pointmass = p2.pointmass
	c\length# = Sqr((c\p1\x-c\p2\x)^2 + (c\p1\y-c\p2\y)^2)
	Return c.constraint

End Function

Function drawconstraints()

For c.constraint = Each constraint
	Line c\p1\x,c\p1\y,c\p2\x,c\p2\y
Next

End Function
</textarea> <br><br></td></tr></table><br>
<a name="957454"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> yeah it was even a learning experience for me, the creator because I realized some stuff that I was doing that had slowed my other physics engines down in the past... so here's how to give them a little starting velocity:<br><br><pre class=code>Global p1.pointmass = createpointmass(400,100,-2,0)
Global p2.pointmass = createpointmass(400,150,0,0)
Global p3.pointmass = createpointmass(450,150,0,0)
Global p4.pointmass = createpointmass(450,100,0,0)

Global t1.pointmass = createpointmass(200,50,0,3)
Global t2.pointmass = createpointmass(180,100,0,0)
Global t3.pointmass = createpointmass(220,100,0,0)</pre><br><br>put this before the loop<br><br>the whole concept is really a lot simpler than people make it out to be at first.  When I have time, I'll convert this to blitz max for the bmax tutorial section for those that have left blitz 3d <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
