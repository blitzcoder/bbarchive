<!DOCTYPE html><html lang="en" ><head ><title >Performance? Tips?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Performance? Tips?</h1><a href="forums.php" >Monkey Targets Forums</a>/<a href="topics.php?forum=502" >Android</a>/<a href="#bottom" >Performance? Tips?</a><br><br>
<a name="2001732"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Anyone else getting bad performance with their Android games?<br><br>Ive just updated my Invaders game to have around 25 sprites on screen and its near enough killed it!<br><br>Any tips to increase performance <br><br></td></tr></table><br>
<a name="2001739"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Xaron</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I guess you have to avoid as much dynamic created content as possible. So any "New" will be a killer. <br><br></td></tr></table><br>
<a name="2001741"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matt</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> We'd have to see your code in order to see where the bottlenecks are? <br><br></td></tr></table><br>
<a name="2001756"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Supertino</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Also what phone do you have, I have a wildfire and that is quite poor on any app.  I'll upgrade to a Desire in a month when y contract is up. <br><br></td></tr></table><br>
<a name="2001765"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BigAnd</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> You are not going to get massive fps on these devices. What I do on the iPhone is keep the fps to about 30 and see what sort of action I can get out of that. I am still finding my feet with Android and monkey so I am not getting the performance I was hoping for. I am sure this will change as we get to know how and when to do stuff with the language.<br><br>@therevills, can I ask, are you drawing the background image every frame and if so, what size is it? <br><br></td></tr></table><br>
<a name="2001830"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> So any "New" will be a killer.  <br></div><br><br>That would suck so much if that was true! :(<br><br><div class="quote"> We'd have to see your code in order to see where the bottlenecks are?  <br></div><br><br>Have a look at Monkey Pong, that also has similar issues.<br><br><div class="quote"> Also what phone do you have <br></div><br><br>LG Optimus One P500 - But Ive also tested on the Nexus One, the LG out performs it, which is strange since the LG has only 600Mhz CPU and the N1 has a 1Ghz CPU.<br><br>The Wildfire has a 500Mhz CPU, but also the screen res is smaller.<br><br><div class="quote"> <br>@therevills, can I ask, are you drawing the background image every frame and if so, what size is it?  <br></div><br><br>Yes I am displaying it every frame - is there another way?<br>Ive tried different size backgrounds thinking it might be this, but its the same when I try 640x480 down to 320x480 - also tried 320x240. <br><br></td></tr></table><br>
<a name="2001848"></a>

<a name="2001850"></a>

<a name="2001851"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BigAnd</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> On platforms like the Xbox its not good to create "new" objects all the time as it makes the garbage collector take a LOT of time, so it might be true on other platforms. Worth doing some tests.<br><br>Thanks for the background screen info. I was just thinking you might have been doing something like dirty rectangles or something else, just checking :) <br><br></td></tr></table><br>
<a name="2001913"></a>

<a name="2001921"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just been doing some testing:<br><br>With collisions disabled, 9 aliens, no asteroids and the player's ship (not shooting):<br><br>* DrawText on: 31-34 FPS<br>* DrawText off apart from the FPS Counter: 35-41 FPS<br><br>With just the player's ship:<br><br>* DrawText on: 50-55 FPS<br>* DrawText off apart from the FPS Counter: 53-58 FPS<br><br>With Collsions on, 9 aliens, asteroids and the player's ship (not shooting):<br><br>* DrawText on: 28-31FPS<br>* DrawText off apart from the FPS Counter: 35-37 FPS<br><br>I was going to add a tilemap to test game... but I dont think Ill bother now :(<br><br>And heres the source code:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

import mojo

Global myApp:MyApp
Global SCREEN_WIDTH%
Global SCREEN_HEIGHT%
Global dt:DeltaTimer
Global debug:Bool = false

Const STATE_MENU% = 1
Const STATE_PLAYING% = 2
Const STATE_PAUSED%	= 3

Const ASTEROID% = 0
Const ALIEN1% = 1

Function Main:Int()
	myApp = new MyApp()
	Return 0
End Function

Class MyApp extends App
	Field player:Player
	Field backgroundImg:Image
	Global bulletImg:Image ' GLOBAL CHANGE LATER
	Global lazerSnd:Sound ' GLOBAL CHANGE LATER
	
	Field playerLifeImg:Image
	Field enemy1Img:Image
	Field rusherImg:Image
	Field explode32Img:Image
	Field explode64Img:Image
	Field playerImg:Image
	Field fpsCount:Int
	Field startTime:Int
	Field totalFPS:Int
	Field boomSnd:Sound
	
	Global GameState:Int = STATE_MENU
	Field menu:Image
	
	Method SetState:Void(state:Int)
		GameState = state
		Select GameState
			Case STATE_MENU
			'	Print "Click mouse to start"
			Case STATE_PLAYING
			'	Print "Press ESC to exit or P to pause"
			Case STATE_PAUSED
			'	Print "Press P or ESC to continue"
			Default
				Print "ERROR: Unknown game state!"
		End
	End
	
	Method OnCreate:Int()
		' Store the device width and height
		SCREEN_WIDTH = DeviceWidth()
		SCREEN_HEIGHT = DeviceHeight()	
		Seed = Millisecs()
		dt = New DeltaTimer(60)
		SetUpdateRate 60

		menu = LoadImage ("menu.png")
		playerImg =  LoadImage("Ship1.png", 7, Image.MidHandle)
		
		playerLifeImg = LoadImage("PULife.png")
		
		bulletImg = LoadImage("missile.png", 2, Image.MidHandle)
		
		local img:Image = LoadImage( "GAster32.png")
		enemy1Img = img.GrabImage( 0, 0, 32, 32, 16, Image.MidHandle)
		
		img = LoadImage( "explode32.png")
		explode32Img = img.GrabImage( 0, 0, 32, 32, 8, Image.MidHandle)
		
		img = LoadImage( "explode64.png")
		explode64Img = img.GrabImage( 0, 0, 64, 64, 10, Image.MidHandle)
		
		img = LoadImage("rusher.png")
		rusherImg = img.GrabImage(0, 0, 64, 32, 24, Image.MidHandle)
		
		backgroundImg = LoadImage("galaxy2.png")

		resetLevel()
		
		' Set the font
		SetFont(LoadImage("font_16.png",16,16,64))		

	
		#if TARGET="flash"
			boomSnd=LoadSound( "boom3.mp3" )
			lazerSnd=LoadSound("lazer.mp3")
		#else If TARGET="android"
			boomSnd=LoadSound( "boom3.ogg" )
			lazerSnd=LoadSound("lazer.ogg")
		#else
			boomSnd=LoadSound( "boom3.wav" )
			lazerSnd=LoadSound("lazer.wav")
		#endif
		
		Return 0
	End Method
	
	Method resetLevel:Void()
		if Bullet.list then Bullet.list.Clear()
		if Enemy.list then Enemy.list.Clear()
		if Explosion.list then Explosion.list.Clear()
				
		player = new Player(playerImg, SCREEN_WIDTH / 2, SCREEN_HEIGHT - playerImg.Height())
		player.frame  =  3
		player.rotationSpeed = 2
		player.speedX = 1
		player.maxXSpeed = 5
		player.lifeImage = playerLifeImg
		
		nextLevel(player.level)		
	End Method
	
	Method OnRender:Int()
		DrawImage(backgroundImg, 0, 0)
		Select GameState

			Case STATE_MENU
				DrawImage menu, (DeviceWidth () * 0.5) - (menu.Width () * 0.5), (DeviceHeight () * 0.5) - (menu.Height () * 0.5)
				
			Case STATE_PLAYING
				drawGame()	
			Case STATE_PAUSED
				drawGame()

		   Default
		   		Cls 255, 0, 0 ' Unknown state -- red screen of death!
		End
		
		Return 0
	End Method
	
	Method drawGame:Void()
		Bullet.drawAll()
		Explosion.drawAll()
		Enemy.drawAll()
		player.draw()
		drawGUI()		
	End Method
	
	Method OnLoading:Int()
		Cls 0,0,0
		DrawText("Loading", SCREEN_WIDTH / 2, SCREEN_HEIGHT / 2, 0.5, 0)
		return 0
	End Method
	
	Method OnUpdate:Int()
		dt.UpdateDelta()
		
		Select GameState
			Case STATE_MENU
				If KeyHit (KEY_LMB) or TouchHit(0) or KeyHit(KEY_SPACE)
			   		SetState STATE_PLAYING
				Endif
	
				' In game...
	
			Case STATE_PLAYING
	
				countFPS()
				
				player.controls()
				Bullet.updateAll()
				Enemy.updateAll()
				Explosion.updateAll()
				createEnemy()
				checkCollisions()		
			
				If KeyHit (KEY_ESCAPE) ' back button on android?!?!?
				   SetState STATE_MENU
				   self.resetLevel()
				Endif
	
				If KeyHit (KEY_P)
			   		SetState STATE_PAUSED
				Endif
		
			Case STATE_PAUSED
	
				If KeyHit (KEY_ESCAPE) Or KeyHit (KEY_P)
				   SetState STATE_PLAYING
				Endif
		End
	

		Return 0
	End Method
		
	#rem
	Method OnSuspend:Int()
		Return 0
	End Method
	
	Method OnResume:int()
		Return 0
	End Method
	#end
	
	Method checkCollisions:Void()
		if not Enemy.list return
		local ex:Explosion
		for local e:Enemy = EachIn Enemy.list
			if Bullet.list then
				for local b:Bullet = eachin Bullet.list
					if b.collide(e)
						if e.type = ASTEROID
							ex = new Explosion(self.explode32Img, e.x, e.y)
							ex.maxFrame = 7
						elseif e.type = ALIEN1
							ex = new Explosion(self.explode64Img, e.x, e.y)
							ex.maxFrame = 9
						endif 
						Bullet.list.Remove(b)
						Enemy.list.Remove(e)					
						player.score+=10
						PlaySound(boomSnd, 3)
						Exit
					EndIf
				Next
			endif
			if e.collide(player)
				ex = new Explosion(self.explode32Img, e.x, e.y)
				ex.maxFrame = 7
				Enemy.list.Remove(e)
				player.lives-=1
				PlaySound(boomSnd, 2)
				if player.lives &lt; 0
					killPlayer()
				EndIf
				Exit
			EndIf
		Next
		
		'count aliens
		local cnt% = 0
		for local a:Enemy = EachIn Enemy.list
			if a.type = ALIEN1
				cnt+=1
			EndIf
		Next
		if cnt = 0
			player.level+=1
			nextLevel(player.level)
		EndIf
	End Method
	
	Method nextLevel:Void(level%)
		'create a few enemies
		local maxAliensAcross%
		local maxAliensDown%
		
		#if TARGET="android"
			maxAliensAcross = 2
			maxAliensDown = 2
		#else
			maxAliensAcross = 7
			maxAliensDown = 4		
		#endif
		
		local e:Enemy
		for local i% = 0 to maxAliensAcross
			for local j% = 0 to maxAliensDown
				e = new Alien(rusherImg, 64 * i, (40 * j) + 34)
				e.frame = 0
				e.maxFrame = 23
				e.dy = 0
				e.dx = 1 * (level) 
				e.movement = 1
				e.type = ALIEN1
			next
		next
	End Method
	
	Method killPlayer:Void()
		resetLevel()
		SetState STATE_MENU		
	End Method
	
	Method countFPS:Void()
		if Millisecs()&gt;startTime+1000
			totalFPS = fpsCount
			fpsCount = 0
			startTime = Millisecs()
		EndIf
		fpsCount+=1		
	End Method	
	
	Method createEnemy:Void()	
		If Rnd(0,10000)&lt;100 * dt.delta
			local e:Enemy
			e = new Enemy(enemy1Img, Rnd(enemy1Img.Width(), SCREEN_WIDTH - enemy1Img.Width()), -enemy1Img.Height())
			e.frame = 0
			e.maxFrame = 15
			e.type = ASTEROID
		EndIf
	End Method
	
	Method drawGUI:void()
		DrawText("SCORE", 10, 10, 0, 0)
		DrawText(player.score, 10 + (5*16), 28, 1, 0) ' right-aligned

		DrawText("LEVEL", SCREEN_WIDTH / 2 , 10, 0.5, 0)
		DrawText(player.level, SCREEN_WIDTH / 2, 28, 0.5, 0) ' right-aligned
		
		DrawText("FPS", SCREEN_WIDTH - 10, 10, 1, 0) ' right-aligned
		DrawText(totalFPS, SCREEN_WIDTH - 10, 28, 1, 0) ' right-aligned
	End Method
End Class

Class Explosion Extends Sprite
	Global list:List&lt;Explosion&gt;

	Method New(img:Image, x#, y#)
		Super.New(img, x, y)
		if list = null then list = new List&lt;Explosion&gt;
		self.frame = 0
	'	self.maxFrame = mxframe
		self.maxFrameDelay = 2
		list.AddLast self
	End Method
	
	Function updateAll:Void()
		If not list Return
		For local b:Explosion = Eachin list
			b.update()
		Next
	End Function

	Method update:Void()
		frameDelay+=1
		If frameDelay &gt; maxFrameDelay
			self.alpha-=.05 * dt.delta
			frame+=1
			frameDelay = 0
			if frame &gt; maxFrame
				list.Remove(self)
			EndIf
		EndIf
	end Method	
		
	Function drawAll:Void()
		If not list Return
		For local b:Explosion = Eachin list
			b.draw()
		Next		
	End Function	
End Class

Class Alien Extends Enemy
	Method New(img:Image, x#, y#)
		Super.New(img, x, y)
		if list = null then list = new List&lt;Enemy&gt;
		self.frame = 0
		self.dy = Rnd(1, 3)
		list.AddLast self
	End Method
	
	Method update:Void()
		Super.update()
		
		Select movement
			Case 1
				if x &gt; SCREEN_WIDTH
					dx = -dx
					x = SCREEN_WIDTH
					moveCounter = 50
					dx*=1.2
				else if x &lt; 0
					dx = -dx
					x = 0
					moveCounter = 50
					dx*=1.2
				EndIf
				if moveCounter &gt; 1
					dy = 1
					moveCounter-=1 * dt.delta
				else
					dy = 0
					moveCounter = 0
				end if
				
		End Select
			
	End Method

	
End Class

Class Enemy Extends Sprite
	Global list:List&lt;Enemy&gt;
	Field movement:Int
	Field moveCounter#
	field type%
	
	Method New(img:Image, x#, y#)
		Super.New(img, x, y)
		if list = null then list = new List&lt;Enemy&gt;
		self.frame = 0
		self.dy = Rnd(1, 3)
		list.AddLast self
	End Method
	
	Function updateAll:Void()
		If not list Return
		For local b:Enemy = Eachin list
			b.update()
		Next
	End Function
	
	Method update:Void()
		animate()
		move()
		if y &gt; SCREEN_HEIGHT + h
			list.Remove(self)
		EndIf
	End Method
	
	Function drawAll:Void()
		If not list Return
		For local b:Enemy = Eachin list
			b.draw()
		Next		
	End Function
End Class

Class Bullet Extends Sprite
	Global list:List&lt;Bullet&gt;
	
	Method New(img:Image, x#, y#)
		Super.New(img, x, y)
		if list = null then list = new List&lt;Bullet&gt;
		self.speedY = -6
		list.AddLast self
	End Method
	
	Function updateAll:Void()
		If not list Return
		For local b:Bullet = Eachin list
			b.update()
		Next
	End Function
	
	Method update:Void()
		self.animate()
		dy = self.speedY
		move()
		if y &lt; -h then
			list.Remove(self)
		EndIf
	End Method
	
	Function drawAll:Void()
		If not list Return
		For local b:Bullet = Eachin list
			b.draw()
		Next		
	End Function
	
End Class

Class Player Extends Sprite
	Field score%
	Field lives%
	Field lifeImage:Image
	Field level%
	
	Method New(img:Image, x#, y#)
		Super.New(img, x, y)
		score = 0
		lives = 3
		level = 1
	End Method
	
	Method controls:Void()
		#If TARGET="android" then
			if Abs(AccelX())&gt;0.1
				dx += (AccelX() * speedX)
				If AccelX() &lt; 0
					rollLeft()
				EndIf
				if AccelX() &gt; 0
					rollRight()
				EndIf
			else
				slowShip()
			endif
			If TouchHit(0) then
				fireBullet()
			EndIf
		#Else	
			if KeyDown(KEY_LEFT)
				self.dx-=self.speedX
				rollLeft()
			else if KeyDown(KEY_RIGHT)
				self.dx+=self.speedX
				rollRight()
			else
				slowShip()
			EndIf
			if KeyHit(KEY_SPACE)
				fireBullet()			
			EndIf			
		#End
		if dx &gt; self.maxXSpeed
			dx = self.maxXSpeed
		EndIf
		if dx &lt; -self.maxXSpeed
			dx = -self.maxXSpeed
		EndIf		
		
		self.move()
		' limit the player to the screen
		if x &lt; 0 + w2 then
			x = w2
		EndIf
		if x &gt; SCREEN_WIDTH - w2 then
			x = SCREEN_WIDTH - w2
		EndIf
	End Method
	
	Method fireBullet:Void()
		local b:=New Bullet(MyApp.bulletImg, x, y - self.h2)
		b.maxFrame = 1
	
	'	local b:=New Bullet(MyApp.bulletImg, x-15, y)
	'	b.maxFrame = 1
	'	b = New Bullet(MyApp.bulletImg, x+15, y)
	'	b.maxFrame = 1
		PlaySound MyApp.lazerSnd, 1
	End Method	
	
	Method rollLeft:Void()
		If frame &gt; 0
			frameDelay+=1
			If frameDelay &gt; maxFrameDelay
				frame-=1
				frameDelay = 0
			EndIf
		EndIf					
	End Method
	
	Method rollRight:Void()
		If frame &lt; 6
			frameDelay+=1
			If frameDelay &gt; maxFrameDelay
				frame+=1
				frameDelay = 0
			EndIf
		EndIf	
	End Method	
					
	Method slowShip:Void()
		If frame &lt; 3
			frameDelay+=1
			If frameDelay &gt; maxFrameDelay
				frame+=1
				frameDelay = 0
			EndIf
		Else If frame &gt; 3
			frameDelay+=1
			If frameDelay &gt; maxFrameDelay
				frame-=1
				frameDelay = 0
			EndIf			
		EndIf
		if dx &gt; 0
			dx-=self.speedX/4
		else if dx &lt; 0
			dx+=self.speedX/4
		EndIf
	End Method

	Method draw:Void()
		SetAlpha 0.5
		for local i% = 1 to self.lives
			DrawImage(lifeImage, (i - 1) * 34 + 12, y)
		Next
		SetAlpha 1
		Super.draw()
	End Method
	
End Class

Class Sprite
	Field x#,y#
	Field dx#, dy#
	Field speedX#, speedY#
	Field maxXSpeed#, maxYSpeed#
	Field image:Image
	Field scaleX# = 1, scaleY# = 1
	Field rotation#, rotationSpeed#
	Field red%, green%, blue%, alpha#
	Field w%, h%
	Field w2%, h2%
	Field hitBoxX:Int = 0
	Field hitBoxY:Int = 0
	Field hitBoxWidth:Int
	Field hitBoxHeight:Int
	Field frame:Int
	Field frameDelay%
	Field maxFrameDelay% = 3
	field maxFrame%
		
	Method New(img:Image,x#, y#)
		Self.image = img
		Self.x = x
		Self.y = y
		Self.w = img.Width()
		Self.h = img.Height()
		Self.w2 = Self.w/2
		Self.h2 = Self.h/2
		self.alpha = 1
		self.SetHitBox(-w2, -h2, w, h)
	End Method
	
	Method setRGB:Void(r%,g%,b%)
		Self.red = r
		Self.green = g
		Self.blue = b
	End Method
	
	Method move:Void()
		Self.x+=Self.dx * dt.delta
		Self.y+=Self.dy * dt.delta
	End Method
	
	Method draw:Void()
		SetAlpha self.alpha
		SetColor red, green, blue ' doesnt work with images!?!??!
		DrawImage(image, x, y, rotation, scaleX, scaleY, frame)
		SetColor 255, 255, 255
		SetAlpha 1
		if debug
			drawRectOutline(x + hitBoxX, y + hitBoxY, hitBoxWidth, hitBoxHeight)
		endif
	End Method
	
	Method collide:Int(sprite:Sprite)
		Return rectsOverlap(x + hitBoxX, y + hitBoxY, hitBoxWidth, hitBoxHeight, 
							sprite.x + sprite.hitBoxX, sprite.y + sprite.hitBoxY, sprite.hitBoxWidth, sprite.hitBoxHeight)
	End Method
		
	Method SetHitBox:Void(hitX:Int, hitY:Int, hitWidth:Int, hitHeight:Int)
		hitBoxX = hitX
		hitBoxY = hitY
		hitBoxWidth = hitWidth
		hitBoxHeight = hitHeight
	End Method
	
	method animate:Void()
		frameDelay+=1
		If frameDelay &gt; maxFrameDelay
			frame+=1
			frameDelay = 0
			if frame &gt; maxFrame
				frame = 0
			EndIf
		EndIf		
	End Method
End

Class DeltaTimer
	Field targetfps:Float = 60
	Field currentticks:Float
	Field lastticks:Float
	Field frametime:Float
	Field delta:Float
	
	Method New (fps:Float)
		targetfps = fps
		lastticks = Millisecs()
	End
	
	Method UpdateDelta:Void()
		currentticks = Millisecs()
		frametime = currentticks - lastticks
		delta = frametime / (1000.0 / targetfps)
		lastticks = currentticks
	End
End

Function rectsOverlap:Int(x0:Float, y0:Float, w0:Float, h0:Float, x2:Float, y2:Float, w2:Float, h2:Float)
	If x0 &gt; (x2 + w2) Or (x0 + w0) &lt; x2 Then Return False
	If y0 &gt; (y2 + h2) Or (y0 + h0) &lt; y2 Then Return False
	Return True
End Function

Function drawRectOutline:Void(x:Int, y:Int, w:Int, h:Int)
	w -= 1
	h -= 1
	DrawLine(x,y,x+w,y)
	DrawLine(x+w,y,x+w,y+h)
	DrawLine(x+w,y+h,x,y+h)
	DrawLine(x,y+h,x,y)	
End Function
</textarea> <br><br></td></tr></table><br>
<a name="2001945"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Why do you use SetColor?<br><br>Other questions: <br><br>when an object is removed from a list (ie: bullets, destroyed aliens...) they still lives around? I put in my code alien=NULL, bullet=NULL to be sure (I dont' really know if this is the *right* way do it or GC on different target acts in different way).<br><br>Have you tried to 'turn off' accel/touch to see how much cycles it costs? <br><br></td></tr></table><br>
<a name="2001949"></a>

<a name="2001953"></a>

<a name="2001956"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> &gt;SetColor?<br><br>In case Monkey ever gets it for DrawImage :) I would like to make the player's ship flash red when hit.<br><br>&gt;When an object is removed from a list...<br><br>Not sure, I'm just doing what I've done in the past using BlitzMax.<br><br>&gt; I put in my code alien=NULL, bullet=NULL to be sure <br><br>Where do you put this code?<br><br>&gt;Have your tried to 'turn off' accel/touch<br><br>Is there a command for this? How do you do it? <br><br></td></tr></table><br>
<a name="2001959"></a>

<a name="2001960"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> About deleting object:<br>Once <br><pre class=code>
list_bullets.Remove(e)
e=NULL
</pre><br>Of course I have a control <b>If e&lt;&gt;NULL</b> to check if the object exists in other loops.<br>I dont' know if is the 'correct' way to do it, but in my games I can have hundreds of bullets (mine and of the enemies) so somewhere this objects are...<br><br>Have your tried to 'turn off' accel/touch<br><br>No, I simply remarked the section #IF TARGET="android" to test it <br><br></td></tr></table><br>
<a name="2001965"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sorry degac, I was asking if you know how to turn off the accel/touch.<br><br>Even if you dont call the commands for accel/touch it seems like it is always polling for them (at least the touch command).<br><br>Check this post out:<br><br><a href="http://www.monkeycoder.co.nz/Community/posts.php?topic=227" target="_blank">http://www.monkeycoder.co.nz/Community/posts.php?topic=227</a> <br><br></td></tr></table><br>
<a name="2001964"></a>

<a name="2001969"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Maybe it's possible to change mojo.android.java to 'skip' the touchEvent...<br>I'm not a java expert, but I think a 'global var' somewhere and a if (skip_touch==true) at the start should do the trick.<br><br>But here we need someone skilled... sorry :)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
public boolean onTouchEvent( MotionEvent event ){
			if( app==null ) return false;
			
			gxtkInput input=app.input;
			
			int action=event.getAction();
			int maskedAction=action &amp; MotionEvent.ACTION_MASK;
			int pid=0;
			
			if( maskedAction==MotionEvent.ACTION_POINTER_DOWN || maskedAction==MotionEvent.ACTION_POINTER_UP ){
				pid=event.getPointerId( action&gt;&gt;MotionEvent.ACTION_POINTER_ID_SHIFT );
			}else{
				pid=event.getPointerId( 0 );
			}
			
			switch( maskedAction ){
			case MotionEvent.ACTION_DOWN:
			case MotionEvent.ACTION_POINTER_DOWN:
				input.OnKeyDown( pid+gxtkInput.KEY_TOUCH0 );
				break;
			case MotionEvent.ACTION_UP:
			case MotionEvent.ACTION_POINTER_UP:
				input.OnKeyUp( pid+gxtkInput.KEY_TOUCH0 );
				break;
			}
			
			for( int i=0;i&lt;event.getPointerCount();++i ){
				int pid2=event.getPointerId(i);
				input.touchX[pid2]=event.getX(i);
				input.touchY[pid2]=event.getY(i);
			}
	
			return true;
		}
</textarea> <br><br></td></tr></table><br>
<a name="2002049"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MikeHart</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Like Xaron said. Pre allocate as much as possible and store it for later use. The dalvik GC is a nasty one. It can drag your framerate down very much. <br><br></td></tr></table><br>
<a name="2002134"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm reading <a href="http://developer.android.com/guide/practices/design/performance.html" target="_blank">this</a><br><br>From what I've understood create an object takes 1) time 2) involve a major work of GC (same for string for some reason...)<br><br>So one solution is to create a 'pool' of object onCreate method (an array?) with a field 'status=1/0' to understand if it is active or not (I dont' know if removing from a list or scanning an array of 100 elements has a cost or not).<br>I will try. <br><br></td></tr></table><br>
<a name="2002136"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Beaker</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Currently the Android mojo uses a canvas to draw with. The speed could be improved alot if it used OpenGL instead (particularly VBO buffers). <br><br></td></tr></table><br>
<a name="2002141"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> I suppose Mark choosed the 'basic' canvas method because not-all Android devices support OpenGL (specially VBO).<br>And Canvas operation are CPU based (maybe I'm wrong or it depends on the AndroidOS version...)<br>After googling around I found that - practically - every games written for Android is OpenGL based... so a word from Mark about this could be interesting. <br><br></td></tr></table><br>
<a name="2002429"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Samah</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> So, I messed around with the native mojo stuff (mojo.android.java) and managed get a significant performance boost.<br>Currently, every DrawImage call using an atlas rather than just a single frame will create a new Bitmap object to hold the source rectangle.<br>Android has a built-in drawBitmap call that will handle this for you without creating a new Bitmap.  I've converted the code to use that call, and cached the rectangle objects, so that's at least one less new() per DrawImage. :)<br>Using some test code by therevills, I managed to get another 10fps back on my Nexus One, and over 20fps boost on his LG.<br>I've made some other minor tweaks that should help, but I won't bother posting those here just now.<br><br>Open mojo.android.java:<br><br>Find the line that says:<br><pre class=code>class gxtkGraphics{</pre><br>Add this after it:<br><pre class=code>Rect srcRect = new Rect();
Rect destRect = new Rect();</pre><br><br>Find these lines in the DrawSurface2() method:<br><pre class=code>Bitmap bitmap=Bitmap.createBitmap( surface.bitmap,srcx,srcy,srcw,srch ); 
canvas.drawBitmap( bitmap,x,y,bitmapPaint );</pre><br>Replace them with these:<br><pre class=code>srcRect.left = srcx;
srcRect.top = srcy;
srcRect.right = srcx + srcw;
srcRect.bottom = srcy + srch;
destRect.left = (int)x;
destRect.top = (int)y;
destRect.right = destRect.left + srcw;
destRect.bottom = destRect.top + srch;
canvas.drawBitmap(surface.bitmap, srcRect, destRect, bitmapPaint);</pre><br><br>Compile your app and enjoy!<br><br>@degac: In Java and many other OO languages, String is a class, not a primitive.  Having said that, Dalvik should be reusing strings from the string pool whenever it can. <br><br></td></tr></table><br>
<a name="2002434"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BigAnd</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> Good work Samah. <br><br>Its very good practice on Android (and iPhone) to keep as many graphics on one atlas as possible, everything if you can or split them up for say, menu and in game. So this is a great change and worthwhile doing. <br><br></td></tr></table><br>
<a name="2002436"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> Holy crap!<br>My crappy-game is now 80-132 while before the max frame rate was 85-90!!!<br>Even on the emulator it runs from 14/16 to 27/29!<br><br>Big thanks!!! <br><br></td></tr></table><br>
<a name="2002479"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice work Samah :) <br><br></td></tr></table><br>
<a name="2002493"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >marksibly</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>Nice fix!<br><br>I wonder why I didn't notice that version of drawBitmap the first time around - I remember looking for it too.<br><br>However, everyone should be relieved to hear there is an opengl driver coming soon, and that if should be a LOT faster. <br><br></td></tr></table><br>
<a name="2002501"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#23">[#23]</a></td></tr></table></td></tr><tr ><td class="posttext"> awesome! <br><br></td></tr></table><br>
<a name="2002514"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#24">[#24]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> an opengl driver coming soon <br></div><br><br>Now this is awesome!! Any luck with the Touch slowness? <br><br></td></tr></table><br>
<a name="2002515"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >marksibly</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#25">[#25]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>&gt; Any luck with the Touch slowness? <br><br>Well, I couldn't duplicate it from your demo - firepaint didn't seem to slow down when I touched the display at all.<br><br>Also, there's not a lot I can do about it - I don't set 'touch rate' or 'enable touch' or anything, just process the touch events.<br><br>If anyone has any ideas I'm all ears though. <br><br></td></tr></table><br>
<a name="2002516"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#26">[#26]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ive added some more details on the bug thread:<br><br><a href="http://www.monkeycoder.co.nz/Community/posts.php?topic=227" target="_blank">http://www.monkeycoder.co.nz/Community/posts.php?topic=227</a> <br><br></td></tr></table><br>
<a name="2002577"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tibit</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#27">[#27]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> an opengl driver coming soon  <br></div><br>Awesome, I'll wait for this. Will probably solve every performance issue on Andriod atm! :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
