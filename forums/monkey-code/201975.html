<!DOCTYPE html><html lang="en" ><head ><title >AdamRedwoods Animation Tween Module</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >AdamRedwoods Animation Tween Module</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >AdamRedwoods Animation Tween Module</a><br><br>
<a name="2018890"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamRedwoods</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's a tween module for people to use. It's robust, but requires some setup on the programmers part. I didn't make an elegant class package (for various reasons), but it's not hard to do.<br><br>Features:<br> - layerable to create custom effects<br> - reversable, hold on last state<br> - written with performance in mind<br> - Added SetAlphaINOUT(handle, step, duration in ms ) for easy fadein/out of images<br><br>Todo:<br> - color tweens<br> - wrap another class around this to make tweens automatic<br><br>Usage:<br> - tweens are in format Command(unique handle, step, start, end, starty, endy)<br> - layer tweens in order: Translate, Scale, Rotate<br> - default setting is EaseOut, optional EaseIn available<br> - you need to make global handles for each tween (a simple process), it's nice to keep them in a class as a global<br><br>Example:<br><pre class=code>
''Do this however you want, but you need to provide unique integers per tween set
Class Tweennum
	Global tweenForImage1:int = 1
	Global othertween:int = 2
End

'' (in Main Routine)
''create a trigger and begin the tween

If someTrigger
	Tween.Begin(Tweennum.tweenForImage1)
	Tween.EaseIn(Tweennum.tweenForImage1)
	someTrigger =0
EndIf

If Tween.IsActive(Tweennum.tweenForImage1)
	''if you need x,y position set that before rotation
	Translate(x,y)

	Tween.Rotate(Tweennum.tweenForImage1, 0.1, 0, 360)
	
	''will fade in as well
	Tween.SetAlpha(Tweennum.tweenForImage1, 0.1, 0.0, 1.0)
	
	DrawImage image1,0,0
	
	''restore will wipe out any previous matrix commands, colors, and alphas, and return to normal drawing operations
	Tween.Restore()
	
	''to chain tweens, add another trigger:
	If Not Tween.IsActive(Tweennum.tweenForImage1)
		Tween.Begin(Tweennum.othertween)
	endif

Else
	''hold image when finished use this:
	Translate(x,y)
	UseFinalState(Tweennum.tweenForImage1)
	DrawImage image1,0,0
	Tween.Restore()

EndIf
</pre><br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'' Animation Tween class
'' Adam of the Redwoods 2011

''usage:
'' - layer tweens in order: Translate, Scale, Rotate
'' - default set to EaseOut
'' - make global handles for each tween (you need to do this yourself)
'' - it's nice to keep them in the class as a global

Import mojo

Class Tween
	Const MAXTWEENOBJ:Int = 30 ''up to 30 sets, add more if you like
	
	Const TWALPHA:Int = 1
	Const TWTRANS:Int = 2
	Const TWROT:Int = 4
	Const TWSCALE:Int = 8
	
	Global tween:TweenObj[MAXTWEENOBJ]
	
	
	
	Function SetAlpha:Float(  var:Int, st:Float, startx:Float=0.0, endx:Float=1.0)
		If Not tween[var] Then Return

		If tween[var].dir &lt; 0
			Local temp:Float
			temp = startx
			startx = endx
			endx = temp
		Endif
		
		tween[var].otherTweens = tween[var].otherTweens | TWALPHA
		
		mojo.SetAlpha(tween[var].alpha)
		
		Local dif:Float = (startx - endx)
		Local a:Float
		
		If tween[var].easing &gt;= 0
			a = tween[var].alpha - dif*st
		Else
			If dif &lt;0 Then a = tween[var].alpha + tween[var].alpha *st Else a = tween[var].alpha - tween[var].alpha *st
		Endif

		If (dif&lt;0.0) And a &gt; endx Then a=endx Elseif (dif&gt;0.0) And (a-0.01)&lt;endx Then a=endx
		tween[var].alpha = a
		
		''turn off only is there's no other tweens
		
		If a=endx And Not tween[var].alphaAction
			If Not ( (~ TWALPHA) &amp; tween[var].otherTweens) Then tween[var].dir=0
			tween[var].otherTweens = (~ TWALPHA) &amp; tween[var].otherTweens
		'Elseif a=endx And tween[var].action =2
			'tween[var].dir = tween[var].dir * -1
		Endif
		
		Return a
	End
	
	Function Translate:Void(  var:Int, st:Float, startx:Float=0.0, endx:Float=0.0, starty:Float=0.0, endy:Float=0.0)
		If Not tween[var] Then Return
		
		If tween[var].dir &lt; 0
			Local temp:Float
			temp = startx
			startx = endx
			endx = temp
		Endif
		
		tween[var].otherTweens = tween[var].otherTweens | TWTRANS
		
		Local difx:Float = startx - endx
		Local dify:Float = starty - endy
		Local x:Float, y:Float
		
		mojo.Transform(1.0,0,0,1.0,-tween[var].x + startx,-tween[var].y + starty)
		
		tween[var].lastx = -tween[var].x + startx
		tween[var].lasty = -tween[var].y + starty
		
		If tween[var].easing &gt;= 0
			x = tween[var].x - (tween[var].x - difx)*st 
			y = tween[var].y - (tween[var].y - dify)*st
		Else
			Local kick:Float = 0.0 ''keep values from getting too small
			If difx&lt;0 Then kick = -0.1 Else If difx&gt;0 Then kick = 0.1
			x = tween[var].x + (tween[var].x)*st +kick
			kick = 0.0
			If dify&lt;0 Then kick = -0.1 Else If dify&gt;0 Then kick = 0.1
			y = tween[var].y + (tween[var].y)*st +kick
		Endif
		
	'Print "t:"+x+" d "+(-difx)
		If (difx&lt;0.0) And x &gt; -difx Then x=-difx Elseif (difx&gt;0.0) And x&gt;difx Then x=difx 
		If (dify&lt;0.0) And y &gt; -dify Then y=-dify Elseif (dify&gt;0.0) And y&gt;dify Then y=dify
		
		tween[var].x = x
		tween[var].y = y
		
		''turn off only is there's no other tweens
		If Abs(tween[var].x - difx) &lt; 1.0 And Abs(tween[var].y - dify) &lt; 1.0 And Not tween[var].transAction
			If Not ( (~ TWTRANS) &amp; tween[var].otherTweens) Then tween[var].dir = 0
			tween[var].otherTweens = (~ TWTRANS) &amp; tween[var].otherTweens
		Endif
		
		Return
	End
	
	Function Scale:Void(  var:Int, st:Float, startx:Float=1.0, endx:Float=2.0, starty:Float=1.0, endy:Float=1.0)
		If Not tween[var] Then Return
		
		If tween[var].dir &lt; 0
			Local temp:Float
			temp = startx
			startx = endx
			endx = temp
		Endif
		
		tween[var].otherTweens = tween[var].otherTweens | TWSCALE
		
		Local difx:Float = startx - endx
		Local dify:Float = starty - endy
		Local x:Float, y:Float
		
		mojo.Scale(-tween[var].scalex + startx,-tween[var].scaley + starty)
		
		tween[var].lastscx = -tween[var].scalex + startx
		tween[var].lastscy = -tween[var].scaley + starty
		
		If tween[var].easing &gt;= 0
			x= tween[var].scalex - (tween[var].scalex - difx)*st 
			y= tween[var].scaley - (tween[var].scaley - dify)*st
		Else
			Local kick:Float = 0.0 ''keep values from getting too small
			If difx&lt;0 Then kick = -0.005 Else If difx&gt;0 Then kick = 0.005
			x= tween[var].scalex + (tween[var].scalex )*st + kick
			kick =0.0
			If dify&lt;0 Then kick = -0.005 Else If dify&gt;0 Then kick = 0.005
			y= tween[var].scaley + (tween[var].scaley )*st + kick
		Endif 
		
	'Print "s:"+x+" d "+difx
		If (difx&lt;0.0) And x &lt; difx Then x=difx Elseif (difx&gt;0.0) And x&gt;difx Then x=difx 
		If (dify&lt;0.0) And y &lt; dify Then y=dify Elseif (dify&gt;0.0) And y&gt;dify Then y=dify
		
		tween[var].scalex = x
		tween[var].scaley = y
			
		''turn off only is there's no other tweens
		If Abs(x - difx) &lt; 0.1 And Abs(y - dify) &lt; 0.1 And Not tween[var].scaleAction
			If Not ( (~ TWSCALE) &amp; tween[var].otherTweens) Then tween[var].dir = 0 
			tween[var].otherTweens = (~ TWSCALE) &amp; tween[var].otherTweens
		Endif
		
		Return
	End
	
	Function Rotate:Float(  var:Int, st:Float, startx:Float=0.0, endx:Float=360.0)
		If Not tween[var] Then Return
		
		If tween[var].dir &lt; 0
			Local temp:Float
			temp = startx
			startx = endx
			endx = temp
		Endif
		
		tween[var].otherTweens = tween[var].otherTweens | TWROT
		
		Local rot:Float
		Local dif:Float = startx - endx
		Local ang:Float = tween[var].rot + startx
		Local ca:Float = Cos(ang)
		Local sa:Float = Sin(ang)
		mojo.Transform(ca,-sa,sa,ca, 0,0)
		
		If tween[var].easing &gt;= 0
			rot = tween[var].rot - (tween[var].rot - dif)*st
		Else
			Local kick:Float = 0.0
			If dif&lt;0 Then kick = -0.1 Else If dif&gt;0 Then kick = 0.1
			rot = tween[var].rot + (tween[var].rot)*st + kick
		Endif
	
	'Print "r: "+rot	+" d "+dif
		If (dif&lt;0.0) And rot &lt; dif Then rot=dif Elseif (dif&gt;0.0) And rot&gt;dif Then rot=dif 
		
		tween[var].rot = rot

		''turn off only is there's no other tweens
		If Abs(rot - dif) &lt; 1.0
			If Not ( (~ TWROT) &amp; tween[var].otherTweens) Then tween[var].dir = 0 And Not tween[var].rotAction
			tween[var].otherTweens = (~ TWROT) &amp; tween[var].otherTweens
			
		Endif
		
		tween[var].lastang = ang
		
		Return ang
	End
	
	''SetAlphaINOUT
	''create our own function for bouncing, as to not disturb other tweens
	
	Function SetAlphaINOUT:Float( var:Int, st:Float, dur:Int)
		If Not tween[var] Then Return 0.0
		
		If Not tween[var].alphaAction Then tween[var].alphaAction = 1
		
		If tween[var].alphaAction = 1
			If SetAlpha( var, st, 0.0, 1.0) &gt;= 1.0
				tween[var].pause = Millisecs()
				tween[var].alphaAction = 2
			Endif
		Elseif tween[var].alphaAction = 2
			If tween[var].pause &gt; Millisecs() -dur
				''pause
			Else
				''fadeout
				If SetAlpha( var, st, 1.0, 0.0) &lt;= 0.0
					If Not ( (~ TWALPHA) &amp; tween[var].otherTweens) Then tween[var].dir = 0
					tween[var].alphaAction = 2
				Endif
			Endif
		Endif
		Return tween[var].alpha
	End
	
	
	Function GetAlpha:Float(var:Int)
		If Not tween[var] Then Return 0.0
		Return tween[var].alpha
	End
	
	''Begin(handle, start variable: x,y or alpha or rotation)
	'' - will reset any prior tweens on that handle
	Function Begin(var:Int, x:Float=0.01, y:Float = 0.0)
		If Not tween[var] Then tween[var] = New TweenObj
		tween[var].alpha = x
		tween[var].x = x
		tween[var].y = y
		tween[var].scalex = 0.0
		tween[var].scaley = 0.0
		tween[var].rot = x
		tween[var].pause = Millisecs()
		tween[var].dir = 1
		tween[var].alphaAction = 0
		tween[var].transAction = 0
		tween[var].scaleAction = 0
		tween[var].rotAction = 0
		tween[var].otherTweens = 0
	End
	
	Function IsActive?(var:Int)
		If Not tween[var] Then Return False
		If tween[var].dir &lt;&gt;0 Then Return True
		Return False
	End
	
	'' UseFinalState(handle)
	'' - this function is when you want to HOLD on your last tween frame, when the tween is NOT active (IsActive())
	'' - this function is faster than running persistant tweens
	'' - if this runs before your first tween, the alpha is 0.0
	'' - could be used like : If Not (FinalMatrix(handle)) Then DoTweenHere()
	
	Function UseFinalState:Int(var:Int)
		If Not tween[var] Then Return 0
		If IsActive(var) Then Return 0

		mojo.SetAlpha(tween[var].alpha)
		Local ca:Float = Cos(tween[var].lastang)
		Local sa:Float = Sin(tween[var].lastang)
		Local scx:Float = tween[var].lastscx
		Local scy:Float = tween[var].lastscy
		
		''use mojo.Transform so we can overlap tweens, but mojo.SetMatrix would be faster
		mojo.Transform(ca*scx,-sa*scy,sa*scy,ca*scx,tween[var].lastx,tween[var].lasty)
		 
		Return 1

	End
	
	Function EaseOut(var:Int)
		tween[var].easing = 1
	End
	Function EaseIn(var:Int)
		tween[var].easing = -1
	End
	Function SetBounce(var:Int)
		tween[var].action = 2 ''future use
	End
	Function SetReverse(var:Int)
		tween[var].dir = tween[var].dir * -1
	End
	
	Function Restore()
		mojo.SetAlpha(1.0)
		SetMatrix(1.0,0,0,1.0,0,0)
		mojo.SetColor(255,255,255)
	End
	
End

Class TweenObj
	Field x:Float
	Field y:Float
	Field scalex:Float = 1.0
	Field scaley:Float = 1.0
	Field rot:Float
	Field alpha:Float = 0.0
	Field easing:Int = 1
	Field pause:Int =0
	Field dir:Int =0
	'Field action:Int=0 ''future use 1=hold, 2=bounce
	Field alphaAction:Int =0
	Field transAction:Int =0
	Field scaleAction:Int =0
	Field rotAction:Int =0
	
	Field otherTweens:Int = 0 ''used to allow all tweens to finish
	
	''persistance
	Field lastang:Float = 0.0
	Field lastx:Float = 0.0
	Field lasty:Float = 0.0
	Field lastscx:Float = 1.0
	Field lastscy:Float = 1.0
End
</textarea> <br><br></td></tr></table><br>
<a name="2018889"></a>

<a name="2018888"></a>

<a name="2018887"></a>

<a name="2018886"></a>

<a name="2018880"></a>

<a name="2018879"></a>

<a name="2018876"></a>

<a name="2018877"></a>

<a name="2018878"></a>

<a name="2018874"></a>

<a name="2018875"></a>

<a name="2018872"></a>

<a name="2018873"></a>

<a name="2018871"></a>

<a name="2018870"></a>

<a name="2018869"></a>

<a name="2018868"></a>

<a name="2018867"></a>

<a name="2018866"></a>

<a name="2020702"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Martian Watts</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> It's really helpfull! I will to use it.. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
