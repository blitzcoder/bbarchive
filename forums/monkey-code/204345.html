<!DOCTYPE html><html lang="en" ><head ><title >Entity Component System</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Entity Component System</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Entity Component System</a><br><br>
<a name="2046245"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Belimoth</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Fun little experiment I did in another thread.<br>In order to write a new Component you have to define an accompanying ComponentGetter&lt;&gt;, which in this example I've prefixed with an underscore.<br><br><pre class=code>
Strict
#REFLECTION_FILTER="*"
Import reflection

Class Entity
	Field components := New StringMap&lt;Component&gt;
	
	Method AddComponent:Void(component:Component)
		Local key:String = GetClass(component).Name
		components.Set(key, component)
		component.owner = Self
	End
End

Class Component
	Field owner:Entity
End

Class ComponentGetter&lt;T&gt;
	Global name:String
	
	Function GetFrom:T(entity:Entity)
		If name = "" Then name = GetClass( New T() ).Name	'be aware of this New
		Return T( entity.components.Get(name) )
	End
End



Class _PositionComponent Extends ComponentGetter&lt;PositionComponent&gt; End

Class PositionComponent Extends Component

Public
	Method X:Int() Property Return _x End
	Method X:Void(x:Int) Property _x = x End
	Method Y:Int() Property Return _y End
	Method Y:Void(y:Int) Property _y = y End
	
	Method New(x:Int, y:Int)
		Self._x = x
		Self._y = y
	End
Private
	Field _x:Int, _y:Int
End

Function Main:Int()
	Local myEntity := New Entity()
	myEntity.AddComponent( New PositionComponent(16, 24) )
	Local position := _PositionComponent.GetFrom(myEntity)
	Print "X: " + position.X + " Y: " + position.Y
	Return 0
End
</pre> <br><br></td></tr></table><br>
<a name="2046244"></a>

<a name="2046243"></a>

<a name="2046242"></a>

<a name="2046248"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Belimoth</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's a version that doesn't involve reflection.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Class Entity
	Field components := New StringMap&lt;Component&gt;
	
	Method AddComponent:Void(component:Component)
		Local key:String = component.ComponentName()
		components.Set(key, component)
		component.owner = Self
	End
End

Class Component
	Field owner:Entity
	Method ComponentName:String() Abstract
End

Class ComponentGetter&lt;T&gt;		'T must extend Component
	Global name:String
	
	Function GetFrom:T(entity:Entity)
		If name = ""
			Local t := New T()
			name = t.ComponentName()
			'do whatever you need to do to finalize t here.
		EndIf
		Return T( entity.components.Get(name) )
	End
End



Class _PositionComponent Extends ComponentGetter&lt;PositionComponent&gt; End

Class PositionComponent Extends Component

Public
	Method ComponentName:String() Return "PositionComponent" End
	
	Method X:Int() Property Return _x End
	Method X:Void(x:Int) Property _x = x End
	Method Y:Int() Property Return _y End
	Method Y:Void(y:Int) Property _y = y End
	
	Method New(x:Int, y:Int)
		Self._x = x
		Self._y = y
	End
Private
	Field _x:Int, _y:Int
End

Function Main:Int()
	Local myEntity := New Entity()
	myEntity.AddComponent( New PositionComponent(16, 24) )
	Local position := _PositionComponent.GetFrom(myEntity)
	Print "X: " + position.X + " Y: " + position.Y
	Return 0
End
</textarea> <br><br></td></tr></table><br>
<a name="2046247"></a>

<a name="2046246"></a>

<a name="2046249"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Belimoth</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> All this to avoid one cast <img src="http://forums.tigsource.com/Smileys/derek/cheesy.gif"> <br><br></td></tr></table><br>
<a name="2046308"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Belimoth</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Version 3, I never miss an opportunity to over-engineer something.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict
#REFLECTION_FILTER="*"
Import reflection



Interface IProperties
	Method SetProperty:Void(key:String, value:Float = 0)
	Method RemoveProperty:Void(key:String)
	Method HasProperty:Bool(key:String)
	Method GetProperty:Float(key:String)
End



Class Entity
	Field components := New StringMap&lt;IComponent&gt;
	Field propertyLocations := New StringMap&lt; StringMap&lt;FloatObject&gt; &gt;
	
	Method SetComponent:Void(component:Component, key:String = "")
		Local type:String = GetClass(component).Name
		
		Local destination:StringMap&lt;IComponent&gt;
		Local destinationKey:String
		
		If key = ""
			destination = components
			destinationKey = type
		Else
			Local multiplicity := ComponentMultiplicity( components.Get(type) )
			If multiplicity = Null
				multiplicity = New ComponentMultiplicity
				components.Add(type, multiplicity)
			EndIf
			destination = multiplicity
			destinationKey = key
		EndIf
		
		Local componentOld:IComponent = destination.Get(destinationKey)
		If componentOld &lt;&gt; Null Then componentOld.Finalize()
		destination.Set(destinationKey, component)
		component.SetOwner(Self)
		component.ownerLocation = destination
		component.ownerKey = destinationKey
	End
	
	Method GetProperty:Float(name:String)
		Local propertyLocation := propertyLocations.Get(name)
		If propertyLocation = Null Return 0
		Local floatObject := propertyLocation.Get(name)
		If floatObject = Null Return 0
		Return floatObject.value
	End
End



Interface IComponent
	Method Finalize:Void()
End



Class ComponentMultiplicity Extends StringMap&lt;IComponent&gt; Implements IComponent
	Method Finalize:Void()
		For Local component := Eachin Values()
			component.Finalize()
		Next
	End
End



Class Component Implements IComponent, IProperties
	Field owner:Entity, ownerLocation:StringMap&lt;IComponent&gt;, ownerKey:String
	Field properties := New StringMap&lt;FloatObject&gt;
	
	Method Finalize:Void() Abstract
	
	Method SetOwner:Void(owner:Entity)
		If Self.owner &lt;&gt; Null
			ownerLocation.Remove(ownerKey)
			For Local key := Eachin Self.owner.propertyLocations.Keys()
				Local location := Self.owner.propertyLocations.Get(key)
				If location = properties
					Self.owner.propertyLocations.Remove(key)
				EndIf
			Next
		EndIf
		
		Self.owner = owner
		If owner = Null Then Return
		For Local name := eachin properties.Keys()
			owner.propertyLocations.Set(name, properties)
		Next
	End
	
	Method Remove:Void()
		SetOwner(Null)
		Finalize()
	End
	
	Method SetProperty:Void(name:String, value:Float = 0)
		properties.Set(name, New FloatObject(value) )
		If owner &lt;&gt; Null
			owner.propertyLocations.Set(name, properties)
		EndIf
	End
	
	Method RemoveProperty:Void(name:String)
		properties.Remove(name)
		If owner &lt;&gt; Null
			owner.propertyLocations.Remove(name)
		EndIf
	End
	
	Method HasProperty:Bool(name:String)
		Return ( properties.Get(name) &lt;&gt; Null )
	End
	
	Method GetProperty:Float(name:String)
		Local floatObject := properties.Get(name)
		If floatObject &lt;&gt; Null Then Return floatObject.value
		Return 0			'sketchy
	End
End



Class ComponentGetter&lt;T&gt;		'T must extend Component
	Global name:String
	
	Function CheckName:Void()
		If name = ""
			Local t := New T()
			name = GetClass(t).Name
			t.Finalize()
		EndIf
	End
	
	Function GetFrom:T(entity:Entity, key:String = "")
		CheckName()
		Local iComponent:IComponent = entity.components.Get(name)
		Local multiplicity := ComponentMultiplicity(iComponent)
		If multiplicity = Null
			Return T(iComponent)
		Else
			If key = ""
				For Local component := Eachin multiplicity.Values()
					Return T(component)
				Next
			Else
				Return T( multiplicity.Get(key) )
			EndIf
		EndIf
		Return Null
	End
	
	Function GetAllFrom:List&lt;T&gt;(entity:Entity)
		CheckName()
		Local list := New List&lt;T&gt;
		Local iComponent:IComponent = entity.components.Get(name)
		If Component(iComponent)
			list.AddLast( T(iComponent) )
		Else
			For Local component := EachIn ComponentMultiplicity(iComponent).Values()
				list.AddLast( T(component) )
			Next
		EndIf
		Return list
	End
End










Class _PositionComponent Extends ComponentGetter&lt;PositionComponent&gt; End



Class PositionComponent Extends Component

Public	
	Method New(x:Float, y:Float)
		SetProperty("x", x)
		SetProperty("y", y)
	End
	
	Method Finalize:Void()
		
	End
End



Class _FooComponent Extends ComponentGetter&lt;FooComponent&gt; End



Class FooComponent Extends Component
	Field value:Int
	
	Method New(value:Int)
		Self.value = value
	End
	
	Method Finalize:Void()
		Print "daisy daisy"
	End
	
	Method Bar:Void()
		Print "bar: " + value
	End
End



Function Main:Int()
	Local myEntity := New Entity()
	
	myEntity.SetComponent( New PositionComponent(16, 24) )
	
	For Local n := 0 To 5
		myEntity.SetComponent( New FooComponent(n), n )
	Next
	
	myEntity.SetComponent( New FooComponent(6), 3 )
	
	_FooComponent.GetFrom(myEntity, "5").Remove()
	
	
	
	
	Print "X: " + myEntity.GetProperty("x") + " Y: " + myEntity.GetProperty("y")
	
	Local foo:FooComponent
	foo = _FooComponent.GetFrom(myEntity)
	If foo &lt;&gt; Null Then foo.Bar
	
	For foo = EachIn _FooComponent.GetAllFrom(myEntity)
		foo.Bar()
	Next
	
	_PositionComponent.GetFrom(myEntity).Remove()
	
	Print "X: " + myEntity.GetProperty("x") + " Y: " + myEntity.GetProperty("y")
	
	Return 0
End
</textarea><br><br>Supports having more than one component of the same type, and adds a "property" thing. I am probably done with this. <br><br></td></tr></table><br>
<a name="2046306"></a>

<a name="2046327"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >c.k.</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I wish I understood all this. :-/<br><br>Maybe someday. <br><br></td></tr></table><br>
<a name="2047440"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Halfdan</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> cool I started looking into this Entity Component thing, here is an article about it <a href="http://t-machine.org/index.php/2007/09/03/entity-systems-are-the-future-of-mmog-development-part-1/" target="_blank">(article link)</a><br><br>your system looks a bit different but I'll use it as starting point <br><br></td></tr></table><br>
<a name="2047666"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Belimoth</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah it's all a bit fuzzy to me, it might be closer to aspect-oriented than component-oriented.<br>In hindsight...<br><pre class=code>
Class Get&lt;T&gt;
	Function From:T(entity:Entity)
		...
	End
End
...
Get&lt;PositionComponent&gt;.From(myEntity)
</pre>...is much cleaner than my first attempt up there. Oh well. <br><br></td></tr></table><br>
<a name="2077405"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sammy</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Having just read up on component systems I was wondering if this was possible in Monkey. You example has show me it most definitely is, many thanks! :) <br><br></td></tr></table><br>
<a name="2077406"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Goodlookinguy</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Entity component systems are overrated. It was a fad that's already expired. They're more work than they're worth. Just go the classic route unless you have a team of 10+ people and millions of lines of code. Because building games with ECS is like building a city out of Legos. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
