<!DOCTYPE html><html lang="en" ><head ><title >Interpolation</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Interpolation</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Interpolation</a><br><br>
<a name="2024238"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Taken from <a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=781" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=781</a> by Klaas and converted quickly to Monkey. Posting here as others may have a use for parts of this.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Import mojo



Class TPoint
	
	Field x : Float
	Field y : Float
	
	Method New( px : Float, py : Float )
		Self.x = px
		Self.y = py
	End Method
	
End Class



' Dimension the pointarray And set startvalues
Global DataPoint : TPoint[ 6 ]


' Set the clearcolor To red, And Select point 0 
Global Selected : Int = 0 
Global linear_draw : Bool
Global cosin_draw : Bool
Global cubic_draw : Bool
Global hermite_draw : Bool
Global tension : Float
Global bias : Float





Class MyApp Extends App


	
	Method OnCreate()

		SetUpdateRate 60
		
		DataPoint[ 0 ] = New TPoint( 50.0, 90.0 )
		DataPoint[ 1 ] = New TPoint( 150.0, 160.0 )
		DataPoint[ 2 ] = New TPoint( 250.0, 300.0 )
		DataPoint[ 3 ] = New TPoint( 350.0, 210.0 )
		DataPoint[ 4 ] = New TPoint( 450.0, 100.0 )
		DataPoint[ 5 ] = New TPoint( 550.0, 300.0 )

	End Method
	

	Method OnUpdate()
	
		' Userinput (Use [Q] And [W] To Select a point,
		' And Arrowkeys To position the selected point)
		If KeyHit( KEY_Q ) And Selected &gt; 0 Then Selected = Selected - 1
		If KeyHit( KEY_W ) And Selected &lt; 5 Then Selected = Selected + 1
		
		If KeyHit( KEY_A ) And tension &gt; -1 Then tension = tension - 0.1
		If KeyHit( KEY_S ) And tension &lt; 1 Then tension = tension + 0.1
		
		If KeyHit( KEY_Z ) And bias &gt; -1 Then bias = bias - 0.1
		If KeyHit( KEY_X ) And bias &lt; 1 Then bias = bias + 0.1
		
		If KeyDown( KEY_LEFT ) Then DataPoint[ Selected ].x = DataPoint[ Selected ].x - 1 
		If KeyDown( KEY_RIGHT ) Then DataPoint[ Selected ].x = DataPoint[ Selected ].x + 1
		If KeyDown( KEY_UP ) Then DataPoint[ Selected ].y = DataPoint[ Selected ].y - 1
		If KeyDown( KEY_DOWN ) Then DataPoint[ Selected ].y = DataPoint[ Selected ].y + 1
		
		If KeyHit( KEY_1 ) Then linear_draw = Not linear_draw
		If KeyHit( KEY_2 ) Then cosin_draw = Not cosin_draw
		If KeyHit( KEY_3 ) Then cubic_draw = Not cubic_draw
		If KeyHit( KEY_4 ) Then hermite_draw = Not hermite_draw
	
	End Method


	Method OnRender()

		Cls 0, 0, 0

		' Draw the interpolated line between point 0 And 4
		DrawInterpolatedLine()

		SetColor 250, 250, 250
		DrawText "1 - Linear Interpolation", 10, 10
		DrawText "2 - Cosine Interpolation", 10, 25
		DrawText "3 - Cubic Interpolation", 10, 40
		DrawText "4 - Hermite Interpolation", 10, 55
	
		DrawText "Tension(+A,-S) - " + tension, 300, 10
		DrawText "Bias(+Z,-X) - " + bias, 300, 25
		
		DrawText "use W,Q and Cursor to select a points", 10, 460

	End Method


End Class




Function Main()
	New MyApp()
End Function





Function DrawInterpolatedLine()
	
	'Startpoint For the first Line
	Local X : Float = DataPoint[ 0 ].x
	Local Y : Float = DataPoint[ 0 ].y
    
	' Draw the interpolated line between point 1 And 4
	If cosin_draw
		SetColor 255, 50, 50	
		For Local I : Int = 1 To 5
			Local OX : Float
			Local OY : Float
			Local MX : Float = -1 * ( DataPoint[ I - 1 ].x - DataPoint[ I ].x ) 
			For Local MU : Float = 0.0 To 1.0 Step 0.08 
				OX = X
				OY = Y
				Y = Cosine_Interpolate( DataPoint[ I - 1 ].y, DataPoint[ I ].y, MU )  
				X = Cosine_Interpolate( DataPoint[ I - 1 ].x, DataPoint[ I ].x, MU )  
				DrawOval X - 1, Y - 1, 3, 3
			Next
		Next
	Endif
	If linear_draw
		SetColor 50, 255, 50
		For Local I : Int = 1 To 5
			Local OX : Float
			Local OY : Float
			Local MX : Float = -1 * ( DataPoint[ I - 1 ].x - DataPoint[ I ].x ) 
			For Local MU : Float = 0.0 To 1.0 Step 0.08 
				OX = X
				OY = Y
				Y = Linear_Interpolate( DataPoint[ I - 1 ].y, DataPoint[ I ].y, MU )  
				X = Linear_Interpolate( DataPoint[ I - 1 ].x, DataPoint[ I ].x, MU )  
				DrawLine OX, OY, X, Y
			Next
		Next
	Endif
	If cubic_draw
		Local f : Bool = False
		SetColor 50, 50, 255
		For Local I : Int = 2 To 4
			Local OX : Float
			Local OY : Float
			Local MX : Float = -1 * ( DataPoint[ I - 1 ].x - DataPoint[ I ].x ) 
			For Local MU : Float = 0.0 To 1.0 Step 0.08 
				OX = X
				OY = Y
				Y = Cubic_Interpolate( DataPoint[ I - 2 ].y, DataPoint[ I - 1 ].y, DataPoint[ I ].y, DataPoint[ I + 1 ].y, MU ) 
				X = Cubic_Interpolate( DataPoint[ I - 2 ].x, DataPoint[ I - 1 ].x, DataPoint[ I ].x, DataPoint[ I + 1 ].x, MU )
				If Not f
					OX = X
					OY = Y
					f = True
				Endif
				DrawLine OX, OY, X, Y
			Next
		Next
	Endif
	If hermite_draw
		Local f : Bool = False
		SetColor 255, 50, 255
		For Local I : Int = 2 To 4
			Local OX : Float
			Local OY : Float
			Local MX : Float = -1 * ( DataPoint[ I - 1 ].x - DataPoint[ I ].x ) 
			For Local MU : Float = 0.0 To 1.0 Step 0.08 
				OX = X
				OY = Y
				Y = Hermite_Interpolate( DataPoint[ I - 2 ].y, DataPoint[ I - 1 ].y, DataPoint[ I ].y, DataPoint[ I + 1 ].y, MU, tension, bias ) 
				X = Hermite_Interpolate( DataPoint[ I - 2 ].x, DataPoint[ I - 1 ].x, DataPoint[ I ].x, DataPoint[ I + 1 ].x, MU, tension, bias )
				If Not f
					OX = X
					OY = Y
					f = True
				Endif
				DrawLine OX, OY, X, Y
			Next
		Next
	Endif
	
	
	' Draw the anchor points
	SetColor 255, 0, 0
	For Local I : Int = 0 To 5
		If Selected = I
			SetColor 0, 0, 255
		Else
			SetColor 255, 0, 0
		Endif
		DrawOval DataPoint[ I ].x - 2, DataPoint[ I ].y - 2, 4, 4
	Next 

End Function



Function Cubic_Interpolate : Float( v0 : Float, v1 : Float, v2 : Float, v3 : Float, x : Float )
	Local P : Float = ( v3 - v2 ) - ( v0 - v1 )
	Local Q : Float = ( v0 - v1 ) - P
	Local R : Float = v2 - v0
	Local S : Float = v1
	Return P * Pow( x, 3 ) + Q * Pow( x, 2 ) + R * x + S
End Function



'Function For cosineinterpolated Line
Function Cosine_Interpolate : Float( Y1 : Float, Y2 : Float, MU : Float ) 
	Local MU2 : Float
	MU2 = ( 1.0 - Cos( MU * 180 )) / 2.0
	Return ( Y1 * ( 1.0 - MU2 ) + Y2 * MU2 ) 
End Function


'Function For normal Line
Function Linear_Interpolate : Float( Y1 : Float, Y2 : Float, MU : Float) 
	Return Y1 * ( 1 - MU ) + Y2 * MU 
End Function


'Tension: 1 is high, 0 normal, -1 is low
'Bias: 0 is even,
'positive is towards First segment,
'negative towards the other
Function Hermite_Interpolate : Float( y0 : Float, y1 : Float, y2 : Float, y3 : Float, t : Float, tension : Float, bias : Float)
	Local t2 : Float = t * t
	Local t3 : Float = t2 * t
	Local tb : Float = ( 1 + bias ) * ( 1 - tension ) / 2
	Return ( 2 * t3 - 3 * t2 + 1 ) * y1 + ((( t3 - 2 * t2 + t ) * ( y1 - y0 + y2 - y1 )) + ( t3 - t2 ) * ( y2 - y1 + y3 - y2 )) * tb + ( 3 * t2 - 2 * t3 ) * y2
End Function

</textarea> <br><br></td></tr></table><br>
<a name="2023003"></a>

<a name="2023002"></a>

<a name="2022966"></a>

<a name="2022965"></a>

<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
