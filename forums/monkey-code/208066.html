<!DOCTYPE html><html lang="en" ><head ><title >mojo.graphics functions: ShearX/Y, MirrorX/Y</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >mojo.graphics functions: ShearX/Y, MirrorX/Y</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >mojo.graphics functions: ShearX/Y, MirrorX/Y</a><br><br>
<a name="2080616"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Danilo</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> <textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict

Import mojo

' -------------------------------------------------------------------------
' mojo.graphics missing functions
'

' - Shear/Skew  --------------------&gt;&gt; REQUIRES DECISION: What's the best name? Skew or Shear?
Function SkewX:Void(angle:Float)
    Transform(1,0,Tan(angle),1,0,0)
End

Function SkewY:Void(angle:Float)
    Transform(1,Tan(angle),0,1,0,0)
End

Function ShearX:Void(angle:Float)
    Transform(1,0,Tan(angle),1,0,0)
End

Function ShearY:Void(angle:Float)
    Transform(1,Tan(angle),0,1,0,0)
End


' - Mirror
Function MirrorX:Void()
    Transform(1,0,0,-1,0,0)
End

Function MirrorY:Void()
    Transform(-1,0,0,1,0,0)
End

' - 'At' functions apply a function
'        at/around a specific point

' - Shear/Skew  --------------------&gt;&gt; REQUIRES DECISION: What's the best name? Skew or Shear?
Function SkewXAt:Void(x:Float, y:Float, angle:Float)
    Translate(x, y)
    Transform(1,0,Tan(angle),1,0,0)
    Translate(-x, -y)
End

Function SkewYAt:Void(x:Float, y:Float, angle:Float)
    Translate(x, y)
    Transform(1,Tan(angle),0,1,0,0)
    Translate(-x, -y)
End

Function ShearXAt:Void(x:Float, y:Float, angle:Float)
    Translate(x, y)
    Transform(1,0,Tan(angle),1,0,0)
    Translate(-x, -y)
End

Function ShearYAt:Void(x:Float, y:Float, angle:Float)
    Translate(x, y)
    Transform(1,Tan(angle),0,1,0,0)
    Translate(-x, -y)
End


Function RotateAt:Void(x:Float, y:Float, angle:Float)
    Translate(x, y)
    Rotate(angle)
    Translate(-x, -y)
End

Function ScaleAt:Void(x:Float, y:Float, scaleX:Float, scaleY:Float)
    Translate(x, y)
    Scale(scaleX, scaleY)
    Translate(-x, -y)
End

Function MirrorXAt:Void(x:Float, y:Float)
    Translate(x, y)
    Transform(1,0,0,-1,0,0)
    Translate(-x, -y)
End

Function MirrorYAt:Void(x:Float, y:Float)
    Translate(x, y)
    Transform(-1,0,0,1,0,0)
    Translate(-x, -y)
End

' - reset Matrix to standard
Function ResetMatrix:Void()
    SetMatrix(1,0,0,1,0,0)
End
'
' -------------------------------------------------------------------------

Class Program Extends App

    Field angle:Float = 0.0
    Field mode:Int = 21
    Field modeNames:String[] = [ "Default", "MirrorX", "MirrorY",
                                 "MirrorX + MirrorY", "ShearX", "ShearY",
                                 "ShearX + ShearY", "ShearX + Rotate",
                                 "Rotate + ShearX + ShearY + Rotate",
                                 "Rotate + ShearX + ShearY + Rotate",
                                 "Scale 0.1 + ShearX + Rotate",
                                 "Scale 0.2 + ShearX + Rotate",
                                 "Scale 0.4 + ShearX + Rotate",
                                 "Scale 0.8 + ShearX + Rotate",
                                 "Scale 1.6 + ShearX + Rotate",
                                 "Scale 3.2 + ShearX + Rotate",
                                 "Scale 6.4 + ShearX + Rotate",
                                 "Scale 3/1 + ShearY + Rotate",
                                 "ShearX", "ShearY", "ShearX + ShearY",
                                 "" ]

    Method OnCreate:Int()
        SetUpdateRate 30
        Return 0
    End
    
    Method OnUpdate:Int()
        If KeyHit(KEY_LMB) Or KeyHit(KEY_SPACE)
            mode += 1
            If mode &lt; 11 Or mode &gt; 17
                angle = 0
            endif
        Endif

        If mode &gt; 20 Then mode = 0

        angle += 1

        Return 0
    End

    Method OnRender:Int()
        Local midX:Float = DeviceWidth() *0.5
        Local midY:Float = DeviceHeight()*0.5

        Cls(64,64,64)
        
        Select mode
            Case 1
                MirrorXAt(midX, midY)
            Case 2
                MirrorYAt(midX, midY)
            Case 3
                MirrorXAt(midX, midY)
                MirrorYAt(midX, midY)
            Case 4
                ShearXAt(midX, midY, -20)
            Case 5
                ShearYAt(midX, midY, -20)
            Case 6
                ShearXAt(midX, midY, 30)
                ShearYAt(midX, midY, 30)
            Case 7
                ScaleAt(midX, midY, 1, 0.3)
                ShearXAt(midX, midY, 45)
                RotateAt(midX, midY, angle)
            Case 8
                ScaleAt(midX, midY, 1, 0.3)
                RotateAt(midX, midY, -angle)
                ShearXAt(midX, midY, 45)
                ShearYAt(midX, midY, 45)
                RotateAt(midX, midY, angle)
            Case 9
                RotateAt(midX, midY, -angle)
                ShearXAt(midX, midY, 45)
                ShearYAt(midX, midY, -45)
                RotateAt(midX, midY, angle)
            Case 10
                ScaleAt(midX, midY, 1, 0.1)
                ShearXAt(midX, midY, -45)
                RotateAt(midX, midY, angle)
            Case 11
                ScaleAt(midX, midY, 1, 0.2)
                ShearXAt(midX, midY, -45)
                RotateAt(midX, midY, angle)
            Case 12
                ScaleAt(midX, midY, 1, 0.4)
                ShearXAt(midX, midY, -45)
                RotateAt(midX, midY, angle)
            Case 13
                ScaleAt(midX, midY, 1, 0.8)
                ShearXAt(midX, midY, -45)
                RotateAt(midX, midY, angle)
            Case 14
                ScaleAt(midX, midY, 1, 1.6)
                ShearXAt(midX, midY, -45)
                RotateAt(midX, midY, angle)
            Case 15
                ScaleAt(midX, midY, 1, 3.2)
                ShearXAt(midX, midY, -45)
                RotateAt(midX, midY, angle)
            Case 16
                ScaleAt(midX, midY, 1, 6.4)
                ShearXAt(midX, midY, -45)
                RotateAt(midX, midY, angle)
            Case 17
                ScaleAt(midX, midY, 3, 1)
                ShearYAt(midX, midY, -45)
                RotateAt(midX, midY, -angle)
            Case 18
                If angle=160 Then angle = 0
                ShearXAt(midX, midY, angle-80)
            Case 19
                If angle=160 Then angle = 0
                ShearYAt(midX, midY, angle-80)
            Case 20
                If angle=160 Then angle = 0
                ShearXAt(midX, midY,-angle+80)
                ShearYAt(midX, midY, angle-80)
        End Select

        '
        ' Draw the stuff:
        '
        ' the rect
        SetColor(255,255,255)
        DrawRect(midX-100, midY-100,200, 200)
        ' 2 lines
        SetColor(0,0,0)
        DrawLine(midX-100, midY-100, midX+100, midY+100)
        DrawLine(midX-100, midY+100, midX+100, midY-100)
        ' 4 circles at the rect corners
        SetColor(255,0,0)
        DrawCircle(midX-100, midY-100, 10)
        SetColor(0,255,0)
        DrawCircle(midX+100, midY-100, 10)
        SetColor(0,255,255)
        DrawCircle(midX-100, midY+100, 10)
        SetColor(0,0,255)
        DrawCircle(midX+100, midY+100, 10)
        ' text
        SetColor(255,255,0)
        DrawText("Some Text", midX-70, midY-70)

        ' draw info text
        ResetMatrix()
        DrawText("Mode: "+String(mode)+" = "+modeNames[mode], 10, 10)
        DrawText("press left mouse button or space for next mode", 10, 30)

        Return 0
    End
End

Function Main:Int()
    New Program
    Return 0
End</textarea> <br><br></td></tr></table><br>
<a name="2080608"></a>

<a name="2080607"></a>

<a name="2080600"></a>

<a name="2080599"></a>

<a name="2080595"></a>

<a name="2080592"></a>

<a name="2080591"></a>

<a name="2080586"></a>

<a name="2080582"></a>

<a name="2080603"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cool!<br>I spent time for looking something like this! (without results :()<br>Many thanks! <br><br></td></tr></table><br>
<a name="2080604"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Goodlookinguy</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Isn't the more commonly used word for 'Shear', 'Skew'? Anyways, neat code. I recommend you put the code in the [ codebox ] tag since it's so big. <br><br></td></tr></table><br>
<a name="2080606"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MikeHart</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cool. I recomment that you make a push request into Mark's repository so it becomes a mojo standard. <br><br></td></tr></table><br>
<a name="2080609"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Danilo</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> OK, replaced all "Shear" with "Skew". Native english speakers know better. <br><br></td></tr></table><br>
<a name="2080610"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Goodlookinguy</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I was actually just asking a question. I had heard the word shear before used to represent what I thought was skew. I actually don't know the difference if any. <br><br></td></tr></table><br>
<a name="2080612"></a>

<a name="2080614"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Danilo</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> @MikeHart: Push request done. <br><br></td></tr></table><br>
<a name="2080651"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DruggedBunny</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cool, would be nice to have this stuff as standard! <br><br></td></tr></table><br>
<a name="2080659"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice ... just dont expect these to work with MonkeyMax ;) <br><br></td></tr></table><br>
<a name="2080666"></a>

<a name="2080667"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice collection of functions... but really no need for integrate them into official monkey! too trivial! <br><br>here's another one:<br><pre class=code>Function DoubleSize:Void()
     Scale 2,2
End</pre><br>;-) <br><br></td></tr></table><br>
<a name="2080665"></a>

<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
