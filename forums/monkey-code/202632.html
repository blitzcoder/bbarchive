<!DOCTYPE html><html lang="en" ><head ><title >Asteroids</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Asteroids</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Asteroids</a><br><br>
<a name="2026380"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Programmed in an hour and a bit, lots still to add but its pretty much a working Asteroids game...<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Import mojo



Class DeltaTimer

	Global targetfps : Float = 60
	Global currentticks : Float
	Global lastticks : Float
	Global frametime : Float
	Global delta : Float
	
	Function SetDelta( fps : Float )
		targetfps = fps
		lastticks = Millisecs()
	End Function
	
	Function UpdateDelta()
		currentticks = Millisecs()
		frametime = currentticks - lastticks
		delta = frametime / (1000.0 / targetfps)
		lastticks = currentticks
	End Function
	
End Class





Class BaseObject

	'/ position
	Field x : Float
	Field y : Float
	
	'/ rotation
	Field a : Float
	
	'/ velocity
	Field vx : Float
	Field vy : Float

	'/ rotation velocity
	Field va : Float

	'/ radius
	Field radius : Float


	'/ Updates position rotation
	Method UpdatePosition()
	
		'update position / rotation by velocity
		Self.x = Self.x + ( Self.vx * DeltaTimer.delta )
		Self.y = Self.y + ( Self.vy * DeltaTimer.delta )
		Self.a = Self.a + ( Self.va * DeltaTimer.delta )
		
		'wrap position using size of asteroid to eliminate jumping
		If Self.x &gt; DeviceWidth() + Self.radius Then Self.x = Self.x - ( DeviceWidth() + ( Self.radius * 2.0 ))
		If Self.y &gt; DeviceHeight() + Self.radius Then Self.y = Self.y - ( DeviceHeight() + ( Self.radius * 2.0 ))
		If Self.x &lt; 0 - Self.radius Then Self.x = Self.x + ( DeviceWidth() + ( Self.radius * 2.0 ))
		If Self.y &lt; 0 - Self.radius Then Self.y = Self.y + ( DeviceHeight() + ( Self.radius * 2.0 ))
		
	End Method
	

	'/ Abstract Update and Render
	Method Update : Void() Abstract
	Method Render : Void() Abstract

End Class




Const MAXIMUM_ASTEROID_VELOCITY : Float = 6.0
Const MAXIMUM_SHIP_VELOCITY : Float = 4.0

Const THRUST_FORCE : Float = 0.15
Const TURN_RATE : Float = 4.0

Const MAX_BULLET_LIFE : Int = 100
Const FIRE_RATE : Int = 100


Const ASTEROID_HIT_BONUS : Int = 10
Const ASTEROID_KILL_BONUS : Int = 100


'/ Bullet Class
Class Bullet Extends BaseObject
	
	Field ID : list.Node&lt; Bullet &gt;

	Global bulletList : List&lt; Bullet &gt; = New List&lt; Bullet &gt;

	'how long bullet lives for
	Field life : Int

	Field owner : Ship

	'/ New Bullet
	Method New( px : Float, py : Float, pa : Float, pv : Float )
		Self.x = px
		Self.y = py
		Self.a = pa
		Self.vx = pv
		Self.radius = 10.0
		Self.life = MAX_BULLET_LIFE
	End Method
	

	'/ Updates Bullet
	Method Update : Void()
	
		'update position / rotation by velocity
		Self.a = Self.a + ( Self.va * DeltaTimer.delta )
		Self.x = Self.x + (( Sin( Self.a ) * Self.vx ) * DeltaTimer.delta )
		Self.y = Self.y + (( Cos( Self.a ) * Self.vx ) * DeltaTimer.delta )

		'wrap position using size of asteroid to eliminate jumping
		If Self.x &gt; DeviceWidth() + Self.radius Then Self.x = Self.x - ( DeviceWidth() + ( Self.radius * 2.0 ))
		If Self.y &gt; DeviceHeight() + Self.radius Then Self.y = Self.y - ( DeviceHeight() + ( Self.radius * 2.0 ))
		If Self.x &lt; 0 - Self.radius Then Self.x = Self.x + ( DeviceWidth() + ( Self.radius * 2.0 ))
		If Self.y &lt; 0 - Self.radius Then Self.y = Self.y + ( DeviceHeight() + ( Self.radius * 2.0 ))	
		
		'kill bullet if alive for too long
		Self.life = Self.life - 1
		If Self.life &lt;= 0 Then Self.ID.Remove()
		
	End Method


	'/ Renders Bullet
	Method Render : Void()
		PushMatrix()
		Translate Self.x + 8, Self.y + 8
		Rotate Self.a
		SetColor 100, 100, 100
		DrawThickLine( 0, -10, 0, 10, 5.0, False )	
		PopMatrix
		PushMatrix()
		Translate Self.x, Self.y
		Rotate Self.a
		SetColor 0, 0, 0
		DrawThickLine( 0, -10, 0, 10, 5.0, False )
		PopMatrix()
	End Method
	
	
	'/ Creates a new Bullet
	Function Create : Bullet( owned : Ship, px : Float, py : Float, pa : Float, pv : Float )
		Local this : Bullet = New Bullet( px, py, pa, pv )
		this.owner = owned
		this.ID = bulletList.AddLast( this )
		Return this
	End Function
	
	
	Function UpdateAll()
		For Local this : Bullet = Eachin bulletList
			this.Update()
		Next
	End Function
	
	Function RenderAll()
		For Local this : Bullet = Eachin bulletList
			this.Render()
		Next
	End Function
	

End Class





'/ Ship Class
Class Ship Extends BaseObject

	Field score : Int
	
	Field health : Int
	
	Field lives : Int
	
	'
	Field fireTime : Int
	
	Field startX : Int
	Field startY : Int

	
	'/ New
	Method New( px : Float, py : Float )
		Self.x = px
		Self.y = py
		Self.startX = px
		Self.startY = py
		Self.radius = 20.0
		Self.lives = 6
		Self.health = 100
	End Method


	'/ Update Ship
	Method Update : Void()
	
		If Self.lives &lt;= 0 Then Return
	
		'down thrust
		If KeyDown( KEY_DOWN )
			Self.vx = Self.vx + ( Sin( Self.a ) * THRUST_FORCE ) * DeltaTimer.delta
			Self.vy = Self.vy + ( Cos( Self.a ) * THRUST_FORCE ) * DeltaTimer.delta
			Self.vx = Max( -MAXIMUM_SHIP_VELOCITY, Min( MAXIMUM_SHIP_VELOCITY, Self.vx ))
			Self.vy = Max( -MAXIMUM_SHIP_VELOCITY, Min( MAXIMUM_SHIP_VELOCITY, Self.vy ))
		Endif
		
		'up thrust
		If KeyDown( KEY_UP )
			Self.vx = Self.vx + ( Sin( 180 + Self.a ) * THRUST_FORCE ) * DeltaTimer.delta
			Self.vy = Self.vy + ( Cos( 180 + Self.a ) * THRUST_FORCE ) * DeltaTimer.delta
			Self.vx = Max( -MAXIMUM_SHIP_VELOCITY, Min( MAXIMUM_SHIP_VELOCITY, Self.vx ))
			Self.vy = Max( -MAXIMUM_SHIP_VELOCITY, Min( MAXIMUM_SHIP_VELOCITY, Self.vy ))	
		Endif
	
		'turning left and right
		Self.va = ( KeyDown( KEY_LEFT ) - KeyDown( KEY_RIGHT )) * TURN_RATE
		
		'firing
		If KeyDown( KEY_SPACE )
			If Millisecs() - Self.fireTime &gt; FIRE_RATE
				Self.fireTime = Millisecs()
				Bullet.Create( Self, Self.x, Self.y, 180.0 + Self.a, Sqrt(( Self.vx * Self.vx ) + ( Self.vy * Self.vy )) + 8.0 )
			Endif
		Endif

		'updates position
		Self.UpdatePosition()	
	
	
		
		'check for asteroid collision
		For Local this : Asteroid = Eachin Asteroid.asteroidList
	
			Local dx : Float = Self.x - this.x
			Local dy : Float = Self.y - this.y
			Local dsq : Float = ( dx * dx ) + ( dy * dy )
			
			If dsq &lt; Pow( Self.radius + this.radius, 2 )
			
				Self.health = Self.health - 10
			
			Endif
	
		Next
		
		
		'kill and reset
		If Self.health &lt;= 0
		
			Self.lives = Self.lives - 1
			Self.Reset()
		
		Endif
		
		
	End Method



	'/ Resets position
	Method Reset()
		Self.x = Self.startX
		Self.y = Self.startY
		Self.health = 100
	End Method
	
	
	
	'/ Draws Solid Shape
	Method DrawSolid()
		DrawPoly([ 0.0, 0.0, -15.0, 20.0, -5.0, 15.0, 5.0, 15.0, 15.0, 20.0, 0.0, -20.0, -15.0, 20.0 ])	
	End Method


	'/ Render Ship
	Method Render : Void()

		If Self.lives &lt;= 0 Then Return
		
		PushMatrix()
		Translate Self.x + 8, Self.y + 8
		Rotate Self.a
		SetColor 100, 100, 100
		Self.DrawSolid()		
		PopMatrix()
	
	
		'push
		PushMatrix()
		
		'transforms matrix
		Translate Self.x, Self.y
		Rotate Self.a

		SetColor 255, 255, 255
		Self.DrawSolid()
		SetColor 0, 0, 0
		DrawThickLine( -15, 20, 0, -20, 4.0, True )
		DrawThickLine( 0, -20, 15, 20, 4.0, True )
		DrawThickLine( -15, 20, -5, 15, 4.0, True )	
		DrawThickLine( 5, 15, 15, 20, 4.0, True )
		DrawThickLine( -5, 15, 5, 15, 4.0, True )
	
		'pop
		PopMatrix()
		
	End Method

End Class











Class Asteroid Extends BaseObject

	Global asteroidList : List&lt; Asteroid &gt; = New List&lt; Asteroid &gt;

	'/ scale
	Field scale : Float
	
	Field health : Int
	
	Field ID : list.Node&lt; Asteroid &gt;
	
	'/ stores x,y
	Field points : Float[]
	
	'/ New Asteroid
	Method New( px : Float, py : Float, scale : Float )
		Self.points = Asteroid.GeneratePoints()
		Self.x = px
		Self.y = py
		Self.vx = Rnd( -3.0, 3.0 )
		Self.vy = Rnd( -3.0, 3.0 )
		Self.va = Rnd( -2.0, 2.0 )
		Self.scale = scale
		Self.health = scale
		Self.a = Rnd( 360.0 )
		Self.CalculateRadius()
	End Method
	
	
	
	'/ Calcualtes Radius for Collision Checking
	Method CalculateRadius()
		Local pLength : Int = Self.points.Length
		Local tDist : Float = 0.0
		For Local i : Int = 0 Until pLength / 2
			Local index : Int = i * 2
			Local x : Float = points[ index ]
			Local y : Float = points[ index + 1 ]
			tDist = tDist + ( Sqrt(( x * x ) + ( y * y )) * ( Self.scale * 16 ))
		Next
		Self.radius = tDist / Float(( pLength / 2 ) - 1 )
	End Method



	'/ Updates Asteroid
	Method Update : Void()
		
		Self.vx = Max( -MAXIMUM_ASTEROID_VELOCITY, Min( MAXIMUM_ASTEROID_VELOCITY, Self.vx ))
		Self.vy = Max( -MAXIMUM_ASTEROID_VELOCITY, Min( MAXIMUM_ASTEROID_VELOCITY, Self.vy ))	
		
		Self.UpdatePosition()
		
	
		'handle collision
		For Local this : Bullet = Eachin Bullet.bulletList
			
			Local dx : Float = Self.x - this.x
			Local dy : Float = Self.y - this.y
			Local dsq : Float = ( dx * dx ) + ( dy * dy )
			
			If dsq &lt;  Pow( Self.radius + this.radius, 2 )
			
				'update velocity
				Local bulletAngle : Float = this.a
				Self.vx = Self.vx + ( Sin( bulletAngle ) * ( this.vx * 0.1 ))
				Self.vy = Self.vy + ( Cos( bulletAngle ) * ( this.vx * 0.1 ))
			
				'update score
				this.owner.score = this.owner.score + ASTEROID_HIT_BONUS
				
				'kill bullet
				this.ID.Remove()
				
				Self.health = Self.health - 1
				
				If Self.health = 0
				
					'update score
					this.owner.score = this.owner.score + ASTEROID_KILL_BONUS
				
					'smaller rock
					Self.scale = Self.scale - 1
					Self.health = Self.scale
		
					'rock is dead
					If Self.scale &lt; 1.0
						Self.ID.Remove()
						Exit
					Else
						
						'spawn second rock
						Local newA : Asteroid = Asteroid.Create( Self.x, Self.y, Self.scale )
						newA.vx = Self.vx
						newA.vy = Self.vy
						
						Self.vx = Self.vx + ( Sin( bulletAngle + 90 ) * 1.0 )
						Self.vy = Self.vy + ( Cos( bulletAngle + 90 ) * 1.0 )
						
						newA.vx = newA.vx + ( Sin( bulletAngle - 90 ) * 1.0 )
						newA.vy = newA.vy + ( Cos( bulletAngle - 90 ) * 1.0 )						
						
					Endif
				
				Endif
				
			Endif
			
		Next
	
		
	End Method
	

	'/ Renders Asteroid
	Method Render : Void()

		'if not points defined, return safely
		If points.Length = 0 Then Return

		'push
		PushMatrix()
		
		
		'transforms matrix
		Translate Self.x + 8, Self.y + 8
		Rotate Self.a
		Scale Self.scale * 16, Self.scale * 16
		SetColor 100, 100, 100
		DrawPoly( Self.points )
		
		PopMatrix()
		
		PushMatrix()
		
		'transforms matrix
		Translate Self.x, Self.y
		Rotate Self.a
		Scale Self.scale * 16, Self.scale * 16

		'renders shape
		SetColor 255, 255, 255
		DrawPoly( Self.points )

		'renders lines
		SetColor 0, 0, 0
		Local x1 : Float = points[ 2 ]
		Local y1 : Float = points[ 3 ]
		Local pLength : Int = Self.points.Length
		For Local i : Int = 2 Until pLength / 2
			Local index : Int = i * 2
			Local x2 : Float = points[ index ]
			Local y2 : Float = points[ index + 1 ]		
			DrawThickLine( x1, y1, x2, y2, (( 1.0 / ( Self.scale * 16 )) * 4.0 ), True )
			x1 = x2
			y1 = y2
		Next


		'pop
		PopMatrix()
		
		'reset color
		SetColor 255, 255, 255

	End Method
	
	
	'function to generate points
	Function GeneratePoints : Float[]()
		Local angle : Float = 0.0
		Local stack : FloatStack = New FloatStack
		stack.Push( 0 )
		stack.Push( 0 )
		Local distance : Float = Rnd( 5.0, 10.0 ) / 10.0		
		Local x1 : Float = Sin( angle ) * distance
		Local y1 : Float = Cos( angle ) * distance
		Local x : Float = x1
		Local y : Float = y1
		Repeat
			stack.Push( x )
			stack.Push( y )
			angle = angle + Rnd( 10.0, 40.0 )
			distance = Rnd( 5.0, 10.0 ) / 10.0
			x = Sin( angle ) * distance
			y = Cos( angle ) * distance
		Until angle &gt;= 360.0
		stack.Push( x1 )
		stack.Push( y1 )
		Return stack.ToArray()
	End Function



	'/ Creates a new Asteroid
	Function Create : Asteroid( px : Float, py : Float, ps : Float )
		Local this : Asteroid = New Asteroid( px, py, ps )
		this.ID = asteroidList.AddLast( this )
		Return this
	End Function
	
	
	Function UpdateAll()
		For Local this : Asteroid = Eachin asteroidList
			this.Update()
		Next
	End Function
	
	Function RenderAll()
		For Local this : Asteroid = Eachin asteroidList
			this.Render()
		Next
	End Function
	
	
	
End Class






'/ Draws thick line
Function DrawThickLine( x1 : Float, y1 : Float, x2 : Float, y2 : Float, thickness : Float = 2.0, rounded : Bool = False )
	Local dx : Float = x2 - x1
	Local dy : Float = y2 - y1
	Local d : Float = Sqrt( dx * dx + dy * dy )
	Local vx : Float = dx / d
	Local vy : Float = dy / d
	Local nx : Float = vy
	Local ny : Float = -vx
	Local points : Float[ 8 ]
	points[ 0 ] = x1 + ( nx * ( thickness / 2.0 ))
	points[ 1 ] = y1 + ( ny * ( thickness / 2.0 ))
	points[ 2 ] = x1 + ( -nx * ( thickness / 2.0 ))
	points[ 3 ] = y1 + ( -ny * ( thickness / 2.0 ))
	points[ 4 ] = x2 + ( -nx * ( thickness / 2.0 ))
	points[ 5 ] = y2 + ( -ny * ( thickness / 2.0 ))	
	points[ 6 ] = x2 + ( nx * ( thickness / 2.0 ))
	points[ 7 ] = y2 + ( ny * ( thickness / 2.0 ))
	DrawPoly( points )
	If rounded = True
		DrawEllipse x1, y1, thickness / 2.0, thickness / 2.0
		DrawEllipse x2, y2, thickness / 2.0, thickness / 2.0
	Endif
End Function



Class Digit

	Field data : Int[]
	
	Field w : Int
	Field h : Int
	
	Method New( w : Int, h : Int, d : Int[])
		Self.w = w
		Self.h = h
		Self.data = d
	End Method
	
	Method Render( px : Float, py : Float, s : Float )
		Local index : Int = 0
		For Local y : Int = 0 Until Self.h
			For Local x : Int = 0 Until Self.w
				If Self.data[ index ] = 1
					DrawRect px + ( x * s ), py + ( y * s ), s, s
				Endif
				index = index + 1
			Next
		Next
	End Method

End Class




Class Font

	Global CharacterMap : StringMap&lt; Digit &gt; = New StringMap&lt; Digit &gt;

	Function CreateCharacterMap()
		CharacterMap.Set( "0", New Digit( 4, 5, [ 1, 1, 1, 1, 1, 0, 0, 1, 1, 0, 0, 1, 1, 0, 0, 1, 1, 1, 1, 1 ] ))
		CharacterMap.Set( "1", New Digit( 2, 5, [ 1, 1, 0, 1, 0, 1, 0, 1, 0, 1 ] ))
		CharacterMap.Set( "2", New Digit( 4, 5, [ 1, 1, 1, 1, 0, 0, 0, 1, 1, 1, 1, 1, 1, 0, 0, 0, 1, 1, 1, 1 ] ))
		CharacterMap.Set( "3", New Digit( 4, 5, [ 1, 1, 1, 1, 0, 0, 0, 1, 0, 1, 1, 1, 0, 0, 0, 1, 1, 1, 1, 1 ] ))
		CharacterMap.Set( "4", New Digit( 4, 5, [ 1, 0, 0, 1, 1, 0, 0, 1, 1, 0, 0, 1, 1, 1, 1, 1, 0, 0, 0, 1 ] ))
		CharacterMap.Set( "5", New Digit( 4, 5, [ 1, 1, 1, 1, 1, 0, 0, 0, 1, 1, 1, 1, 0, 0, 0, 1, 1, 1, 1, 1 ] ))
		CharacterMap.Set( "6", New Digit( 4, 5, [ 1, 1, 1, 0, 1, 0, 0, 0, 1, 1, 1, 1, 1, 0, 0, 1, 1, 1, 1, 1 ] ))
		CharacterMap.Set( "7", New Digit( 4, 5, [ 1, 1, 1, 1, 0, 0, 0, 1, 0, 0, 1, 0, 0, 1, 0, 0, 0, 1, 0, 0 ] ))
		CharacterMap.Set( "8", New Digit( 4, 5, [ 1, 1, 1, 1, 1, 0, 0, 1, 1, 1, 1, 1, 1, 0, 0, 1, 1, 1, 1, 1 ] ))
		CharacterMap.Set( "9", New Digit( 4, 5, [ 1, 1, 1, 1, 1, 0, 0, 1, 1, 1, 1, 1, 0, 0, 0, 1, 0, 1, 1, 1 ] ))
		CharacterMap.Set( "A", New Digit( 4, 5, [ 1, 1, 1, 1, 1, 0, 0, 1, 1, 1, 1, 1, 1, 0, 0, 1, 1, 0, 0, 1 ] ))
		CharacterMap.Set( "B", New Digit( 4, 5, [ 1, 1, 1, 0, 1, 0, 1, 0, 1, 1, 1, 1, 1, 0, 0, 1, 1, 1, 1, 1 ] ))
		CharacterMap.Set( "C", New Digit( 4, 5, [ 1, 1, 1, 1, 1, 0, 0, 0, 1, 0, 0, 0, 1, 0, 0, 0, 1, 1, 1, 1 ] ))
		CharacterMap.Set( "D", New Digit( 4, 5, [ 1, 1, 1, 0, 1, 0, 0, 1, 1, 0, 0, 1, 1, 0, 0, 1, 1, 1, 1, 0 ] ))
		CharacterMap.Set( "E", New Digit( 4, 5, [ 1, 1, 1, 1, 1, 0, 0, 0, 1, 1, 1, 1, 1, 0, 0, 0, 1, 1, 1, 1 ] ))
		CharacterMap.Set( "F", New Digit( 4, 5, [ 1, 1, 1, 1, 1, 0, 0, 0, 1, 1, 1, 1, 1, 0, 0, 0, 1, 0, 0, 0 ] ))
		CharacterMap.Set( "G", New Digit( 4, 5, [ 1, 1, 1, 1, 1, 0, 0, 0, 1, 0, 1, 1, 1, 0, 0, 1, 1, 1, 1, 1 ] ))
		CharacterMap.Set( "H", New Digit( 4, 5, [ 1, 0, 0, 1, 1, 0, 0, 1, 1, 1, 1, 1, 1, 0, 0, 1, 1, 0, 0, 1 ] ))
		CharacterMap.Set( "I", New Digit( 3, 5, [ 1, 1, 1, 0, 1, 0, 0, 1, 0, 0, 1, 0, 1, 1, 1 ] ))
		CharacterMap.Set( "J", New Digit( 4, 5, [ 0, 0, 1, 1, 0, 0, 0, 1, 0, 0, 0, 1, 1, 0, 0, 1, 1, 1, 1, 1 ] ))
		CharacterMap.Set( "K", New Digit( 4, 5, [ 1, 0, 0, 1, 1, 0, 0, 1, 1, 1, 1, 0, 1, 0, 0, 1, 1, 0, 0, 1 ] ))
		CharacterMap.Set( "L", New Digit( 4, 5, [ 1, 0, 0, 0, 1, 0, 0, 0, 1, 0, 0, 0, 1, 0, 0, 0, 1, 1, 1, 1 ] ))
		CharacterMap.Set( "M", New Digit( 5, 5, [ 1, 1, 1, 1, 1, 1, 0, 1, 0, 1, 1, 0, 1, 0, 1, 1, 0, 1, 0, 1, 1, 0, 1, 0, 1 ] ))
		CharacterMap.Set( "N", New Digit( 4, 5, [ 1, 0, 0, 1, 1, 1, 0, 1, 1, 0, 1, 1, 1, 0, 0, 1, 1, 0, 0, 1 ] ))
		CharacterMap.Set( "O", New Digit( 4, 5, [ 1, 1, 1, 1, 1, 0, 0, 1, 1, 0, 0, 1, 1, 0, 0, 1, 1, 1, 1, 1 ] ))
		CharacterMap.Set( "P", New Digit( 4, 5, [ 1, 1, 1, 1, 1, 0, 0, 1, 1, 0, 0, 1, 1, 1, 1, 1, 1, 0, 0, 0 ] ))
		CharacterMap.Set( "Q", New Digit( 4, 5, [ 1, 1, 1, 1, 1, 0, 0, 1, 1, 0, 0, 1, 1, 0, 1, 1, 1, 1, 1, 1 ] ))
		CharacterMap.Set( "R", New Digit( 4, 5, [ 1, 1, 1, 1, 1, 0, 0, 1, 1, 1, 1, 1, 1, 0, 1, 0, 1, 0, 1, 1 ] ))
		CharacterMap.Set( "S", New Digit( 4, 5, [ 1, 1, 1, 1, 1, 0, 0, 0, 1, 1, 1, 1, 0, 0, 0, 1, 1, 1, 1, 1 ] ))
		CharacterMap.Set( "T", New Digit( 3, 5, [ 1, 1, 1, 0, 1, 0, 0, 1, 0, 0, 1, 0, 0, 1, 0 ] ))
		CharacterMap.Set( "U", New Digit( 4, 5, [ 1, 0, 0, 1, 1, 0, 0, 1, 1, 0, 0, 1, 1, 0, 0, 1, 1, 1, 1, 1 ] ))
		CharacterMap.Set( "V", New Digit( 4, 5, [ 1, 0, 0, 1, 1, 0, 0, 1, 1, 0, 1, 0, 1, 0, 1, 0, 0, 1, 0, 0 ] ))
		CharacterMap.Set( "W", New Digit( 5, 5, [ 1, 0, 1, 0, 1, 1, 0, 1, 0, 1, 1, 0, 1, 0, 1, 1, 0, 1, 0, 1, 1, 1, 1, 1, 1 ] ))
		CharacterMap.Set( "X", New Digit( 4, 5, [ 1, 0, 0, 1, 1, 0, 0, 1, 0, 1, 1, 0, 1, 0, 0, 1, 1, 0, 0, 1 ] ))
		CharacterMap.Set( "Y", New Digit( 4, 5, [ 1, 0, 0, 1, 1, 0, 0, 1, 1, 1, 1, 1, 0, 0, 0, 1, 1, 1, 1, 1 ] ))
		CharacterMap.Set( "Z", New Digit( 4, 5, [ 1, 1, 1, 1, 0, 0, 0, 1, 0, 1, 1, 0, 1, 0, 0, 0, 1, 1, 1, 1 ] ))
		CharacterMap.Set( " ", New Digit( 3, 5, [ 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0 ] ))
		CharacterMap.Set( "!", New Digit( 1, 5, [ 1, 1, 1, 0, 1 ] ))
		CharacterMap.Set( ".", New Digit( 1, 5, [ 0, 0, 0, 0, 1 ] ))
		CharacterMap.Set( "'", New Digit( 1, 5, [ 1, 1, 0, 0, 0 ] ))
		CharacterMap.Set( ":", New Digit( 1, 5, [ 0, 1, 0, 1, 0 ] ))
		CharacterMap.Set( ";", New Digit( 1, 5, [ 0, 1, 0, 1, 1 ] ))
		CharacterMap.Set( "|", New Digit( 1, 5, [ 1, 1, 1, 1, 1 ] ))
		CharacterMap.Set( ")", New Digit( 2, 5, [ 1, 0, 0, 1, 0, 1, 0, 1, 1, 0 ] ))
		CharacterMap.Set( "(", New Digit( 2, 5, [ 0, 1, 1, 0, 1, 0, 1, 0, 0, 1 ] ))
		CharacterMap.Set( "/", New Digit( 2, 5, [ 0, 1, 0, 1, 0, 1, 1, 0, 1, 0 ] ))
		CharacterMap.Set( "\", New Digit( 2, 5, [ 1, 0, 1, 0, 1, 0, 0, 1, 0, 1 ] ))
		CharacterMap.Set( "+", New Digit( 3, 5, [ 0, 0, 0, 0, 1, 0, 1, 1, 1, 0, 1, 0, 0, 0, 0 ] ))
		CharacterMap.Set( "-", New Digit( 3, 5, [ 0, 0, 0, 0, 0, 0, 1, 1, 1, 0, 0, 0, 0, 0, 0 ] ))
		CharacterMap.Set( "=", New Digit( 3, 5, [ 0, 0, 0, 1, 1, 1, 0, 0, 0, 1, 1, 1, 0, 0, 0 ] ))
		CharacterMap.Set( "*", New Digit( 3, 5, [ 0, 0, 0, 1, 0, 1, 0, 1, 0, 1, 0, 1, 0, 0, 0 ] ))
		CharacterMap.Set( "[", New Digit( 2, 5, [ 1, 1, 1, 0, 1, 0, 1, 0, 1, 1 ] ))
		CharacterMap.Set( "]", New Digit( 2, 5, [ 1, 1, 0, 1, 0, 1, 0, 1, 1, 1 ] ))
		CharacterMap.Set( "?", New Digit( 3, 5, [ 1, 1, 1, 0, 0, 1, 0, 1, 1, 0, 0, 0, 0, 1, 0 ] ))
		CharacterMap.Set( ",", New Digit( 2, 5, [ 0, 0, 0, 0, 0, 0, 0, 1, 1, 0 ] ))
		CharacterMap.Set( String.FromChar( 34 ), New Digit( 3, 5, [ 1, 0, 1, 1, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0 ] ))
	End Function



	Function Draw : Void( val : String, px : Float, py : Float, pScale : Float, pXSpace : Int = 8, xCentre : Float = 0.0, yCentre : Float = 0.0 )
		
		Local xSpace : Int = 0
		
		Local cx : Int = 0
		Local cy : Int = 0
		
		If xCentre &lt;&gt; 0.0 Or yCentre &lt;&gt; 0.0
			Local yMax : Int = 0
			For Local ii : Int = 0 Until val.Length
				Local dig : Digit = CharacterMap.Get( val[ ii..ii + 1 ].ToUpper() )
				xSpace = xSpace + ( dig.w * pScale )
				If yMax &lt; ( dig.h * pScale ) Then yMax = dig.h * pScale
				If ii &lt; val.Length - 1 Then xSpace = xSpace + pXSpace
			Next
			cx = xSpace * xCentre
			cy = yMax * yCentre
		Endif
		xSpace = 0
		
		For Local i : Int = 0 Until val.Length
			Local dig : Digit = CharacterMap.Get( val[ i..i + 1 ].ToUpper() )
			dig.Render( px - cx + xSpace, py - cy, pScale )
			xSpace = xSpace + ( dig.w * pScale ) + pXSpace
		Next
	End Function

End Class






Class MyApp Extends App


	Field player : Ship

	
	Method OnCreate()
	
		'set update rate and delta time
		SetUpdateRate 60
		DeltaTimer.SetDelta( 60 )
		
		
		Asteroid.Create( Rnd( 0, DeviceWidth()), Rnd( 0, DeviceHeight()), 4 )
		Asteroid.Create( Rnd( 0, DeviceWidth()), Rnd( 0, DeviceHeight()), 4 )
		Asteroid.Create( Rnd( 0, DeviceWidth()), Rnd( 0, DeviceHeight()), 4 )
		
		Self.player = New Ship( 300, 300 )

		Font.CreateCharacterMap

	End Method
	


	Method OnUpdate()
		
		'update delta time
		DeltaTimer.UpdateDelta()


		Self.player.Update()


		'/ Update all Asteroids
		Asteroid.UpdateAll()
	
	
		'/ Update all Bullets
		Bullet.UpdateAll()
		
		
	End Method
	


	Method OnRender()
		Cls 255, 255, 255	


		'/ Render all Bullets
		Bullet.RenderAll()
		

		'/ Render all Asteroids
		Asteroid.RenderAll()
		

		'/ Render Player
		Self.player.Render()



		'/ Renders Score
		SetColor 100, 100, 100
		Font.Draw( Self.player.score, 14, 14, 6 )
		SetColor 0, 0, 0
		Font.Draw( Self.player.score, 10, 10, 6 )
		
		
		'/ Renders Lives
		PushMatrix()
		SetColor 100, 100, 100
		Translate 0, 60
		Scale 0.65, 0.65
		Translate 6, 4
		For Local i : Int = 1 To Self.player.lives		
			Translate 35, 0
			Self.player.DrawSolid()
		Next
		PopMatrix()
		PushMatrix()
		SetColor 0, 0, 0
		Translate 0, 60
		Scale 0.65, 0.65
		For Local i : Int = 1 To Self.player.lives		
			Translate 35, 0
			Self.player.DrawSolid()
		Next
		PopMatrix()
		
		
		'/ Renders win / lose message
		If Self.player.lives &lt;= 0
			SetColor 100, 100, 100
			Font.Draw( "Game Over", 4 + ( DeviceWidth() / 2), 4 + ( DeviceHeight() / 2 ), 6, 8, 0.5, 0.5 )
			SetColor 175, 0, 0
			Font.Draw( "Game Over", DeviceWidth() / 2, DeviceHeight() / 2, 6, 8, 0.5, 0.5 )
		
		Else
			If Asteroid.asteroidList.IsEmpty() = True
				SetColor 100, 100, 100
				Font.Draw( "You Win", 4 + ( DeviceWidth() / 2), 4 + ( DeviceHeight() / 2 ), 6, 8, 0.5, 0.5 )
				SetColor 0, 175, 0
				Font.Draw( "You Win", DeviceWidth() / 2, DeviceHeight() / 2, 6, 8, 0.5, 0.5 )		
			Endif
		Endif
		
		
		
		
	End Method
	

End Class


Function Main()
	New MyApp()
End Function

</textarea> <br><br></td></tr></table><br>
<a name="2026356"></a>

<a name="2026621"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Neuro</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is cool :). <br><br></td></tr></table><br>
<a name="2026628"></a>

<a name="2026633"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> yeah pretty good, great graphical style and physics<br><br>Im gonna put it on kongregate and keep all the money!<br><br>(just kidding)<br><br>(or am I??)<br><br>(yeah just kidding really)<br><br>(or is that just a ruse to make you think Im kidding?)<br><br>(well if you go on kongregate and see asteroids by slenkar youll know) <br><br></td></tr></table><br>
<a name="2026640"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DruggedBunny</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice, and as slenkar says, great graphical style. <br><br></td></tr></table><br>
<a name="2026641"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> haha nice.  I didn't know Kongregate pays for having apps on the site. I'll have to look into that and focus a bit more on doing something I can sell. <br><br></td></tr></table><br>
<a name="2026714"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Why0Why</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Noodle,<br><br>Just wanted to say thanks for all of the code snippets that you put on here.  I really enjoy them and they have helped me out.<br><br>Thanks again! <br><br></td></tr></table><br>
<a name="2026717"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Nice, and as slenkar says, great graphical style. <br></div><br>Cheers James, having no media is pretty limiting but as a code snippet its probably better!<br><br><div class="quote"> Just wanted to say thanks for all of the code snippets that you put on here. I really enjoy them and they have helped me out. <br></div><br><br>No problem, I am glad people are finding them useful. Hopefully they aren't too poorly coded so that people can learn from them. Either way its better than having them stashed on my hard drive where they are of no use to anyone! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
