<!DOCTYPE html><html lang="en" ><head ><title >Simple image filters using convolution matrix</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Simple image filters using convolution matrix</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Simple image filters using convolution matrix</a><br><br>
<a name="2046197"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Beaker</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here is some nice code that can apply realtime (but sometimes slow) filters to images. Including blur, sharpen, emboss, edge detect, median blur, gaussian blur etc.<br><br>The Median function is particularly slow.<br><br>See an example here:<br><a href="http://beaker.powweb.com/convolve/MonkeyGame.html" target="_blank">Example</a><br><br>Enjoy. If you make any improvements, please post them here.<br><br>example.monkey<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">'Convolve image by Beaker 2013

Import mojo
Import convmatrix

Class ConvTest Extends App
	Field img:Image
	Field newImg:Image
	
	Field conv:ConvMatrix = ConvMatrix.GaussianBlur
'	Field conv:ConvMatrix = ConvMatrix.Emboss
'	Field conv:ConvMatrix = ConvMatrix.Blur
'	Field conv:ConvMatrix = ConvMatrix.BlurMore
'	Field conv:ConvMatrix = ConvMatrix.EdgeDetect
'	Field conv:ConvMatrix = ConvMatrix.EdgeDetect2
'	Field conv:ConvMatrix = ConvMatrix.EdgeDetect3
'	Field conv:ConvMatrix = ConvMatrix.Sharpen
	
	Method OnCreate()
		SetUpdateRate(30)
		
'		img = LoadImage("skull.png")
		img = LoadImage("original.jpg")
	End
	
	Method OnUpdate()
		
	End
	
	Method OnRender()
		If Not newImg
			newImg = CreateImage(img.Width(), img.Height())
			Cls
			DrawImage img,0,0
			
			Convolve(img, newImg, conv)
'			Median(img, newImg, conv)
			'img = newImg
		End
	
		Cls 0, 0, 0
		DrawImage newImg,0,0
		DrawImage img, img.Width(),0
		
		DrawText "Tap SPACE to apply filter again", 320,390,0.5,0.5
		If KeyDown(KEY_SPACE)
			Convolve(img, newImg, conv)
'			Median(img, newImg, conv)
			img = newImg
		End
	End
	
End

Function Main()
	New ConvTest
End</textarea><br><br>convmatrix.monkey<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">'Convolution Matrix by Beaker 2013

Strict
Import mojo

Class ConvMatrix
	Field matrix:Float[9]
	Field factor:Float = 1
	Field offset:Float = 0
	Field size:Int = 3
	
	Method New(factor:Float=1, offset:Float=0)
		Self.factor = factor
		Self.offset = offset
	End
	
	
	Method New(mat:Float[], factor:Float=1, offset:Float=0)
		Self.matrix = mat
		Self.factor = factor
		Self.offset = offset
	End
	
	Method SetAll:Void(val:Float)
		For Local i:= 0 To 8
			matrix[i] = val
		End
	End
	
	Function Blur:ConvMatrix()
		Local conv:ConvMatrix = New ConvMatrix()
		conv.SetAll(1)
		Return conv
	End
	
	Function BlurMore:ConvMatrix()
		Local conv:ConvMatrix = New ConvMatrix([1.0,2.0,1.0, 2.0,4.0,2.0, 1.0,2.0,1.0], 1,0)
		Return conv
	End
	
	Function Emboss:ConvMatrix()
		Local conv:ConvMatrix = New ConvMatrix([-2.0,-1.0,0.0, -1.0,1.0,1.0, 0.0,1.0,2.0], 1,127)
		Return conv
	End
	
	Function GaussianBlur:ConvMatrix()
		Local conv:ConvMatrix = New ConvMatrix()
		conv.matrix = [
			0.0,0.0,0.0,5.0,0.0,0.0,0.0,
			0.0,5.0,18.0,32.0,18.0,5.0,0.0,
			0.0,18.0,64.0,100.0,64.0,18.0,0.0,
			5.0,32.0,100.0,100.0,100.0,32.0,5.0,
			0.0,18.0,64.0,100.0,64.0,18.0,0.0,
			0.0,5.0,18.0,32.0,18.0,5.0,0.0,
			0.0,0.0,0.0,5.0,0.0,0.0,0.0]
		'Print Sqrt(conv.matrix.Length)
		conv.size = Sqrt(conv.matrix.Length)	'should be 7
		Return conv
	End
	
	Function EdgeDetect:ConvMatrix()
		Local conv:ConvMatrix = New ConvMatrix([-1.0,-1.0,-1.0, -1.0,8.0,-1.0, -1.0,-1.0,-1.0], 1,0)
		Return conv
	End

	Function EdgeDetect2:ConvMatrix()
		Local conv:ConvMatrix = New ConvMatrix([-5.0,0.0,0.0, 0.0,0.0,0.0, 0.0,0.0,5.0], 1,0)
		Return conv
	End
	
	Function EdgeDetect3:ConvMatrix()
		Local conv:ConvMatrix = New ConvMatrix([1.0,1.0,1.0, 1.0,-7.0,1.0, 1.0,1.0,1.0], 1,0)
		Return conv
	End
	

	Function Sharpen:ConvMatrix()
		Local conv:ConvMatrix = New ConvMatrix([-1.0,-1.0,-1.0, -1.0,9.0,-1.0, -1.0,-1.0,-1.0], 1,0)
		Return conv
	End
End

Function Convolve:Bool(image:Image, newImage:Image, conv:ConvMatrix)
	If conv.factor = 0 Return False
	
	Local rgb:Int
	Local r:Int, g:Int, b:Int
	Local divisor:Float = 0
	
	Local halfSize:Int = Floor(Float(conv.size)/2.0)
'	Print "halfSize="+halfSize
	
	Local pixels:Int[] = New Int[image.Width()*image.Height()]
	Local newPixels:Int[] = New Int[image.Width()*image.Height()]
	ReadPixels(pixels, 0,0, image.Width(), image.Height())
'	Print pixels.Length+" "+image.Width()+" "+image.Height()
	
	For Local x2:Int = 0 Until conv.size
		For Local y2:Int = 0 Until conv.size
			divisor += conv.matrix[x2+y2*conv.size]
		End
	End
	
	
	For Local x:Int = halfSize Until image.Width()-halfSize
		For Local y:Int = halfSize Until image.Height()-halfSize
			'Print x+" "+y
			Local newr:Int = 0
			Local newg:Int = 0
			Local newb:Int = 0
			For Local x2:Int = -halfSize To halfSize
				For Local y2:Int = -halfSize To halfSize
					'Print (x+x2)*image.Height()+(y+y2)
					rgb = pixels[(x+x2)+(y+y2)*image.Width()]
					
					r = (rgb Shr 16) &amp; $ff
					g = (rgb Shr 8) &amp; $ff
					b = rgb &amp; $ff
					
					Local offset:Int = (x2+halfSize)+(y2+halfSize)*conv.size
					newr += Float(r) * conv.matrix[offset]
					newg += Float(g) * conv.matrix[offset]
					newb += Float(b) * conv.matrix[offset]
				End
			End
			
			If divisor = 0 divisor = 1
			newr /= divisor
			newg /= divisor
			newb /= divisor
			
			newr += conv.offset
			newg += conv.offset
			newb += conv.offset
			
			newr = Min(Max(newr,0),255)
			newg = Min(Max(newg,0),255)
			newb = Min(Max(newb,0),255)
						
			newPixels[x+y*image.Width()] = $ff000000 | (newr Shl 16) | (newg Shl 8) | newb
		End
	End
	newImage.WritePixels(newPixels, 0,0, image.Width(), image.Height())
		
	Return True
End


Function Median:Bool(image:Image, newImage:Image, conv:ConvMatrix)
	If conv.factor = 0 Return False
	
	Local rgb:Int
	Local r:Int, g:Int, b:Int
	
	Local halfSize:Int = Floor(Float(conv.size)/2.0)
'	Print "halfSize="+halfSize
	
	Local pixels:Int[] = New Int[image.Width()*image.Height()]
	Local newPixels:Int[] = New Int[image.Width()*image.Height()]
	ReadPixels(pixels, 0,0, image.Width(), image.Height())
'	Print pixels.Length+" "+image.Width()+" "+image.Height()
	
	Local newr:Int = 0
	Local newg:Int = 0
	Local newb:Int = 0
	
	For Local x:Int = halfSize Until image.Width()-halfSize
		For Local y:Int = halfSize Until image.Height()-halfSize
			'Print x+" "+y
			
			Local rList:= New FloatList
			Local gList:= New FloatList
			Local bList:= New FloatList
			
			For Local x2:Int = -halfSize To halfSize
				For Local y2:Int = -halfSize To halfSize
					'Print (x+x2)*image.Height()+(y+y2)
					rgb = pixels[(x+x2)+(y+y2)*image.Width()]
					
					r = (rgb Shr 16) &amp; $ff
					g = (rgb Shr 8) &amp; $ff
					b = rgb &amp; $ff
					
					rList.AddLast(r)
					gList.AddLast(g)
					bList.AddLast(b)
					
				End
			End
			
			rList.Sort()
			gList.Sort()
			bList.Sort()
			
			Local rArr:= rList.ToArray()
			Local gArr:= gList.ToArray()
			Local bArr:= bList.ToArray()
			
			If rArr.Length Mod 2 = 1
				newr = rArr[rArr.Length/2]				
				newg = gArr[gArr.Length/2]
				newb = bArr[bArr.Length/2]
			Else
				newr = (rArr[rArr.Length/2]	 + rArr[rArr.Length/2+1]) /2			
				newg = (gArr[gArr.Length/2] + gArr[gArr.Length/2+1]) /2
				newb = (bArr[bArr.Length/2] + bArr[gArr.Length/2+1]) /2
			End
			
			newPixels[x+y*image.Width()] = $ff000000 | (newr Shl 16) | (newg Shl 8) | newb
		End
	End
	newImage.WritePixels(newPixels, 0,0, image.Width(), image.Height())
		
	Return True
End</textarea> <br><br></td></tr></table><br>
<a name="2046198"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Beaker</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I forgot to mention that it currently doesn't handle image edges very elegantly. I will probably fix this at some point. <br><br></td></tr></table><br>
<a name="2046250"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >semar</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice ! <br><br></td></tr></table><br>
<a name="2046254"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Difference</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> 2x Nice ! <br><br></td></tr></table><br>
<a name="2046268"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gerry Quinn</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> What should one do with edges?  I'm thinking two options: average the colour near an edge and treat outside pixels as being that colour, or wrap around (useful for tiled graphics). <br><br></td></tr></table><br>
<a name="2046276"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> gimp offers a choice for tiled/nontiled <br><br>this opens the possibility for monkey to do an elite style game with procedurally generated planet images ^_^ <br><br></td></tr></table><br>
<a name="2046349"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >muddy_shoes</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've done a little tweaking to improve performance a bit. The speed-up ranges from a little to a lot depending on target and build settings. I only tried HTML5, Flash, XNA and GLFW but the techniques are mostly transferable. Going native with this sort of thing would be the natural end-game though.<br><br>The output may not be exactly the same as the original but it looks visually equivalent. <br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

'This defines whether the colour channels are split into arrays first.
'Whether this is useful or not mostly depends on the cost of accessing array elements.
'For the most part turning it off is better or no worse than on.
#SEPARATE_CHANNELS = "False"

Function Convolve2:Bool(image:Image, newImage:Image, conv:ConvMatrix)
	
    If conv.factor = 0 Return False
	
    'store values locally to avoid call/reference costs 
    Local imgWidth:Int = image.Width()
    Local imgHeight:Int = image.Height()
    Local convSize:Int = conv.size
    Local convMatLength:Int = conv.matrix.Length()
    Local convOffset:Int = conv.offset
    
    Local divisor:Float = 0
	
    Local halfSize:Int = Floor(Float(convSize) / 2.0)
	
    Local numPixels:Int = imgWidth * imgHeight
    
    Local pixels:Int[] = New Int[numPixels]
	
    ReadPixels(pixels, 0, 0, imgWidth, imgHeight)
    
#If SEPARATE_CHANNELS = "True"
    'separate the colour channels
    Local rPixels:Int[] = New Int[numPixels]
    Local gPixels:Int[] = New Int[numPixels]
    Local bPixels:Int[] = New Int[numPixels]
	
    For Local pixIndex:Int = 0 Until numPixels
        Local rgb:Int = pixels[pixIndex]
	
        rPixels[pixIndex] = (rgb Shr 16) &amp; $ff
        gPixels[pixIndex] = (rgb Shr 8) &amp; $ff
        bPixels[pixIndex] = rgb &amp; $ff
    End
    
    'no need for second buffer
    Local newPixels:Int[] = pixels
	
#Else
    Local newPixels:Int[] = New Int[numPixels]
#End

    Local matrix:Float[] = New Float[convMatLength]
	
    For Local i:Int = 0 Until convMatLength
        divisor += conv.matrix[i]
    End
    
    If divisor = 0
        divisor = 1
    End
    
    'The divisor seems to be just used to normalise the result, so normalise the matrix instead
    divisor = 1.0 / divisor
    For Local i:Int = 0 Until convMatLength
        matrix[i] = conv.matrix[i] * divisor
    End
	
    For Local x:Int = halfSize Until imgWidth - halfSize
        For Local y:Int = halfSize Until imgHeight-halfSize
			
            Local r:Int, g:Int, b:Int
            Local newr:Float = convOffset
            Local newg:Float = convOffset
            Local newb:Float = convOffset
            
            Local pixIndex:Int = x - halfSize + (y - halfSize) * imgWidth
		    
            Local matIndex:Int = 0
           
            For Local y2:Int = -halfSize To halfSize
                For Local x2:Int = -halfSize To halfSize                    
	
#If SEPARATE_CHANNELS = "True"
                    r = rPixels[pixIndex]
                    g = gPixels[pixIndex]
                    b = bPixels[pixIndex]
#Else					
                    Local rgb:Int = pixels[pixIndex]
	
                    r = (rgb Shr 16) &amp; $ff
                    g = (rgb Shr 8) &amp; $ff
                    b = rgb &amp; $ff
#End 
					
                    Local convVal:Float = matrix[matIndex]
                    newr += r * convVal
                    newg += g * convVal
                    newb += b * convVal
                    matIndex += 1
                    pixIndex += 1
		End
                pixIndex += imgWidth - convSize
            End
            
            r = Clamp(Int(newr), 0, 255)
            g = Clamp(Int(newg), 0, 255)
            b = Clamp(Int(newb), 0, 255)
						
            newPixels[x + y * imgWidth] = $ff000000 | (r Shl 16) | (g Shl 8) | b
        End
    End

    newImage.WritePixels(newPixels, 0,0, imgWidth, imgHeight)
		
    Return True
End

Function Swap:Void(arr:Int[], a:Int, b:Int)
    Local tmp:Int = arr[a]
    arr[a] = arr[b]
    arr[b] = tmp
End

'QuickSelect. Not tested much. Based on psuedocode from <a href="http://en.wikipedia.org/wiki/Selection_algorithm" target="_blank">http://en.wikipedia.org/wiki/Selection_algorithm</a>

Function Partition:Int(arr:Int[], left:Int, right:Int, pivotIndex:Int)
    Local pivotValue:Int = arr[pivotIndex]
    Swap(arr, pivotIndex, right) ' Move pivot to end
    Local storeIndex:Int = left
    For Local i:Int = left Until right
        If arr[i] &lt;= pivotValue
            Swap(arr, storeIndex, i)
            storeIndex += 1
        End
    End
    Swap(arr, right, storeIndex) ' Move pivot To its final place
    Return storeIndex
End

Function SelectVal:Int(arr:Int[], left:Int, right:Int, k:Int)
    While True
        ' select pivotIndex between left and right
        Local pivotIndex:Int = Int(Rnd(left, right))
        Local pivotNewIndex:Int = Partition(arr, left, right, pivotIndex)
        Local pivotDist:Int = pivotNewIndex - left + 1
        If pivotDist = k
            Return arr[pivotNewIndex]
        ElseIf k &lt; pivotDist
            right = pivotNewIndex - 1
        Else
            k = k - pivotDist
            left = pivotNewIndex + 1
        End
    End
End


Function Median2:Bool(image:Image, newImage:Image, conv:ConvMatrix)
    Local imgWidth:Int = image.Width()
    Local imgHeight:Int = image.Height()
    Local convSize:Int = conv.size
    
    If conv.factor = 0 Return False
	
    Local halfSize:Int = Floor(Float(convSize) / 2.0)
	
    Local numPixels:Int = imgWidth * imgHeight
    Local pixels:Int[] = New Int[numPixels]
	
    ReadPixels(pixels, 0, 0, imgWidth, imgHeight)
	
    Local newr:Int = 0
    Local newg:Int = 0
    Local newb:Int = 0
	
    Local convMatLength:Int = conv.matrix.Length()
    Local arrMid:Int = (convMatLength + 1) * 0.5
			
    Local rWindow:Int[] = New Int[convMatLength]
    Local gWindow:Int[] = New Int[convMatLength]
    Local bWindow:Int[] = New Int[convMatLength]
      
#If SEPARATE_CHANNELS = "True"
    'separate the colour channels
    Local rPixels:Int[] = New Int[numPixels]
    Local gPixels:Int[] = New Int[numPixels]
    Local bPixels:Int[] = New Int[numPixels]
	
    For Local pixIndex:Int = 0 Until numPixels
        Local rgb:Int = pixels[pixIndex]
	
        rPixels[pixIndex] = (rgb Shr 16) &amp; $ff
        gPixels[pixIndex] = (rgb Shr 8) &amp; $ff
        bPixels[pixIndex] = rgb &amp; $ff
    End
    
    'no need for second buffer
    Local newPixels:Int[] = pixels
	
#Else
    Local newPixels:Int[] = New Int[numPixels]
#End
    		
    For Local x:Int = halfSize Until imgWidth-halfSize
        For Local y:Int = halfSize Until imgHeight - halfSize

            Local convIndex:Int = 0

            For Local x2:Int = -halfSize To halfSize
                For Local y2:Int = -halfSize To halfSize
                    Local pixIndex:Int = (x + x2) + (y + y2) * imgWidth
            
#If SEPARATE_CHANNELS = "True"
                    rWindow[convIndex] = rPixels[pixIndex]
                    gWindow[convIndex] = gPixels[pixIndex]
                    bWindow[convIndex] = bPixels[pixIndex]
#Else					
                    Local rgb:Int = pixels[pixIndex]
	
                    rWindow[convIndex] = (rgb Shr 16) &amp; $ff
                    gWindow[convIndex] = (rgb Shr 8) &amp; $ff
                    bWindow[convIndex] = rgb &amp; $ff
#End                    
                    convIndex += 1
			
            	End
            End

            newr = SelectVal(rWindow, 0, convMatLength - 1, arrMid)
            newg = SelectVal(gWindow, 0, convMatLength - 1, arrMid)
            newb = SelectVal(bWindow, 0, convMatLength - 1, arrMid)
			
            newPixels[x + y * imgWidth] = $ff000000 | (newr Shl 16) | (newg Shl 8) | newb
        End
    End

    newImage.WritePixels(newPixels, 0, 0, imgWidth, imgHeight)
		
    Return True
End

</textarea> <br><br></td></tr></table><br>
<a name="2046348"></a>

<a name="2046356"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Beaker</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> The only problem (and a quite major one) that I can see with your nice optimised code is that it will be much harder (if not impossible) to adjust it to handle image edges. Or am I missing something? <br><br></td></tr></table><br>
<a name="2046372"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >muddy_shoes</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> You're missing a description of how you would handle edges. <br><br></td></tr></table><br>
<a name="2046387"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Beaker</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Like this:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Code removed to protect the stupid (ie. me)</textarea> <br><br></td></tr></table><br>
<a name="2046378"></a>

<a name="2046380"></a>

<a name="2046381"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >muddy_shoes</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> What's that meant to be doing? It looks like it wraps the pixel values to the right and bottom and repeats the border values to the left and top. <br><br></td></tr></table><br>
<a name="2046382"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Beaker</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> It doesn't wrap them, instead it clamps the edges, which works great. <br><br></td></tr></table><br>
<a name="2046383"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >muddy_shoes</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> [monkeycode]<br>For Local x2:Int = -halfSize To halfSize<br>    Local newx:Int = (x+x2) Mod image.Width()<br>    If newx &lt; 0 newx = 0<br>    For Local y2:Int = -halfSize To halfSize<br>        Local newy:Int = (y+y2) Mod image.Height()<br>	If newy &lt; 0 newy = 0<br>					<br>[/monkeycode]<br><br>If you put (x+x2) = width and (y+y2) = height into that you get 0,0 out. It wraps.<br><br>If you put (x+x2) = -1 and (y+y2) = -1 into that you get 0,0 out. It clamps. <br><br></td></tr></table><br>
<a name="2046385"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >muddy_shoes</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Anyway, I believe this does what you intend:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'This defines whether the colour channels are split into arrays first.
'Whether this is useful or not mostly depends on the cost of accessing array elements.
'For the most part turning it off is better or no worse than on.
#SEPARATE_CHANNELS = "False"

Function Convolve2:Bool(image:Image, newImage:Image, conv:ConvMatrix)
	
    If conv.factor = 0 Return False
	
    'store values locally to avoid call/reference costs 
    Local imgWidth:Int = image.Width()
    Local imgHeight:Int = image.Height()
    Local convSize:Int = conv.size
    Local convMatLength:Int = conv.matrix.Length()
    Local convOffset:Int = conv.offset
    
    Local divisor:Float = 0
	
    Local halfSize:Int = Floor(Float(convSize) / 2.0)
	
    Local numPixels:Int = imgWidth * imgHeight
    
    Local pixels:Int[] = New Int[numPixels]
	
    ReadPixels(pixels, 0, 0, imgWidth, imgHeight)
    
#If SEPARATE_CHANNELS = "True"
    'separate the colour channels
    Local rPixels:Int[] = New Int[numPixels]
    Local gPixels:Int[] = New Int[numPixels]
    Local bPixels:Int[] = New Int[numPixels]
	
    For Local pixIndex:Int = 0 Until numPixels
        Local rgb:Int = pixels[pixIndex]
	
        rPixels[pixIndex] = (rgb Shr 16) &amp; $ff
        gPixels[pixIndex] = (rgb Shr 8) &amp; $ff
        bPixels[pixIndex] = rgb &amp; $ff
    End
    
    'no need for second buffer
    Local newPixels:Int[] = pixels
	
#Else
    Local newPixels:Int[] = New Int[numPixels]
#End

    Local matrix:Float[] = New Float[convMatLength]
	
    For Local i:Int = 0 Until convMatLength
        divisor += conv.matrix[i]
    End
    
    If divisor = 0
        divisor = 1
    End
    
    'The divisor seems to be just used to normalise the result, so normalise the matrix instead
    divisor = 1.0 / divisor
    For Local i:Int = 0 Until convMatLength
        matrix[i] = conv.matrix[i] * divisor
    End
	
    For Local x:Int = 0 Until imgWidth
        For Local y:Int = 0 Until imgHeight
			
            Local r:Int, g:Int, b:Int
            Local newr:Float = convOffset
            Local newg:Float = convOffset
            Local newb:Float = convOffset
            
            Local pixIndex:Int '= x - halfSize + (y - halfSize) * imgWidth
		    
            Local matIndex:Int = 0
           
            For Local y2:Int = -halfSize To halfSize
                Local mappedY:Int = Clamp(y + y2, 0, imgHeight - 1)
                For Local x2:Int = -halfSize To halfSize                    
                    Local mappedX:Int = Clamp(x + x2, 0, imgWidth - 1)
                    
                    pixIndex = mappedX+mappedY*imgWidth

#If SEPARATE_CHANNELS = "True"
                    r = rPixels[pixIndex]
                    g = gPixels[pixIndex]
                    b = bPixels[pixIndex]
#Else					
                    Local rgb:Int = pixels[pixIndex]
	
                    r = (rgb Shr 16) &amp; $ff
                    g = (rgb Shr 8) &amp; $ff
                    b = rgb &amp; $ff
#End 
					
                    Local convVal:Float = matrix[matIndex]
                    newr += r * convVal
                    newg += g * convVal
                    newb += b * convVal
                    matIndex += 1
                End
            End
            
            r = Clamp(Int(newr), 0, 255)
            g = Clamp(Int(newg), 0, 255)
            b = Clamp(Int(newb), 0, 255)
						
            newPixels[x + y * imgWidth] = $ff000000 | (r Shl 16) | (g Shl 8) | b
        End
    End

    newImage.WritePixels(newPixels, 0,0, imgWidth, imgHeight)
		
    Return True
End

</textarea> <br><br></td></tr></table><br>
<a name="2046386"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Beaker</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oops. Sorry, I'm an idiot. In my defense it's been a very long day. How about this:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Function Convolve:Bool(image:Image, newImage:Image, conv:ConvMatrix)
	If conv.factor = 0 Return False
	
	Local rgb:Int
	Local r:Int, g:Int, b:Int
	Local divisor:Float = 0
	
	Local halfSize:Int = Floor(Float(conv.size)/2.0)
	
	Local pixels:Int[] = New Int[image.Width()*image.Height()]
	Local newPixels:Int[] = New Int[image.Width()*image.Height()]
	
	ReadPixels(pixels, 0,0, image.Width(),image.Height())
	
	For Local x2:Int = 0 Until conv.size
		For Local y2:Int = 0 Until conv.size
			divisor += conv.matrix[x2+y2*conv.size]
		End
	End
	
	
	For Local x:Int = 0 Until image.Width()
		For Local y:Int = 0 Until image.Height()
			Local newr:Int = 0
			Local newg:Int = 0
			Local newb:Int = 0
			For Local x2:Int = -halfSize To halfSize
				Local newx:Int = Clamp(x+x2,0,image.Width()-1)
				For Local y2:Int = -halfSize To halfSize
					Local newy:Int = Clamp(y+y2,0,image.Height()-1)
					
					rgb = pixels[newx+newy*image.Width()]
					
					r = (rgb Shr 16) &amp; $ff
					g = (rgb Shr 8) &amp; $ff
					b = rgb &amp; $ff
					
					Local offset:Int = (x2+halfSize)+(y2+halfSize)*conv.size
					newr += Float(r) * conv.matrix[offset]
					newg += Float(g) * conv.matrix[offset]
					newb += Float(b) * conv.matrix[offset]
				End
			End
			
			If divisor = 0 divisor = 1
			newr /= divisor
			newg /= divisor
			newb /= divisor
			
			newr += conv.offset
			newg += conv.offset
			newb += conv.offset
			
			newr = Clamp(newr,0,255)
			newg = Clamp(newg,0,255)
			newb = Clamp(newb,0,255)
						
			newPixels[x+y*image.Width()] = $ff000000 | (newr Shl 16) | (newg Shl 8) | newb
		End
	End
	newImage.WritePixels(newPixels, 0,0, image.Width(), image.Height())
		
	Return True
End</textarea> <br><br></td></tr></table><br>
<a name="2046388"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Beaker</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> muddy_shoes - looks great. <br><br></td></tr></table><br>
<a name="2046560"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> thank you for posting this, I have never heard of convolution matrices before, very interesting reading up on them and their uses. I saw this as an interesting programming challenge and decided to try writing my own now. After a few hours I managed to get mine working correctly, It's always easier implementing something when there is reference code to help!<br><br>I believe I have a working Sobel edge filter although I need to read up on it some more to make sure, the output seems to match but it was a weird one.<br><br>Is there a list or website showing kernel values or filter names that can be used with this method? I have about 14 so far, would be good to get a complete collection. <br><br></td></tr></table><br>
<a name="2046557"></a>

<a name="2046556"></a>

<a name="2046555"></a>

<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
