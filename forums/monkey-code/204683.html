<!DOCTYPE html><html lang="en" ><head ><title >Useful functions</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Useful functions</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Useful functions</a><br><br>
<a name="2050509"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >EdzUp</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here is some code I found useful :)<br><br><pre class=code>
'
'	EdzUpFunctions.monkey - Copyright ©EdzUp
'	Pragrammed by Ed 'EdzUp' Upton
'
Strict

Import mojo.graphics

'for scaling system
Global DesignScaleX:Float = 0.0
Global DesignScaleY:Float = 0.0

Global PixelScaleX:Float = 0.0
Global PixelScaleY:Float = 0.0

'for platform specific checks
Global Platform:Int =0
Const Platform_Mobile:Int = 1
Const Platform_Internet:Int = -1
Const Platform_Desktop:Int = 0

Function CheckPlatform:Int()
	Platform =Platform_Desktop					'desktop (default but will be changed if its a different target)
	#If TARGET="android" Or TARGET="ios" Or TARGET ="winphone8"
		Platform = Platform_Mobile				'mobile
	#Else
		#If TARGET="html5" Or TARGET="flash" Or TARGET="java"
			Platform = Platform_Internet			'internet
		#Endif
	#Endif
	
	Return( Platform )
End

Function SetDesignScale:Int( X:Float, Y:Float )
	DesignScaleX = X
	DesignScaleY = Y
	
	'this should work out the scales of the pixels on screen for system movement etc
	PixelScaleX = DeviceWidth() /X
	PixelScaleY = DeviceHeight() /Y	
	
	Return( 0 )
End

Function ScaleX:Float()
	If DesignScaleX = 0 Then Error "No scaling set, use SetDesignScale( float, float ) in OnCreate"
	
	Return( DesignScaleX /DeviceWidth() )
End

Function ScaleY:Float()
	If DesignScaleY = 0 Then Error "No scaling set, use SetDesignScale( float, float ) in OnCreate"
	
	Return( DesignScaleY /DeviceHeight() )
End
</pre><br><br>To use the scaling system just call SetDesignScale( width, height ) where width and height are the size of what your original graphics were scaled at so if you design a game thats 1024 by 768 it would be SetDesignScale( 1024, 768 ). After that you can use PixelScaleX and PixelScaleY in your systems like this:<br><pre class=code>
DrawImage( MyImage, X *PixelScaleX, Y *PixelScaleY, 0, 1.0 *PixelScaleX, 1.0 *PixelScaleY, frame )
</pre><br>This would then scale the position and size of the graphics on screen so no matter the resolution your game would still fit the screen.<br><br>With the code above I have also done a font module:<br><pre class=code>
'
'	EdzUpFontSystem.monkey - Copyright ©EdzUp
'	Programmed by Ed 'EdzUp' Upton
'
Strict

Import mojo
Import EdzUpFunctions

Class EdzUpFontClass
	'for font system
	Field FontString:String = " !`#$% '()*+,-./0123456789:;&lt;=&gt;?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]^_"	
	Field FontImage:Image					'this is the animimage that will contain the font
	Field FontWidth:Int =0
	Field FontHeight:Int

	Method New()
		FontString = " !`#$% '()*+,-./0123456789:;&lt;=&gt;?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]^_"
		FontImage = Null
		FontWidth = 0
		FontHeight =0
	End

	Method SetFont:Int( characterlist:String )
		'this will change the font for that particular system
		FontString = characterlist
	
		Return( 0 )
	End

	Method LoadFont:Int( filename:String, width:Int, height:Int, count:Int )
		'this loads in the image and then uses it to render the font required
		FontImage = LoadImage( filename, width, height, count )
		FontWidth = width
		FontHeight = height
	
		Return( 0 )
	End

	Method ChangeFontSize:Int( newWidth:Int, newHeight:Int )
		FontWidth = newWidth
		FontHeight = newHeight
	
		Return( 0 )
	End 

	Method DrawText:Int( X:Int, Y:Int, Text:String, CenterX:Bool =False, CenterY:Bool =False, XScale:Float =1.0, YScale:Float =1.0 )
		Local CP:Int
		Local Char:Int
		Local Finder:String
		Local EDTX:Int = X
		Local EDTY:Int = Y
	
		If FontWidth = 0 Then Error "No font loaded please use EdzUpLoadFont first"
	
		If CenterX = True
			EDTX -= ( ( ( Text.Length() *( FontWidth *XScale ) ) /2 ) *PixelScaleX )
		Endif
	
		If CenterY = True Then EDTY -= ( ( FontHeight *YScale ) /2 )*PixelScaleY
	
		For CP=0 To Text.Length()
			Finder = String.FromChar( Text[ CP ] )
			Char = FontString.Find( Finder, 0 )
			If Char&gt;-1
				DrawImage( FontImage, EDTX, EDTY, 0, XScale *PixelScaleX, YScale *PixelScaleY, Char )
			Endif
			
			EDTX += ( ( FontWidth *XScale ) *PixelScaleX )
		Next
	
		Return( 0 )
	End
End
</pre><br>To use this simply have a Global MyFont:EdzUpFontClass = New EdzUpFontClass in your variable declarations and then load the font in OnCreate like this:<br><pre class=code>
GameFont.LoadFont( "font_16.png", 16, 16, 64 )
GameFont.SetFont( " !`#$% '()*+,-./0123456789:;&lt;=&gt;?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]^_" )
GameFont.ChangeFontSize( 10, 16 )
</pre><br>LoadFont just simply loads the font into the class for use, SetFont will tell the class what characters are in the font and how they are arranged this can be in any order and finally ChangeFontSize will adjust the font size to accomodate the font. Changing the font size is useful when you have a font that when drawn looks like its spaced out a bit on some letters it just bunches them up more and makes it look better.<br><br>Another bonus of the font system is you can load more than one font at once into the monkey so you could have as many fonts as you want under different variable names and call them as you want. To render the font you can use the built in function like this:<br><pre class=code>
GameFont.DrawText( X, Y, "MyText", CenterX, CenterY, XScale, YScale )
</pre><br><br>Hope it helps someone :)<br><br>UPDATE:Added scaling to the font system <br><br></td></tr></table><br>
<a name="2050497"></a>

<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
