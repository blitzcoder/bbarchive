<!DOCTYPE html><html lang="en" ><head ><title >Scrolling Tile Map with Simple Dungeon Generation</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Scrolling Tile Map with Simple Dungeon Generation</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Scrolling Tile Map with Simple Dungeon Generation</a><br><br>
<a name="2055924"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Why0Why</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here is some code that scrolls a 2D map of tiles that is centered on a player sprite.  It is culled from a larger project that has multiple files so it isn't quite as neat as usual and there might be some extra variables laying around.  I didn't see anything like this and I felt like there should be an example up here for people just getting started with Monkey.<br><br>It requires 2 32x32 png tiles.  Here is a link to the source and the sprites or you can just pull the code and supply your own.  The tiles I have supplied are free for use from the David Gervais Angband tileset(which is awesome for placeholders.)<br><br><a href="http://sdrv.ms/ZvYJml" target="_blank">http://sdrv.ms/ZvYJml</a><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Import mojo

Global Player1:Player
Global ScreenWidth:Int = 640
Global ScreenHeight:Int = 480
Global Tiles:Int[][]

Class TileEngine Extends App
	
	Field CurrentLevel:Level
	
	Method OnCreate:Int()
		SetUpdateRate(60)
		Player1 = New Player()
		CurrentLevel = New Level(100, 100, CAVERNS)
		Return 0
	End

	Method OnUpdate:Int()
		If KeyHit(KEY_RIGHT)
			Player1.Move(East)
		End
		If KeyHit(KEY_LEFT)
			Player1.Move(West)
		End
		If KeyHit(KEY_UP)
			Player1.Move(North)
		End
		If KeyHit(KEY_DOWN)
			Player1.Move(South)
		End
		Return 0
	End

	Method OnRender:Int()
		Cls(0, 0, 0)
		' Sends the players coordinates through so the level can be drawn cenetered on the player
		CurrentLevel.Draw(Player1.x, Player1.y)
		Player1.Draw()
		Return 0
	End
End


Function Main:Int()

	New TileEngine()
	Return 0
	
End

' Cardinal Directions
	Const North:Int=1
	Const NorthEast:Int=2
	Const East:Int=3
	Const SouthEast:Int=4
	Const South:Int=5
	Const SouthWest:Int=6
	Const West:Int=7
	Const NorthWest:Int = 8

' Tile Types	
	Const IMPASSABLE:Int = 0
	Const SOLID:Int = 1
	Const FLOOR:Int = 2
	Const CORRIDORFLOOR:Int = 3
	Const WALL:Int = 4
	Const DOOR:Int = 5
	Const SECRETDOOR:Int = 6
	Const GOLD:Int = 7
	
	
' Dungeon Types
	Const ROOMS:Int = 1
	Const RANDOMROOMS:Int = 2
	Const CAVERNS:Int = 3
	
' Globals

Class Level
	' Size of the level in tiles
	Field LevelWidth:Int
	Field LevelHeight:Int
	
	' Number of pixels
	Field TileWidth:Int = 32
	Field TileHeight:Int = 32
	
	'If you want to add space to the top or side of hte level
	Field xPad:Int = 0
	Field yPad:Int = 0
	
	'If you want to add a fixed width around the whole screen
	Field Border:Int = 0
	'Images for the tiles
	Field SolidPic:Image
	Field FloorPic:Image
	Field WallPic:Image
	
	' I plan to generate multiple dungeon types in my project so many variables here are geared towrds that.  If something isn't used, that is why
	Method New(Width:Int, Height:Int, DungeonType:Int)
		LevelWidth = Width
		LevelHeight = Height
		Tiles = AllocateArray(Width, Height)
		
		' Seed the whole map with solid tiles
		For Local i:Int = 0 To LevelWidth - 1
			For Local j:Int = 0 To LevelHeight - 1
				Tiles[i][j] = SOLID
			Next
		Next
		
		Select DungeonType
			Case CAVERNS
				GenerateCaverns
		End Select
		
		
	End

	Method Draw:Void(xPos:Int, yPos:Int, Centered:Bool = True)
		Local NumXTiles:Int
		Local NumYTiles:Int
		Local StartX:Int
		Local StartY:Int
		Local EndX:Int
		Local EndY:Int
		Local ScreenX:Int = 0
		Local ScreenY:Int = 0
		
		NumXTiles = ScreenWidth / (TileWidth + xPad + Border * 2)
		NumYTiles = ScreenHeight / (TileHeight + yPad + Border * 2)
			StartX = xPos - (NumXTiles / 2)
		StartY = yPos - (NumYTiles / 2)
		If StartX &lt; 0 Then StartX = 0
		If StartY &lt; 0 Then StartY = 0
		EndX = xPos + (NumXTiles / 2)
		EndY = yPos + (NumYTiles / 2)
		If EndX &gt; LevelWidth - 1 Then EndX = LevelWidth - 1
		If EndY &gt; LevelHeight - 1 Then EndY = LevelHeight - 1
		
		For Local X:Int = StartX To EndX
			For Local Y:Int = StartY To EndY

				Select Tiles[X][Y]
					Case FLOOR
						DrawImage(FloorPic, ScreenX * TileWidth, ScreenY * TileHeight)
				End
				ScreenY += 1
			Next
			ScreenY = 0
			ScreenX += 1
		Next
		ScreenX = 0
		ScreenY = 0
	
	End
	
	'This level generates a very simple cavern by using cellular automata in its most basic form
	Method GenerateCaverns:Void()
		Local CellCount:Int = 0
		FloorPic = LoadImage("floor.png")

		For Local x:Int = 3 To LevelWidth - 3
			For Local y:Int = 3 To LevelHeight - 3
				If Rnd(1, 11) &lt; 6 Then Tiles[x][y] = FLOOR
			Next
		Next
		
		For Local x:Int = 3 To LevelWidth - 3
			For Local y:Int = 3 To LevelHeight - 3
				If Tiles[x - 1][y - 1] = FLOOR Then CellCount += 1
				If Tiles[x - 1][y + 1] = FLOOR Then CellCount += 1
				If Tiles[x + 1][y - 1] = FLOOR Then CellCount += 1
				If Tiles[x + 1][y + 1] = FLOOR Then CellCount += 1
				If Tiles[x][y - 1] = FLOOR Then CellCount += 1
				If Tiles[x][y + 1] = FLOOR Then CellCount += 1
				If Tiles[x - 1][y] = FLOOR Then CellCount += 1
				If Tiles[x + 1][y] = FLOOR Then CellCount += 1
				If CellCount &gt; 4 Then Tiles[x][y] = FLOOR
				CellCount = 0
			Next
		Next
	
	End
	
	End


Class Character
	Field x:Int = 10
	Field y:Int = 7
	Field Gold:Int = 0
	Field Picture:Image
	Field PlayerTileWidth:Int = 32
	Field PlayerTileHeight:Int = 32
	
	Field Race:String
	Field Profession:String	
	
	Method Move:Int(direction:Int)
		Select direction
			Case East
				If LegalMove(x + 1, y) Then Self.x += 1
			Case West
				If LegalMove(x - 1, y) Then Self.x -= 1
			Case North
				If LegalMove(x, y - 1) Then Self.y -= 1
			Case South
				If LegalMove(x, y + 1) Then Self.y += 1
		End Select
		If x &lt; 0 Then x = 0
		If y &lt; 0 Then y = 0
		If x &gt; 99 Then x = 99
		If y &gt; 99 Then y = 99
		
		Return 0
	End
	
	'This method draws the player and keeps him in the center of the screen unless you are at the edge of the map
	Method Draw:Void()
		Local DrawX:Int
		Local DrawY:Int
		
		DrawX = ScreenWidth / 2
		DrawY = ScreenHeight / 2
		
		If x * PlayerTileWidth &lt; DrawX Then
			DrawX = x * PlayerTileWidth
		EndIf
		If y * PlayerTileHeight &lt; DrawY Then
			DrawY = y * PlayerTileHeight
		EndIf
		DrawImage(Picture, DrawX, DrawY, 7)
	
	End
	
	Method LegalMove:Bool(x:Int, y:Int)
		If Tiles[x][y] = SOLID or Tiles[x][y] = WALL
			Return False
		Else
			Return True
		EndIf
	End
	
End

Class Player Extends Character
		
	Method New()
		Picture = LoadImage("player.png", 32, 32, 12)
	End
End

' Muddy Shoes 2D Array Code
Function AllocateArray:Int[][] (i:Int, j:Int)
    Local arr:Int[][] = New Int[i][]
	Local ind:Int
    For ind = 0 Until i
        arr[ind] = New Int[j]
    Next
    Return arr		
End
</textarea> <br><br></td></tr></table><br>
<a name="2055922"></a>

<a name="2055923"></a>

<a name="2055902"></a>

<a name="2056068"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sammy</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> <a href="http://sdrv.ms/ZvYJml" target="_blank">http://sdrv.ms/ZvYJml</a> produces an error for me. <br><br></td></tr></table><br>
<a name="2056097"></a>

<a name="2056098"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Why0Why</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I just tried it on both Chrome and Explorer(not signed in) and it works fine.  Right now it is a link from Skydrive.  Once I get my new website up I will move the link to my dedicated server.  Would be interested to hear if anyone else is having issues. <br><br></td></tr></table><br>
<a name="2056103"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Works here,<br><br>seems like a very open-plan dungeon, <br><br>The evil wizard must have an open-plan kitchen at his house <br><br></td></tr></table><br>
<a name="2056131"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Why0Why</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> It is just a cavern.  I stripped out the other dungeon generation I had since it wasn't quite complete.  I just put this up for the tile scrolling. <br><br></td></tr></table><br>
<a name="2056132"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> ohh ok its nice for a cavern <br><br></td></tr></table><br>
<a name="2064017"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DeltaWolf7</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>Great example, I found it to be great for learning Monkey.<br>One question though (from a noob). How would you add an object like a chest or something, without it moving with the player? <br><br></td></tr></table><br>
<a name="2064056"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Why0Why</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Glad you found it helpful.  There are different ways.  One way would be make an item class with a list field.  Then you can iterate through the list of items in the draw routine for the item class.  In the primary OnRender loop call a draw routine for items between the currentlevel.draw and the player.draw.  When you call items.draw and iterate through the list it will draw on top of the map.<br><br>You can also add a field to level for items.  Have an list or array of items(so that you can stack more than one item per map square) and then during the level draw routine check to see if there are any items on that square when it is going through and drawing the map.  If so, draw them there. <br><br></td></tr></table><br>
<a name="2064054"></a>

<a name="2064135"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gerry Quinn</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> It has always seemed pragmatic to me to make each tile have a reference to any item or monster on it, as well as having items or monsters available elsewhere in a list with their locations.  It does mean information exists in two places, but not having it quickly accessible in both ways is a pain.<br><br>To avoid problems, all movement of items or creatures must pass through a small set of functions/methods that update both data structures at once. <br><br></td></tr></table><br>
<a name="2064259"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DeltaWolf7</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>Finally got some time to have a play. I have trimmed out so of your code to simplify it and make it easier to learn from.<br><br><pre class=code>
Strict

Import mojo

Global Player1:Player
Global ScreenWidth:Int = 640
Global ScreenHeight:Int = 480
Global Tiles:Int[][]

Class TileEngine Extends App
	
	Field CurrentLevel:Level
	
	Method OnCreate:Int()
		SetUpdateRate(60)
		Player1 = New Player()
		CurrentLevel = New Level(20, 20)
		Return 0
	End

	Method OnUpdate:Int()
		If KeyHit(KEY_RIGHT)
			Player1.Move(East)
		End
		If KeyHit(KEY_LEFT)
			Player1.Move(West)
		End
		If KeyHit(KEY_UP)
			Player1.Move(North)
		End
		If KeyHit(KEY_DOWN)
			Player1.Move(South)
		End
		Return 0
	End

	Method OnRender:Int()
		Cls(0, 0, 0)
		' Sends the players coordinates through so the level can be drawn cenetered on the player
		CurrentLevel.Draw(Player1.x, Player1.y)
		Player1.Draw()
		Return 0
	End
End


Function Main:Int()

	New TileEngine()
	Return 0
	
End

' Cardinal Directions
	Const North:Int=1
	Const East:Int=2
	Const South:Int=3
	Const West:Int=4

' Tile Types	
	Const IMPASSABLE:Int = 0
	Const WALL:Int = 1
	Const GRASS:Int = 2
	Const DIRT:Int = 3

	
Class Level
	' Size of the level in tiles
	Field LevelWidth:Int
	Field LevelHeight:Int
	
	' Number of pixels
	Field TileWidth:Int = 32
	Field TileHeight:Int = 32
	
	Field FloorPic:Image
	
	Method New(Width:Int, Height:Int)
		LevelWidth = Width
		LevelHeight = Height
		Tiles = AllocateArray(Width, Height)
		
		' Seed the whole map with solid tiles
		For Local i:Int = 0 To LevelWidth - 1
			For Local j:Int = 0 To LevelHeight - 1
				Tiles[i][j] = WALL
			Next
		Next
		
		Generate
		
	End

	Method Draw:Void(xPos:Int, yPos:Int)
		Local NumXTiles:Int
		Local NumYTiles:Int
		Local StartX:Int
		Local StartY:Int
		Local EndX:Int
		Local EndY:Int
		Local ScreenX:Int = 0
		Local ScreenY:Int = 0
		
		NumXTiles = ScreenWidth / TileWidth
		NumYTiles = ScreenHeight / TileHeight
		
		StartX = xPos - (NumXTiles / 2)
		StartY = yPos - (NumYTiles / 2)
		
		If StartX &lt; 0 Then StartX = 0
		If StartY &lt; 0 Then StartY = 0
		
		EndX = xPos + (NumXTiles / 2)
		EndY = yPos + (NumYTiles / 2)
		
		If EndX &gt; LevelWidth - 1 Then EndX = LevelWidth - 1
		If EndY &gt; LevelHeight - 1 Then EndY = LevelHeight - 1
		
		For Local X:Int = StartX To EndX
			For Local Y:Int = StartY To EndY
				If X &gt; LevelWidth - 1 Then X = LevelWidth - 1
				If Y &gt; LevelHeight - 1 Then Y = LevelHeight - 1
				Select Tiles[X][Y]
					Case GRASS
						If xPos = X And yPos = Y
							Tiles[X][Y] = DIRT
						Endif
						DrawImage(FloorPic, ScreenX * TileWidth, ScreenY * TileHeight, 1)
					Case WALL
						DrawImage(FloorPic, ScreenX * TileWidth, ScreenY * TileHeight, 3)
					Case IMPASSABLE
						DrawImage(FloorPic, ScreenX * TileWidth, ScreenY * TileHeight, 4)
					Case DIRT
						DrawImage(FloorPic, ScreenX * TileWidth, ScreenY * TileHeight, 2)
				End
				ScreenY += 1
			Next
			ScreenY = 0
			ScreenX += 1
		Next
		ScreenX = 0
		ScreenY = 0
	
	End
	

	Method Generate:Void()
		Local CellCount:Int = 0
		FloorPic = LoadImage("tiles.png", 32, 32, 5)

		For Local x:Int = 3 To LevelWidth - 3
			For Local y:Int = 3 To LevelHeight - 3
				Tiles[x][y] = GRASS
			Next
		Next	
	End	
	
End


Class Character
	Field x:Int = 10
	Field y:Int = 7
	Field Picture:Image
	Field PlayerTileWidth:Int = 32
	Field PlayerTileHeight:Int = 32
	
	Method Move:Int(direction:Int)
		Select direction
			Case East
				If LegalMove(x + 1, y) Then Self.x += 1
			Case West
				If LegalMove(x - 1, y) Then Self.x -= 1
			Case North
				If LegalMove(x, y - 1) Then Self.y -= 1
			Case South
				If LegalMove(x, y + 1) Then Self.y += 1
		End Select
		
		If x &lt; 0 Then x = 0
		If y &lt; 0 Then y = 0
		If x &gt; 99 Then x = 99
		If y &gt; 99 Then y = 99
		
		Return 0
	End
	
	'This method draws the player and keeps him in the center of the screen unless you are at the edge of the map
	Method Draw:Void()
		Local DrawX:Int
		Local DrawY:Int
		
		DrawX = ScreenWidth / 2
		DrawY = ScreenHeight / 2
		
		If x * PlayerTileWidth &lt; DrawX Then
			DrawX = x * PlayerTileWidth
		Endif
		If y * PlayerTileHeight &lt; DrawY Then
			DrawY = y * PlayerTileHeight
		Endif
		DrawImage(Picture, DrawX, DrawY, 1)
	
	End
	
	Method LegalMove:Bool(x:Int, y:Int)
		If Tiles[x][y] = IMPASSABLE or Tiles[x][y] = WALL
			Return False
		Else
			Return True
		EndIf
	End
	
End

Class Player Extends Character
		
	Method New()
		Picture = LoadImage("hero.png", 32, 32, 3)
	End
End


' Muddy Shoes 2D Array Code
Function AllocateArray:Int[][] (i:Int, j:Int)
    Local arr:Int[][] = New Int[i][]
	Local ind:Int
    For ind = 0 Until i
        arr[ind] = New Int[j]
    Next
    Return arr		
End
</pre><br><br>The only problem I have so far it that the character overlaps the wall at the bottom, I think he's getting out of position or something. Any ideas what causing it?<br><br>Thanks <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
