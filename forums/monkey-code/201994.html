<!DOCTYPE html><html lang="en" ><head ><title >Triangulate a polygon</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Triangulate a polygon</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Triangulate a polygon</a><br><br>
<a name="2019080"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JIM</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello!<br><br>Here's a piece of code that handles turning a concave polygon into triangles in order for it to be drawn correctly.<br><br>You may not see the difference in the test app in HTML5 (I think it might have something similar already embedded) but it's definitely visible in GLFW.<br><br>IMPORTANT NOTE: It only works with simple polygons (no holes and no crossing of edges).<br><br>You may want to replace this with a simple DrawPoly on HTML5 to save the computation (also due to antialiasing some seams are visible).<br><br>Enjoy!<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import mojo

'-------------------------------------------- TEST APP

Global poly:Float[] = [110.0, 0.0, 110.0, 160.0, 0.0, 160.0, 0.0, 150.0, 100.0, 150.0, 100.0, 10.0, 0.0, 10.0, 0.0, 0.0]
Global complex:Float[][]

Class TestApp Extends App
	Method OnCreate()
		SetUpdateRate(30)
		
		complex = TriangulatePoly(poly)
	End
	
	Method OnRender()
		Cls
		
		'bad one
		PushMatrix()
		Translate(100, 100)
		SetColor(255, 0, 0)
		DrawPoly(poly)
		PopMatrix()
		
		'good one
		PushMatrix()
		Translate(300, 100)
		SetColor(0, 255, 0)
		DrawPolyEx(complex)
		PopMatrix()
	End
End

Function Main()
	New TestApp
End

'----------------------------------------- FUNCTIONS

Function DrawPolyEx(poly:Float[][])
	For Local i:Int	= 0 Until poly.Length()
		DrawPoly(poly[i])
	Next
End

Function TriangulatePoly:Float[][](ply:Float[])
	Local tmpPoly:Float[] = ply
	
	Local ComplexPoly:Float[][]
	Local cPolyCount:Int = 0
	
	While tmpPoly.Length() &gt; 6
		For Local i:Int = 0 Until (tmpPoly.Length() / 2)
			If (IsPolygonEar(tmpPoly, i))
				'clip the ear and exit
				Local j:Int = (i - 1 + (tmpPoly.Length() / 2)) Mod (tmpPoly.Length() / 2)
				Local k:Int = (i + 1) Mod (tmpPoly.Length() / 2)
				Local tri:Float[] = [tmpPoly[j*2], tmpPoly[j*2+1], tmpPoly[i*2], tmpPoly[i*2+1], tmpPoly[k*2], tmpPoly[k*2+1]]
				
				cPolyCount += 1
				ComplexPoly = ComplexPoly.Resize(cPolyCount)
				ComplexPoly[cPolyCount - 1] = tri
				
				tmpPoly = ClipEar(tmpPoly, i)
			EndIf
		Next
	Wend
	
	cPolyCount += 1
	ComplexPoly = ComplexPoly.Resize(cPolyCount)
	ComplexPoly[cPolyCount - 1] = tmpPoly

	Return ComplexPoly
End

Function PolygonArea:Float(polyPoints:Float[])

	Local accum:Float
	Local  j:Int = polyPoints.Length()-2

	For Local i:Int =0 Until polyPoints.Length() Step 2
		accum += polyPoints[j] * polyPoints[i+1] - polyPoints[i] * polyPoints[j+1]
    		j=i 
	Next

	Return accum / 2.0
End Function

Function IsConvexVertex:Bool(poly:Float[], vert:Int)
	Local i:Int = (vert - 1 + (poly.Length() / 2)) Mod (poly.Length() / 2)
	Local j:Int = (vert + 1) Mod (poly.Length() / 2)
	
	Local tri:Float[] = [poly[i*2], poly[i*2+1], poly[vert*2], poly[vert*2+1], poly[j*2], poly[j*2+1]]
	
	If (PolygonArea(tri) &lt; 0)
		Return True
	Else
		Return False
	EndIf
End

Function PointInsideTriangle:Bool(px:Float, py:Float, tri:Float[])
	If (tri.Length() &lt;&gt; 6)
		Return False
	EndIf
	
	'C - A
	Local v0x:Float = tri[4] - tri[0]
	Local v0y:Float = tri[5] - tri[1]
	
	'B - A
	Local v1x:Float = tri[2] - tri[0]
	Local v1y:Float = tri[3] - tri[1]
	
	'P - A
	Local v2x:Float = px - tri[0]
	Local v2y:Float = py - tri[1]
	
	Local dot00:Float = v0x * v0x + v0y * v0y
	Local dot01:Float = v0x * v1x + v0y * v1y
	Local dot02:Float = v0x * v2x + v0y * v2y
	Local dot11:Float = v1x * v1x + v1y * v1y
	Local dot12:Float = v1x * v2x + v1y * v2y
	
	Local invDenom:Float = 1.0 / (dot00 * dot11 - dot01 * dot01)
	Local u:Float = (dot11 * dot02 - dot01 * dot12) * invDenom
	Local v:Float = (dot00 * dot12 - dot01 * dot02) * invDenom
	
	Return ((u &gt;= 0) And (v &gt;= 0) And ((u + v) &lt; 1))
End

Function IsPolygonEar:Bool(poly:Float[], vert:Int)
	Local bEar:Bool = True
	
	If (Not IsConvexVertex(poly, vert))
		Local i:Int = vert
		Local j:Int = (vert - 1 + (poly.Length() / 2)) Mod (poly.Length() / 2)
		Local k:Int = (vert + 1) Mod (poly.Length() / 2)
		
		For Local idx:Int = 0 Until (poly.Length() / 2)
			If ((idx &lt;&gt; i) And (idx &lt;&gt; j) And (idx &lt;&gt; k))
				Local tri:Float[] = [poly[j*2], poly[j*2+1], poly[i*2], poly[i*2+1], poly[k*2], poly[k*2+1]]
				If (PointInsideTriangle(poly[idx*2], poly[idx*2+1], tri))
					Return False
				EndIf
			EndIf
		Next
	Else
		Return False
	EndIf
	
	Return True
End

Function ClipEar:Float[](ply:Float[], vert:Int)
	Local newply:Float[] = New Float[ply.Length() - 2]
	Local x:Int = 0
	For Local i:Int = 0 Until (ply.Length() / 2)
		If (i &lt;&gt; vert)
			newply[x*2] = ply[i*2]
			newply[x*2+1] = ply[i*2+1]
			x = x + 1
		EndIf
	Next
	
	Return newply
End
</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
