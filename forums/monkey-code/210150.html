<!DOCTYPE html><html lang="en" ><head ><title >Bresenham's Line Algorithm (Function)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Bresenham's Line Algorithm (Function)</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Bresenham's Line Algorithm (Function)</a><br><br>
<a name="2111408"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pakz</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I found a site that had lots of code examples of the bresenham line algorithm. I translated one to Monkey.<br><br>You can use this to draw lines in arrays or move ai in a straight line. Probably lots more what a line function is good for.<br><br><pre class=code>
Import mojo

Class MyApp Extends App
    
    Method OnCreate()
        SetUpdateRate(60)
        Local date := GetDate()
        Seed = date[5]        
    End Method
    Method OnUpdate()
    End Method    
    Method OnRender()
        Cls 0,0,0
        SetColor 255,255,255
        DrawText "Monkey-X Bresenham's line algorithm - function.",10,10
		For Local i=0 Until 32
			bline Rnd(0,640),Rnd(0,480),Rnd(0,640),Rnd(0,480)
		Next
    End Method
    
End Class

Function bline:Void(x1:Int,y1:Int,x2:Int,y2:Int)
	  Local dx:Int, dy:Int, sx:Int, sy:Int, e:Int
      dx = Abs(x2 - x1)
      sx = -1
      If x1 &lt; x2 Then sx = 1      
      dy = Abs(y2 - y1)
      sy = -1
      If y1 &lt; y2 Then sy = 1
      If dx &lt; dy Then 
      	e = dx / 2 
      Else 
      	e = dy / 2      	
      End If
      Local exitloop:Bool=False
      While exitloop = False
        SetColor 255,255,255
        DrawPoint x1,y1
        If x1 = x2 
        	If y1 = y2
        		exitloop = True
        	End If
        End If
        If dx &gt; dy Then
        	x1 += sx ; e -= dy 
          	If e &lt; 0 Then e += dx ; y1 += sy
        Else
        	y1 += sy ; e -= dx 
        	If e &lt; 0 Then e += dy ; x1 += sx
        Endif
      Wend
End Function

Function Main()
    New MyApp
End Function

</pre><br><br>Here a example where I use the line algorithm function to draw in a pixel array that can be turned into a image. It shows astroids on the screen.<br><br><img src="http://i58.tinypic.com/t66t61.png"><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import mojo

Global numasteroids:Int=10
Global awidth:Int=64
Global aheight:Int=64

Class asteroid
	Field im:Image
	Field pixels:Int[awidth*aheight]
	Field x:Int,y:Int
	Field angle:Float=0
	Field rotspeed:Float=Rnd(-3.3,3.3)
	Method New(x:Int,y:Int)
		im = CreateImage(awidth,aheight,1,Image.MidHandle)
		Self.x = x
		Self.y = y
		Local myan:Int=0
		Local x1:Int,y1:Int
		Local x2:Int,y2:Int		
		Local sx:Int,sy:Int
		x1 = awidth/2+((Cos(myan)*Rnd(16,32)))
		y1 = aheight/2+((Sin(myan)*Rnd(16,32)))		
		sx=x1
		sy=y1
		For Local i=0 Until 10
			myan+=350/10
			x2 = awidth/2+((Cos(myan)*Rnd(16,32)))
			y2 = aheight/2+((Sin(myan)*Rnd(16,32)))
			bline(x1,y1,x2,y2)
			x1 = x2
			y1 = y2
		Next
		bline x1,y1,sx,sy
		im.WritePixels(pixels, 0, 0, awidth, aheight, 0)
	End Method
	Method update()
		angle+=rotspeed
		If angle &gt; 359 Then angle = 0
		If angle &lt; 0 Then angle = 359
	End Method
	Method draw()
		DrawImage im,x,y,angle,1.0,1.0,0
	End Method
	Method bline:Void(x1:Int,y1:Int,x2:Int,y2:Int)
		Local dx:Int, dy:Int, sx:Int, sy:Int, e:Int
    	dx = Abs(x2 - x1)
      	sx = -1
      	If x1 &lt; x2 Then sx = 1      
      	dy = Abs(y2 - y1)
      	sy = -1
      	If y1 &lt; y2 Then sy = 1
      	If dx &lt; dy Then 
      		e = dx / 2 
      	Else 
      		e = dy / 2      	
      	End If
      	Local exitloop:Bool=False
      	While exitloop = False
        	Local pc:Int = y1*awidth+x1
        	If pc&gt;=0 And pc &lt;awidth*aheight
        		pixels[pc] = argb(255,255,255,255)
        	End If
        	If x1 = x2 
        		If y1 = y2
        			exitloop = True
        		End If
        	End If
        	If dx &gt; dy Then
        		x1 += sx ; e -= dy 
          		If e &lt; 0 Then e += dx ; y1 += sy
        	Else
        		y1 += sy ; e -= dx 
        		If e &lt; 0 Then e += dy ; x1 += sx
        	Endif
      	Wend
	End Method
    Function argb:Int(r:Int, g:Int, b:Int ,alpha:Int=255)
        Return (alpha Shl 24) | (r Shl 16) | (g Shl 8) | b          
    End Function    	
End Class

Global myasteroids:List&lt;asteroid&gt; = New List&lt;asteroid&gt;

Class MyApp Extends App
    
    Method OnCreate()
        SetUpdateRate(60)
        Local date := GetDate()
        Seed = date[5]        
        For Local y=64 Until DeviceHeight Step 128
        For Local x=64 Until DeviceWidth Step 128
        myasteroids.AddLast(New asteroid(x,y))
        Next
        Next
    End Method
    Method OnUpdate()
    	For Local i:=Eachin myasteroids
    		i.update
    	Next
    End Method    
    Method OnRender()
        Cls 0,0,0
        For Local i:=Eachin myasteroids
        	i.draw
        Next
        SetColor 255,255,255
        DrawText "Monkey-X Class Image Asteroids example.",10,10
    End Method
    
End Class


Function Main()
    New MyApp
End Function

</textarea> <br><br></td></tr></table><br>
<a name="2111407"></a>

<a name="2111470"></a>

<a name="2111471"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GarBenjamin</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Excellent work and thanks for sharing! If you get bored a very good update would be to make a version that uses linear interpolation basically just a LERP function.<br><br>So your Function signature would look something like this:<br><pre class=code>PointToPointLerp:Point(x1:Int,y1:Int,x2:Int,y2:Int,step:Float)</pre><br><br>Point would be a class with x1 and y1 fields.<br>Step would be a value from 0.01 to 100 where 0.01 would return x1, y1, 1.0 returns x2, y2, 0.5 would return a point halfway between (x1,y1) and (x2,y2) and so forth.<br><br>The step could also be an integer from 1 to 100 with 1 returning x1, y1, 100 returning x2, y2, 50  returning a point halfway between (x1,y1) and (x2,y2) . Whatever way is easiest for you to use.<br><br>Another option would be to create a function that returns the full array of 100 movement points, something like:<br><pre class=code>GetPointToPointLerpArray:Point[](x1:Int,y1:Int,x2:Int,y2:Int,step:Float)</pre><br><br>This kind of thing is highly useful because then objects can track their oldpos(x1,y1), targetpos(x2,y2) and current step on the path allowing step to be incremented gradually each frame meaning it can be used during Update() to move to the next point on the path.<br><br>You probably already have a ton of ideas for stuff you plan to make. Just throwing this out in case you ever run out of ideas. ;) <br><br></td></tr></table><br>
<a name="2111430"></a>

<a name="2111429"></a>

<a name="2111428"></a>

<a name="2111427"></a>

<a name="2111426"></a>

<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
