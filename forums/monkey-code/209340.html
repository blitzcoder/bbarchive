<!DOCTYPE html><html lang="en" ><head ><title >Platformer with player and roaming/jumping ai</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Platformer with player and roaming/jumping ai</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Platformer with player and roaming/jumping ai</a><br><br>
<a name="2097318"></a>

<a name="2097300"></a>

<a name="2125435"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pakz</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I made a platformer with ai that can navigate the map. The ai can jump on other platforms and can get around the entire map.<br>I gave the ai states like Move left and move right and jump right up, fall, jump ect.. The ai checks if it can jump on a platform and then based on a random value jumps there.<br>There is also a player that can be controlled. No shooting though.<br>I got it done without to much problems. Mostly becourse the ai code style I used I typed in more then once with my civilization game testbeds last year.<br><br>The code is below 400 lines. It is also on my blog. (Almost 40 monkey example code posts on it already)<br><br>Here a screenshot :<br><img src="http://i57.tinypic.com/mtv52o.jpg"><br><br>Here a video :<br><iframe width="560" height="345" src="http://www.youtube.com/embed/DMbs7ZTtFB0" frameborder="0" allowfullscreen></iframe><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Import mojo

Class ai
    Field x:Float
    Field y:Float
    Field state:String
    Field laststate:String
    Field incy:Float
    Method New()
        Local exitloop = False
        While exitloop = False
            Local x1 = Rnd(0,mapwidth)
            Local y1 = Rnd(0,mapheight-1)
            If map[y1][x1] = 0
            If map[y1+1][x1] = 1
                Local gothrough = True
                For Local i:ai = Eachin ailist
                    If i.x = x1 And i.y = y1 Then gothrough = False
                Next
                If gothrough = True
                    x = x1*tilewidth
                    y = y1*tileheight
                    exitloop = True
                End If
            End If
            End If
        Wend
        state = "Wait"
    End Method
End Class

Const numai:Int=10
Const aiwidth:Int = 32
Const aiheight:Int = 32
Const playerwidth:Int=32
Const playerheight:Int=32
Global playerx:Float = 640/2-16
Global playery:Float = 480/2
Global pincy:Float
Global playerjump:Bool=False
Const tilewidth = 32
Const tileheight = 32
Const mapwidth:Int=20
Const mapheight:Int=10
Global map:Int[][] = [      [1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1],
                            [1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1],
                            [1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1],
                            [1,0,0,0,0,0,0,0,1,1,1,1,1,0,0,0,0,0,0,1],
                            [1,0,0,0,0,0,0,0,1,0,0,0,1,0,0,0,0,0,0,1],
                            [1,0,0,0,0,0,1,1,1,0,0,0,1,1,0,0,0,0,0,1],
                            [1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1],
                            [1,1,1,1,0,0,0,0,0,0,0,0,0,0,0,0,1,1,1,1],
                            [1,1,1,1,0,0,0,0,0,0,0,0,0,0,0,0,1,1,1,1],
                            [1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1] ]

Global ailist:List&lt;ai&gt; = New List&lt;ai&gt;


Class MyGame Extends App
    Method OnCreate()
        SetUpdateRate(60)
        Seed = 2
        For Local i:Int = 0 Until numai
            ailist.AddLast(New ai)
        Next

    End
    Method OnUpdate()
        updateai
        ' Player left and right movement
        playermovement()        
        ' player gravity
        playergravity()
    End
    Method OnRender()
        Cls(0,0,0)
        SetColor(255,255,255)
        ' draw the map
        For Local y:Int = 0 Until mapheight
        For Local x:Int = 0 Until mapwidth
            If map[y][x] = 1 Then DrawRect(x*tilewidth,y*tileheight,tilewidth,tileheight)
        End
        End
        SetColor 255,0,0
        For Local i:ai = Eachin ailist
            DrawRect i.x,i.y,aiwidth,aiheight
        Next
        DrawText "Platformer with ai Example",10,10
        DrawText "Use cursor left/right and space bar to move player",10,25
        ' draw the player
        SetColor 255,255,0        
        DrawOval playerx,playery,playerwidth,playerheight
    End
End

Function updateai:Void()
    For Local i:ai = Eachin ailist
        Select i.state
            Case "Wait"
                If Rnd(0,100) &lt; 2
                    If Int(Rnd(0,2)) = 1
                        i.laststate="Wait"
                        i.state="Left"
                    Else
                        i.laststate="Wait"
                        i.state="Right"
                    End If
                End If
            Case "Left"
                If aitc(i.x,i.y,0,1) = False
                    i.laststate="Left"
                    i.state="Falling"
                    i.incy = 0
                End If
                If canjumpleftupfar(i.x,i.y) = True And Int(Rnd(0,18)) = 1
                    i.laststate="Left"
                    i.state="Jumpleftup"
                    i.incy=-4
                End If

                If aitc(i.x,i.y,-2,0) = False
                        i.x-=2
                Else
                    If canjumpleftup(i.x,i.y) = True And Int(Rnd(0,3)) = 1
                        i.laststate="Left"
                        i.state = "Jumpleftup"
                        i.incy = -4
                    Else 
                        i.laststate="Left"
                        i.state="Right"
                    End If
                End If
            Case "Right"
                If aitc(i.x,i.y,0,1) = False
                    i.laststate="Right"
                    i.state="Falling"
                    i.incy = 0
                End If
                If canjumprightupfar(i.x,i.y) = True And Int(Rnd(0,18)) = 1
                    i.laststate="Right"
                    i.state="Jumprightup"
                    i.incy=-4
                End If
                If aitc(i.x,i.y,2,0) = False
                    i.x+=2
                Else
                    If canjumprightup(i.x,i.y) = True And Int(Rnd(0,3)) = 1 Then
                        i.laststate="Right"
                        i.state = "Jumprightup"
                        i.incy = -4
                    Else
                    i.laststate="Right"
                    i.state="Left"
                    End If
                End If
            Case "Falling"
                If i.incy &lt;= 3 Then i.incy +=.1
                For Local ii:Int = 0 Until i.incy
                    i.y += 1
                    If aitc(i.x,i.y,0,1) = True
                        i.state = i.laststate
                        i.laststate = "Falling"                        
                        Exit
                    End
                End
            Case "Jumprightup"
                If aitc(i.x,i.y,2,0) = False
                    i.x+=2
                End If
                i.incy += .1
                For Local ii:Int = 0 Until Abs(i.incy)
                    i.y -= 1
                    If aitc(i.x,i.y,0,-1) = True Or i.incy &gt; 0
                        i.state="Fallrightdown"
                        Exit
                    End If        
                Next
            Case "Fallrightdown"
                If aitc(i.x,i.y,2,0) = False
                    i.x+=2
                End If
                i.incy+=.1
                For Local ii:Int = 0 Until i.incy
                    i.y += 1
                    If aitc(i.x,i.y,0,1) = True
                        i.state = i.laststate
                        i.laststate = "Fallrightdown"                        
                        Exit
                    End
                End
            Case "Jumpleftup"
                If aitc(i.x,i.y,-2,0) = False
                    i.x-=2
                End If
                i.incy += .1
                For Local ii:Int = 0 Until Abs(i.incy)
                    i.y -= 1
                    If aitc(i.x,i.y,0,-1) = True Or i.incy &gt; 0
                        i.state="Fallleftdown"
                        Exit
                    End If        
                Next
            Case "Fallleftdown"
                If aitc(i.x,i.y,-2,0) = False
                    i.x-=2
                End If
                i.incy+=.1
                For Local ii:Int = 0 Until i.incy
                    i.y += 1
                    If aitc(i.x,i.y,0,1) = True
                        i.state = i.laststate
                        i.laststate = "Fallleftdown"                        
                        Exit
                    End
                End
                
        End Select
    Next
End Function

Function canjumpleftupfar:Bool(x:Int,y:Int)
    Local tx=x/tilewidth
    Local ty=y/tileheight
    If tx - 3 &gt; 0
    If ty - 3 &gt; 0
    If map[ty-1][tx-2] = 1
    If map[ty-2][tx-2] = 0
        Return True
    End If
    End If
    End If
    End If
    Return False
End Function


Function canjumprightupfar:Bool(x:Int,y:Int)
    Local tx=x/tilewidth
    Local ty=y/tileheight
    If tx + 3 &lt; mapwidth
    If ty - 3 &gt; 0
    If map[ty-1][tx+2] = 1
    If map[ty-2][tx+2] = 0
        Return True
    End If
    End If
    End If
    End If
    Return False
End Function

Function canjumprightup:Bool(x:Int,y:Int)
    Local tx=x/tilewidth
    Local ty=y/tileheight
    If map[ty-2][tx+1] = 0 Then Return True
    Return False
End Function

Function canjumpleftup:Bool(x:Int,y:Int)
    Local tx=x/tilewidth
    Local ty=y/tileheight
    If map[ty-2][tx-1] = 0 Then Return True
    Return False
End Function


Function playermovement:Void()
       If KeyDown(KEY_RIGHT)
           For Local i=0 Until 2
            If playertc(1,0) = False
                   playerx+=1
            End If
           Next
       End If
       If KeyDown(KEY_LEFT)
           For Local i=0 Until 2    
               If playertc(-1,0) = False
                   playerx-=1
               End If
           Next
       End If
End Function

Function playergravity:Void()
        ' If the player is on the ground and the space bar is pressed
        If playerjump = False And playertc(0,1) = False
            playerjump = True
            pincy = 0
        End If
        If playerjump = False And KeyDown(KEY_SPACE) = True
            pincy = -4
            playerjump = True
        End
        'If the player is in the jump
        If playerjump = True
            pincy += 0.1
            'if the player is going up
            If pincy &lt;=0
                For Local i:Int = 0 Until Abs(pincy)                
                    playery -= 1
                    If playertc(0,-1) = True
                        pincy = 0
                        Exit
                    End If
                End
            End
            ' if the player if going down
            If pincy &gt; 0
                For Local i:Int = 0 Until pincy
                    playery += 1
                    'if the player touches the ground
                    If playertc(0,1) = True
                        playerjump = False                        
                        Exit
                    End
                End
            End
        End
End Function

' Ai with tile collision
Function aitc:Bool(x:Int,y:Int,offsetx:Int=0,offsety:Int=0)
    Local cx = (x + offsetx) / tilewidth
    Local cy = (y + offsety) / tileheight
    For Local y2=cy-1 Until cy+2
    For Local x2=cx-1 Until cx+2
        If x2&gt;=0 And x2&lt;mapwidth And y2&gt;=0 And y2&lt;mapheight
            If map[y2][x2] &gt; 0
                If rectsoverlap(x+offsetx,y+offsety,aiwidth,aiheight,x2*tilewidth,y2*tileheight,tilewidth,tileheight) = True
                    Return True
                End If
            End If
        End If
    Next
    Next
    Return False
End Function

Function playertc:Bool(x1:Int=0,y1:Int=0)
    Local cx = (playerx + x1) / tilewidth
    Local cy = (playery + y1) / tileheight
    For Local y2=cy-1 Until cy+2
    For Local x2=cx-1 Until cx+2
        If x2&gt;=0 And x2&lt;mapwidth And y2&gt;=0 And y2&lt;mapheight
            If map[y2][x2] &gt; 0
                If rectsoverlap(playerx+x1,playery+y1,playerwidth,playerheight,x2*tilewidth,y2*tileheight,tilewidth,tileheight) = True
                    Return True
                End If
            End If
        End If
    Next
    Next
    Return False
End Function


Function rectsoverlap:Bool(x1:Int, y1:Int, w1:Int, h1:Int, x2:Int, y2:Int, w2:Int, h2:Int)
    If x1 &gt;= (x2 + w2) Or (x1 + w1) &lt;= x2 Then Return False
    If y1 &gt;= (y2 + h2) Or (y1 + h1) &lt;= y2 Then Return False
    Return True
End

Function Main()
    New MyGame()
End
</textarea> <br><br></td></tr></table><br>
<a name="2097351"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Why0Why</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just wanted to say that I appreciate what you are doing and think that it is great for Monkey! <br><br></td></tr></table><br>
<a name="2097356"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice example! Thank you! <br><br></td></tr></table><br>
<a name="2097674"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skape</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for all of the code examples you've been posting! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
