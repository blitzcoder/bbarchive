<!DOCTYPE html><html lang="en" ><head ><title >EazyGadgets - Simple but functional GUI</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >EazyGadgets - Simple but functional GUI</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >EazyGadgets - Simple but functional GUI</a><br><br>
<a name="2089295"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Supertino</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey all<br><br>I just put together a simple GUI module that support 10 different gadget types.<br><br>You can try it out here <a href="http://moarlasers.com/ezgadgets/html5/ezgadgets.html" target="_blank">http://moarlasers.com/ezgadgets/html5/ezgadgets.html</a><br><br>You can also go to my google drive and grab the source used in this example to see how all the gadgets are put together. <a href="https://drive.google.com/folderview?id=0B1jBbicq8v_dbXBybzVDLVNJLUU&amp;usp=sharing" target="_blank">https://drive.google.com/folderview?id=0B1jBbicq8v_dbXBybzVDLVNJLUU&amp;usp=sharing</a><br><b><br>1. Canvases<br>2. List boxes<br>3. Buttons<br>4. Text boxes<br>5. Spinners<br>6. Combo boxes<br>7. Sliders<br>8. Labels<br>9. Check Boxes<br>10. Message Boxes<br></b><br>There are much better ones already available but this one does not require any setup, external files or frameworks, just import the .monkey file and that's it.<br><br>Basic but functional is how I would describe it good for internal tools and editors probably not good for public.<br><br><img src="http://moarlasers.com/ezgadgets/ezg.png"><br><br><b>Save this out and import into your project, or grab from my google (link above)</b><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'╔════════════════════════════════════════════════════════════════════════════════════════════════╗
'   Styles :    Visual stuff                               
'╚════════════════════════════════════════════════════════════════════════════════════════════════╝
Class ezStyles

	Function Box:Void(style:Int, x:Int, y:Int, width:Int, height:Int, state:Bool = False, r:Int = 0, g:Int = 0, b:Int = 0)
		Select style
			Case 0 ' button, slider, textbox
				SetColorLight
				DrawRect x, y, width, height
				SetColorTrim
				DrawRect x + 1, y + 1, width - 2, height - 2
				If state = True Then SetColorDark Else SetColorLight
				DrawRect x + 2, y + 2, width - 4, height - 4
				SetColor 255, 255, 255
			Case 1 'canvas
				SetColorLight
				DrawRect x, y, width, height
				SetColorTrim
				DrawRect x + 1, y + 1, width - 2, height - 2
				SetColor r, g, b
				DrawRect x + 2, y + 2, width - 4, height - 4
				SetColor 255, 255, 255
			Case 2 ' check box
				SetColorLight
				DrawRect x, y, width, height
				SetColorTrim
				DrawRect x + 1, y + 1, width - 2, height - 2
				SetColorLight
				DrawRect x + 2, y + 2, width - 4, height - 4
				SetColorTrim
				If state = True
					DrawLine x + 6, y + 14, x + 9, y + 17
					DrawLine x + 7, y + 14, x + 9, y + 16
					DrawLine x + 18, y + 8, x + 9, y + 17
					DrawLine x + 17, y + 8, x + 8, y + 17
				End If
			Case 3 ' listbox selector
				SetColorDark
				DrawRect x, y - 1, width, height + 2
				SetColor 255, 255, 255
			Case 4 ' listbox scroller
				SetColor 255, 255, 255
				DrawRect x - 2, y + 4, 1, height - 8
			Case 5 ' spinner
				SetColorLight
				DrawRect x, y, width, height
				SetColorTrim
				DrawRect x + 1, y + 1, width - 2, height - 2
				SetColorLight
				DrawRect x + 2, y + 2, width - 4, height - 4
				If state = True Then SetColorDark Else SetColorLight
				DrawRect x + 2, y + 2, width - 44, height - 4
				SetColor 255, 255, 255
				DrawRect x + width - 40, y + 4, 1, height - 8
			Case 6 ' spinner buttons
				If state = True Then SetColorDark Else SetColorLight
				DrawRect x + 2, y + 2, width - 4, height - 4
				SetColor 255, 255, 255
			Case 7 ' combo box
				SetColorLight
				DrawRect x, y, width, height
				SetColorTrim
				DrawRect x + 1, y + 1, width - 2, height - 2
				If state = True Then SetColorDark Else SetColorLight
				DrawRect x + 2, y + 2, width - 4, height - 4
				SetColor 255, 255, 255
				DrawRect x + width - 25, y + 4, 1, height - 8
				DrawRect x + width - 20, y + 7, 13, 1
				DrawRect x + width - 20, y + 12, 13, 1
				DrawRect x + width - 20, y + 17, 13, 1
			Case 8 ' msg box
				SetColor 0, 0, 0
				SetAlpha 0.3
				DrawRect x + 4, y + 4, width, height
				SetAlpha 1.0
				SetColorLight
				DrawRect x, y, width, height
				SetColorTrim
				DrawRect x + 1, y + 1, width - 2, height - 2
				SetColorLight
				DrawRect x + 2, y + 2, width - 4, height - 4
				SetColor 255, 255, 255
				DrawRect x + 6, y + 27, width - 12, 1
			Case 99
				SetColor 0, 0, 0
				SetAlpha 0.6
				DrawRect x, y, width, height
				SetAlpha 1.0
				SetColor 255, 255, 255
		End Select
	End Function
	
	Function SetColorDark:Void()
		SetColor 30, 89, 218
	End Function
	
	Function SetColorLight:Void()
		SetColor 68, 134, 250
	End Function
	
	Function SetColorTrim:Void()
		SetColor 255, 255, 255
	End Function
	
	Function Text(text:String, x:Int, y:Int, center_x:Float = 0.0, center_y:Float = 0.0)
		SetBlend AdditiveBlend
		DrawText(text, x, y, center_x, center_y)
		SetBlend 0
	End Function
	
End Class

'╔════════════════════════════════════════════════════════════════════════════════════════════════╗
'   Gadget :	Canvas                               
'╚════════════════════════════════════════════════════════════════════════════════════════════════╝
Class ezCanvas
	Private
	Field x_Pos:Int
	Field y_Pos:Int
	Field width:Int
	Field height:Int
	Field hidden:Bool
	
	Method New(canvas_x:Int, canvas_y:Int, canvas_width:Int, canvas_height:Int)
		x_Pos = canvas_x
		y_Pos = canvas_y
		width = canvas_width
		height = canvas_height
	End Method
	Public
	
	'Summary: Returns mouse x position relative to this canvas
	Method Mouse_X:Int()
		If MouseInCanvas = False Then Return 0
		Return MouseX - x_Pos
	End Method
	
	'Summary: Returns mouse y position relative to this canvas
	Method Mouse_Y:Int()
		If MouseInCanvas = False Then Return 0
		Return MouseY - y_Pos
	End Method
	
	'Summary: Return if the mouse is within the canvas area
	Method MouseInCanvas:Bool()
		If ezMessageBox_Active Then Return
		If hidden Then Return
		If MouseX &gt; x_Pos And MouseX &lt; x_Pos + width And MouseY &gt; y_Pos And MouseY &lt; y_Pos + height
			Return True
		EndIf
	End Method
	
	'Summary: Call before rendering to this canvas, also set CLS color
	Method Push:Void(r:Int = 0, g:Int = 0, b:Int = 0)
		PushMatrix
		If hidden=false ezStyles.Box(1, x_Pos - 2, y_Pos - 2, width + 4, height + 4, False, r, g, b)
		If hidden SetScissor 0, 0, 0, 0 Else SetScissor x_Pos, y_Pos, width, height
		Translate x_Pos, y_Pos
	End Method
	
	'Summary: Call when done rendering on this canvas
	Method Pop:Void()
		SetScissor 0, 0, DeviceWidth, DeviceHeight
		PopMatrix
	End Method
	
	'Summary: Return the x position of the gadget
	Method LayoutX:Int(); Return x_Pos; End Method
	'Summary: Return the y position of the gadget
	Method LayoutY:Int(); Return y_Pos; End Method
	'Summary: Return the width of the gadget
	Method LayoutWidth:Int(); Return width; End Method
	'Summary: Return the height of the gadget
	Method LayoutHeight:Int(); Return height; End Method
	'Summary: hide\Show this gadget, hidden gadgets do not function
	Method Hide:Void(answer:Bool); hidden = answer; End Method
	
End Class

'╔════════════════════════════════════════════════════════════════════════════════════════════════╗
'   Gadget :	Slider                              
'╚════════════════════════════════════════════════════════════════════════════════════════════════╝
Class ezSlider
	Private
	Const VERT:Int = 0, HORZ:Int = 1
	Field x_Pos:Int
	Field y_Pos:Int
	Field width:Int
	Field height:Int
	Field value:Int
	Field size:Float[2]
	Field totalValue:Int
	Field orientation:Int
	Field active:Bool
	Field disabled:Bool
	Field hidden:Bool
	
	Method New(slider_orientation:Int, slider_x:Int, slider_y:Int, slider_length:Int)
		orientation = slider_orientation
		x_Pos = slider_x
		y_Pos = slider_y
		If orientation = HORZ Then width = slider_length; height = 25
		If orientation = VERT Then height = slider_length; width = 25
	End Method
	
	Method MouseOver:Bool()
		If MouseX &gt; x_Pos And MouseX &lt; x_Pos + width And MouseY &gt; y_Pos And MouseY &lt; y_Pos + height
			Return True
		EndIf
	End Method
	Public
	
	'Summary: Set slider (0 .. max value) and visual knob size
	Method SetRange:Void(slider_knob_size:Int, slider_max_value:Int)
		totalValue = (slider_max_value - 1)
		If orientation = HORZ Then size[0] = (Float(width) - Float(slider_knob_size)) / Float(totalValue)
		If orientation = VERT Then size[0] = (Float(height) - Float(slider_knob_size)) / Float(totalValue)
		size[1] = slider_knob_size
	End Method
	
	'Summary: Set slider value
	Method SetValue:Void(slider_value:Int)
		If slider_value &lt; 0 Then Return
		If slider_value &gt; totalValue Then Return
		value = slider_value
	End Method
	
	'Summary: Return slider value
	Method Value:Int(); Return value; End Method
	
	'Summary: Render the gadget
	Method Render:Void(alt:Bool = False)
		If hidden Then Return
		PushMatrix
		If alt = False Then ezStyles.Box(0, x_Pos, y_Pos, width, height, active)
		If alt = True Then ezStyles.Box(4, x_Pos, y_Pos, width, height, active)
		Translate x_Pos, y_Pos
		If size[1] &gt; 0
		If orientation = HORZ Then DrawRect Int( (value * size[0]) + 4), 4, size[1] - 8, height - 8
		If orientation = VERT Then DrawRect 4, Int( (value * size[0]) + 4), width - 8, size[1] - 8
		EndIf
		PopMatrix
		If disabled Then ezStyles.Box(99, x_Pos, y_Pos, width, height)
	End Method
	
	'Summary: Update the gadget
	Method Update:Void()
		If ezMessageBox_Active Then Return
		If disabled or hidden Then Return
		If active = False
			If MouseOver = True and MouseHit = True
				active = True
			End If
		ElseIf active = True
			If MouseDown = False
				active = False
			EndIf	
		End If		
		If active = False Then Return
		If MouseDown = True
			Local i:Int
			If orientation = HORZ
				i = (MouseX - x_Pos) / size[0]
			ElseIf orientation = VERT
				i = (MouseY - y_Pos) / size[0]
			End If
			
			If i &gt; totalValue Then i = totalValue
			SetValue(i)
		EndIf
	End Method
	
	'Summary: Return the x position of the gadget
	Method LayoutX:Int(); Return x_Pos; End Method
	'Summary: Return the y position of the gadget
	Method LayoutY:Int(); Return y_Pos; End Method
	'Summary: Return the width of the gadget
	Method LayoutWidth:Int(); Return width; End Method
	'Summary: Return the height of the gadget
	Method LayoutHeight:Int(); Return height; End Method
	'Summary: Disable\Enable this gadget
	Method Disable:Void(answer:Bool); disabled = answer; End Method
	'Summary: hide\Show this gadget, hidden gadgets do not function
	Method Hide:Void(answer:Bool); hidden = answer; End Method
	
End Class

'╔════════════════════════════════════════════════════════════════════════════════════════════════╗
'   Gadget :	Button                               
'╚════════════════════════════════════════════════════════════════════════════════════════════════╝
Class ezButton
	Private
	Field x_Pos:Int
	Field y_Pos:Int
	Field width:Int
	Field height:Int
	Field label:String
	Field pressed_down:Bool
	Field disabled:Bool
	Field hidden:Bool
	Field forceUpdate:Bool
	
	Method New(button_x:Int, button_y:Int, button_width:Int, button_height:Int, button_label:String = "")
		x_Pos = button_x
		y_Pos = button_y
		width = button_width
		height = button_height
		label = button_label
	End Method
	
	Method MouseOver:Bool()
		If MouseX &gt; x_Pos And MouseX &lt; x_Pos + width And MouseY &gt; y_Pos And MouseY &lt; y_Pos + height
			Return True
		EndIf
	End Method
	
	Method ForceUpdate:Void()
		forceUpdate = True
	End Method
	
	Public
	
	'Summary: Set the text label of the gadget
	Method SetLabel:Void(button_label:String)
		label = button_label
	End Method
	
	'Summary: Render the gadget
	Method Render:Void(style:Int = 0)
		If hidden Then Return
		ezStyles.Box(style, x_Pos, y_Pos, width, height, pressed_down)
		SetScissor x_Pos + 4, y_Pos, width - 8, height
		ezStyles.Text(label, x_Pos + (width / 2), y_Pos + (height / 2), 0.5, 0.5)
		SetScissor 0, 0, DeviceWidth, DeviceHeight
		If disabled Then ezStyles.Box(99, x_Pos, y_Pos, width, height)
	End Method
	
	'Summary: Update the gadget
	Method Update:Bool()
	If ezMessageBox_Active and forceUpdate = False Then Return
	If disabled or hidden Then Return
		If pressed_down = False
			If MouseOver = True and MouseHit = True
				pressed_down = True
			End If
		ElseIf pressed_down = True
			If MouseOver
				If MouseDown = False Then pressed_down = False; Return True
			Else
				pressed_down = False
				Return False
			End If	
		End If
		Return False
	End Method
	
	'Summary: Return the x position of the gadget
	Method LayoutX:Int(); Return x_Pos; End Method
	'Summary: Return the y position of the gadget
	Method LayoutY:Int(); Return y_Pos; End Method
	'Summary: Return the width of the gadget
	Method LayoutWidth:Int(); Return width; End Method
	'Summary: Return the height of the gadget
	Method LayoutHeight:Int(); Return height; End Method
	'Summary: Disable\Enable this gadget
	Method Disable:Void(answer:Bool); disabled = answer; End Method
	'Summary: hide\Show this gadget, hidden gadgets do not function
	Method Hide:Void(answer:Bool); hidden = answer; End Method
	
End Class

'╔════════════════════════════════════════════════════════════════════════════════════════════════╗
'   Gadget :	Text Box                               
'╚════════════════════════════════════════════════════════════════════════════════════════════════╝
Class ezTextBox
	Private
	Field x_Pos:Int
	Field y_Pos:Int
	Field width:Int
	Field box_Width:Int
	Field height:Int
	Field value:String
	Field new_Value:String
	Field selected:Bool
	Field blink:Bool
	Field blink_Timer:Int
	Field label:String
	Field x_Offset:Int
	Field x_Cursor:Int
	Field cursor_Index:Int
	Field font_Width:Int = 7
	Field disabled:Bool
	Field hidden:Bool
	
	Method New(textbox_x:Int, textbox_y:Int, textbox_width:Int, textbox_value:String = "")
		x_Pos = textbox_x
		y_Pos = textbox_y
		width = textbox_width
		box_Width = textbox_width
		height = 25
		value = textbox_value
		new_Value = value
	End Method
	
	Method MouseOver:Bool()
		If MouseX &gt; x_Pos And MouseX &lt; x_Pos + width And MouseY &gt; y_Pos And MouseY &lt; y_Pos + height
			Return True
		EndIf
	End Method
	
	Method StrMid:String(value:String, index:Int, count:Int)
		Return value[ (index - 1) .. ( (index - 1) + count)]
	End Method
	
	Method UpdateBlink:Bool()
		If selected = False Then Return False
		If blink_Timer &lt; Millisecs
			blink_Timer = Millisecs + 250
			If blink = True Then blink = False Else blink = True
		End If
		Return blink
	End Method

	Public
	
	'Summary: Clear the text from the box
	Method Clear:Void()
		SetValue("")
	End Method
	
	'Summary: Set the text label of the gadget
	Method SetLabel:Void(textbox_label:String)
		label = textbox_label
		box_Width = width - TextWidth(label) - 8
		If box_Width &lt;50 Then box_Width = 50
	End Method
	
	'Summary: Return the text box string
	Method Value:String(); Return value; End Method
	
	'Summary: Set the text box string
	Method SetValue:Void(textbox_value:String)
		value = textbox_value
		new_Value = value
	End Method
	
	'Summary: Render the gadget
	Method Render:Void()
		If hidden Then Return
		SetScissor x_Pos, y_Pos, width - box_Width - font_Width, height
		ezStyles.Text(label, x_Pos, y_Pos + (height * 0.5), 0.0, 0.5)
		SetScissor 0, 0, DeviceWidth, DeviceHeight
		ezStyles.Box(0, x_Pos + width - box_Width + 1, y_Pos, box_Width, height, selected)
		PushMatrix
		Translate x_Pos - x_Offset + font_Width, y_Pos
			SetScissor x_Pos + width - box_Width + font_Width, y_Pos, box_Width - (font_Width * 2), height
			ezStyles.Text(new_Value, width - box_Width, height * 0.5, 0, 0.5)
			If UpdateBlink Then DrawRect width - box_Width + (x_Cursor * font_Width), Int( (height * 0.5) - (TextHeight * 0.5)), font_Width, 12
			SetScissor 0, 0, DeviceWidth, DeviceHeight
		PopMatrix
		If disabled Then ezStyles.Box(99, x_Pos + width - box_Width, y_Pos, box_Width, height)
	End Method
	
	'Summary: Update the gadget
	Method Update:Void()
		If ezMessageBox_Active Then Return
		If disabled or hidden Then Return
		If MouseHit
			If MouseOver
				If selected = False Then x_Cursor = value.Length; new_Value = value
				selected = True
			Else
				selected = False
				x_Cursor = 0
				x_Offset = 0
				value = new_Value
			End If
		EndIf
		If selected = False Then Return
		Repeat
            Local char=GetChar()
            If Not char Exit
            If char = CHAR_BACKSPACE
				If x_Cursor = 0 Then Continue
				Local parts:String[2]
				parts[0] = StrMid(new_Value, 1, x_Cursor - 1)
				parts[1] = StrMid(new_Value, x_Cursor + 1, new_Value.Length)
				new_Value = parts[0] + parts[1]
				x_Cursor = x_Cursor - 1
			ElseIf char = CHAR_ENTER
				selected = False
				x_Cursor = 0
				x_Offset = 0
				value = new_Value
			ElseIf char = CHAR_RIGHT
				x_Cursor = x_Cursor + 1
				If x_Cursor &gt; new_Value.Length Then x_Cursor = new_Value.Length
			ElseIf char = CHAR_LEFT
				x_Cursor = x_Cursor - 1
				If x_Cursor &lt;= 0 Then x_Cursor = 0; x_Offset = 0
			ElseIf char &gt;= 32
				Local parts:String[2]
				parts[0] = StrMid(new_Value, 1, x_Cursor)
				parts[1] = StrMid(new_Value, x_Cursor + 1, new_Value.Length)
                new_Value = parts[0] + String.FromChar(char) + parts[1]
				x_Cursor = x_Cursor + 1
			EndIf
        Forever
		If (x_Cursor + 3) &gt; (box_Width / font_Width) Then x_Offset = - ( (box_Width / font_Width) - (x_Cursor + 2)) * font_Width
	End Method
	
	'Summary: Return the x position of the gadget
	Method LayoutX:Int(); Return x_Pos; End Method
	'Summary: Return the y position of the gadget
	Method LayoutY:Int(); Return y_Pos; End Method
	'Summary: Return the width of the gadget
	Method LayoutWidth:Int(); Return width; End Method
	'Summary: Return the height of the gadget
	Method LayoutHeight:Int(); Return height; End Method
	'Summary: Disable\Enable this gadget
	Method Disable:Void(answer:Bool); disabled = answer; End Method
	'Summary: hide\Show this gadget, hidden gadgets do not function
	Method Hide:Void(answer:Bool); hidden = answer; End Method

End Class

'╔════════════════════════════════════════════════════════════════════════════════════════════════╗
'   Gadget :	Check Box                              
'╚════════════════════════════════════════════════════════════════════════════════════════════════╝
Class ezCheckBox
	Private
	Field x_Pos:Int
	Field y_Pos:Int
	Field width:Int
	Field height:Int
	Field label:String
	Field state:Int
	Field disabled:Bool
	Field hidden:Bool
	
	Method New(checkbox_x:Int, checkbox_y:Int, checkbox_width:Int, checkbox_label:String = "")
		x_Pos = checkbox_x
		y_Pos = checkbox_y
		width = checkbox_width
		height = 25
		label = checkbox_label
	End Method
	
	Method MouseOver:Bool()
		If MouseX &gt; x_Pos And MouseX &lt; x_Pos + width And MouseY &gt; y_Pos And MouseY &lt; y_Pos + height
			Return True
		EndIf
	End Method
	Public
	
	'Summary: Render the gadget
	Method Render:Void()
		If hidden Then Return
		ezStyles.Box(2, x_Pos, y_Pos, height, height, Bool(state))
		SetScissor x_Pos, y_Pos, width, height
		ezStyles.Text(label, x_Pos + height+7, y_Pos + (height * 0.5), 0, 0.5)
		SetScissor 0, 0, DeviceWidth, DeviceHeight
		If disabled Then ezStyles.Box(99, x_Pos, y_Pos, height, height)
	End Method
	
	'Summary: Update the gadget
	Method Update:Bool()
		If ezMessageBox_Active Then Return
		If disabled or hidden Then Return
		If MouseHit
			If MouseOver
				If state = 1 Then state = 0 Else state = 1
			End If
		EndIf
	End Method
	
	'Summary: Set the State; 1 (checked) or 0 (unchecked) 
	Method SetState:Void(checkbox_state:Int); state = checkbox_state; End Method
	'Summary: Return the State; 1 (checked) or 0 (unchecked)
	Method State:Int(); Return state; End Method
	'Summary: Set the text label of the gadget
	Method SetLabel:Void(checkbox_label:String); label = checkbox_label; End Method
	'Summary: Return the x position of the gadget
	Method LayoutX:Int(); Return x_Pos; End Method
	'Summary: Return the y position of the gadget
	Method LayoutY:Int(); Return y_Pos; End Method
	'Summary: Return the width of the gadget
	Method LayoutWidth:Int(); Return width; End Method
	'Summary: Return the height of the gadget
	Method LayoutHeight:Int(); Return height; End Method
	'Summary: Disable\Enable this gadget
	Method Disable:Void(answer:Bool); disabled = answer; End Method
	'Summary: hide\Show this gadget, hidden gadgets do not function
	Method Hide:Void(answer:Bool); hidden = answer; End Method
	
End Class

'╔════════════════════════════════════════════════════════════════════════════════════════════════╗
'   Gadget :	List Box                            
'╚════════════════════════════════════════════════════════════════════════════════════════════════╝
Class ezListBox
	Private
	Field x_Pos:Int
	Field y_Pos:Int
	Field width:Int
	Field height:Int
	Field list:String[]
	Field font_Width:Int = 7
	Field line_Height:Int = 14
	Field top_Space:Int = 8
	Field index:Int
	Field new_index:Int
	Field slider:ezSlider
	Field disabled:Bool
	Field hidden:Bool
	
	Method New(listbox_x:Int, listbox_y:Int, listbox_width:Int, listbox_height:Int)
		x_Pos = listbox_x
		y_Pos = listbox_y
		width = listbox_width
		height = listbox_height
		slider = New ezSlider(0, x_Pos + width - 25 - 2, y_Pos + 2, height - 4)
	End Method
	
	Method MouseOver:Bool()
		If MouseX &gt; x_Pos And MouseX &lt; x_Pos + width - 27 And MouseY &gt; y_Pos And MouseY &lt; y_Pos + height
			Return True
		EndIf
	End Method
	Public
	
	'Summary: Add an item to the bottom of the list
	Method AddItem:Void(text:String)
		If text.Length = 0 Return
		list = list.Resize(list.Length + 1)
		list[list.Length - 1] = text
		Local a:Int = (list.Length - 1) - ( (height - line_Height - top_Space) / line_Height)
		Local knobSize:Int = (Float(height) / (Float(a) + 1))
		If knobSize &lt; 25 Then knobSize = 25
		If a &gt; 0 slider.SetRange(knobSize, a + 1)
		If a &lt;= 0 slider.SetRange(0, 0)
	End Method
	
	'Summary: Export the list of items
	Method Export:String[] ()
		Local export:String[list.Length]
		For Local a:Int = 0 Until list.Length
			export[a] = list[a]
		Next
		Return export
	End Method

	'Summary: Clear all items off the list
	Method Clear:Void()
		slider.SetValue(0)
		slider.SetRange(0, 0)
		list = list.Resize(0)
		index = -999
	End Method
	
	'Summary: Render the list box
	Method Render:Void()
		If hidden Then Return
		ezStyles.Box(0, x_Pos - 2, y_Pos - 2, width + 4, height + 4)
		PushMatrix
		Translate x_Pos + font_Width, y_Pos + top_Space
		SetScissor x_Pos, y_Pos + top_Space, width - font_Width - 25, height-(top_Space*2)
		ezStyles.Box(3, 0, ( (index - slider.Value) * line_Height), width, line_Height-1)
		For Local i:Int = slider.Value Until list.Length
			ezStyles.Text(list[i], 0, ((i-slider.Value) * line_Height))
		Next
		PopMatrix
		SetScissor 0, 0, DeviceWidth, DeviceHeight
		slider.Render(True)
		If disabled Then ezStyles.Box(99, x_Pos, y_Pos, width, height)
	End Method
	
	'Summary: Update the list box
	Method Update:Void()
		If ezMessageBox_Active Then Return
		If disabled or hidden Then Return
		slider.Update
		If MouseOver = False Then Return
		new_index = slider.Value + ( (MouseY - y_Pos - top_Space) / line_Height)
		If MouseHit
			If new_index &gt;= list.Length Then Return
			index = new_index
		End If
	End Method
	
	'Summary: Return the selected index value
	Method Index:Int()
		If index &lt; 0 Then Return 0
		Return index
	End Method
	
	'Summary: Return the selected index text
	Method Item:String()
		If index &lt; 0 Then Return ""
		Return list[index]
	End Method
	
	'Summary: Return the x position of the gadget
	Method LayoutX:Int(); Return x_Pos; End Method
	'Summary: Return the y position of the gadget
	Method LayoutY:Int(); Return y_Pos; End Method
	'Summary: Return the width of the gadget
	Method LayoutWidth:Int(); Return width; End Method
	'Summary: Return the height of the gadget
	Method LayoutHeight:Int(); Return height; End Method
	'Summary: Disable\Enable this gadget
	Method Disable:Void(answer:Bool); disabled = answer; End Method
	'Summary: hide\Show this gadget, hidden gadgets do not function
	Method Hide:Void(answer:Bool); hidden = answer; End Method
	
End Class

'╔════════════════════════════════════════════════════════════════════════════════════════════════╗
'   Gadget :	Spinner
'╚════════════════════════════════════════════════════════════════════════════════════════════════╝
Class ezSpinner
	Private
	Field x_Pos:Int
	Field y_Pos:Int
	Field width:Int
	Field box_Width:Int
	Field height:Int
	Field value:Int
	Field selected:Bool
	Field blink:Bool
	Field blink_Timer:Int
	Field x_Offset:Int
	Field x_Cursor:Int
	Field cursor_Index:Int
	Field font_Width:Int = 7
	Field button_plus:ezButton
	Field button_minus:ezButton
	Field min_Value:Int
	Field max_Value:Int
	Field jump_Value:Int
	Field disabled:Bool
	Field hidden:Bool
	
	Method New(spinner_x:Int, spinner_y:Int)
		x_Pos = spinner_x
		y_Pos = spinner_y
		height = 25
	End Method
	
	Method MouseOver:Bool()
		If MouseX &gt; x_Pos And MouseX &lt; x_Pos + width - 40 And MouseY &gt; y_Pos And MouseY &lt; y_Pos + height
			Return True
		EndIf
	End Method
	
	Public
	
	'Summary: Set the max and min values and step value
	Method SetRange:Void(min:Int, max:Int, steps:Int = 1)
		min_Value = min
		max_Value = max
		jump_Value = steps
		value = min_Value
		Local a:String = max_Value
		box_Width = 44 + TextWidth(a) + 4 + font_Width
		width = box_Width
		button_plus = New ezButton(x_Pos + width - 20, y_Pos, 20, 25, "+")
		button_minus = New ezButton(x_Pos + width - 40, y_Pos, 20, 25, "-")
	End Method
	
	'Summary: Return the spinner value
	Method Value:Int(); Return value; End Method
	
	'Summary: Set the spinner value
	Method SetValue:Void(spinner_value:Int)
		value = spinner_value
		If value &gt; max_Value Then value = min_Value
		If value &lt; min_Value Then value = max_Value
	End Method
	
	'Summary: Render the gadget
	Method Render:Void()
		If hidden Then Return
		ezStyles.Box(5, x_Pos + width - box_Width + 1, y_Pos, box_Width, height, selected)
		PushMatrix
		Translate x_Pos - x_Offset + font_Width, y_Pos
			SetScissor x_Pos + width - box_Width + font_Width - 40, y_Pos, box_Width - (font_Width * 2), height
			ezStyles.Text(value, (width - box_Width), height * 0.5, 0, 0.5)
			SetScissor 0, 0, DeviceWidth, DeviceHeight
		PopMatrix
		button_minus.Render(6)
		button_plus.Render(6)
		If disabled Then ezStyles.Box(99, x_Pos, y_Pos, width, height)
	End Method
	
	'Summary: Update the gadget
	Method Update:Void()
		If ezMessageBox_Active Then Return
		If disabled or hidden Then Return
		If button_plus.Update Then value = value + jump_Value; SetValue(value)
		If button_minus.Update Then value = value - jump_Value; SetValue(value)
	End Method
	
	'Summary: Return the x position of the gadget
	Method LayoutX:Int(); Return x_Pos; End Method
	'Summary: Return the y position of the gadget
	Method LayoutY:Int(); Return y_Pos; End Method
	'Summary: Return the width of the gadget
	Method LayoutWidth:Int(); Return width; End Method
	'Summary: Return the height of the gadget
	Method LayoutHeight:Int(); Return height; End Method
	'Summary: Disable\Enable this gadget
	Method Disable:Void(answer:Bool); disabled = answer; End Method
	'Summary: hide\Show this gadget, hidden gadgets do not function
	Method Hide:Void(answer:Bool); hidden = answer; End Method

End Class

'╔════════════════════════════════════════════════════════════════════════════════════════════════╗
'   Gadget :	Combo box
'╚════════════════════════════════════════════════════════════════════════════════════════════════╝
Class ezComboBox
	Private
	Field x_Pos:Int
	Field y_Pos:Int
	Field width:Int
	Field height:Int
	Field list:String[]
	Field index:Int
	Field active:Bool
	Field disabled:Bool
	Field hidden:Bool
	
	Method MouseOver:Bool()
		If MouseX &gt; x_Pos And MouseX &lt; x_Pos + width And MouseY &gt; y_Pos And MouseY &lt; y_Pos + height
			Return True
		EndIf
	End Method
	
	Method New(combobox_x:Int, combobox_y:Int, combobox_width:Int)
		x_Pos = combobox_x
		y_Pos = combobox_y
		width = combobox_width
		height = 25
	End Method
	Public
	
	'Summary: Add an item to the combo box
	Method AddItem:Void(text:String)
		list = list.Resize(list.Length + 1)
		list[list.Length - 1] = text
	End Method
	
	'Summary: Clear all items
	Method Clear:Void()
		list = list.Resize(0)
		index = 0
	End Method
	
	'Summary: Render this gadget
	Method Render:Void()
		If hidden Then Return
		ezStyles.Box(7, x_Pos, y_Pos, width, height, active)
		SetScissor x_Pos + 4, y_Pos, width - 8 - 25, height
		ezStyles.Text(list[index], x_Pos + (width / 2) - 12, y_Pos + (height / 2), 0.5, 0.5)
		SetScissor 0, 0, DeviceWidth, DeviceHeight
		If disabled Then ezStyles.Box(99, x_Pos, y_Pos, width, height)
	End Method
	
	'Summary: Update this gadget
	Method Update:Void()
	If ezMessageBox_Active Then Return
	If disabled or hidden Then Return
		If MouseHit
			If MouseOver
				If MouseDown active = True
				index = index + 1
				If index &gt; list.Length-1 Then index = 0
			Else
				active = False
			EndIf
		Else
			If MouseDown = False active = False
		End If
	End Method
	
	'Summary: Return the selected index value
	Method Index:Int()
		If index &lt; 0 Then Return 0
		Return index
	End Method
	
	'Summary: Return the selected index text
	Method Item:String()
		If index &lt; 0 Then Return ""
		Return list[index]
	End Method
	
	'Summary: Return the x position of the gadget
	Method LayoutX:Int(); Return x_Pos; End Method
	'Summary: Return the y position of the gadget
	Method LayoutY:Int(); Return y_Pos; End Method
	'Summary: Return the width of the gadget
	Method LayoutWidth:Int(); Return width; End Method
	'Summary: Return the height of the gadget
	Method LayoutHeight:Int(); Return height; End Method
	'Summary: Disable\Enable this gadget
	Method Disable:Void(answer:Bool); disabled = answer; End Method
	'Summary: hide\Show this gadget, hidden gadgets do not function
	Method Hide:Void(answer:Bool); hidden = answer; End Method
	
End Class

'╔════════════════════════════════════════════════════════════════════════════════════════════════╗
'   Gadget :	Label
'╚════════════════════════════════════════════════════════════════════════════════════════════════╝
Class ezLabel
	Private
	Field x_Pos:Int
	Field y_Pos:Int
	Field width:Int
	Field height:Int
	Field text:String
	Field hidden:Bool
	
	Method New(label_x:Int, label_y:Int, label_width:Int)
		x_Pos = label_x
		y_Pos = label_y
		width = label_width
		height = 25
	End Method
	Public
	
	Method SetText:Void(label_text:String)
		text = label_text
	End Method
	
	Method Render:Void()
		If hidden Then Return
		SetScissor x_Pos, y_Pos, width, height
		ezStyles.Text(text, x_Pos, y_Pos + (height / 2), 0, 0.5)
		SetScissor 0, 0, DeviceWidth, DeviceHeight
	End Method
	
	'Summary: Return the x position of the gadget
	Method LayoutX:Int(); Return x_Pos; End Method
	'Summary: Return the y position of the gadget
	Method LayoutY:Int(); Return y_Pos; End Method
	'Summary: Return the width of the gadget
	Method LayoutWidth:Int(); Return width; End Method
	'Summary: Return the height of the gadget
	Method LayoutHeight:Int(); Return height; End Method
	'Summary: hide\Show this gadget, hidden gadgets do not function
	Method Hide:Void(answer:Bool); hidden = answer; End Method
	
End Class

'╔════════════════════════════════════════════════════════════════════════════════════════════════╗
'   Gadget :	MessageBox
'╚════════════════════════════════════════════════════════════════════════════════════════════════╝
Global ezMessageBox_Active:Bool
Global ezMessageBox_Text:String[]
Global ezMessageBox_Title:String
Global ezMessageBox_Button1:ezButton
Global ezMessageBox_Button0:ezButton
Class ezMessageBox
	
	'Summary: Show the message box, style 0 = Yes\No, style 1 = Ok\Cancel
	Function Show:Void(style:Int = 0)
		Local width:Int = 400
		Local height:Int = 100 + ( (ezMessageBox_Text.Length + 1) * 12)
		Local x:Int = (DeviceWidth / 2) - (width / 2)
		Local y:Int = (DeviceHeight / 2) - (height / 2)	
		Local button1_text:String = "Yes"
		Local button0_text:String = "No"
		If style &lt;&gt; 0
			button1_text = "Ok"
			button0_text = "Cancel"
		End If
		If ezMessageBox_Active = False
			ezMessageBox_Active = True
			ezMessageBox_Button1 = New ezButton( (DeviceWidth / 2) - 8 - 50, y + height - 33, 55, 25, button1_text)
				ezMessageBox_Button1.ForceUpdate
			ezMessageBox_Button0 = New ezButton( (DeviceWidth / 2) + 8, y + height - 33, 55, 25, button0_text)
				ezMessageBox_Button0.ForceUpdate
		End If
		ezStyles.Box(8, x, y, width, height)
		ezStyles.Text(ezMessageBox_Title, x + (width / 2), y + 14, 0.5, 0.5)
		PushMatrix
		Translate x + (width / 2), (DeviceHeight / 2) - (ezMessageBox_Text.Length*6)
		For Local a:Int = 0 Until ezMessageBox_Text.Length
			ezStyles.Text(ezMessageBox_Text[a], 0, (a * 12), 0.5, 0.5)
		Next
		PopMatrix
		ezMessageBox_Button1.Render
		ezMessageBox_Button0.Render
	End Function
	
	'Summary: Return when the Yes or Ok button is pressed
	Function YesOk:Bool()
		If ezMessageBox_Active = False Return False
		If ezMessageBox_Button1.Update Then ezMessageBox_Active = False; Return True
		Return False
	End Function
	
	'Summary: Return when the No or cancel button is pressed
	Function NoCancel:Bool()
		If ezMessageBox_Active = False Return False
		If ezMessageBox_Button0.Update Then ezMessageBox_Active = False; Return True
		Return False
	End Function
	
	' Summary: Set the message body, ~r for new lines
	Function SetMessage:Void(msg_text:String)
		ezMessageBox_Text = msg_text.Split("~r")
	End Function
	
	'Summary: Set the title of the message box
	Function SetTitle:Void(title_text:String)
		ezMessageBox_Title = title_text
	End Function
	
End Class
</textarea><br><br><b>Example</b><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import mojo
Import eazyGadget

Function Main(); New Game(); End
Class Game Extends App

	Field butSend:ezButton
	Field txtInput:ezTextBox
	
	'Summary: Main Create
	Method OnCreate()
		SetUpdateRate(30)
		txtInput = New ezTextBox(100, 100, 100)
		butSend = New ezButton(100, 140, 100, 35, "Send")
	End Method

	'Summary: Main Update
	Method OnUpdate()
		txtInput.Update
		If butSend.Update Then Print txtInput.Value; txtInput.Clear
	End Method
	
	'Summary: Main Render
	Method OnRender()
		txtInput.Render
		butSend.Render
	End Method

End
</textarea><br><br><b>[h2]List of gadget Methods[/h2]</b><br><br><b>Shared methods, all gadget have these methods</b><br>Update - Needs to be called within OnUpdate<br>Render - Needs to be called within OnRender<br>LayoutX - Screen X position of the gadget<br>LayoutY - Screen Y position of the gadget<br>LayoutWidth - The width of the gadget<br>LayoutHeight - The height of the gadget<br>Hide - A hidden gadget is not rendered and cannot be used<br>Disabled - Gadget cannot be used but is rendered.<br><br><b>Canvas</b><br>Mouse_X - Relative mouse Y position<br>Mouse_Y - Relative mouse X position<br>MouseInCanvas - Return true if the mouse is over the canvas<br>Push - Call before needing to draw to the canvas<br>Pop - Call after your done drawing on the canvas<br><br><b>List box</b><br>AddItem - Add an item to the list<br>Clear - Clear all items off the list<br>Index - Return the selected index value<br>Item - Return the selected Item text<br>Export - Return the list of items as a String array<br><br><b>Button</b><br>SetLabel - Sets the text that appears on the button<br>Update - Return true if the button has been pressed<br><br><b>Text box</b><br>Clear - Clears any text<br>SetLabel - Sets the label that appears to the left of the box<br>Value - Returns the text in the box<br>SetValue - Set what text to appear in the box<br><br><b>Spinner</b><br>SetRange - set min and max values and the step value (how much each press should add/remove)<br>Value - Return the number is the box<br>SetValue - Set a box value<br><br><b>Combo box</b><br>AddItem - Add a item to the list<br>Clear - Clear all items<br>Index - Return the selected index value<br>Item - Return the selected Item text<br><br><b>Slider</b><br>SetRange - Set the size of the knob (handle you drag), set max slide value (0..max)<br>SetValue - Set the slider value<br>Value - Return the slider value<br><br><b>Label</b><br>SetText - Set the label text<br><br><b>Check box</b><br>SetState - Set the state of the checkbox 0=uncheck 1=checked<br>State - Return the checkbox state 0=uncheck 1=checked<br>SetLabel - Set the label text to appear to the right of the check box<br><br><br><br><b>Message box is not an object you create unlike the others, just call ezMessageBox when you want to use it, see commands below</b><br><br>When you want to show the message you must continually call <b>ezMessageBox.Show</b>, best assign a variable to flag when to show the gadget, then when a message box button is pressed the variable should be turned off.<br><br>ezMessageBox.Show - Show the message box, this disabled all other gadgets<br>ezMessageBox.YesOk - Returns True if the Yes or OK button has been pressed<br>ezMessageBox.NoCancel - Returns True if the No or Cancel button has been pressed<br>ezMessageBox.SetMessage - Sets the body message ~r for line breaks<br>ezMessageBox.SetTitle - Sets the messagebox title <br><br></td></tr></table><br>
<a name="2089294"></a>

<a name="2089293"></a>

<a name="2089292"></a>

<a name="2089291"></a>

<a name="2089290"></a>

<a name="2089289"></a>

<a name="2089288"></a>

<a name="2089287"></a>

<a name="2089283"></a>

<a name="2089282"></a>

<a name="2089281"></a>

<a name="2089280"></a>

<a name="2089278"></a>

<a name="2089277"></a>

<a name="2089274"></a>

<a name="2089273"></a>

<a name="2089272"></a>

<a name="2089269"></a>

<a name="2089304"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DruggedBunny</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Works really well, thanks! <br><br></td></tr></table><br>
<a name="2089410"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >consty</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Very nice! Good work and thanks for posting. <br><br></td></tr></table><br>
<a name="2091794"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DiabloV</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> This domain has expired. If you owned this domain, contact your domain registration service provider for further assistance. If you need ............. <br><br></td></tr></table><br>
<a name="2091806"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Supertino</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I knew there was something I was meant to do last weekend, should be back up in a few hours. <br><br></td></tr></table><br>
<a name="2091817"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sammy</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Lucky for me, I missed this, thank you Supertino. <br><br></td></tr></table><br>
<a name="2092150"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sammy</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Worked fine but it really does not like Autofit, mainly due to SetScissors() being device coord dependant I believe. Not the fault of your lib though. <br><br></td></tr></table><br>
<a name="2092319"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Supertino</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah lots of scissor and matrix calls, not virtual resolution friendly at all I am afraid. <br><br></td></tr></table><br>
<a name="2102008"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GC-Martijn</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm searching for a one file class simple gui for glfw (desktop only).<br>So I'm very happy with this, except its not working :( :(<br><br>First it was complaining about Private bla, so I removed some Private things.<br>Now its saying<br>eazyGadget.monkey&lt;450&gt; : Error : Identifier 'SetScissor' not found.<br><br>But then I tought let add this line at top of the file <br>Import mojo<br><br>And i'm using strict so the code I'm using was<br><pre class=code>
txtInput.Update()
		If butSend.Update() Then 
			Print txtInput.Value()
			txtInput.Clear()
		End
</pre><br><br>And now I can see the sample inside my game !<br>Hoping that the slider is working <br><br></td></tr></table><br>
<a name="2102022"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Supertino</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> You might want to try this version, its a newer version without all the scissors and other fancy stuff, cleaner code too and some nicer gadgets.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import main

Class GUI_Properties
	Private
	Const UP:Int = 0, RIGHT:Int = 1, DOWN:Int = 2, LEFT:Int = 3
	Global Color:= New GUI_Colors
	Global Shape:= New GUI_Shapes
	Global GadgetID:Int						' keep track of the number of gadgets to assign new id's
	Global SelectedGadgetID:Int				' what gadget is currently selected
	Const DEFAULTGADSIZE:Int = 22			' gadget height of gadgets that do not have a height attribute
	Field _textWidth:Int = 7				' width of a char
	Field _x:Int							' x position of the gadget
	Field _y:Int							' y position of the gadget
	Field _width:Int						' width of gadget
	Field _height:Int						' height of gadget
	Field _id:Int							' unique id of the gadget
	Field _label:String 					' for gadgets that have a label such as button and textbox
	Field _value:Int 						' for gadgets that return an Int value
	Field _text:String 						' for gadgets that return a String value
	Field _index:Int 						' for gadgets that return a index value
	Field _state:Int						' for gagets that can have states, buttons, checkboxes
	Field _enabled:Bool = True				' enable\disable gadget flag
	Field _hidden:Bool						' hide\show gadget flag
	
	'Summary:
	Method SetGadgetID:Int(); GadgetID = GadgetID + 1; Return GadgetID; End Method
	
	'Summary:
	Method MouseOverGadget:Bool()
		If MouseX() &gt; _x And MouseY() &gt; _y And MouseX() &lt; _x + _width And MouseY() &lt; _y + _height Then Return True
	End Method
	
	'Summary:
	Function StrMid:String(value:String, index:Int, count:Int)
		Return value[ (index - 1) .. ( (index - 1) + count)]
	End Function
	Public
	
	Method X:Int(); Return _x; End Method
	Method Y:Int(); Return _y; End Method
	Method Width:Int(); Return _width; End Method
	Method Height:Int(); Return _height; End Method
	
	Method Disable:Void(); _enabled = False; End Method
	Method Enable:Void(); _enabled = True; End Method
	
	Method Show:Void(); _hidden = False; End Method
	Method Hide:Void(); _hidden = True; End Method
	
End Class

'╔════════════════════════════════════════════════════════════════════════════════════════════════╗
'╚════════════════════════════════════════════════════════════════════════════════════════════════╝

Class GUI_Colors
	Private
	Method Reset:Void(); SetColor 255, 255, 255; End Method
	Method Base:Void(); SetColor 68, 134, 250; End Method
	Method Dark:Void(); SetColor 39, 59, 155; End Method
	Method Lite:Void(); SetColor 100, 200, 255; End Method
End Class

'╔════════════════════════════════════════════════════════════════════════════════════════════════╗
'╚════════════════════════════════════════════════════════════════════════════════════════════════╝

Class GUI_Shapes
	Private
	Const THICKNESS:Int = 1
	Const LEFT:Int = 0, CENTER:Int = 1, RIGHT:Int = 2
	Const TOP:Int = 0, MIDDLE:Int = 1, BOTTOM:Int = 2
	
	'Summary: Draw a button gadget
	Method Button:Void(a:GUI_Button)
		Select a._state
			Case 0
				a.Color.Lite; DrawRect a._x, a._y, a._width, a._height
				a.Color.Dark; DrawRect a._x + THICKNESS, a._y + THICKNESS, a._width - THICKNESS, a._height - THICKNESS
				a.Color.Base; DrawRect a._x + THICKNESS, a._y + THICKNESS, a._width - (THICKNESS * 2), a._height - (THICKNESS * 2)
				a.Color.Reset
				If a._arrow
					DrawButtonArrow(a._arrowType, a._x, a._y, a._width, a._height)
				Else
					DrawLabel(a._label, a._x, a._y, a._width, a._height, MIDDLE, CENTER)
				End If
			Case 1
				a.Color.Dark; DrawRect a._x, a._y, a._width, a._height
				a.Color.Lite; DrawRect a._x + THICKNESS, a._y + THICKNESS, a._width - THICKNESS, a._height - THICKNESS
				a.Color.Base; DrawRect a._x + THICKNESS, a._y + THICKNESS, a._width - (THICKNESS * 2), a._height - (THICKNESS * 2)
				a.Color.Reset
				If a._arrow
					DrawButtonArrow(a._arrowType, a._x + 1, a._y + 1, a._width, a._height)
				Else
					DrawLabel(a._label, a._x + 1, a._y + 1, a._width, a._height, MIDDLE, CENTER)
				End If
		End Select
	End Method
	
	'Summary: Draw the arrows on sliders and spinner
	Method DrawButtonArrow:Void(dir:Int, x:Int, y:Int, width:float, height:float)
		Local point:Float[6]
		Select dir
			Case 0
				point[0] = x + (width / 2)
				point[1] = y + (height / 2) - (height / 4)
				point[2] = x + (width / 2) + (width / 4)
				point[3] = y + (height / 2) + (height / 4)
				point[4] = x + (width / 2) - (width / 4)
				point[5] = point[3]
			Case 1
				point[0] = x + (width / 2) - (width / 4)
				point[1] = y + (height / 2) - (height / 4)
				point[2] = x + (width / 2) + (width / 4)
				point[3] = y + (height / 2)
				point[4] = point[0]
				point[5] = y + (height / 2) + (height / 4)
			Case 2
				point[0] = x + (width / 2) - (width / 4)
				point[1] = y + (height / 2) - (height / 4)
				point[2] = point[0] + (width / 2)
				point[3] = point[1]
				point[4] = point[0] + (width / 4)
				point[5] = point[1] + (height / 2)
			Case 3
				point[0] = x + (width / 2) + (width / 4)
				point[1] = y + (height / 2) - (height / 4)
				point[2] = point[0]
				point[3] = y + (height / 2) + (height / 4)
				point[4] = x + (width / 2) - (width / 4)
				point[5] = y + (height / 2)
		End Select
		DrawPoly(point)
	End Method
	
	'Summary: Draw a text box gadget
	Method TextBox:Void(a:GUI_TextBox)
		a.Color.Dark; DrawRect a._x, a._y, a._width, a._height
		a.Color.Lite; DrawRect a._x + THICKNESS, a._y + THICKNESS, a._width - THICKNESS, a._height - THICKNESS
		a.Color.Base; DrawRect a._x + THICKNESS, a._y + THICKNESS, a._width - (THICKNESS * 2), a._height - (THICKNESS * 2)
		a.Color.Reset; DrawLabel(a._label, a._x - TextWidth(a._label) + 1, a._y + 1, a._width, a._height, MIDDLE, LEFT)
		DrawLabel(a._visableText, a._x + 4, a._y, a._width, a._height, MIDDLE, LEFT)
	End Method
	
	'Summary: Draw a spinner gadget
	Method Spinner:Void(a:GUI_Spinner)
		a.Color.Dark; DrawRect a._x, a._y, a._width, a._height
		a.Color.Dark; DrawRect a._x + THICKNESS, a._y + THICKNESS, a._width - THICKNESS, a._height - THICKNESS
		a.Color.Base; DrawRect a._x + THICKNESS, a._y + THICKNESS, a._width - (THICKNESS * 2), a._height - (THICKNESS * 2)
		a.Color.Reset; DrawLabel(a._label, (a._x - TextWidth(a._label)) + 1, a._y + 1, a._width, a._height, MIDDLE, LEFT)
		DrawLabel(a._value, a._x, a._y, a._width, a._height, MIDDLE, CENTER)
	End Method
	
	'Summary: Draw a gadget label (attached to a gadget)
	Method DrawLabel:Void(text:String, x:Int, y:Int, width:Int, height:Int, align_vert:Int = LEFT, align_horz:Int = TOP)
		If text.Length = 0 Then Return
		Local tw:float = TextWidth(text) ' need to caste out else Desktop build crashes
		Local th:float = TextHeight ' need to caste out else Desktop build crashes

		Select align_horz
			Case CENTER; x = float(x) + (float(width) / 2.0) - (tw / 2.0)
			Case RIGHT; x = float(x) + float(width) - tw
			Default;
		End Select

		Select align_vert
			Case MIDDLE; y = float(y) + (float(height) / 2) - (th / 2)
			Case BOTTOM; y = float(y) + float(height) - th
			Default;
		End Select

		SetBlend AdditiveBlend
		DrawText text, x, y
		SetBlend AlphaBlend
	End Method
	
	'Summary: Draw a slider gadget
	Method Slider:Void(a:GUI_Slider)
		a.Color.Lite; DrawRect a._x, a._y, a._width, a._height
		a.Color.Dark; DrawRect a._x + THICKNESS, a._y + THICKNESS, a._width - THICKNESS, a._height - THICKNESS
		a.Color.Base; DrawRect a._x + THICKNESS, a._y + THICKNESS, a._width - (THICKNESS * 2), a._height - (THICKNESS * 2)
		a.Color.Reset		
		If a._vertBar
			If a._value = a._maxValue
				DrawRect a._x + (THICKNESS * 4), a._y + a._height - a._knobSize + (THICKNESS * 4), a._knobSize - (THICKNESS * 8), a._knobSize - (THICKNESS * 8)
			Else
				DrawRect a._x + (THICKNESS * 4), a._y + (a._increment * a._value) + (THICKNESS * 4), a._knobSize - (THICKNESS * 8), a._knobSize - (THICKNESS * 8)
			EndIf
		Else
			If a._value = a._maxValue
				DrawRect a._x + a._width - a._knobSize + (THICKNESS * 4), a._y + (THICKNESS * 4), a._knobSize - (THICKNESS * 8), a._knobSize - (THICKNESS * 8)
			Else
				DrawRect a._x + (a._increment * a._value) + (THICKNESS * 4), a._y + (THICKNESS * 4), a._knobSize - (THICKNESS * 8), a._knobSize - (THICKNESS * 8)
			EndIf
		EndIf
	End Method
	
	'Summary: Draw a slider gadget
	Method DropDown:Void(a:GUI_DropDown, part:Int = 0)
		Select part
			Case 0 ' Main gadget
				If Not a._pressed
					a.Color.Lite; DrawRect a._x, a._y, a._width, a._height
					a.Color.Dark; DrawRect a._x + THICKNESS, a._y + THICKNESS, a._width - THICKNESS, a._height - THICKNESS
					a.Color.Base; DrawRect a._x + THICKNESS, a._y + THICKNESS, a._width - (THICKNESS * 2), a._height - (THICKNESS * 2)
					a.Color.Reset
					DrawButtonArrow(a.DOWN, a._x + a._width - a.DEFAULTGADSIZE, a._y, a.DEFAULTGADSIZE, a.DEFAULTGADSIZE)
					If a._itemList.Count = 0 Return
					DrawLabel(a.StrMid(a._itemList.Get(a._index + 1), 1, ( (a._width - a.DEFAULTGADSIZE) / a._textWidth) - 1), a._x + 4, a._y, a._width, a._height, MIDDLE, LEFT)
				Else
					a.Color.Dark; DrawRect a._x, a._y, a._width, a._height
					a.Color.Lite; DrawRect a._x + THICKNESS, a._y + THICKNESS, a._width - THICKNESS, a._height - THICKNESS
					a.Color.Base; DrawRect a._x + THICKNESS, a._y + THICKNESS, a._width - (THICKNESS * 2), a._height - (THICKNESS * 2)
					a.Color.Reset
					DrawButtonArrow(a.DOWN, a._x + a._width - a.DEFAULTGADSIZE + 1, a._y + 1, a.DEFAULTGADSIZE, a.DEFAULTGADSIZE)
					If a._itemList.Count = 0 Return
					DrawLabel(a.StrMid(a._itemList.Get(a._index + 1), 1, ( (a._width - a.DEFAULTGADSIZE) / a._textWidth) - 1), a._x + 4 + 1, a._y + 1, a._width, a._height, MIDDLE, LEFT)
				EndIf
			Case 1 ' drop down list
				If a._itemList.Count = 0 Then Return
				Local height:Int = a._itemList.Count * a._listLineHeight
				Local y = a._y + a._height
				If a._showListAbove
					y = a._y - height
				End If
				a.Color.Lite; DrawRect a._x, y, a._width, height
				a.Color.Dark; DrawRect a._x + THICKNESS, y + THICKNESS, a._width - THICKNESS, height - THICKNESS
				a.Color.Base; DrawRect a._x + THICKNESS, y + THICKNESS, a._width - (THICKNESS * 2), height - (THICKNESS * 2)
				a.Color.Dark
				If a._showListAbove
					DrawRect a._x + THICKNESS, a._y - (a._itemList.Count * a._listLineHeight) + THICKNESS + (a._index * a._listLineHeight), a._width - (THICKNESS * 2), a._listLineHeight - (THICKNESS * 2)
				Else
					DrawRect a._x + THICKNESS, (a._y + a._height) + (a._index * a._listLineHeight) + THICKNESS, a._width - (THICKNESS * 2), a._listLineHeight - (THICKNESS * 2)
				EndIf
				a.Color.Reset
				For Local b:Int = 1 To a._itemList.Count
					DrawLabel(a.StrMid(a._itemList.Get(b), 1, (a._width / a._textWidth) - 1), a._x + 4, (y) + (b - 1) * a._listLineHeight, a._width, a._listLineHeight, MIDDLE, 0)
				Next
		End Select
	End Method
	
	'Summary: Draw a text box gadget
	Method CheckBox:Void(a:GUI_CheckBox)
		a.Color.Dark; DrawRect a._x, a._y, a._width, a._height
		a.Color.Lite; DrawRect a._x + THICKNESS, a._y + THICKNESS, a._width - THICKNESS, a._height - THICKNESS
		a.Color.Base; DrawRect a._x + THICKNESS, a._y + THICKNESS, a._width - (THICKNESS * 2), a._height - (THICKNESS * 2)
		a.Color.Reset; DrawLabel(a._label, a._x + a._textWidth + a._width, a._y, a._width, a._height, MIDDLE, LEFT)
		If a._state
			DrawLine a._x + (a._width / 3), a._y + (a._height / 2), (a._x + a._width) - (a._width / 2), (a._y + a._height) - (a._height / 4)
			DrawLine a._x + (a._width / 3) - 1, a._y + (a._height / 2), (a._x + a._width) - (a._width / 2) - 1, (a._y + a._height) - (a._height / 4)
			DrawLine(a._x + a._width) - (a._width / 4), a._y + (a._height / 4), a._x + (a._width / 2), (a._y + a._height) - (a._height / 4)
			DrawLine(a._x + a._width) - (a._width / 4) - 1, a._y + (a._height / 4), a._x + (a._width / 2) - 1, (a._y + a._height) - (a._height / 4)
		EndIf
	End Method
	
	'Summary: Draw a text box gadget
	Method ListBox:Void(a:GUI_ListBox)
		a.Color.Dark; DrawRect a._x, a._y, a._width, a._height
		a.Color.Lite; DrawRect a._x + THICKNESS, a._y + THICKNESS, a._width - THICKNESS, a._height - THICKNESS
		a.Color.Base; DrawRect a._x + THICKNESS, a._y + THICKNESS, a._width - (THICKNESS * 2), a._height - (THICKNESS * 2)
		a.Color.Reset; DrawLabel(a._label, a._x + a._textWidth + a._width, a._y, a._width, a._height, MIDDLE, LEFT)
		a.Color.Dark
		If a._selectedIndex &gt;= a._listOffset And a._selectedIndex &lt; (a._visableItemCount + a._listOffset)
			DrawRect a._x + THICKNESS,(a._y) + (a._selectedIndex * a._listLineHeight) + THICKNESS - (a._listOffset * a._listLineHeight),a._width - (THICKNESS * 2), a._listLineHeight - (THICKNESS * 2)
		EndIf
		a.Color.Reset
		For Local b:Int = 1 + a._listOffset To a._visableItemCount + a._listOffset
			DrawLabel(a.StrMid(a._itemList.Get(b), 1, (a._width / a._textWidth) - 1), a._x + 4, ( (a._y) + (b - 1) * a._listLineHeight) - (a._listOffset * a._listLineHeight), a._width, a._listLineHeight, MIDDLE, 0)
		Next
	End Method
	
End Class

'╔════════════════════════════════════════════════════════════════════════════════════════════════╗
'╚════════════════════════════════════════════════════════════════════════════════════════════════╝
'╔════════════════════════════════════════════════════════════════════════════════════════════════╗
'╚════════════════════════════════════════════════════════════════════════════════════════════════╝
'╔════════════════════════════════════════════════════════════════════════════════════════════════╗
'╚════════════════════════════════════════════════════════════════════════════════════════════════╝

Class GUI_Button Extends GUI_Properties
Private
	Field _arrow:Bool
	Field _arrowType:Int
Public
	'Summary:
	Method Create:Void(x:Int, y:Int, width:Int, text:String = "", height:Int = DEFAULTGADSIZE)
		_x = x
		_y = y
		_width = width
		_height = height
		_id = SetGadgetID
		If text = "" Then _label = "button " + _id Else _label = text
		If text = "@!U" Then _arrow = True; _arrowType = UP
		If text = "@!R" Then _arrow = True; _arrowType = RIGHT
		If text = "@!D" Then _arrow = True; _arrowType = DOWN
		If text = "@!L" Then _arrow = True; _arrowType = LEFT
	End Method
	
	'Summary:
	Method Render:Void()
		If _hidden = True Then Return
		Shape.Button(Self)
	End Method
	
	'Summary:
	Method Update:Bool()
		If _enabled = False or _hidden = True Then Return
		If SelectedGadgetID = 0
			If MouseOverGadget() And MouseHit(MOUSE_LEFT) Then SelectedGadgetID = _id
		ElseIf SelectedGadgetID = _id
			If MouseDown(MOUSE_LEFT) And MouseOverGadget()
				_state = 1
			ElseIf MouseDown(MOUSE_LEFT) And Not MouseOverGadget()
				_state = 0
			ElseIf NOT MouseDown(MOUSE_LEFT) And MouseOverGadget()
				_state = 0
				SelectedGadgetID = 0
				Return True
			ElseIf NOT MouseDown(MOUSE_LEFT) And NOT MouseOverGadget()
				_state = 0
				SelectedGadgetID = 0
				Return False
			EndIf
		EndIf
	End Method
	
End Class

'╔════════════════════════════════════════════════════════════════════════════════════════════════╗
'╚════════════════════════════════════════════════════════════════════════════════════════════════╝

Class GUI_TextBox Extends GUI_Properties
	Private
	Field _visableText:String
	Field _cursorBlink:Int
	Field _cursorTimer:Int
	Field _cursorOffset:Int
	Field _textOffset:Int
	
	'Summary:
	Method EditText:Void()
		If _state = 0 Return
		Repeat
			Local char=GetChar()
			If Not char Exit
			If char = CHAR_BACKSPACE
				If _textOffset = 0 And _cursorOffset = 0 Then Exit
				Local part:String[2]
				part[0] = StrMid(_text, 1, _textOffset + _cursorOffset - 1)
				part[1] = StrMid(_text, _textOffset + _cursorOffset + 1, _text.Length - (_textOffset + _cursorOffset))
				_text = part[0] + part[1]
				_textOffset = _textOffset - 1
				If _textOffset &lt; 0
					_textOffset = 0
					_cursorOffset = _cursorOffset - 1
				End If
			ElseIf char = CHAR_ENTER or char = CHAR_ESCAPE
				SelectedGadgetID = 0
				_state = 0
				_textOffset = 0
				Exit
			ElseIf char = CHAR_UP
			ElseIf char = CHAR_DOWN
			ElseIf char = CHAR_LEFT
				_cursorOffset = _cursorOffset - 1
				If _cursorOffset &lt;= 0
					_cursorOffset = 0
					_textOffset = _textOffset - 1
					If _textOffset &lt;= 0 Then _textOffset = 0
				EndIf
			ElseIf char = CHAR_RIGHT
				_cursorOffset = _cursorOffset + 1
				If _cursorOffset &gt;= _visableText.Length
					_cursorOffset = _visableText.Length
					_textOffset = _textOffset + 1
					If _textOffset &gt;= _text.Length - _visableText.Length Then _textOffset = _text.Length - _visableText.Length
				EndIf
			ElseIf char &gt;= 32
				Local part:String[2]
				part[0] = StrMid(_text, 1, _textOffset + _cursorOffset)
				part[1] = StrMid(_text, _textOffset + _cursorOffset + 1, _text.Length - (_textOffset + _cursorOffset))
				_text = part[0] + String.FromChar(char) + part[1]
				CalcVisableText()
				If _cursorOffset &lt; _visableText.Length
					_cursorOffset = _cursorOffset + 1
				Else
					_textOffset = _textOffset + 1
				EndIf
			EndIf
		Forever
	End Method
	
	'Summary:
	Method DrawCursor:Void()
		If _state = 0 Return
		If _cursorTimer &lt; Millisecs
			_cursorTimer = Millisecs + 200
			If _cursorBlink = 0 Then _cursorBlink = 1 Else _cursorBlink = 0
		End If
		If _cursorBlink = 0 Then Return
		Color.Reset; DrawRect _x + (_textWidth * _cursorOffset) + 4, _y + (_height / 2) - (TextHeight / 2) + 1, _textWidth / 2, TextHeight
	End Method
	
	'Summary:
	Method CalcVisableText:Void()
		_visableText = StrMid(_text, (_textOffset + 1), (_width / _textWidth) - 1)
	End Method
	
	Public
	'Summary:
	Method Create:Void(x:Int, y:Int, width:Int, label:String = "")
		If label.Length &gt; 0 Then _label = label + " "
		_x = x + TextWidth(_label)
		_y = y
		_width = width - TextWidth(_label)
		_height = DEFAULTGADSIZE
		_id = SetGadgetID
	End Method
	
	'Summary:
	Method Render:Void()
		If _hidden = True Then Return
		Shape.TextBox(Self)
		DrawCursor()
	End Method
	
	'Summary:
	Method Update:Bool()
		CalcVisableText()
		If _enabled = False or _hidden = True Then Return
		If SelectedGadgetID = 0
			If MouseOverGadget() And MouseHit() Then SelectedGadgetID = _id
		ElseIf SelectedGadgetID = _id
			If _state = 0
				If _text.Length &gt; _visableText.Length
					_cursorOffset = _visableText.Length
					_textOffset = _text.Length - _visableText.Length
				Else
					_cursorOffset = _text.Length
				EndIf
			EndIf
			_state = 1
			If MouseHit() Then SelectedGadgetID = 0; _state = 0; _textOffset = 0
		EndIf
		EditText()
	End Method
	
	'Summary:
	Method SetText:Void(text:string); _text = text; End Method
	
	'Summary:
	Method GetText:String(); Return _text; End Method
	
	'Summary:
	Method X:Int(); Return _x - TextWidth(_label); End Method
	
	'Summary:
	Method Width:Int(); Return _width + TextWidth(_label); End Method
	
End Class

'╔════════════════════════════════════════════════════════════════════════════════════════════════╗
'╚════════════════════════════════════════════════════════════════════════════════════════════════╝

Class GUI_Spinner Extends GUI_Properties
	
	Private
	Field _cmdAdd:= New GUI_Button
	Field _cmdSub:= New GUI_Button
	Field _increment:Int
	
	Public
	
	'Summary:
	Method Create:Void(x:Int, y:Int, max_width:Int, digit_count:Int = 3, value:Int = 0, increment:Int = 1, label:String = "")
		If label.Length &gt; 0 Then _label = label + " "
		_y = y
		_width = ( (digit_count + 2) * _textWidth)
		If (_width + DEFAULTGADSIZE + TextWidth(_label)) &gt; max_width Then
			_label = StrMid(_label, 1, ( (max_width - (_width + DEFAULTGADSIZE)) / _textWidth) - 1)
			_label = _label + " "
			_x = x + TextWidth(_label)
		Else
			_x = x + TextWidth(_label)
		EndIf
		_height = DEFAULTGADSIZE
		_id = SetGadgetID
		_value = value
		_increment = increment
		_cmdAdd.Create(_x + _width, _y, DEFAULTGADSIZE, "@!U", (DEFAULTGADSIZE / 2))
		_cmdSub.Create(_x + _width, _y + (DEFAULTGADSIZE / 2), DEFAULTGADSIZE, "@!D", (DEFAULTGADSIZE / 2))
	End Method
	
	'Summary:
	Method Render:Void()
		If _hidden = True Then Return
		Shape.Spinner(Self)
		_cmdAdd.Render
		_cmdSub.Render
	End Method
	
	'Summary:
	Method Update:Void()
		If _enabled = False or _hidden = True Then Return
		If _cmdAdd.Update Then _value = _value + _increment
		If _cmdSub.Update Then _value = _value - _increment
	End Method
	
	'Summary:
	Method SetValue:Void(value:Int); _value = value; End Method
	
	'Summary:
	Method GetValue:Int(); Return _value; End Method
	
	'Summary:
	Method SetIncrement:Void(value:Int); _increment = value; End Method
	
	'Summary:
	Method X:Int(); Return _x - TextWidth(_label); End Method
	
	'Summary:
	Method Width:Int(); Return _width + TextWidth(_label) + DEFAULTGADSIZE; End Method
End Class

'╔════════════════════════════════════════════════════════════════════════════════════════════════╗
'╚════════════════════════════════════════════════════════════════════════════════════════════════╝

Class GUI_Slider Extends GUI_Properties
	Private
	Field _cmdAdd:= New GUI_Button
	Field _cmdSub:= New GUI_Button
	Field _maxValue:Int
	Field _vertBar:Bool
	Field _knobSize:Int = DEFAULTGADSIZE
	Field _increment:Int
	
	Public
	
	'Summary:
	Method Create:Void(x:Int, y:Int, length:Int, maxValue:Int, verticalBar:Bool = True)
		_id = SetGadgetID
		_maxValue = maxValue
		If verticalBar = True
			_width = DEFAULTGADSIZE
			_height = length - DEFAULTGADSIZE
			_cmdAdd.Create(x, y, DEFAULTGADSIZE, "@!U", DEFAULTGADSIZE)
			_cmdSub.Create(x, y + _height, DEFAULTGADSIZE, "@!D", DEFAULTGADSIZE)
			_height = length - (DEFAULTGADSIZE * 2)
			_y = y + DEFAULTGADSIZE
			_x = x
			_increment = ( (Float(_height) - Float(DEFAULTGADSIZE)) / Float(_maxValue))
			_vertBar = True
		Else
			_width = length - DEFAULTGADSIZE
			_height = DEFAULTGADSIZE
			_cmdSub.Create(x + _width, y, DEFAULTGADSIZE, "@!R", DEFAULTGADSIZE)
			_cmdAdd.Create(x, y, DEFAULTGADSIZE, "@!L", DEFAULTGADSIZE)
			_width = length - (DEFAULTGADSIZE * 2)
			_y = y
			_x = x + DEFAULTGADSIZE
			_increment = ( (_width - DEFAULTGADSIZE) / _maxValue)
			_vertBar = False
		End If		
	End Method
	
	'Summary:
	Method Render:Void()
		If _hidden = True Then Return
		Shape.Slider(Self)
		_cmdAdd.Render
		_cmdSub.Render
	End Method
	
	'Summary:
	Method Update:Void()
		If _enabled = False or _hidden = True Then Return
		If _cmdSub.Update And _value &lt; _maxValue
			_value = _value + 1
			Return
		EndIf
		If _cmdAdd.Update And _value &gt; 0
			_value = _value - 1
			Return
		EndIf		
		If SelectedGadgetID = 0
			If MouseOverGadget() And MouseHit(MOUSE_LEFT) Then SelectedGadgetID = _id
		ElseIf SelectedGadgetID = _id
			If MouseDown(MOUSE_LEFT) And _increment &gt; 0
				If _vertBar
					_value = (MouseY() - (_y + (DEFAULTGADSIZE * 0.5))) / _increment
				Else
					_value = (MouseX() - (_x + (DEFAULTGADSIZE * 0.5))) / _increment
				EndIf
				If _value &lt; 0 Then _value = 0
				If _value &gt; _maxValue Then _value = _maxValue
			ElseIf Not MouseDown(MOUSE_LEFT)
				SelectedGadgetID = 0
			EndIf
		EndIf
	
	End Method
	
	'Summary:
	Method GetValue:Int(); Return _value; End Method
	
	'Summary:
	Method SetValue:Void(value:Int)
		_value = value
		If _value &lt; 0 Then value = 0
		If _value &gt; _maxValue Then _value = _maxValue	
	End Method
	
	'Summary:
	Method X:Int()
		If _vertBar Then Return _x Else Return _x - DEFAULTGADSIZE
	End Method
	
	'Summary:
	Method Y:Int()
		If _vertBar Then Return _y - DEFAULTGADSIZE Else Return _y
	End Method
	
	'Summary:
	Method Width:Int()
		If _vertBar Then Return DEFAULTGADSIZE Else Return _width + (DEFAULTGADSIZE * 2)
	End Method
	
	'Summary:
	Method Height:Int()
		If _vertBar Then Return _height + (DEFAULTGADSIZE * 2) Else Return DEFAULTGADSIZE
	End Method
		
End Class

'╔════════════════════════════════════════════════════════════════════════════════════════════════╗
'╚════════════════════════════════════════════════════════════════════════════════════════════════╝

Class GUI_DropDown Extends GUI_Properties
	Private
	Field _itemList:= New IntMap&lt;String&gt;
	Field _pressed:Bool
	Field _listLineHeight:Int = 18
	Field _showListAbove:Bool
	Public

	'Summary:
	Method Create:Void(x:Int, y:Int, width:Int)
		_x = x
		_y = y
		_width = width
		_height = DEFAULTGADSIZE
		_id = SetGadgetID
	End Method
	
	'Summary:
	Method Render:Void()
		If _hidden = True Then Return
		Shape.DropDown(Self, 0)
		If _pressed Then Shape.DropDown(Self, 1)
	End Method
	
	'Summary:
	Method Update:Bool()
		If _enabled = False or _hidden = True Then Return
		If SelectedGadgetID = 0
			If MouseOverGadget() And MouseHit(MOUSE_LEFT) Then SelectedGadgetID = _id
		ElseIf SelectedGadgetID = _id
			If MouseDown(MOUSE_LEFT) And MouseOverGadget()
				_pressed = True
				_showListAbove = False
				If (_y + _height) + (_itemList.Count * _listLineHeight) &gt; DeviceHeight Then _showListAbove = True
			ElseIf NOT MouseDown(MOUSE_LEFT)
				SelectedGadgetID = 0
				_pressed = False
			Else
				If _showListAbove
					_index = ( (MouseY() -_y) + (_itemList.Count * _listLineHeight)) / _listLineHeight
				Else
					_index = (MouseY() - (_y + _height)) / _listLineHeight
				EndIf
				If _index &lt; 0 Then _index = 0
				If _index &gt;= _itemList.Count Then _index = (_itemList.Count - 1)
			EndIf
		EndIf
	End Method
	
	'Summary:
	Method AddItem:Void(text:String)
		_itemList.Add(_itemList.Count + 1, text)
	End Method
	
	'Summary:
	Method ReplaceItem:Void(index:Int, new_text:String)
		If index &gt; (_itemList.Count - 1) Or index &lt; 0 Then Print "!ReplaceItemERROR"; Return
		_itemList.Set(index + 1, new_text)
	End Method
	
	'Summary:
	Method RemoveItem:Void(index:Int)
		If index &gt; (_itemList.Count - 1) Or index &lt; 0 Then Print "!RemoveItemERROR"; Return
		Local list:String[_itemList.Count]
		Local count:Int
		For Local a:Int = 0 Until list.Length
			If a &lt;&gt; index
				 list[count] = _itemList.Get(a + 1)
				 count = count + 1
			End If
		Next
		_itemList.Clear
		For Local a:Int = 0 Until list.Length - 1
			AddItem(list[a])
		Next
		If _index &gt; (_itemList.Count - 1) Then SetIndex(0)
	End Method
	
	'Summary:
	Method RemoveAll:Void()
		_itemList.Clear
		SetIndex(0)
	End Method
	
	'Summary:
	Method SetIndex:Void(index); _index = index; End Method
	
	'Summary:
	Method GetIndex:Int(); Return _index; End Method
	
	'Summary:
	Method GetText:String(); Return _itemList.Get(_index + 1); End Method
	
End Class

'╔════════════════════════════════════════════════════════════════════════════════════════════════╗
'╚════════════════════════════════════════════════════════════════════════════════════════════════╝

Class GUI_CheckBox Extends GUI_Properties
	
	'Summary:
	Method Create:Void(x:Int, y:Int, label:String = "", width:Int = DEFAULTGADSIZE)
		If label.Length &gt; 0
			_label = StrMid(label, 1, ( (width - DEFAULTGADSIZE) / _textWidth) - 1)
		EndIf
		If width &lt;= (DEFAULTGADSIZE*2) Then _label = ""
		_x = x
		_y = y
		_width = DEFAULTGADSIZE
		_height = DEFAULTGADSIZE
		_id = SetGadgetID
	End Method
	
	'Summary:
	Method Render:Void()
		If _hidden = True Then Return
		Shape.CheckBox(Self)
	End Method
	
	'Summary:
	Method Update:Bool()
		If _enabled = False or _hidden = True Then Return
		If SelectedGadgetID = 0
			If MouseOverGadget() And MouseHit(MOUSE_LEFT)
				If _state = 0 Then _state = 1 Else _state = 0
			EndIf
		EndIf
	End Method
	
	'Summary:
	Method SetState:Void(state:Int); _state = state; End Method
	
	'Summary:
	Method GetState:Int(); Return _state; End Method
	
	'Summary:
	Method X:Int(); Return _x; End Method
	
	'Summary:
	Method Width:Int()
		If _label.Length = 0 Return _width
		Return _width + TextWidth(_label) + _textWidth + 4
	End Method
End Class

'╔════════════════════════════════════════════════════════════════════════════════════════════════╗
'╚════════════════════════════════════════════════════════════════════════════════════════════════╝

Class GUI_ListBox Extends GUI_Properties
	Private
	Field _itemList:= New IntMap&lt;String&gt;
	Field _listLineHeight:Int = 18
	Field _slider:= New GUI_Slider
	Field _visableItemCount:Int
	Field _listOffset:Int
	Field _selectedIndex:Int
	Public
	
	
	'Summary:
	Method Create:Void(x:Int, y:Int, width:Int, height:Int)
		_x = x
		_y = y
		_width = width - DEFAULTGADSIZE
		_height = height
		_id = SetGadgetID
		_slider.Create(_x + _width, _y, _height, 0)
	End Method
	
	'Summary:
	Method Render:Void()
		If _hidden = True Then Return
		Shape.ListBox(Self)
		_slider.Render
	End Method
	
	'Summary:
	Method Update:Bool()
		If _enabled = False or _hidden = True Then Return
		If SelectedGadgetID = 0
			If MouseOverGadget() And MouseHit(MOUSE_LEFT)
				_index = ( (MouseY() - (_y)) / _listLineHeight)
				If _index &lt; 0 Then _index = 0
				If _index &gt;= _visableItemCount Then _index = _visableItemCount - 1
				If _index &gt;= _itemList.Count Then _index = _itemList.Count - 1
				_selectedIndex = _index + _listOffset
			EndIf
		EndIf
		_slider.Update
		_listOffset = _slider.GetValue
	End Method
	
	'Summary:
	Method AddItem:Void(text:String)
		_itemList.Add(_itemList.Count + 1, text)
		_visableItemCount = _height / _listLineHeight
		_slider.Create(_x + _width, _y, _height, _itemList.Count - _visableItemCount)
		_listOffset = 0
	End Method
	
	'Summary:
	Method ReplaceItem:Void(index:Int, new_text:String)
		If index &gt; (_itemList.Count - 1) Or index &lt; 0 Then Print "!ReplaceItemERROR"; Return
		_itemList.Set(index + 1, new_text)
	End Method
	
	'Summary:
	Method RemoveAll:Void()
		_itemList.Clear
		_index = 0
		_selectedIndex = 0
		_visableItemCount = 0
		_listOffset = 0
		_slider.Create(_x + _width, _y, _height, 0)
		_slider.SetValue(0)
	End Method
	
	'Summary:
	Method RemoveItem:Void(index:Int)
		If index &gt; (_itemList.Count - 1) Or index &lt; 0 Then Print "!RemoveItemERROR"; Return
		Local list:String[_itemList.Count]
		Local count:Int
		For Local a:Int = 0 Until list.Length
			If a &lt;&gt; index
				list[count] = _itemList.Get(a + 1)
				count = count + 1
			End If
		Next
		_itemList.Clear
		For Local a:Int = 0 Until list.Length - 1
			AddItem(list[a])
		Next
		_index = 0
		_selectedIndex = 0
		_listOffset = 0
		If _itemList.Count - _visableItemCount &lt;= 0 Then _slider.Create(_x + _width, _y, _height, 0)
		_slider.SetValue(0)
	End Method
	
	'Summary:
	Method GetIndex:Int(); Return _selectedIndex; End Method
	
	'Summary:
	Method GetText:String(); Return _itemList.Get(_selectedIndex + 1); End Method
	
	'Summary:
	Method X:Int(); Return _x; End Method
	
	'Summary:
	Method Width:Int() Return _width + DEFAULTGADSIZE; End Method
End Class

'╔════════════════════════════════════════════════════════════════════════════════════════════════╗
'╚════════════════════════════════════════════════════════════════════════════════════════════════╝

Class GUI_Label Extends GUI_Properties
	Method Create:Void(x:Int, y:Int, width:Int, text:String = "")
		_x = x
		_y = y
		_width = width
		_height = DEFAULTGADSIZE
		_id = SetGadgetID
		_text = text
	End Method
	
	'Summary:
	Method Render:Void()
		If _hidden = True Then Return
		Shape.DrawLabel(StrMid(_text, 1, _width / _textWidth), _x, _y, _width, _height)
	End Method
	
	'Summary:
	Method SetText:Void(text:String); _text = text; End Method
	
	'Summary:
	Method GetText:String(); Return _text; End Method

End Class
</textarea><br><br>Working example<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import mojo
Import gui

Global but1:= New GUI_Button
Global but2:= New GUI_Button
Global txt1:= New GUI_TextBox
Global txt2:= New GUI_TextBox
Global spn1:= New GUI_Spinner
Global sld1:= New GUI_Slider
Global sld2:= New GUI_Slider
Global drp1:= New GUI_DropDown
Global drp2:= New GUI_DropDown
Global chk1:= New GUI_CheckBox
Global lst1:= New GUI_ListBox
Global lab1:= New GUI_Label
        
'╔════════════════════════════════════════════════════════════════════════════════════════════════╗
'   Main :    Main program loop                             
'╚════════════════════════════════════════════════════════════════════════════════════════════════╝
Function Main(); New Game(); End
Class Game Extends App
	
	'Summary: Main CREATE method -----------------------------------------------------------------
	Method OnCreate:Int()
		but1.Create(50, 50, 100, "Remove")
		but2.Create(50, 150, 200, "Add item")
		txt1.Create(50, 200, 200, "Text box label two"); txt1.SetText("hello world")
		txt2.Create(50, 250, 200, ""); txt2.SetText("SOME TEXT HERE")
		spn1.Create(50, 300, 200, 3, 0, 5, "this is a spinner gadget")
		sld1.Create(10, 50, 200, 10)
		sld2.Create(50, 350, 200, 10, False)
		drp1.Create(50, 10, 100)
		drp1.AddItem("item 1 dsddasadsa dsa")
		drp1.AddItem("item 2")
		drp1.AddItem("item 3")
		drp2.Create(50, 400, 100)
		drp2.AddItem("item 1 dsddasadsa dsa")
		drp2.AddItem("item 2")
		drp2.AddItem("item 3")
		chk1.Create(50, 100, "this is a check box howdoesit look?", 200)
		chk1.SetState(1)
		lst1.Create(300, 100, 200, 290)
		lst1.AddItem("l item 1")
		lst1.AddItem("l item 2")
		lst1.AddItem("l item 3")
		lst1.AddItem("l item 4")
		lst1.AddItem("l item 5")
		lst1.AddItem("l item 6")
		lst1.AddItem("l item 7")
		lst1.AddItem("l item 8")
		lst1.AddItem("l item 9")
		lst1.AddItem("l item 10")
		lst1.AddItem("l item 11")
		lst1.AddItem("l item 12")
		lst1.AddItem("l item 13")
		lab1.Create(but2.X, but2.Y + 30, 200, "this is some")
	End Method
	
	'Summary: Main UPDATE method -----------------------------------------------------------------
	Method OnUpdate:Int()	
		If but1.Update Then Print "but 1"; lst1.RemoveItem(lst1.GetIndex)
		If but2.Update Then Print "but 2"; lst1.AddItem("item " + Millisecs)
		If KeyHit(KEY_SPACE) lst1.RemoveAll
		txt1.Update
		txt2.Update
		spn1.Update
		sld1.Update
		sld2.Update
		drp1.Update
		drp2.Update
		lab1.SetText(drp2.GetText)
		chk1.Update
		txt1.SetText(drp1.GetIndex + " " + drp1.GetText)
		lst1.Update
	End Method

	'Summary: Main RENDER method -----------------------------------------------------------------
	Method OnRender:Int()
		Cls(155,155,155)
		but1.Render
		but2.Render
		txt1.Render
		txt2.Render
		spn1.Render
		sld1.Render
		sld2.Render
		drp1.Render
		drp2.Render
		chk1.Render
		lst1.Render
		lab1.Render
	End Method
	
End Class
</textarea> <br><br></td></tr></table><br>
<a name="2102021"></a>

<a name="2102023"></a>

<a name="2102028"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GC-Martijn</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> going to check that tomorrow !<br>edit: Yep the code is looking better, and now with real sizes (instead of -2 or +2 etc) ;) <br><br></td></tr></table><br>
<a name="2102076"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Supertino</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Still not as featured as I'd like it, but each version should get better. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
