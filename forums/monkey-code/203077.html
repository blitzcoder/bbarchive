<!DOCTYPE html><html lang="en" ><head ><title >Simple INI Reader and Writer</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Simple INI Reader and Writer</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Simple INI Reader and Writer</a><br><br>
<a name="2065152"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Goodlookinguy</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> <b>Edit 3/5/13:</b> I'm not sure what I was talking about in the below edit...but I hope I haven't made any changes to it since this...I think.<br><b>Edit 7/13/12:</b> I'm reworking several parts of this at the moment. The next stable version of it will be between 7/13-7/21 probably.<br><br>I ported a BlitzMAX INI reader and writer for some of my stuff. Decided it didn't have enough features and added some. I figured it was worth sharing since there weren't any other INI type file readers.<br><br>I'm heading to bed at the time of this posting, so I'll just leave this list of methods, functions, and constants until I come back. When I come back later, I'll document and explain anything that needs to be explained.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">#Rem
	Title: INI file reader and writer
	Original Author: JoshK
	Monkey Port and Additions/Optimizations/Changes:  Nicholas Grant
	Date: 6/20/2012 rev2
	Description: INI property reader and writer
	Original Code: <a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=2814" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=2814</a>
#End

Class Ini
Private
	Field _Map:StringMap&lt;StringMap&lt;String&gt;&gt; = New StringMap&lt;StringMap&lt;String&gt;&gt;()
	Field _UsingQuotes:Bool
	
Public
	
	Method New( useQuotes:Bool = AcceptQuotes )
		_UsingQuotes = useQuotes
	End
	
	
	
	Method ContainsKey:Bool( key:String, section:String = DefaultSection )
		If _Map.Contains(section) Then Return _Map.Get(section).Contains(key)
		Return False
	End
	
	Method ContainsSection:Bool( section:String = DefaultSection )
		Return _Map.Contains(section)
	End
	
	
	
	Method Add:Void( key:String, value:String, section:String = DefaultSection )
		Local subMap:StringMap&lt;String&gt;
		
		If Not _Map.Contains(section) Then _Map.Set(section, New StringMap&lt;String&gt;())
		
		subMap = _Map.Get(section)
		subMap.Add(key, value)
	End
	
	Method Set:Void( key:String, value:String, section:String = DefaultSection )
		Local subMap:StringMap&lt;String&gt;
		
		If Not _Map.Contains(section) Then _Map.Set(section, New StringMap&lt;String&gt;())
		
		subMap = _Map.Get(section)
		subMap.Set(key, value)
	End
	
	Method Get:String( key:String, section:String = DefaultSection, defaultValue:String = "" )
		Local subMap:StringMap&lt;String&gt;
		
		If _Map.Contains(section)
			subMap = _Map.Get(section)
			If Not subMap.Contains(key) Then Return defaultValue
			Return subMap.Get(key)
		End
		
		Return defaultValue
	End
	
	Method GetInt:Int( key:String, section:String = DefaultSection, defaultValue:Int = 0 )
		Local value:String = Get(key, section)
		If value &lt;&gt; "" Then Return Int(value)
		Return defaultValue
	End
	
	Method GetFloat:Float( key:String, section:String = DefaultSection, defaultValue:Float = 0.0 )
		Local value:String = Get(key, section)
		If value &lt;&gt; "" Then Return Float(value)
		Return defaultValue
	End
	
	Method GetIntArray:Int[]( key:String, section:String = DefaultSection, defaultValue:Int[] = [] )
		Local intValues:Int[]
		Local values:String[] = Get(key, section).Split(",")
		
		If values.Length() &lt;= 0 Then Return defaultValue
		
		intValues = New Int[values.Length()]
		
		For Local index:Int = 0 Until values.Length()
			intValues[index] = Int(values[index].Trim())
		Next
		
		Return intValues
	End
	
	Method GetFloatArray:Float[]( key:String, section:String = DefaultSection, defaultValue:Float[] = [] )
		Local floatValues:Float[]
		Local values:String[] = Get(key, section).Split(",")
		
		If values.Length() &lt;= 0 Then Return defaultValue
		
		floatValues = New Float[values.Length()]
		
		For Local index:Int = 0 Until values.Length()
			floatValues[index] = Float(values[index].Trim())
		Next
		
		Return floatValues
	End
	
	
	
	Method GetAsFile:String() ' Use only when needed, as this has massive string concatination
		Local section:String
		Local subMap:StringMap&lt;String&gt;
		Local key:String
		Local data:String = ""
		
		For section = EachIn _Map.Keys()
			subMap = _Map.Get(section)
			
			If Not subMap.IsEmpty()
				data += "[" + section + "]~n"
				
				For key = EachIn subMap.Keys()
					If _UsingQuotes
						data += key + "=~q" + subMap.Get(key) + "~q~n"
					Else
						data += key + "=" + subMap.Get(key) + "~n"
					End
				Next
			End
		Next
		
		Return data
	End
	
	Function Load:Ini( data:String, quoteUsage:Bool = AcceptQuotes )
		Local ini:Ini = New Ini(quoteUsage)
		Local enclosure:Int
		Local lines:String[]
		Local section:String
		Local key:String
		Local value:String
		Local delimiterIndex:Int = -1
		
		lines = data.Split("~n")
		
		For Local line:String = EachIn lines
			line = line.Trim()
			
			If line.Length() &gt; 0
				If line[0] = 59 Then Continue ' ;
				
				If line[0] = 91 And line[line.Length() - 1] = 93 ' [ and ]
					section = line[1..-1]
				Else
					delimiterIndex = line.Find("=")
					
					If delimiterIndex &gt; 0
						key = line[..delimiterIndex].Trim()
						value = line[delimiterIndex + 1..].Trim()
						
						If value.Length() &gt; 0 And key.Length() &gt; 0
							If quoteUsage
								Local leftEnclosure:Int = value[0]
								Local rightEnclosure:Int = value[value.Length() - 1]
								
								If leftEnclosure = rightEnclosure And (leftEnclosure = 39 Or leftEnclosure = 34)
									value = value[1..-1]
								End
							End
							
							ini.Set(key, value, section)
						End
					End
				End
			End
		Next
		
		Return ini
	End
	
	Function Create:Ini( useQuotes:Bool = UseQuotes )
		Return New Ini(useQuotes)
	End
	
	
	
	Method GetSections:String[]()
		Local index:Int = 0
		Local sections:String[] = New String[_Map.Count()]
		
		For Local section:String = EachIn _Map.Keys()
			sections[index] = section
			index += 1
		Next
		
		Return sections
	End
	
	Method GetKeys:String[]( section:String )
		Local index:Int = 0
		Local keys:String[]
		
		If _Map.Contains(section)
			keys = New String[_Map.Get(section).Count()]
			
			For Local key:String = EachIn _Map.Get(section).Keys()
				keys[index] = key
				index += 1
			Next
		End
		
		Return keys
	End
	
	
	
	Method Merge:Void( data:String, mergeMode:Bool = IgnoreSetKeys, useQuotes:Bool = AcceptQuotes )
		Local ini:Ini = Ini.Load(data, useQuotes)
		
		Local sections:String[] = ini.GetSections()
	
		For Local section:String = EachIn sections
			For Local key:String = EachIn ini.GetKeys(section)
				If mergeMode
					Add(key, ini.Get(key, section), section)
				Else
					Set(key, ini.Get(key, section), section)
				End
			Next
		Next
	End
	
	Method Merge:Void( ini:Ini, mergeMode:Bool = IgnoreSetKeys )
		Local sections:String[] = ini.GetSections()
	
		For Local section:String = EachIn sections
			For Local key:String = EachIn ini.GetKeys(section)
				If mergeMode
					Add(key, ini.Get(key, section), section)
				Else
					Set(key, ini.Get(key, section), section)
				End
			Next
		Next
	End
	
	
	
	
	' Default section
	Const DefaultSection:String = "ini"
	
	' Quote mode
	Const AcceptQuotes:Bool = True
	Const IgnoreQuotes:Bool = False
	
		' Legacy quote mode constants
		Const UseQuotes:Bool = True
		Const NoQuotes:Bool = False
	
	' Merge mode
	Const IgnoreSetKeys:Bool = True
	Const OverwriteKeys:Bool = False
End
</textarea><br><br><br>Test<br>[monkeycode]Import cIni<br><br>Function Main:Int()<br>	Local iniData:String = "[Data]~nValue 1 = 123456789~nValue 2 = 9.123456~nValue 3 = 15, 7.5, 3.75"<br>	Local ini:Ini = Ini.Load(iniData)<br>	<br>	Print ini.Get("Value 1", "Data") + ini.Get("Value 2", "Data")<br>	Print String(ini.GetInt("Value 1", "Data"))<br>	Print String(ini.GetFloat("Value 1", "Data"))<br>	Print String(ini.GetFloat("Value 2", "Data"))<br>	Print String(ini.GetInt("Value 2", "Data"))<br>	Print String(ini.GetInt("Value 1", "Data") + ini.GetFloat("Value 2", "Data"))<br>	Print ini.Get("Value 3", "Data")<br>	Print "==================================="<br>	<br>	Local intArray:Int[] = ini.GetIntArray("Value 3", "Data")<br>	Local floatArray:Float[] = ini.GetFloatArray("Value 3", "Data")<br>	<br>	For Local intVal:Int = EachIn intArray<br>		Print "Int -&gt; " + String(intVal)<br>	Next<br>	<br>	For Local floatVal:Float = EachIn floatArray<br>		Print "Flt -&gt; " + String(floatVal)<br>	Next<br>	<br>	Return 0<br>End[/monkeycode]<br><br><br><b>Methods, Functions, and Constants</b><br><br>[monkeycode]'- Methods<br> * ContainsKey:Bool( key:String, section:String )<br> * ContainsSection:Bool( section:String )<br> * Add:Void( key:String, value:String, section:String )<br> * Set:Void( key:String, value:String, section:String )<br> * Get:String( key:String, section:String, defaultValue:String )<br> * GetInt:Int( key:String, section:String, defaultValue:Int )<br> * GetFloat:Float( key:String, section:String, defaultValue:Float )<br> * GetIntArray:Int[]( key:String, section:String, defaultValue:Int[] )<br> * GetFloatArray:Float[]( key:String, section:String, defaultValue:Float[] )<br> * GetAsFile:String()<br> * GetSections:String[]()<br> * GetKeys:String[]( section:String )<br> * Merge:Void( data:String, mergeMode:Bool, useQuotes:Bool )<br> * Merge:Void( ini:Ini, mergeMode:Bool )<br><br>'- Functions<br> * Load:Ini( data:String, useQuotes:Bool )<br> * Create:Ini( useQuotes:Bool )<br><br>'- Consts<br> * DefaultSection:String<br>'-- How to handle quote reading<br> * AcceptQuotes:Bool '&lt;-- Take only the contents wrapped in the quotes if in quotes<br> * IgnoreQuotes:Bool '&lt;-- Take everything including quotes at beginning and end<br> * <strike>UseQuotes:Bool</strike><br> * <strike>NoQuotes:Bool</strike><br>'-- Merge Modes<br> * IgnoreSetKeys:Bool<br> * OverwriteKeys:Bool[/monkeycode] <br><br></td></tr></table><br>
<a name="2050795"></a>

<a name="2050796"></a>

<a name="2050797"></a>

<a name="2033494"></a>

<a name="2031584"></a>

<a name="2031581"></a>

<a name="2031582"></a>

<a name="2031583"></a>

<a name="2031579"></a>

<a name="2031580"></a>

<a name="2031519"></a>

<a name="2031552"></a>

<a name="2031533"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice to share.<br>I wrote a similar thing for Bmax (years ago): I will add something about 'arrays-keys' (like KEYS=1,2,3) for multiple keys-value. <br><br></td></tr></table><br>
<a name="2125362"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >[VOLOFORCE]</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am sorry... but how exactly does this work? <br><br>Like say, I want to read an INI-file.<br><br>In which directory do I have to put it?<br><br>Then, how do I save? Or create a new one? <br><br></td></tr></table><br>
<a name="2125934"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Goodlookinguy</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> This reads and writes INI files in memory. You have to handle the saving and loading since there are numerous systems people have designed for virtually saving data and loading it. <br><br></td></tr></table><br>
<a name="2126236"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >[VOLOFORCE]</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oh, ok. Now I see, thanks. I was just confused by this. Mostly use JSON but INI can be handy. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
