<!DOCTYPE html><html lang="en" ><head ><title >Basic Pang remake (old Amiga Game)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Basic Pang remake (old Amiga Game)</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Basic Pang remake (old Amiga Game)</a><br><br>
<a name="2097852"></a>

<a name="2097851"></a>

<a name="2097773"></a>

<a name="2125434"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pakz</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I remade a little bit of the video game Pang. You can not die and the game does not end. Space to shoot rope to pop balls. Balls turn into smaller balls unless they are to small.<br><br>Here a screenshot.<br><br><img src="http://i57.tinypic.com/iwkv86.jpg"><br><br><iframe width="560" height="345" src="http://www.youtube.com/embed/UodQ82-_RTI" frameborder="0" allowfullscreen></iframe><br><br>Here the code :<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import mojo

Class ball
	Field x:Float
	Field y:Float
	Field radius:Float = 32
	Field ballbounce:Float
	Field direction:String
	Field incy:Float=0
	Field delete:Bool=False
	Method New(_x:Int,_y:Int,_direction:String,_radius:Int,_ballbounce:Float)
		x = _x
		y = _y
		radius = _radius
		ballbounce = _ballbounce
		direction = _direction
	End Method
	Method update()
		If incy &gt;= 0 Then 
			If incy &lt; Abs(ballbounce) Then incy+=.1
			For Local i=0 Until incy
				y+=0.5
				If y+radius &gt; 288 Then incy=ballbounce
			Next
		End If
		If incy &lt; 0 Then
			incy+=.1
			For Local i=0 Until Abs(incy)
				y-=0.5				
			Next
		End If
		If direction = "left"
			x-=1
			If x-radius&lt;=0 Then direction="right"
		Else
			x+=1
			If x+radius&gt;=640 Then direction="left"
		End If
		For Local i:=Eachin r
			If circlerectcollide(x,y,radius,i.x,i.y-i.height,i.width,i.height) = True
				r.Remove i
				delete = True
				Select radius
					Case 32
						b.AddLast(New ball(x,y,"left",20,-6))
						b.AddLast(New ball(x+20,y,"right",20,-6))
					Case 20
						b.AddLast(New ball(x,y,"left",10,-5))
						b.AddLast(New ball(x+10,y,"right",10,-5))
					Case 10
						b.AddLast(New ball(x,y,"left",6,-4))
						b.AddLast(New ball(x+6,y,"right",6,-4))					
				End Select
			End If
		Next
		For Local i:=Eachin b
			If i.delete = True Then
				b.Remove i
			End If
		Next
	End Method
	Method draw()
		SetColor 255,0,0
		DrawCircle x,y,radius
	End Method
End Class

Class rope
	Field x:Float
	Field y:Float
	Field height:Int
	Field width
	Field incy:Float=5
	Field state:String="Nothing"
	Method New(_x:Int,_y:Int)
		x = _x
		y = _y
		state="expanding"
		height=0
		width=8
	End Method
	Method draw()
		SetColor 255,255,0
		DrawRect x,y-height,width,height
	End Method
	Method update()
		Select state
			Case "expanding"
				For Local i=0 Until incy
					height+=1
					If y-height &lt; 0 Then 
						state = "remove"
						Exit
					End If
				Next
			Case "remove"
				For	Local i:=Eachin r
					If i.state = "remove" Then r.Remove i
				Next
		End Select
	End Method
End Class

Class player
	Field x:Float = 640/2
	Field y:Float = 480/2
	Field width:Int = 32
	Field height:Int = 48
	Field wait:Bool=False
	Method update()
		If KeyDown(KEY_RIGHT)
			For Local i=0 Until 3
				If x+width &lt; 640
					x+=1
				End If
			Next
		End If
		If KeyDown(KEY_LEFT)
			For Local i=0 Until 3
				If x &gt; 0
					x-=1
				End If
			Next
		End If
		If KeyHit(KEY_SPACE)
			r.AddFirst(New rope(x+12,y+48))
		End If
	End Method
	Method draw()
		SetColor 255,255,255
		DrawRect x,y,width,height		
	End Method
End Class

Global r:List&lt;rope&gt; = New List&lt;rope&gt;
Global b:List&lt;ball&gt; = New List&lt;ball&gt;
Global p:player = New player

Class MyGame Extends App

    Method OnCreate()
        SetUpdateRate(60)
        b.AddFirst(New ball(100,100,"left",32,-8))
    End Method
    Method OnUpdate()        
        For Local i:=Eachin r
        	i.update
        Next
        For Local i:=Eachin b
        	i.update
        Next
        If b.IsEmpty() = True Then
			b.AddLast(New ball(100,100,"left",32,-8))
        End If
    	p.update
    End Method
    Method OnRender()
        Cls 0,0,0 
        SetColor 255,255,255
        For Local i:=Eachin b
        	i.draw
        Next
        For Local i:=Eachin r
        	i.draw
        Next
        SetColor 255,255,255
        DrawText "Use cursor left/right to move, space to shoot rope.",0,0
        DrawRect 0,288,640,480-288
        p.draw
    End Method
End Class


Function circlerectcollide:Bool(cx:Int,cy:Int,cr:Int, rx:Int,ry:Int,rw:Int,rh:Int)
    Local closestx:Float = Clamp(cx, rx, rx+rw)
    Local closesty:Float = Clamp(cy, ry, ry+rh)
    Local distancex :Float = cx - closestx
    Local distancey:Float = cy - closesty
    Local distancesquared:Float = (distancex * distancex) + (distancey * distancey)
    Return distancesquared &lt; (cr * cr)
End Function

Function Main()
    New MyGame()
End Function
</textarea> <br><br></td></tr></table><br>
<a name="2097840"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Steve Ancell</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> The Caffeine Kid made a version of the some years back, it's a fun game. I look forward to playing your version. <br><br></td></tr></table><br>
<a name="2097841"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice example and very clean code.<br>I noticed there's no 'delta' code for the balls,rope and player movement (and to be honest it plays very smooth on HTML5-Firefox...) <br><br></td></tr></table><br>
<a name="2097893"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Steve Ancell</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> So will you be turning this into a full-blown game? <br><br></td></tr></table><br>
<a name="2097895"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pakz</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am not planning on making this game more complete anytime soon. I have other things that I am working on. Though if it gets lots of hits then I might continue with it. There are already more verslons of it by other people that can be played.<br>I also Need to study the game more and learn more to be able to make it. The complete game is not that easy to make. <br><br></td></tr></table><br>
<a name="2098061"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hotshot</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Be careful with Game Remake because someone made smurf game remake clone on Amiga and got sued by smurf company(I dont know if they are real company or it is scam company!) <br><br></td></tr></table><br>
<a name="2098070"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pakz</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> There are a lot of remakes for Pang (google images) so I am not that worried. I will not remake anything from the Smurfs however. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
