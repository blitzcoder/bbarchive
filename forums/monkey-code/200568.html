<!DOCTYPE html><html lang="en" ><head ><title >Vector Class</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Vector Class</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Vector Class</a><br><br>
<a name="2004319"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tibit</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sharing my Vector2D class. ´<br><br>I use this in almost every single little thing I do in Monkey.<br><br>Very tested, very useful.<br><br>Feel free to use it for free for whatever you want - use,share,improve,extend,bundle,and so on. It's even OK if you want to bundle this with a framework.<br><br><pre class=code>
Strict

' A vector class with a lot of handy functionality for game-programming! 

' In simplicy a vector is an Arrow. It has an length
' and a direction. You can use these properties to 
' make the vector model a Position, a Velocity, a 
' Line, a Distance and many other things that can be
' measured using a length and a direction or anything
' that uses two float pair-values!

' Wikipedia: A vector is what is needed to "carry" the
' point A to the point B; the Latin word vector means "one who carries"
Class Vector
Public
	Field X:Float
	Field Y:Float 

	Const Zero:Vector = New Vector(0,0)
	
	Method New(x:Float=0,y:Float=0)
		X = x
		Y = y
	End
	
	'	    S E T
	'----------------------------------------------	
	Method Set:Vector( vector:Vector ) 
		X = vector.X 
		Y = vector.Y 
		Return Self	
	End
	Method Set:Vector(x:Float,y:Float ) 
		X = x
		Y = y
		Return Self	
	End
		
	'		A D D
	'----------------------------------------------	
	Method Add:Vector( vector:Vector ) 
		X += vector.X 
		Y += vector.Y 
		Return Self	
	End
	
	'		S U B T R A C T
	'----------------------------------------------	
	Method Subtract:Vector( vector:Vector ) 
		X = X - vector.X 
		Y = Y - vector.Y 
		Return Self	
	End
	
	'		 D O T  P R O D U C T 
	'---------------------------------------------------
	' Dot Product which is X*X2 + Y*Y2
	'
	'---------------------------------------------------
	Method Dot:Float( Vector:Vector )
		Return ( X * Vector.X + Y * Vector.Y)
	End
	
		'		M U L T I P L Y   V E C T O R 
	'----------------------------------------------
	Method Multiply:Vector( Value:Float )
		X*=Value
		Y*=Value
		Return Self	
	End
		
	' M I R R O R
	'----------------------------------------------------------
	' if the mirrorImage vector is a unit-vector this will 
	' make this vector flip 180 degrees around it
	' So that this vector now points in the exact opppisite 
	' direction To the mirrorImage vector
	' Returns Self, which will be opposite to mirrorImage.
	Method Mirror:Vector( mirrorImage:Vector )
		Local Dotprod# = -X * mirrorImage.X - Y * mirrorImage.Y
		X=X+2 * mirrorImage.X * dotprod
		Y=Y+2 * mirrorImage.Y * dotprod
		Return Self
	End
	
	' Set the vector to a direction and a length x,y
	' returns Self, which is now pointin the directio
	' provided, with the length provided
	Method MakeField:Vector( direction:Float, length:Float )	
		X = Cos( -direction )*length
		Y = Sin( -direction )*length
		Return Self
	End
	
	'----------------------------------------------
	' Create a copy --&gt; depending on situation you might
	' want to use the VectorPool to do this instead for
	' when extrenme performance is required
	'-----------------------------------------------
	Method Copy:Vector() Property
		Local vector:Vector = New Vector
		vector.X = X
		vector.Y = Y
		Return vector	
	End
	
		'	 C R E A T E   L E F T   N O R M A L
	'----------------------------------------------	
	' Make this an Perpendicular Vector
	' As if you would rotate it 90 degrees Counter Clockwise
	' return ( Y, -X )
	Method LeftNormal:Vector( )
		Local tempX:Float = Y
		Y = -X
		X = tempX
		Return Self
	End

		
	'	 C R E A T E   R I G H T   N O R M A L
	'----------------------------------------------	
	' Make this a Perpendicular Vector
	' Same as rotating it 90 degrees ClockWise
	' return ( -Y, X )
	Method RightNormal:Vector( )
		Local tempY:Float = Y
		Y = X
		X = -tempY
		Return Self
	End
	
	'		N O R M A L I Z E 
	'----------------------------------------------	
	' Purpose: Sets Vector length To ONE but keeps it's direction		
	' Returns Self as a UnitVector
	Method Normalize:Vector()
		Local Length:Float = Self.Length()
		If Length = 0 
			Return Self ' Don't divide by zero, 
			'we do not normalize a zero-vector 
			'since this vector's direction is in
			' mathematical terms all directions!
		Endif
		Set( X / Length, Y / Length ) 'Make length = 1
		Return Self
	End
	
	'   G E T   L E N G T H
	'-----------------------------
	' Get current Length of vector, uses a single sqr operation
	Method Length:Float() Property
		Return Sqrt( X*X + Y*Y )'
	End
	
	'	S E T	L E N G T H
	'-------------------------------------
	Method Length:Void( length:Float ) Property
		'If we want to set vector to zero 
		If length = 0 
			X=0 
			Y=0
			Return
		Endif

		If X = 0 And Y = 0
			X = length
		Endif
		
		Normalize()
		Multiply( length )				
	End
	
	Method ReduceLength:Vector( amount:Float )
		Local currentAngle:Float = Direction
		Local currentLength:Float = Length		
		Local newLength:Float = currentLength - amount
		If newLength &gt; 0
			MakeField( currentAngle, currentLength - amount  )
		Else 
			Set 0,0
		Endif
		Return Self	
	End
	
	' G E T  D I R E C T I O N
	'----------------------------------------------------
	' Calculates the current direction in degrees
	' in the 0 To &lt; 360 range
	Method Direction:Float() Property
		Local angle:Float = ATan2(-Y , X)
		'If angle &lt; 0 Then angle =+ 360
		Return angle
	End
	
	' S E T  D I R E C T I O N
	'----------------------------------------------
	'
	' Set the angle of this vector without changing the length,
	' has no effect if vector length is 0
	' uses a single sqr operation
	Method Direction:Void( direction:Float ) Property
		MakeField( direction, Length ) 
	End Method
	
	' D I S T A N C E
	'----------------------------------------------	
	' The Distance between the two points
	' This is NOT related to the vectors Length#	
	Method DistanceTo:Float(Other:Vector) 
		Return DistanceTo(Other.X, Other.Y)
	End
	Method DistanceTo:Float(x:Float, y:Float) 
		Local dx:Float = x - X 
		Local dy:Float = y - Y 
		Return Sqrt( dx*dx + dy*dy ) 	
	End
		
'		G E T  A N G L E   B E T W E E N
	'----------------------------------------------
	' If you have two vectors that start at the same position
	' the angle-distance between two vectors Result is from 0 
	' to 180 degrees, because two vectors can not be more than 180 
	' degrees apart, check AngleClockwise &amp; AngleAntiClockwise
	' to get a 0-360 result instead
	' even tough it is counted as they are on the same position,
	' that is not a requirement at all for this to be correct
	Method AngleTo:Float( target:Vector )	
		Local dot:Float = Self.Dot( target )
		
		Local combinedLength:Float = Self.Length()*target.Length()
		If combinedLength = 0 Then Return 0
		
		Local quaski:Float = dot/combinedLength
		
		Local angle:Float = ACos( quaski )
		Return angle
	End
	
	
	' If you have two vectors so they both start at the same position
	' returns the angle from this vector to target vector (in degrees)
	' if you where to go Clockwise to it, result is 0 to 360	
	Method AngleToClockwise:Float( target:Vector )
		Local angle:Float = ATan2(-Y , X)  - ATan2(-target.Y , target.X)
		If angle &lt; 0 Then angle += 360
		If angle &gt;= 360 Then angle -= 360
		Return angle		
	End
	
	' If you have two vectors so they both start at the same position
	' returns the angle fromt this vector to target vector (in degrees)
	' if you where to go AntiClockwise to it, result is 0 to 360	
	Method AngleToAntiClockwise:Float( target:Vector )
		Local angle:Float =  AngleToClockwise(target)-360
		Return -angle
	End	
	
		'		V E C T O R    B E T W E E N
	'----------------------------------------------
	' Change the vector into a vector from Position1 to Position2
	' Return Self, as a vector that goes from first 
	' parameter's position to the second 
	Method MakeBetween:Vector( PositionFrom:Vector , PositionToo:Vector)
		If PositionFrom = Null Or PositionToo = Null Then Return Self
		X = ( PositionToo.X - PositionFrom.X ) 	
		Y = ( PositionToo.Y - PositionFrom.Y )
		Return Self		
	End

End 
</pre><br><br>Short use example can be found here: <a href="http://www.truplo.com/TibitInteractive/viewtopic.php?f=5&amp;t=8&amp;sid=fb54294a58d6c1f44bc72bbe267e7267" target="_blank"> Vector Class </a> <br><br></td></tr></table><br>
<a name="2004317"></a>

<a name="2004318"></a>

<a name="2004315"></a>

<a name="2004316"></a>

<a name="2004345"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sammy</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Great stuff! :D<br><br>But possibly move it to the code archives for better archiving? <br><br></td></tr></table><br>
<a name="2004348"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Xaron</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yep great! Thanks for that. <br><br></td></tr></table><br>
<a name="2004350"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> ive used the angle functions of a vector class before its very useful, thanks <br><br></td></tr></table><br>
<a name="2004352"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sledge</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Many thanks! I reckon this would be a good time to mention the <a href="http://blog.wolfire.com/2009/07/linear-algebra-for-game-developers-part-1/" target="_blank">Wolfire vector articles</a> again. <br><br></td></tr></table><br>
<a name="2004354"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tibit</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> How many of you guys are familiar with Vectors since before?<br><br>My old Vector Tutorial for newbies, should brush it up for Monkey and this Vector class, but it might be somewhat helpful anyway since Blitzmax is quite similar and the focus is on understanding :)<br><br><a href="http://www.truplo.com/TibitInteractive/Downloads/Basic%20Vectors%20and%20Physics%20v3.doc" target="_blank">Basic Vectors and Physics in Blitzmax</a> <br><br></td></tr></table><br>
<a name="2004356"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Xaron</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's some vector stuff I do for my chipmunk port at the moment. It's still not optimized and need to be done more within the vector class (so methods instead of functions), but I work on that right now (at the moment it's more a 1:1 conversion from the original C code)<br><br><pre class=code>
' chipmonkey - A Monkey Port of Scott Lembcke's chipmunk 2d physics engine
' 
' chipmonkey homepage: <a href="http://code.google.com/p/chipmonkey/" target="_blank">http://code.google.com/p/chipmonkey/</a>
' chipmunk homepage: <a href="http://code.google.com/p/chipmunk-physics/" target="_blank">http://code.google.com/p/chipmunk-physics/</a>
' 
' This port is maintained by Martin Leidel @ <a href="http://www.monkeycoder.de" target="_blank">http://www.monkeycoder.de</a>
'
' This software is provided 'as-is', without any express or implied
' warranty.  In no event will the authors be held liable for any damages
' arising from the use of this software.
' 
' Permission is granted to anyone to use this software for any purpose,
' including commercial applications, and to alter it and redistribute it
' freely, subject to the following restrictions:
' 
' 1. The origin of this software must not be misrepresented; you must not
' claim that you wrote the original software. If you use this software
' in a product, an acknowledgment in the product documentation would be
' appreciated but is not required.
' 2. Altered source versions must be plainly marked as such, and must not be
' misrepresented as being the original software.
' 3. This notice may not be removed or altered from any source distribution.

' Module chipmonkey.vect

Class cpVect
  Public  
    Const zero:cpVect = New cpVect( 0.0, 0.0 )
  
    Method New( x:Float, y:Float )
      Self.x = x
      Self.y = y
    End Method

  Private
    Field x:Float
    Field y:Float
End Class

' Returns the length of v.
Function cpvlength:Float( v:cpVect )
  Return Sqrt( cpvdot( v, v ) )
End Function

' Spherical linearly interpolate between v1 and v2. 
Function cpvslerp:cpVect( v1:cpVect, v2:cpVect, t:Float )
  Local omega:Float = ACos( cpvdot( v1, v2 ) )

  If( omega &lt;&gt; 0.0 )
    Local denom:Float = 1.0 / Sin( omega )
    Return cpvadd( cpvmult( v1, Sin( ( 1.0 - t ) * omega ) * denom ), cpvmult( v2, Sin( t * omega ) * denom ) )
  Else
    Return v1
  End If
End Function

' Spherical linearly interpolate between v1 towards v2 by no more than angle a radians 
Function cpvslerpconst:cpVect( v1:cpVect, v2:cpVect, a:Float )
  Local angle:Float = ACos( cpvdot( v1, v2 ) )
  Return cpvslerp( v1, v2, Min( a, angle ) / angle )
End Function

' Returns the unit length vector for the given angle (in radians). 
Function cpvforangle:cpVect( a:Float )
  Return New cpVect( Cos( a ), Sin( a ) )
End Function

' Returns the angular direction v is pointing in (in radians). 
Function cpvtoangle:Float( v:cpVect )
  Return ATan2( v.y, v.x )
End Function

' Returns a string representation of v. Intended mostly for debugging purposes and not production use.
Function cpvstr:String( v:cpVect )
  Return String( "( " + v.x + ", " + v.y + " )" )
End Function

' Check if two vectors are equal. (Be careful when comparing floating point numbers!) 
Function cpveql:Bool( v1:cpVect, v2:cpVect )
  Return ( v1.x = v2.x And v1.y = v2.y )
End Function

' Add two vectors
Function cpvadd:cpVect( v1:cpVect, v2:cpVect )
  Return New cpVect( v1.x + v2.x, v1.y + v2.y )
End Function

' Negate a vector.
Function cpvneg:cpVect( v:cpVect )
  Return New cpVect( -v.x, -v.y )
End Function 

' Subtract two vectors.
Function cpvsub:cpVect( v1:cpVect, v2:cpVect )
  Return New cpVect( v1.x - v2.x, v1.y - v2.y )
End Function

' Scalar multiplication.
Function cpvmult:cpVect( v:cpVect, s:Float )
  Return New cpVect( v.x * s, v.y * s )
End Function

' Vector dot product.
Function cpvdot:Float( v1:cpVect, v2:cpVect )
  Return v1.x * v2.x + v1.y * v2.y
End Function

' 2D vector cross product analog.
' The cross product of 2D vectors results in a 3D vector with only a z component.
' This function returns the magnitude of the z value.
Function cpvcross:Float( v1:cpVect, v2:cpVect )
  Return v1.x * v2.y - v1.y * v2.x
End Function

' Returns a perpendicular vector. (90 degree rotation)
Function cpvperp:cpVect( v:cpVect )
  Return New cpVect( -v.y, v.x )
End Function

' Returns a perpendicular vector. (-90 degree rotation)
Function cpvrperp:cpVect( v:cpVect )
  Return New cpVect( v.y, -v.x )
End Function

' Returns the vector projection of v1 onto v2.
Function cpvproject:cpVect( v1:cpVect, v2:cpVect )
  Return cpvmult( v2, cpvdot( v1, v2 ) / cpvdot( v2, v2 ) )
End Function

' Uses complex number multiplication to rotate v1 by v2. Scaling will occur if v1 is not a unit vector.
Function cpvrotate:cpVect( v1:cpVect, v2:cpVect )
  Return New cpVect( v1.x * v2.x - v1.y * v2.y, v1.x * v2.y + v1.y * v2.x )
End Function

' Inverse of cpvrotate().
Function cpvunrotate:cpVect( v1:cpVect, v2:cpVect )
  Return New cpVect( v1.x * v2.x + v1.y * v2.y, v1.y * v2.x - v1.x * v2.y )
End Function

' Returns the squared length of v. Faster than cpvlength() when you only need to compare lengths.
Function cpvlengthsq:Float( v:cpVect )
  Return cpvdot( v, v )
End Function

' Linearly interpolate between v1 and v2.
Function cpvlerp:cpVect( v1:cpVect, v2:cpVect, t:Float )
  Return cpvadd( cpvmult( v1, 1.0 - t ), cpvmult( v2, t ) )
End Function

' Returns a normalized copy of v.
Function cpvnormalize:cpVect( v:cpVect )
  Return cpvmult( v, 1.0 / cpvlength( v ) )
End Function

' Returns a normalized copy of v or vzero if v was already vzero. Protects against divide by zero errors.
Function cpvnormalize_safe:cpVect( v:cpVect )
  If( v.x = 0.0 And v.y = 0.0 )
    Return New cpVect()
  Else
    Return cpvnormalize( v )
  End If
End Function

' Clamp v to length len.
Function cpvclamp:cpVect( v:cpVect, len:Float )
  If( cpvdot( v, v ) &gt; len * len )
    Return cpvmult( cpvnormalize( v ), len )
  Else
    Return v
  End If
End Function

' Linearly interpolate between v1 towards v2 by distance d.
Function cpvlerpconst:cpVect( v1:cpVect, v2:cpVect, d:Float )
  Return cpvadd( v1, cpvclamp( cpvsub( v2, v1 ), d ) )
End Function

' Returns the distance between v1 and v2.
Function cpvdist:Float( v1:cpVect, v2:cpVect )
  Return cpvlength( cpvsub( v1, v2 ) )
End Function

' Returns the squared distance between v1 and v2. Faster than vdist() when you only need to compare distances.
Function cpvdistsq:Float( v1:cpVect, v2:cpVect )
  Return cpvlengthsq( cpvsub( v1, v2 ) )
End Function

' Returns true if the distance between v1 and v2 is less than dist.
Function cpvnear:Bool( v1:cpVect, v2:cpVect, dist:Float )
  Return cpvdistsq( v1, v2 ) &lt; dist * dist
End Function
</pre> <br><br></td></tr></table><br>
<a name="2004355"></a>

<a name="2016050"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have been trying to figure out how to really use vectors and in the process I ran in to these tutorials:<br><a href="http://tonypa.pri.ee/vectors/start.html" target="_blank">http://tonypa.pri.ee/vectors/start.html</a><br>they are in flash but are really good for beginners and for learning how to do stuff. such as ball to ball collision, ball to line collision, ball to arc collision and  how to apply friction and gravity. I got most of it except how to do ball to arc collision.<br><br>here is the ball to wall collision in monkey:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict
Import mojo

Class Tpoint
	Field x:Float
	Field y:Float
End Class

Class Tvector
	Field p0:Tpoint
	Field p1:Tpoint
	Field length:Float
	Field dx:Float
	Field dy:Float
	Field vx:Float
	Field vy:Float
	
	Field rx:Float
	Field ry:Float
	Field lx:Float
	Field ly:Float
	
	Field b:Float
	Field f:Float
	Field r:Float
	Field lastTime:Float
	Field timeFrame:Float
	
	Method New()
		p0 = New Tpoint
		p1 = New Tpoint
	End Method
	
	Function Create:Tvector(x1:Float,y1:Float,x2:Float,y2:Float,b:Float,f:Float)
		Local v:Tvector = New Tvector
		v.p0.x = x1
		v.p0.y = y1
		v.p1.x = x2
		v.p1.y = y2
		v.b = b
		v.f = f
		Return v
	End Function
	
	'Function To find all parameters For the vector
	Method update:Void(frompoints:Int= False)
		'x And y components
		If (frompoints)
			vx = p1.x-p0.x
			vy = p1.y-p0.y
		Else
			p1.x = p0.x+vx
			p1.y = p0.y+vy
		Endif
		'length of vector
		length = Sqrt(vx*vx+vy*vy)
		'normalized unti-sized components
		If (length &gt; 0)
			dx = vx/length
			dy = vy/length
		Else
			dx = 0
			dy = 0
		Endif
		'Right hand normal
		rx = -dy
		ry = dx
		'Left hand normal
		lx = dy
		ly = -dx
	End Method

	'find New vector bouncing from v2
	Method bounce:Tvector(v2:Tvector)
		'projection of v1 on v2
		Local proj1:Tvector = project(v2.dx, v2.dy)
		'projection of v1 on v2 normal
		Local proj2:Tvector = project(v2.lx, v2.ly)
		Local proj:Tvector = New Tvector
		'reverse projection on v2 normal
		proj2.length = Sqrt(proj2.vx*proj2.vx+proj2.vy*proj2.vy)
		proj2.vx = v2.lx*proj2.length
		proj2.vy = v2.ly*proj2.length
		'add the projections
		proj.vx = f*v2.f*proj1.vx+b*v2.b*proj2.vx
		proj.vy = f*v2.f*proj1.vy+b*v2.b*proj2.vy
		Return proj
	End Method
	
	'project vector v1 on unit-sized vector dx/dy
	Method project:Tvector(dx:Float, dy:Float)
		'find dot product
		Local dp:Float = vx*dx+vy*dy
		Local proj:Tvector = New Tvector
		'projection components
		proj.vx = dp*dx
		proj.vy = dp*dy
		Return proj
	End Method

	'find intersection point of 2 vectors
	Method findIntersection:Tvector(v2:Tvector)
		Local v:Tvector,v4:Tvector
		'vector between center of ball And starting point of wall
		Local v3:Tvector = New Tvector
		v3.vx = (p0.x+vx)-v2.p0.x
		v3.vy = (p0.y+vy)-v2.p0.y
		'check If we have hit starting point
		Local dp:Float = v3.vx*v2.dx+v3.vy*v2.dy
		If (dp&lt;0)
			'hits starting point
			v = v3
		Else
			v4 = New Tvector
			v4.vx = (p0.x+vx)-(v2.p0.x+v2.vx)
			v4.vy = (p0.y+vy)-(v2.p0.y+v2.vy)
			'check If we have hit side Or endpoint
			dp = v4.vx*v2.dx+v4.vy*v2.dy
			If (dp&gt;0)
				'hits ending point
				v = v4
			Else
				'it hits the wall
				'project this vector on the normal of the wall
				v = v3.project(v2.lx, v2.ly)
			End If
		Endif
		Return v
	End Method
	
	Method display:Void()
		DrawLine p0.x,p0.y,p1.x,p1.y
	End Method

End Class



Class Tball Extends Tvector


	Function CreateBall:Tball(x1:Float,y1:Float,r:Float,vx:Float,vy:Float,bn:Float,f:Float)
	Local b:Tball = New Tball
	b.p0.x = x1
	b.p0.y = y1
	b.r = r
	b.vx = vx
	b.vy = vy
	b.b = bn
	b.f = f
	b.lastTime = Millisecs()
	Return b
	End Function

	Method wrapAround:Void()
		'reset Object To other side If gone out of stage
		If (p1.x&gt;game.stageW+r)
			p1.x = -r
		Elseif (p1.x&lt;-r)
			p1.x = game.stageW+r
		Endif
		If (p1.y&gt;game.stageH+r)
			p1.y = -r
		Elseif (p1.y&lt;-r)
			p1.y = game.stageH+r
		Endif
	End Method
	
	Method maxSpeed:Void()
		'dont let it go over Max speed
		If (vx&gt;game.maxV)
			vx = game.maxV
		Elseif (vx&lt;-game.maxV)
			vx = -game.maxV
		Endif
		If (vy&gt;game.maxV)
			vy = game.maxV
		Elseif (vy&lt;-game.maxV)
			vy = -game.maxV
		Endif
	
	End Method
	
	Method interactWall:Void(w:Tvector)
		Local v:Tvector = findIntersection(w)
		v.update()
		Local pen:Float = r-v.length
		'If we have hit the wall
		If (pen&gt;=0)
			'move Object away from the wall
			p1.x += v.dx*pen
			p1.y += v.dy*pen
			'change movement, bounce off from the normal of v
			Local vbounce:Tvector = New Tvector
			vbounce.dx = v.lx
			vbounce.dy = v.ly
			vbounce.lx = v.dx
			vbounce.ly = v.dy
			vbounce.b  = 1
			vbounce.f  = 1
			Local vb:Tvector = bounce(vbounce)
			vx = vb.vx
			vy = vb.vy
		Endif

	End Method
	
	Method update:Void(n:Int = True)
		'find time passed from last update
		Local thisTime:Float = Millisecs()
		Local time:Float = (thisTime-lastTime)/100
		time = .5 'If time &gt; 1.0 time = 1
		'we use time, Not frames To move so multiply movement vector with time passed
		vx *= time
		vy *= time
		'add gravity, also based on time
		vy = vy+time*game.gravity
		'find End point coordinates
		p1.x = p0.x+vx
		p1.y = p0.y+vy
		'length of vector
		length = Sqrt(vx*vx+vy*vy)
		'normalized unti-sized components
		If length &gt; 0
			dx = vx/length
			dy = vy/length
		Else
			dx = 0
			dy = 0
		Endif
		'Right hand normal
		rx = -vy
		ry = vx
		'Left hand normal
		lx = vy
		ly = -vx
		'save the Current time
		lastTime = thisTime
		'save time passed
		timeFrame = time
	End Method
	
	Method addAirResistance:Void()
		'add air resistance
		vx *= game.airf
		vy *= game.airf
	End Method
	
	Method display:Void()
		drawCircle p0.x,p0.y,r
	End Method
	
	Function drawCircle:Void(x:Float,y:Float,r:Float)
		For Local a:Float = 0 Until 360
			Local vx:Float = Cos(a)*r
			Local vy:Float = Sin(a)*r
			DrawRect x+vx,y+vy,1,1
		Next
		SetColor 100,100,0
		DrawRect(game.stageW,0,DeviceWidth()-game.stageW,DeviceHeight())
		DrawRect(0,game.stageH,game.stageW,DeviceHeight()-game.stageH)
	End Function

End Class

Class Tgame Extends App
	Field stageW:Float
	Field stageH:Float
	Field maxV:Float
	Field gravity:Float
	Field airf:Float
	Field LastTime:Float
	Field myOb:Tball
	Field v:Tvector[4]


	Method OnCreate:Int()
		stageW = 350
		stageH = 250
		maxV = 10
		gravity = 0.5
		airf = 0.99
		
		myOb = Tball.CreateBall(150,30,20,5,-5,1,1)
		
		v[0] = Tvector.Create(300,80,50,100,1,1)
		v[1] = Tvector.Create(350,150,300,80,1,1)
		v[2] = Tvector.Create(50,150,350,150,1,1)
		v[3] = Tvector.Create(50,100,50,150,1,1)
		
		v[0].update(True)
		v[1].update(True)
		v[2].update(True)
		v[3].update(True)
		SetUpdateRate(30)
		Return 1
	End Method
	
	Method OnUpdate:Int()
		'start To calculate movement
		myOb.addAirResistance()
		myOb.maxSpeed()
		'update the vector parameters
		myOb.update()
		'check the walls For collisions
		For Local w:Tvector = Eachin v
			myOb.interactWall(w)
		Next
		myOb.wrapAround()
	
		'save the movement without time
		If myOb.timeFrame &gt; 0
			myOb.vx /= myOb.timeFrame
			myOb.vy /= myOb.timeFrame
		Endif
		'draw it
		'make End point equal To starting point For Next cycle
		myOb.p0 = myOb.p1
	
		Return 1
	End Method
	
	Method OnRender:Int()
		'draw it
		Cls()
		 myOb.display()
		For Local w:Tvector = Eachin game.v
			w.display()
		Next
		Return 1	
	End Method
End Class

Global game:Tgame
Function Main:Int()
	game = New Tgame
	Return 1
End Function
</textarea> <br><br></td></tr></table><br>
<a name="2004375"></a>

<a name="2004374"></a>

<a name="2004541"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tibit</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Xaron, cool, looking forward to see the physics library btw, feel free to use my Vector class in the port, might be simpler and more powerful to use, however obviously the names of the methods won't be the same. <br><br></td></tr></table><br>
<a name="2004700"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Richard Betson</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> All, great stuff. :)<br><br>L8r, <br><br></td></tr></table><br>
<a name="2005600"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >xzess</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> indeed, very interesting, thanks! <br><br></td></tr></table><br>
<a name="2005602"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Shinkiro1</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Because I spotted it in the above code: What is the advantage of using Property after a Method? Is it only for readability? <br><br></td></tr></table><br>
<a name="2006372"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tibit</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes, it makes the code cleaner.<br><br>You can go myVector.Direction = 300<br><br>Instead of myVector.SetDirection 300<br><br>Not much of a difference, but it's nice still :) <br><br></td></tr></table><br>
<a name="2011815"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GC-Martijn</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Tibit made this simple mario movement example<br><pre class=code>
Strict


Import mojo.app
Import mojo.graphics
Import mojo.input


Import classes.vector


Function Main:Int()
New MarioApp
End


Class Mario
	Field Position:Vector = New Vector( 200,200 )
	Field Velocity:Vector = New Vector
	Field Force:Vector = New Vector 
	Field MaximumVelocity:Float = 5
	Field jumpCount:Int = 0 ' Just for fun, showing how to add double-jumping
	Field jumpForce:Float = 5 'How high we can jump
	
	Method Update:Void()
		Local groundHeight:Float = DeviceHeight() - 15
		If KeyDown( KEY_LEFT ) 
			Force.Add( New Vector( -0.1, 0) )
		End
		If KeyDown( KEY_RIGHT ) 
			Force.Add( New Vector(0.1, 0) )
		End
		If KeyHit( KEY_SPACE ) 
			' We can only jump when out feet touch the ground
			If Abs(Position.Y - groundHeight) &lt; 0.1
				Force.Add( New Vector(0, -jumpForce) ) 'Jump up = Add force to Y-axis 
				jumpCount+=1
			Else If Abs(Position.Y - groundHeight) &gt; 30 And jumpCount &gt; 0 And jumpCount &lt; 2 And Velocity.Y &gt; 0
				jumpCount+=1
				Force.Add( New Vector(0, -jumpForce*0.8) ) 'Double jump ability
			End
		End
		
	'We are always affected by gravity
	Force.Add( New Vector(0, 0.1) ) ' This affects how fast we will fall
	Velocity.Add( Force )
	Force.Set 0,0 'Reset force, sicne we converted it all into velocity
	' If Velocity.Length &gt; MaximumVelocity
	' Velocity.Length = MaximumVelocity
	' Endif
	
	
	Position.Add Velocity
	Velocity.Multiply( 0.99 ) 'Friction 
	'Here I Reduce velocity by 5% each Update
	' Collision against ground (Against the whole X-Axis actually)
	If Position.Y &gt; groundHeight
	'Print "Crash"
	Position.Y = groundHeight
	Velocity.Y = 0
	jumpCount = 0
	End
End

Method Draw:Void()
SetColor 0,0,0
DrawOval Position.X-15, Position.Y-15, 30, 30
End

End


Class MarioApp Extends App
Field Mario:Mario = New Mario
Method OnCreate:Int() 
SetUpdateRate 60
Return 0
End
Method OnUpdate:Int()
Mario.Update()


Return 0
End
Method OnRender:Int()
Cls 200,200,200
Mario.Draw


Return 0
End
End

</pre> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
