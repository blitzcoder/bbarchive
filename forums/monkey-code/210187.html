<!DOCTYPE html><html lang="en" ><head ><title >Mojo2 screen scrolling using SetCameraMatrix()</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Mojo2 screen scrolling using SetCameraMatrix()</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Mojo2 screen scrolling using SetCameraMatrix()</a><br><br>
<a name="2112594"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Richard Betson</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> <iframe width="560" height="345" src="http://www.youtube.com/embed/ZvTJAoMlIxA" frameborder="0" allowfullscreen></iframe><br><br>Hi,<br><br>I have started to get my head around what Mark is up to with mojo2 'Renderer'. Below is a modified version of Marks 'Renderer' demo (just save it in his modules/mojo2/bananas/rendererdemo folder) which uses SetCameraMatrix() to move the camera view around (in this case side scrolling) the renderer scene.<br><br>Pretty cool Mark S, seeing this working. :) <br><br>Edit- I made a little progress video for my crew and posted it here as well. ;)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'----------------------------------------------------------
' A modified version of Mark Sibly's Renderer demo
' using SetCameraMatrix() to simulate screen scrolling.
'----------------------------------------------------------


'very simple demo of using Renderer for &gt;4 lights + shadows.


Import mojo2
Import monkey

Global NUM_LIGHTS:=5


'create an orthographics projection matrix
Function Mat4Ortho:Float[]( left:Float,right:Float,bottom:Float,top:Float,znear:Float,zfar:Float )

	Local w:=right-left,h:=top-bottom,d:=zfar-znear
	
	Return [ 2.0/w,0,0,0, 0,2.0/h,0,0, 0,0,2.0/d,0, -(right+left)/w,-(top+bottom)/h,-(zfar+znear)/d,1 ]
End

Class MyLightA Implements ILight

	'note: x,y,z,w go in last 4 components of matrix...
	Field matrix:=[1.0,0.0,0.0,0.0, 0.0,1.0,0.0,0.0, 0.0,0.0,1.0,0.0, 0.0,0.0,-60.0,1.0]
	Field color:=[0.5,0.5,0.5,1.0]
	Field range:=1000.0

	'implement ILight interface...
	'
	Method LightMatrix:Float[]()
		Return matrix
	End
	
	Method LightType:Int()
		Return 1
	End
	
	Method LightColor:Float[]()
		Return color
	End
	
	Method LightRange:Float()
		Return range
	End
	
	Method LightImage:Image()
		Return Null
	End

End



Class MyLayerA Extends DrawList Implements ILayer

	Field lights:=New Stack&lt;MyLightA&gt;
	Field layerMatrix:=[1.0,0.0,0.0,0.0, 0.0,1.0,0.0,0.0, 0.0,0.0,1.0,0.0, 0.0,0.0,0.0,1.0]
	Field layerFogColor:=[0.0,0.0,0.0,0.0]

	'implement ILayer interface...
	'
	Method LayerMatrix:Float[]()
		Return layerMatrix
	End
	
	Method LayerFogColor:Float[]()
		Return layerFogColor
	End
	
	Method LayerLightMaskImage:Image()
		Return Null
	End
	
	Method EnumLayerLights:Void( lights:Stack&lt;ILight&gt; )
		For Local light:=Eachin Self.lights
			lights.Push light
		Next
	End

	Method OnRenderLayer:Void( drawLists:Stack&lt;DrawList&gt; )
		drawLists.Push Self
	End

End



Class Test
	Field _rend:Renderer
	Field _layer:MyLayerA

	Method AddScene(tile:Image,renderer:Renderer,layer0:MyLayerA)

		For Local i:=0 Until NUM_LIGHTS
			Self._layer.lights.Push(New MyLightA)
		Next

		For Local x:=0 To 1280 Step 128
			For Local y:=0 Until 640 Step 128	
				Self._layer.DrawImage tile,x-50,y
			Next
		Next
		Self._rend.Layers.Push layer0
	End Method

	Method SetRenderer(render:Renderer)
		Self._rend=render
	End Method

	Method SetLayer(layer:MyLayerA)
		Self._layer=layer
	End Method
	
	Method MyRenderer(renderer:Renderer)
		renderer.Render()
	End Method

End Class


Class MyApp Extends App
Field rtime,rframes,rfps
	Field tile:Image
	Field shadowCaster:ShadowCaster
	Field renderer:Renderer'=New Renderer
	Field layer0:MyLayerA'=New MyLayerA
	Field rimage:Image
	Field canvas:Canvas'=New Canvas
	'Field weird:=New DrawList
	Field test:Test=New Test
	
	Method OnCreate()
		Self.canvas=New Canvas
		
		'create renderer
		renderer=New Renderer
		renderer.SetViewport( 0,0,DeviceWidth,DeviceHeight )
		renderer.SetProjectionMatrix( Mat4Ortho( 0,DeviceWidth,0,DeviceHeight,-1,1 ) )
		renderer.SetAmbientLight( [0.21,0.1,0.1,1.0] )
		
		'load some gfx
		tile=Image.Load( "t3.png",0,0 ,Image.Filter|Image.Mipmap,Shader.MatteShader()  )
		
		'create layer 0
		layer0=New MyLayerA
		
		
		'create simple rect shadow caster
		shadowCaster=New ShadowCaster
		shadowCaster.SetVertices( [0.0,0.0, 32.0,0.0, 32.0,32.0, 0.0,32.0] )
		
		'draw some shadow casters
		For Local x:=100 Until 640 Step 220
		
			For Local y:=60 Until 480 Step 180
			
				layer0.SetColor 1,1,0
				layer0.DrawRect x+150,y-16,32,32
				layer0.SetColor 1,1,1
				
				layer0.AddShadowCaster shadowCaster,x+150,y-16
			Next
		Next

		'add layer to renderer		
		renderer.Layers.Push layer0

		test.SetRenderer(renderer)
		test.SetLayer(layer0)
		test.AddScene(tile,renderer,layer0)

	End


	Global cc:Float
	Method OnRender()

		
		rframes+=1
		Local e=Millisecs-rtime
		If e&gt;=1000
			rfps=rframes
			rframes=0
			rtime+=e
		Endif
		
		cc=cc+1
		renderer.SetCameraMatrix([1.0,0.0,0.0,0.0, 0.0,1.0,0.0,0.0, 0.0,0.0,1.0,0.0, 500+(Sin(cc)*100),0.0,0.0,1.0])
		'canvas.SetViewMatrix([1.0,0.0,0.0,0.0, 0.0,1.0,0.0,0.0, 0.0,0.0,1.0,0.0, 100.0,0.0,0.0,1.0])
			'renderer.SetProjectionMatrix( Mat4Ortho( 0,DeviceWidth,0,DeviceHeight,-1,1+ ) )
		canvas.Clear(.4,0,0)
		
		'move lights around a bit
		For Local i:=0 Until NUM_LIGHTS
			Local light:=layer0.lights.Get(i)
			Local radius:=120.0
			Local an:=(i*360.0/NUM_LIGHTS)+(Millisecs/50.0)
			light.matrix[12]=150+Cos( an )*radius+320
			light.matrix[13]=Sin( an )*radius+240
		Next

		'render scene
		test.MyRenderer(renderer)
			
		
		Self.canvas.SetColor(1,1,1)
		Self.canvas.DrawText("FPS-&gt; "+rfps,550,10)
		Self.canvas.Flush()	
	End
End

Function Main()

	New MyApp

End

</textarea> <br><br></td></tr></table><br>
<a name="2112593"></a>

<a name="2112587"></a>

<a name="2112584"></a>

<a name="2112583"></a>

<a name="2112577"></a>

<a name="2112619"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Richard Betson</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> You can use this to get it to orbit. Just replace the similar line in 'OnRender'<br><pre class=code>renderer.SetCameraMatrix([1.0,0.0,0.0,0.0, 0.0,1.0,0.0,0.0, 0.0,0.0,1.0,0.0, 500+(Sin(cc)*100),100+(Cos(cc)*50),0.0,1.0])
</pre> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
