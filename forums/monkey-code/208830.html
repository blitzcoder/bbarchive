<!DOCTYPE html><html lang="en" ><head ><title >Cape simulation</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Cape simulation</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Cape simulation</a><br><br>
<a name="2092443"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just had this crazy idea tonight, to do a basic 2D cape simulation... found nothing on Google, so this is what I came up with: the game is a bunch of segments, as as the root segment moves, all child segments are updated accordingly.<br><br>It kinda of work... but I think I'm missing something, the movement isn't fluid enough. Take a look at what I coded, and if someone can suggest something (besides using a physics engine :P) I'm all ears :)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import mojo

#If TARGET="glfw"
	#GLFW_USE_MINGW=True
	#GLFW_WINDOW_TITLE="Cape Simulation Test"
	#GLFW_WINDOW_WIDTH=1024
	#GLFW_WINDOW_HEIGHT=768
	#GLFW_WINDOW_FULLSCREEN=False
	#MUSIC_FILES="*.wav|*.ogg"
#End

Global gravity:Float = 0.7

Class CapePoint
	Field x:Float, y:Float

   Method New()
   End Method

   Method New(x:Float, y:Float)
      Self.x = x
      Self.y = y
   End Method
End Class

Class CapeNode
   Field p1:CapePoint
   Field p2:CapePoint
   Field child:CapeNode
   Field length:Float
   Field transmission:Float ' how much energy passes from one node to another
   
   Method New(x:Float, y:Float, length:Float)
   	Self.p1 = New CapePoint(x,y)
   	Self.p2 = New CapePoint(x,y+length)
   	Self.length = length
   	Self.child = Null
   End Method
   
   Method draw()
   	For Local c:Int=0 To 19
      	DrawLine p1.x+c, p1.y, p2.x+c, p2.y   
   	Next

   	If (child) Then child.draw()	
   End Method
   
   Method move(x:Float, y:Float)
   	' here the magic happens
   	p1.x +=x
   	p1.y +=y   	
   End Method
   
   Method update(parent:CapeNode=Null)
   	If parent Then   	
   		p1.x = parent.p2.x
   		p1.y = parent.p2.y   		   		
   	End If

		p2.y +=gravity*2.5
   	If (p1.x - p2.x) &gt; length Then 
   	   p2.x = p1.x - length 
   	Else
   		p2.x += (p1.x - p2.x)*transmission
   	End If	   	

   	If Abs(p2.y - p1.y) &gt; length Then 
   	   If p1.y &gt; p2.y Then p2.y = p1.y - length Else p2.y = p1.y + length   	  
   	Else
   	   If p2.y &lt; p1.y+length Then p2.y+=gravity + 2*(0.5 + transmission)
   	End If	   	

   	If (child) Then child.update(Self)
   End Method
End Class

Class Cape
   Field root:CapeNode

   Method setPos(x:Float, y:Float)
   	root.p1.x = x
   	root.p1.y = y
   End Method
   
   Method move(x:Float, y:Float)
   	root.move(x,y)
   End Method
   
   Method draw()
   	root.draw()
   End Method
   
   Method update()
   	root.update()
   End Method
End Class

Class Player
	Field x:Float, y:Float
	Field speedX:Float, speedY:Float
	
	Field cape:Cape
	
	Method New(x:Float, y:Float)
		Self.x = x
		Self.y = y
		
		Self.cape = New Cape()
		Self.cape.root = New CapeNode(x-35,y-10, 10)
		Self.cape.root.transmission = 0.5
		
		Self.cape.root.child = New CapeNode(x-35,y,15)
		Self.cape.root.child.transmission = 0.4
		
		Self.cape.root.child.child = New CapeNode(x-35,y+15,20)
		Self.cape.root.child.child.transmission = 0.3

		Self.cape.root.child.child.child = New CapeNode(x-35,y+35,20)
		Self.cape.root.child.child.child.transmission = 0.25
	End Method
	
	Method update()
		x += speedX
		y += speedY
		cape.move(speedX, speedY)

		speedX*=0.90
		If Abs(speedX)&lt;0.05 Then speedX=0

		speedY*=0.90
		If Abs(speedY)&lt;0.05 Then speedY=0
		
		cape.update()
	End Method
	
	Method draw()
		'DrawCircle x, y, 30
		SetColor 0,0,255
		DrawRect x-25,y-15,15,80
		DrawRect x-10,y+55,10,10
		
		SetColor 208,148,0
		DrawRect x-25, y-35,25,20
		
		SetColor 255,0,0
		cape.draw()
		SetColor 255,255,255
	End Method
End Class

Class Game Extends App
	Field player:Player
	
	Method OnCreate()
		SetUpdateRate(60)
		player = New Player(100,300)
		player.speedX = 0
	End Method
	
	Method OnRender()
		Cls
		player.draw()		
		DrawText "Cape simulation test",10,10
	End Method
	
	Method OnUpdate()
		player.update()
		
		If KeyDown(KEY_RIGHT) And player.speedX&lt;4 Then player.speedX+=1.0
		If KeyDown(KEY_LEFT) And player.speedX&gt;-4 Then player.speedX-=1.0

		If KeyDown(KEY_UP) And player.speedY&lt;4 Then player.speedY-=1.0
		If KeyDown(KEY_DOWN) And player.speedY&gt;-4 Then player.speedY+=1.0
		
		If KeyHit(KEY_ESCAPE) Then Error ""
	End Method
End Class

Function Main()
	New Game()
End Function
</textarea> <br><br></td></tr></table><br>
<a name="2092446"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> The effect isn't bad, it might look cool on a 32x32 sprite :) I think the cape settles too fast.<br><br>Also, apart from the music files pre-processor you don't need to wrap the #GFLW pre-processors in the #If statement, the other targets will just ignore them. <br><br></td></tr></table><br>
<a name="2092445"></a>

<a name="2092449"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oh, those pre-processors I just copied from another project, feel free to ignore them :) <br><br></td></tr></table><br>
<a name="2092454"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Why0Why</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> That is really nice!  Thanks for sharing. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
