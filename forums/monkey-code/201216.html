<!DOCTYPE html><html lang="en" ><head ><title >Another Texture Atlas Loader</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Another Texture Atlas Loader</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Another Texture Atlas Loader</a><br><br>
<a name="2065294"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamRedwoods</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's another texture atlas or sprite sheet loader, as with Android programming you can't have enough options for these atlas loaders.<br>It is compatible with this texture packer (windows):<br><a href="http://www.texturepacker.com/" target="_blank">http://www.texturepacker.com/</a><br><br>Export the atlas as "libGDX", which exports a plain text file. Also it's recommended that you uncheck TRIM and ROTATION, since offsets and rotations cannot be used.<br><br><br>EXAMPLE OF USAGE:<br><pre class=code>
Class Game extends App
   Field unitAtlas:CTextureAtlas
   Field imgunit:Image[10]

   Method OnCreate()
      unitAtlas = LoadAtlas("unit_atlas.png")
      imgunit[0] = unitatlas.Grab("tank_bottom.png",1,Image.MidHandle)
      imgunit[1] = unitatlas.Grab("tank_top.png",1,Image.MidHandle)
      ''..or..
      imgunit[2] = unitatlas.Grab("tank_bullet")
      ''....etc...

   Method OnRender()
      DrawImage imgunit[0],x,y
</pre><br><br>Create a field for the atlas texture, load it, then Grab() the files.<br><br>With my version, it's almost interchangeable with LoadImage(). This way you can develop with LoadImage() and swap it out when you're ready. It could load animations, if you use GrabSet() and return an array.<br><br>- You don't need the ".png" at the end of each filename if you don't want to. <br>- Don't use more than one "." in the filename or it will truncate it.<br>- The index and flags are optional.<br>- The index is used for multiple images when the filenames end with numbers (tree01, tree02, etc.). Then you would need to use the index=1, index=2, etc. (TexturePacker truncates numbers at the end of the file name.)<br><br><br>UPDATE:<br>- added GrabSet:Images[] which returns an array of images based on a width, height, and number of individual images you want returned from a specified file in the atlas. Example: say I added an already-made sprite sheet of an explosion, then I would use this to extract the explosion frames so I could animate it in the array.<br>- could also be used for a font array<br>- page fix by Raph<br>- load atlas array into one atlas<br><br>SOURCE:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Import mojo.app
Import mojo.graphics
Import monkey

'' libgdx sprite sheet loader
'' 
''this is best used in loading all textures, don't create during the game time, as it would create lag

Function LoadAtlas:CTextureAtlas(fn:String)
	Local atlas:CTextureAtlas = New CTextureAtlas
	If Not atlas.LoadTex(fn) Then Return Null
	Return atlas
End

Function LoadAtlas:CTextureAtlas(fn:String[])
	Local atlas:CTextureAtlas = New CTextureAtlas
	Local fconcat$
	
	For Local f$ = Eachin fn
		fconcat += LoadString( f )+"~n"
		
	Next
	If Not atlas.ProcessAtlas(fconcat) Then Return Null
	
	Return atlas
End

Class CTextureAtlas
	Field tmap:StringMap&lt;CSubTextureAtlas&gt; = New StringMap&lt;CSubTextureAtlas&gt;
	Field texture:Image[]
	Field total:Int =0
	Field subimage:CSubTextureAtlas[]

	Method LoadTex:Int(fn:String)
		
		If Not fn Return 0
		Local txtfile:String = fn[..(fn.FindLast("."))]+".txt"
		Local str:String=LoadString(txtfile)
		
		Return ProcessAtlas(str)
		
	End

	Method ProcessAtlas:Int(str:String)
		
		'Print "******BEGIN"
		
		If Not str Then Return 0
		
		texture = New Image[1]
		
		Local all:String[] = str.Split("~n")
		Local textureCount:Int = 0
		
		texture[textureCount] = LoadImage(all[0].Trim(), 1)
		DebugLog("Loading new texture " + all[0].Trim() + " here, index " + textureCount)
		If Not texture Then Return
		
		''count total
		For Local i:Int = 0 To all.Length()-1
			If all[i].Contains("xy:") Then total += 1
		Next
				
		''create sub images
		subimage = New CSubTextureAtlas[total + 1]
		Local si:Int = 0
		Local nextimage:Int = 0
		Local nextfile:Int = 0
		Local xy:String[] =["",""] '= New String[10]
		Local row:String = ""

		For Local i:Int = 4 To all.Length()-1
			Local EOF:Bool = (i=(all.Length()-1))
			
			row = all[i].Trim()
			If row.Contains(":")
			
				If row.Contains("xy")
					xy = row[ (row.FindLast(":")+1)..].Split(",")				
					subimage[si].x = Int(xy[0].Trim())
					subimage[si].y = Int(xy[1].Trim())
				Elseif row.Contains("size")
					xy = row[ (row.FindLast(":") + 1) ..].Split(",")
					subimage[si].w = Int(xy[0].Trim())
					subimage[si].h = Int(xy[1].Trim())
				Elseif row.Contains("offset")
					xy = row[ (row.FindLast(":") + 1) ..].Split(",")
					subimage[si].offsetx = Int(xy[0].Trim())
					subimage[si].offsety = Int(xy[1].Trim())
				Elseif row.Contains("index")
					Local ind:String = row[ (row.FindLast(":") + 1) ..]
					subimage[si].index = Int(ind.Trim() )
				EndIf
				
			Else
				
				If row="" Then
					' blank lines lead to a new texture file
					nextfile = 1
					Continue
				EndIf
				
				If nextfile = 1
					If row.Contains(".")
						' new texture file
						textureCount = textureCount + 1
						DebugLog("Loading new texture " + row + " here, index " + textureCount)
						texture = texture.Resize(texture.Length + 1)
						texture[textureCount] = LoadImage(row, 1)
						nextfile = 0
					Endif
					Continue					
				EndIf
				
				'' nextimage=0 is used for first time through
				If nextimage
					If subimage[si].index &gt;-1 Then subimage[si].file = subimage[si].file+"_"+String(subimage[si].index)
					tmap.Set( (subimage[si].file), subimage[si])
					DebugLog("Added " + subimage[si].file + "~n")
					si += 1
				EndIf

				''start new image
				subimage[si] = New CSubTextureAtlas
				subimage[si].file = row
				subimage[si].page = textureCount ' added to handle pages
				
				nextimage = 1
				
			Endif
		Next
	
		If si&gt;0 Or total &lt;=1
			''add last image
			If subimage[si].index &gt;-1 Then subimage[si].file = subimage[si].file+"_"+String(subimage[si].index)
			DebugLog subimage[si].file
			tmap.Set((subimage[si].file), subimage[si])
		Endif
		
		total = si
		
		Return 1
	End
	
	Method GetSubTexture:CSubTextureAtlas(fn:String, index:Int=0)
		Local noindexfn:String
		
		''drop any .png ext
		If fn.Contains(".") Then fn = fn[..(fn.FindLast(".")) ]
		noindexfn = fn.Trim()
		If index&gt;0 Then fn=fn+"_"+String(index)

		Local subimage:CSubTextureAtlas = tmap.Get(fn)
		If Not subimage
			''try without index
			subimage = tmap.Get(noindexfn)
			If Not subimage
				Print "**atlas not found:"+fn
				Return Null
			Endif
		Endif

		Return subimage
	End
	
	Method Grab:Image(fn:String, index:Int=0, flags:Int=Image.DefaultFlags)
	
		Local subimage:CSubTextureAtlas = GetSubTexture(fn,index)
		If Not subimage Then Return Null
		
		Local p:Int = subimage.page ' added to handle pages
		Return texture[p].GrabImage(subimage.x, subimage.y, subimage.w, subimage.h, 1, flags)
	End
	
	Method GrabSet:Image[](fn:String, w:Int, h:Int, frames:Int=0, flags:Int=Image.DefaultFlags, index:Int=0)
		Local imgs:Image[frames+1]
		imgs[0] = Null
		
		Local subimage:CSubTextureAtlas = GetSubTexture(fn,index)
		Local p:Int = subimage.page ' added to handle pages
		Local newx:Int =0 , newy:Int=0
		For Local i:=0 To frames-1
			imgs[i] = texture[p].GrabImage(subimage.x + newx, subimage.y + newy, w, h, 1, flags)
			newx += w+1
			If newx &gt;= subimage.w-2
				newx=0
				newy += h+1
				If newy&gt;subimage.h
					Exit
				Endif
			Endif
			
		Next
		Return imgs
	End

End

Class CSubTextureAtlas
	Field file:String = ""
	Field page:Int = 0 ' added to handle pages
	Field x:Int, y:Int, offsetx:Int, offsety:Int
	Field w:Int, h:Int
	Field index:Int
	Field totalframes:Int
End


</textarea> <br><br></td></tr></table><br>
<a name="2014046"></a>

<a name="2014050"></a>

<a name="2010881"></a>

<a name="2010878"></a>

<a name="2010877"></a>

<a name="2010874"></a>

<a name="2010873"></a>

<a name="2010872"></a>

<a name="2010871"></a>

<a name="2010870"></a>

<a name="2010869"></a>

<a name="2010868"></a>

<a name="2010782"></a>

<a name="2010781"></a>

<a name="2010780"></a>

<a name="2010779"></a>

<a name="2010880"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamRedwoods</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Updated to pseudo-handle animations, fonts. <br><br></td></tr></table><br>
<a name="2010879"></a>

<a name="2010876"></a>

<a name="2013861"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GC-Martijn</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oke, thanks for sharing.<br><br>Could someone give an example how to use it with moving a player ?<br>I don't understand the idea with multiply images, so it look that the player is moving.<br><br> <br>What I have is for example:<br>this image map: <img src="http://www.setera.org/wp-content/uploads/2010/12/walker.png"><br><br>then<br><br><pre class=code>
unitAtlas = LoadAtlas("char1.txt")
imgunit[0] = unitAtlas.Grab("char__0",1,Image.MidHandle)
imgunit[1] = unitAtlas.Grab("char__1",1,Image.MidHandle)
imgunit[2] = unitAtlas.Grab("char__2",1,Image.MidHandle)
imgunit[3] = unitAtlas.Grab("char__3",1,Image.MidHandle)
...

If KeyDown( KEY_LEFT )
 DrawImage imgunit[0],DrawAtX,DrawAtY
End

</pre><br><br>but where i do the animation , so the player image changing ? <br><br></td></tr></table><br>
<a name="2013887"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> to make an animation you need to make use of variables for the array index that change as time goes by.<br><br>the steps required in pseudocode<br><br>load all of the images in the OnCreate method.<br>variables are needed for the first and last image for each direction.<br>a variable that keep track which frame is being displayed<br>all of the keyboard input and calculations for the animation should be performed on OnUpdate() method.<br>finally the On Render will just be used to display the image.<br><pre class=code>
    Method OnCreate:int()
        'set frame rate
        'loadImages
    End

    Method OnUpdate:int()
        'check for key presses and select correct animation frame
    End

    Method OnRender()
        'draw the correct image based on calculations done on the OnUpdate() Method
    End
</pre> <br><br></td></tr></table><br>
<a name="2013886"></a>

<a name="2013885"></a>

<a name="2013896"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GC-Martijn</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> thank Jesse,<br><br>but I already have that, I don't understand the animation frame key.<br><br>the thing I have to do in the OnUpdate() and Onrender<br><br><pre class=code>
 Method OnCreate:int()
       
        unitAtlas = LoadAtlas("char1.txt")
imgunit[0] = unitAtlas.Grab("char__0",1,Image.MidHandle) ' walk left step 1
imgunit[1] = unitAtlas.Grab("char__1",1,Image.MidHandle) ' walk left step 2
imgunit[2] = unitAtlas.Grab("char__2",1,Image.MidHandle) ' walk left step 3
imgunit[3] = unitAtlas.Grab("char__3",1,Image.MidHandle)
    End

    Method OnUpdate:int()
        'check for key presses and select correct animation frame
If KeyDown( KEY_LEFT )
 DrawImage imgunit[0],DrawAtX,DrawAtY
End

    End

    Method OnRender()
        'draw the correct image based on calculations done on the OnUpdate() Method
    End
</pre><br><br>Let keep it simple that moving left is 4 pictures, like in this example.<br>What to do next ? <br><br></td></tr></table><br>
<a name="2013904"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> you say you understand but your example illustrates otherwise.<br>the OnRender is where all of the drawing go.  the OnUpdate will give you an error if you try to use <br>any of the draw commands on.<br><br>something like this at it's simplest form:<br><pre class=code>

	Field index:Int


	Method OnCreate:Int()
               SetFrameRate(60)
         	unitAtlas = LoadAtlas("char1.txt")
		imgunit[0] = unitAtlas.Grab("char__0",1,Image.MidHandle) ' walk left step 1
		imgunit[1] = unitAtlas.Grab("char__1",1,Image.MidHandle) ' walk left step 2
		imgunit[2] = unitAtlas.Grab("char__2",1,Image.MidHandle) ' walk left step 3
		imgunit[3] = unitAtlas.Grab("char__3",1,Image.MidHandle)
    		index = 0
	End

    Method OnUpdate:Int()
        'check for key presses and select correct animation frame
		If KeyDown( KEY_LEFT )
		 	index += 1
			If index &gt; 3
				index = 0
			Endif
		End

    End

    Method OnRender()
        'draw the correct image based on calculations done on the OnUpdate() Method
 		DrawImage imgunit[index],DrawAtX,DrawAtY
	End
</pre><br>note that the code doesn't take into consideration desired speed, it changes frames at game rate speed.<br><br>have you ever programmed animations in any other programming language and have you ever done any type of object oriented programming? <br><br></td></tr></table><br>
<a name="2013903"></a>

<a name="2013902"></a>

<a name="2013901"></a>

<a name="2013906"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> a bit more advance:<br><pre class=code>

Strict
Import Mojo

Function Main:Int()
	New Game
End Function

Class Game Extends App
	Field walkingLeft:Animation
	Field walkingRight:Animation
	Field standingRight:Animation
	Field standingLeft:Animation
	Field currentAnimation:Animation
	Field pinguin:Image
	
	Method OnCreate:Int()
		SetUpdateRate(60)
       	pinguin = LoadImage("walker.png",32,32,16)
    		walkingLeft = New Animation(0,7,5,pinguin)'first frame, last frame ,duration, animated image set
		walkingRight = New Animation(8,15,5,pinguin)
		standingLeft = New Animation(6,6,5,pinguin)
		standingRight = New Animation(9,9,5,pinguin)
		currentAnimation = standingRight
	End

    Method OnUpdate:Int()
        'check for key presses and select correct animation frame
		If KeyDown( KEY_LEFT )
			currentAnimation = walkingLeft
		Elseif KeyDown( KEY_RIGHT)
			currentAnimation = walkingRight
		Else
			Select currentAnimation
				Case walkingLeft
					currentAnimation = standingLeft
				Case walkingRight
					currentAnimation = standingRight
			End Select
		Endif
		currentAnimation.update()
    End

    Method OnRender:Int()
		Cls()
        'draw the correct image based on calculations done on the OnUpdate() Method
 		currentAnimation.display(100,100)
	End
	
End Class	

Class Animation

	Field firstFrame:Int
	Field lastFrame:Int
	Field duration:Int
	Field delay:Int
	Field index:Int
	Field images:Image

	Method New(first:Int,last:Int,dur:Int,img:Image)
		firstFrame = first
		lastFrame = last
		duration = dur
		images = img
		index = first
		delay = 0
	End Method

	Method update:Int()
		delay = delay + 1
		If delay &gt; duration
			index  = index + 1
			If index &gt; lastFrame
				index = firstFrame
			Endif
			delay = 0
		Endif
	End Method
	
	Method display:Int(x:Int,y:Int)
		DrawImage images,x,y,index
	End Method
End Class
</pre> <br><br></td></tr></table><br>
<a name="2013945"></a>

<a name="2013946"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Heres a quick example using Adam's Another Texture Atlas Loader:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Import mojo
Import textureloader

Global myGame:MyGame

Function Main:Int()
	myGame = new MyGame
	Return 0
End

Class MyGame Extends App
	Field unitAtlas:CTextureAtlas
	Field imgunit:Image[16]
	Field frame:Int
	Field frameTimer:Int
	Field frameEnd:Int = 15
	Field frameSpeed:Int = 100

	Method OnCreate:Int()
		SetUpdateRate 60
		unitAtlas = LoadAtlas("penwalker.txt")
		For Local i:Int = 0 to 15
			if i &lt; 10
				imgunit[i] = unitAtlas.Grab("0"+i,0,Image.MidHandle)
			Else
				imgunit[i] = unitAtlas.Grab(i,0,Image.MidHandle)
			End
		Next

		Return 0
	End
	
	Method OnUpdate:Int()
		if frameSpeed &gt; 0
			If Millisecs() &gt; frameTimer + frameSpeed
				frame+=1
				If frame &gt; frameEnd
					frame = 0
				End
				frameTimer = Millisecs()
			End	
		end
		Return 0		
	End
	
	Method OnRender:Int()
		Cls
		DrawImage imgunit[frame], 100, 100
		Return 0
	End
End
</textarea><br><br>BTW Adam I think there is a slight issue with your code, it doesnt like it if there is only one image in the data file ;) <br><br></td></tr></table><br>
<a name="2013961"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GC-Martijn</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Jesse, sorry I copy/past to quick.<br>But you saved my life with that advanced code ! thanks very much its realy works very good.<br><br>I had to change the CLS() to SetColor(255,255,255) because everything was black except for a red penguin.<br><br>@therevills, I will going to combine the code with @Jesse thanks ! <br><br></td></tr></table><br>
<a name="2013981"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> it's better if you use the millisecs setup that therevills has by replacing the delay and duration on my example. <br><br></td></tr></table><br>
<a name="2014048"></a>

<a name="2014049"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamRedwoods</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>BTW Adam I think there is a slight issue with your code, it doesnt like it if there is only one image in the data file ;) <br> <br></div><br>Whooops!<br>Updated, fixed! Thx! (was one small edit) :D <br><br></td></tr></table><br>
<a name="2065157"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Raph</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm getting a crash using this... <br><br><pre class=code>
Monkey Runtime Error : Array index out of range
C:/Monkey/MonkeyModules/modules/textureatlas/textureatlas.monkey&lt;85&gt;
C:/Monkey/MonkeyModules/modules/textureatlas/textureatlas.monkey&lt;12&gt;</pre><br><br>It is specifically happening with atlases that are multiple pages, and seems to always be on the last image on the last page.<br><br>A single page atlas works fine in a different project. <br><br></td></tr></table><br>
<a name="2065268"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Raph</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> So, handling of multiple texture pages is definitely broken. Each page is adding a new image index, which results in the array getting blown. It's doing a Continue when it hits the blank lines. Also means that even if you expand the array, the indices are wrong from that point forward:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
				If row = "" Then Continue
				'' nextimage=0 is used for first time through
				If nextimage
					DebugLog("Nextimage")
					If subimage[si].index &gt;-1 Then subimage[si].file = subimage[si].file+"_"+String(subimage[si].index)
					tmap.Set((subimage[si].file), subimage[si])
					DebugLog(subimage[si].file)
					si += 1
					DebugLog("si is now " + si)
				Endif

				''start new image
				DebugLog("New image")
				subimage[si] = New CSubTextureAtlas
				subimage[si].file = row
				
				nextimage = 1</textarea><br><br>A brief sample:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
sbutton_gray2
  rotate: False
  xy: 520, 906
  size: 256, 64
  orig: 256, 64
  offset: 0, 0
  index: -1

frac_1.png
format: RGBA8888
filter: Linear,Linear
Repeat: none
lbutton_green2
  rotate: False
  xy: 4, 518
  size: 768, 192
  orig: 768, 192
  offset: 0, 0
  index: -1
</textarea><br><br>This gives the following output across a page break...<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
sbutton_gray2
Nextimage
sbutton_cyan2
si is now 31
rotate: False
xy: 520, 906
 520: 906
size: 256, 64
 256: 64
orig: 256, 64
offset: 0, 0
 0: 0
index: -1
-1

frac_1.png
Nextimage
sbutton_gray2
si is now 32
format: RGBA8888
filter: Linear,Linear
Repeat: none
lbutton_green2
Nextimage
frac_1.png_0
si is now 33
rotate: False
xy: 4, 518
 4: 518
size: 768, 192
 768: 192
orig: 768, 192
offset: 0, 0
 0: 0
index: -1
-1
</textarea><br><br>I can take a whack at fixing it later, unless you happen to have a version that works already, Adam... <br><br></td></tr></table><br>
<a name="2065267"></a>

<a name="2065266"></a>

<a name="2065271"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Raph</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's a version that seems to work. I haven't tested it with single page atlases, but it should be fine. I added a page field to CSubTextureAtlas and changed CTextureAtlas's field "texture" into an image array. A blank line triggers loading a new file at the end of the array, and Grab and GrabSet now just pick from the right texture in the array.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Import mojo.app
Import mojo.graphics
Import monkey

'' libgdx sprite sheet loader
'' 
''this is best used in loading all textures, don't create during the game time, as it would create lag

Function LoadAtlas:CTextureAtlas(fn:String)
	Local atlas:CTextureAtlas = New CTextureAtlas
	If Not atlas.LoadTex(fn) Then Return Null
	Return atlas
End

Class CTextureAtlas
	Field tmap:StringMap&lt;CSubTextureAtlas&gt; = New StringMap&lt;CSubTextureAtlas&gt;
	Field texture:Image[]
	Field total:Int =0
	Field subimage:CSubTextureAtlas[]

	Method LoadTex:Int(fn:String)
		texture = New Image[1]
		'Print "******BEGIN"
		If Not fn Return 0
		Local txtfile:String = fn[..(fn.FindLast("."))]+".txt"
		Local str:String=LoadString(txtfile)
		If Not str Then Return 0
		
		Local all:String[] = str.Split("~n")
		Local textureCount:Int = 0
		
		texture[textureCount] = LoadImage(all[0].Trim(), 1)
		DebugLog("Loading new texture " + all[0].Trim() + "here, index " + textureCount)
		If Not texture Then Return
		
		''count total
		For Local i:Int = 0 To all.Length()-1
			If all[i].Contains("xy:") Then total += 1
		Next
				
		''create sub images
		subimage = New CSubTextureAtlas[total + 1]
		Local si:Int = 0
		Local nextimage:Int = 0
		Local nextfile:Int = 0
		Local xy:String[] =["",""] '= New String[10]
		Local row:String = ""

		For Local i:Int = 4 To all.Length()-1
			row = all[i].Trim()
			If row.Contains(":")
			
				If row.Contains("xy")
					xy = row[ (row.FindLast(":")+1)..].Split(",")				
					subimage[si].x = Int(xy[0].Trim())
					subimage[si].y = Int(xy[1].Trim())
				Elseif row.Contains("size")
					xy = row[ (row.FindLast(":") + 1) ..].Split(",")
					subimage[si].w = Int(xy[0].Trim())
					subimage[si].h = Int(xy[1].Trim())
				Elseif row.Contains("offset")
					xy = row[ (row.FindLast(":") + 1) ..].Split(",")
					subimage[si].offsetx = Int(xy[0].Trim())
					subimage[si].offsety = Int(xy[1].Trim())
				Elseif row.Contains("index")
					Local ind:String = row[ (row.FindLast(":") + 1) ..]
					subimage[si].index = Int(ind.Trim() )
				EndIf
				
			Else
				
				If row = "" Then
					' blank lines lead to a new texture file
					nextfile = 1
					Continue
				EndIf
				
				If nextfile = 1
					' new texture file
					textureCount = textureCount + 1
					DebugLog("Loading new texture " + row + " here, index " + textureCount)
					texture = texture.Resize(texture.Length + 1)
					texture[textureCount] = LoadImage(row, 1)
					nextfile = 0
					Continue					
				EndIf
				
				'' nextimage=0 is used for first time through
				If nextimage
					If subimage[si].index &gt;-1 Then subimage[si].file = subimage[si].file+"_"+String(subimage[si].index)
					tmap.Set( (subimage[si].file), subimage[si])
					DebugLog("Added " + subimage[si].file + "~n")
					si += 1
				EndIf

				''start new image
				subimage[si] = New CSubTextureAtlas
				subimage[si].file = row
				subimage[si].page = textureCount ' added to handle pages
				
				nextimage = 1
				
			Endif
		Next
	
		If si&gt;0 Or total &lt;=1
			''add last image
			If subimage[si].index &gt;-1 Then subimage[si].file = subimage[si].file+"_"+String(subimage[si].index)
			DebugLog subimage[si].file
			tmap.Set((subimage[si].file), subimage[si])
		Endif
		
		total = si
		
		Return 1
	End
	
	Method GetSubTexture:CSubTextureAtlas(fn:String, index:Int=0)
		Local noindexfn:String
		
		''drop any .png ext
		If fn.Contains(".") Then fn = fn[..(fn.FindLast(".")) ]
		noindexfn = fn.Trim()
		If index&gt;0 Then fn=fn+"_"+String(index)

		Local subimage:CSubTextureAtlas = tmap.Get(fn)
		If Not subimage
			''try without index
			subimage = tmap.Get(noindexfn)
			If Not subimage
				Print "**atlas not found:"+fn
				Return Null
			Endif
		Endif

		Return subimage
	End
	
	Method Grab:Image(fn:String, index:Int=0, flags:Int=Image.DefaultFlags)
	
		Local subimage:CSubTextureAtlas = GetSubTexture(fn,index)
		Local p:Int = subimage.page ' added to handle pages
		Return texture[p].GrabImage(subimage.x, subimage.y, subimage.w, subimage.h, 1, flags)
	End
	
	Method GrabSet:Image[](fn:String, w:Int, h:Int, frames:Int=0, flags:Int=Image.DefaultFlags, index:Int=0)
		Local imgs:Image[frames+1]
		imgs[0] = Null
		
		Local subimage:CSubTextureAtlas = GetSubTexture(fn,index)
		Local p:Int = subimage.page ' added to handle pages
		Local newx:Int =0 , newy:Int=0
		For Local i:=0 To frames-1
			imgs[i] = texture[p].GrabImage(subimage.x + newx, subimage.y + newy, w, h, 1, flags)
			newx += w+1
			If newx &gt;= subimage.w-2
				newx=0
				newy += h+1
				If newy&gt;subimage.h
					Exit
				Endif
			Endif
			
		Next
		Return imgs
	End

End

Class CSubTextureAtlas
	Field file:String = ""
	Field page:Int = 0 ' added to handle pages
	Field x:Int, y:Int, offsetx:Int, offsety:Int
	Field w:Int, h:Int
	Field index:Int
	Field totalframes:Int
End



</textarea> <br><br></td></tr></table><br>
<a name="2065295"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamRedwoods</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, cool, thanks for updating that!<br>I also updated the code to check for a "." in the next line (indication of an actual filename and not just an inadvertent blank line)<br><br>So with that in mind, I've added the capability to load an array of atlas txt files, so they can be combined into one atlas.<br><pre class=code>
unitAtlas = LoadAtlas(["cards0.txt","cards1.txt"])
imgunit[0] = unitAtlas.Grab("queen_of_spades") ''in cards0.txt/cards0.png
imgunit[1] = unitAtlas.Grab("red_joker")
imgunit[2] = unitAtlas.Grab("queen_of_diamonds") ''in cards1.txt/cards1.png
imgunit[3] = unitAtlas.Grab("queen_of_hearts")
</pre> <br><br></td></tr></table><br>
<a name="2065293"></a>

<a name="2065296"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Raph</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice! I like this loader because it has virtually no assumptions about the images after, you really can just drop it in and find-and-replace LoadImage, except for animations.<br><br>I dug into a GrabAnim that you could load animation frames without the GrabSet workaround, but it looks like there's some limitations in monkey.graphics which prevent it. It isn't that hard to hack it to work, but I tossed a feature request in the bug forum and left it at that. <br><br></td></tr></table><br>
<a name="2065306"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Raph</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think there is a bug in GrabSet as well. newx and newy were being incremented by w+1 and h+1 respectively. That created a growing offset that corrupted the frames, and could go off the surface altogether.<br><br>I put this in instead and it seems to work fine:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
	Method GrabSet:Image[](fn:String, w:Int, h:Int, frames:Int=0, flags:Int=Image.DefaultFlags, index:Int=0)
		Local imgs:Image[frames+1]
		imgs[0] = Null
		
		Local subimage:CSubTextureAtlas = GetSubTexture(fn,index)
		Local p:Int = subimage.page ' added to handle pages
		Local newx:Int =0 , newy:Int=0
		For Local i:=0 To frames-1
			imgs[i] = texture[p].GrabImage(subimage.x + newx, subimage.y + newy, w, h, 1, flags)
			newx += w  ' removed the +1
			If newx &gt;= subimage.w-2
				newx=0
				newy += h  ' removed the + 1
				If newy&gt;subimage.h
					Exit
				Endif
			Endif
			
		Next
		Return imgs
	End
</textarea> <br><br></td></tr></table><br>
<a name="2092915"></a>

<a name="2092916"></a>

<a name="2092917"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for the code.  However, there's a bug (of sorts) if you have the png and txt file in the same folder (but not in the exe folder)<br><br>For example: If you call LoadAtlas() with a filepath e.g. "graphics/myAtlast.png" it fails because this line:<br><br><pre class=code>texture[textureCount] = LoadImage(all[0].Trim(), 1)</pre><br><br>Uses the texture filename from the top of the .txt file which doesn't have path info if it's in the same file as the texture.<br>I made a workaround by passing in the original png filepath to ProcessAtlas (in LoadText()):<br><br><pre class=code>Return ProcessAtlas(str, fn)</pre><br><br>And modified the Method declaration as follows:<br><pre class=code>ProcessAtlas:Int(str:String, texturePath:String)</pre><br><br>Then changed the texture loading as follows:<br><pre class=code>texture[textureCount] = LoadImage(texturePath, 1)</pre><br><br>However it should be noted that I had to comment out this line in the version of LoadAtlas that takes an array of texture filenames:<br><pre class=code>
If Not atlas.ProcessAtlas(fconcat) Then Return Null
</pre><br><br>Really the code needs to get the path for each png (which may not be the same) and add it to the top of each text file as it joins them together and then use that to load the png.<br><br>Alternatively you can click Advanced in the Data section of TexturePacker and fill out the Texture Path and it will add it to the filename at the top of the .txt file. But this is kinda lame really... <br><br></td></tr></table><br>
<a name="2093030"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> Quick heads up that I tried using the offset values but I've discovered that they are sometimes slightly wrong by a pixel or two!  If I output the same data as xml the offset values are correct, so I don't know what is going on there... <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
