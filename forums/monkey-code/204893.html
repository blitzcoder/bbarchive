<!DOCTYPE html><html lang="en" ><head ><title >Fast Text Phrases with AngelFont()</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Fast Text Phrases with AngelFont()</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Fast Text Phrases with AngelFont()</a><br><br>
<a name="2055364"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is a example how to speed up text phrases in monkey. Often the text phrases don't change. Than an image would be faster than DrawText().<br><br>PhotoText.Draw() draws a new text first with DrawText(), but saves also a screenshot of the text to a list. When the same text is drawn again, it re-finds the screenshot and draws the image instead of the characters.<br><br>PhotoText is 10 times faster than AngelFont and the speed does not depend on SETCOLOR(). <br><br>The class is more a tuturial than a complete module...<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">'  Example for using on-the-fly screenshots of text phrases instead of DrawText()
'
'  the Class PhotoText draws any text with AngelFont, but saves the result 
'  in an image for recalls. 
'
'  PhotoText.Draw() is 10 times faster than AngelFont.Draw()
'
'

Strict
Import mojo
Import angelfont


Class PhotoTextExample Extends App
	Field font:AngelFont, Farbe%, Mode%
	Field ZufallString#[100],R%[100],G%[100],B%[100]
	
	Method OnCreate%()
			SetUpdateRate 60
			font = New AngelFont()
			font.LoadFont("angel_verdana")
	
			For Local i%=0 To 99  
				ZufallString[i]=Rnd(1000000)
				R[i]=Rnd(0,255)
				G[i]=Rnd(0,255)
				B[i]=Rnd(0,255)
			Next
			Return 0			
	End




	Method OnUpdate%()
			If KeyHit(KEY_ESCAPE) Then Error ""		
			If KeyHit(KEY_1) Then Mode=1-Mode		
			Return 0			
	End



	
	Method OnRender%()
			Farbe=Farbe+1
			Cls (Farbe Mod 150)+150,((Farbe*2)Mod 150)+50,((250-Farbe*1.5) Mod 150)+50
			SetColor 1,1,1

			
			'also phrases out of screen slow down performance:
			Local TestCount%=20  ' try 3-90
			
			' the same test for both methods
			Local Zeit%=Millisecs()
			If Mode=1
				' TEXTPHOTO
					TextPhoto.DrawText ZufallString[0],Farbe*3 Mod 400,60,font	
				 	For Local i%=5 To TestCount+2
							SetColor R[i], G[i] , B[i]
							TextPhoto.DrawText ZufallString[i],0,i*20,font	
					Next
					SetColor 221,222,222
					TextPhoto.DrawText "NOW= PHOTOTEXT     press &lt;1&gt; to change ",0,0,font
			Else
				' ANGELFONT
					font.DrawText ZufallString[0],Farbe*3 Mod 400,60
				 	For Local i%=5 To TestCount+2
							SetColor R[i], G[i] , B[i]
							' white would speed up AngelFont:
							'SetColor 255,255,255
							font.DrawText ZufallString[i],0,i*20
					Next
					SetColor 221,222,222
					font.DrawText "NOW= ANGELFONT    press &lt;1&gt; to change ",0,0
			Endif
			font.DrawText "time=" + ((Millisecs()-Zeit)/TestCount+1) + "msec/string",0,20
		Return 0
	End
			
End




Function Main%()
	New PhotoTextExample
	Return 0			
End




Class TextPhoto
	Global Liste:List&lt;TextPhoto&gt; = New List&lt;TextPhoto&gt;
	Field Text$, Photo:Image


	
	Function AddOne:Image(T$, tmpFont:AngelFont)
		' function creates a new photo and adds it to a list for recalls
		'
		'
		Local Width% = tmpFont.TextWidth(T)
		Local Height% = tmpFont.TextHeight(T)
		PushMatrix()
			' reset the screen to startup:
			SetMatrix 1,0,0,1,0,0
			
			' this line would save the orignal screen*
			'Local BackPhoto:Image = ScreenGrab(0, 0, Width, Height)

			' backgroud color black and textcolor white
			SetColor 0,0,0
			DrawRect 0,0, Width, Height
			SetColor 255,255,255

			Local loc:TextPhoto = New TextPhoto			
			' store original text for re-finding
			loc.Text = T
			
			' draw the text with angelfont:
			tmpFont.DrawText T,0,0
			
			' now shoot a photo:
			loc.Photo=TextGrab(0,0, Width, Height)
			Liste.AddLast loc
			
			' this line would restore the orignal screen:
			'DrawImage BackPhoto,0,0
			
		PopMatrix()
		' function returns the photo:
		Return loc.Photo
	End



	
	Function DrawText:Void(Text$, X%, Y%, tmpFont:AngelFont)
		' function draws text on the screen
		' first time slow as AngelFont
		' following calls are 10 times faster
		'
		'
		
		
		' this lines would speed up if image is out of screen:
		'If Y&gt;DeviceHeight() Return
		'If X&gt;DeviceWidth() Return
		
		' find correct photo and display it
		For Local loc:TextPhoto = Eachin Liste
			If loc.Text = Text
				DrawImage loc.Photo, X, Y
				Return 
			Endif
		Next
		
		' not found? so add it first:
		Local tmpImage:Image = AddOne(Text, tmpFont)
		DrawImage tmpImage, X, Y   
	End
	
	
	
	Function TextGrab:Image(X%, Y%, Width%, Height%)
		' function grabs a white on black text from the screen
		' restores alpha informations
		' and return an image of that
		' 
		Local pixels:Int[] = New Int[Width*Height]
		Local ScreenShot:Image = CreateImage(Width, Height)
		
		' read screenshot:
		ReadPixels(pixels, X, Y, Width, Height)
		
		'  create alpha channel:
		For Local i%=0 To Width*Height-1
			If pixels[i]&lt;-65535
				Local Alpha% = (pixels[i] Mod 256)
				Alpha = Alpha*16777216
				If Alpha&lt;&gt;0
					Alpha = Alpha+16777215
				Endif
				pixels[i] = Alpha
			Endif
		Next
		
		' create an image from the results:
		ScreenShot.WritePixels(pixels, 0, 0, Width, Height)
		Return ScreenShot
	End
End
</textarea> <br><br></td></tr></table><br>
<a name="2053368"></a>

<a name="2053365"></a>

<a name="2053362"></a>

<a name="2053360"></a>

<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
