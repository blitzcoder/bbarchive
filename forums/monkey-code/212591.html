<!DOCTYPE html><html lang="en" ><head ><title >Dungeon Generator - Smart Placed Random Rectangle</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Dungeon Generator - Smart Placed Random Rectangle</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Dungeon Generator - Smart Placed Random Rectangle</a><br><br>
<a name="2123594"></a>

<a name="2123595"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pakz</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> <img src="http://cromdesi.home.xs4all.nl/images/linking/monkeyx/nexttomapgen.jpg"><br><br>This might be useful for further modification.<br><br>Very simple : <br><br>Create a rectangle on the map<br>enter loop<br>Create random rectangle<br>if it fits next to a previous placed rectangle<br>place there<br>exit if enough placed or counter hit max<br><br>It can be set to different map sizes.. (be sure to also set the tilewidth and height then)<br><br>I never got it this easy into code yet. The generator part is not that complicated now.<br><br><pre class=code>
Import mojo

Class tilemap	
	Field tilew:Int
	Field tileh:Int
	Field width:Int
	Field height:Int
	Field map:Int[][]
	Method New(width:Int,height:Int,tilew:Int,tileh:Int)
		Self.width=width
		Self.height=height
		Self.tilew=tilew
		Self.tileh=tileh
		map = New Int[width][]
		For Local i=0 Until width
			map[i] = New Int[height]
		Next
		makemap
		settiles
	End Method
	' Here we create the walls
	Method settiles()
		'create walls
		For Local y=1 Until height-1
		For Local x=1 Until width-1
			Local t:Int=map[x][y]
			If t=0 And map[x+1][y] = 1 Then map[x][y] = 2
			If t=0 And map[x][y+1] = 1 Then map[x][y] = 2
			If t=0 And map[x-1][y] = 1 Then map[x][y] = 2
			If t=0 And map[x][y-1] = 1 Then map[x][y] = 2
		Next
		Next
		' get corners
		For Local y=1 Until height-1
		For Local x=1 Until width-1
			Local t:Int=map[x][y]
			If t=0 And map[x+1][y] = 2 And map[x][y+1] = 2 Then map[x][y] = 3
			If t=0 And map[x-1][y] = 2 And map[x][y+1] = 2 Then map[x][y] = 3
			If t=0 And map[x][y-1] = 2 And map[x+1][y] = 2 Then map[x][y] = 3
			If t=0 And map[x-1][y] = 2 And map[x][y-1] = 2 Then map[x][y] = 3
		Next
		Next
		'set corners to value 2
		For Local y=1 Until height-1
		For Local x=1 Until width-1
			If map[x][y] = 3 Then map[x][y] = 2
		Next
		Next
	End Method
	Method makemap()
		'here we make random rectangles and see if they 
		'are placed next to another rectangle.
		'This will grow a random level...
		Local w1:Int=Rnd(1,4)
		Local h1:Int=Rnd(1,4)
		Local x1:Int=width/2
		Local y1:Int=height/2
		
		drawmaprect(x1,y1,w1,h1)
	
	
		Local maxrooms:Int=(width*height)/30
		Local numrooms:Int=0
		Local cnt:Int=0
		While numrooms &lt; maxrooms And cnt&lt;15000
			cnt+=1
			Local done:Bool=False
			w1=Rnd(1,4)
			h1=Rnd(1,4)
			x1=Rnd(3,width-5)
			y1=Rnd(3,height-5)
			'check right
			If map[x1][y1] = 1 And map[x1+1][y1] = 0 And map[x1+1][y1-2] = 0 And map[x1+1][y1+2] = 0 
				drawmaprect(x1,y1,w1,h1)
				numrooms+=1
				done=True
			End If
			'check bottom
			If done=False And map[x1][y1] = 1 And map[x1][y1+1] = 0 And map[x1-2][y1+1] = 0 And map[x1+2][y1+1] = 0
				drawmaprect(x1,y1,w1,h1)
				numrooms+=1
				done=True
			End If
			'check top
			If done=False And y1+h1&lt;height-1
				If map[x1][y1+h1+1] = 1 And map[x1][y1+h1] = 0 And map[x1-2][y1+h1] = 0  And map[x1+2][y1+h1] = 0
					drawmaprect(x1,y1,w1,h1)
					numrooms+=1
					done=True
				End If
			End If
			'check left
			If done=False And x1+w1&lt;width-1
				If map[x1+w1+1][y1] = 1 And map[x1+w1][y1] = 0 And map[x1+w1][y1-2] = 0 And map[x1+w1][y1+2] = 0
					drawmaprect(x1,y1,w1,h1)
					numrooms+=1					
				End If
			End If
		Wend

	End Method
	Method drawmaprect(x:Int,y:Int,w:Int,h:Int)
		For Local y1=y To y+h
		For Local x1=x To x+w
			If x1&gt;-1 And y1&gt;-1 And x1&lt;width And y1&lt;height
				map[x1][y1] = 1
			End If
		Next
		Next
	End Method
	Method draw()
		SetColor 255,255,255
		For Local y=0 Until height
		For Local x=0 Until width
			Local t:Int=map[x][y]
			Select t
				Case 1 'wall
				SetColor 100,100,100
				DrawRect x*tilew,y*tileh,tilew,tileh
				Case 2 'wall
				SetColor 200,200,200
				DrawRect x*tilew,y*tileh,tilew,tileh				
			End Select			
		Next
		Next
	End Method
End Class

Global m:tilemap

Class MyGame Extends App

    Method OnCreate()
    	Seed = GetDate[5]
        SetUpdateRate(60)
        createrandommap()
    End Method
    Method OnUpdate()  
    	If KeyHit(KEY_SPACE) Or MouseHit(MOUSE_LEFT)
	        createrandommap()
    	End If      
    End Method
    Method OnRender()
        Cls 0,0,0 
        m.draw
        SetColor 255,255,255
        DrawText "Press Mouse or Tab to create new level",0,0
    End Method
End Class

Function createrandommap()
	Local r:Int=Rnd(0,3)
	Local w:Int,h:Int,tw:Int,th:Int
	Select r
	Case 0 
		w=20;h=15;tw=32;th=32
	Case 1 
		w=20*2;h=15*2;tw=16;th=16
	Case 2 
		w=20*4;h=15*4;tw=8;th=8
	End Select
	m = New tilemap(w,h,tw,th)
End Function

Function Main()
    New MyGame()
End Function
</pre> <br><br></td></tr></table><br>
<a name="2123596"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nobuyuki</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I like the way this generator looks on its output.  I created a similar generator a while back, but it does a boolean operation and doesn't care much about the overlap.  As a result, the inside walls are a bit "different". <br><br>The only screen I could find was from before it supported multiple spawners.  Newer iterations had a lot more variety:  <a href="https://twitter.com/nobuyukinyuu/status/644410538739367936" target="_blank">https://twitter.com/nobuyukinyuu/status/644410538739367936</a> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
