<!DOCTYPE html><html lang="en" ><head ><title >Fractal Tree</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Fractal Tree</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Fractal Tree</a><br><br>
<a name="2026358"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Draws a fractal tree and it crudely sways in the wind. Try playing with the controls, they produce some cool effects!<br><br>SpaceKey         Starts / stops wind<br>Left / Right      Rotates tree<br>A / D               Changes angle difference (Very Cool Effect!!!)<br>- / +               Changes depth of tree, makes the tree shrink/grow <br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import mojo


Global ANGLE_DIF : Float = 20.0
Global SHAKE : Float = 0.0
Global SHAKE_STRENGTH : Float = 0.0
Global MAX_SHAKE_STRENGTH : Float = 1.0


'/ Recursively Draw Tree
Function DrawTree( x1 : Float, y1 : Float, angle : Float, depth : Int )
	SHAKE = SHAKE + 0.01
	If depth &lt;&gt; 0
		Local x2 : Float = x1 + ( Cos( angle ) * depth * 10.0 )
		Local y2 : Float = y1 + ( Sin( angle ) * depth * 10.0 )
		SetColor 0, 0, 0
		DrawThickLine( x1, y1, x2, y2, depth, True )
		DrawTree( x2, y2, angle - ANGLE_DIF + ( Sin( SHAKE ) * SHAKE_STRENGTH ), depth - 1 )
		DrawTree( x2, y2, angle + ANGLE_DIF + ( Sin( SHAKE ) * SHAKE_STRENGTH ), depth - 1 )
	Endif
End Function




'/ Draws thick line
Function DrawThickLine( x1 : Float, y1 : Float, x2 : Float, y2 : Float, thickness : Float = 2.0, rounded : Bool = False )
	Local dx : Float = x2 - x1
	Local dy : Float = y2 - y1
	Local d : Float = Sqrt( dx * dx + dy * dy )
	Local vx : Float = dx / d
	Local vy : Float = dy / d
	Local nx : Float = vy
	Local ny : Float = -vx
	Local points : Float[ 8 ]
	points[ 0 ] = x1 + ( nx * ( thickness / 2.0 ))
	points[ 1 ] = y1 + ( ny * ( thickness / 2.0 ))
	points[ 2 ] = x1 + ( -nx * ( thickness / 2.0 ))
	points[ 3 ] = y1 + ( -ny * ( thickness / 2.0 ))
	points[ 4 ] = x2 + ( -nx * ( thickness / 2.0 ))
	points[ 5 ] = y2 + ( -ny * ( thickness / 2.0 ))	
	points[ 6 ] = x2 + ( nx * ( thickness / 2.0 ))
	points[ 7 ] = y2 + ( ny * ( thickness / 2.0 ))
	DrawPoly( points )
	If rounded = True
		DrawEllipse x1, y1, thickness / 2.0, thickness / 2.0
		DrawEllipse x2, y2, thickness / 2.0, thickness / 2.0
	Endif
End Function







Class MyApp Extends App


	Field angle : Float = 0.0
	Field depth : Int = 9	
	Field mode : Bool = False


	Method OnCreate()
		SetUpdateRate 60		
	End Method
	
	
	Method OnUpdate()
		
		If KeyDown( KEY_LEFT )
			angle = angle - 1.0
		Endif
		
		If KeyDown( KEY_RIGHT )
			angle = angle + 1.0
		Endif
	
	
		If KeyDown( KEY_A )
			ANGLE_DIF = ANGLE_DIF - 1.0
		Endif
		
		If KeyDown( KEY_D )
			ANGLE_DIF = ANGLE_DIF + 1.0
		Endif	

		If KeyHit( KEY_MINUS )
			depth = depth - 1
			If depth &lt; 1 Then depth = 1
		Endif
		
		If KeyHit( KEY_EQUALS )
			depth = depth + 1
		Endif	
		
	
		If KeyHit( KEY_SPACE )
			mode = Not mode
		Endif
		
	End Method
	
	
	Method OnRender()
		Cls 255, 255, 255
		SetColor 0, 0, 0
		
		If mode
			If SHAKE_STRENGTH &lt; MAX_SHAKE_STRENGTH
				SHAKE_STRENGTH = SHAKE_STRENGTH + 0.01
			Endif
		Else
			If SHAKE_STRENGTH &gt; 0
				SHAKE_STRENGTH = SHAKE_STRENGTH - 0.01
			Endif
		Endif
		DrawTree( 300, 480, -90 + angle, depth )

	End Method


End Class



Function Main()
	New MyApp()
End Function

 
</textarea> <br><br></td></tr></table><br>
<a name="2026359"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >muddy_shoes</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Fun. Here, I had a play and added randomisation and an additional sway element (it's fun to have them both going as they interact). <br><br>'R' changes random seed. 'S' turns on the sway.<br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import mojo


Global ANGLE_DIF : Float = 20.0
Global SHAKE : Float = 0.0
Global SHAKE_STRENGTH : Float = 0.0
Global MAX_SHAKE_STRENGTH : Float = 1.0

Global DrawSeed:Int = 0
Global sway:Bool = False
Global sway_angle:Float = 0.0
Global sway_speed:Float = 0.15
Const MAX_SWAY:Float = 3.0
Const MIN_SWAY:Float = -MAX_SWAY


'/ Recursively Draw Tree
Function DrawTree(x1:Float, y1:Float, angle:Float, sway:Float, depth:Int)
	SHAKE = SHAKE + 0.01
	If depth &lt;&gt; 0
		Local x2:Float = x1 + (Cos(angle + sway) * depth * 10.0 * (Rnd() + 0.5))
		Local y2:Float = y1 + (Sin(angle + sway) * depth * 10.0 * (Rnd() + 0.5))
		SetColor 0, 0, 0
		DrawThickLine( x1, y1, x2, y2, depth, True )
	    sway *= 1.2
    	DrawTree(x2, y2, angle - (Rnd() + 0.5) * ANGLE_DIF + (Sin(SHAKE) * SHAKE_STRENGTH), sway, depth - 1)
		DrawTree(x2, y2, angle + (Rnd() + 0.5) * ANGLE_DIF + (Sin(SHAKE) * SHAKE_STRENGTH), sway, depth - 1)
	Endif
End Function




'/ Draws thick line
Function DrawThickLine( x1 : Float, y1 : Float, x2 : Float, y2 : Float, thickness : Float = 2.0, rounded : Bool = False )
	Local dx : Float = x2 - x1
	Local dy : Float = y2 - y1
	Local d : Float = Sqrt( dx * dx + dy * dy )
	Local vx : Float = dx / d
	Local vy : Float = dy / d
	Local nx : Float = vy
	Local ny : Float = -vx
	Local points : Float[ 8 ]
	points[ 0 ] = x1 + ( nx * ( thickness / 2.0 ))
	points[ 1 ] = y1 + ( ny * ( thickness / 2.0 ))
	points[ 2 ] = x1 + ( -nx * ( thickness / 2.0 ))
	points[ 3 ] = y1 + ( -ny * ( thickness / 2.0 ))
	points[ 4 ] = x2 + ( -nx * ( thickness / 2.0 ))
	points[ 5 ] = y2 + ( -ny * ( thickness / 2.0 ))	
	points[ 6 ] = x2 + ( nx * ( thickness / 2.0 ))
	points[ 7 ] = y2 + ( ny * ( thickness / 2.0 ))
	DrawPoly( points )
	If rounded = True
		DrawEllipse x1, y1, thickness / 2.0, thickness / 2.0
		DrawEllipse x2, y2, thickness / 2.0, thickness / 2.0
	Endif
End Function







Class MyApp Extends App


	Field angle : Float = 0.0
	Field depth : Int = 9	
	Field mode : Bool = False


	Method OnCreate()
		SetUpdateRate 30
	End Method
	
	
	Method OnUpdate()
		
		If KeyDown( KEY_LEFT )
			angle = angle - 1.0
		Endif
		
		If KeyDown( KEY_RIGHT )
			angle = angle + 1.0
		Endif
	
	
		If KeyDown(KEY_S)
			sway = Not sway
		Endif
		
        If KeyDown(KEY_A)
			ANGLE_DIF = ANGLE_DIF - 1.0
		Endif
		
		If KeyDown( KEY_D )
			ANGLE_DIF = ANGLE_DIF + 1.0
		Endif	

		If KeyHit( KEY_MINUS )
			depth = depth - 1
			If depth &lt; 1 Then depth = 1
		Endif
		
		If KeyHit( KEY_EQUALS )
			depth = depth + 1
		Endif	
		
        If KeyHit(KEY_R)
            DrawSeed = Int(Rnd() * 32768)
        End
	
		If KeyHit( KEY_SPACE )
			mode = Not mode
		Endif
		
	End Method
	
	
	Method OnRender()
		Cls 255, 255, 255
		SetColor 0, 0, 0
		
        If sway
            sway_angle += sway_speed
            If sway_angle &gt;= MAX_SWAY Or sway_angle &lt;= MIN_SWAY
                sway_speed *= - 1
            End
        End
        
		If mode
			If SHAKE_STRENGTH &lt; MAX_SHAKE_STRENGTH
				SHAKE_STRENGTH = SHAKE_STRENGTH + 0.01
			Endif
		Else
			If SHAKE_STRENGTH &gt; 0
				SHAKE_STRENGTH = SHAKE_STRENGTH - 0.01
			Endif
		Endif
        random.Seed = DrawSeed
		DrawTree(300, 480, -90 + angle, sway_angle, depth)

	End Method


End Class



Function Main()
	New MyApp()
End Function

 


</textarea> <br><br></td></tr></table><br>
<a name="2026361"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice. I didn't know you could assign a value to the seed, very handy! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
