<!DOCTYPE html><html lang="en" ><head ><title >A simple pathfinder (nothing fancy)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >A simple pathfinder (nothing fancy)</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >A simple pathfinder (nothing fancy)</a><br><br>
<a name="2086242"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Supertino</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I finally got around to creating an all in one Class for 2D pathfinding, it's nothing fancy and it's not a slick as A* or indeed anything more advanced.  This will always find the shortest route but it wont always be the nicest.<br><br>I don't know what the technique is called but basically it spirals out for the Start location until it finds the Goal location which does result in a lot of squares being checked that don't need to be, it should not have much of an impact on performance unless you're going with large arrays with dimensions in the several hundreds.<br><br>You can play a test app here <a href="http://moarlasers.com/path/MonkeyGame.html" target="_blank">http://moarlasers.com/path/MonkeyGame.html</a> [HTML5] and grab the source from here <a href="https://drive.google.com/folderview?id=0B1jBbicq8v_dbUtHWlNfZFh2Qm8&amp;usp=drive_web" target="_blank">https://drive.google.com/folderview?id=0B1jBbicq8v_dbUtHWlNfZFh2Qm8&amp;usp=drive_web</a><br><br><img src="http://s29.postimg.org/t1z16oizb/Capture.png"><br><br>It's requires you use an Array in the <b>array:Int[a][b]</b> format.<br><br><b>The Methods</b><br><pre class=code>
Prepare:Void(arr:Int[][], unpassable_vals:Int[], start_x:Int, start_y:Int, finish_x:Int, finish_y:Int) ' Call before you search
Search:Bool() ' Start the search, return True or False if path can be found
GetString:String() ' Get the Path as single string of U,D,L,R
Get:String[] () ' Get the Path as an Array containing U,D,L,R
</pre><br><br><b>The <i>Prepare</i> method needs a little explaining, lets assume this is being used for PacMan;</b><br>- The first argumant is requesting you feed in an array, this will typical be your level containing the walls, floors all represended with a value, walls are '1' floors as '2' for example, the Class will take a take a copy of this for it's needs and wont change the original.<br><br>- The second argument is where you can feed in values that you want the Class to treat as unpassable, such as walls<br><br>- The 3rd\4th is the array x&amp;y of the where you want to start your search from<br><br>- The 5th and 6th us the array x&amp;y where you want to end the search<br><br><br>The idea is you call <b>Prepare</b> then <b>Search</b> and get your Path info by calling <b>Get</b> or <b>GetString</b><br><br><br><br><b>The Class</b><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Class PathingClass
	Private
	Const PASSABLE:Int = 0, UNPASSABLE:Int = -1
	Field unpassable:Int[]
	Field arrData:Int[][]
	Field arrWidth:Int
	Field arrHeight:Int
	Field start:Int[2]
	Field finish:Int[2]
	Field arrCheckList:Int[][]
	Field path:String[]
	
	'Summary: Reset values and arrays (is not normally required to be called at any time)
	Method Reset:Void()
		For Local x:Int = 0 Until arrWidth
			For Local y:Int = 0 Until arrHeight
				arrData[x][y] = PASSABLE
				arrCheckList[x][y] = 0
			Next y
		Next x
		path = path.Resize(0)
	End Method
	
	'Summary: Return true if the value is an Unpassable value
	Method IsUnPassable:Bool(value:Int)
		For Local i:Int = 0 Until unpassable.Length
			If value = unpassable[i] Then Return True
		Next
	End Method
	
	'Summary: Trace back and complie the path
	Method TracePath:Void()
		Local Score:Int = arrData[finish[0]][finish[1]]
		Local x:Int = finish[0]
		Local y:Int = finish[1]
		'Store the directions
		path = path.Resize(Score)
		'Start from finish and work back to Start using Score
		Repeat
			Score = Score - 1
			If Score = 0 Then Exit ' Found Start so exit here
			If y - 1 &gt;= 0
				If arrData[x][y - 1] = Score Then path[Score - 1] = "D"; y = y - 1; Continue
			EndIf
			If x + 1 &lt; arrWidth
				If arrData[x + 1][y] = Score Then path[Score - 1] = "L"; x = x + 1; Continue
			EndIf
			If y + 1 &lt; arrHeight
				If arrData[x][y + 1] = Score Then path[Score - 1] = "U"; y = y + 1; Continue
			EndIf
			If x - 1 &gt;= 0
				If arrData[x - 1][y] = Score Then path[Score - 1] = "R"; x = x - 1; Continue
			EndIf
		Forever
	End Method
	
		'Summary:
	Method CheckNeighbours:Void(x:Int, y:Int, value:Int)
		'above
		If y - 1 &gt;= 0
			If arrData[x][y - 1] = PASSABLE
				arrData[x][y - 1] = value
			EndIf
		End If
		'right
		If x + 1 &lt; arrWidth
			If arrData[x + 1][y] = PASSABLE
				arrData[x + 1][y] = value
			EndIf	
		End If
		'below
		If y + 1 &lt; arrHeight
			If arrData[x][y + 1] = PASSABLE
				arrData[x][y + 1] = value
			EndIf
		End If
		'left
		If x - 1 &gt;= 0
			If arrData[x - 1][y] = PASSABLE
				arrData[x - 1][y] = value
			EndIf
		End If
	End Method
	
	'Summary: Create a return a 2 Dim array [x][y]
	Function AllocateArrayInt:Int[][] (i:Int, j:Int)
	    Local arr:Int[][] = New Int[i][]
	    For Local ind = 0 Until i
	        arr[ind] = New Int[j]
	    Next
    	Return arr
	End Function
	
	Public
	
	'Summary: Return the path as a String [Array] U,D,R,L
	Method Get:String[] ()
		Return path
	End Method
	
	'Summary: Return the Path U,D,R,L as a single String
	Method GetString:String()
		Local t:String
		For Local a:Int = 0 Until path.Length - 1
			t = t + path[a]
		Next
		Return t
	End Method
	
	'Summary: The main method that makes it all go
	Method Search:Bool()
		Const NEEDS_CHECKING:Int = 1, BEEN_CHECKED:Int = 2
		Local Score:Int
		Local NothingToCheck:Bool
		Local FinishFound:Bool
		
		Repeat
			' Increase Score
			Score = Score + 1
			' Ready this
			NothingToCheck = True
			' Update what positions to check
			For Local x:Int = 0 Until arrWidth
				For Local y:Int = 0 Until arrHeight
				 	If arrData[x][y] = Score Then arrCheckList[x][y] = NEEDS_CHECKING; NothingToCheck = False
				Next y
			Next x
			' All searchable sqaures have been checked so exit here
			If NothingToCheck = True Then FinishFound = False; Exit
			' The Finish has been found so lets stop the search here
			If arrCheckList[finish[0]][finish[1]] = NEEDS_CHECKING Then FinishFound = True; Exit
			' Check neighbouring squares
			For Local x:Int = 0 Until arrWidth
				For Local y:Int = 0 Until arrHeight
				 	If arrCheckList[x][y] = NEEDS_CHECKING Then CheckNeighbours(x, y, Score + 1); arrCheckList[x][y] = BEEN_CHECKED
				Next y
			Next x
		Forever	
		' Results
		If FinishFound = False
			Reset()
			Return False
		Else
			TracePath()
			Return True
		End If
		
	End Method
	
	'Summary: Call before you Search, sets up the all the data the Path Class requires
	Method Prepare:Void(arr:Int[][], unpassable_vals:Int[], start_x:Int, start_y:Int, finish_x:Int, finish_y:Int)
		' Get list of unpassable values
		unpassable = unpassable_vals
		' Set start &amp; finish locations
		start =[start_x, start_y]
		finish =[finish_x, finish_y]
		' Config the array
		arrWidth = arr.Length
		arrHeight = arr[1].Length
		arrData = AllocateArrayInt(arrWidth, arrHeight)
		arrCheckList = AllocateArrayInt(arrWidth, arrHeight)
		' Copy array values and set as PASSABLE\UNPASSBLE
		For Local x:Int = 0 Until arrWidth
			For Local y:Int = 0 Until arrHeight
				arrData[x][y] = arr[x][y]
				If IsUnPassable(arrData[x][y]) Then arrData[x][y] = UNPASSABLE Else arrData[x][y] = PASSABLE
			Next y
		Next x
		' Set start position as score '1' (start)
		arrData[start[0]][start[1]] = 1
	End Method
		
End Class
</textarea> <br><br></td></tr></table><br>
<a name="2086241"></a>

<a name="2086240"></a>

<a name="2086239"></a>

<a name="2086238"></a>

<a name="2086237"></a>

<a name="2086236"></a>

<a name="2086256"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Snader</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Great work! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
