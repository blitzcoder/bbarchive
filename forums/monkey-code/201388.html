<!DOCTYPE html><html lang="en" ><head ><title >Rudimentary Particle Emitter</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Rudimentary Particle Emitter</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Rudimentary Particle Emitter</a><br><br>
<a name="2012535"></a>

<a name="2012536"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Big Jim</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is a basic particle emitter.  <br>Feel free to use it or abuse it any way you wish.<br><br><pre class=code>

Import mojo

Class Particle
	Field X#, Y#, XD#, YD#
	Field Age#  = 1.0
	Field DecayRate#
	Field GravityX# = 0.0
	Field GravityY# =0.0

	Method New(x#, y#, Scatter)
		Self.X = x
		Self.Y = y
		Self.XD = Rnd(-Scatter, Scatter)
		Self.YD = Rnd(-Scatter, Scatter)
		Self.Age = Rnd()
		Self.DecayRate = Rnd(.005, .01)
	End Method

	Method Update()
		X += XD + GravityX
		Y += YD + GravityY
		If X &lt; 0 Or X &gt; DeviceWidth Then Age = 0
		If Y &lt; 0 Or Y &gt; DeviceWidth Then Age = 0
		Age -= DecayRate
	End Method

End Class

Class ParticleEmitter
	Field Particles:= New List&lt;Particle&gt;
	Field Scatter#, ParticleCount
	Field GravityX# = 0, GravityY# = 0
	Field ParticleImage:Image
	Field RemainingParticles
	
	Method New(x#, y#, scatter#, particleCount, particleImage:Image, gravX#, gravY#)
		GravityX = gravX
		GravityY = gravY
		For Local i = 0 To ParticleCount	
			Particles.AddLast(New Particle(x, y, scatter))
		Next
		Self.ParticleImage = particleImage
		RemainingParticles = particleCount
	End Method
	
	Method Update()
		Local LiveParticles:= New List&lt;Particle&gt;
		For Local p:Particle = Eachin Particles
			p.GravityX = GravityX
			p.GravityY = GravityY
			p.Update()
			If p.Age &gt; 0 Then LiveParticles.AddLast(p)
		Next
		Particles = LiveParticles
		RemainingParticles = Particles.Count
	End Method
	
	Method Render()
		For Local p:Particle = Eachin Particles
			SetAlpha p.Age
			SetBlend LightenBlend
			SetColor Rnd(100,255), Rnd(100,255), Rnd(100,255) 
			DrawImage (ParticleImage, p.X, p.Y,0,p.Age, p.Age)
		Next
	End Method

End Class

Class ParticleManager
	
	Field ParticleEmitters:= New List&lt;ParticleEmitter&gt;
	Field TotalParticles = 0
	
	Method AddParticleEmitter(x#, y#, scatter#, particleCount, particleImage:Image, gravX#, gravY#)
		ParticleEmitters.AddLast( New ParticleEmitter ( x, y, scatter, particleCount, particleImage, gravX, gravY ) )
	End Method
	
	Method Update()
		Local LivePE:= New List&lt;ParticleEmitter&gt;
		TotalParticles = 0
		For Local pe:= Eachin ParticleEmitters
			pe.Update()
			If pe.RemainingParticles &gt; 0 
				LivePE.AddLast(pe)
				TotalParticles += pe.RemainingParticles
			End If
		Next
		ParticleEmitters = LivePE

	End Method

	Method Render()
		For Local pe:= Eachin ParticleEmitters
			pe.Render()
		Next
	End Method


End Class
</pre><br><br>Here's a test program<br><pre class=code>

Import mojo
Import particleemitter

Class ParticleEmitterTester Extends App

	Field PMgr:ParticleManager = New ParticleManager
	Field Spark:Image
	
	Method OnCreate()
		SetUpdateRate 60
		Spark = LoadImage	("bluspark.png")
	End Method
	
	Method OnUpdate()
		Local gx# = 0.0, gy# = 0.0
		For Local i = 0 To 32
			If TouchDown(i)
#if TARGET = "android" Or TARGET = "ios" 
				gx = 2 * AccelX()				
				gy = 2* AccelY()
#endif
				PMgr.AddParticleEmitter(TouchX(i), TouchY(i), 2, 100, Spark, gx, gy)
			End If
		Next
		
		PMgr.Update()
		
	End Method
	
	Method OnRender()
		Cls 
		PMgr.Render()
	End Method 

End Class


Function Main()
	New ParticleEmitterTester
End Function

</pre> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
