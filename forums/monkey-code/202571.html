<!DOCTYPE html><html lang="en" ><head ><title >nPoly to Monkey</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >nPoly to Monkey</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >nPoly to Monkey</a><br><br>
<a name="2025462"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> here is the old nPoly library from BlitzBasic . Maybe somebody will find some use for it:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Strict
Import mojo

Function Main:Int()
	New Demo
	Return True
End Function


Class Demo Extends App
	Field gear1:Poly
	Field gear2:Poly
	Field lever:Poly
	Field Rectangle:Poly[]
	Field ry:Int[]
	Field box:Poly
	Field rad:Int
	
	Field maxRect:Int
	
	Method OnCreate:Int()

		rad = 15
		maxRect = 4

		gear1 = CreateGear()

		gear2 = gear1.Copy()
		gear2.Scale(0.7, 0.7)
		
		lever = CreateLever()

		Rectangle = New Poly[4]
		ry = New Int[maxRect]
		
		Rectangle[0] = CreateRectangle()
		ry[0] = 100

		For Local i:Int = 1 Until maxRect
			Rectangle[i] = Rectangle[0].Copy()
			ry[i] = ry[0]
		Next
		
		box = CreateBox()

		SetUpdateRate(30)

		Return True

	End Method
	
	Method OnUpdate:Int()

		' Gears
		gear1.Rotate(1)
		While gear1.Overlap(150, 150, gear2, 212, 150, False)
			gear2.Rotate(-1)
		Wend
	
		' Gear And lever
		If Not gear2.Overlap(212, 150, lever, 320, 130, False)
			lever.Rotate(-1)
		End If
		While gear2.Overlap(212, 150, lever, 320, 130, False)
			lever.Rotate(1)
		Wend
		
		' Rectangles with lever
		For Local i:Int = 0 Until maxRect
			If Not Rectangle[i].Overlap(230 + 22 * i, ry[i], lever, 320, 130, False)
				ry[i] = ry[i] + 3
			End If	
			While Rectangle[i].Overlap(230 + 22 * i, ry[i], lever, 320, 130, False)
				ry[i] = ry[i] - 1
			Wend
		Next
	
		If KeyDown(KEY_UP) Then
			box.Scale(1.01, 1.01)
		End If
		If KeyDown(KEY_DOWN) Then
			box.Scale(0.99, 0.99)
		End If
		If KeyDown(KEY_LEFT) Then
			box.Rotate(3)
		End If
		If KeyDown(KEY_RIGHT) Then
			box.Rotate(-3)
		End If

			
		Return True
	End Method
	
	Method OnRender:Int()
		Cls
	
		Local mx:Int = MouseX()
		Local my:Int = MouseY()
	
		gear1.Draw(150, 150, 255, 0, 0, False)
		gear2.Draw(212, 150, 255, 0, 0, False)
		lever.Draw(320, 130, 255, 0, 0, False)
		For Local i:Int = 0 Until maxRect
			Rectangle[i].Draw(230 + 22 * i, ry[i], 255, 0, 0, False)
		Next

		box.Draw(400, 50, 255, 255, 0, False)

		DrawText "Hover mouse about this box",400, 5
		DrawText "Arrow keys",400, 20

		If box.OverlapsCircle(400, 50, mx, my, rad, False) Or box.PointInPoly(400,50,mx,my,True)
			SetColor 0, 0, 255
		Else
			SetColor 255, 255, 255
		End If
		DrawOval mx - rad, my - rad, rad*2, rad*2
	
		Return True
	End Method


	Method CreateGear:Poly()
		Local gear:Poly = New Poly
		gear.AddVertex(0, -50, False)
		gear.AddVertex(10, -10, False)
		gear.AddVertex(50, 0, False)
		gear.AddVertex(10, 10, False)
		gear.AddVertex(0, 50, False)
		gear.AddVertex(-10, 10, False)
		gear.AddVertex(-50, 0, False)
		gear.AddVertex(-10, -10, False)
		gear.AddVertex(0, -50)
		Return gear
	End Method
	
	Method CreateLever:Poly()
		Local lever:Poly = New Poly
		lever.AddVertex(-100, 5, False)
		lever.AddVertex(0, 5, False)
		lever.AddVertex(0, -5, False)
		lever.AddVertex(-100, -5, False)
		lever.AddVertex(-100, 5)
		Return lever
	End Method
	
	Method CreateRectangle:Poly()
		Local Rectangle:Poly = New Poly
		Rectangle.AddVertex(-9, 20, False)
		Rectangle.AddVertex(9, 20, False)
		Rectangle.AddVertex(9, -20, False)
		Rectangle.AddVertex(-9, -20, False)
		Rectangle.AddVertex(-9, 20)
		Return Rectangle
	End Method	

	Method CreateBox:Poly()
		Local box:Poly = New Poly()
		box.AddVertex(0, 0, False)
		box.AddVertex(200, 0, False)
		box.AddVertex(170, 70, False)
		box.AddVertex(200, 200, False)
		box.AddVertex(30, 200, False)
		box.AddVertex(65, 130, False)
		box.AddVertex(0, 180, False)
		box.AddVertex(0, 0)
		Return box
	End Method	
	
End Class











'============================================= nPoly library follows ==========================================================








' nPoly - Polygonal Collision Library
' By Jocelyn 'GoSsE' Perreault, <a href="http://www.nuloen.com/" target="_blank">http://www.nuloen.com/</a>
' Last Updated: July 8 2004
' Version: 1.0.0

' This source code is copyrighted (c) To Jocelyn Perreault.
' It is FreeWare. You have no limitiations in regard To 
' applications/games that you Release/create with it, either
' they be freeware Or commercial. However, you cannot Release
' this source code under another name Or Release a similar
' library with it. It cannot be redistributed. The code may be 
' modified To be tweaked but you cannot distribute it. If you
' made a modification that you think the community would
' benifit, please let me know.
' If you make tons of money with a commercial project,
' a free copy wouldn't hurt ')
' I would love To have an e-mail And a place in the credits
' of your projects, but it is up To you.
' Thanks,
'                 Jocelyn "GoSsE Korupted" Perreault
'                  gosse@...
'                  Nuclear Loaded Entertainment
'                  <a href="http://www.nuloen.com/" target="_blank">http://www.nuloen.com/</a>
' Several modifications made by Me Jesse 'JumpMan' Perez
'
' ======================================================================
' Globals And Constants
' ======================================================================
Const nP_MAX:Int = 32			' Maximum number of vertices For a poly

' ======================================================================
' Different objects
' ======================================================================
Class PVector
	Field x:Float
	Field y:Float
End Class

' Polygon

Class Poly
	Field iN:PVector[nP_MAX]	' Vertices X position Y position
	Field fX:PVector[nP_MAX]	' Scaled And rotated X and Y position
	Field iVertexCount:Int		' Number of vertices
	Field fScaleX:Float			' X-Scale
	Field fScaleY:Float			' Y-Scale
	Field fAngle:Float			' Angle rotation
	Field iMinX:Int				' Bounding box
	Field iMinY:Int				' Bounding box
	Field iMaxX:Int				' Bounding box
	Field iMaxY:Int				' Bounding box
	Field lateBoundingBoxCheck:Int =False
	' ======================================================================
	' Functions
	' ======================================================================

	' Creates a New empty polygon
	Method New()
		fScaleX = 1
		fScaleY = 1
	End Method

	' ----------------------------------------------------------------------
	
	' Adds a vertex To a polygon
	Method AddVertex:Void(x:Float, y:Float, bUpdate:Int = True)
		If iVertexCount &gt; nP_MAX Then
			Error "Cannot add one more vertex to polygon!"
		Else
			iN[iVertexCount] = New PVector
			fX[iVertexCount] = New PVector
			iN[iVertexCount].x = x
			iN[iVertexCount].y = y
			iVertexCount += 1
			If bUpdate Then
				Update()
			End If
		End If
	End Method

	' ----------------------------------------------------------------------
	' Checks If 2 polygons overlap each other
	
	Method Overlap:Int(x1:Float, y1:Float, poly2:Poly, x2:Float, y2:Float, bCheckBounding:Int = True)
	
		'Temp positions
		Local tx1:Float, ty1:Float, tx2:Float, ty2:Float, tx3:Float, ty3:Float, tx4:Float, ty4:Float
		Local xi:Float,yi:Float,xj:Float,yj:Float,nx:Float,ny:Float
		Local i:Int, j:Int, k:Int, c:Int
		' Checks bounding box first (If specified)
		If bCheckBounding Then	
			tx1 = x1 + iMinX
			ty1 = y1 + iMinY
			tx2 = x1 + iMaxX
			ty2 = y1 + iMaxY
			tx3 = x2 + poly2.iMinX
			ty3 = y2 + poly2.iMinY
			tx4 = x2 + poly2.iMaxX
			ty4 = y2 + poly2.iMaxY
			If Not (tx3 &gt;= tx1 And ty3 &gt;= ty1 And tx3 &lt;= tx2 And ty3 &lt;= ty2) Then
				If Not (tx4 &gt;= tx1 And ty4 &gt;= ty1 And tx4 &lt;= tx2 And ty4 &lt;= ty2) Then
					If Not (tx3 &gt;= tx1 And ty4 &gt;= ty1 And tx3 &lt;= tx2 And ty4 &lt;= ty2) Then
						If Not (tx4 &gt;= tx1 And ty3 &gt;= ty1 And tx4 &lt;= tx2 And ty3 &lt;= ty2) Then
							Return False
						End If
					End If
				End If
			End If
		End If
		For k = 0 To poly2.iVertexCount -1
			nx = poly2.fX[k].x+x2
			ny = poly2.fX[k].y+y2
			j = iVertexCount -1
			For i = 0 To iVertexCount - 1
   	    		xi = fX[i].x + x1
				yi = fX[i].y + y1
				xj = fX[j].x + x1
				yj = fX[j].y + y1
				If ((((yi&lt;=ny) And (ny&lt;yj)) Or ((yj&lt;=ny) And (ny&lt;yi))) And 
					(nx &lt; (xj - xi) * (ny - yi) / (yj - yi) + xi))
					c = Not c
				Endif
				j = i
   		 	Next
   		 	If c Return c
		Next
		For k=0 To iVertexCount -1
			nx = fX[k].x+x1
			ny = fX[k].y+y1
			j = poly2.iVertexCount - 1
			For i = 0 To poly2.iVertexCount - 1
    	   		xi = poly2.fX[i].x + x2
				yi = poly2.fX[i].y + y2
				xj = poly2.fX[j].x + x2
				yj = poly2.fX[j].y + y2
				If ((((yi&lt;=ny) And (ny&lt;yj)) Or ((yj&lt;=ny) And (ny&lt;yi))) And
					(nx &lt; (xj - xi) * (ny - yi) / (yj - yi) + xi)) c = Not c
				j = i
    		Next
    		If c Return c
		Next
		Return False
	End Method

	' ----------------------------------------------------------------------

	Method OverlapsCircle:Int(X:Float, Y:Float, iCircleX:Float, iCircleY:Float, iCircleRadius:Float, bCheckBounding:Int = True)
		' Temp positions
		Local tx1:Float, ty1:Float, tx2:Float, ty2:Float, tx3:Float, ty3:Float, tx4:Float, ty4:Float
		Local CX1:Float, CY1:Float, CX2:Float, CY2:Float, pX1:Float, pY1:Float, pX2:Float, pY2:Float, pXT:Float, pYT:Float
		' Delta calculations
		Local d1:Float, d2:Float, dX:Float, dY:Float, dSqr:Float
		' Checks bounding box first (If specified)
		If bCheckBounding Then
			tx1 = X + iMinX
			ty1 = Y + iMinY
			tx2 = X + iMaxX
			ty2 = Y + iMaxY
			tx3 = iCircleX - iCircleRadius
			ty3 = iCircleY - iCircleRadius
			tx4 = iCircleX + iCircleRadius
			ty4 = iCircleY + iCircleRadius
			If Not (tx3 &gt;= tx1 And ty3 &gt;= ty1 And tx3 &lt;= tx2 And ty3 &lt;= ty2) Then
				If Not (tx4 &gt;= tx1 And ty4 &gt;= ty1 And tx4 &lt;= tx2 And ty4 &lt;= ty2) Then
					If Not (tx3 &gt;= tx1 And ty4 &gt;= ty1 And tx3 &lt;= tx2 And ty4 &lt;= ty2) Then
						If Not (tx4 &gt;= tx1 And ty3 &gt;= ty1 And tx4 &lt;= tx2 And ty3 &lt;= ty2) Then
							Return False
						End If
					End If
				End If
			End If
		End If
		' Store the last vertex &amp; circle
		Local lx:Float = fX[0].x + X
		Local ly:Float = fX[0].y + Y
		CX2 = iCircleX
		CY2 = iCircleY
		' Cycle through all other vertices
		For Local i:Int = 0 To iVertexCount - 1	
			CX1 = fX[i].x + X
			CY1 = fX[i].y + Y
			pX1 = lx - CX1
			pY1 = ly - CY1
			pX2 = CX2 - CX1
			pY2 = CY2 - CY1
			d1 = pX1 * pX2 + pY1 * pY2
			If d1 &lt;= 0 Then
				dX = CX2 - CX1
				dY = CY2 - CY1
				dSqr = dX * dX + dY * dY
			Else
				d2 = pX1 * pX1 + pY1 * pY1
				If (d2 &lt;= d1) Then
					dX = CX2 - lx
					dY = CY2 - ly
					dSqr = dX * dX + dY * dY
				Else
					d1 = d1 / d2
					pXT = CX1 + d1 * pX1
					pYT = CY1 + d1 * pY1
					dX = CX2 - pXT
					dY = CY2 - pYT
					dSqr = dX * dX + dY * dY
				End If
			End If
			If dSqr &lt; iCircleRadius * iCircleRadius Then
				Return True
			End If
			lx = CX1
			ly = CY1
		Next
		Return False	
	End Method

	' ----------------------------------------------------------------------
	Method  PointInPoly:Int(polyx:Float,polyy:Float,pointx:Float,pointy:Float,bCheckBounding:Int = True)
      
      	Local tx1:Float, ty1:Float, tx2:Float, ty2:Float
		Local xi:Float,yi:Float,xj:Float,yj:Float,nx:Float,ny:Float
		Local i:Int, j:Int, k:Int
		' Checks bounding box first (If specified)
		If bCheckBounding Then	
			tx1 = polyx + iMinX
			ty1 = polyy + iMinY
			tx2 = polyx + iMaxX
			ty2 = polyy + iMaxY
			If Not (pointx &gt;= tx1 And pointy &gt;= ty1 And pointx &lt;= tx2 And pointy &lt;= ty2) Then
				If Not (pointx &gt;= tx1 And pointy &gt;= ty1 And pointx &lt;= tx2 And pointy &lt;= ty2) Then
					If Not (pointx &gt;= tx1 And pointy &gt;= ty1 And pointx &lt;= tx2 And pointy &lt;= ty2) Then
						If Not (pointx &gt;= tx1 And pointy &gt;= ty1 And pointx &lt;= tx2 And pointy &lt;= ty2) Then
							Return False
						End If
					End If
				End If
			End If
		End If
		j = iVertexCount -1
		Local collided:Int = False
		For i = 0 To iVertexCount - 1
      	   	xi = fX[i].x + polyx
			yi = fX[i].y + polyy
			xj = fX[j].x + polyx
			yj = fX[j].y + polyy    
			If ((((yi&lt;=pointy) And (pointy&lt;yj)) Or ((yj&lt;=pointy) And (pointy&lt;yi))) And
				(pointx &lt; (xj - xi) * (pointy - yi) / (yj - yi) + xi)) collided = Not collided
			j = i
      	Next
      	Return collided
		
	End Method
	
	' ---------------------------------------------------------------------- 	
	
	' Scales the polygon by the specified scale
	Method Scale:Void(sx:Float, sy:Float, bUpdate:Int = True)
		fScaleX = fScaleX * sx
		fScaleY = fScaleY * sy
		If bUpdate Then
			Update()
		End If
	End Method
	
	' ----------------------------------------------------------------------
	
	' Resizes the polygon To the specieid scale
	Method Resize:Void(sx:Float, sy:Float, bUpdate:Float = True)
		fScaleX = sx
		fScaleY = sy
		If bUpdate Then
			Update()
		End If
	End Method

	' ----------------------------------------------------------------------
	
	' Rotates the polygon by the specified rotation angle
	Method Rotate:Void(fRot:Float, bUpdate:Int = True)
		fAngle += fRot
		If bUpdate Then
			Update()
		End If
	End Method

	' ----------------------------------------------------------------------
	
	' Turns the polygon To the specified angle
	Method FreeRotate:Void(fAngle:Float, bUpdate:Int = True)
		Self.fAngle = fAngle
		If bUpdate Then
			Update()
		End If
	End Method
	
	' ----------------------------------------------------------------------
	
	' Updates the polygon according To scale And rotation
	Method Update:Void()
		
		Local fX:Float, fY:Float, f1:Float, f2:Float
		Local i:Int
		' Set up big extremums
		iMinX = 9999999
		iMinY = 9999999
		iMaxX = -9999999
		iMaxY = -9999999
		' Find a valid collision setting
		For i = 0 To iVertexCount - 1
			' Standard data
			fX = iN[i].x
			fY = iN[i].y
			' Scale
			fX = fX * fScaleX
			fY = fY * fScaleY
			' Rotation
			If fAngle &lt;&gt; 0 Then
				f1 = Cos(fAngle) * fX - Sin(fAngle) * fY
				f2 = Sin(fAngle) * fX + Cos(fAngle) * fY
				fX = f1
				fY = f2
			End If
			' Final Values
			Self.fX[i].x = fX
			Self.fX[i].y = fY
			' Check For bounding box
			If fX &lt; iMinX Then
				iMinX = fX
			End If
			If fY &lt; iMinY Then
				iMinY = fY
			End If
			If fX &gt; iMaxX Then
				iMaxX = fX
			End If
			If fY &gt; iMaxY Then
				iMaxY = fY
			End If
		Next
	End Method
	
	' ----------------------------------------------------------------------
	
	' Draws the polygon on screen at the specified location
	Method Draw:Void(x:Float, y:Float, r:Int=255, g:Int=255, b:Int=255, bDrawBoundingBox:Int = True, br:Int=255, bg:Int=255, bb:Int=255)
		Local i:Int, lx:Float, ly:Float
		' Draw bounding box (If specified)
		If bDrawBoundingBox Then
			SetColor br, bg, bb
			Local x1:Int = iMinX + x
			Local y1:Int = iMinY + y
			Local x2:Int = iMaxX + x
			Local y2:Int = iMaxY + y
			DrawLine x1,y1,x2,y1
			DrawLine x1,y1,x1,y2
			DrawLine x1,y2,x2,y2
			DrawLine x2,y1,x2,y2
	'		DrawRect nPoly.iMinX + x, nPoly.iMinY + y, nPoly.iMaxX - nPoly.iMinX + 1, nPoly.iMaxY - nPoly.iMinY + 1
		End If	
		' Draw all the lines	
		SetColor r, g, b
		lx = fX[0].x + x
		ly = fX[0].y + y
		For i = 1 To iVertexCount - 1
			DrawLine lx, ly, fX[i].x + x, fX[i].y + y
			lx = fX[i].x + x
			ly = fX[i].y + y
		Next
	End Method
	
	' ----------------------------------------------------------------------
	
	' Copies a polygon And Return the newly created poly
	Method Copy:Poly()
		Local newPoly:Poly = New Poly
		Local i:Float
		' Copy point data
		For i = 0 To iVertexCount - 1
			newPoly.iN[i] = New PVector
			newPoly.fX[i] = New PVector
			newPoly.iN[i].x = iN[i].x
			newPoly.iN[i].y = iN[i].y
			newPoly.fX[i].x = fX[i].x
			newPoly.fX[i].y = fX[i].y
		
		Next
		' Copy all data
		newPoly.iVertexCount = iVertexCount
		newPoly.fScaleX = fScaleX
		newPoly.fScaleY = fScaleY
		newPoly.fAngle = fAngle
		newPoly.iMinX = iMinX
		newPoly.iMinY = iMinY
		newPoly.iMaxX = iMaxX
		newPoly.iMaxY = iMaxY
		Return newPoly
	End Method
	
	
End Class

' ----------------------------------------------------------------------
' Returns True If both circles overlap

Function CirclesOverlap:Int(x1:Float, y1:Float, rad1:Float, x2:Float, y2:Float, rad2:Float)
	
	Local dx:Float = x2 - x1
	Local dy:Float = y2 - y1
	Local rsqr:Float = rad1 + rad2
	rsqr = rsqr*rsqr
	Return (dx*dx+dy*dy &lt; rsqr)

End Function

</textarea> <br><br></td></tr></table><br>
<a name="2025453"></a>

<a name="2025447"></a>

<a name="2025451"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey Jesse, just tried this.<br><br>I HTML5 (Chrome) I get: Monkey runtime error: TypeError: Cannot call method 'KeyDown' of null<br><br>And in GLFW I get: Monkey runtime error: Rendering operations can only be performed inside OnRender <br><br></td></tr></table><br>
<a name="2025452"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> are you sure?<br>it works fine here in Chrome and Safari with Monkey51 and Monkey56b.<br><br>maybe you have a project locked? <br><br></td></tr></table><br>
<a name="2025454"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CopperCircle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I get the same error in Chrome. <br><br></td></tr></table><br>
<a name="2025455"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I works fine here. Chrome(HTML5,Flash),Safari(HTML5,Flash), and GLFW.<br><br>maybe it's because I am on a Mac LOL. <br><br>I really don't know what can be wrong. Any Idea? <br><br></td></tr></table><br>
<a name="2025456"></a>

<a name="2025458"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> on glfw I get 'Rendering operations can only be performed inside OnRender<br>'<br>in firefox on windows I get null input device, weird, <br><br></td></tr></table><br>
<a name="2025457"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >muddy_shoes</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Try "Import mojo", not "Import Mojo". <br><br></td></tr></table><br>
<a name="2025459"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> ah yes very good thanks <br><br></td></tr></table><br>
<a name="2025464"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> that fixed it.<br><br>I wonder why I can use Mojo on Mac and not on Windows?<br>I thought Mac is the one that supposed to be case sensitive. <br><br></td></tr></table><br>
<a name="2025463"></a>

<a name="2025460"></a>

<a name="2025461"></a>

<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
