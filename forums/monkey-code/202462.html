<!DOCTYPE html><html lang="en" ><head ><title >2D Shapes / Outlines / Lines with thickness</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >2D Shapes / Outlines / Lines with thickness</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >2D Shapes / Outlines / Lines with thickness</a><br><br>
<a name="2024610"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I became slightly distracted from what I was meant to be doing and ended up writing some shape functions. There are two versions provided for drawing a thick line, one uses sin cos, the other uses vector math. Haven't done a speed test yet so I have left them both in there. Use as you wish, enjoy :)<br><br>Could do with some optimising but I need to get back to what I was meant to be doing!<br><br>[edit] You may notice that after some DrawPoly commands there is a DrawLine command. This is to hide the seams that you get on HTLM5 target so it can be deleted for other targets.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import mojo



Function DrawRectOutline( x : Float, y : Float, w : Float, h : Float, thickness : Float = 1.0 )
	If thickness = 1.0
		DrawLine x, y, x + w, y
		DrawLine x, y, x, y + h
		DrawLine x + w, y, x + w, y + h
		DrawLine x, y + h, x + w, y + h
	Else
		DrawThickLine( x, y, x + w, y, thickness, True )
		DrawThickLine( x, y, x, y + h, thickness, True )
		DrawThickLine( x + w, y, x + w, y + h, thickness, True )
		DrawThickLine( x, y + h, x + w, y + h, thickness, True )
	Endif
End Function



'/ Draws An Arc Outline. Can be used to draw circle outline as well
Function DrawArcOutline( x : Float, y : Float, xRad : Float, yRad : Float, aStart : Float = 0.0, aEnd : Float = 360.0, segments : Int = 8, thickness : Float = 1.0 )
	Local x1 : Float = x + ( Sin( aStart ) * xRad )
	Local y1 : Float = y + ( Cos( aStart ) * yRad )
	Local x2 : Float
	Local y2 : Float
	Local div : Float  = ( aEnd - aStart ) / segments
	For Local i : Int = 1 To segments
		x2 = x + ( Sin( aStart + ( i * div )) * xRad )
		y2 = y + ( Cos( aStart + ( i * div )) * yRad )
		If thickness = 1.0
			DrawLine x1, y1, x2, y2
		Else
			DrawThickLine( x1, y1, x2, y2, thickness, True )
		Endif
		x1 = x2
		y1 = y2
	Next
End Function


'/ Draws a Donut, can also be used to draw Donut Segment
Function DrawDonut( x : Float, y : Float, aRad : Float, bRad : Float, aStart : Float = 0.0, aEnd : Float = 360.0, segments : Int = 8 )
	Local points : Float[ 8 ]
	points[ 0 ] = x + Float( Sin( aStart ) * aRad )
	points[ 1 ] = y + Float( Cos( aStart ) * aRad )
	points[ 2 ] = x + Float( Sin( aStart ) * bRad )
	points[ 3 ] = y + Float( Cos( aStart ) * bRad )
	Local div : Float  = ( aEnd - aStart ) / segments
	For Local i : Int = 1 To segments
		points[ 4 ] = x + Float( Sin( aStart + ( i * div )) * bRad )
		points[ 5 ] = y + Float( Cos( aStart + ( i * div )) * bRad )		
		points[ 6 ] = x + Float( Sin( aStart + ( i * div )) * aRad )
		points[ 7 ] = y + Float( Cos( aStart + ( i * div )) * aRad )
		DrawPoly( points )
		DrawLine points[ 4 ], points[ 5 ], points[ 6 ], points[ 7 ]
		points[ 0 ] = points[ 6 ]
		points[ 1 ] = points[ 7 ]
		points[ 2 ] = points[ 4 ]
		points[ 3 ] = points[ 5 ]
	Next
End Function




'/ Draws a circle with sides or pie segment
Function DrawPie( x : Float, y : Float, xRadius : Float, yRadius : Float, aStart : Float = 0.0, aEnd : Float = 360.0, segments : Int = 8 )
	Local points : Float[( segments * 2 ) + 4 ]
	points[ 0 ] = x
	points[ 1 ] = y
	points[ 2 ] = x + Sin( aStart ) * xRadius
	points[ 3 ] = y - Cos( aStart ) * yRadius
	Local div : Float  = ( aEnd - aStart ) / segments
	For Local i : Int = 1 To segments
		points[( i + 1 ) * 2 ] = x + Sin( aStart + ( i * div )) * xRadius
		points[(( i + 1 ) * 2 ) + 1 ] = y - Cos( aStart + ( i * div )) * yRadius
	Next
	DrawPoly( points )
End Function




'/ Draws outline of pie
Function DrawPieOutline( x : Float, y : Float, xRad : Float, yRad : Float, aStart : Float = 0.0, aEnd : Float = 360.0, segments : Int = 8, thickness : Float = 1.0 )
	Local x1 : Float = x + ( Sin( aStart ) * xRad )
	Local y1 : Float = y + ( Cos( aStart ) * yRad )
	Local x2 : Float
	Local y2 : Float
	If thickness = 1.0
		DrawLine x, y, x1, y1
	Else
		DrawThickLine( x, y, x1, y1, thickness, True )
	Endif
	Local div : Float  = ( aEnd - aStart ) / segments
	For Local i : Int = 1 To segments
		x2 = x + ( Sin( aStart + ( i * div )) * xRad )
		y2 = y + ( Cos( aStart + ( i * div )) * yRad )
		If thickness = 1.0
			DrawLine x1, y1, x2, y2
		Else
			DrawThickLine( x1, y1, x2, y2, thickness, True )
		Endif
		x1 = x2
		y1 = y2
	Next
	If thickness = 1.0
		DrawLine x, y, x2, y2
	Else
		DrawThickLine( x, y, x2, y2, thickness, True )
	Endif
End Function




'/ Draws Star
Function DrawStarOutline( x : Float, y : Float, aRadius : Float, bRadius : Float, segments : Int = 5, thickness = 1.0 )

	Local div : Float  = 360 / segments

	Local x1 : Float = x + Sin( 0.0 ) * aRadius
	Local y1 : Float = y + Cos( 0.0 ) * aRadius
	Local x2 : Float
	Local y2 : Float

	For Local i : Int = 1 To segments
		
		x2 = x + Sin( i * div ) * aRadius
		y2 = y + Cos( i * div ) * aRadius
		
		Local mid_a : Float = (( i * div ) + (( i - 1 ) * div )) / 2.0
		Local mx : Float = x + Sin( mid_a ) * bRadius
		Local my : Float = y + Cos( mid_a ) * bRadius

		If thickness = 1.0
			DrawLine x1, y1, mx, my
			DrawLine x2, y2, mx, my
		Else
			DrawThickLine( x1, y1, mx, my, thickness, True )
			DrawThickLine( x2, y2, mx, my, thickness, True )
		Endif
		
		x1 = x2
		y1 = y2

	Next


End Function




'/ Draws Star
Function DrawStar( x : Float, y : Float, aRadius : Float, bRadius : Float, segments : Int = 5 )
	Local div : Float  = 360 / segments
	Local points : Float[ 8 ]
	points[ 0 ] = x
	points[ 1 ] = y
	points[ 2 ] = x + Sin( 0.0 ) * aRadius
	points[ 3 ] = y + Cos( 0.0 ) * aRadius
	Local mid : Float = div / 2.0
	For Local i : Int = 1 To segments
		points[ 4 ] = x + Sin( mid ) * bRadius
		points[ 5 ] = y + Cos( mid ) * bRadius
		points[ 6 ] = x + Sin( i * div ) * aRadius
		points[ 7 ] = y + Cos( i * div ) * aRadius
		DrawPoly( points )
		DrawLine x, y, points[ 2 ], points[ 3 ]
		points[ 2 ] = points[ 6 ]
		points[ 3 ] = points[ 7 ]
		mid = mid + div
	Next
End Function






'/ Draws thick line, rounded ends optional
Function DrawThickLine( x1 : Float, y1 : Float, x2 : Float, y2 : Float, thickness : Float = 2.0, rounded : Bool = False )
	Local dx : Float = x2 - x1
	Local dy : Float = y2 - y1
	Local d : Float = Sqrt( dx * dx + dy * dy )
	Local vx : Float = dx / d
	Local vy : Float = dy / d
	Local nx : Float = vy
	Local ny : Float = -vx
	Local points : Float[ 8 ]
	points[ 0 ] = x1 + ( nx * ( thickness / 2.0 ))
	points[ 1 ] = y1 + ( ny * ( thickness / 2.0 ))
	points[ 2 ] = x1 + ( -nx * ( thickness / 2.0 ))
	points[ 3 ] = y1 + ( -ny * ( thickness / 2.0 ))
	points[ 4 ] = x2 + ( -nx * ( thickness / 2.0 ))
	points[ 5 ] = y2 + ( -ny * ( thickness / 2.0 ))	
	points[ 6 ] = x2 + ( nx * ( thickness / 2.0 ))
	points[ 7 ] = y2 + ( ny * ( thickness / 2.0 ))
	DrawPoly( points )
	If rounded = True
		DrawEllipse x1, y1, thickness / 2.0, thickness / 2.0
		DrawEllipse x2, y2, thickness / 2.0, thickness / 2.0
	Endif
End Function



'/ Draws thick line, rounded ends optional
Function DrawThickLine2( x1 : Float, y1 : Float, x2 : Float, y2 : Float, thickness : Float = 2.0, rounded : Bool = False )
	Local da : Float = 90 - ATan2( y2 - y1, x2 - x1 )
	Local points : Float[ 8 ]
	points[ 0 ] = x1 + ( Sin( da - 90 ) * ( thickness / 2.0 ))
	points[ 1 ] = y1 + ( Cos( da - 90 ) * ( thickness / 2.0 ))
	points[ 2 ] = x1 + ( Sin( da + 90 ) * ( thickness / 2.0 ))
	points[ 3 ] = y1 + ( Cos( da + 90 ) * ( thickness / 2.0 ))
	points[ 4 ] = x2 + ( Sin( da + 90 ) * ( thickness / 2.0 ))
	points[ 5 ] = y2 + ( Cos( da + 90 ) * ( thickness / 2.0 ))
	points[ 6 ] = x2 + ( Sin( da - 90 ) * ( thickness / 2.0 ))
	points[ 7 ] = y2 + ( Cos( da - 90 ) * ( thickness / 2.0 ))
	DrawPoly( points )
	If rounded = True
		DrawEllipse x1, y1, thickness / 2.0, thickness / 2.0
		DrawEllipse x2, y2, thickness / 2.0, thickness / 2.0
	Endif
End Function











Class MyApp Extends App


	Method OnCreate()
		SetUpdateRate 60
	End Method
	

	Method OnUpdate()
	End Method


	Method OnRender()
		Cls 0, 0, 0
		
		
		'Draws a Pie segmentsment at 200x200 with radius 100, start angle 45, end angle 90, segmentsments 8
		SetColor 255, 255, 0
		DrawPie( 200, 200, 100, 100, 45, 100, 8 )


		'Draws pie outline at 400x200 with xradius 100, yradius 100, start angle 45, end angle 100, segmentsments 8 and thickness 1.0
		SetColor 0, 255, 0
		DrawPieOutline( 400, 200, 100, 100, 45.0, 100.0, 8, 1.0 )
		DrawPieOutline( 415, 205, 75, 75, 45.0, 100.0, 8, 4.0 )
		SetColor 0, 160, 0
		DrawPieOutline( 500, 390, 75, 75, 45.0, 100.0, 8, 50.0 )

		'Draws circle outline at 50x100 with xradius 50, yradius 100, start angle 0, end angle 360 with 32 segmentsments and thickness of 4
		SetColor 255, 0, 0
		DrawArcOutline( 50, 100, 50, 100, 0, 360, 32, 4 )
		DrawArcOutline( 50, 100, 30, 70, 0, 360, 32, 1 )


		'Draws Arc Outline at 175x70 with xradius 50, yradius 50, startAngle 90, endAngle 360 and segmentsments 12
		SetColor 255, 0, 255
		DrawArcOutline( 175, 70, 50, 50, 90, 360, 12, 1.0 )
		DrawArcOutline( 175, 70, 30, 30, 90, 360, 12, 6.0 )

		'Draws Donut at 150x370 with xradius 80, yradius 60, start angle 0, end angle 360 and 24 segmentsments
		SetColor 0, 0, 255
		DrawDonut( 150, 370, 80, 60, 0, 360, 24 )

		'Another donut, this one is a segmentsment
		SetColor 150, 150, 255
		DrawDonut( 150, 370, 40, 30, 50, 160, 8 )
		
		
		'Draws Thick Lines with rounded flag true
		SetColor 0, 255, 255
		DrawThickLine( 100, 100, 320, 460, 10, True )
		DrawThickLine( 320, 460, 530, 260, 10, True )
		SetColor 150, 100, 0
		DrawThickLine( 300, 300, 380, 320, 80, True )


		'Draws Thick Line not rounded		
		SetColor 255, 0, 255
		DrawThickLine( 400, 70, 560, 100, 50 )

		
		
		'Draws Rect outline
		SetColor 255, 100, 180
		DrawRectOutline( 300, 10, 70, 40, 1.0 )
		DrawRectOutline( 310, 20, 50, 20, 6.0 )

		'Draws a star
		SetColor 255, 255, 255
		DrawStarOutline( 575, 190, 30, 60, 5, 4 )
		DrawStar( 575, 190, 20, 40, 5 )
		DrawStar( 80, 250, 20, 25, 8 )
		
		
	End Method
	

End Class



Function Main()
	New MyApp()
End Function
</textarea> <br><br></td></tr></table><br>
<a name="2024442"></a>

<a name="2024437"></a>

<a name="2024378"></a>

<a name="2024377"></a>

<a name="2024375"></a>

<a name="2024376"></a>

<a name="2024436"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> [edit] Added a Star and StarOutline to the code above <br><br></td></tr></table><br>
<a name="2024441"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> thanks the star could be useful <br><br></td></tr></table><br>
<a name="2024443"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> no problem, I'm taking shape requests if you have any? This is a lot more fun that what I should be doing lol <br><br></td></tr></table><br>
<a name="2024449"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I just noticed there is a demo in the code, it looks great, cant think of any shapes at the moment <br><br></td></tr></table><br>
<a name="2024452"></a>

<a name="2024453"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> cheers, yea I though it would be good to show what the functions can do. Im tempted to add an Arrow... but I've just had another idea I want to try so maybe later.. <br><br></td></tr></table><br>
<a name="2024465"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DruggedBunny</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ooh, nice. I'm also thinking the star could be useful!<br><br>Perhaps a spiral with options like line thickness, number of rotations and spread of "rings" would be an idea? <br><br></td></tr></table><br>
<a name="2024611"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Perhaps a spiral with options like line thickness, number of rotations and spread of "rings" would be an idea? <br>  <br></div><br><br>Yea that sounds good! <br><br>[edit] Just fixed an error in the DrawPie function. <br><br></td></tr></table><br>
<a name="2024468"></a>

<a name="2024469"></a>

<a name="2034323"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nobuyuki</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> hi!<br><br>The pie functions' start and end points base themselves on the screen coordinate system, not the mathematical coordinate system.  Therefore, Y is inverted!  DrawArcOutline doesn't do this, so if you're drawing a pie and an arc directly afterwards with the same arguments, the pieces won't line up.<br><br>Thanks for the code, I'm borrowing those two functions and probably will extend it to draw rounded rectangles. <br><br></td></tr></table><br>
<a name="2034322"></a>

<a name="2034334"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nobuyuki</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Whew.  Okay, I changed some routines around so that they start from 3 O'clock and run counter-clockwise, which is how I expect these routines should work.  I swapped Cos/Sin in noodle's code in order to achieve this.  In the following code, DrawArc and DrawPie will draw to the same place when given the same arguments.  <br><br>Furthermore, included are 2 rounded rectangle routines, DrawRoundedRect, and FillRoundedRect.  Their usage should be fairly obvious, however, they will not draw correctly if the absolute value of the width or heights is less than r*2.  There are multiple ways to solve this;  I plan on adding in code to automatically reduce the radius size of the corners if W or H is too small, but I haven't yet because I don't know whether Floats are passed to the function by value or by reference, and I don't want to waste an extra Local variable if I don't have to.<br><br>[monkeycode]<br>Import mojo<br><br>Function DrawRectOutline:Void(x:Float, y:Float, w:Float, h:Float)<br>	DrawLine(x,y,x+w,y)<br>	DrawLine(x,y,x,y+h)<br>	DrawLine(x,y+h,x+w,y+h)<br>	DrawLine(x+w,y,x+w,y+h)<br>End Function<br><br>Function DrawArc:Void(x:Float, y:Float, xRad:Float, yRad:Float, aStart:Float=0.0, aEnd:Float = 360.0, segments:Int=8)<br>	Local x1:Float = x + ( Cos( aStart ) * xRad )<br>	Local y1:Float = y - ( Sin( aStart ) * yRad )<br>	Local x2:Float<br>	Local y2:Float<br>	Local div:Float  = ( aEnd - aStart ) / segments<br>	For Local i:Int = 1 To segments<br>		x2 = x + ( Cos( aStart + ( i * div )) * xRad )<br>		y2 = y - ( Sin( aStart + ( i * div )) * yRad )<br>			DrawLine(x1, y1, x2, y2)<br>		x1 = x2<br>		y1 = y2<br>	Next<br>End Function<br><br>'/ Draws a circle with sides or pie segment<br>Function DrawPie:Void(x:Float, y:Float, xRadius:Float, yRadius:Float, aStart:Float=0.0, aEnd:Float=360.0, segments:Int=8 )<br>	Local points:Float[( segments * 2 ) + 4 ]<br>	points[0] = x<br>	points[1] = y<br>	points[2] = x + Cos(aStart) * xRadius<br>	points[3] = y - Sin(aStart) * yRadius<br>	Local div:Float = (aEnd - aStart) / segments<br>	For Local i:Int = 1 To segments<br>		points[( i + 1 ) * 2 ] = x + Cos( aStart + ( i * div )) * xRadius<br>		points[(( i + 1 ) * 2 ) + 1 ] = y - Sin( aStart + ( i * div )) * yRadius<br>	Next<br>	DrawPoly( points )<br>End Function<br><br>Function DrawRoundedRect:Void (x:Float, y:Float, w:Float, h:Float, r:Float=8, segments:Int=8)<br>	DrawLine(x,y+r,x,y+h-r) 'Left<br>	DrawLine(x+r,y,x+w-r,y) 'Top<br>	DrawLine(x+r,y+h,x+w-r,y+h) 'Bottom<br>	DrawLine(x+w,y+r,x+w,y+h-r) 'Right<br>	<br>	DrawArc(x+r,y+r,r,r,90,180,segments) 'UL<br>	DrawArc(x+w-r,y+r,r,r,0,90,segments) 'UR<br>	DrawArc(x+r,y+h-r,r,r,180,270,segments) 'LL<br>	DrawArc(x+w-r,y+h-r,r,r,270,360,segments) 'LR<br>End Function<br><br>Function FillRoundedRect:Void(x:Float, y:Float, w:Float, h:Float, r:Float=8, segments:Int=8)<br>	DrawPie(x+r,y+r,r,r,90,180,segments) 'UL<br>	DrawPie(x+w-r,y+r,r,r,0,90,segments) 'UR<br>	DrawPie(x+r,y+h-r,r,r,180,270,segments) 'LL<br>	DrawPie(x+w-r,y+h-r,r,r,270,360,segments) 'LR<br>	<br>	'We could do this with just 2 calls, but the rects would overlap and create a dark box in the center if alpha were to be &lt;1	<br>	DrawRect(x+r,y,w-r-r,r) 'Top<br>	DrawRect(x+r,y+h-r,w-r-r,r) 'Bottom<br>	DrawRect(x,y+r,r,h-r-r) 'Left<br>	DrawRect(x+w-r,y+r,r,h-r-r) 'Right<br>	DrawRect(x+r,y+r,w-r-r,h-r-r) 'Center<br>End Function<br>[/monkeycode]<br><br>Someone else, feel free to modify this code to work with the line thickness stuff.  I'm not sure how slow that would be, so I left it out. <br><br></td></tr></table><br>
<a name="2034333"></a>

<a name="2034369"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> thanks nice job, I had been meaning to revisit this and improve it, now I don't need to :)<br><br>I hope you don't mind, I made a small change to your FillRoundedRect:<br><br>[monkeycode]<br>Function FillRoundedRect:Void(x:Float, y:Float, w:Float, h:Float, r:Float=8, segments:Int=8)<br>        DrawPie(x+r,y+r,r,r,90,180,segments) 'UL<br>        DrawPie(x+w-r,y+r,r,r,0,90,segments) 'UR<br>        DrawPie(x+r,y+h-r,r,r,180,270,segments) 'LL<br>        DrawPie(x+w-r,y+h-r,r,r,270,360,segments) 'LR<br><br>	DrawRect( x, y + r, w, h - r - r )		   'Centre + Left/Right<br>	DrawRect( x + r, y, w - r - r, r )		     'Top<br>	DrawRect( x + r, y + h - r, w - r - r, r )        'Bottom<br>End Function<br>[/monkeycode]<br><br>and you can add this to the top of the RoundedRect Functions to reduce the radius to prevent errors.<br><br>[monkeycode]<br>		If h &lt; w<br>			If r &gt; ( h / 2.0 ) Then r = ( h / 2.0 )<br>		Else<br>			If r &gt; ( w / 2.0 ) Then r = ( w / 2.0 )<br>		Endif<br>[/monkeycode] <br><br></td></tr></table><br>
<a name="2034367"></a>

<a name="2034365"></a>

<a name="2034366"></a>

<a name="2034363"></a>

<a name="2034362"></a>

<a name="2034360"></a>

<a name="2034413"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nobuyuki</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> cool!  I was holding off on setting "r" to a new value within these routines until I knew whether or not it would automatically copy the value back to the caller (see <a href="http://www.monkeycoder.co.nz/Community/posts.php?topic=3282" target="_blank">http://www.monkeycoder.co.nz/Community/posts.php?topic=3282</a> ).  Now, everything's fantastic.<br><br>Just FYI, the checks for radius size fail if any of the values are negative.  To keep it relatively sane (ie, the shape still stays within bounds even though the corners invert), only change the radius from checking absolute value, like so:<br><br>[monkeycode]<br>	If h &lt; w<br>		If r &gt; Abs( h / 2.0 ) Then r = ( h / 2.0 )<br>	Else<br>		If r &gt; Abs( w / 2.0 ) Then r = ( w / 2.0 )<br>	End If<br>[/monkeycode] <br><br></td></tr></table><br>
<a name="2034500"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes, I didn't consider negative values. <br><br>I quickly wrote a Draw Heart Function with outline, might prove useful. (Not extensively tested yet)<br><br>[monkeycode]<br>Function DrawHeart : Void( x : Float, y : Float, w : Float, h : Float, segments : Int = 8 )<br>	'Draws 4 Sided Pie. Diamond, hearts centre.<br>	DrawPie( x, y, w, h, 0, 360.0, 4 )<br>	'Draws 2 Pies for the hearts ears. Radius = 1 / Sqrt( 2 ) = 0.707. Angles are larger than 180 to prevent seams.<br>	DrawPie( x - ( 0.5 * w ), y - ( 0.5 * h ), 0.707 * w, 0.707 * h, 0, 270, segments )<br>	DrawPie( x + ( 0.5 * w ), y - ( 0.5 * h ), 0.707 * w, 0.707 * h, -90, 180, segments )<br>End Function<br><br>Function DrawHeartOutline : Void( x : Float, y : Float, w : Float, h : Float, segments : Int = 8 )<br>	'Draws 2 Sided Arc. Bottom of Diamond, hearts centre.<br>	DrawArc( x, y, w, h, 180.0, 360.0, 2 )<br>	'Draws 2 Arcs for the hearts ears. 1 / Sqrt( 2 ) = 0.707<br>	DrawArc( x - ( 0.5 * w ), y - ( 0.5 * h ), 0.707 * w, 0.707 * h, 45, 225, segments )<br>	DrawArc( x + ( 0.5 * w ), y - ( 0.5 * h ), 0.707 * w, 0.707 * h, -45, 135, segments )<br>End Function<br>[/monkeycode]<br><br>I have also just written a Draw Spiral Function, outline only at the moment and still a WIP: (increasing the passed aEnd value loops the spiral more than once)<br><br>[monkeycode]<br>Function DrawSpiralOutline : Void( x : Float, y : Float, xRad : Float, yRad : Float, aStart : Float=0.0, aEnd : Float = 360.0, segments : Int = 8 )<br>	Local x1 : Float = x<br>	Local y1 : Float = y<br>	Local x2 : Float<br>	Local y2 : Float<br>	Local div : Float  = ( aEnd - aStart ) / segments<br>	For Local i : Int = 1 To segments<br>	    x2 = x + ( Cos( aStart + ( i * div )) * ( xRad * ( Float( i ) / segments )))<br>	    y2 = y - ( Sin( aStart + ( i * div )) * ( yRad * ( Float( i ) / segments )))<br>	    DrawLine( x1, y1, x2, y2)<br>	    x1 = x2<br>	    y1 = y2<br>	Next<br>End Function<br>[/monkeycode] <br><br></td></tr></table><br>
<a name="2034499"></a>

<a name="2034498"></a>

<a name="2034497"></a>

<a name="2034494"></a>

<a name="2034496"></a>

<a name="2034492"></a>

<a name="2034493"></a>

<a name="2034491"></a>

<a name="2082724"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >chrisc2977</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> NoOdle! You are my most favorite person of the week! Or maybe even month!<br>I was picking my brains on how to render a pie chart and i diddnt even know the 'DrawPoly()' existed. This makes my task alot easier, Thank you! <br><br></td></tr></table><br>
<a name="2082775"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Supertino</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes these are very handy functions, I use them to help render my custom GUI module. +1 <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
