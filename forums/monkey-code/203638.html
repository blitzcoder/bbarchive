<!DOCTYPE html><html lang="en" ><head ><title >2d ball to line collision</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >2d ball to line collision</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >2d ball to line collision</a><br><br>
<a name="2038891"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> a simple example of a ball to line collision including gravity.<br>[monkeycode]<br>Strict <br><br>Import  mojo<br><br><br>Function Main:Int()<br>	New Game<br>	Return 1<br>End Function<br><br>Class Point<br>	Field x:Float<br>	Field y:Float<br>	<br>	Method New(x:Float,y:Float)<br>		Self.x = x<br>		Self.y = y<br>	End Method<br>End Class<br><br><br>Class Game Extends App<br><br>	Field velocity:Point<br>	Field gravity:Float = .05<br>	Field damping:Float = 0.999<br>	Field segments:Int = 30	<br>	Field ground:Ground[]<br>	Field peakHeights:Float[]<br>	Field vertices:Float[]<br>	Field ball:Ball<br>	Field width:float<br>	Field height:float<br><br><br><br>	Method OnCreate:Int()	<br>	<br>		ground = New Ground[segments]<br>		peakHeights = New Float[segments+1]<br>		vertices = New Float[segments*2+2]<br>  		ball = New Ball(50, 50, 10)<br>		velocity = New Point(.5, 0)<br>		width = DeviceWidth()<br>		height = DeviceHeight()<br>	<br>		' Calculate ground peak heights <br>		For Local i:Int = 0 Until peakHeights.Length()<br>    		peakHeights[i] = Rnd(height-50, height-100)<br>  		Next<br>		#rem<br>		/* Float value required For segment width (segs)<br>		calculations so the ground spans the entire <br>		display window, regardless of segment number. */<br>		#End rem<br>  		Local segs:Float = segments<br>  		ground[0] = New Ground(0,480,0,peakHeights[0])<br>  		For Local i:Int = 1 Until segments<br>    		ground[i]  = New Ground(width/segs*i, peakHeights[i],<br>    		width/segs*(i+1), peakHeights[i+1])<br>  		Next<br>  		ground[segments-1].x2 = 640<br>  		ground[segments-1].y2 = 480<br>		SetUpdateRate 60<br>	  <br>  		Return 1<br>	End method<br><br>	Method OnUpdate:Int()<br>	  ' Move ball<br>	  ball.x += velocity.x<br>	  velocity.y += gravity<br>	  ball.y += velocity.y<br>	<br>	  ' Collision detection<br>	  checkWallCollision()<br>	  For Local i:Int = 0 Until segments<br>	    checkGroundCollision(ground[i])<br>	  Next<br>	  <br>	  Return 1<br>	End Method<br><br>	Method OnRender:Int()<br>		Cls<br>		SetColor 0,105,255	<br>		DrawRect(0, 0, width, height)<br>		SetColor 255,0,255<br>		<br>		For Local i:Int =0 Until segments<br>		  vertices[i*2] = ground[i].x1<br>		  vertices[i*2+1] = ground[i].y1<br>		Next<br>		vertices[segments*2] = ground[segments-1].x2<br>		vertices[segments*2+1] = ground[segments-1].y2<br>		DrawPoly(vertices)<br>		SetColor 255,100,50<br>		DrawCircle(ball.x, ball.y, ball.r)	<br>		Return 1<br>	End Method<br><br><br>	Method checkWallCollision:Void()<br>		If (ball.x &gt; width-ball.r)<br>			ball.x = width-ball.r<br>			velocity.x *= -1<br>			velocity.x *= damping<br>		Else If (ball.x &lt; ball.r)<br>			ball.x = ball.r<br>			velocity.x *= -1<br>			velocity.x *= damping<br>		Endif<br>	End Method<br><br><br>	Method checkGroundCollision:Void(groundSegment:Ground)<br>	<br>		' Get difference between ball and ground<br>		Local deltaX:Float = ball.x - groundSegment.x<br>		Local deltaY:Float = ball.y - groundSegment.y<br>		<br>		' Precalculate trig values<br>		Local cosine:Float = Cos(groundSegment.rot)<br>		Local sine:Float = Sin(groundSegment.rot)<br>		#rem	<br>		/* Rotate ground And velocity To allow <br>		orthogonal collision calculations */<br>		#End rem<br>		Local groundXTemp:Float = cosine * deltaX + sine * deltaY<br>		Local groundYTemp:Float = cosine * deltaY - sine * deltaX<br>		Local velocityXTemp:Float = cosine * velocity.x + sine * velocity.y<br>		Local velocityYTemp:Float = cosine * velocity.y - sine * velocity.x<br>		#Rem<br>		/* Ground collision - check For surface <br>		collision And also that ball is within <br>		left/rights bounds of ground segment */<br>		#End rem<br>		If (groundYTemp &gt; -ball.r And<br>			ball.x &gt; groundSegment.x1 And<br>			ball.x &lt; groundSegment.x2 )<br>			' keep ball from going into ground<br>			groundYTemp = -ball.r<br>			' bounce and slow down ball<br>			velocityYTemp *= -1.0<br>			velocityYTemp *= damping<br>		End If<br>		<br>		' Reset ground, velocity and ball<br>		deltaX = cosine * groundXTemp - sine * groundYTemp<br>		deltaY = cosine * groundYTemp + sine * groundXTemp<br>		velocity.x = cosine * velocityXTemp - sine * velocityYTemp<br>		velocity.y = cosine * velocityYTemp + sine * velocityXTemp<br>		ball.x = groundSegment.x + deltaX<br>		ball.y = groundSegment.y + deltaY<br>	End Method<br>End Class<br><br>Class Ground <br>  Field x1:Float<br>  Field y1:Float<br>  Field x2:Float<br>  Field y2:float  <br>  Field x:Float<br>  Field y:Float<br>  Field len:Float<br>  Field rot:Float<br><br>  ' Constructor<br>  Method New(x1:Float, y1:Float, x2:Float, y2:Float)<br>    Self.x1 = x1<br>    Self.y1 = y1<br>    Self.x2 = x2<br>    Self.y2 = y2<br>    x = (x1+x2)/2<br>    y = (y1+y2)/2<br>    len = dist(x1, y1, x2, y2)<br>    rot = ATan2((y2-y1), (x2-x1))<br>  End Method<br>  <br>  Method dist:float(x1:Float,y1:Float,x2:Float,y2:Float)<br>  	Return Sqrt((x2-x1)*(x2-x1) +(y2-y1)*(y2-y1))<br>  End Method<br>  <br>End Class<br><br>Class Ball<br>  Field x:Float<br>  Field y:Float<br>  Field r:Float<br><br>  Method New(x:Float, y:Float, r:Float)<br>    Self.x = x<br>    Self.y = y<br>    Self.r = r<br>  End Method<br><br>End Class<br><br>[/monkeycode] <br><br></td></tr></table><br>
<a name="2038890"></a>

<a name="2038889"></a>

<a name="2042842"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Raz</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Really useful for me thanks Jesse :) <br><br></td></tr></table><br>
<a name="2042845"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Raz</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Actually Jesse, maybe you can help.<br><br>is this code able to check collisions against lines of all angles?<br><br>For example, I added 4 additional Ground instances to form a square (I renamed the Ground class to Line)<br><br><pre class=code>tL.lines2 = New Line[4]
tL.lines2[0] = New Line(500.0, 200.0, 550.0, 200.0)
tL.lines2[1] = New Line(550.0, 200.0, 550.0, 250.0)
tL.lines2[2] = New Line(500.0, 250.0, 550.0, 250.0)
tL.lines2[3] = New Line(500.0, 200.0, 500.0, 250.0)</pre><br><br>and when the left vertical line is hit (lines2[3]), the ball pops up on top of the square, however when lines2[0] is hit (horizontal line on top) the ball behaves as expected.<br><br>Any tips? :)<br><br>Thanks <br><br></td></tr></table><br>
<a name="2042851"></a>

<a name="2042852"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Algorithms with polygons usually make assumptions about orientation. Maybe that's the problem here.<br><br>In the original code the line segments are oriented consistently. As you travel from (x1,y1) to (x2,y2) the sky is on the left, ground on the right. Your lines 0 and 1 have this property, but lines 2 and 3 are the reverse.<br><br>As you define lines 1,2,3 the start point of each line should be the end point of the previous line.<br><br>If that doesn't work then post your code so we can see what is happening. <br><br></td></tr></table><br>
<a name="2042854"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Raz</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey Floyd, thanks for the reply. Yeah I thought this might be it, hence the dodgy order (originally it was in a more logical order for drawing a square)<br><br>I know it's not pretty, but here is the code I've been messing with<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict

Import  mojo


Function Main:Int()
        New Game
        Return 1
End Function

Class Point
        Field x:Float
        Field y:Float
        
        Method New(x:Float,y:Float)
                Self.x = x
                Self.y = y
        End Method
End Class


Class Game Extends App

        Field level:Level
		
        Method OnCreate:Int()
		
			level = Level.CreateTestLevel()
        
			SetUpdateRate 60
			
			Return 1
			
        End Method

        Method OnUpdate:Int()
		
			level.Update()

			Return 1
			
        End Method

        Method OnRender:Int()
		
			Cls
			
			level.Render()
			
			Return 1
				
        End Method

End Class

Function CheckGameObjectAgainstLine:Void(tObject:GameObject, tLine:Line)
	' Get difference between ball and ground
	Local deltaX:Float = tObject.x - tLine.x
	Local deltaY:Float = tObject.y - tLine.y
	
	' Precalculate trig values
	Local cosine:Float = Cos(tLine.rot)
	Local sine:Float = Sin(tLine.rot)
	' Rotate ground And velocity To allow 
	' orthogonal collision calculations */
	Local groundXTemp:Float = cosine * deltaX + sine * deltaY
	Local groundYTemp:Float = cosine * deltaY - sine * deltaX
	Local velocityXTemp:Float = cosine * tObject.velocity.x + sine * tObject.velocity.y
	Local velocityYTemp:Float = cosine * tObject.velocity.y - sine * tObject.velocity.x
	' Ground collision - check For surface 
	' collision And also that ball is within 
	' left/rights bounds of ground segment */
	If (groundYTemp &gt; - tObject.r And
		tObject.x &gt; tLine.x1 And
		tObject.x &lt; tLine.x2)
		' keep ball from going into ground
		groundYTemp = -tObject.r
		' bounce and slow down ball
		velocityYTemp *= -1.0
		velocityYTemp *= tObject.damping
	End If
	
	' Reset ground, velocity and ball
	deltaX = cosine * groundXTemp - sine * groundYTemp
	deltaY = cosine * groundYTemp + sine * groundXTemp
	tObject.velocity.x = cosine * velocityXTemp - sine * velocityYTemp
	tObject.velocity.y = cosine * velocityYTemp + sine * velocityXTemp
	tObject.x = tLine.x + deltaX
	tObject.y = tLine.y + deltaY
End

Function CheckGameObjectAgainstLevel:Void(tObj:GameObject, tLev:Level)
	If (tObj.x &gt; tLev.width - tObj.r)
		tObj.x = tLev.width - tObj.r
		tObj.velocity.x *= -1
		tObj.velocity.x *= tObj.damping
	Else If (tObj.x &lt; tObj.r)
		tObj.x = tObj.r
		tObj.velocity.x *= -1
		tObj.velocity.x *= tObj.damping
	Endif
End Function

Class Line
	Field x1:Float
	Field y1:Float
	Field x2:Float
	Field y2:float  
	Field x:Float
	Field y:Float
	Field len:Float
	Field rot:Float
	
	' Constructor
	Method New(x1:Float, y1:Float, x2:Float, y2:Float)
		Self.x1 = x1
		Self.y1 = y1
		Self.x2 = x2
		Self.y2 = y2
		x = (x1+x2)/2
		y = (y1+y2)/2
		len = dist(x1, y1, x2, y2)
		rot = ATan2( (y2 - y1), (x2 - x1))
	End Method
	
	Method dist:float(x1:Float,y1:Float,x2:Float,y2:Float)
		Return Sqrt( (x2 - x1) * (x2 - x1) + (y2 - y1) * (y2 - y1))
	End Method
	
	Method Render:Void()
		DrawLine(x1, y1, x2, y2)
	End
  
End Class

Class GameObject
	
	Field x:Float
	Field y:Float
	Field r:Float
	
	Field level:Level
	
	Field velocity:Point
	Field damping:Float = 0.999
	
	Method New(x:Float, y:Float, r:Float, tLev:Level)
		Self.x = x
		Self.y = y
		Self.r = r
		Self.level = tLev
	End Method
	
	Method Update:Void()
	
		x += velocity.x * level.delta
        velocity.y += level.gravity * level.delta
        y += velocity.y * level.delta
	
		For Local tLine:Line = EachIn level.lines
			CheckGameObjectAgainstLine(Self, tLine)
		Next
		
		For Local i:Int = 0 Until 4
			CheckGameObjectAgainstLine(Self, level.lines2[i])
		Next
		CheckGameObjectAgainstLevel(Self, level)
	End
	
	Method Render:Void()
		SetColor 255, 100, 50
		DrawCircle(x, y, r)
	End
	
	End Class

Class Level

	Field gravity:Float = 0.05
	Field segments:Int = 30
	Field lines:Line[]
	Field peakHeights:Float[]
	Field vertices:Float[]
	Field ball:GameObject
	Field width:float
	Field height:float
	Field delta:Float = 1.0
	
	Field lines2:Line[]
	
	Field player:GameObject
	Field gameObjects:List&lt;GameObject&gt;
	
	Function CreateTestLevel:Level()
	
		Local tL:Level = New Level()
		tL.gameObjects = New List&lt;GameObject&gt;
		tL.lines = New Line[tL.segments]
		tL.peakHeights = New Float[tL.segments + 1]
		tL.vertices = New Float[tL.segments * 2 + 2]
		tL.player = New GameObject(50, 50, 10, tL)
		tL.gameObjects.AddLast(tL.player)
		tL.player.velocity = New Point(0.5, 0)
		tL.width = DeviceWidth()
		tL.height = DeviceHeight()
		
		' Squares
		tL.lines2 = New Line[4]
		tL.lines2[0] = New Line(500.0, 200.0, 550.0, 200.0)
		tL.lines2[1] = New Line(550.0, 200.0, 550.0, 250.0)
		tL.lines2[2] = New Line(550.0, 250.0, 500.0, 250.0)
		tL.lines2[3] = New Line(500.0, 250.0, 500.0, 200.0)
		
		' Calculate ground peak heights 
		For Local i:Int = 0 Until tL.peakHeights.Length()
			tL.peakHeights[i] = Rnd(tL.height - 50, tL.height - 100)
		Next
		
		Local segs:Float = tL.segments
		tL.lines[0] = New Line(0, 480, 0, tL.peakHeights[0])
		For Local i:Int = 1 Until tL.segments
			tL.lines[i] = New Line(tL.width / segs * i, tL.peakHeights[i], tL.width / segs * (i + 1), tL.peakHeights[i + 1])
		Next
		tL.lines[tL.segments - 1].x2 = 640
		tL.lines[tL.segments - 1].y2 = 480
		Return tL
	End
	
	Method Update:Void()
		If KeyHit(KEY_SPACE)
			If delta = 1.0
				delta = 0.2
			Else
				delta = 1.0
			EndIf
		EndIf
		
		For Local tGO:GameObject = EachIn gameObjects
			tGO.Update()
		Next
		
	End
	

	
	Method Render:Void()
	
		SetColor 0, 105, 255
        DrawRect(0, 0, width, height)
	
		SetColor 255, 0, 255
		
		For Local i:Int =0 Until segments
			vertices[i * 2] = lines[i].x1
			vertices[i * 2 + 1] = lines[i].y1
		Next
		vertices[segments * 2] = lines[segments - 1].x2
		vertices[segments * 2 + 1] = lines[segments - 1].y2
		DrawPoly(vertices)
		
		SetColor(0, 0, 0)
		For Local i:Int = 0 Until 4
			lines2[i].Render()
		Next
	
		For Local tGO:GameObject = EachIn gameObjects
			tGO.Render()
		Next
		
		
	End
End</textarea> <br><br></td></tr></table><br>
<a name="2042890"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> The idea seems to be to rotate everything (line, ball, velocity vector) around the center of line so the line becomes horizontal. The collision checking and bounce are calculated for this easier configuration and the results are rotated back to the original orientation.<br><br>There's no obvious reason it wouldn't work for arbitrary angles, but something is going wrong. <br><br></td></tr></table><br>
<a name="2042920"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Raz</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> It almost looks like the repositioning code just handles vertical stuff, I'll have a play <br><br></td></tr></table><br>
<a name="2042928"></a>

<a name="2042931"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>It almost looks like the repositioning code just handles vertical stuff<br> <br></div><br><br>yes it is. After the line and ball are  rotated for movement calculations, the line is checked to see if the ball went below the line. If it did, the ball is moved above the line and then everything is rotated back to it's original orientations along with the correct bounce mechanism. <br><br>I learned to do that while I was researching the vector math.  I had it in my hard Drive so I decided to post it here. I have not had much use for it really. <br><br>anyway I decided that I am going to create a vector object  engine that works with a single ball and a line(s). I will post it as soon I am done with it.  it shouldn't take me more than a day or two.  it will have elastic and dynamic collision.  stay tuned. <br><br></td></tr></table><br>
<a name="2042939"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Raz</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cheers Jesse :)<br><br>I found what I believe to be your vector code examples on blitzbasic.com as well and I've been messing around converting them for Monkey.<br><br>I think it's about time I learnt about trigonometry properly seeing as I will probably end up using it in one way or another in just about everything I create! <br><br></td></tr></table><br>
<a name="2042948"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Samah</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Diddy has a 2D vector module if you want to take a look at it. <br><br></td></tr></table><br>
<a name="2042960"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> @raz<br>Just be careful if you go through the tutorials posted by toypa they are good but have steps that I guess he didn't either know how to do or decided not to share(?) so he did some work around that had me puzzled.  he did something somewhat similar to this circle to line demo above.  I had to do a lot more searching around vector tutorials before I figured it out. <br><br></td></tr></table><br>
<a name="2042981"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Raz</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'll have to have a look Samah, does it include reflection and point of intersection stuff?<br><br>Jesse: By chance I had been looking at these tutorials myself and I wasn't able to get my head around them!<br><br>With your blitzmax code though, from what I can tell, all angles worked fine :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
