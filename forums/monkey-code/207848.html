<!DOCTYPE html><html lang="en" ><head ><title >Simple Expression Compiler</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Simple Expression Compiler</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Simple Expression Compiler</a><br><br>
<a name="2077229"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GW_</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I knocked this up yesterday and thought I would share. <br>The code parses basic style expressions and translates to x86 assembly. <br>I'm not sure if the generated asm is correct or not. <br><br><br>lex.monkey (the lexer)<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
#rem
	Simple Expression compiler v0.1 by Aaron Woodard, Feb 2014 [admin@...]
	
	
	TODO: 
		Add 'else' clause to 'if'
		Check if the generated asm is even remotely correct..
		better error handling
		fix 'Float2Hex' for in Monkey
#end

Strict

Const TOK_NUMBER%=1
Const TOK_FLOAT%=2
Const TOK_PLUS%=3
Const TOK_MINUS%=4
Const TOK_MUL%=5
Const TOK_DIV%=6
Const TOK_IDENT%=7
Const TOK_EQUALS%=8
Const TOK_DBLEQUALS%=9
Const TOK_LPAREN%=10
Const TOK_RPAREN%=11
Const TOK_LBRAC%=12
Const TOK_RBRAC%=13
Const TOK_STRING%=14
Const TOK_VAR%=15
Const TOK_SEMICOL%=16
Const TOK_IF%=17
Const TOK_ELSE%=18
Const TOK_ENDIF%=19
Const TOK_LT%=20
Const TOK_GT%=21
Const TOK_LTE%=22
Const TOK_GTE%=23
Const TOK_NE%=24
Const TOK_THEN%=25 '!

Const TOK_EOF%=100


'------------------------------------------------------------------------------------------------------------------------------
Class tToken
	Field Typ%
	Field Value$
	
	Method dump:void()
		'DebugLog("Typ:"+typ+ " &gt; '" + Value + "'")
	End Method
	
	Function Create:tToken(_typ%,_value$)
		Local t:tToken = New tToken
		t.Typ=_typ
		t.Value=_value
		Return t
	End Function
End Class
'------------------------------------------------------------------------------------------------------------------------------
 
Global CurrIdx%=0
Global CurrToke:tToken
Global NextToke:tToken
Global CodeString$

'------------------------------------------------------------------------------------------------------------------------------
Function Consume:void()
	CurrToke = NextToke
	NextToke= Toke()
	'If CurrToke Then DebugLog("CT='"+CurrToke.Value+"'")
End Function
'------------------------------------------------------------------------------------------------------------------------------
Function Toke:tToken()
	Local t:tToken=New tToken
	Local tmp$
	
	If CurrIdx+1 &gt; CodeString.Length Then 
		t.Typ = TOK_EOF 
		Return t
	EndIf
	
	tmp = CodeString[CurrIdx..CurrIdx+1]
	While "~t~r~n ".Contains(tmp)
		CurrIdx += 1		
		tmp = CodeString[CurrIdx..CurrIdx+1]
		If CurrIdx+1 &gt; CodeString.Length Then 
			t.Typ = TOK_EOF
			Return t
		EndIf 
	Wend
	
	'  NUMBER
	If "1234567890.".Contains(tmp) Then
		t = LexNumber()
		If Not t Then Error("null token!")
		Return t
	EndIf
	
	' +
	If tmp = "+" Then
		CurrIdx += 1
		Return tToken.Create(TOK_PLUS,tmp)
	EndIf
	
	' -
	If tmp = "-" Then
		CurrIdx += 1
		Return tToken.Create(TOK_MINUS,tmp)
	EndIf
	
	' *
	If tmp = "*" Then
		CurrIdx += 1
		Return tToken.Create(TOK_MUL,tmp)
	EndIf
	
	' /
	If tmp = "/" Then
		CurrIdx += 1
		Return tToken.Create(TOK_DIV,tmp)
	EndIf
	
	' WORD
	
	If "abcdefghijklmnopqrstuvwxyz_".Contains(tmp) Then
		t = LexWord()
		Select t.Value
			Case "var"
				Return tToken.Create(TOK_VAR,"var")
			Case "if"
				Return tToken.Create(TOK_IF,"if")
			Case "then"
				Return tToken.Create(TOK_THEN,"then")
			Case "else"
				Return tToken.Create(TOK_ELSE,"else")
			Case "endif"
				Return tToken.Create(TOK_ENDIF,"endif")
			Default
				Return t
				'Error("Unkown token '" + t.Value + "'" )
		End Select
	EndIf
	
	' == or =
	If tmp = "=" Then		
		If CodeString[CurrIdx..CurrIdx+2] = "==" Then
			CurrIdx += 2
			Return tToken.Create(TOK_DBLEQUALS,"==")
		Else
			CurrIdx += 1
			Return tToken.Create(TOK_EQUALS,tmp)
		EndIf
	EndIf
	
	' (
	If tmp = "(" Then
		CurrIdx += 1
		Return tToken.Create(TOK_LPAREN,tmp)
	EndIf
	
	' )
	If tmp = ")" Then
		CurrIdx += 1
		Return tToken.Create(TOK_RPAREN,tmp)
	EndIf
	
	' ;
	If tmp = ";" Then
		CurrIdx += 1
		Return tToken.Create(TOK_SEMICOL,tmp)
	EndIf
	
	
	' [
	If tmp = "[" Then
		CurrIdx += 1
		Return tToken.Create(TOK_LBRAC,tmp)
	EndIf
	
	' ]
	If tmp = "]" Then
		CurrIdx += 1
		Return tToken.Create(TOK_RBRAC,tmp)
	EndIf
	
	If tmp = "&lt;" Then
		If CodeString[CurrIdx..CurrIdx+2] = "&lt;=" Then
			CurrIdx += 2
			Return tToken.Create(TOK_LTE,"&lt;=")
		ElseIf CodeString[CurrIdx..CurrIdx+2] = "&lt;&gt;" Then
			CurrIdx += 2
			Return tToken.Create(TOK_NE,"&lt;&gt;")
		Else
			CurrIdx += 1
			Return tToken.Create(TOK_LT,"&lt;")
		EndIf
	EndIf
	
	If tmp = "&gt;" Then
		If CodeString[CurrIdx..CurrIdx+2] = "&gt;=" Then
			CurrIdx += 2
			Return tToken.Create(TOK_GTE,"&gt;=")
		Else
			CurrIdx += 1
			Return tToken.Create(TOK_GT,"&gt;")
		EndIf
	EndIf

	If tmp = "~q" Then
		t=LexString()
		CurrIdx += 1
		Return t 
	EndIf

	Error("Unknown Token '" + tmp + "'")
	Return null
End Function
'------------------------------------------------------------------------------------------------------------------------------
Function LexNumber:tToken()
	Local t:tToken=New tToken
	Local tmp$
	Local dotcount%=0
	
	While "1234567890.".Contains(CodeString[CurrIdx..CurrIdx+1])
		If CodeString[CurrIdx..CurrIdx+1] = "." Then dotcount += 1
		If dotcount &gt; 1 Then Error("Malformed number! '" + tmp + "'")
		tmp += CodeString[CurrIdx..CurrIdx+1]
		CurrIdx += 1
	Wend
	t.Typ = TOK_FLOAT
	t.Value = tmp
	Return t
End Function
'------------------------------------------------------------------------------------------------------------------------------
Function LexWord:tToken()
	Local t:tToken=New tToken
	Local tmp$
	While "abcdefghijklmnopqrstuvwxyz_".Contains(CodeString[CurrIdx..CurrIdx+1])
		tmp += CodeString[CurrIdx..CurrIdx+1]
		CurrIdx += 1
	Wend
	t.Typ = TOK_IDENT
	t.Value = tmp
	Return t
End Function
'------------------------------------------------------------------------------------------------------------------------------
Function LexString:tToken()
	Local t:tToken=New tToken
	Local tmp$
	Local endt$
	CurrIdx += 1

	Repeat
		tmp += CodeString[CurrIdx..CurrIdx+1]
		CurrIdx += 1
	Until "~r~n~q".Contains(CodeString[CurrIdx..CurrIdx+1])
	endt = CodeString[CurrIdx..CurrIdx+1]	
	
	If endt = "~q" Then
		t.Typ = TOK_STRING
		t.Value = tmp
		Return t
	EndIf
	
	If "~r~n".Contains(endt) Then
		Error("Malformed string literal '" + tmp + "'")
	Endif
	Return t
End Function
'------------------------------------------------------------------------------------------------------------------------------
</textarea><br><br>parser.monkey<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
#rem
	Simple Expression compiler v0.1 by Aaron Woodard, Feb 2014 [admin@...]
	
	
	TODO: 
		Add 'else' clause to 'if'
		Check if the generated asm is even remotely correct..
		better error handling
		fix 'Float2Hex' for in Monkey
#end

Strict

Import lex

Global num_numbers#[]
Global vars$[]
Global cLabels%
Global asmstring$


Function Main:Int()
	Print("Parsing..")
	
	CodeString = 	"var x;~n"
	CodeString += 	"var myvar;~n"
	CodeString += 	"var anothervar;~n"
	CodeString += 	"x=0.5*2;~n"
	CodeString += 	"if(x&gt;1) then~n"
	CodeString += 	"	myvar=1+2-3*4/5;~n"
	CodeString += 	"	if(myvar==1.1) then~n"
	CodeString += 	"		anothervar=x*(myvar+123.456)*0.1;~n"
	CodeString += 	"	endif~n"
	CodeString += 	"endif~n"
	
	
	Print "~n--------------------~n"+CodeString+"~n--------------------~n"
	Consume()
	Consume()
	Parse()
	
	Print prolog()
	Print asmstring
	Print epilog()
	Print datasec()
	
	
	Return 1
End Function




'------------------------------------------------------------------------------------------------------------------------------
Function Parse:Void()
	'// var... or x=... or if(...
	While  CurrToke.Typ &lt;&gt; TOK_EOF And (CurrToke.Typ = TOK_VAR Or (CurrToke.Typ = TOK_IDENT And NextToke.Typ = TOK_EQUALS) Or CurrToke.Typ = TOK_IF)
		If CurrToke.Typ = TOK_VAR Then
			DeclareVar()
		ElseIf CurrToke.Typ = TOK_IDENT And NextToke.Typ = TOK_EQUALS Then
			Assignment()		
		ElseIf CurrToke.Typ = TOK_IF Then
			IfExpression()		
		EndIf
	Wend
End Function
'------------------------------------------------------------------------------------------------------------------------------
Function DeclareVar:void()
	Local idx%
	Local v$
	If CurrToke.Typ = TOK_VAR Then
		Consume()
		If CurrToke.Typ = TOK_IDENT Then 
			If checkvars(CurrToke.Value) Then DoError("Already Defined '"+CurrToke.Value+"'")
			v=CurrToke.Value
			'vars += [v]
			vars = AppendSArray(vars,v)
			Consume()
			If CurrToke.Typ &lt;&gt; TOK_SEMICOL Then DoError("missing ';'")
			Consume()
		EndIf
	EndIf
End Function
'------------------------------------------------------------------------------------------------------------------------------
Function Assignment:void()
	Local idx%
	Local v$
	While CurrToke.Typ = TOK_IDENT And NextToke.Typ = TOK_EQUALS 
		v=CurrToke.Value
		Consume()
		If CurrToke.Typ = TOK_EQUALS Then	
			Consume()
			Expression()
			idx = getvarindex(v)
			If idx&lt;0 Then DoError("invalid var  '"+v+"'")
			addasm("	fstp dword [ebp-" + (4+idx*4) + "]		;store in "+v )
			
			If CurrToke.Typ &lt;&gt; TOK_SEMICOL Then DoError("missing ';'")
			Consume()
		EndIf
	Wend
End Function

'------------------------------------------------------------------------------------------------------------------------------
Function Expression:void()
	AddExpression()
End Function
'------------------------------------------------------------------------------------------------------------------------------
Function IfExpression:void()
	Local labElseif$ 
	Local labEndif$
	Local cond$[1]
	 
	While CurrToke.Typ = TOK_IF 
		If CurrToke.Typ = TOK_IF Then

			labEndif= MakeLabel("_endif")
			
			Consume()
			If CurrToke.Typ &lt;&gt; TOK_LPAREN Then
				DoError("Expected '('" )
			EndIf
			Consume()
						
			CondExpression(cond)
			
			If CurrToke.Typ &lt;&gt; TOK_RPAREN Then
				DoError("Expected ')'" )
			EndIf
			Consume()
			'Then
			If CurrToke.Typ &lt;&gt; TOK_THEN Then
				DoError("Expected 'then'" )
			EndIf
			Consume()
			
			addasm("	fxch")
			addasm("	fucompp")
			addasm("	fnstsw ax")
			addasm("	sahf")
		
			Select cond[0]
				Case "&gt;"
					addasm("	setbe al")
				Case "&lt;"
					addasm("	setae al")
				Case "&gt;="
					addasm("	setb al")
				Case "&lt;="
					addasm("	seta al")
				Case "=="
					addasm("	setnz al")
				Case "&lt;&gt;"
					addasm("	setz al")
				Default
					DebugLog("What!?")
			End Select
			
			addasm("	movzx eax,al")
			addasm("	cmp eax,0")
			addasm("	jne "+labEndif)
			
			Parse()
			
			If CurrToke.Typ &lt;&gt; TOK_ENDIF Then
				DoError("Expected 'endif'" )
			EndIf
			addasm(labEndif)
			Consume()
		EndIf
	Wend
End Function
'------------------------------------------------------------------------------------------------------------------------------
Function CondExpression:void(cond$[])
	Expression()
	If CurrToke.Typ &lt;&gt; TOK_LT And CurrToke.Typ &lt;&gt; TOK_GT And CurrToke.Typ &lt;&gt; TOK_LTE And CurrToke.Typ &lt;&gt; TOK_GTE And CurrToke.Typ &lt;&gt; TOK_NE And CurrToke.Typ &lt;&gt; TOK_DBLEQUALS Then
		DoError("Expected '&lt;,&gt;,&lt;=,&gt;=,&lt;&gt;,=='" )
	EndIf
	
	Select CurrToke.Value
		Case "&gt;","&lt;","&gt;=","&lt;=","==","&lt;&gt;"
			cond[0] = CurrToke.Value
		Default 
			DoError("Unkown token")
	End Select
	 
	Consume()
	Expression()
End Function

'------------------------------------------------------------------------------------------------------------------------------
Function AddExpression:void()
	If CurrToke.Typ&lt;&gt;TOK_MINUS And CurrToke.Typ&lt;&gt; TOK_LPAREN And CurrToke.Typ&lt;&gt; TOK_FLOAT And CurrToke.Typ&lt;&gt;TOK_IDENT Then DoError("Expected '-' or '(' or number or ident'")
	 
	MulExpression()
	While CurrToke.Typ = TOK_PLUS 'Then
		Consume()
		MulExpression()
		addasm("	faddp ")
	Wend
	While CurrToke.Typ = TOK_MINUS 'Then
		Consume()
		MulExpression()
		addasm("	fsubp ")
	Wend
End Function

'------------------------------------------------------------------------------------------------------------------------------
Function MulExpression:void()
	If CurrToke.Typ&lt;&gt;TOK_MINUS And CurrToke.Typ&lt;&gt; TOK_LPAREN And CurrToke.Typ&lt;&gt; TOK_FLOAT And CurrToke.Typ&lt;&gt;TOK_IDENT Then DoError("Expected '-' or '(' or number or ident'")
	Primary()
	While CurrToke.Typ = TOK_MUL 'Then
		Consume()
		Primary()
		addasm("	fmulp ")
	Wend
	While CurrToke.Typ = TOK_DIV 'Then
		Consume()
		Primary()
		addasm("	fdivp ")
	Wend
End Function

'------------------------------------------------------------------------------------------------------------------------------
Function Primary:void()
	Local idx%,v$
	If CurrToke.Typ&lt;&gt;TOK_MINUS And CurrToke.Typ&lt;&gt; TOK_LPAREN And CurrToke.Typ&lt;&gt; TOK_FLOAT And CurrToke.Typ&lt;&gt;TOK_IDENT Then DoError("Expected '-' or '(' or number or ident'")
	While CurrToke.Typ = TOK_MINUS
		Consume()
		If CurrToke.Typ = TOK_FLOAT Then
			CurrToke.Value= String(-Float(CurrToke.Value))
		Else 
			Expression()
			addasm("	fchs")	
		End If
	Wend
	
	While CurrToke.Typ = TOK_LPAREN 		
		Consume()
		Expression()
		If CurrToke.Typ &lt;&gt; TOK_RPAREN Then DoError("!!")
		Consume() 
	Wend
	
	While CurrToke.Typ = TOK_FLOAT 
		addasm("	fld dword [_" + addorgetnum(CurrToke.Value) +"]")
		Consume()
	Wend
	
	While CurrToke.Typ = TOK_IDENT
		v=CurrToke.Value 
			idx = getvarindex(v)
			If idx&lt;0 Then DoError("unkown var '"+v+"'")
			addasm("	fld dword [ebp-"+(4+idx*4)+"]			;load '"+v+"'" )
			Consume()
		Return 
	Wend
End Function

'------------------------------------------------------------------------------------------------------------------------------
Function DoError:Void(s$)
	Local v$[] = vars
	Print("ERR!  " + s)
	DebugStop	
End Function

'------------------------------------------------------------------------------------------------------------------------------
Function dbg:void(s$)
	'Print("dbg:  " + s)
End Function

'------------------------------------------------------------------------------------------------------------------------------
Function FloatHex:String(num:Float) 
	Return num
'oh monkey.. u dirty beast!	
'	Local p:Float Ptr = Varptr num
'	Local bp:Byte Ptr = Int Ptr Int(p)
'	Local out:String = ""
	
'	out+=Right(Hex(bp[3]), 2)
'	out += Right(Hex(bp[2]),2)
'	out += Right(Hex(bp[1]),2)
'	out += Right(Hex(bp[0]),2)
'	Return "0x" + out
End Function 
'------------------------------------------------------------------------------------------------------------------------------
Function addorgetnum%(v$)
	For Local i% = 0 Until num_numbers.Length
		If num_numbers[i] = Float(v) Then
			Return i
		Endif
	Next
	num_numbers = AppendFArray( num_numbers,Float(v) )
	Return num_numbers.Length-1 'num_numbers[num_numbers.Length-1]
End Function
'------------------------------------------------------------------------------------------------------------------------------
Function checkvars%(v$)
	If Not vars.Length Then Return False
	For Local i% = 0 Until vars.Length
		If v=vars[i] Then Return True
	Next
	Return False
End Function

'------------------------------------------------------------------------------------------------------------------------------
Function getvarindex%(v$)
	For Local i% = 0 Until vars.Length
		If vars[i]=v Then 
			Return i
		EndIf
	Next
	Return -1
End Function

'------------------------------------------------------------------------------------------------------------------------------
Function addasm:void(s$)
	asmstring += s + "~n"
End Function
'------------------------------------------------------------------------------------------------------------------------------
Function prolog$()
	Local tmp$
	tmp = ";------Begin------;~n_func:~n"
	tmp += "	push ebp~n~tmov ebp,esp~n"
	If vars.Length Then
		tmp += "	sub esp, " + (vars.Length*4)
	EndIf 
	Return tmp
End Function	
'------------------------------------------------------------------------------------------------------------------------------
Function epilog$()
	Local tmp$
	tmp = "	mov esp,ebp~n~tpop ebp~n~tret~n;------End------;"
	Return tmp
End Function
'------------------------------------------------------------------------------------------------------------------------------
Function datasec$()
	Local tmp$
	tmp ="~nsection ~qdata~q~n"
	For Local i% = 0 Until num_numbers.Length
		tmp += "_"+i+":~n"
		tmp +="dw " + FloatHex(num_numbers[i]) + "~n"'"		;-&gt; "+num_numbers[i]+"~n"
		tmp +="align 4~n"
	Next
	Return tmp
End Function
'------------------------------------------------------------------------------------------------------------------------------
Function MakeLabel$(pre$="_")
	cLabels += 1
	Return pre + String(cLabels) +":"
End Function
'------------------------------------------------------------------------------------------------------------------------------
Function AppendSArray$[](a$[], val$)
	a=a.Resize(a.Length+1) 
	a[a.Length-1]=val
	Return a
End Function
'------------------------------------------------------------------------------------------------------------------------------
Function AppendFArray#[](a#[], val#)
	a=a.Resize(a.Length+1)
	a[a.Length-1]=val
	Return a
End Function
</textarea> <br><br></td></tr></table><br>
<a name="2079794"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sammy</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have no use for this whats-so-ever but I did enjoy flicking through the code, very interesting, thank you! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
