<!DOCTYPE html><html lang="en" ><head ><title >Simple Save/Load Game module</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Simple Save/Load Game module</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Simple Save/Load Game module</a><br><br>
<a name="2018670"></a>

<a name="2018671"></a>

<a name="2018675"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamRedwoods</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Very simple and requires work on your part, but is set up for an arbitrary number of save game slots. Saves within the SaveState setup in monkey. For mobile, it saves as XML files in memory, so you should have quite a bit of room for data.<br><br>Game data is SEQUENTIAL: FIFO (first in, first out) order.<br><br>The save data is in arrays, so remember the brackets.<br><br>The CPlayer class is my own arbitrary class, showing that you must pass a class (or whatever) to access and set your game variables appropriately.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'' Simple Save/Load Game
'' Adam of the Redwoods 2011
''

Class GameUtils
	Const MAXSLOTS:Int = 3
	
	Global data:String
	Global pointer:Int
	Global loadstr:String[]
	
	Function SaveGame(Player:CPlayer, slot:Int)
		Local savestr:String = ""
		Local loadstr:String[] = New String[MAXSLOTS+1]
		Local tempstr:String[] = New String[MAXSLOTS+1]
		Local s:String
		
		For Local i:= 0 To MAXSLOTS
			loadstr[i] = ""
		Next
		
		s = LoadState()
		
		If s
			tempstr= s.Split("!!!---")
			For s = Eachin tempstr
				If Not s Continue
				
				Local sl:Int = Int(String.FromChar(s[0]))
				Print "found slot"+sl+" "+s
				
				If sl = slot
					data = s ''for loading
					Exit
				Else
					''
					loadstr[sl] = s[..(s.Length-1)] ''get rid of trailing comma
				Endif
			Next	
		Endif
		
	
		savestr = ""
		
		For Local i:= 1 To MAXSLOTS
			Print "slot "+i
			
			ClearData()
			
			If i=slot
				

				'' SAVE YOUR DATA HERE
				
				SetData ([slot, Player.gold,Player.wave,Player.lives,Player.interest,Player.powerup,Player.hasFire,Player.hasIce])
				SetData ([Player.choptreecost])
				SetData (["banana"])
				
				
				
			Else
				SetData ( [loadstr[i] ] )
			Endif
			
			savestr += data
			
		Next
		
		SaveState(savestr)
		
	End
	
	Function LoadGame:Int(Player:CPlayer, slot:Int)

		'Local loadstr:String[]
		Local tempstr:String[] = New String[MAXSLOTS+1]
		Local s:String

		
		s = LoadState()
		
		If s
			tempstr= s.Split("!!!---")
			For s = Eachin tempstr
				If Not s Continue
				
				Local sl:Int = Int(String.FromChar(s[0]))
				Print "found slot"+sl+" "+s
				
				If sl = slot
					data = s ''for loading
					Exit
				Endif
			Next	
		Else
			Print "no savedata found"
			Return 0
		Endif
		
		BeginRead()
		

		'' PUT YOUR LOAD DATA HERE
		
		Player.gold = ReadInt()
		Player.wave = ReadInt()
		Player.lives = ReadInt()
		Player.interest = ReadInt()
		Player.powerup = ReadInt()
		Player.hasFire = ReadInt()
		Player.hasIce = ReadInt()
		Player.choptreecost = ReadInt()
		
		ReadString() ''for the banana
		
		
		Return True
		
	End
	
	Function ClearData()
		data = ""
	End
	
	Function SetData(o:Int[])
		If data = "" Then data = "!!!---"
		
		For Local j:Int = Eachin o
			data += String(j) +","
		Next
	End

	Function SetData(o:String[])
		If data = "" Then data = "!!!---"
		
		For Local j:String = Eachin o
			data += j +","
		Next
	End
	
	Function SetData(o:Float[])
		If data = "" Then data = "!!!---"
		
		For Local j:Float = Eachin o
			data += String(j) +","
		Next
	End

	Function BeginRead()
		pointer = 1
		loadstr = data.Split(",")
	End

	Function ReadInt:Int()
		pointer = pointer +1
		Return Int(loadstr[pointer-1].Trim())
	End

	Function ReadFloat:Float()
		pointer = pointer +1
		Return Float(loadstr[pointer-1].Trim())
	End

	Function ReadString:String()
		pointer = pointer +1
		Return loadstr[pointer-1]
	End

End
</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
