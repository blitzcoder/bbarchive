<!DOCTYPE html><html lang="en" ><head ><title >YAML Lite</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >YAML Lite</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >YAML Lite</a><br><br>
<a name="2002110"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Soap</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's a file I converted from BlitzMax, it can take simple YAML files and saves/loads them. I am basically using this as a way to be able to store complex information into SaveState() and LoadState() Please note I have only tested it on HTML5 so far, and it's works on my Firefox aswell as Chrome broswers. There may (probably?) be bugs still within the code, I just wanted to get this out as soon as I confirmed it's working.<br><br>Here's a sample usage:<br><pre class=code>
local stateFile := New YAMLFile("state")
local lastState:String = LoadState()
Print "'"+lastState+"'"
if lastState = ""
	local tmpEle:YAMLElement = stateFile.GetElement("PlayerData",true)
	tmpEle.GetChildElement("position",true).data = playerShip.pos.ToString()
	tmpEle.GetChildElement("target",true).data = target.ToString()
else
	stateFile.__debug = true
	stateFile.ParseFromString( lastState )
	if not stateFile.elements.IsEmpty() then
		local parts:String[] = stateFile.GetElement("PlayerData").GetChildElement("position").data.Split(",")
		playerShip.pos.x = float(parts[0])
		playerShip.pos.y = float(parts[1])
		parts = stateFile.GetElement("PlayerData").GetChildElement("target").data.Split(",")
		target.x = float(parts[0])
		target.y = float(parts[1])
	else
		local tmpEle:YAMLElement = stateFile.GetElement("PlayerData",true)
		tmpEle.GetChildElement("position",true).data = playerShip.pos.ToString()
		tmpEle.GetChildElement("target",true).data = target.ToString()
	end
	stateFile.__debug = false
end</pre><br><br>Vector is my own simple class with just x and y fields and ToString() outputs "x,y".<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' File yamlLite.monkey
' Basic implementation of YAML
'
' No warranty implied; use at your own risk.

Strict

Import mojo.app

Class YAMLElement
	Field aFile:YAMLFile = Null
	Field parent:YAMLElement = Null
	Field name$ = ""
	Field data$ = ""
	
	Method New(aaFile:YAMLFile, nname:String, pparent:YAMLElement = Null)
		aFile = aaFile
		name = nname
		parent = pparent
		aFile.elements.AddLast Self
	End
	
	Method GetChildElement:YAMLElement(nname$,createNewElement:Bool=False)
		if aFile = null then return	null
		if aFile.elements.IsEmpty() then return	null
	
		For Local tmp:YAMLElement = EachIn aFile.elements
			If tmp.parent = Self Then
				If tmp.name.ToLower() = nname.ToLower() Then
					Return tmp
				End
			End
		Next
		
		If createNewElement Then
			Return New YAMLElement ( aFile,nname,Self )
		End
		
		Return New YAMLElement ' Return a element with "" for a name and "" for data.
	End
	
	Method SaveChildren:String(SaveString:String, tabDepth%=1)
		if aFile.elements.IsEmpty() then return	""
		
		Local tabStr$ = ""
		For Local i% = 1 To tabDepth
			tabStr += "~t"
		Next
		
		For Local tmp:YAMLElement = EachIn aFile.elements
			If tmp.parent = Self
				Local prefix$ = tmp.name + " :"
				
				If tmp.name.Trim() = "" Then prefix = "-"
				
				SaveString += tabStr + prefix + " " + tmp.data + "~n"
				
				SaveString = tmp.SaveChildren(SaveString, tabDepth+1)
			End
		Next
		
		return SaveString		
	End
	
	Method DeleteChildren:Void()	
		if aFile.elements.IsEmpty() then return	
			
		For Local tmp:YAMLElement = EachIn aFile.elements
			If tmp.parent = Self
				tmp.DeleteChildren
				tmp.Delete
			End
		Next		
	End
	
	Method Delete:Void()
		If parent Then parent = Null
		If aFile Then 
			if not aFile.elements.IsEmpty() then aFile.elements.Remove Self	
			aFile = Null
		End	
	End
End

Class YAMLFile
	Field __debug:Bool = False
	Field elements := New List&lt;YAMLElement&gt;
	Field name$ = ""
	
	Method GetElement:YAMLElement(nname$,createNewElement:Bool=False)
		For Local tmp:YAMLElement = EachIn elements
			If tmp.name.ToLower() = nname.ToLower() Then
				If tmp.parent = Null Then Return tmp
			End
		Next
		
		If createNewElement Then
			Return New YAMLElement( self,nname )
		end
		
		Return New YAMLElement ' Return a element with "" for a name and "" for data.
	End
	
	Method GetElementData$(name$)
		Local tmp:YAMLElement = GetElement(name)
		If tmp Then Return tmp.data
		Print "Could not find Element Data of '"+name+"'"
		Return ""
	End
	
	Method New(nname$)
		name = nname
	End
	
	Method ParseFromString:YAMLFile(fileStr:String)
		If Not fileStr Then Error "ERROR :: YAMLFile.ParseFromString :: Invalid file string!"
		
		Local fileData:String[] = fileStr.Split("~n")
		Print "Loading YAML File with "+fileData.Length()+" lines"
		Local currentLineCount% = 0
		
		Local elementStack := new Stack&lt;YAMLElement&gt;
		Local lastElement:YAMLElement = Null
		
		Local CurrentLine$ = fileData[currentLineCount]
		currentLineCount+=1
		Print "First line: '"+CurrentLine+"' length: "+fileData.Length()
		While True
			Local currentElement:YAMLElement = Null
			Local tabDepth% = 0
			If __debug Then Print ""
			If __debug Then Print "New Line : '"+CurrentLine+"'"
			
			''' Is the line a comment?
			If CurrentLine[..3] = "---" Then
				CurrentLine = CurrentLine.Replace("---","")
				CurrentLine = CurrentLine.Trim()
				name=CurrentLine
				If __debug Then Print "YAML File name = '"+name+"'"
				CurrentLine = ""
			End
			If CurrentLine[..1] = "#" Then CurrentLine = ""
			If CurrentLine.Contains("#")		' Check for comment past the start
				CurrentLine = CurrentLine[..(CurrentLine.Find("#"))]
			End
			If CurrentLine.Trim() = "" Then
				CurrentLine = ""			
			End
			
			''' Are there tabs?
			While CurrentLine[..1] = "~t"
				CurrentLine = CurrentLine[1..]
				Print("Format... '"+CurrentLine+"'")
				tabDepth += 1
			Wend
			If __debug Then Print "Formatted: '"+CurrentLine+"' TabDepth: "+tabDepth 
			
			''' If it's a value...
			If CurrentLine.Contains(":") Or CurrentLine.Contains("-")
				If __debug Then Print "Element Found"
				Local lclLine$ = CurrentLine.Trim()
				If __debug Then Print "Formatted: '"+lclLine+"'"
				If __debug Then Print "TabDepth: "+tabDepth+" elementStack.Depth: "+elementStack.Length()
				
				''' Push and Pop the stack!
				If tabDepth &gt; elementStack.Length() Then
				
					elementStack.Push lastElement
					If __debug Then Print "TabDepth greater than EleDepth"
					If __debug Then Print "Pushed "+lastElement.name+" onto the stack"
					
				ElseIf tabDepth &lt; elementStack.Length()
				
					If __debug Then Print "TabDepth lesser than EleDepth"
					''' Be ready for several down depths at a time.
					While tabDepth &lt; elementStack.Length()
						elementStack.Pop()
						If __debug Then Print "*Pop!*"
					Wend
					
				End
								''' The element is ready to be added!
				currentElement = New YAMLElement
				currentElement.aFile = Self
				If CurrentLine.Contains(":") Then
					local tmpStrArr:String[] = lclLine.Split(":")
					currentElement.name = tmpStrArr[0].Trim()
					currentElement.data = tmpStrArr[1].Trim()
				Else
					local tmpStrArr:String[] = lclLine.Split("-")
					currentElement.data = tmpStrArr[1].Trim()
				End If
				currentElement.parent = Null
				If Not elementStack.IsEmpty() Then
					currentElement.parent = elementStack.Top()
					If __debug Then Print "Added Object: '"+currentElement.name+"', '"+currentElement.data+"', '"+currentElement.parent.name+"'"
				Else
					If __debug Then Print "Added Object: '"+currentElement.name+"', '"+currentElement.data+"'"
				End
				elements.AddLast currentElement
				
				If currentElement.data = "&gt;" Then
					currentElement.data = ""
					
					Local tCurrentLine$ = ""
					While currentLineCount+1 &lt; fileData.Length()
						tCurrentLine = fileData[currentLineCount]
						currentLineCount+=1
						CurrentLine = tCurrentLine
						tabDepth = 0
						
						While CurrentLine[..1] = 9
							CurrentLine = CurrentLine[(CurrentLine.Length() - 1)..]
							tabDepth += 1
						Wend
						If tabDepth &lt;= elementStack.Length()
							If __debug Then Print "TabDepth lesser than EleDepth"
							While tabDepth &lt; elementStack.Length()
								elementStack.Pop
								If __debug Then Print "*Pop!*"
							Wend
							Exit
						End
						
						currentElement.data += CurrentLine +" "
					Wend
					
					CurrentLine = tCurrentLine
					
					If __debug Then Print "Finished!"
					If __debug Then Print currentElement.data
					If __debug Then Print "CurLine: '"+CurrentLine.Trim()+"'"
					Continue
				End
			End
			
			If currentElement &lt;&gt; Null Then lastElement = currentElement
			If currentLineCount+1 &lt; fileData.Length()
				CurrentLine = fileData[currentLineCount]
				currentLineCount+=1
			Else
				Exit
			End
		Wend
		
		If __debug Then Print "Elements Added: "+elements.Count()
		If __debug Then Print ""
		
		Return Self
	End
	
	Method SaveToString:String()
		Local SaveString := "--- "+name + "~n"
		
		If __debug Then Print "Saving a YAML file: "+name
		For Local tmp:YAMLElement = EachIn elements
			If tmp.parent = Null
				Local prefix$ = tmp.name + " :"
				
				If tmp.name.Trim( ) = "" Then prefix = "-"
				
				SaveString += prefix + " " + tmp.data + "~n"
					
				SaveString = tmp.SaveChildren (SaveString)
			End
		Next
		
		return (SaveString)
	End
End



</textarea> <br><br></td></tr></table><br>
<a name="2002109"></a>

<a name="2002395"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Soap</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Unlike skn3's XML example, you can easily save the data back to a string. Continuing on the first example, inside of OnUpdate():<br><br><pre class=code>If saveTimer &lt; Millisecs() then ''' saveTimer is just a Int field
	saveTimer = Millisecs() + 500
	local tmpEle:YAMLElement = stateFile.GetElement("PlayerData",true)
	tmpEle.GetChildElement("position",true).data = playerShip.pos.ToString()
	tmpEle.GetChildElement("target",true).data = target.ToString()
	SaveState( stateFile.SaveToString() )
end</pre> <br><br></td></tr></table><br>
<a name="2014054"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brüggemann</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Soap, Nice YAML Implementation ;)<br><br>Can you give me your blitzmax code for YAML <br>Would be nice :-)<br><br>Thx <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
