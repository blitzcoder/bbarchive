<!DOCTYPE html><html lang="en" ><head ><title >Bucket Deque</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Bucket Deque</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Bucket Deque</a><br><br>
<a name="2069149"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamRedwoods</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> What is a bucket deque?<br><br>It is a collection class, like a deque, but the data is kept in 2^n bucket arrays as a list. This allows quick allocation for increasing the deque, but at the cost of random access speed.<br><br>An enumerator search is a tad slower than a normal array deque, but memory efficiency is greater at higher capacities.<br>(Probably completely pointless for most uses, but the second step for this would be to make an <a href="http://en.wikipedia.org/wiki/Unrolled_linked_list" target="_blank">unrolled linked list</a>.)<br><br>Example of usage:<br><pre class=code>
Const MAX_DOGS:Int = 20

Function Main()

	Local dq:BucketDeque&lt;Dog&gt; = New BucketDeque&lt;Dog&gt;
	dq.Clear()

	Print "length "+dq.Length()
	
	For Local j:=0 To MAX_DOGS
		dq.PushLast(New Dog(5,6,"larry"+j))
		dq.PushFirst(New Dog(1,3.5,"curly"+j))
		dq.PushFirst(New Dog(10,15,"moe"+j))
	Next
	
	Print "length "+dq.Length()
	
	dq.DumpBuckets() ''debugging

	For Local dx:Dog = Eachin dq
		dx.x=2.0
	Next
	
	Print "..."
	
	Local j:Int=dq.Length()/2
	For Local i:Int = 0 To j-1
		
		Local r:Int=Rnd(0,dq.Length()-1)
		Local dg:Dog = dq.Get(r)
		Print r+" "+dg.name

		'Local dd:Dog = dq.PopFirst()
		'Print dd.name
		'dd = dq.PopLast()
		'Print dd.name
	Next
	
	Print ""
	Print dq.Get(dq.Length()-1).name
	Print dq.Get(0).name

	
	Print "length "+dq.Length()
	'dq.DumpBuckets()
	
	Print "done."

End


Class Dog

	Field x:Float
	Field y:Float
	Field name:String
	
	Method New(x#,y#,n$)
		self.x=x
		self.y=y
		self.name = n
	End

End
</pre><br><br><b>CLASS CODE</b><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Class BucketDeque&lt;T&gt;
	
	Const CAPACITY:Int = 64 ''** MUST be power of 2 **
	Const CAPACITY_SHR:Int = 8 ''n is 2^n=capacity
	
	Field bucketList:List&lt;BucketBase&lt;T&gt;&gt; = New List&lt;BucketBase&lt;T&gt;&gt;
	
	Field topBucketNode:list.Node&lt;BucketBase&lt;T&gt;&gt;
	Field botBucketNode:list.Node&lt;BucketBase&lt;T&gt;&gt;
	Field topBucket:BucketBase&lt;T&gt;
	Field botBucket:BucketBase&lt;T&gt;
	Field length:int
	
	Method New()
		Self.Clear()

	End
	
	Method Clear:Void()
		bucketList.Clear()

		topBucket = New BucketBase&lt;T&gt;
		topBucketNode = bucketList.AddLast(topBucket)
		botBucket = topBucket
		botBucketNode =topBucketNode

	End
	
	Method Length:Int() Property
		Return length
	End
	
	Method GetTopIndex:Int()
		Return topBucket.top
	End
	
	Method GetBottomIndex:Int()
		Return botBucket.bot
	End
	
	
	Method PushFirst:Void(value:T)
		
		If topBucket.top= 0
			''add new bucket to top
			topBucket = New BucketBase&lt;T&gt;
			topBucketNode = bucketList.AddFirst(topBucket)
			topBucket.top=CAPACITY
		Endif
		
		topBucket.top-=1
		length+=1
		topBucket.data[topBucket.top] = value
'Print "add top "+topBucket.top+" len:"+length		
	End
	
	Method PushLast:Void(value:T)
		
		If botBucket.bot=CAPACITY-1
			''add new bottom bucket
			botBucket = New BucketBase&lt;T&gt;
			botBucketNode = bucketList.AddLast(botBucket)
			botBucket.bot = -1
		Endif
		
		botBucket.bot+=1
		length+=1
		botBucket.data[botBucket.bot] = value	
'Print "add bot "+botBucket.bot+" len:"+length		
	End

	
	Method PopFirst:T()
		
		If topBucket.top=CAPACITY
			topBucketNode = topBucketNode.NextNode()
			topBucket = topBucketNode.Value()
			
			If topBucket = Null Then Return Null
			topBucket.top = 0
		Endif
		
		If topBucket=botBucket And topBucket.top&gt;botBucket.bot Then Return Null
		
		Local rv:T = topBucket.data[topBucket.top]
		topBucket.data[topBucket.top] = Null
		
		topBucket.top +=1
		If length&gt;0 Then length-=1
		
		Return rv
		
	End
	
	Method PopLast:T()
		
		If botBucket.bot&lt;0
			botBucketNode = botBucketNode.PrevNode()
			botBucket = botBucketNode.Value()
			
			If botBucket = Null Then Return null
			botBucket.bot = CAPACITY-1
		Endif
		
		If topBucket=botBucket And topBucket.top&gt;botBucket.bot Then Return Null
		
		Local rv:T = botBucket.data[botBucket.bot]
		botBucket.data[botBucket.bot] = Null
		
		botBucket.bot -=1
		If length&gt;0 Then length-=1
		
		Return rv
		
	End
	
	
	Private ''
	
	Field _curBucket:list.Node&lt;BucketBase&lt;T&gt;&gt;
	Field _curBucketNum:Int
	Field _offset:int
	
	Public ''
	
	Method Get:T( index:Int)

#If CONFIG="debug"	
		If index&lt;0 Then Error "Error: No neg index"
#Endif
		
		index = index+topBucket.top
		Local num:Int = index Shr CAPACITY_SHR 'index/(CAPACITY+1)
		index = index-(num Shl CAPACITY_SHR) '*(CAPACITY+1)
'Print "get "+num+" "+index


		If _curBucket = Null
			_curBucket = topBucketNode
			If _curBucket = Null Then Return null
		Endif
		
		While (num&lt;&gt;_curBucketNum)
			If num&gt;_curBucketNum
				_curBucket = _curBucket.NextNode()
				If _curBucket=Null Then Exit
				_curBucketNum +=1
			Else If num&lt;_curBucketNum
				_curBucket = _curBucket.PrevNode()
				If _curBucket=Null Then Exit
				_curBucketNum -=1
			Endif
		Wend
		
		Return _curBucket.Value().data[index]
		
	End
	
	
	Method ObjectEnumerator:Enumerator&lt;T&gt;()
		Return New Enumerator&lt;T&gt;(Self)
	End
	
	Method DumpBuckets:Void( )
		For Local b:BucketBase&lt;T&gt; = Eachin bucketList
			Print "-bucket"
			For Local i:Int=0 To b.CAPACITY-1
				If b.data[i] Then Print "i:"+i+" "+b.data[i].name ''this may error out if data has no name
			Next
		Next
	End
End



Class BucketBase&lt;T&gt;

	Field data:T[]
	Field top:Int=0
	Field bot:Int=-1
	
	Method New()
		data = New T[BucketDeque&lt;T&gt;.CAPACITY]
	End

End


Class Enumerator&lt;T&gt;

	Method New( stack:BucketDeque&lt;T&gt; )
		Self.stack=stack
		Self.bucket = stack.bucketList.FirstNode()
		Self.offset = bucket.Value().top-1
		Self.index = 0
		Self.sub_index = offset
		
	End

	Method HasNext:Bool()
		Return index&lt;stack.Length()
	End

	Method NextObject:T()
	
		sub_index +=1
		index+=1
		
		'' bucket change
		If sub_index&gt;BucketBase&lt;T&gt;.CAPACITY-1
			bucket =bucket.NextNode()
			sub_index = 0
		Endif
		
#If CONFIG="debug"		
		If bucket=Null Then Error "BucketDeque error!"
#Endif
		
		Local v:T = bucket.Value().data[sub_index]
		
		Return v
		
	End

Private

	Field stack:BucketDeque&lt;T&gt;
	Field bucket:list.Node&lt;BucketBase&lt;T&gt;&gt;
	Field index:Int, sub_index:Int, offset:int
	
End
</textarea> <br><br></td></tr></table><br>
<a name="2069135"></a>

<a name="2069134"></a>

<a name="2069133"></a>

<a name="2069130"></a>

<a name="2069129"></a>

<a name="2069128"></a>

<a name="2069127"></a>

<a name="2069126"></a>

<a name="2069125"></a>

<a name="2069124"></a>

<a name="2069202"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sammy</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> What coding scenarios do you see using data structure be advantageous Adam? <br><br></td></tr></table><br>
<a name="2069210"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamRedwoods</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Any time you have a stack or deque where the object type is rather large, and resizing the array takes a lot of time. <br><br></td></tr></table><br>
<a name="2069228"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sammy</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, thank a Adam, no harm in having another weapon at our disposal! ;) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
