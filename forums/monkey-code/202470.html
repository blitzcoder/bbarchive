<!DOCTYPE html><html lang="en" ><head ><title >QuadTree - drawing a Circle</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >QuadTree - drawing a Circle</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >QuadTree - drawing a Circle</a><br><br>
<a name="2024447"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> As the title suggests. Press SpaceKey to toggle the display.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import mojo


Function PointInCircle : Bool( x : Float, y : Float, r : Float, px : Float, py : Float )
	Local dx : Float = px - x
	Local dy : Float = py - y
	If (( dx * dx ) + ( dy * dy )) &gt; ( r * r )
		Return False
	Endif
	Return True
End Function



'smallest quad threshold
Const THRESHOLD : Float = 10.0



Class QuadNode
	
	Field x1 : Float
	Field y1 : Float
	Field x2 : Float
	Field y2 : Float
	
	Field id : Int
	
	Field children : QuadNode[]
	
	Method New( x1 : Float, y1 : Float, x2 : Float, y2 : Float )
		Self.x1 = x1
		Self.y1 = y1
		Self.x2 = x2
		Self.y2 = y2
	End Method
	
	Method Draw( flag : Bool = False )
		If children.Length &lt;&gt; 0
			For Local i : Int = 0 Until children.Length
				children[ i ].Draw( flag )
			Next
		Else
			SetColor 255, 255, 255
			If Self.id = 4 Then SetColor 255, 0, 0
			If Self.id = 1 Then SetColor 100, 100, 100
			If Self.id = 0 Then SetColor 255, 255, 0
			If flag = False Or flag = True And Self.id &gt; 1 And Self.id &lt; 4
				DrawRectOutline( x1, y1, x2 - x1, y2 - y1, 1.0)
			Endif
		Endif
	End Method


	Method Check : Int()
		Local result : Int = 0
		result = result + PointInCircle( DeviceHeight() / 2.0, DeviceHeight() / 2.0, DeviceHeight() / 2.0, x1, y1 )
		result = result + PointInCircle( DeviceHeight() / 2.0, DeviceHeight() / 2.0, DeviceHeight() / 2.0, x2, y1 )
		result = result + PointInCircle( DeviceHeight() / 2.0, DeviceHeight() / 2.0, DeviceHeight() / 2.0, x1, y2 )
		result = result + PointInCircle( DeviceHeight() / 2.0, DeviceHeight() / 2.0, DeviceHeight() / 2.0, x2, y2 )
		'this prevents a quad from containing the circle
		If result = 0
			result = PointInCircle( DeviceHeight() / 2.0, DeviceHeight() / 2.0, DeviceHeight() / 2.0, ( x2 + x1 ) / 2.0, ( y2 + y1 ) / 2.0 )
		Endif
		Return result
	End Method


	Method Split()
		
		Self.id = Self.Check()
		If Self.id = 4 Or Self.id = 0 Then Return

		If Self.x2 - Self.x1 &lt; THRESHOLD Or Self.y2 - Self.y1 &lt; THRESHOLD Then Return

		Self.children = Self.children.Resize( 4 )
		
		Local mx : Float = ( x2 + x1 ) / 2.0
		Local my : Float = ( y2 + y1 ) / 2.0
		
		Self.children[ 0 ] = New QuadNode( x1, y1, mx, my )
		Self.children[ 1 ] = New QuadNode( mx, y1, x2, my )
		Self.children[ 2 ] = New QuadNode( x1, my, mx, y2 )
		Self.children[ 3 ] = New QuadNode( mx, my, x2, y2 )
		
		For Local i : Int = 0 Until 4
			children[ i ].Split()
		Next
		
	End Method
	


End Class




Class QuadTree

	Field root : QuadNode
	
	Method Draw( flag : Bool = False )
		root.Draw( flag )
	End Method

End Class






Class MyApp Extends App


	Field tree : QuadTree
	
	Field debug : Bool = False
	
	
	Method OnCreate()
		SetUpdateRate 60
		

		tree = New QuadTree
		tree.root = New QuadNode( 0, 0, DeviceHeight(), DeviceHeight())
		tree.root.Split()
		
	End Method
	


	Method OnUpdate()
		
		If KeyHit( KEY_SPACE )
			Self.debug = Not Self.debug
		Endif
	
	End Method
	



	Method OnRender()
		Cls 255, 255, 255
		
		'Draws Background
		SetColor 0, 0, 0
		DrawRect 0, 0, DeviceHeight(), DeviceHeight()

		'Draws Tree
		SetColor 255, 0, 0
		tree.Draw( Self.debug )
		
	End Method
	

End Class



Function Main()
	New MyApp()
End Function




'/ Draws An Rect Outline.
Function DrawRectOutline( x : Float, y : Float, w : Float, h : Float, thickness : Float = 1.0 )
	If thickness = 1.0
		DrawLine x, y, x + w, y
		DrawLine x, y, x, y + h
		DrawLine x + w, y, x + w, y + h
		DrawLine x, y + h, x + w, y + h
	Else
		DrawThickLine( x, y, x + w, y, thickness, True )
		DrawThickLine( x, y, x, y + h, thickness, True )
		DrawThickLine( x + w, y, x + w, y + h, thickness, True )
		DrawThickLine( x, y + h, x + w, y + h, thickness, True )
	Endif
End Function



'/ Draws thick line, rounded ends optional
Function DrawThickLine( x1 : Float, y1 : Float, x2 : Float, y2 : Float, thickness : Float = 2.0, rounded : Bool = False )
	Local dx : Float = x2 - x1
	Local dy : Float = y2 - y1
	Local d : Float = Sqrt( dx * dx + dy * dy )
	Local vx : Float = dx / d
	Local vy : Float = dy / d
	Local nx : Float = vy
	Local ny : Float = -vx
	Local points : Float[ 8 ]
	points[ 0 ] = x1 + ( nx * ( thickness / 2.0 ))
	points[ 1 ] = y1 + ( ny * ( thickness / 2.0 ))
	points[ 2 ] = x1 + ( -nx * ( thickness / 2.0 ))
	points[ 3 ] = y1 + ( -ny * ( thickness / 2.0 ))
	points[ 4 ] = x2 + ( -nx * ( thickness / 2.0 ))
	points[ 5 ] = y2 + ( -ny * ( thickness / 2.0 ))	
	points[ 6 ] = x2 + ( nx * ( thickness / 2.0 ))
	points[ 7 ] = y2 + ( ny * ( thickness / 2.0 ))
	DrawPoly( points )
	If rounded = True
		DrawEllipse x1, y1, thickness / 2.0, thickness / 2.0
		DrawEllipse x2, y2, thickness / 2.0, thickness / 2.0
	Endif
End Function
</textarea> <br><br></td></tr></table><br>
<a name="2024448"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> whoa nice<br>why do you use quadtrees to draw a circle? they seem like they could be useful for something <br><br></td></tr></table><br>
<a name="2024450"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote">  why do you use quadtrees to draw a circle?  <br></div><br><br>lol why not? Its a fun visual representation, and yes they have lots of uses! Some taken from wikipedia:<br><br>+ Spatial indexing<br>+ Efficient collision detection in two dimensions<br>+ View frustum culling of terrain data<br>+ Storing sparse data, such as a formatting information for a spreadsheet or for some matrix calculations<br>+ Solution of multidimensional fields (computational fluid dynamics, electromagnetism)<br>+ Conway's Game of Life simulation program<br>+ State estimation<br>+ Quadtrees are also used in the area of fractal image analysis <br><br></td></tr></table><br>
<a name="2024466"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DruggedBunny</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Looks cool! <br><br></td></tr></table><br>
<a name="2024467"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> another update, press 1 - 4 to change the shape and press space to toggle display<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import mojo


Function PointInCircle : Bool( x : Float, y : Float, r : Float, px : Float, py : Float )
	Local dx : Float = px - x
	Local dy : Float = py - y
	If (( dx * dx ) + ( dy * dy )) &gt; ( r * r )
		Return False
	Endif
	Return True
End Function


Function PointInRect : Bool( x : Float, y : Float, x1 : Float, y1 : Float, x2 : Float, y2 : Float )
	If x &gt; x1 And x &lt; x2
		If y &gt; y1 And y &lt; y2
			Return True
		Endif
	Endif
	Return False
End Function



Function PointInTriangle : Bool( px : Float, py : Float, tri : Float[])
	If Orientation( tri[ 0 ], tri[ 1 ], tri[ 2 ], tri[ 3 ], px, py ) &lt;&gt; 1
		If Orientation( tri[ 2 ], tri[ 3 ], tri[ 4 ], tri[ 5 ], px, py ) &lt;&gt; 1
			If Orientation( tri[ 4 ], tri[ 5 ], tri[ 0 ], tri[ 1 ], px, py ) &lt;&gt; 1
				Return True	
			Endif
		Endif
	Endif
End Function



Function Orientation : Int ( x1 : Float,y1 : Float, x2 : Float,y2 : Float, Px : Float,Py : Float )
	Return Sgn((x2 - x1) * (Py - y1) - (Px - x1) * (y2 - y1))
End Function





'function test point in poly
Function PointInPoly : Bool( pointX : Float, pointY : Float, tris : Float[] )
	For Local i : Int = 0 Until tris.Length / 6
		Local index : Int = i * 6
		Local tri : Float[] = tris[ index..index + 6 ]
		If PointInTriangle( pointX, pointY, tri ) = True Then Return True
	Next
	Return False
End Function



'smallest quad threshold
Const THRESHOLD : Float = 12.0



Class QuadNode
	
	Field x1 : Float
	Field y1 : Float
	Field x2 : Float
	Field y2 : Float
	
	Field id : Int
	
	Field children : QuadNode[]
	
	Method New( x1 : Float, y1 : Float, x2 : Float, y2 : Float )
		Self.x1 = x1
		Self.y1 = y1
		Self.x2 = x2
		Self.y2 = y2
	End Method
	
	Method Draw( flag : Bool = False )
		If children.Length &lt;&gt; 0
			For Local i : Int = 0 Until children.Length
				children[ i ].Draw( flag )
			Next
		Else
			SetColor 255, 255, 255
			If Self.id = 4 Then SetColor 255, 0, 0
			If Self.id = 1 Then SetColor 100, 100, 100
			If Self.id = 0 Then SetColor 255, 255, 0
			If flag = False Or flag = True And Self.id &gt; 1 And Self.id &lt; 4
				DrawRectOutline( x1, y1, x2 - x1, y2 - y1, 1.0)
			Endif
		Endif
	End Method


	Method Check : Int()
		Local result : Int = 0
		result = result + PointInPoly( x1, y1, MapPoints )
		result = result + PointInPoly( x2, y1, MapPoints )
		result = result + PointInPoly( x1, y2, MapPoints )
		result = result + PointInPoly( x2, y2, MapPoints )
		'this prevents a quad from containing the shape
		If result = 0
			result = PointInPoly( ( x2 + x1 ) / 2.0, ( y2 + y1 ) / 2.0, MapPoints )
		Endif
		Return result
	End Method


	Method Split()
		
		Self.id = Self.Check()
		If Self.id = 4 Or Self.id = 0 Then Return

		If Self.x2 - Self.x1 &lt; THRESHOLD Or Self.y2 - Self.y1 &lt; THRESHOLD Then Return

		Self.children = Self.children.Resize( 4 )
		
		Local mx : Float = ( x2 + x1 ) / 2.0
		Local my : Float = ( y2 + y1 ) / 2.0
		
		Self.children[ 0 ] = New QuadNode( x1, y1, mx, my )
		Self.children[ 1 ] = New QuadNode( mx, y1, x2, my )
		Self.children[ 2 ] = New QuadNode( x1, my, mx, y2 )
		Self.children[ 3 ] = New QuadNode( mx, my, x2, y2 )
		
		For Local i : Int = 0 Until 4
			children[ i ].Split()
		Next
		
	End Method
	


End Class




Class QuadTree

	Field root : QuadNode
	
	Method New()
		Self.root = New QuadNode( 0, 0, DeviceHeight(), DeviceHeight())
		Self.root.Split()		
	End Method
	
	Method Draw( flag : Bool = False )
		root.Draw( flag )
	End Method

End Class







Global MapPoints : Float[]



Class MyApp Extends App


	Field tree : QuadTree
	
	Field debug : Bool = False
	Field mode : Int = 0
	
	
	Method OnCreate()
		SetUpdateRate 60
		
		MapPoints = ComputeCircle( DeviceHeight() / 2.0, DeviceHeight() / 2.0, -50 + DeviceHeight() / 2.0, -50 + DeviceHeight() / 2.0, 32 )
		
		Self.tree = New QuadTree()
	
	End Method
	


	Method OnUpdate()
		
		If KeyHit( KEY_SPACE )
			Self.debug = Not Self.debug
		Endif
		
		If KeyHit( KEY_1 )
			MapPoints = ComputeCircle( DeviceHeight() / 2.0, DeviceHeight() / 2.0, -50 + DeviceHeight() / 2.0, -50 + DeviceHeight() / 2.0, 32 )
			Self.tree = New QuadTree()
		Endif
		If KeyHit( KEY_2 )
			MapPoints = ComputeCircle( DeviceHeight() / 2.0, DeviceHeight() / 2.0, -50 + DeviceHeight() / 2.0, -50 + DeviceHeight() / 2.0, 4 )
			Self.tree = New QuadTree()
		Endif
		If KeyHit( KEY_3 )
			MapPoints = ComputeCircle( DeviceHeight() / 2.0, DeviceHeight() / 2.0, -50 + DeviceHeight() / 2.0, -50 + DeviceHeight() / 2.0, 6 )
			Self.tree = New QuadTree()
		Endif
		If KeyHit( KEY_4 )
			MapPoints = ComputeStar( DeviceHeight() / 2.0, DeviceHeight() / 2.0, -10 + DeviceHeight() / 4.0, DeviceHeight() / 2.0 )
			Self.tree = New QuadTree()
		Endif
	
	End Method
	



	Method OnRender()
		Cls 255, 255, 255
		
		'Draws Background
		SetColor 0, 0, 0
		DrawRect 0, 0, DeviceHeight(), DeviceHeight()

		'Draws Tree
		SetColor 255, 0, 0
		tree.Draw( Self.debug )

	End Method
	

End Class



Function Main()
	New MyApp()
End Function





Function ComputeStar : Float[]( x : Float, y : Float, aRadius : Float, bRadius : Float, segments : Int = 5 )
	Local div : Float  = 360 / segments
	Local mid : Float = div / 2.0
	Local points : Float[ segments * 12 ]
	For Local i : Int = 0 Until segments
		Local index : Int = i * 12
		points[ index + 0 ] = x
		points[ index + 1 ] = y
		points[ index + 2 ] = x + Sin( i * div ) * aRadius
		points[ index + 3 ] = y + Cos( i * div ) * aRadius
		points[ index + 4 ] = x + Sin( mid ) * bRadius
		points[ index + 5 ] = y + Cos( mid ) * bRadius
		points[ index + 6 ] = x
		points[ index + 7 ] = y
		points[ index + 8 ] = points[ index + 4 ]
		points[ index + 9 ] = points[ index + 5 ]
		points[ index + 10 ] = x + Sin(( i + 1 ) * div ) * aRadius
		points[ index + 11 ] = y + Cos(( i + 1 ) * div ) * aRadius
		mid = mid + div
	Next
	Return points
End Function



Function ComputeCircle : Float[]( x : Float, y : Float, xRadius : Float, yRadius : Float, segments : Int = 5 )
	Local div : Float = 360.0 / Float( segments )
	Local points : Float[ segments * 6 ]
	For Local i : Int = 0 Until segments
		Local index : Int = i * 6
		points[ index + 0 ] = x
		points[ index + 1 ] = y
		points[ index + 2 ] = x + Sin( i * div ) * xRadius
		points[ index + 3 ] = y + Cos( i * div ) * xRadius
		points[ index + 4 ] = x + Sin(( i + 1 ) * div ) * xRadius
		points[ index + 5 ] = y + Cos(( i + 1 ) * div ) * yRadius
	Next
	Return points
End Function



'/ Draws An Rect Outline.
Function DrawRectOutline( x : Float, y : Float, w : Float, h : Float, thickness : Float = 1.0 )
	If thickness = 1.0
		DrawLine x, y, x + w, y
		DrawLine x, y, x, y + h
		DrawLine x + w, y, x + w, y + h
		DrawLine x, y + h, x + w, y + h
	Else
		DrawThickLine( x, y, x + w, y, thickness, True )
		DrawThickLine( x, y, x, y + h, thickness, True )
		DrawThickLine( x + w, y, x + w, y + h, thickness, True )
		DrawThickLine( x, y + h, x + w, y + h, thickness, True )
	Endif
End Function



'/ Draws thick line, rounded ends optional
Function DrawThickLine( x1 : Float, y1 : Float, x2 : Float, y2 : Float, thickness : Float = 2.0, rounded : Bool = False )
	Local dx : Float = x2 - x1
	Local dy : Float = y2 - y1
	Local d : Float = Sqrt( dx * dx + dy * dy )
	Local vx : Float = dx / d
	Local vy : Float = dy / d
	Local nx : Float = vy
	Local ny : Float = -vx
	Local points : Float[ 8 ]
	points[ 0 ] = x1 + ( nx * ( thickness / 2.0 ))
	points[ 1 ] = y1 + ( ny * ( thickness / 2.0 ))
	points[ 2 ] = x1 + ( -nx * ( thickness / 2.0 ))
	points[ 3 ] = y1 + ( -ny * ( thickness / 2.0 ))
	points[ 4 ] = x2 + ( -nx * ( thickness / 2.0 ))
	points[ 5 ] = y2 + ( -ny * ( thickness / 2.0 ))	
	points[ 6 ] = x2 + ( nx * ( thickness / 2.0 ))
	points[ 7 ] = y2 + ( ny * ( thickness / 2.0 ))
	DrawPoly( points )
	If rounded = True
		DrawEllipse x1, y1, thickness / 2.0, thickness / 2.0
		DrawEllipse x2, y2, thickness / 2.0, thickness / 2.0
	Endif
End Function
</textarea> <br><br></td></tr></table><br>
<a name="2027357"></a>

<a name="2027358"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice, I think I need quadtrees to see if many people are close enough on a map.<br><br>Would you mind giving a small explanation of how to do that with quadtrees? <br><br></td></tr></table><br>
<a name="2027506"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JIM</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Depending on your particular case I would advise against quadtrees.<br><br>Where to use them:<br><br>Large area to cover, but very little space occupied with objects mostly static.<br><br>Where not to use them:<br><br>Small/medium area to cover with lots of objects that constantly move.<br><br>A quadtree essentially works by splitting an area into 4 every time there's too much data in that area. Suppose you want to keep a maximum of 16 objects in an area.<br><br>1. You start with a big area covering your map and consider all units in there.<br>2. Less than 16 units? You are done.<br>3. More than 16 units? Split the are into 4 equal parts and redistribute units. For each of those areas, go back to 2.<br><br>Why isn't this a very good idea with lots of moving units? If your units end up walking around the whole map, you will end up with a maximum split on your quad tree.<br><br>This is quite bad because at the lowest level you will essentially have a grid, except it's not all in one place in memory, it's all over the place and besides that, it has a whole tree structure above it that's mostly useless.<br><br>That's why I would go for a "bucket grid" if you may. Preallocate a grid of cells, each cell having space for n unit pointers (8, 16, etc.) and a counter (to avoid resizing the "bucket"). Updating is extremely easy. Each unit can instantly find it's proper bucket (x / grid_size, y / grid_size).<br>It's equally easy to find the neighbours of any cell.<br><br>To add to a cell you do something simple like:<br><br><pre class=code>
grid[x].storage[grid[x].crt_count] = unit_pointer
grid[x].crt_count += 1
</pre><br><br>Removing is easier:<br><br><pre class=code>
grid[x].crt_count -= 1
</pre><br><br>All of them are very fast operations that only change values in memory without messing it up :)<br><br>The only downside is the memory usage for preallocation. Try to find a balance to have as few cells as possible, but always make sure the bucket is large enough to hold the maximum possible units in that cell.<br><br>Again, depending on application, it might work to actually spill the bucket into the nearest neighbour that has enough room.<br><br>Whew! Wall of text. Hope that doesn't scare you :) <br><br></td></tr></table><br>
<a name="2027415"></a>

<a name="2027414"></a>

<a name="2027439"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> yeah thats probably the right approach thanks, <br>basically only test distance between units that are in the same or neighbouring squares <br><br></td></tr></table><br>
<a name="2028838"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> oh sorry slenkar not sure how I didn't notice your question!! JIM is correct, although there are examples on the internet showing how to use quadtrees for proximity testing, the speed gained by using this is dependant on a good spread. Quite often brute force distance detection is as as fast if not faster in some extreme circumstances. I would recommend a grid based approach as well. Again, sorry for not replying sooner! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
