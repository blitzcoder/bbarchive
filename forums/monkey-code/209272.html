<!DOCTYPE html><html lang="en" ><head ><title >EdzUp File Handling</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >EdzUp File Handling</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >EdzUp File Handling</a><br><br>
<a name="2096839"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >EdzUp</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> File handling made easy<br><br><pre class=code>
'
'	FileHandle.monkey - Copyright (C)EdzUp
'	Programmed by Ed 'EdzUp' Upton
'
Import brl.filestream
Import brl.filesystem

Const FILE_USEMONKEY = True			'Set to true for the monkey://internal and monkey://external use on android etc

Class FileHandlerClass
	Field FileHand:FileStream
	Field FileOk:Bool
	Field writing:Bool
	
	Method FileExists:Bool( filename:String )
		Select FileType( filename )
		Case FILETYPE_FILE
			Return( True )
		Default
			Return( False )
		End
		
		Return( False )
	End
	
	Method OpenFile( filename:String, writeable:Bool = False, internal:Bool = False )
		Local ActualFile:String
		
		If FILE_USEMONKEY = True
			If internal = True
				ActualFile = "monkey://internal/"+filename
			Else
				ActualFile = "monkey://external/"+filename
			Endif
		Else
			ActualFile = filename
		Endif
		
		FileOk = False
		
		If FileExists( ActualFile ) = True
			If writeable = True
				'create a file for writing
				FileHand = FileStream.Open( ActualFile, "w" )
				'New FileStream( ActualFile, "w" )
			Else
				'create a file for reading
				FileHand = FileStream.Open( ActualFile, "r" )
				'New FileStream( ActualFile, "r" )
			Endif
			writing = writeable
			FileOk = True
		Else
			If writeable = True
				'nothing here so make it
				FileHand = FileStream.Open( ActualFile, "w" )
				writing = writeable
				FileOk = True
			Endif
		Endif
	End
	
	Method IsOpen:Bool()
		Return( FileOk )
	End
	
	Method CloseFile( )
		FileHand.Close()
	End
	
	Method UseInt:Int( value:Int = 0 )
		If writing = True
			FileHand.WriteInt( value )
		Else
			Return( FileHand.ReadInt() )
		Endif
		
		Return( 0 )
	End
	
	Method UseByte:Int( value:Int = 0 )
		If writing = True
			FileHand.WriteByte( value )
		Else
			Return( FileHand.ReadByte() )
		Endif
		
		Return( 0 )
	End
	
	Method UseShort:Int( value:Int = 0 )
		If writing = True
			FileHand.WriteShort( value )
		Else
			Return( FileHand.ReadShort() )
		Endif
		
		Return( 0 )
	End
	
	Method UseFloat:Float( value:Float = 0.0 )
		If writing = True
			FileHand.WriteFloat( value )
		Else
			Return( FileHand.ReadFloat() )
		Endif
		
		Return( 0.0 )
	End
	
	Method UseString:String( value:String = "" )
		Local LL:Int
		
		If writing = True
			FileHand.WriteInt( value.Length )
			FileHand.WriteString( value )
		Else
			LL = FileHand.ReadInt()
			Return( FileHand.ReadString( LL ) )
		Endif
		
		Return( "" )
	End
	
	Method UseBool:Bool( value:Bool = False )
		Local UBI:Int
		
		If writing = True
			If value = True
				FileHand.WriteInt( 1 )
			Else
				FileHand.WriteInt( 0 )
			Endif
		Else
			UBI = FileHand.ReadInt()
			If UBI = 0
				Return( False )
			Else
				Return( True )
			Endif
		Endif
		
		Return( False )
	End
End

Global FileHandler:FileHandlerClass = New FileHandlerClass
</pre><br><br>Works on most targets BUT NOT ON HTML5 :)<br><br>Call FileHandler.OpenFile( "MyFile.txt", True, True ) would make a internal file in any application, internal means that it cannot be seen by file handlers on things like android etc. If you set the last True to False it will be created in places like /storage/0/ on android which can be found on file handlers.<br><br>Once you created the file you can use things like FileHandler.UseInt( 30 ) to write a file to it.<br><br>Reading from files you just call FileHandler.OpenFile( "MyFile.txt", false, True )<br>and then use MyInt:Int = FileHandler.UseInt()<br><br>Afer all that just call FileHandler.CloseFile() to close once its closed thats it. All of these have been tested on Android and work perfectly (My Star Rogue Editor uses it :) ), I put it here to make it easier on monkey to use files and also so I dont lose it LOL <br><br></td></tr></table><br>
<a name="2096849"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Steve Ancell</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I also constructed a file handler when I did Mutant Monty, this one's only designed for reading a file though; very quick and dirty. I'll post it below, with a usage example, in addition to Edz contribution. Anyone's welcome to use it if it's any good to them. ;)<br><br><br><pre class=code>
Import mojo


Function Main:Int()
	New C_Game()
	Return 0
	
End




Class C_Game extends App
	Field file:FileLoaderClass
	Field dataList:String[]
	
	
	Method OnCreate:Int()
		file = New FileLoaderClass("testfile.txt")
		
		While Not file.Eof()
			dataList = dataList.Resize(dataList.Length() + 1)
			dataList[dataList.Length() - 1] = file.ReadLine()
			
		Wend
		
		Return 0
		
	End
	
	
	Method OnUpdate:Int()
		Return 0
		
	End
	
	
	Method OnRender:Int()
		Local i:Int
		
		
		Cls
		
		For i = 0 to (dataList.Length() - 1)
			DrawText(dataList[i], 0, i * 20)
			
		Next
		
		Return 0
		
	End
	
End




Class FileLoaderClass
	Field fileData:String[]
	Field currentLine:Int
	
	
	Method New(fileName:String)
		fileData = LoadString(fileName).Split("~n")
		currentLine = 0
		
	End
	
	
	Method ReadLine:String()
		Local currentData:String
		
		
		if Eof() = False
			currentData = Self.fileData[Self.currentLine]
			currentLine = (currentLine + 1)
			
		Else
			currentData = "Data Exhausted!"
			
		EndIf
		
		Return currentData.Trim()
		
	End
	
	
	Method Eof:Bool()
		if Self.currentLine &gt; (fileData.Length() - 1)
			Return True
			
		Else
			Return False
			
		EndIf
	End
	
	
	Method Empty:Void()
		Local counter:Int
		
		
		For counter = 0 to (fileData.Length() - 1)
			fileData[counter] = ""
			
		Next
		
		fileData = fileData.Resize(0)
		
	End
End
</pre> <br><br></td></tr></table><br>
<a name="2096848"></a>

<a name="2096847"></a>

<a name="2114376"></a>

<a name="2114377"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MonkeyPlotter</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Edzup &amp; Steve, thank you for the example code, really appreciated ;) <br><br></td></tr></table><br>
<a name="2114507"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >EdzUp MkII</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Your welcome and merry xmas ;) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
