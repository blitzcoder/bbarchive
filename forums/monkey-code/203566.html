<!DOCTYPE html><html lang="en" ><head ><title >Auto rename and build with icon (WIP)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Auto rename and build with icon (WIP)</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Auto rename and build with icon (WIP)</a><br><br>
<a name="2037953"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dawlane</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi to all<br>For the last day or two I've been tinkering with trans to get it to do three things automatically.<br>1) To change the default MonkeyGame file out putted.<br>2) Change the savestate file name from .monkeystate.<br>3) To detect a default icon and add it to the executable file.<br><br>Currently V64 GLFW and STDCPP supported.<br><br>The code below is still a WORK IN PROGRESS. The way that it currently parses the VS and Xcode project template files need to be improved (a lot). And I don't really know if the VS/Xcode project files will break if they are edited before the icon is added to the build, but changing the name should be safe.<br><br>Now to use it add at the top of your main monkey project source code:<br><pre class=code>#PROJECT_NAME="My Game"</pre><br>then compile your code you should find in the .build folder that the projects back-end compiler files names have changed and your executable has been renamed.<br>Currently spaces are removed to stop the back-end compilers from complaining. At some point in the future I'll looking it.<br><br>Opening up the main.cpp file and looking at the LoadState functions, you should see .mygamestate instead of .monkeystate. <br><br>Inside the xcode/mingw/vc2010 folders is a file called “dont_delete.txt” this is use to keep track of the name of the last build, so deleting it will break trans and the back-end compiler link.<br><br>To add a icon on the fly. You can use Import or put the icon in the .data folder but you must add<br><pre class=code>#IMAGE_FILES="*.png|*.jpg|*.ico" 'Windows
#IMAGE_FILES="*.png|*.jpg|*.icns" 'OS X</pre><br>and name the icon app_icon.ico or app_icon.icns.<br><br>The code.<br><br>Back up and modify src/trans/targets/glfw.monkey<br><br>At the top of the file under "Import target" add "Import modify"<br>Change the methods MakeMingw, MakeVc2010 and MakeXcode to read..<br><pre class=code>	Method MakeMingw()
		CreateDir "mingw/"+CASED_CONFIG
		
		CreateDataDir "mingw/"+CASED_CONFIG+"/data"

		Local main$=LoadString( "main.cpp" )
		
		main=ReplaceBlock( main,"TRANSCODE",transCode )
		main=ReplaceBlock( main,"CONFIG",Config() )
		
		'#### DAWLANE CHANGE
		'Change the projects back-end build files
		Local project:String = AlterBackEnd.Project()
		main = AlterBackEnd.Main(main)
		'##### CHANGE END
		
		SaveString main,"main.cpp"
		
		If OPT_ACTION &gt;= ACTION_BUILD

			ChangeDir "mingw"

			Local ccopts:=""
			Select ENV_CONFIG
			Case "release"
				ccopts="-O3 -DNDEBUG"
			Case "debug"
			End
			
			'#### DAWLANE CHANGE
			Execute "mingw32-make CCOPTS=~q" + ccopts + "~q OUT=~q" + CASED_CONFIG + "/" + project + "~q"
			'##### CHANGE END
			
			If OPT_ACTION&gt;=ACTION_RUN
				ChangeDir CASED_CONFIG
				'#### DAWLANE CHANGE
				Execute project
				'##### CHANGE END
			Endif
			
		Endif
	End
	
	Method MakeVc2010()
		CreateDir "vc2010/"+CASED_CONFIG
		
		CreateDataDir "vc2010/"+CASED_CONFIG+"/data"
		
		Local main$=LoadString( "main.cpp" )
		
		main=ReplaceBlock( main,"TRANSCODE",transCode )
		main=ReplaceBlock( main,"CONFIG",Config() )
		
		'#### DAWLANE CHANGE
		'Change the projects back-end build files
		Local project:String = AlterBackEnd.Project()
		main = AlterBackEnd.Main(main)
		'##### CHANGE END
		
		SaveString main,"main.cpp"
		
		If OPT_ACTION&gt;=ACTION_BUILD

			ChangeDir "vc2010"
			
			'#### DAWLANE CHANGE
			Execute MSBUILD_PATH + " /p:Configuration=" + CASED_CONFIG + ";Platform=~qwin32~q ~q" + project + ".sln~q"
			'##### CHANGE END
			
			If OPT_ACTION&gt;=ACTION_RUN
				ChangeDir CASED_CONFIG
				Execute project
			Endif
			
		Endif
	End
	
	Method MakeXcode()
		CreateDataDir "xcode/data"

		Local main$=LoadString( "main.cpp" )
		
		main=ReplaceBlock( main,"TRANSCODE",transCode )
		main=ReplaceBlock( main,"CONFIG",Config() )
		
		'#### DAWLANE CHANGE
		'Change the projects back-end build files
		Local project:String = AlterBackEnd.Project()
		main = AlterBackEnd.Main(main)
		'##### CHANGE END
		
		SaveString main, "main.cpp"
		
		If OPT_ACTION&gt;=ACTION_BUILD

			ChangeDir "xcode"
		
			Execute "xcodebuild -configuration " + CASED_CONFIG
			'#### DAWLANE CHANGE
			'Remove the extra icon from the app bundle
			AlterBackEnd.RemoveExtras()
			'##### CHANGE END
			
			If OPT_ACTION&gt;=ACTION_RUN
				ChangeDir "build/" + CASED_CONFIG
				
				'#### DAWLANE CHANGE
				ChangeDir project + ".app/Contents/MacOS"
				Execute "./" + project
				'##### CHANGE END
			Endif
			
		Endif
	End</pre><br>For src/trans/targets/stdcpp.monkey<br>Add Import modify to the top of the file under "Import target"<br>And change<br><pre class=code>Local out$="main_" + HostOS</pre> to <pre class=code>Local out:String = AlterBackEnd.Project() + "_" + HostOS</pre><br><br>Now make a new file and name it as modify.monkey and save it in src/trans/targets then copy and paste the code below.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'Version 0.3
Strict
Import trans.trans
Import os

Global AlterBackEnd:Modify = New Modify

Class Modify

	Field project_name:String 		'May use this at some point so generate exe's can have spaces currently just used to hold the name
	Field project:String			'The name of the project with out spaces
	Field project_path:String		'Current compilers project path
	Field old_project:String		'The last project that was builts name without spaces
	Field use_mingw:Bool = False	'True if the mingw compiler is to be used
	Field current_file:String		'Temp work file for when a project file needs modifying
	
	'Call this Method for checking and altering the projects name
	Method Project:String()
		'The projects name
		Self.project_name = Env.Get("PROJECT_NAME")
		
		'If there isn't then just use the Monkey default			
		If Self.project_name = ""
			Print "USING DEFAULT NAMES"
			If ENV_TARGET = "stdcpp"
				Self.project_name = "main"
			Else
				Self.project_name = "MonkeyGame"
			EndIf
		EndIf
		
		'Get rid of any spaces
		Self.project = StripSpaces()
		
		'Print some info
		Print "Project name = " + Self.project_name
		Print "TARGET = " + ENV_TARGET
		Print "HOST = " + ENV_HOST
				
		'See What Target were building for
		Select ENV_TARGET
			
			Case "glfw"
							
				Print "Projects Back End Files to be named = " + Self.project
				
				'Say what compiler is to be used
				If Env.Get("GLFW_USE_MINGW") = "true" Then use_mingw = True Else use_mingw = False
				Print "USING COMPILER"
				
				'Find what the host is for glfw
				Select ENV_HOST
					Case "winnt"
						If Self.use_mingw Then Print "MINGW-32" Else Print "VISUAL STUDIO C++"
						'Set the project path to reflect which compiler is to be used
						If Self.use_mingw Then Self.project_path = "mingw/" Else Self.project_path = "vc2010/"
						'Are we to rename?
						If Self.HasChanged()
							'If not using MINGW then use VC++
							If Not use_mingw
								Self.VC_ChangeGLFW()
							 Else
							 	Print ""
								Print "RE-NAMING MINGW PROJECT......"
								Print ""
							Endif
							'Atempt to delete any old build executable
							DeleteFile(Self.project_path + "Release/" + Self.old_project + ".exe")
							DeleteFile(Self.project_path + "Debug/" + Self.old_project + ".exe")
						EndIf
						'Atempt to make a resource file
						Self.MakeRC_WINNT()
						
					Case "macos"
						Print "XCODE"
						'Set the project path to reflect which compiler is to be used
						Self.project_path = "xcode/"
						'Are we to rename?
						If Self.HasChanged()
							MACOS_ChangeGLFW()
							'Atempt to delete any old build executable
							DeleteDir(Self.project_path + "build/" + Self.old_project + ".build",True)
							DeleteDir(Self.project_path + "build/Release/" + Self.old_project + ".app", True)
							DeleteDir(Self.project_path + "build/Debug/" + Self.old_project + ".app", True)
							DeleteDir(Self.project_path + "build/Release/" + Self.old_project + ".app.dSYM", True)
							DeleteDir(Self.project_path + "build/Debug/" + Self.old_project + ".app.dSYM",True)
						EndIf
						'Atempt to make a resource file
						Self.MakeRC_MACOS()					
				End Select
				
			Case "stdcpp"
				Select ENV_HOST
					Case "winnt"		
						If Self.HasChanged()
							DeleteFile(Self.project_path + Self.old_project + "_winnt.exe")
						Endif				
					Case "macos"
					
						If Self.HasChanged()
							DeleteFile(Self.project_path + Self.old_project + "_macos")
						Endif
				End Select
				
			Case "html5"
			Case "flash"
			Case "android"
			Case "xna"
			Case "ios"
			
		End Select
		Return Self.project
	End Method
	
	'Remove any unwanted files
	Method RemoveExtras:Void( dir:String="", item:String = "" )
		Select ENV_HOST
		
			Case "macos"
				Select ENV_TARGET
					Case "glfw"
						Print ""
						Print "REMOVING EXTRA ICON COPIED TO APP BUNDLE"
						DeleteFile( "build/Release/" + Self.project + ".app/Contents/Resources/data/app_icon.icns")
						DeleteFile( "build/Debug/" + Self.project + ".app/Contents/Resources/data/app_icon.icns")
				End Select
		End Select
			
	End Method
	
	'Call this method to change what savestate name is to be used 
	'First parm is the file to scan the second parm is optional and is the new name to be used
	Method Main:String(main:String, project:String = "")
		If project &gt; "" Then Self.project = project
		'Differet thing for different targets
		Select ENV_TARGET
			Case "glfw"
				main = ChangeMainGLFW(main)
		End Select
		Return main
	End Method
	
	'Parse the file for any glfw savestate names
	Method ChangeMainGLFW:String(main:String)
	
		Local pro:String
		Local old:String
		
		If Self.project = "" Then pro = "monkey" 'make sure that something gets writen
		
		'Force the names to lower case
		pro = Self.project.ToLower()
		old = Self.old_project.ToLower()
		
		Local app_state:String = "." + pro
		
		If main.Find("fp=fopen( " + String.FromChar(34) + ".monkeystate") &gt; - 1
			main = main.Replace("fp=fopen( " + String.FromChar(34) + ".monkeystate", "fp=fopen( " + String.FromChar(34) + app_state + "state")
		Else
			main = main.Replace("fp=fopen( " + String.FromChar(34) + "." + old + "state", "fp=fopen( " + String.FromChar(34) + app_state + "state")
		Endif
		
		Return main	
	End
	
	'Load and alter Visual Studio project files
	'Not very elegant way of parsing but gets the job done
	Method VC_ChangeGLFW:Void()
		Print ""
		Print "RE-NAMING VISUAL STUDIO PROJECT......"
		Print ""
		
		'Load, change the old projects name and save the new sln file						
		Self.current_file = LoadString(Self.project_path + Self.old_project + ".sln")												
		Self.current_file = Self.current_file.Replace(Self.old_project, Self.project)
		SaveAndDelete(Self.current_file, Self.project_path, Self.project, ".sln", Self.old_project)
		Print "SAVING: " + Self.old_project + ".sln as " + Self.project + ".sln"
					
		'Load, change the old, save the new projects vcxproj file	
		Self.current_file = LoadString(Self.project_path + Self.old_project + ".vcxproj")
		Self.current_file = Self.current_file.Replace(Self.old_project, Self.project)
		SaveAndDelete(Self.current_file, Self.project_path, Self.project, ".vcxproj", Self.old_project)
		Print "SAVING: " + old_project + ".vcxproj as " + project + ".vcxproj"
					
		'Change the name of the vcxproj.filters file	
		Self.current_file = LoadString(Self.project_path + Self.old_project + ".vcxproj.filters")
		SaveAndDelete(Self.current_file, Self.project_path, Self.project, ".vcxproj.filters", Self.old_project)
					
		'Add a vcxproject.user to fix the root name space					
		Self.current_file = "&lt;?xml version=" + String.FromChar(34) + "1.0" + String.FromChar(34) + " encoding=" + String.FromChar(34) + "utf-8" + String.FromChar(34) + "?&gt;~n"
		Self.current_file += "&lt;Project ToolsVersion=" + String.FromChar(34) + "4.0" + String.FromChar(34) + " xmlns=" + String.FromChar(34) + "http://schemas.microsoft.com/developer/msbuild/2003" + String.FromChar(34) + "&gt;~n"
		Self.current_file += "&lt;/Project&gt;"
		SaveAndDelete(Self.current_file, Self.project_path, Self.project, ".vcxproj.user", Self.old_project)
		
	End Method
	
	'See if the project name has change from the last time
	'Puts a file in the projects root folder with the to be used use
	Method HasChanged:bool()
		'check to see if there is a a file that has the old name
		Self.old_project = LoadString(Self.project_path + "dont_delete.txt")
		
		'If there isn't then assume all files are MonkeyGame
		If Self.old_project = ""
			If ENV_TARGET = "stdcpp"
				Self.old_project = "main"
			Else
				Self.old_project = "MonkeyGame"
			EndIf
		EndIf

		'If the project name has changed the update the file and save it
		If Self.old_project &lt;&gt; Self.project_name
			'Save the projects new title for later
			SaveString(Self.project_name, Self.project_path + "dont_delete.txt")
			Print "STORED PROJECTS NAME.........."
			Self.old_project = StripSpaces(Self.old_project)
			Return True
		Endif
		Return False
	End Method
	
	'Strip any spaces from out of the projects name
	Method StripSpaces:String(name:String = project_name)
		Local str:String
		For Local i:Int = 0 To name.Length() -1
			If name[i] &gt; 32 Then str += String.FromChar(name[i])
		Next
		Return str
	End
	
	'Detete the old and save the new
	Method SaveAndDelete:Void( data:String, path:String, name:String, ext:String, old_name:String )
		Print "Saving: " + old_name + ext + " as " + name + ext
		DeleteFile( path + old_name + ext )
		SaveString( data, path + name + ext )		
	End
	
	'Go through the projects back end build files and do the alterations.
	'Again not very elegant way of parsing but gets the job done
	Method MACOS_ChangeGLFW:Void()
						
		Print ""
		Print "RE-NAMING MAC OS PROJECT......"
		Print ""
				
		'Change the name of pch and save it with the new (don't bother changing contents as it's irrelevant to the build)
		Self.current_file = LoadString(Self.project_path + Self.old_project + "_Prefix.pch")
		SaveAndDelete(Self.current_file, Self.project_path, Self.project + "_Prefix", ".pch", Self.old_project + "_Prefix")
		Print "Saved: " + Self.old_project + "_Prefix.pch as " + Self.project + "_Prefix.pch"
				
		'Change the name of the plist to the new
		Self.current_file = LoadString(Self.project_path + Self.old_project + "-Info.plist")				
		SaveAndDelete(Self.current_file, Self.project_path, Self.project + "-Info", ".plist", Self.old_project + "-Info")
		Print "Saved: " + old_project + "-Info.plist as " + project + "-Info.plist"
						
		'As MonkeyGame.xcodeproj is actually a directory we'll copy it with a new name and delete the old
		CopyDir(Self.project_path + Self.old_project + ".xcodeproj", Self.project_path + Self.project + ".xcodeproj", True, True)
		DeleteDir(Self.project_path + Self.old_project + ".xcodeproj", True)
		Print "Saved: " + Self.old_project + ".xcodeproj as " + Self.project + ".xcodeproj"
				
		'Swith to the new xcode project
		ChangeDir(Self.project_path + Self.project + ".xcodeproj")
		Print "Changing path to " + Self.project_path + Self.project + ".xcodeproj"
				
		'We should only need to change project.pbxproj and whats inside project.xcworkspace-&gt;contents.xcworkspacedata
		'First project.pbxproj
		Self.current_file = LoadString("project.pbxproj")
		'Find all _Prefix.pch
		Self.current_file = Self.current_file.Replace(Self.old_project + "_Prefix.pch", Self.project + "_Prefix.pch")
		'Next all -Info.plist
		Self.current_file = Self.current_file.Replace(Self.old_project + "-Info.plist", Self.project + "-Info.plist")
		'Next all .app
		Self.current_file = Self.current_file.Replace(Self.old_project + ".app", Self.project + ".app")
		'Next the name
		Self.current_file = Self.current_file.Replace("name = " + Self.old_project + ";", "name = " + Self.project + ";")
		Self.current_file = Self.current_file.Replace("productName = " + Self.old_project + ";", "productName = " + Self.project + ";")
		Self.current_file = Self.current_file.Replace("PRODUCT_NAME = " + Self.old_project + ";", "PRODUCT_NAME = " + Self.project + ";")
							
		'Save the altered file
		SaveAndDelete(Self.current_file, "", "project", ".pbxproj", "project")
		Print "Changed and re-saving file project.pbxproj"
				
		'Next change the contest of project.xcworkspace
		ChangeDir("project.xcworkspace")
				
		'Change and save
		Self.current_file = LoadString("contents.xcworkspacedata")
		Self.current_file = Self.current_file.Replace("self:" + Self.old_project, "self:" + Self.project)
				
		'Save the altered file
		SaveAndDelete(Self.current_file, "", "contents", ".xcworkspacedata", "contents")
		Print "Changed and re-saving file contents.xcworkspacedata"
				
		'Jump back to the builds root glfw directory
		ChangeDir("../../../")
	End
	
	'Automaticaly create a resource file for Windows if there is a icon file with the name app_icon.ico in the data folder
	Method MakeRC_WINNT:Void()
		
		Local index:Int = 0
		Local temp_file:String[]
		
		'If there is no resource file but there is an app_icon the make a resource file and move the app_icon to the location of the projects compiler build folder				
		If CopyFile(project_path + "Debug/data/app_icon.ico", project_path + "app_icon.ico") Or CopyFile(project_path + "Release/data/app_icon.ico", project_path + "app_icon.ico")
			Print ""
			Print "DEFAULT ICON FOUND AND COPIED.....Windows"
			
			'Create a rc file if ther isn't one			
			current_file = LoadString(project_path + "resource.rc")
			If current_file = ""
				current_file = "GLFW_ICON ICON " + String.FromChar(34) + "app_icon.ico" + String.FromChar(34)
				SaveString(current_file, project_path + "resource.rc")
			
				'Creat a rc include file
				current_file = "#include " + String.FromChar(34) + "resource.rc" + String.FromChar(34)
				SaveString(current_file, project_path + "resource.h")
				Print "CREATED NEW RESOURCE FILE"
				
				'Modify the project back-ends to reflect that there is a RC file
				If use_mingw
					'MINGW PROJECT
					current_file = LoadString(project_path + "Makefile")
					current_file = current_file.Replace("../main.o", "../main.o resource.res")
					SaveString(current_file, project_path + "Makefile")
					Print "MODIFIED MAKEFILE TO INCLUDE RESOURCE FILE"
					DeleteFile("resource.res")
					Print "COMPILING RESOURCE FILE"
					Execute "windres " + project_path + "resource.rc -O coff -o " + project_path + "resource.res"
				Else
					'VC2010 PROJECT: Another rough way of parsing
					current_file = LoadString(project_path + project + ".vcxproj")
					temp_file = current_file.Split("~r")
					
					'Find the last xml Item Group tag
					index = Seek( "&lt;/ItemGroup&gt;", temp_file )
					temp_file = Insert("    &lt;CInclude Include=" + String.FromChar(34) + "resource.h" + String.FromChar(34) + " /&gt;", temp_file, index)
					index += 1
					temp_file = Insert("  &lt;/ItemGroup&gt;", temp_file, index)
					index += 1
					temp_file = Insert("  &lt;ItemGroup&gt;", temp_file, index)
					index += 1
					temp_file = Insert("    &lt;ResourceCompile Include=" + String.FromChar(34) + "resource.rc" + String.FromChar(34) + " /&gt;", temp_file, index)
					
					'Rebuild the file before saving it
					current_file = Rebuild( temp_file )
					Print "MODIFIED VISUAL STUDIO PROJECT TO INCLUDE RESOURCE FILE"					
					SaveAndDelete(current_file, project_path, project, ".vcxproj", project)
					
				EndIf
			Endif
			Print ""
		Endif
	End Method
	
	'Automaticaly add a icon to the app bundle if there is an icon named app_icon.icns in the data folder
	Method MakeRC_MACOS:Void()
	
		'WARNING EDITING XCODE PROJECT FILES DIRECTLY TO ADD DATA IS NOT WISE, SO IF THIS FAILS REMOVE OR MODIFY THE CODE BELOW
		'AND RECOMPILE TRANS THEN DELETE THE .build/glfw folder IN YOUR SOUCE CODES DIRECTORY THEN TRY AGAIN
				
		If CopyFile(project_path + "data/app_icon.icns", project_path + "app_icon.icns")
			Print ""
			Print "DEFAULT ICON FOUND AND COPIED....."
			'Load the project file
			current_file = LoadString(project_path + project + ".xcodeproj/project.pbxproj")
					
			'Chect to see if there is an icon already in it
			Local index:Int = current_file.Find("app_icon.icns")
			If index &lt; 0
				index = 0
				'If there isn't the add the data to the project file
				Local temp_file:String[] = current_file.Split("~n")
				
				index = Seek("/* End PBXBuildFile section */", temp_file)
				temp_file = Insert("968D451915F3A2B400A9E97C /* app_icon.icns in Resources */ = {isa = PBXBuildFile; fileRef = 968D451815F3A2B400A9E97C /* app_icon.icns */; };", temp_file, index)
				index = Seek("/* End PBXFileReference section */", temp_file, index)
				temp_file = Insert("968D451815F3A2B400A9E97C /* app_icon.icns */ = {isa = PBXFileReference; lastKnownFileType = image.icns; path = app_icon.icns; sourceTree = " + String.FromChar(34) + "&lt;group&gt;" + String.FromChar(34) + "; };", temp_file, index)
				index = Seek("29B97314FDCFA39411CA2CEA", temp_file, index) 'This should be the MonkeyGame if it's different fine the new GUID and replace it
				index += 3
				temp_file = Insert("968D451815F3A2B400A9E97C /* app_icon.icns */,", temp_file, index)
				index = Seek("/* Begin PBXResourcesBuildPhase section */", temp_file, index)
				index += 7
				temp_file = Insert("968D451915F3A2B400A9E97C /* app_icon.icns in Resources */,", temp_file, index)
				current_file = Rebuild(temp_file)
				SaveString(current_file, project_path + project + ".xcodeproj/project.pbxproj")
						
				current_file = LoadString(project_path + project + "-Info.plist")
				current_file = current_file.Replace("&lt;string&gt;&lt;/string&gt;", "&lt;string&gt;app_icon&lt;/string&gt;")
				SaveString(current_file, project_path + project + "-Info.plist")
				Print "MODIFIED XCODE PROJECT TO INCLUDE ICON FILE"
			Endif
			Print ""
		Endif
	End Method
	
	'Join any string arrays to make a whole string
	Method Rebuild:String( str:String[] )
		Local build:String
		For Local i:Int = 0 To str.Length() - 1
			build += str[i] + "~n"
		Next
		Return build
	End Method
	
	'Look for a string and return it's index number
	Method Seek:Int( str:String, temp_file:String[], start:Int = 0 )
		For Local i:Int = start to temp_file.Length() -1
			If temp_file[i].Contains( str ) Then Return i
		Next
		Return -1
	End Method
	
	'Resize and instert a string
	Method Insert:String[] (str:String, dst:String[], index:Int)
		dst = dst.Resize(dst.Length + 1)
		For Local i:Int = dst.Length() -1 to index Step - 1
			dst[i] = dst[i - 1]
		Next
		dst[index] = str
		Return dst
	End Method
	
End Class</textarea><br><br>With that done open src/trans/trans.monkey and compile it, when that's done copy the file main_winnt or main_macos in src/trans/trans.build/stdcpp folder to monkeys bin folder.<br>Back up the trans executable by renaming it, then rename the main_winnt or main_macos to trans_winnt or trans_macos. <br><br></td></tr></table><br>
<a name="2037945"></a>

<a name="2037944"></a>

<a name="2037942"></a>

<a name="2037941"></a>

<a name="2037939"></a>

<a name="2037938"></a>

<a name="2037937"></a>

<a name="2037936"></a>

<a name="2037914"></a>

<a name="2037913"></a>

<a name="2037912"></a>

<a name="2037911"></a>

<a name="2037918"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Samah</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Would you like to add this to monkey-ext?  If so, make a Google Code clone, update to the development branch, then make your changes and push.  I'll check it out and mention any changes you might want to make.<br><br><a href="http://code.google.com/p/monkey-ext/" target="_blank">http://code.google.com/p/monkey-ext/</a> <br><br></td></tr></table><br>
<a name="2037916"></a>

<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
