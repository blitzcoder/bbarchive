<!DOCTYPE html><html lang="en" ><head ><title >Virtual Stick</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Virtual Stick</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Virtual Stick</a><br><br>
<a name="2002936"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Samah</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> One of the staple components of many touchscreen games is the so-called "virtual stick".  Basically it's a small circle that you touch and move your thumb around in a similar manner to an analog stick on a game controller.<br><br>Here's my take on it, though it's mostly a work-in-progress.  If you see any glaring errors, please fix and/or let me know. ;)<br><br>The test app is ugly, but the VirtualStick class is pretty easy to use.<br>If you're testing it on Android, it'd be best to force landscape and fullscreen.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
strict
import mojo

class VirtualStickTestApp extends App
	const PLAYFIELD_WIDTH:float = 200
	const PLAYFIELD_HEIGHT:float = 200
	const PLAYER_SPEED:float = 5
	
	' our virtual stick
	field mystick:MyStick
	
	' whether or not we're "touching"
	#if TARGET="android" then
		field touching:bool[] = new bool[32]
	#else
		field touching:bool
	#endif
	
	' the "player"'s location
	field playerX:float = PLAYFIELD_WIDTH/2
	field playerY:float = PLAYFIELD_HEIGHT/2
	field playfieldX:float
	field playfieldY:float = 10
	
	method OnCreate:int()
		mystick = new MyStick
		mystick.SetRing(100, DeviceHeight()-100, 40)
		mystick.SetStick(0, 0, 15)
		mystick.SetDeadZone(0.2)
		mystick.SetTriggerDistance(5)
		playfieldX = DeviceWidth()-PLAYFIELD_WIDTH-10
		SetUpdateRate 30
		return 0
	end
	
	method OnUpdate:int()
		' update the stick usage
		#if TARGET="android" then
			for local i:int = 0 to 31
				if TouchHit(i) then
					touching[i] = true
					mystick.StartTouch(TouchX(i), TouchY(i), i)
				elseif TouchDown(i) then
					mystick.UpdateTouch(TouchX(i), TouchY(i), i)
				elseif touching[i] then
					touching[i] = false
					mystick.StopTouch(i)
				end
			end
		#else
			if MouseHit(0) then
				touching = true
				mystick.StartTouch(MouseX(), MouseY(), 0)
			elseif MouseDown(0) then
				mystick.UpdateTouch(MouseX(), MouseY(), 0)
			elseif touching then
				touching = false
				mystick.StopTouch(0)
			end
		#endif

		' update the player position
		if mystick.GetVelocity() &lt;&gt; 0 then
			playerX += mystick.GetDX() * PLAYER_SPEED
			playerY -= mystick.GetDY() * PLAYER_SPEED
			if playerX &lt; 0 then
				playerX = 0
			elseif playerX &gt; PLAYFIELD_WIDTH then
				playerX = PLAYFIELD_WIDTH
			end
			if playerY &lt; 0 then
				playerY = 0
			elseif playerY &gt; PLAYFIELD_HEIGHT then
				playerY = PLAYFIELD_HEIGHT
			end
		end
		return 0
	end
	
	method OnRender:int()
		Cls(0,0,0)
		
		mystick.DoRenderRing()
		mystick.DoRenderStick()
		DrawOutlineRect(playfieldX, playfieldY, PLAYFIELD_WIDTH, PLAYFIELD_HEIGHT)
		DrawCircle(playfieldX + playerX, playfieldY + playerY, 5)
		
		' some test info
		DrawText("angle="+mystick.GetAngle(), 10, 10)
		DrawText("vel="+mystick.GetVelocity(), 10, 30)
		DrawText("dx="+mystick.GetDX(), 10, 50)
		DrawText("dy="+mystick.GetDY(), 10, 70)
		return 0
	end
end

class MyStick extends VirtualStick
	method RenderRing:void(x:float, y:float)
		SetColor 0, 0, 255
		super.RenderRing(x, y)
		SetColor 255, 255, 255
	end
	
	method RenderStick:void(x:float, y:float)
		SetColor 0, 255, 0
		super.RenderStick(x, y)
		SetColor 255, 255, 255
	end
end

class VirtualStick
private
	' the coordinates and dimensions for the virtual stick's ring (where the user will first touch)
	field ringX:float
	field ringY:float
	field ringRadius:float
	
	' the coordinates and dimensions for the stick (what the user is pushing around)
	' X/Y is relative to the centre of the ring, and positive Y points up
	field stickX:float = 0
	field stickY:float = 0
	field stickRadius:float
	field stickAngle:float
	field stickPower:float
	
	' where the user first touched
	field firstTouchX:float
	field firstTouchY:float
	
	' power must always be &gt;= this, or we return 0
	field deadZone:float
	
	' we need to move the stick this much before it triggers
	field triggerDistance:float = -1
	field triggered:bool = false
	
	' the index of the touch event that initiated the stick movement
	field touchNumber:int = -1
	
	' clips the stick to be within the ring, and updates angles, etc.
	method UpdateStick:void()
		if touchNumber&gt;=0 then
			local length:float = Sqrt(stickX*stickX+stickY*stickY)
			stickPower = length/ringRadius
			if stickPower &gt; 1 then stickPower = 1
			
			if stickPower &lt; deadZone then
				stickPower = 0
				stickAngle = 0
				stickX = 0
				stickY = 0
			else
				if stickX = 0 and stickY = 0 then
					stickAngle = 0
					stickPower = 0
				elseif stickX = 0 and stickY &gt; 0 then
					stickAngle = 90
				elseif stickX = 0 and stickY &lt; 0 then
					stickAngle = 270
				elseif stickY = 0 and stickX &gt; 0 then
					stickAngle = 0
				elseif stickY = 0 and stickX &lt; 0 then
					stickAngle = 180
				elseif stickX &gt; 0 and stickY &gt; 0 then
					stickAngle = ATan(stickY/stickX)
				elseif stickX &lt; 0 then
					stickAngle = 180+ATan(stickY/stickX)
				else
					stickAngle = 360+ATan(stickY/stickX)
				end
				if length &gt; ringRadius then
					stickPower = 1
					stickX = Cos(stickAngle) * ringRadius
					stickY = Sin(stickAngle) * ringRadius
				end
			end
		end
	end
	
public

	' the angle in degrees that the user is pushing, going counter-clockwise from right
	method GetAngle:float()
		return stickAngle
	end
	
	' the strength of the movement (0 means dead centre, 1 means at the edge of the ring (or past it)
	method GetVelocity:float()
		return stickPower
	end
	
	' based on the angle and velocity, get the DX
	method GetDX:float()
		return Cos(stickAngle) * stickPower
	end
	
	' based on the angle and velocity, get the DY
	method GetDY:float()
		return Sin(stickAngle) * stickPower
	end
	
	' we just touched the screen at point (x,y), so start "controlling" if we touched inside the ring
	method StartTouch:void(x:float, y:float, touchnum:int)
		if touchNumber &lt; 0 then
			if (x-ringX)*(x-ringX) + (y-ringY)*(y-ringY) &lt;= ringRadius*ringRadius then
				touchNumber = touchnum
				firstTouchX = x
				firstTouchY = y
				triggered = false
				if triggerDistance &lt;= 0 then
					triggered = true
					stickX = x-ringX
					stickY = ringY-y
				end
				UpdateStick()
			end
		end
	end
	
	' a touch just moved, so we may need to update the stick
	method UpdateTouch:void(x:float, y:float, touchnum:int)
		if touchNumber&gt;=0 and touchNumber = touchnum then
			if not triggered then
				if (x-firstTouchX)*(x-firstTouchX)+(y-firstTouchY)*(y-firstTouchY) &gt; triggerDistance*triggerDistance then
					triggered = true
				end
			end
			if triggered then
				stickX = x - ringX
				stickY = ringY - y
				UpdateStick()
			end
		end
	end
	
	' we just released a touch, which may have been this one
	method StopTouch:void(touchnum:int)
		if touchNumber&gt;=0 and touchNumber = touchnum then
			touchNumber = -1
			stickX = 0
			stickY = 0
			stickAngle = 0
			stickPower = 0
			triggered = false
		end
	end
	
	method DoRenderRing:void()
		RenderRing(ringX, ringY)
	end
	
	method DoRenderStick:void()
		RenderStick(ringX+stickX, ringY-stickY)
	end
	
	' draws the stick (may be overridden to do images, etc.)
	method RenderStick:void(x:float, y:float)
		DrawCircle(x, y, stickRadius)
	end
	
	' draws the outside ring (may be overridden to do images, etc.)
	method RenderRing:void(x:float, y:float)
		DrawCircle(x, y, ringRadius)
	end
	
	' set the location and radius of the ring
	method SetRing:void(ringX:float, ringY:float, ringRadius:float)
		self.ringX = ringX
		self.ringY = ringY
		self.ringRadius = ringRadius
	end
	
	' set the location and radius of the stick
	method SetStick:void(stickX:float, stickY:float, stickRadius:float)
		self.stickX = stickX
		self.stickY = stickY
		self.stickRadius = stickRadius
	end
	
	method SetDeadZone:void(deadZone:float)
		self.deadZone = deadZone
	end
	
	method SetTriggerDistance:void(triggerDistance:float)
		self.triggerDistance = triggerDistance
	end
end

function Main:int()
	new VirtualStickTestApp
	return 0
end

function DrawOutlineRect:void(x:float, y:float, width:float, height:float)
	DrawLine(x, y, x+width, y)
	DrawLine(x, y, x, y+height)
	DrawLine(x+width, y, x+width, y+height)
	DrawLine(x, y+height, x+width, y+height)
end
</textarea> <br><br></td></tr></table><br>
<a name="2002880"></a>

<a name="2002883"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Raz</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice one thank you :) This will be very handy for me soonish. <br><br></td></tr></table><br>
<a name="2002885"></a>

<a name="2002886"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >pinete</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> useful!!! :) thank you very much! <br><br></td></tr></table><br>
<a name="2002939"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cool :)<br><br>Im not a fan of this sort of controls as it blocks the screen (with the control and your fingers), but I realise in some games you need to do it this way on a touch screen device...<br><br>Oh BTW why do you loop thru all 32 touch events?<br><pre class=code>
			for local i:int = 0 to 31
				if TouchHit(i) then
					touching[i] = true
					mystick.StartTouch(TouchX(i), TouchY(i), i)
				elseif TouchDown(i) then
					mystick.UpdateTouch(TouchX(i), TouchY(i), i)
				elseif touching[i] then
					touching[i] = false
					mystick.StopTouch(i)
				end
			end

</pre> <br><br></td></tr></table><br>
<a name="2002946"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Samah</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Because I'm a noob and don't know how the touch system works yet. :)<br>What's the best way to check for a touch event in a given area?  I can't guarantee it'll be the first touch event.<br><br>I threw that code together really quickly, so I realise now I can make it check only the relevant touch event once it's been started.<br><br>Updated version:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
strict
import mojo

class VirtualStickTestApp extends App
	const PLAYFIELD_WIDTH:float = 200
	const PLAYFIELD_HEIGHT:float = 200
	const PLAYER_SPEED:float = 5
	
	' our virtual stick
	field mystick:MyStick
	
	' the "player"'s location
	field playerX:float = PLAYFIELD_WIDTH/2
	field playerY:float = PLAYFIELD_HEIGHT/2
	field playfieldX:float
	field playfieldY:float = 10
	
	method OnCreate:int()
		mystick = new MyStick
		mystick.SetRing(100, DeviceHeight()-100, 40)
		mystick.SetStick(0, 0, 15)
		mystick.SetDeadZone(0.2)
		mystick.SetTriggerDistance(5)
		playfieldX = DeviceWidth()-PLAYFIELD_WIDTH-10
		SetUpdateRate 30
		return 0
	end
	
	method OnUpdate:int()
		' update the stick usage
		UpdateStick()

		' update the player position
		if mystick.GetVelocity() &lt;&gt; 0 then
			playerX += mystick.GetDX() * PLAYER_SPEED
			playerY -= mystick.GetDY() * PLAYER_SPEED
			if playerX &lt; 0 then
				playerX = 0
			elseif playerX &gt; PLAYFIELD_WIDTH then
				playerX = PLAYFIELD_WIDTH
			end
			if playerY &lt; 0 then
				playerY = 0
			elseif playerY &gt; PLAYFIELD_HEIGHT then
				playerY = PLAYFIELD_HEIGHT
			end
		end
		return 0
	end
	
	method OnRender:int()
		Cls(0,0,0)
		
		mystick.DoRenderRing()
		mystick.DoRenderStick()
		DrawOutlineRect(playfieldX, playfieldY, PLAYFIELD_WIDTH, PLAYFIELD_HEIGHT)
		DrawCircle(playfieldX + playerX, playfieldY + playerY, 5)
		
		' some test info
		DrawText("angle="+mystick.GetAngle(), 10, 10)
		DrawText("vel="+mystick.GetVelocity(), 10, 30)
		DrawText("dx="+mystick.GetDX(), 10, 50)
		DrawText("dy="+mystick.GetDY(), 10, 70)
		return 0
	end
	
	method UpdateStick:void()
		if mystick.GetTouchNumber() &lt; 0 then
			#if TARGET="android" then
				for local i:int = 0 to 31
					if TouchHit(i) and mystick.GetTouchNumber() &lt; 0 then
						mystick.StartTouch(TouchX(i), TouchY(i), i)
					end
				end
			#else
				if MouseHit(0) then
					mystick.StartTouch(MouseX(), MouseY(), 0)
				end
			#endif
		end
		
		if mystick.GetTouchNumber() &gt;= 0 then
			#if TARGET="android" then
				if TouchDown(mystick.GetTouchNumber()) then
					mystick.UpdateTouch(TouchX(mystick.GetTouchNumber()), TouchY(mystick.GetTouchNumber()))
				else
					mystick.StopTouch()
				end
			#else
				if MouseDown(0) then
					mystick.UpdateTouch(MouseX(), MouseY())
				else
					mystick.StopTouch()
				end
			#endif
		end
	end
end

class MyStick extends VirtualStick
	method RenderRing:void(x:float, y:float)
		SetColor 0, 0, 255
		super.RenderRing(x, y)
		SetColor 255, 255, 255
	end
	
	method RenderStick:void(x:float, y:float)
		SetColor 0, 255, 0
		super.RenderStick(x, y)
		SetColor 255, 255, 255
	end
end

class VirtualStick
private
	' the coordinates and dimensions for the virtual stick's ring (where the user will first touch)
	field ringX:float
	field ringY:float
	field ringRadius:float
	
	' the coordinates and dimensions for the stick (what the user is pushing around)
	' X/Y is relative to the centre of the ring, and positive Y points up
	field stickX:float = 0
	field stickY:float = 0
	field stickRadius:float
	field stickAngle:float
	field stickPower:float
	
	' where the user first touched
	field firstTouchX:float
	field firstTouchY:float
	
	' power must always be &gt;= this, or we return 0
	field deadZone:float
	
	' we need to move the stick this much before it triggers
	field triggerDistance:float = -1
	field triggered:bool = false
	
	' the index of the touch event that initiated the stick movement
	field touchNumber:int = -1
	
	' clips the stick to be within the ring, and updates angles, etc.
	method UpdateStick:void()
		if touchNumber&gt;=0 then
			local length:float = Sqrt(stickX*stickX+stickY*stickY)
			stickPower = length/ringRadius
			if stickPower &gt; 1 then stickPower = 1
			
			if stickPower &lt; deadZone then
				stickPower = 0
				stickAngle = 0
				stickX = 0
				stickY = 0
			else
				if stickX = 0 and stickY = 0 then
					stickAngle = 0
					stickPower = 0
				elseif stickX = 0 and stickY &gt; 0 then
					stickAngle = 90
				elseif stickX = 0 and stickY &lt; 0 then
					stickAngle = 270
				elseif stickY = 0 and stickX &gt; 0 then
					stickAngle = 0
				elseif stickY = 0 and stickX &lt; 0 then
					stickAngle = 180
				elseif stickX &gt; 0 and stickY &gt; 0 then
					stickAngle = ATan(stickY/stickX)
				elseif stickX &lt; 0 then
					stickAngle = 180+ATan(stickY/stickX)
				else
					stickAngle = 360+ATan(stickY/stickX)
				end
				if length &gt; ringRadius then
					stickPower = 1
					stickX = Cos(stickAngle) * ringRadius
					stickY = Sin(stickAngle) * ringRadius
				end
			end
		end
	end
	
public

	method GetTouchNumber:int()
		return touchNumber
	end
	
	' the angle in degrees that the user is pushing, going counter-clockwise from right
	method GetAngle:float()
		return stickAngle
	end
	
	' the strength of the movement (0 means dead centre, 1 means at the edge of the ring (or past it)
	method GetVelocity:float()
		return stickPower
	end
	
	' based on the angle and velocity, get the DX
	method GetDX:float()
		return Cos(stickAngle) * stickPower
	end
	
	' based on the angle and velocity, get the DY
	method GetDY:float()
		return Sin(stickAngle) * stickPower
	end
	
	' we just touched the screen at point (x,y), so start "controlling" if we touched inside the ring
	method StartTouch:void(x:float, y:float, touchnum:int)
		if touchNumber &lt; 0 then
			if (x-ringX)*(x-ringX) + (y-ringY)*(y-ringY) &lt;= ringRadius*ringRadius then
				touchNumber = touchnum
				firstTouchX = x
				firstTouchY = y
				triggered = false
				if triggerDistance &lt;= 0 then
					triggered = true
					stickX = x-ringX
					stickY = ringY-y
				end
				UpdateStick()
			end
		end
	end
	
	' a touch just moved, so we may need to update the stick
	method UpdateTouch:void(x:Float, y:float)
		if touchNumber&gt;=0 then
			if not triggered then
				if (x-firstTouchX)*(x-firstTouchX)+(y-firstTouchY)*(y-firstTouchY) &gt; triggerDistance*triggerDistance then
					triggered = true
				end
			end
			if triggered then
				stickX = x - ringX
				stickY = ringY - y
				UpdateStick()
			end
		end
	end
	
	' we just released a touch, which may have been this one
	method StopTouch:void()
		if touchNumber&gt;=0 then
			touchNumber = -1
			stickX = 0
			stickY = 0
			stickAngle = 0
			stickPower = 0
			triggered = false
		end
	end
	
	method DoRenderRing:void()
		RenderRing(ringX, ringY)
	end
	
	method DoRenderStick:void()
		RenderStick(ringX+stickX, ringY-stickY)
	end
	
	' draws the stick (may be overridden to do images, etc.)
	method RenderStick:void(x:float, y:float)
		DrawCircle(x, y, stickRadius)
	end
	
	' draws the outside ring (may be overridden to do images, etc.)
	method RenderRing:void(x:float, y:float)
		DrawCircle(x, y, ringRadius)
	end
	
	' set the location and radius of the ring
	method SetRing:void(ringX:float, ringY:float, ringRadius:float)
		self.ringX = ringX
		self.ringY = ringY
		self.ringRadius = ringRadius
	end
	
	' set the location and radius of the stick
	method SetStick:void(stickX:float, stickY:float, stickRadius:float)
		self.stickX = stickX
		self.stickY = stickY
		self.stickRadius = stickRadius
	end
	
	method SetDeadZone:void(deadZone:float)
		self.deadZone = deadZone
	end
	
	method SetTriggerDistance:void(triggerDistance:float)
		self.triggerDistance = triggerDistance
	end
end

function Main:int()
	new VirtualStickTestApp
	return 0
end

function DrawOutlineRect:void(x:float, y:float, width:float, height:float)
	DrawLine(x, y, x+width, y)
	DrawLine(x, y, x, y+height)
	DrawLine(x+width, y, x+width, y+height)
	DrawLine(x, y+height, x+width, y+height)
end
</textarea> <br><br></td></tr></table><br>
<a name="2002945"></a>

<a name="2002944"></a>

<a name="2003078"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Virtech</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Samah<br><br>Could you possibly update your example (or add one) that show how to create 2 virtual sticks? Thanks <br><br></td></tr></table><br>
<a name="2003138"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Samah</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Virtech<br>I'm currently working on an app that uses two sticks, and I'm cleaning up/enhancing the VirtualStick class as I go.  Once I'm happy with it I'll post the updated code with an example on using any number of sticks.<br><br>It's sorta hard for me to test multiple sticks at the moment since the Nexus One's multitouch is so damned awful.  Once the Xoom comes out (early next month hopefully) I'm ordering a Nexus S with it. <br><br></td></tr></table><br>
<a name="2003151"></a>

<a name="2003152"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Virtech</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cool! Looking forward to it :)<br><br>Edit: Multitouch on my samsung galaxy s works pretty good. I can test dual stick support on my phone if you wish. <br><br></td></tr></table><br>
<a name="2003150"></a>

<a name="2003632"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Perturbatio</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> This looks useful.  Thanks :) <br><br></td></tr></table><br>
<a name="2004431"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Loofadawg</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Works great on my Captivate ! <br><br></td></tr></table><br>
<a name="2005581"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >xzess</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> superb thanks very much for sharing! <br><br></td></tr></table><br>
<a name="2007158"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dopeyrulz</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Samah,<br><br>This works great on Windows Phone 7 - thanks for sharing.  Would be be possible to implement an old school 4/8 way stick with this process?<br><br>I've sorta hacked in a check based on the angles but something where it would lock directly into position (on the 8 axis of the compass) would be great. <br><br></td></tr></table><br>
<a name="2007171"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Samah</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm going to clean this up a bit at some point and check it in to the Diddy project. <br><br></td></tr></table><br>
<a name="2007189"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dopeyrulz</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah - ok.  Sounds good.  Thanks again for the code. <br><br></td></tr></table><br>
<a name="2010094"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks, this works great on the iPhone as well :) <br><br></td></tr></table><br>
<a name="2029439"></a>

<a name="2029440"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Neuro</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> This code actually works great but one issue i noticed on IOS target, that if you have 2 virtual joysticks on screen, the second one won't activate if the first joystick is touched first. However, if the second joystick is touched then both of them will actually work. <br><br>Oddly enough, this does NOT occur in HTML5 (when running on an IOS browser) though, not sure whats causing it to do this in IOS.<br><br>EDIT : nevermind, figured out the problem :) <br><br></td></tr></table><br>
<a name="2029449"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Paul - Taiphoz</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> I was also wondering about the looping through the touch events, as I understand it the first touch event is assumed to be the player 1 finger and gets treated as such.<br><br>So you would I think be able to drop that loop and just look for touch 1, unless your planning duel sticks, then it would be touch 1 and 2, touch two being called into play as soon as a second finger touches the screen at the same time.<br><br>I might be wrong tho, like you iv not really played with touch much at all. but all that being said I'm glad your working this into Diddy because its something I have on my todo list for Terminal 2, so you doing it now is gona save me tons of time. <br><br><br>thanks. and nice work. <br><br></td></tr></table><br>
<a name="2029450"></a>

<a name="2029451"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> EDIT : nevermind, figured out the problem :)  <br></div><br>What was the issue and how did you fix it? <br><br></td></tr></table><br>
<a name="2029487"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Neuro</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> What was the issue and how did you fix it?  <br></div><br>Wasn't really an issue, it was me stupidly forgetting to also filter by the IOS target on the second virtual joystick which is why it never activated :). <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
