<!DOCTYPE html><html lang="en" ><head ><title >Conway's Game of Life</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Conway's Game of Life</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Conway's Game of Life</a><br><br>
<a name="2033046"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >impixi</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> An implementation of Conway's Game of Life.<br><br>http://en.wikipedia.org/wiki/Conway's_Game_of_Life.<br><br>Not the simplest version, but that's the price of glitz and interactivity. ;)<br><br>To try different patterns, pause the 'game', click/touch cells, then unpause.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
#rem
An implementation of Conway's Game of Life

http://en.wikipedia.org/wiki/Conway's_Game_of_Life

Not the simplest version, but that's the price 
of glitz and interactivity. ;)

To try different patterns, pause the 'game', click/touch cells,
then unpause.

#end


Strict

Import mojo
Import monkey.random

Function Main:Int()

	New MyApp

	Return 0
	
End


'-------------------------------
Global ACTION_NONE:Int = 0
Global ACTION_PAUSETOGGLE:Int = 1
Global ACTION_SPEEDUP:Int = 2
Global ACTION_SPEEDDOWN:Int = 3
Global ACTION_RESET:Int = 4
Global ACTION_CLEAR:Int = 5
'-------------------------------

Class MyApp Extends App


	Field procCount:Int			'Process count (iterations)
	Field procRate:Float		'Process rate (hz)
	Field procVal:Float			
	Field paused:Bool

	Field map:World				'Map grid
	Field renderSys:RenSys

	Field action:Int


	Method OnCreate:Int()

		procCount = 0
		procVal = 0.0
		procRate = 8.0
		paused = False
		
		'Map creation and initialisation
		map = New World(30, 20)		
		map.Init()

		'Render system creation and initialisation
		renderSys = New RenSys(map)
		renderSys.SetGrid(5, 25, 20, 20, 1)
		Local lbls:String[] = "Pause, Speed+, Speed-, Reset, Clear".Split(",")
		Local tlx:Int = 5
		For Local i:Int = 0 Until lbls.Length
			Local butt:Button = renderSys.AddButton(lbls[i], tlx + (i * 20), DeviceHeight() - 25)
			tlx += butt.width
		Next
		
		renderSys.MakeStatusStr(procCount, procRate, paused)

		SetUpdateRate 30
	
		Return 0
		
	End

	
	Method OnUpdate:Int()
						
		action = ACTION_NONE
		
		If TouchHit(0)
	
			Local tchx:Int = TouchX(0)
			Local tchy:Int = TouchY(0)
			Local butt:Button = renderSys.GetTouchedButton(tchx, tchy)
			
			If butt 
				
				Local str:String = butt.label.ToLower()
				If str.Contains("pause") Then action = ACTION_PAUSETOGGLE
				If str.Contains("clear") Then action = ACTION_CLEAR
				If str.Contains("speed+") Then action = ACTION_SPEEDUP
				If str.Contains("speed-") Then action = ACTION_SPEEDDOWN
				If str.Contains("reset") Then action = ACTION_RESET
					
			Else
				
				Local tcell:Cell = renderSys.GetTouchedCell(tchx, tchy)
			
				If tcell
			
					If tcell.status = CELL_STATUS_ALIVE 
						tcell.status = CELL_STATUS_DEAD
					Else
						tcell.status = CELL_STATUS_ALIVE
					Endif
			
				Endif	
				
			Endif
					
		Endif
					
		If KeyHit(KEY_SPACE) Then action = ACTION_PAUSETOGGLE			
		If KeyHit(KEY_C) Then action = ACTION_CLEAR
		If KeyDown(KEY_EQUALS) Then action = ACTION_SPEEDUP
		If KeyDown(KEY_MINUS) Then action = ACTION_SPEEDDOWN 
		If KeyHit(KEY_ENTER) Then action = ACTION_RESET
	
		Select action
		
			Case ACTION_PAUSETOGGLE
				paused = Not paused
			
			Case ACTION_SPEEDUP
				procRate += 0.5
				If procRate &gt; UpdateRate() Then procRate = UpdateRate()
				
			Case ACTION_SPEEDDOWN
				procRate -= 0.5
				If procRate &lt; 0.5 Then procRate = 0.5
				
			Case ACTION_RESET
				Seed = Millisecs()
				map.Init
				procCount = 0
				procVal = 0.0
			
			Case ACTION_CLEAR
				map.ClearGrid()
		
		End
	
		If (Not paused)
			procVal += procRate
			If procVal &gt;= UpdateRate()
				procVal = 0.0
				procCount += 1
				map.Process()
			Endif
		Endif
	
		renderSys.MakeStatusStr(procCount, procRate, paused)
	
		Return 0
		
	End

	
	Method OnRender:Int()

		renderSys.Render()		
	
		Return 0
		
	End

End


'------------------


Class RenSys
	
	
	'Reference to a previously created and initialised map grid (World class):
	Field map:World			

	'Render start coordinates (top left x, top left y):
	Field gridTLX:Int 		
	Field gridTLY:Int
	
	'Cell rendering size and space in between:
	Field gridRectWidth:Int
	Field gridRectHeight:Int
	Field gridRectBorder:Int

	'Status string
	Field statusStr:String

	'Array of interactive text buttons:
	Field buttons:Button[]


	Method New(map:World)
	
		Self.map = map
		
		SetGrid(5, 5, 2, 2, 0)

	End	
	
	
	Method SetGrid:Void(tlx:Int, tly:Int, rw:Int, rh:Int, rb:Int)
		
		gridTLX = tlx
		gridTLY = tly
		gridRectWidth = rw
		gridRectHeight = rh
		gridRectBorder = rb
	
	End
	
	
	Method MakeStatusStr:Void(procCount:Int, procRate:Float, paused:Bool)
	
		statusStr = "Iterations: " + procCount + "    Speed (hz): "
		
		Local procStr:String = String(procRate)[0..5]
		If Not procStr.Contains(".") Then procStr += ".0"
		
		statusStr += procStr
		
		If paused Then statusStr += " (Paused)"
		
	End
		
	
	Method AddButton:Button(label:String, tlx:Int, tly:Int)
	
		Local font:Image = GetFont()
		Local width:Int = font.Width() 
		Local height:Int = font.Height()
	
		Local butt:Button = New Button(label, tlx, tly, width * label.Length, height)
	
		If butt
			buttons = buttons.Resize(buttons.Length + 1)
			buttons[buttons.Length - 1] = butt
			Return butt
		Endif
	
		Return Null
		
	End
		

	Method GetTouchedButton:Button(x:Int, y:Int)
		
		For Local butt:Button = Eachin buttons	
			If butt.XYInside(x, y)
				Return butt
			Endif
		End
	
		Return Null
	
	End
	

	Method GetTouchedCell:Cell(x:Int, y:Int)
	
		If (x &gt;= gridTLX) And (x &lt; (gridTLX + (map.cols * (gridRectWidth + gridRectBorder)))) 
			If (y &gt;= gridTLY) And (y &lt; (gridTLY + (map.rows * (gridRectHeight + gridRectBorder))))
			
				Local c:Int = Floor((x - gridTLX) / (gridRectWidth + gridRectBorder))
				Local r:Int = Floor((y - gridTLY) / (gridRectHeight + gridRectBorder))
				
				Return map.GetCell(c, r)
			
			Endif
		Endif
		
		Return Null
		
	End
	

	Method Render:Void()
	
		Cls 0, 0, 0
	
		For Local c:Int = 0 Until map.cols
			For Local r:Int = 0 Until map.rows
				Local curCell:Cell = map.GetCell(c, r)
				SetColor 128, 128, 128
				If curCell.status = CELL_STATUS_ALIVE
					SetColor 128, 255, 128
				Endif
				DrawRect gridTLX + (c * (gridRectWidth + gridRectBorder)) , gridTLY + (r * (gridRectHeight + gridRectBorder)) , gridRectWidth, gridRectHeight
			Next
		Next
		
		SetColor 255, 255, 255
		
		DrawText statusStr, 5, 5
		
		For Local butt:Button = Eachin buttons
			DrawText butt.label, butt.tlx, butt.tly
		Next
		
	End


End


Class Button


	Field label:String
	Field tlx:Int, tly:Int
	Field width:Int, height:Int


	Method New(label:String, tlx:Int, tly:Int, width:Int, height:Int)
	
		Self.label = label
		Self.tlx = tlx
		Self.tly = tly
		Self.width = width
		Self.height = height
	
	End
	
	
	Method XYInside:Button(x:Int, y:Int)
	
		If (x &gt;= tlx) And (x &lt; (tlx + width)) And (y &gt;= tly) And (y &lt; (tly + height))
			Return Self
		Endif
		
		Return Null
		
	End
	

End



'----------------------------------
Global CELL_STATUS_DEAD:Int = 0
Global CELL_STATUS_ALIVE:Int = 1
'----------------------------------


Class World

	'Size of map in columns and rows:
	Field cols:Int
	Field rows:Int

	'Grid is a 1D array cols*rows size.
	Field grid:Cell[]


	Method New(cols:Int, rows:Int)

		Self.cols = cols
		Self.rows = rows

		grid = New Cell[cols * rows]
		For Local c:Int = 0 Until cols
			For Local r:Int = 0 Until rows
				Local i:Int = (r * cols) + c
				grid[i] = New Cell
				grid[i].col = c
				grid[i].row = r
			Next
		Next

	End


	Method PlacePattern:Void(pat:Int[], width:Int, height:Int, atTLCol:Int, atTLRow:Int)
	
		For Local c:Int = 0 Until width
			For Local r:Int = 0 Until height
				If ((atTLCol + c) &lt; cols) And ((atTLRow + r) &lt; rows)
					Local i:Int = ((atTLRow + r) * cols) + (atTLCol + c)
					grid[i].status = pat[(r * width) + c]
				End
			Next
		Next
	
	End


	Method GetCell:Cell(col:Int, row:Int)
	
		If (col &lt; cols) And (col &gt;= 0) And (row &lt; rows) And (row &gt;= 0)
			Return grid[(row * cols) + col]
		Endif
		
		Return Null
		
	End


	Method ClearGrid:Void()	
				
		For Local i:Int = 0 Until grid.Length
			grid[i].status = CELL_STATUS_DEAD
		Next
	
	End


	Method Init:Void()
	
		ClearGrid

		For Local i:Int = 0 Until grid.Length
			'25% of the cells will start off alive
			If Floor(Rnd(0, 3)) = 1 
				grid[i].status = CELL_STATUS_ALIVE
			Endif
		Next
	
	End
	

	Method Process:Void()
		
		For Local i:Int = 0 Until grid.Length
		
			Local curCell:Cell = grid[i]
			curCell.pending = CELL_STATUS_DEAD
			
			Local liveCount:Int = CountLiveNeighbours(curCell.col, curCell.row)
					
			Select curCell.status
				Case CELL_STATUS_ALIVE
					curCell.pending = CELL_STATUS_ALIVE
					If (liveCount &lt; 2) Or (liveCount &gt; 3)
						curCell.pending = CELL_STATUS_DEAD
					Endif
				Case CELL_STATUS_DEAD
					If (liveCount = 3) Then curCell.pending = CELL_STATUS_ALIVE
			End
					
		Next
	
		For Local i:Int = 0 Until grid.Length
			grid[i].status = grid[i].pending
		Next
	
	End


	Method CountLiveNeighbours:Int(cc:Int, cr:Int)
	
		Local count:Int = 0
		
		Local startc:Int = cc - 1
		If startc &lt; 0 Then startc = 0
		
		Local endc:Int = cc + 1
		If endc &gt;= cols Then endc = cols - 1
		
		Local startr:Int = cr - 1
		If startr &lt; 0 Then startr = 0
		
		Local endr:Int = cr + 1
		If endr &gt;= rows Then endr = rows - 1

		For Local c:Int = startc To endc
			For Local r:Int = startr To endr
				If grid[(r * cols) + c].status = CELL_STATUS_ALIVE Then count += 1
			End
		End
		
		If grid[(cr * cols) + cc].status = CELL_STATUS_ALIVE Then count -= 1
		
		Return count
		
	End


End


'----------------------------------


Class Cell

	'Current status:
	Field status:Int
	
	'Pending status to be applied after ALL cells have been 
	'evaluated in the current process 'pass':
	Field pending:Int
	
	'The cells location in the grid.
	Field col:Int
	Field row:Int

	Method New()
		
		status = CELL_STATUS_DEAD
	
	End

End
</textarea> <br><br></td></tr></table><br>
<a name="2033045"></a>

<a name="2033044"></a>

<a name="2033043"></a>

<a name="2033042"></a>

<a name="2033047"></a>

<a name="2033048"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oh, I do love a bit of ALife, I did my dissertation on Artificial Life :)<br><br>I'll check this out tonight! <br><br></td></tr></table><br>
<a name="2033049"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Samah</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> <img src="http://i1.cpcache.com/product/37219598/glider_2333_hacker_emblem_shirt.jpg?height=380&amp;width=380"> <br><br></td></tr></table><br>
<a name="2033138"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice, I loved messing about with this. It also gave me a little inspiration to write my own version, I'll post it here. Let me know if you'd prefer I create my own topic. :)<br><br> +/- to speed up<br>space to pause/unpause<br>g show/hide grid<br>mouse/touch to add cell. <br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import mojo

Global World : Int[ 40 ][]
Global NewWorld : Int[ 40 ][]

Global scale : Int = 16

'/ Counts Cells Neighbours and wraps at borders (Toroidal)
Function CountNeighbours : Int( sx : Int, sy : Int )
	Local count : Int = 0
	Local UP : Int = sy - 1
	Local DOWN : Int = sy + 1
	Local LEFT : Int = sx - 1
	Local RIGHT : Int = sx + 1
	If UP &lt; 0 Then UP = 29
	If DOWN &gt; 29 Then DOWN = 0
	If LEFT &lt; 0 Then LEFT = 39
	If RIGHT &gt; 39 Then RIGHT = 0
	If World[ LEFT ][ UP ] Then count = count + 1
	If World[ LEFT ][ sy ] Then count = count + 1
	If World[ LEFT ][ DOWN ] Then count = count + 1
	If World[ RIGHT ][ UP ] Then count = count + 1
	If World[ RIGHT ][ sy ] Then count = count + 1
	If World[ RIGHT ][ DOWN ] Then count = count + 1
	If World[ sx ][ UP ] Then count = count + 1
	If World[ sx ][ DOWN ] Then count = count + 1
	Return count
End Function

'/Applys next generation to empty array using rules. 
Function NextGeneration()
	For Local x : Int = 0 Until 40
		For Local y : Int = 0 Until 30
			Local nCount : Int = CountNeighbours( x, y )
			If World[ x ][ y ]
				If nCount = 2 Or nCount = 3 Then NewWorld[ x ][ y ] = 1
			Else
				If nCount = 3 Then NewWorld[ x ][ y ] = 1
			Endif
		Next
	Next
End Function


'/ Applies NewWorld to World and resets NewWorld for next pass
Function UpdateWorld()
	For Local x : Int = 0 Until 40
		For Local y : Int = 0 Until 30
			World[ x ][ y ] = NewWorld[ x ][ y ]
			NewWorld[ x ][ y ] = 0
		Next
	Next
End Function


'/ Draws the World
Function DrawWorld()
	For Local x : Int = 0 Until 40
		For Local y : Int = 0 Until 30
			If World[ x ][ y ]
				SetColor 0, 180, 0
				DrawRect x * scale, y * scale, scale, scale
			Endif
		Next		
	Next
End Function


'/ Draws a Grid
Function DrawGrid()
	SetColor 200, 200, 200
	For Local x : Int = 0 Until 40
		DrawLine x * scale, 0, x * scale, DeviceHeight()
		If x &lt; 30 Then DrawLine 0, x * scale, DeviceWidth(), x * scale
	Next
End Function



Class MyApp Extends App
	
	Field T : Int
	Field TT : Int = 100
	
	Field Paused : Bool = True
	Field Grid : Bool = True
	
	Method OnCreate()

		SetUpdateRate( 60 )			

		'/ Init Arrays	
		For Local I : Int = 0 Until 40
			World[ I ] = New Int[ 30 ]
			NewWorld[ I ] = New Int[ 30 ]
		Next
	
		'adds blinker
		World[ 10 ][ 10 ] = 1
		World[ 11 ][ 10 ] = 1
		World[ 12 ][ 10 ] = 1
	
		'adds glider
		World[ 3 ][ 28 ] = 1
		World[ 4 ][ 28 ] = 1
		World[ 5 ][ 28 ] = 1
		World[ 5 ][ 27 ] = 1
		World[ 4 ][ 26 ] = 1
		
	End Method
	
	


	Method OnUpdate()
		
		If KeyHit( KEY_SPACE ) Then Paused = Not Paused
		If KeyHit( KEY_G ) Then Grid = Not Grid
		
		If KeyHit( KEY_EQUALS ) Then TT = TT - 10
		If KeyHit( KEY_MINUS ) Then TT = TT + 10
		If TT &lt; 10 Then TT = 10
		
		Local M : Int = Millisecs()
		
		If Paused = False
			If M &gt; T + TT
				T = M
				NextGeneration()
				UpdateWorld()
			Endif
		Endif

		If TouchHit()
			Local ax : Int = Int( MouseX() / scale )
			Local ay : Int = Int( MouseY() / scale )
			World[ ax ][ ay ] = 1 - World[ ax ][ ay ]
		Endif
				
	End Method
	


	Method OnRender()
		Cls 255, 255, 255

		If Grid Then DrawGrid()
		DrawWorld()
	
	End Method
	
End Class



Function Main()
	New MyApp()
End Function
</textarea> <br><br></td></tr></table><br>
<a name="2033137"></a>

<a name="2033215"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >impixi</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cool. Keeping it in this topic is fine with me. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
