<!DOCTYPE html><html lang="en" ><head ><title >Perlin Noise Class</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Perlin Noise Class</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Perlin Noise Class</a><br><br>
<a name="2007203"></a>

<a name="2007204"></a>

<a name="2007205"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bladko</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> basic perlin class<br><br><pre class=code>

'==========================================
'           CLASS                          
'   BASIC PERLIN CLASS WITHOUT BITMAP
'==========================================
Class TPerlinNoiseBasic

	Field sizeX = 640
	Field sizeY = 480

	'first random noise table	
	Field noiseArr:Float[]
	'output table
	Field terrainArr:Float[]
	
	'frequency, the lower the larger terrains of same level	
	Field frequency:Float = 1.0
	'starting amplitude
	Field amplitude:Float = 1.0
	'change of amplitude of next octave
	Field persistance:Float = 0.6
	'number of octaves
	Field octaves = 8
	
	'=========================================
	'	just make some data, using fake 2D arrays
	'===========================================
	Method New(_x, _y)
		
		Self.sizeX = _x
		Self.sizeY = _y	
		Self.noiseArr = new float[_x * _y]
		Self.terrainArr = new float[_x * _y]
	
	End
	
	'==========================================
	'    to init perlin noise parameters
	'==========================================
	Method ChangeParams(fre:Float, amp:Float, pers:Float, oct)
	
		frequency = fre
		amplitude = amp
		persistance = pers
		octaves = oct
	
	End 
	
	'==========================================
	'       single field noise generation
	'=========================================
	Method GetRandomNoise:Float(x:Float, y:Float)
		
		Local fre:Float = frequency
		Local amp:Float = amplitude
		Local finalValue:Float = 0.0
		
		For Local i = 0 To octaves
			finalValue = finalValue + LinearFilterNoise(x * fre, y * fre) * amp
			fre = fre * 2.0
			amp = amp * persistance
		Next
		
		If(finalValue &lt; - 1.0) finalValue = -1.0
		If(finalValue &gt; 1.0) finalValue = 1.0
		
		finalValue = finalValue * 0.5 + 0.5
		
		Return finalValue
	
	End 
	
	'==========================================
	'      create output terrain array
	'==========================================
	Method MakeTerrainMap()
		
		For Local x = 0 To sizeX - 1
			For Local y = 0 To sizeY - 1
				terrainArr[x + y * sizeX] = GetRandomNoise(x, y)
			Next
		Next
	
	End 

	'============================================
	'    perlin noise with linear interpolation
	'===========================================
	Method LinearFilterNoise:Float(x:Float, y:Float)
		
		Local fractionX:Float = x - Int(x)
		Local fractionY:Float = y - Int(y)
		
		Local x1 = (Int(x) + sizeX) Mod sizeX
		Local y1 = (Int(y) + sizeY) Mod sizeY
		Local x2 = (Int(x) + sizeX - 1) Mod sizeX
		Local y2 = (Int(y) + sizeY - 1) Mod sizeY
		
		If(x1 &lt; 0) x1 = x1 + sizeX
		If(x2 &lt; 0) x2 = x2 + sizeX
		If(y1 &lt; 0) y1 = y1 + sizeY
		If(y2 &lt; 0) y2 = y2 + sizeY
		
		Local finVal:Float = 0
		
		finVal = finVal + fractionX * fractionY * noiseArr[x1 + y1*sizeX ]
		finVal = finVal + fractionX * (1 - fractionY) * noiseArr[x1 + y2*sizeX]
		finVal = finVal + (1 - fractionX) * fractionY * noiseArr[x2 + y1 * sizeX]
		finVal = finVal + (1 - fractionX) * (1 - fractionY) * noiseArr[x2 + y2*sizeX ]
		
		Return finVal
	
	End 
	
	'===========================================
	'     to fill noise array with white noise
	'===========================================
	Method InitNoise()
		
		noiseArr = New Float[sizeX * sizeY]

		For Local x = 0 To sizeX - 1
			For Local y = 0 To sizeY - 1
				noiseArr[x + y * sizeX] = (Rnd() - 0.5) * 2.0
			Next
		Next
			
	End 
	
	'===========================================
        '   might be usefull, process whole array with sinus
        '============================================
	Method terrainSinus(p:Float)
		
		For Local x = 0 To sizeX - 1
			For Local y = 0 To sizeY - 1
		
				Local md:Float = Sin(y * 180 / sizeY) * 2 - 1
				terrainArr[x + sizeX * y] = md * p + terrainArr[x + sizeX * y] * (1.0 - p)
				
			Next
		Next
	
	End 
	
End 

</pre><br><br>extended class to draw data on screen and make some quantization<br>due to fact we cannot draw to image / backbuffer so this has limited usage<br><br><pre class=code>


'==========================================
'           CLASS                          
'   EXTENDED PERLIN CLASS WITH BITMAP AND LEVELS
'==========================================
Class TPerlinNoise Extends TPerlinNoiseBasic
	
	'min and max colors
	Field colMin:TColor = new TColor(0, 0, 0)
	Field colMax:TColor = new TColor(200,200,200)
	
	Field levels[] = New Int[100]
	Field levelsColor:TColor[] = new TColor[20]
	
	'=========================================
	'		just make new data
	'===========================================
	Method New(_x, _y)
	
		Super.New(_x,_y)
	
		'init levels
		For Local i = 0 To 99
			Self.levels[i] = 0
		Next
		
	End
	
	'==========================================
	'   draw image
	'==========================================
	Method OnRender(scale, dx, dy)

		For Local x = 0 To sizeX - 1
			For Local y = 0 To sizeY - 1
			
				Local val# = terrainArr[x + sizeX * y]
				Local R = colMax.R * val + colMin.R * (1 - val)
				Local G = colMax.G * val + colMin.G * (1 - val)
				Local B = colMax.B * val + colMin.B * (1 - val)
								
				SetColor(R, G, B)
				DrawRect(x * scale + dx, y * scale + dy, scale, scale)
				
			Next
		Next
	End
	
	'==========================================
	'   draw image according to levels, like izolines
	'==========================================
	Method OnRenderLevel(scale, dx, dy)

		For Local x = 0 To sizeX - 1
			For Local y = 0 To sizeY - 1
			
				Local val:TColor = levelsColor[levels[terrainArr[x + sizeX * y]*99]]
				if(val = null) Continue 
				
				SetColor(val.R, val.G, val.B)
				DrawRect(x * scale + dx, y * scale + dy, scale, scale)
				
			Next
		Next
	End
	
	
	'============================================
	'      start process
	'===========================================
	Method OnCreate()
				
		InitNoise()
		MakeTerrainMap()
		
	End 
	
	'============================================
	' setup terrain N from lvl min to lvl max
        ' this is quantization of terrain to one color in range
        ' this creates area of same value = izo lines
	'===========================================	
	Method SetupLevel(levelMin, levelMax, val, R,G,B)
		levelsColor[val] = New TColor(R,G,B)
		For Local i = levelMin To levelMax - 1
			levels[i] = val
		Next
	End 
	
End 


</pre><br><br><br>example how to use<br><br><pre class=code>
Local Perlinek:TPerlinNoise

Perlinek = New TPerlinNoise(64,48)		'to fill screen
Perlinek.ChangeParams(0.1, 0.99, 0.65, 6)       'experiment !! 
Perlinek.colMin = New TColor(0, 50, 0)          'dark green 
Perlinek.colMax = New TColor(150, 200, 150)     'light green
		
Perlinek.SetupLevel(0,40,0,0,40,80)             'low level water
Perlinek.SetupLevel(40,80,1,40,240,40)          'medium grass
Perlinek.SetupLevel(80,100,2,140,140,0)         'high rocks
		
Perlinek.OnCreate()

'===========================================================

'draw levels quantization or draw just all
'10 is scale 64 * 10 = 640
if(SpaceKeyIsHit = 1) Perlinek.OnRenderLevel(10,0,0)
if(SpaceKeyIsHit = 0) Perlinek.OnRender(10,0,0)

</pre><br><br>output<br><img src="http://i53.tinypic.com/2ykzuao.png"><br><img src="http://i53.tinypic.com/2irvi89.png"> <br><br></td></tr></table><br>
<a name="2007301"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DruggedBunny</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice one, thanks. <br><br></td></tr></table><br>
<a name="2007305"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bladko</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> if any one can help me how to create such image on the fly in monkey (grab image from backbuffer) <br><br></td></tr></table><br>
<a name="2007314"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >muddy_shoes</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> There's no way to access the framebuffer in the standard Monkey libraries. If you want to be able to do that you will have to write the target code and Monkey abstraction layer yourself. <br><br></td></tr></table><br>
<a name="2008281"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jjsonick</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'd like to test this out, but the TColor class is undefined.  Can you add your code for TColor please? <br><br></td></tr></table><br>
<a name="2008684"></a>

<a name="2008685"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bladko</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>
Class TColor
	
	Field R,G,B

	'****************************
	'*  	 
	'****************************
	Method New(R,G,B)
		Self.R = R
		Self.G = G
		Self.B = B		
	End 
	
	'****************************
	'*   
	'****************************
	Method New(col)
		Self.R = col &amp; $00FF0000		
		Self.R = Self.R / $10000
		Self.G = col &amp; $0000FF00		
		Self.G = Self.G / $100
		Self.B = col &amp; $000000FF		
		
	End
	
	'****************************
	'*   
	'****************************
	Method GenerateColor:Int()
	 	Return $FF000000 + (R * $10000 + G * $100 + B)
	End
	
End
</pre> <br><br></td></tr></table><br>
<a name="2013645"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bladko</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> use it with temperatur / altitude / humidity layers and pick if low temp and high hum then its tundra etc... <br><br></td></tr></table><br>
<a name="2090557"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >V. Lehtinen</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> if any one can help me how to create such image on the fly in monkey (grab image from backbuffer) <br></div><br><br>Forget rendering and then grabbing an image. You have all the data you need in your TPerlinNoiseBasic class arrays. :)<br>Just calculate the pixel color values from your floats and put these in a new array, which is then put into an image:<br><br><pre class=code>Strict 'Come on.. Don't forget this..
'Create an image
Local img:= CreateImage(width, height)
'And an array
Local pixels:= New Int[width * height]

'...calculate pixels's color values
' and then
img.WritePixels(pixels, x, y, width, height)</pre><br><br>It is also possible to create terrain images bigger than the screen, thanks to WritePixels parameters!<br><br>I use this code to get my tilemap-layers into images, and tilemaps usually are bigger than the screen:<br><pre class=code>    Method SetupChunks:Void()
        chunkWidth = fullWidth
        chunkHeight = fullHeight
        
        While chunkWidth &gt; DeviceWidth()
            chunkWidth /= 2
        Wend
        While chunkHeight &gt; DeviceHeight()
            chunkHeight /= 2
        Wend
        
        chunkCols = fullWidth / chunkWidth
        chunkRows = fullHeight / chunkHeight
    End
    
    Method PreRender:Void()
        Local x:Int, y:Int
        Local w:Int = data.width * tileWidth 'Layer's width * Tile width
        Local h:Int = data.height * tileHeight 'Layer's height * Tile height
        
        graphic = CreateImage(w, h)
        
        Local pixels:= New Int[chunkWidth * chunkHeight] ' We only need an array for a chunk, not whole image...
    
        For Local chunkX:Int = 0 Until chunkCols
            For Local chunkY:Int = 0 Until chunkRows
                Cls(0, 0, 0)
                
                x = chunkX * chunkWidth
                y = chunkY * chunkHeight
        
                Select renderingType
                    Case ORTHOGONAL
                        RenderOrtho(x, y, chunkWidth, chunkHeight)
                    Case ISOMETRIC
                        RenderIsometric() ' This doesn't even work yet, so don't ask... :)
                End
                
                ReadPixels(pixels, 0, 0, chunkWidth, chunkHeight) 'Read where we rendered
                MaskPixels(pixels, 0, 0, 0)
                graphic.WritePixels(pixels, x, y, chunkWidth, chunkHeight) '...but put it in right place in our image.
            Next
        Next
    End</pre><br><br><br>EDIT: Sorry.. Didn't notice this was so old... T__T <br><br></td></tr></table><br>
<a name="2090556"></a>

<a name="2090683"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >V. Lehtinen</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Okay, so I played around a little and got it working with images. It "renders" the pixels straight into the image. Should there be no need for any pre-rendering. Also the terrain can be any size, even bigger than the rendering screen.<br><br>I will still be optimizing the code since there's some left-overs from the earlier rendering.. :) [DONE]<br>Also will be adding colormap, that isn't yet implemented. Will be using gradients which you are able to set key-colors to. [DONE]<br><br>EDIT: How it looks with 2 gradient colors (red and lighter blue):<br><img src="http://puu.sh/9Mhwr/c957ecf4a0.png"><br><br>Usage:<br><br><pre class=code> 'Use these, in for example OnCreate()
Local Perlinek = New TPerlinNoise(640, 480)
Perlinek.SetParams(0.05, 0.99, 0.65, 8) ' Experiment with these.. ;)
Perlinek.MakeTerrainMap()
        
Perlinek.SetLevelColor(0.0, New Color(255, 0, 0))
Perlinek.SetLevelColor(1.1, New Color(0, 128, 255)) ' Have to fix this, doesn't work as it should with key 1.0...

Perlinek.CreateTerrain()


'....

'And simply draw the image in OnRender():
DrawImage(Perlinek.image, 0, 0)
</pre><br><br>Code (updated with gradient):<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict

'==========================================
'           CLASS                          
'   EXTENDED PERLIN CLASS WITH BITMAP AND LEVELS
'==========================================
Class TPerlinNoise Extends TPerlinNoiseBasic
    
    Field image:Image
    Field chunkWidth:Int, chunkHeight:Int
    Field chunkCols:Int, chunkRows:Int
    
    Field gradient:Gradient

    Method New(_x:Int, _y:Int)
        Super.New(_x, _y)
        InitNoise()
        gradient = New Gradient()
        
        image = CreateImage(_x, _y)
        
        SetupChunks()
    End
    
    Method Render:Void(x:Int, y:Int, w:Int, h:Int, arr:Int[])
        Local dx:Int, dy:Int, height:Float, pixel:Color
        
        For dx = x Until w
            For dy = y Until h
                height = terrainArr[dx + dy * sizeX]
                pixel = gradient.GetColor(height)
                
                arr[dx + dy * sizeX] = pixel.Rgb()
            Next
        Next
    End
    
    Method CreateTerrain:Void()
        Local x:Int, y:Int
        
        Local pixels:= New Int[chunkWidth * chunkHeight]
    
        For Local chunkX:Int = 0 Until chunkCols
            For Local chunkY:Int = 0 Until chunkRows
               
                x = chunkX * chunkWidth
                y = chunkY * chunkHeight
                Render(x, y, chunkWidth, chunkHeight, pixels)
                
                image.WritePixels(pixels, x, y, chunkWidth, chunkHeight)
            Next
        Next

    End
	
    Method SetLevelColor:Void(level:Float, color:Color)
        gradient.AddKey(level, color)
    End
    
    Method SetupChunks:Void()
        chunkWidth = image.Width()
        chunkHeight = image.Height()
        
        While chunkWidth &gt; DeviceWidth()
            chunkWidth /= 2
        Wend
        While chunkHeight &gt; DeviceHeight()
            chunkHeight /= 2
        Wend
        
        chunkCols = image.Width() / chunkWidth
        chunkRows = image.Height() / chunkHeight
    End
    

End



Class Gradient Extends Color
    Field keys:= New FloatMap&lt;Color&gt;
    
    Method AddKey:Void(position:Float, color:Color)
        keys.Set(position, color)
    End
    
    Method GetColor:Color(key:Float)
        Local thisKey:Float, thisValue:Color
        Local lastKey:Float, lastValue:Color
        Local alpha:Float
        
        For Local k:= EachIn keys.Keys()
            thisKey = k
            thisValue = keys.Get(thisKey)
            
            If thisKey &gt; key Then
                alpha = (key - lastKey) / (thisKey - lastKey)
                
                _rtnColor.r = Lerp(lastValue.r, thisValue.r, alpha)
                _rtnColor.g = Lerp(lastValue.g, thisValue.g, alpha)
                _rtnColor.b = Lerp(lastValue.b, thisValue.b, alpha)
                
                Return _rtnColor
            End
            
            lastKey = k
            lastValue = keys.Get(lastKey)
        Next
        
        Return _nullColor
    End
    
    Private
    Field _rtnColor:= New Color(0, 0, 0)
    Field _nullColor:= New Color(0, 0, 0)
    
    Function Lerp:Float(a:Float, b:Float, c:Float)
        Return a + c * (b - a)
    End
End

Class Color

	Global _cached := new IntMap&lt;Color&gt;
    
	Function Get:Color(rgb:Int)
		Local colorTemp:Color = _cached.Get(rgb)
		If colorTemp = Null
			colorTemp = New Color(rgb)
			_cached.Add(rgb, colorTemp)
		EndIf
		Return colorTemp
	End
	
	Field r:Int, g:Int, b:Int, a:Int
	
	Method Rgb:Int() Property Return (a shl 24) | (r Shl 16) | (g Shl 8) | b End
	
	Method Rgb:Void(rgbNew:Int) Property
        Self.a = (rgbNew Shr 24) &amp; $FF
		Self.r = (rgbNew Shr 16) &amp; $FF
		Self.g = (rgbNew Shr 8) &amp; $FF
		Self.b = rgbNew &amp; $FF
	End
	
	Method New(r:Int, g:Int, b:Int)
		Self.r = r
		Self.g = g
		Self.b = b
        Self.a = 255
	End
    
    Method New(r:Int, g:Int, b:Int, a:Int)
		Self.r = r
		Self.g = g
		Self.b = b
        Self.a = a
	End
	
	Method New(rgb:Int)
		Self.Rgb = rgb
	End
	
	Method ToString:String(rgbValue:Bool = False)
		If rgbValue Return String(Rgb)
        Return (a + ", " + r + ", " + g + ", " + b)
	End
    
    #REM * *
     * Converts an RGB color value To HSL.Conversion formula
     * adapted from <a href="http://en.wikipedia.org/wiki/HSL_color_space." target="_blank">http://en.wikipedia.org/wiki/HSL_color_space.</a>
     * Assumes r, g, and b are contained in the set [0, 255] and
     * returns h, s, and l in the set [0, 1].
     *
     * @param   Number  r       The red color value
     * @param   Number  g       The green color value
     * @param   Number  b       The blue color value
     * @return  Array           The HSL representation
    #END 
    Function RGBtoHSL:Float[] (r:Float, g:Float, b:Float)
        r /= 255
        g /= 255
        b /= 255
        Local max:Float = Math.Max(r, g, b), min:Float = Math.Min(r, g, b)
        Local h:Float, s:Float, l:Float = (max + min) / 2;
        Local temp:Float
        
        If (max = min) ' achromatic
            h = 0
            s = 0
        Else
            Local d:Float = max - min;
            If l &gt; 0.5 s = d / (2 - max - min) Else s = d / (max + min)
            Select max
                Case r
                    If g &lt; b Then temp = 6 Else temp = 0
                    h = (g - b) / d + temp
                Case g
                    h = (b - r) / d + 2
                Case b
                    h = (r - g) / d + 4
            End
            h /= 6
        End
    
        Return[h, s, l]
    End
    
    #REM/ * *
     * Converts an HSL color value to RGB. Conversion formula
     * adapted from <a href="http://en.wikipedia.org/wiki/HSL_color_space." target="_blank">http://en.wikipedia.org/wiki/HSL_color_space.</a>
     * Assumes h, s, and l are contained in the set [0, 1] and
     * returns r, g, and b in the set [0, 255].
     *
     * @param   Number  h       The hue
     * @param   Number  s       The saturation
     * @param   Number  l       The lightness
     * @return  Array           The RGB representation
    #END
    Function HSLtoRGB:Float[] (h:Float, s:Float, l:Float)
        Local r:Float, g:Float, b:Float;
    
        If (s = 0) ' achromatic
            r = l
            g = l
            b = l
        Else
            Local q:Float
            Local p:Float = 2 * l - q
            
            If l &lt; 0.5 Then q = l * (1 + s) Else q = l + s - l * s
            
            r = hue2rgb(p, q, h + 1 / 3)
            g = hue2rgb(p, q, h)
            b = hue2rgb(p, q, h - 1 / 3)
        End
    
        Return[r * 255, g * 255, b * 255]
    End
    
    Function hue2rgb:Float(p:Float, q:Float, t:Float)
        If (t &lt; 0) t += 1
        If (t &gt; 1) t -= 1
        If (t &lt; 1 / 6) Return p + (q - p) * 6 * t
        If (t &lt; 1 / 2) Return q
        If (t &lt; 2 / 3) Return p + (q - p) * (2 / 3 - t) * 6
        Return p
    End
End
</textarea> <br><br></td></tr></table><br>
<a name="2090662"></a>

<a name="2090663"></a>

<a name="2090664"></a>

<a name="2090665"></a>

<a name="2090666"></a>

<a name="2090667"></a>

<a name="2090661"></a>

<a name="2090668"></a>

<a name="2090669"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >V. Lehtinen</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here is another with Temperate-like colors:<br><img src="http://puu.sh/9Miov/4c2296d996.png"><br><br><pre class=code>Perlinek.SetLevelColor(1.1, New Color(2, 43, 68)) ' Water
Perlinek.SetLevelColor(0.55, New Color(9, 62, 92))
Perlinek.SetLevelColor(0.51, New Color(17, 82, 112))
Perlinek.SetLevelColor(0.5, New Color(69, 108, 118)) ' Shore
Perlinek.SetLevelColor(0.49, New Color(42, 102, 41)) ' Land
Perlinek.SetLevelColor(0.25, New Color(115, 128, 77))
Perlinek.SetLevelColor(0.15, New Color(153, 143, 92))
Perlinek.SetLevelColor(0.05, New Color(179, 160, 120))
Perlinek.SetLevelColor(0.0, New Color(220, 200, 180)) ' Mountains</pre> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
