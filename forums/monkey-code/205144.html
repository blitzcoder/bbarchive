<!DOCTYPE html><html lang="en" ><head ><title >Sample "basic run/jump game" using fantomEngine</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Sample "basic run/jump game" using fantomEngine</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=512" >Monkey Code</a>/<a href="#bottom" >Sample "basic run/jump game" using fantomEngine</a><br><br>
<a name="2056731"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ordigdug</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hope this is helpful to those learning the basics with fantomEngine.  Mike as usual, feel free to use all or part in fE distro.  <br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Strict

#rem
	Script:		RunJump.monkey
	Description:	This fantomEngine sample script shows basics of run &amp; jump game 
			Player stays in place, while clouds and enemy move
	Author: 	Douglas Williams
	Version:      	1.00
#End

' Import the fantomEngine framework which imports mojo itself
Import fantomEngine

' The _g variable holds an instance to the cGame class
Global _g:cGame

'***************************************
' The cGame class controls the app
Class cGame Extends App
	' Create a field to store the instance of the cEngine class, which is an instance of the ftEngine class itself
	Field fE:cEngine
	
	'Score canvas width/height
	Field cw:Float = 0.0
	Field ch:Float = 0.0
	
	'Store objects
	Field player:ftObject			'For storing PLAYER
	Field zoneBox:ftObject			'For storing PLAYER ZONE BOX
	Field enemy:ftObject			'For storing ENEMY
	
	'Store Player Attributes
	Field playerY:Float = 0.0		'For storing Player Y position
	Field health:ftObject			'For storing Player Health Bar
	
	'Store image sprite/atlas
	Field atlas:Image
	
	'Store Collision Groups
	Const grpPlayer:Int = 1
	Const grpEnemy:Int = 2
	
	'------------------------------------------
	Method OnCreate:Int()
		' Set the update rate of Mojo's OnUpdate events to 60 FPS
		SetUpdateRate(60)
		
		' Create an instance of the fantomEngine, which was created via the cEngine class
		fE = New cEngine
		
		'Set canvas Width/Height field variables
		cw = fE.canvasWidth
		ch = fE.canvasHeight
		
		'Load image atlas
		'--- Uncomment and replace "xxxx.xxx" with sprite/atlas sheet file name stored in RunJump.data folder ---
		'atlas = LoadImage("xxxx.xxx")
		'-------------------------------------------------------------------------------------------------------
		
		'Print directions in debug window
		Print("~t Press up arrow to jump over obstacles. ~n")
		
		'Print canvas X &amp; Y in debug window
		Print("Canvas Width (X)  = "+cw)
		Print("Canvas Height (Y) = "+ch)

		'Create sun
		Local sun := fE.CreateCircle(100, cw, 0)			'(radius, width, height)
		sun.SetColor(255,255,0)			'Yellow		'RGB Colors: <a href="http://www.rapidtables.com/web/color/RGB_Color.htm" target="_blank">http://www.rapidtables.com/web/color/RGB_Color.htm</a>
		
		'Create clouds
		Local cloud1 := fE.CreateCircle(40, cw/2, ch/4)		'Center
		Local cloud2 := fE.CreateCircle(30, cw/2-40, ch/4)	'Left
		Local cloud3 := fE.CreateCircle(30, cw/2+40, ch/4)	'Right
		cloud1.SetID(3)
		cloud1.SetAlpha(0.5)
		cloud1.SetSpeed(-1.0, 90)			'Only have to set speed of parent "cloud1" others will follow
		cloud2.SetAlpha(0.6)
		cloud2.SetParent(cloud1)			'Follow parent "cloud1"
		cloud3.SetAlpha(0.4)
		cloud3.SetParent(cloud1)			'Follow parent "cloud1"
		
		'Create ground
		Local ground := fE.CreateBox(cw, ch/2, cw/2, ch) 	'(width, height, x, y)
		ground.SetColor(139,69,19)							'Saddle brown
		
		'Create health bar
		Local bar := fE.CreateBox(cw/2, 20, 0, 10)
		bar.SetColor(169,169,169)						'dark grey
		
		health = fE.CreateBox(cw/2, 16, 0, 10)
		health.SetColor(124,252,0)						'lawn green
		health.SetID(0)
						
		'Create player
		'-------------------------- For an aminated sprite use -------------------------------------------------------
		'player = fE.CreateAnimImage(atlas, frameStartX, frameStartY, frameWidth, frameHeight, frameCount, xpos, ypos)
		'player.SetAnimTime(2.0)
		
		'--------------------------- For a static sprite use ---------------------------------------------------------
		'player = fE.CreateImage(atlas, x, y, width, height, xpos, ypos)
		
		'---------------------------- Basic player as a box ----------------------------------------------------------
		player = fE.CreateBox(44, 54, cw/4+6, (ch-ch/4)-30) 	
		player.SetColor(0,128,128)	'Teal
		'-------------------------------------------------------------------------------------------------------------
		player.SetID(1)
		playerY = player.GetPosY()
		
		'Create player zone box
		zoneBox = fE.CreateZoneBox(40, 50, cw/4+6, (ch-ch/4)-30)		'Invisible box

		'--- Comment above and Uncomment below for a visible zone box ---
		'zoneBox = fE.CreateBox(40, 50, cw/4+6, (ch-ch/4)-30)
		'zoneBox.SetColor(178,34,34)							'Red
		'zoneBox.SetAlpha(0.5)
		'----------------------------------------------------------------
		
		zoneBox.SetColGroup(grpPlayer)						'Set collision group
		zoneBox.SetColType(ftEngine.ctBox)					'ctCirlce, ctBox, ctBound
		zoneBox.SetParent(player)						'Set parent so zone box will follow player movement
				
		'Create enemy 
		enemy = fE.CreateCircle(10, cw, (ch-ch/4)-10)		'(radius, width, height)
		enemy.SetColor(220,20,60)							'Crimson
		enemy.SetID(2)
		enemy.SetSpeed(-8, 90)
		enemy.SetColGroup(grpEnemy)							'Set collision group
		enemy.SetColWith(grpPlayer, True)					'Set collision with player

		Return 0
	End
	'------------------------------------------
	Method OnUpdate:Int()
		If KeyHit( KEY_CLOSE ) fE.ExitApp()
		' Determine the delta time and the update factor for the engine
		Local d:Float = Float(fE.CalcDeltaTime())/60.0

		' Update all objects of the engine
		If fE.GetPause() = False Then
			fE.Update(d)
		 	
		 	'- Check for collisions of objects -
        	fE.CollisionCheck()
		Endif
			
		Return 0
	End
	'------------------------------------------
	Method OnRender:Int()
		' Check if the engine is not paused
		If fE.GetPause() = False Then
			' Clear the screen 
			Cls(135,206,235) 	'Sky blue background
		
			' Render all visible objects of the engine
			fE.Render()
		Endif
		Return 0
	End
	'------------------------------------------
	Method OnResume:Int()
		' Set the pause flag of the engine to FALSE so objects, timers and transitions are updated again
		fE.SetPause(False)
		
		Return 0
	End
	'------------------------------------------
	Method OnSuspend:Int()
		' Set the pause flag of the engine to TRUE so objects, timers and transitions are paused (not updated)
		fE.SetPause(True)
		
		Return 0
	End
End	

'***************************************
' The cEngine class extends the ftEngine class to override the On... methods
Class cEngine Extends ftEngine
	'------------------------------------------
	Method OnLayerTransition:Int(transId:Int, layer:ftLayer)
		' This method is called when a layer finishes its transition
		Return 0
	End
	'------------------------------------------
	Method OnLayerUpdate:Int(layer:ftLayer)
		' This method is called when a layer finishes its update
		Return 0
	End	
	'------------------------------------------
	Method OnObjectCollision:Int(obj:ftObject, obj2:ftObject)
		' This method is called when an object collided with another object
		
		_g.health.SetWidth(_g.health.GetWidth() - 40)	'Reduce health
		_g.enemy.SetColWith(_g.grpPlayer, False)		'Disable enemy collisions with player
		_g.enemy.CreateTimer(0, 600, 0)			'After 600 msecs peform "OnOjectTimer" code to re-enable collisions
		
		Return 0
	End
	'------------------------------------------
	Method OnObjectRender:Int(obj:ftObject)
		' This method is called when an object was being rendered
		Return 0
	End
	'------------------------------------------
	Method OnObjectSort:Int(obj1:ftObject, obj2:ftObject)
		' This method is called when objects are compared during a sort of its layer list
		Return 0
	End	
	'------------------------------------------
	Method OnObjectTimer:Int(timerId:Int, obj:ftObject)
		' This method is called when an objects' timer was being fired
				
		_g.enemy.SetColWith(_g.grpPlayer, True)			'Enable enemy collisions with player
		
		Return 0
	End	
	'------------------------------------------
	Method OnObjectTouch:Int(obj:ftObject, touchId:Int)
		' This method is called when an object was touched
		Return 0
	End
	'------------------------------------------
	Method OnObjectTransition:Int(transId:Int, obj:ftObject)
		' This method is called when an object finishes its transition
		Return 0
	End
	'------------------------------------------
	Method OnObjectUpdate:Int(obj:ftObject)
		' This method is called when an object finishes its update

		'If health
		If obj.GetID() = 0
			If obj.GetWidth() &lt;= 0				'When health is less than or equal to zero
				Print("~n     GAME OVER!!")		'Print in debug window
				_g.fE.SetPause(True)			'Pause game
			Endif		
		Endif
		
		'If player
		If obj.GetID() = 1
			'Jump
			If KeyDown(KEY_UP) 
				If obj.GetPosY() &gt;= _g.playerY		'If player at or above ground
					obj.SetSpeedY(-10)				'Move player up
				Endif
			Endif
			
			'Fall
			If obj.GetPosY() &lt;= (_g.playerY - 60)	'If player jump height is reached
				obj.SetSpeedY(10)					'Move player down
			Endif
			
			'Stop
			If obj.GetPosY() &gt; _g.playerY			'If player below ground
				obj.SetPosY(_g.playerY)				'Stop movement at ground
			Endif
		Endif	
		
		'If enemy
		If obj.GetID() = 2
			If obj.GetPosX() &lt;= 0								'If enemy is off screen
				obj.SetColor(Rnd(220, 255) ,Rnd(20, 255) ,Rnd(60, 255))			'Set random enemy color
				obj.SetPosX(_g.cw)							'Reset enemy start position
				obj.SetSpeed(Rnd (-8, -14), 90)						'Set random enemy speed
			Endif
		Endif
		
		'If cloud
		If obj.GetID() = 3
			If obj.GetPosX() &lt;= -100					'If cloud is off screen
				obj.SetPosX(_g.cw + 100)				'Reset cloud start position
				obj.SetSpeed(Rnd (-0.8, -3.6), 90)		'Set random cloud speed
			Endif
		Endif
		
		Return 0
	End
	'------------------------------------------
	Method OnSwipeDone:Int(touchIndex:Int, sAngle:Float, sDist:Float, sSpeed:Float)
		' This method is called when a swipe gesture was detected
		Return 0
	End
    '------------------------------------------
	Method OnTimer:Int(timerId:Int)
		' This method is called when an engine timer was being fired.
		Return 0
	End	
End

'***************************************
Function Main:Int()
	' Create an instance of the cGame class and store it inside the global var 'g'
	_g = New cGame
	
	Return 0
End

</textarea> <br><br></td></tr></table><br>
<a name="2056730"></a>

<a name="2056729"></a>

<a name="2056728"></a>

<a name="2056727"></a>

<a name="2056726"></a>

<a name="2056725"></a>

<a name="2056724"></a>

<a name="2056723"></a>

<a name="2056722"></a>

<a name="2056721"></a>

<a name="2056720"></a>

<a name="2056719"></a>

<a name="2056718"></a>

<a name="2056717"></a>

<a name="2056716"></a>

<a name="2056874"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >vmakar85</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> ordigdug. Greate stuff!!! <br><br></td></tr></table><br>
<a name="2056888"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Why0Why</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nicely done! <br><br></td></tr></table><br>
<a name="2056910"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MikeHart</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes great stuff. Thank you! Of course I will ship it with fE in the next update! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
