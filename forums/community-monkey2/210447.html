<!DOCTYPE html><html lang="en" ><head ><title >Simple Examples</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Simple Examples</h1><a href="forums.php" >Community Forums</a>/<a href="topics.php?forum=530" >Monkey2 Talk</a>/<a href="#bottom" >Simple Examples</a><br><br>
<a name="2117951"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >impixi</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here are some simple examples demonstrating a few of Monkey 2's language features. Hopefully self explanatory but let me know if not...<br><br>I'll post more examples later as I get the time.<br><br>(Not sure if I should post this thread here or on the Monkey2 dev forums?)<br><br>********<br>Pointers<br>********<br><br>pointers1.monkey2<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

'Pointers Simple Example 
'Reference(s):
'https://en.wikipedia.org/wiki/Pointer_(computer_programming)

'NOTE: It is NOT recommended to use pointers to Monkey2 objects.
'This code is for demonstration purposes only, and may 'break' in a future
'MX2 compiler release.

#Import "&lt;std.monkey2&gt;"

Using std

Function Main()
	
	Local obj1 := New MyClass("Object 1")
	Local obj2 := New MyClass("Object 2")
	
	Local ptrObj:MyClass Ptr 
	
	ptrObj=Varptr(obj1)
	Print Cast&lt;MyClass Ptr&gt;(ptrObj)[0].Label
	
	ptrObj = Varptr(obj2)
	Print Cast&lt;MyClass Ptr&gt;(ptrObj)[0].Label

End

Class MyClass
	Private 
		Field _label:String
		
	Public
		Method New(label:String)
			Label = label
		End
	
		Property Label:String()
			Return _label
		Setter (label:String)
			_label = label
		End
End

</textarea><br><br>pointers2.monkey2<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

'Pointers Simple Example 2
'Reference(s):
'https://en.wikipedia.org/wiki/Pointer_(computer_programming)

#Import "&lt;std.monkey2&gt;"

Using std

Function Main()

	Local myInt:Int = 5
	Print "Before modification: " + myInt
	
	ModifyByAddress(Varptr(myInt))
	Print "After modification: " + myInt
	
End

Function ModifyByAddress:Void(pInt:Int Ptr)
	Cast&lt;Int Ptr&gt;(pInt)[0] = 10
End

</textarea><br><br>*****************<br>Function Pointers<br>*****************<br><br>fp1.monkey2<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

'Function Pointers Simple Example
'Reference(s):
'https://en.wikipedia.org/wiki/Function_pointer

#Import "&lt;std.monkey2&gt;"

Using std

Function Main()

	Local n1:Int = 7
	Local n2:Int = 5
	
	Print n1 + " + " + n2 + " = " + Operation(n1, n2, Add)
	Print n1 + " - " + n2  + " = " + Operation(n1, n2, Subtract) 

End

Function Add:int(first:Int, second:Int)
	Return first+second
End

Function Subtract:int(first:Int, second:Int)
	Return first-second
End

Function Operation:Int(first:Int, second:Int, func:Int(Int, Int))
	Return func(first, second)
End


</textarea><br><br>fp2.monkey2<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

'Function Pointers Simple Example 2
'Reference(s):
'https://en.wikipedia.org/wiki/Function_pointer

#Import "&lt;std.monkey2&gt;"

Using std

Function Main()

	Local n1:Int = 7
	Local n2:Int = 5
	
	Print n1 + " + " + n2 + " = " + Operation&lt;Int&gt;(n1, n2, Add)
	Print n1 + " - " + n2  + " = " + Operation&lt;Int&gt;(n1, n2, Subtract) 

	Local n3:Double = 19.25
	Local n4:Double = 10.5

	Print n3 + " + " + n4 + " = " + Operation&lt;Double&gt;(n3, n4, Add)
	Print n3 + " - " + n4  + " = " + Operation&lt;Double&gt;(n3, n4, Subtract) 

End

Function Add&lt;T&gt;:T(first:T, second:T)
	Return first+second
End

Function Subtract&lt;T&gt;:T(first:T, second:T)
	Return first-second
End

Function Operation&lt;T&gt;:T(first:T, second:T, func:T(T, T))
	Return func(first, second)
End

</textarea><br><br>*****************<br>Generics<br>*****************<br><br>gen1.monkey2<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

'Generics Simple Example
'Reference(s):
'https://en.wikipedia.org/wiki/Generic_programming

#Import "&lt;std.monkey2&gt;"

Using std

Function Main()

	Local v1:Int = 10
	Local v2:Int = 14
	Print "Maximum of " +v1+ " and " + v2 + " is " + Max&lt;Int&gt;(v1, v2)
	
	Local v3:Float = 19.25
	Local v4:Float = 12.5
	Print "Maximum of " +v3+ " and " + v4 + " is " + Max&lt;Float&gt;(v3, v4)

End

Function Max&lt;T&gt;:T(a:T, b:T)
	Return (a &lt; b) ? b Else a
End

</textarea><br><br>gen2.monkey2<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">


'Generics Simple Example 2
'Reference(s):
'https://docs.oracle.com/javase/tutorial/java/generics/types.html

#Import "&lt;std.monkey2&gt;"

Using std

Function Main()
	
	Local myBox := New Box&lt;Int&gt;
	myBox.Item = 12
	Print "Box item: " + myBox.Item
	 
	Local myBox2 := New Box&lt;MyClass&gt;
	Print "Box item: " + myBox2.Item.ToString()
	
End

Class Box&lt;T&gt;
	
	Private 
	
	Field _item:T
	
	Public
		
	Property Item:T()
		Return _item
	Setter (item:T)
		_item=item
	End

End

Class MyClass
	Public
	Method ToString:String()
		Return "This is my class!"
	End
End

</textarea> <br><br></td></tr></table><br>
<a name="2117811"></a>

<a name="2117830"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >impixi</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> ******************<br>Enumerated Types<br>******************<br><br>enum1.monkey2<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

'Enumerated Type Simple Example
'Reference(s):
'https://en.wikipedia.org/wiki/Enumerated_type

#Import "&lt;std.monkey2&gt;"

Using std

Enum CardSuit
	Clubs, Diamonds, Spades, Hearts
End

Function Main()
	Local suit:CardSuit = CardSuit.Hearts
	Print Cast&lt;UInt&gt;(suit) + ": " + CardSuitLabel(suit)
End

Function CardSuitLabel:String(suit:CardSuit)
	Select suit
		Case CardSuit.Clubs
			Return "Clubs"
		Case CardSuit.Diamonds
			Return "Diamonds"
		Case CardSuit.Spades
			Return "Spades"
		Case CardSuit.Hearts
			Return "Hearts"
	End
	Return ""
End

</textarea><br><br>enum2.monkey2<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">


'Enumerated Type Simple Example 2
'Reference(s):
'https://en.wikipedia.org/wiki/Enumerated_type

#Import "&lt;std.monkey2&gt;"

Using std

Enum ItemFlag
	Moveable = 1
	Carryable = 2
	Equipable = 4
	Destructable = 8
End

Function Main()

	Local item1:=New Item("Sword", Cast&lt;UByte&gt;(ItemFlag.Moveable) | Cast&lt;UByte&gt;(ItemFlag.Carryable))
	item1.SetFlag(ItemFlag.Equipable)
	Print item1.ToString()
	
	Local item2:=New Item("Table")
	item2.SetFlag(ItemFlag.Moveable)
	Print item2.ToString()
	
End

Class Item

	Private
		Field _label:String
		Field _flags:UByte
		
	Public
		Method New(label:String, flags:UByte = 0)
			Label = label
			_flags = flags
		End
		
		Property Label:String()
			Return _label
			Setter (label:String)
				_label = label
		End
		
		Method SetFlag:void(flag:ItemFlag)
			_flags |= (Cast&lt;UByte&gt;(flag))
		End
		
		Method CheckFlag:Bool(flag:ItemFlag)
			Return (_flags &amp; Cast&lt;UByte&gt;(flag))
		End
		
		Method FlagToString:String(flag:ItemFlag)
			Local str:String = "ERR"
			If CheckFlag(flag) 
				str = "True" 
			Else 
				str = "False"
			endif
			Return str
		End
		
		Method ToString:String()
			Local str:String = "-----------------------~n" + Label
			str += "~n-----------------------~n"
			str += ("Moveable: " + FlagToString(ItemFlag.Moveable) + "~n")
			str += ("Carryable: " + FlagToString(ItemFlag.Carryable) + "~n")
			str += ("Equipable: " + FlagToString(ItemFlag.Equipable) + "~n")
			str += ("Destructable: " + FlagToString(ItemFlag.Destructable) + "~n")
			Return str
		End
End

</textarea> <br><br></td></tr></table><br>
<a name="2117829"></a>

<a name="2117835"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >marksibly</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice examples!<br><br>One thing to note is that you have to be *very* careful when using Varptr with 'class objects'.<br><br>A 'Ptr' wont keep an object alive GC-wise, so unless a normal variable is also keeping the object alive, there's a chance it will be GC'd 'behind your back'. I'd consider the pointer1.monkey2 example to be borderline dodgy for this reason  - the 'obj1' var is, strictly speaking, 'dead' before the pointer is used. It'll work, but only because of how compilers work...<br><br>Also, an object var is already a pointer, ie: "sizeof( blah )" where blah is an instance of a class/interface will always return 4/8, so Varptr blah is sort of redundant for what I think I most people will instinctively want to use it for.<br><br>Varptr with a struct is also dangerous - but marginally less so, and it's a necessary evil for the sake of integrating with 3rd party stuff. Also, note that you can still cast 'extends void' objects to 'void ptr' (it's an automatic conversion in fact) so you still get to play with pointers when dealing with non-mx2 classes.<br><br>But I'm not sure it's a good idea with pure mx2 class objects, so I'm tempted to disallow it entirely. If you DO have to mess with pointers to mx2 objects, there is probably some native glue code involved so perhaps it's best to just leave that stuff for the glue code?<br><br>I do think a general, safe mechanism for dealing with 'variable references' (the one sensible use I can think of for Varptr obj) would be a very use feature though, and I do have a few ideas here. Raw pointers are a crappy way to achieve this though and one I don't really want to encourage. <br><br></td></tr></table><br>
<a name="2117834"></a>

<a name="2117832"></a>

<a name="2117833"></a>

<a name="2117831"></a>

<a name="2117952"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >impixi</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for the info, Mark! <br><br><div class="quote"> <br>But I'm not sure it's a good idea with pure mx2 class objects, so I'm tempted to disallow it entirely. If you DO have to mess with pointers to mx2 objects, there is probably some native glue code involved so perhaps it's best to just leave that stuff for the glue code?<br> <br></div><br><br>Yep, I'd agree with that.<br><br>EDIT: I put a little note in the pointers1.monkey2 example. <br><br></td></tr></table><br>
<a name="2117858"></a>

<a name="2117950"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >impixi</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> ******************<br>Lambda Functions<br>******************<br><br>lambda1.monkey2<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

'Lambda Function Simple Example
'Reference(s):
'http://monkey2.monkey-x.com/forums/topic/what-the-sam-hill-is-a-lambda-function/
'https://en.wikipedia.org/wiki/Anonymous_function

#Import "&lt;std.monkey2&gt;"

Using std

Function Main()
	Local sqr := Lambda:Int(v:int) 
		Return (v * v)
	End
	
	Print sqr(5)
	Print sqr(8)
End
</textarea><br><br>lambda2.monkey2<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

'Lambda Function Simple Example 2
'Reference(s):
'http://monkey2.monkey-x.com/forums/topic/what-the-sam-hill-is-a-lambda-function/
'https://en.wikipedia.org/wiki/Anonymous_function
'https://dzone.com/articles/why-we-need-lambda-expressions

#Import "&lt;std.monkey2&gt;"

Using std

Function Main()
	
	Local values := New Int[](8, 5, 2, 6, 1, 6)
	
	Local str:String = "~nData set: "
	For Local v:=Eachin values
		str += (v + ", ")
	Next
	str = str.Slice(0, str.Length-2)
	Print str + "~n"
	
	Print "Sum of all values = ~t" + SumAll(values, Lambda:Bool(v:Int) 
		Return True 
	End)

	Print "Sum of even values = ~t" + SumAll(values, Lambda:Bool(v:Int) 
		Return ((v Mod 2 = 0) ? 1 Else 0)
	End)
	
	Print "Sum of values &lt;= 5 = ~t" + SumAll(values, Lambda:Bool(v:Int) 
		Return ((v &lt;= 5) ? 1 Else 0)
	End)
	
End

Function SumAll:Int(vals:Int[], filter:bool(int))
	Local sum:Int = 0
   	For Local v:= Eachin vals
   		If (filter(v)) 
            		sum += v
        		Endif
        	Next
	Return sum
End
</textarea> <br><br></td></tr></table><br>
<a name="2118060"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >impixi</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> **************<br>External C<br>**************<br><br>Place all files in the same folder and run exte1.monkey2.<br><br>utils.h<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
#if _WIN32
	#include &lt;windows.h&gt;
#else
	#if __APPLE__
		#include &lt;mach/mach_time.h&gt;
	#else
		#include &lt;time.h&gt;
	#endif
#endif

//THANKS Danilo!
#ifdef __cplusplus
	extern "C" {
#endif

	unsigned long GetSysSeed(void);

#ifdef __cplusplus
	}
#endif
</textarea><br><br>utils.c<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
#include "utils.h"

unsigned long GetSysSeed(void)
{
	#if _WIN32
		return (unsigned long)GetTickCount();
	#else
		#if __APPLE__
			return (unsigned long)mach_absolute_time(); 
		#else
			struct timespec tm;
			clock_gettime(CLOCK_MONOTONIC, &amp;tm);
			return (unsigned long)tm.tv_nsec;
		#endif
	#endif
}
</textarea><br><br>utils.monkey2<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Namespace utils

#Import "utils.h"
#Import "utils.c"

Extern

Function GetSysSeed:ULong()

</textarea><br><br>exte1.monkey2<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

'External C Code Access Simple Example

'NOTE: Linux untested, but the principle is the same
'on all platforms.

#Import "utils.monkey2"
#Import "&lt;std.monkey2&gt;"

Using std
Using std.random

Using utils

Function Main()

	'Get a time-related value from the wrapped C function.
	'Use it to seed the random number generator.
	Local seed := GetSysSeed()
	SeedRnd(seed)
	
	'--- Work around some unwanted behavior that happens when setting a new seed.
	RndULong()
	RndULong()
	'---

	For Local set := 1 To 9	
		Local str:String = "Set #" + set + ": ~t"
		For Local vals := 1 To 10
			str += String(Rand(-9, 9)) + "~t"
		Next
		Print str
	Next
	
End

'Return a random int within a given range (min and max inclusive)
Function Rand:Int(min:Int, max:Int)
	Return (min + (Cast&lt;Int&gt;(RndULong()) Mod (max+1) - min))
End

</textarea><br><br>EDIT (22-Mar-16): Finally had a chance to test this on a Mac - fixed a bug in utils.h and utils.c and it now runs okay... <br><br></td></tr></table><br>
<a name="2118059"></a>

<a name="2118058"></a>

<a name="2117955"></a>

<a name="2118302"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >impixi</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> **************<br>Operator Overloading<br>**************<br>opover1.monkey2<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

'Operator Overloading Example
'Reference(s):
'http://monkey2.monkey-x.com/forums/topic/operator-overloading/
'https://en.wikipedia.org/wiki/Operator_overloading

'https://msdn.microsoft.com/en-us/library/5tk49fh2.aspx

#Import "&lt;std.monkey2&gt;"

Using std

Function Main()
	
	Local a := New Complex(1.25, 2.25)
   	Local b := New Complex(5.5, 3.25)
   	Local c:Complex
	
	c = a + b
	
	Print "a= ~t" + a.ToString()
	Print "b= ~t" + b.ToString()
	Print "a+b= ~t" + c.ToString()
	
End

Struct Complex

	Private
		
		Field _re:Double
		Field _im:Double 

	Public
	
		Method New(re:Double, im:Double) 
			_re = re
			_im = im
		End
   
   		Operator+:Complex(other:Complex)
   			Return New Complex(Re + other.Re, Im + other.Im)
   		End
   		
   		Property Re:Double()
   			Return _re
   		End
   		
   		Property Im:Double()
   			Return _im
   		End
   		
   		Method ToString:String()
   			Return Re + ", " + Im
   		End
   		
End

</textarea><br><br>opover2.monkey2<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

'Operator Overloading Example 2
'Reference(s):
'http://monkey2.monkey-x.com/forums/topic/operator-overloading/
'https://en.wikipedia.org/wiki/Operator_overloading

#Import "&lt;std.monkey2&gt;"

Using std

Function Main()
	
	Local objA:=New MyClass("Gold", 100)
	Local objB:=New MyClass("Silver", 33)
	
	Print ""
	
	If (objA &gt; objB) Then Print objA.Label + " &gt; " + objB.Label
	If (objA &lt; objB) Then Print objA.Label + " &lt; " + objB.Label

	If (objA = objB) Then Print objA.Label + " = " + objB.Label
	If (objA &lt;&gt; objB) Then Print objA.Label + " &lt;&gt; " + objB.Label

	Print ""
	
End


Class MyClass
	
	Private
	
		Field _label:String
		Field _value:UByte
	
	Public
		
		Method New(label:String, value:UByte)
			Label = label
			_value = value
		End
		
		Property Label:String()
			Return _label
		Setter(label:String)
			_label = label
		End
		
		
		Operator&lt;=&gt;:Int(o2:MyClass)
			Return (Self._value &lt;=&gt; o2._value)
		End
		
		'Overloading &lt;=&gt; is shorthand for overloading all 
		'the comparison operators:
		
		#rem
		Operator&gt;:Bool(o2:MyClass)
			Return Self._value &gt; o2._value
		End
	
		Operator&lt;:Bool(o2:MyClass)
			Return Self._value &lt; o2._value
		End
	
		Operator=:Bool(o2:MyClass)
			Return Self._value = o2._value
		End
		
		Operator&lt;&gt;:Bool(o2:MyClass)
			Return Self._value &lt;&gt; o2._value
		End
		#end
		
End

</textarea><br><br>**************<br>Struct<br>**************<br><br>struct1.monkey2<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

'Struct Example

#Import "&lt;std.monkey2&gt;"

Using std

Function Main()
	
	'Structs are allocated on the stack and passed by value.
	Local ms:=New MyStruct(3,6)
	Print "Original Struct object before processing function: " + ms.col + ", " + ms.row
	ProcessMyStruct(ms, -1, -2)
	Print "Original Struct object after processing function: " + ms.col + ", " + ms.row + " (No change)"
	
	'Classes are allocated on the heap and passed by reference.
	Local mc:=New MyClass(3,6)
	Print "Original Class object before processing function: " + mc.col + ", " + mc.row
	ProcessMyClass(mc, -1, -2)
	Print "Original Class object after processing function: " + mc.col + ", " + mc.row
	
End

'----

Struct MyStruct

	Public

	Field col:Int, row:Int
	
	Method New(c:Int, r:Int)
		col = c
		row = r
	End
	
End

Function ProcessMyStruct(ms:MyStruct, c:Int, r:Int)
	ms.col = c
	ms.row = r
End

'----

Class MyClass

	Public

	Field col:Int, row:Int
	
	Method New(c:Int, r:Int)
		col = c
		row = r
	End
	
End

Function ProcessMyClass(mc:MyClass, c:Int, r:Int)
	mc.col = c
	mc.row = r
End

</textarea><br><br>struct2.monkey2<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

'Struct Example 2
'Reference(s):
'https://en.wikipedia.org/wiki/Stack-based_memory_allocation

#Import "&lt;std.monkey2&gt;"
#Import "&lt;libc.monkey2&gt;"

Using std
Using libc

Function Main()
	
	'Qty too high might cause stack overflow?
	Local qty := 10000000
	
	'Try running in "release" mode too...
	
	Local tm0 := clock()
	For Local n := 1 To qty
		Local ms:=New MyStruct(3, 6)
	Next
	Local tm1 := clock()
	Local diff:Double = Cast&lt;Double&gt;((Double(tm1) - Double(tm0)) / CLOCKS_PER_SEC)
	Print "Time taken to allocate and initialize " + qty + " struct items (stack) = " + diff + " seconds"
	
	tm0 = clock()
	For Local n := 1 To qty
		Local mc:=New MyClass(3, 6)
	Next
	tm1 = clock()
	diff = Cast&lt;Double&gt;((Double(tm1) - Double(tm0)) / CLOCKS_PER_SEC)
	Print "Time taken to allocate and initialize " + qty + " class objects (heap) = " + diff + " seconds"
	
End


'----

Struct MyStruct

	Private

		Field _col:UInt, _row:UInt
		
	Public
	
	Method New(col:UInt, row:UInt)
		Col = col
		Row = row
	End
	
	Property Col:Int()
		Return _col
	Setter (col:UInt)
		_col = col
	End
	
	Property Row:Int()
		Return _row
	Setter (row:UInt)
		_row = row
	End
	
End



'----

Class MyClass

	Private

		Field _col:UInt, _row:UInt
		
	Public
	
	Method New(col:UInt, row:UInt)
		Col = col
		Row = row
	End
	
	Property Col:Int()
		Return _col
	Setter (col:UInt)
		_col = col
	End
	
	Property Row:Int()
		Return _row
	Setter (row:UInt)
		_row = row
	End
	
End

</textarea><br><br>EDIT (27 Mar 2016): Updated opover2.monkey2 to overload the &lt;=&gt; operator. <br><br></td></tr></table><br>
<a name="2118301"></a>

<a name="2118128"></a>

<a name="2118138"></a>

<a name="2118139"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >impixi</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> ********************<br>Abstract Class<br>********************<br><br>ab1.monkey2<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

'Abstract Class Example
'Reference(s):
'https://en.wikipedia.org/wiki/Abstract_type

#Import "&lt;std.monkey2&gt;"

Using std

Function Main()

	Local shapes:Shape[] = New Shape[](New Rectangle(5.0, 15.0), New Circle(4.0), New Rectangle(2.0, 4.0), New Circle(2.5))

	For Local s := eachin shapes
		Print s.ToString()
	Next

End


'----


'Abstract classes must be used as a base/super class;
'they cannot be instantiated themselves.

Class Shape Abstract

	Private
		Field _label:String

	Public

		Property Label:String()
			Return _label
		Setter(label:String)
			_label = label
		End

	'Members marked Virtual can be overridden by subclasses. 

	Method Area:Double() Virtual
		Return 0
	End
	
	Method ToString:String() Virtual
		Return ""
	End
	
End


'----


'Subclasses should implement their superclass's virtual members.

Class Rectangle Extends Shape
	
	Private
		Field _width:Double, _length:Double
	
	Public
	
	Method New(width:Double, length:Double)
		Label = "Rectangle"
		Width = width
		Length = length
	End
	
	Property Width:Double()
		Return _width
	Setter (width:Double)
		_width = width
	End

	Property Length:Double()
		Return _length
	Setter (length:Double)
		_length = length
	End

	'The Override keyword must be used when overriding a superclass's virtual members.
	
	Method Area:Double() Override
		Return (Width * Length)
	End
	
	Method ToString:String() Override
		Return (Label + " area = w*l: " + Width + "*" + Length + "= " + Area()) 
	End
	
End


'----


Class Circle Extends Shape
	
	Private
		Field _radius:Double
	
	Public
	
	Method New(radius:Double)
		Label = "Circle"
		Radius = radius
	End
	
	Property Radius:Double()
		Return _radius
	Setter (radius:Double)
		_radius = radius
	End
	
	Method Area:Double() Override
		Return Pi * Radius * Radius
	End
	
	Method ToString:String() Override
		Return (Label + " area = pi*r*r: " + Pi + "*" + Radius + "* " + Radius + "= " + Area()) 
	End
	
End

</textarea> <br><br></td></tr></table><br>
<a name="2118137"></a>

<a name="2118153"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >kmac</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is great.  I have a few hard deadlines over the next few days but will definitely study these next week. <br><br></td></tr></table><br>
<a name="2118162"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >marksibly</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Great stuff!<br><br>A quick comment re: comparison overloads: you only need to overload Operator&lt;=&gt; to implement ALL comparisons. This is very convenient and something people should probably know about. <br><br></td></tr></table><br>
<a name="2118198"></a>

<a name="2118199"></a>

<a name="2118200"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >impixi</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Mark:<br><br>Wouldn't that only work for sorting-relating comparisons? The result of a &lt;=&gt; comparison can be -1, 0, or +1. Useful to determine whether a given value is less than, equal to, or greater than another given value.  <br><br>The &gt;, &lt;, &lt;&gt; and = comparisons return either true (1) or false (0). How could I overload &lt;=&gt; so that all those checks would present the correct boolean result when used, for example, in an If statement? <br><br>I've experimented with my opover2.monkey2 example above but just cannot think how to make it work. It's probably something obvious I'm overlooking, so apologies in advance. ;) <br><br></td></tr></table><br>
<a name="2118210"></a>

<a name="2118211"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >marksibly</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Basically, "X op Y" is transformed to "(X&lt;=&gt;Y) op 0" where op is one of &lt;, &gt;, &lt;= etc.<br><br>So "5&lt;8" is transformed to "(5&lt;=&gt;8)&lt;0" and "p&gt;=q" is transformed to "(p&lt;=&gt;q)&gt;=0" etc.<br><br>If you think it through it works!<br><br>So you should be able to replace your 3 operators with:<br><br>Operator&lt;=&gt;:Int( o2:MyClass )<br>   Return Self._value&lt;=&gt;o2._value<br>End<br><br>...which is really doing the same thing as...<br><br>Operator&lt;=&gt;:Int( o2:MyClass )<br>   If Self._value&lt;o2._value Return -1<br>   If Self._value&gt;o2_value Return 1<br>   Return 0<br>End<br><br>Note also that the compiler 'writes' a version of &lt;=&gt; for structs (but yours is a class...) that don't already have one. This applies &lt;=&gt; to all members, and returns the first non-0 result. If there is no non-0 result, it returns 0. For classes, the default '&lt;=&gt;' simply subtracts pointers.<br><br>You can actually derive all comparisons with just '&lt;' (or '&gt;' ), eg:<br><br>"x&gt;y"   -&gt; "y&lt;x"<br>"x&lt;&gt;y" -&gt; "x&lt;y or y&lt;x"<br>"x=y"   -&gt; "not (x&lt;y or y&lt;x)"<br><br>...etc. but I thought adding "&lt;=&gt;" would be simpler to understand and more flexible/useful. Also, the '&lt;' approach can result in 2 operator method calls for some comparisons (although those methods would probably be simpler). <br><br></td></tr></table><br>
<a name="2118207"></a>

<a name="2118205"></a>

<a name="2118206"></a>

<a name="2118204"></a>

<a name="2118203"></a>

<a name="2118202"></a>

<a name="2118201"></a>

<a name="2118227"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pakz</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Keep em coming! :D <br><br></td></tr></table><br>
<a name="2118231"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >impixi</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Mark<br><br>Thanks for the explanation! Your logic seems correct. However...<br><br><div class="quote"> <br>So you should be able to replace your 3 operators with:<br><br>Operator&lt;=&gt;:Int( o2:MyClass )<br>Return Self._value&lt;=&gt;o2._value<br>End<br> <br></div><br><br>That's the first thing I tried but it produces incorrect results in my example (which is why I thought overloading &lt;=&gt; might be more involved than I assumed).<br><br>The original opover2.monkey2 produces the following output (correct).<br><br><div class="quote"> <br>Gold &gt; Silver<br>Gold &lt;&gt; Silver<br> <br></div><br><br>A modified version, simply commenting out the &lt;, &gt;, and = overloads and adding the above &lt;=&gt; overload produces:<br><br><div class="quote"> <br>Gold &lt; Silver<br>Gold &lt;&gt; Silver<br> <br></div><br><br>That is incorrect. Altering the initial values also produces incorrect results for most combinations (whereas the original code produces correct results).<br><br>@Pakz:<br><br>Off the top of my head, I still need to write some Interface examples. Maybe some String and Array initialization and manipulation examples. I do have a more advanced Extern example "completed", except for one remaining bug I haven't yet squashed.<br><br>Any other MX2 language-related items I should write examples for? <br><br></td></tr></table><br>
<a name="2118230"></a>

<a name="2118229"></a>

<a name="2118228"></a>

<a name="2118261"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tiresius</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> These are great!!!  You covered all the topics I wondered about for Monkey2.  If there are any others that would be cool, too! <br><br></td></tr></table><br>
<a name="2118262"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pakz</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> I wonder how mx2 has the arrays done. This is something I use a lot.<br>2 dimensional arrays, 3 dimensional arrays, 2 dimensional data (tilemap) in a array. <br><br></td></tr></table><br>
<a name="2118265"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >marksibly</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> &gt; That's the first thing I tried but it produces incorrect results in my example (which is why I thought overloading &lt;=&gt; might be more involved than I assumed).<br><br>It's a bug! Found and fixed, will github soon.<br><br>There are likely to be a bunch of little things like this still in there, so if something looks like it should be working but isn't, chances are pretty good that it's my fault, not yours! So please let me if you discover any other issues like this in future. Even if it's not a bug, if it's confusing or inconsistent that's worth checking out too. <br><br></td></tr></table><br>
<a name="2118263"></a>

<a name="2118267"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >marksibly</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> &gt; 2 dimensional arrays, 3 dimensional arrays, 2 dimensional data (tilemap) in a array.<br><br>2+ dimensional arrays are supported, but not as well as 1d yet.<br><br>You can create and index them using...<br><br>Local t:=New Int[10,20]<br>Print t[5,5]<br><br>...but you can't yet create them using 'New Int[](...)' syntax, or slice them etc. I'm not sure yet how much you'll be able to do with them, but they will at least be in there. <br><br></td></tr></table><br>
<a name="2118266"></a>

<a name="2118264"></a>

<a name="2118268"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >marksibly</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, I've pushed the fix for Operator&lt;=&gt; to github. <br><br></td></tr></table><br>
<a name="2118269"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tiresius</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> For some reason I thought arrays would be the same as Monkey1, good to know.<br>This makes more sense to my brain than the "array of arrays" we had earlier. <br><br></td></tr></table><br>
<a name="2118270"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pakz</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> So it's <br><br>Local t:=New Int[10,20,3]<br>Print t[5,5,0]<br><br>for a 3 dimensional array?<br><br>Is easier then in monkey 1.<br><br>The tilemap data I put in 2 dimensional arrays since there is no read data in monkey 1 afaik. <br><br></td></tr></table><br>
<a name="2118275"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >marksibly</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's a little 'LoadMap2d' demo that uses a JSON file to store data.<br><br>Note: it requires the latest version of mx2cc on github - just fixed a few array issues.<br><br>'----- loadmap2d.monkey2 -----<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
#Import "&lt;std&gt;"

#Import "map2d.json"

Using std.json
Using std.stringio

Function LoadMap2d:Int[,]( path:String )

	Local src:=LoadString( "asset::map2d.json" )
	
	Local json:=New JsonParser( src ).ParseJson()
	
	Local w:=json["width"].ToInt()
	Local h:=json["height"].ToInt()
	
	Local map:=New Int[w,h]
	
	For Local y:=0 Until h
		For Local x:=0 Until w
			map[x,y]=json["data"][y][x].ToInt()
		Next
	Next
	
	Return map
End

Function Main()

	Local map:=LoadMap2d( "asset::map2d.json" )
	
	Local w:=map.Size( 0 )
	Local h:=map.Size( 1 )
	
	Print "w="+w
	Print "h="+h

	For Local y:=0 Until h
		Local str:=""
		For Local x:=0 Until w
			str+=","+map[x,y]
		Next
		Print str.Slice( 1 )
	Next

End
</textarea><br><br>'----- map2d.json -----<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
{
	"width":10,
	"height":10,
	"data":[
		[1,2,3,4,5,6,7,8,9,10],
		[2,3,4,5,6,7,8,9,10,1],
		[3,4,5,6,7,8,9,10,1,2],
		[4,5,6,7,8,9,10,1,2,3],
		[5,6,7,8,9,10,1,2,3,4],
		[6,7,8,9,10,1,2,3,4,5],
		[7,8,9,10,1,2,3,4,5,6],
		[8,9,10,1,2,3,4,5,6,7],
		[9,10,1,2,3,4,5,6,7,8],
		[10,1,2,3,4,5,6,7,8,9]
	]
}
</textarea> <br><br></td></tr></table><br>
<a name="2118274"></a>

<a name="2118271"></a>

<a name="2118272"></a>

<a name="2118273"></a>

<a name="2118303"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >impixi</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#23">[#23]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>Ok, I've pushed the fix for Operator&lt;=&gt; to github.<br> <br></div><br><br>Thanks. Now it works as expected.<br><br>PS: I like the new documentation layout, linked to and described in the MX2 dev blog. The logo is cool too... <br><br></td></tr></table><br>
<a name="2118305"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >impixi</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#24">[#24]</a></td></tr></table></td></tr><tr ><td class="posttext"> ***********<br>Interfaces<br>***********<br><br>inter1.monkey2<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'Interface Example

#Import "&lt;std.monkey2&gt;"

Using std

Function Main()
	
	Local beast := New Beast()
	Local npc := New NPC()
	Local a:Actor
	Local conv:Conversor

	Print "-----"

	a = beast
	a.Move()
	a.Attack()
	a.Flee()
	
	Print "-----"
	
	a = npc
	a.Move()
	a.Attack()
	a.Flee()
	
	conv = npc
	conv.ParsePhrase("BLAH BLAH")
	
	Print "-----"
	
	Local beast_npc := New BeastNPC()
	beast_npc.Move()
	beast_npc.ParsePhrase("BLAH BLAH")
	beast_npc.Emote()
	
	Print "-----"
	
End

'Interfaces are purely abstract and cannot be instantiated themselves.

'All Interface members are public and virtual by default.
Interface Actor
	Method Move:Void()
	Method Attack:Void()
	Method Flee:Void()
End

Interface Conversor
	Method ParsePhrase:Void(src:String)
End

'Classes "implement" Interfaces.
'A class must implement ALL of the interface's methods.
Class Beast Implements Actor
	Method Move:Void()
		Print "Beast Move!"
	End
	Method Attack:Void()
		Print "Beast Attack!"
	End
	Method Flee:Void()
		Print "Beast Flee!"
	End
End

'A class can implement more than one interface.
Class NPC Implements Actor, Conversor
	Method Move:Void()
		Print "NPC Move!"
	End
	Method Attack:Void()
		Print "NPC Attack!"
	End
	Method Flee:Void()
		Print "NPC Flee!"
	End
	Method ParsePhrase:Void(src:String)
		Print "I am an NPC and I know all about " + src + ", but I'm not tellin'!"
	End
End

'Subinterfaces can be declared by extending an existing interface.
'The same principles of class inheritance apply to interface inheritance.
'(Except interfaces can extend more than one interface, whereas classes
'cannot extend more than one class.) 

Interface ActorConversor Extends Actor, Conversor
	Method Emote:Void()
End

Class BeastNPC Extends Beast Implements ActorConversor 

	Method ParsePhrase:Void(src:String)
		Print "I am a BEAST - I know nothing about " + src + "!"
	End
	
	Method Emote:Void()
		Print ":hiss:"
	End

End

</textarea> <br><br></td></tr></table><br>
<a name="2118381"></a>

<a name="2118382"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >impixi</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#25">[#25]</a></td></tr></table></td></tr><tr ><td class="posttext"> **************<br>External C++<br>**************<br><br>Place all files in the same folder and run exte2.monkey2.<br><br>cpprand.h<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

#ifndef CPP_RAND_INTS
#define CPP_RAND_INTS

//Import the bbmonkey.h header file.
#include &lt;bbmonkey.h&gt;

#include &lt;random&gt;

//Inherit bbObject.
class random_ints : public bbObject
{
	private:
		unsigned int _qty;
		int* _data;
		
		std::random_device _rd;	
		
	public:
		random_ints(unsigned int qty);
		~random_ints();
		unsigned int getQty();
		int* generate(int min, int max);
		int* getDataPtr();
};

#endif

</textarea><br><br>cpprand.cpp<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

#include "cpprand.h"

random_ints::random_ints(unsigned int qty)
{
	_qty = qty;
	if (_qty &lt;= 0)
		_qty = 1;
	
	_data = NULL;
	_data = new int[_qty]();
}

random_ints::~random_ints()
{
	if (_data != NULL)
		delete[] _data;
    
    puts("~random_ints");
}

unsigned int random_ints::getQty()
{
	return _qty;
}

int* random_ints::generate(int min, int max)
{
	if (_data == NULL)
		return NULL;
	
	std::mt19937 gen(_rd());
	std::uniform_int_distribution&lt;int&gt; dist(min, max);
	
	for (unsigned int i=0; i&lt;_qty; i++)
		_data[i] = dist(gen);

	return _data;
}

int* random_ints::getDataPtr()
{
	return _data;
}

</textarea><br><br>cpprand.monkey2<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Namespace CppRand

#Import "cpprand.h"
#Import "cpprand.cpp"

Extern

'The quoted strings are the names of our c++ class equivalents.
Class RandomInts = "random_ints"
	Method New(qty:UInt)
	Property Qty:UInt()="getQty"
	Method Generate:Int Ptr(min:Int, max:Int)="generate"
	Method GetDataPtr:Int Ptr()="getDataPtr"
	
	'Declare a function so we can trigger the c++ class destructor.
	Function Destroy:Void(ris:RandomInts) = "delete"
End

</textarea><br><br>exte2.monkey2<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

'External C++ Code Access Simple Example

#Import "&lt;std.monkey2&gt;"
#Import "cpprand.monkey2"

Using std
Using CppRand

Function Main()

	Local ris:= New RandomInts(UInt(10))
	
	Print "First Batch of random integers:"
	Local data:= ris.Generate(-10, 10)

	For Local i:Int = 0 Until ris.Qty
		Print data[i]
	Next
	
	Print "Second batch of random integers:"
	ris.Generate(100, 200)
	data = ris.GetDataPtr()
	
	For Local i:Int = 0 Until ris.Qty
		Print data[i]
	Next
	
	'Explicitly destroy the object so its memory is freed. 
	RandomInts.Destroy(ris)
	
End

</textarea> <br><br></td></tr></table><br>
<a name="2118378"></a>

<a name="2118379"></a>

<a name="2118380"></a>

<a name="2118377"></a>

<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
