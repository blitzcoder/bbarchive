<!DOCTYPE html><html lang="en" ><head ><title >MonkeyPong!</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >MonkeyPong!</h1><a href="forums.php" >Monkey Archive Forums</a>/<a href="topics.php?forum=511" >Monkey Projects</a>/<a href="#bottom" >MonkeyPong!</a><br><br>
<a name="2000450"></a>

<a name="2000451"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> A quick game of Pong using Monkey!!<br><br><a href="http://www.therevillsgames.com/monkey/monkeyPong/MonkeyPong.html" target="_blank">http://www.therevillsgames.com/monkey/monkeyPong/MonkeyPong.html</a><br><br>Cursor keys Up and Down to control the left "bat".<br><br>Using James Boyd's delta timing code.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Import mojo

Global SCREEN_WIDTH%
Global SCREEN_HEIGHT%
Global dt:DeltaTimer
Global app:MyApp

Function Main:Void()
	app = New MyApp()
End Function


Class MyApp Extends App
	Field ball:Sprite
	Field playerBat:Sprite
	Field cpuBat:Sprite
	Field backgroundImg:Image
	Global FPS:Int = 60
		
	Method OnCreate:Void()
		' Store the device width and height
		SCREEN_WIDTH = DeviceWidth()
		SCREEN_HEIGHT = DeviceHeight()

		Seed = Millisecs()
		dt = New DeltaTimer (60)
		
		' Load the image
		backgroundImg = LoadImage("bg.jpg")
		
		' Create the ball
		ball = New Sprite(LoadImage("ball.png", 1, Image.MidHandle), SCREEN_WIDTH / 2, SCREEN_HEIGHT / 2)
		ball.dx = RndMin(-5,5,2)
		ball.dy = RndMin(-5,5,2)
				
		' Create the player
		playerBat = New Sprite(LoadImage("bat.png", 1, Image.MidHandle), 30, SCREEN_HEIGHT / 2)
		playerBat.setRGB(255,0,0)
		playerBat.speedY = 1
		playerBat.maxYSpeed = 7
		
		' Create the AI
		cpuBat = New Sprite(LoadImage("bat.png", 1, Image.MidHandle), SCREEN_WIDTH - 30, SCREEN_HEIGHT / 2)
		cpuBat.setRGB(0,0,255)
		cpuBat.speedY = 0.2
		cpuBat.maxYSpeed = 5
		cpuBat.scaleX = -1
		
		' 60 FPS please
		SetUpdateRate FPS
	End Method
	
	Method OnRender:Void()
		DrawImage backgroundImg, 0, 0
		playerBat.draw()
		cpuBat.draw()
		ball.draw()
		drawGUI()
	End Method
	
	Method drawGUI:Void()
		DrawText self.playerBat.score, SCREEN_WIDTH / 4, SCREEN_HEIGHT - 50
		DrawText self.cpuBat.score, SCREEN_WIDTH - SCREEN_WIDTH / 4, SCREEN_HEIGHT - 50
	End
	
	Method OnUpdate:Void()
		dt.UpdateDelta
		controls()
		ai()
		ballUpdate()
		collisions()
	End Method
	
	Method collisions:Void()
		if rectsOverlap(ball.x - ball.w2, ball.y - ball.h2, ball.w, ball.h, playerBat.x - playerBat.w2, playerBat.y - playerBat.h2, playerBat.w, playerBat.h)
			ball.dx = Abs(ball.dx)+0.5 * dt.delta
		End
		if rectsOverlap(ball.x - ball.w2, ball.y - ball.h2, ball.w, ball.h, cpuBat.x - cpuBat.w2, cpuBat.y - cpuBat.h2, cpuBat.w, cpuBat.h)
			ball.dx = -(Abs(ball.dx)+0.5 * dt.delta)
		End
	End
	
	Method ballUpdate:Void()
		ball.move()
		if ball.x &lt; 0
			self.cpuBat.score+=1
			resetBall()
		End
		if ball.x &gt; SCREEN_WIDTH
			self.playerBat.score+=1
			resetBall()
		End
		if ball.y &lt; 0 or ball.y &gt; SCREEN_HEIGHT
			ball.dy = -ball.dy
		End
		
		ball.rotation-=ball.dx * dt.delta
	End
	
	Method resetBall:Void()
		ball.x = SCREEN_WIDTH / 2
		ball.y = SCREEN_HEIGHT / 2
		ball.dx = RndMin(-5,5,2)
		ball.dy = RndMin(-5,5,2)
	End
	
	Method controls:Void()
		If KeyDown(KEY_UP)
			playerBat.dy-=playerBat.speedY
			if playerBat.dy&lt;-playerBat.maxYSpeed then playerBat.dy = -playerBat.maxYSpeed
		else If KeyDown(KEY_DOWN)
			playerBat.dy+=playerBat.speedY
			if playerBat.dy&gt;playerBat.maxYSpeed then playerBat.dy = playerBat.maxYSpeed
		else
			playerBat.dy*=0.95 ' friction to slow it down
		End
		
		if playerBat.y &lt; 0
			playerBat.y = 0
			playerBat.dy = 0
		End
		if playerBat.y &gt; SCREEN_HEIGHT
			playerBat.y = SCREEN_HEIGHT
			playerBat.dy = 0
		End		
		playerBat.move()
		
	End
	
	Method ai:Void()
		if ball.dx &gt; 0 then
			if ball.y &lt; cpuBat.y
				cpuBat.dy-=cpuBat.speedY
				if cpuBat.dy&lt;-cpuBat.maxYSpeed then cpuBat.dy = -cpuBat.maxYSpeed
			End
			if ball.y &gt; cpuBat.y
				cpuBat.dy+=cpuBat.speedY
				if cpuBat.dy&gt;cpuBat.maxYSpeed then cpuBat.dy = cpuBat.maxYSpeed
			End
		else
			cpuBat.dy*=0.95 ' friction to slow it down
		end if
		if cpuBat.y &lt; 0
			cpuBat.y = 0
			cpuBat.dy = 0
		End
		if cpuBat.y &gt; SCREEN_HEIGHT
			cpuBat.y = SCREEN_HEIGHT
			cpuBat.dy = 0
		End		
		cpuBat.move()
	End
	
End Class

Class Sprite
	Field x#,y#
	Field dx#, dy#
	Field speedX#, speedY#
	Field maxXSpeed#, maxYSpeed#
	Field image:Image
	field scaleX# = 1, scaleY# = 1
	field rotation#
	field red%, green%, blue%
	field w%, h%
	field w2%, h2%
	field score%
	
	Method New(img:Image,x#, y#)
		self.image = img
		self.x = x
		self.y = y
		self.w = img.Width()
		self.h = img.Height()
		self.w2 = self.w/2
		self.h2 = self.h/2
	End
	
	Method setRGB:Void(r%,g%,b%)
		self.red = r
		self.green = g
		self.blue = b
	End
	
	Method move:Void()
		self.x+=self.dx * dt.delta
		self.y+=self.dy * dt.delta
	End
	
	Method draw:Void()
		SetColor red, green, blue ' doesnt work with images!?!??!
		DrawImage(image, x, y, rotation, scaleX, scaleY)
		SetColor 255, 255, 255
	End
End

Function rectsOverlap:Int(x0:Float, y0:Float, w0:Float, h0:Float, x2:Float, y2:Float, w2:Float, h2:Float)
	If x0 &gt; (x2 + w2) Or (x0 + w0) &lt; x2 Then Return False
	If y0 &gt; (y2 + h2) Or (y0 + h0) &lt; y2 Then Return False
	Return True
End Function

Function RndMin:Float(min#,max#,minAmount#)
	local rv# = Rnd(min, max)

	While Abs(rv) &lt; minAmount
		rv = Rnd(-min,max)
	Wend
	return rv
End

Class DeltaTimer
	
	Field targetfps:Float = 60
	Field currentticks:Float
	Field lastticks:Float
	Field frametime:Float
	Field delta:Float
	
	Method New (fps:Float)
		targetfps = fps
		lastticks = Millisecs()
	End
	
	Method UpdateDelta:Void()
		currentticks = Millisecs()
		frametime = currentticks - lastticks
		delta = frametime / (1000.0 / targetfps)
		lastticks = currentticks
	End
	
End
</textarea> <br><br></td></tr></table><br>
<a name="2000462"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warpy</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> sweet! <br><br></td></tr></table><br>
<a name="2000466"></a>

<a name="2000467"></a>

<a name="2000468"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Canardian</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice! But I think you got the bananas and monkeys the wrong way, shouldn't the players be monkeys and the ball a banana? :)<br>Or will that come in the sequel: Revenge of the Monkey? <br><br></td></tr></table><br>
<a name="2000477"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Beaker</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I like. <br><br></td></tr></table><br>
<a name="2000583"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >xlsior</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I scored 3/0 before even realizing that I could control the banana on the left. ;-)<br><br>Worked well, although the score text was /very/ flickery on my PC. <br><br></td></tr></table><br>
<a name="2001003"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Slightly updated it:<br><br><a href="http://www.therevillsgames.com/monkey/monkeyDoc.html" target="_blank">http://www.therevillsgames.com/monkey/monkeyDoc.html</a><br><br>Now displays HTML5, Flash and you can download the Windows Exe and the Android APK. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
