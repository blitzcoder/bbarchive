<!DOCTYPE html><html lang="en" ><head ><title >Box2D....</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Box2D....</h1><a href="forums.php" >Monkey Archive Forums</a>/<a href="topics.php?forum=511" >Monkey Projects</a>/<a href="#bottom" >Box2D....</a><br><br>
<a name="2002797"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's a small example of a possible box2d implementation in Monkey...<br><br><img src="http://brucey.net/programming/monkey/misc/demos/flash_monkey_box2d.png"><br><br>... running in Flash, using the AS3 Box2D framework.<br><br>The Monkey source:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Strict

Import mojo
Import bah.box2d

Class HelloWorld Extends App

	Field world:b2World
	
	Field physScale:Float = 20
	Field timeStep:Float = 1.0/30.0
	Field velocityIterations:Int = 10
	Field positionIterations:Int = 10


	Method OnCreate:Int()

		SetUpdateRate(30)

		' Define the gravity vector
		Local gravity:b2Vec2 = New b2Vec2(0.0, 10.0)


		' Allow bodies to sleep
		Local doSleep:Bool = True
		
		
		' Construct a world object
		world = New b2World(gravity, True)
		world.SetWarmStarting(True)
		
		' debug draw...
		' TODO

		' Create border of boxes
		Local wall:b2PolygonShape = New b2PolygonShape()
		Local wallBd:b2BodyDef = New b2BodyDef()
		Local wallB:b2Body
		
		
		' Left
		wallBd.position().Set( -95 / physScale, 360 / physScale / 2)
		wall.SetAsBox(100/physScale, 400/physScale/2)
		wallB = world.CreateBody(wallBd)
		wallB.CreateFixture(wall)
		' Right
		wallBd.position().Set((640 + 95) / physScale, 360 / physScale / 2)
		wallB = world.CreateBody(wallBd)
		wallB.CreateFixture(wall)
		' Top
		wallBd.position().Set(640 / physScale / 2, -95 / physScale)
		wall.SetAsBox(680/physScale/2, 100/physScale)
		wallB = world.CreateBody(wallBd)
		wallB.CreateFixture(wall)
		' Bottom
		wallBd.position().Set(640 / physScale / 2, (360 + 95) / physScale)
		wallB = world.CreateBody(wallBd)
		wallB.CreateFixture(wall)

		
		' Add bodies
		Local fd:b2FixtureDef = New b2FixtureDef()
		Local sd:b2PolygonShape = New b2PolygonShape()
		Local bd:b2BodyDef = New b2BodyDef()
		bd.SetType(b2Body.b2_dynamicBody)

		Local b:b2Body
		fd.SetDensity(1.0)
		fd.SetFriction(0.5)
		fd.SetRestitution(0.1)
		fd.SetShape(sd)


		' Create stack
		For Local i:Int = 0 Until 10
			sd.SetAsBox((5) / physScale, (5) / physScale)
			bd.position().Set((640/2+100) / physScale, (360-5-i*25) / physScale)
			b = world.CreateBody(bd)
			b.CreateFixture(fd)
		Next

		Return 0
	End

	Method OnUpdate:Int()

		' Update physics
		world.DoStep(timeStep, velocityIterations, positionIterations)
		world.ClearForces()

		Return 0
	End


	Method OnRender:Int()


		Cls 0, 0, 0


		SetColor 255, 255, 255
		
		
		PushMatrix
	
			' joints
			'Local joint:b2Joint = world.JointList()
			'While joint
			'	DrawJoint(joint)
			'	joint = joint.NextJoint()
			'Wend
			
			
			' bodies
			Local bb:b2Body = world.GetBodyList()
			While bb

				Local tx:b2Transform = bb.GetTransform()
				
				Local f:b2Fixture = bb.GetFixtureList()
			
				While f
					DrawShape(f.GetShape(), tx)
					f = f.GetNext()
				Wend
			
				bb = bb.GetNext()
			Wend
		
		PopMatrix
		
		
		Return 0
	End

	

	Method OnLoading:Int()
		Return 0
	End


	Method DrawJoint:Void(joint:b2Joint)
		
	End Method

	Method DrawShape:Void(shape:b2Shape, tx:b2Transform)

		Local drawScale:Int = 20

		Local type:Int = shape.GetType()
	
		Select type
			Case e_polygonShape
			
				Local poly:b2PolygonShape = b2PolygonShape(shape)
			
			
				Local count:Int = poly.GetVertexCount()

				Local startVec:b2Vec2
				Local lastVec:b2Vec2
				
				For Local i:Int = 0 Until count
					Local vec:b2Vec2 = b2Math.MulX(tx, poly.GetVertex(i))
					
					If Not startVec
						startVec = vec
					EndIf
					
					If lastVec
						DrawLine lastVec.x() * drawScale, lastVec.y() * drawScale, vec.x() * drawScale, vec.y() * drawScale
					EndIf

					lastVec = vec
				Next
				
				DrawLine lastVec.x() * drawScale, lastVec.y() * drawScale, startVec.x() * drawScale, startVec.y() * drawScale

		End Select

	End Method

End

Function Main:Void()

	New HelloWorld

End
</textarea><br><br>I've wrapped the framework in two layers... one is the (kind of) abstract monkey interface, which hides the raw, target-specific implementation  + glue.<br><br>It runs verrrrry smoothly indeed ! :-)<br><br>It took me a day (well, what else is there to do on a Sunday?) to get my head around how this might work, and then to get the basics down. If I hadn't have spent a few hours not realising that my OnUpdate() wasn't actually being called, and that I also had to apply a body transform to the vertices (for drawing), I could have been much further on... pah!<br><br>I had to modify trans, to support multiple "PATHS" for mxmlc (the -sp option), since my module includes the AS3 source for Box2D, which is compiled in during the build.<br>It now does something like this:<br><pre class=code>
Execute "mxmlc -static-link-runtime-shared-libraries=true -sp . " + FLASH_MODS + " -- MonkeyGame.as"
</pre><br>from which there is a config option where you can define:<br><pre class=code>
FLASH_MODS="MODULES_PATH/bah/box2d/as"
</pre><br>MODULES_PATH is replaced with the root modules folder wherever trans is...<br>( ${} ) doesn't work here, as MODULES_PATH is not an environment variable :-p<br><br>The idea behind a multi-layer wrap is that, in theory, one can simply add the relevant other targets+glue at some point.<br><br>Things to note :<br> * It's hard to add new functionality in the way of modules, without hacking big changes into (at least) trans.<br> * It's not impossible to integrate existing frameworks into Monkey - it just requires a lot of work at the moment.<br><br>fun fun...<br><br>&lt;EDIT&gt; amended for my own foolishness to add SetUpdateRate()...<br>&lt;EDIT&gt; updated original image to show complete example in action... <br><br></td></tr></table><br>
<a name="2002750"></a>

<a name="2002741"></a>

<a name="2002758"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Xaron</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Uh that's incredible cool. Actually I work on a clean Box2d port for Monkey but currently port chipmunk physics (with help of Mark) first because it's more lightweight and has almost the power Box2d has. <br><br></td></tr></table><br>
<a name="2002795"></a>

<a name="2002798"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Now that I have read some of the Monkey documentation (haw haw), development of the first app has been very quick to get it running nicely. In fact, most of the StackTest port from the AS3 code was a case of copy/paste/tweak.<br><br>You can see more about the working example <b><a href="/posts.php?topic=387" target="_blank">here</a></b>. (and screenshot in the first post)<br><br>I split out the Box2D code from the main "App", in an effort to tidy things up a bit :<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Strict

Import mojo
Import bah.box2d
Import monkey.random

Class HelloWorld Extends App

	Field test:StackTest

	Method OnCreate:Int()
	
		SetUpdateRate(30)

		Print ""
		Print "      SPACE TO RESET"


		test = New StackTest()

		Return 0
	End

	Method OnUpdate:Int()
	
		If KeyHit(KEY_SPACE)
			test = New StackTest()
		EndIf
	

		test.Update()

		Return 0
	End


	Method OnRender:Int()

		Cls 0, 0, 0


		SetColor 255, 255, 255
		
		
		PushMatrix
		
			test.Render()
		
		PopMatrix
		
		
		Return 0
	End

	Method OnLoading:Int()
		Return 0
	End

End

Function Main:Void()

	New HelloWorld

End


Class Test

	Field world:b2World
	
	Field physScale:Float = 20
	Field timeStep:Float = 1.0/30.0
	Field velocityIterations:Int = 10
	Field positionIterations:Int = 10

	Method Init:Void()
	
		' Define the gravity vector
		Local gravity:b2Vec2 = New b2Vec2(0.0, 10.0)


		' Allow bodies to sleep
		Local doSleep:Bool = True
		
		
		' Construct a world object
		world = New b2World(gravity, True)
		world.SetWarmStarting(True)
		
		' debug draw...
		' TODO

		' Create border of boxes
		Local wall:b2PolygonShape = New b2PolygonShape()
		Local wallBd:b2BodyDef = New b2BodyDef()
		Local wallB:b2Body
		
		
		' Left
		wallBd.position().Set( -95 / physScale, 360 / physScale / 2)
		wall.SetAsBox(100/physScale, 400/physScale/2)
		wallB = world.CreateBody(wallBd)
		wallB.CreateFixture(wall)
		' Right
		wallBd.position().Set((640 + 95) / physScale, 360 / physScale / 2)
		wallB = world.CreateBody(wallBd)
		wallB.CreateFixture(wall)
		' Top
		wallBd.position().Set(640 / physScale / 2, -95 / physScale)
		wall.SetAsBox(680/physScale/2, 100/physScale)
		wallB = world.CreateBody(wallBd)
		wallB.CreateFixture(wall)
		' Bottom
		wallBd.position().Set(640 / physScale / 2, (360 + 95) / physScale)
		wallB = world.CreateBody(wallBd)
		wallB.CreateFixture(wall)

		InitTest()
		
	End Method

	Method InitTest:Void()
	End Method
	
	Method Update:Void()

		' Update physics
		world.DoStep(timeStep, velocityIterations, positionIterations)
		world.ClearForces()

	End Method
	
	Method Render:Void()

		' joints
		'Local joint:b2Joint = world.JointList()
		'While joint
		'	DrawJoint(joint)
		'	joint = joint.NextJoint()
		'Wend
		
		
		' bodies
		Local bb:b2Body = world.GetBodyList()
		While bb

			Local tx:b2Transform = bb.GetTransform()
			
			Local f:b2Fixture = bb.GetFixtureList()
		
			While f
				DrawShape(f.GetShape(), tx)
				f = f.GetNext()
			Wend
		
			bb = bb.GetNext()
		Wend

	End Method
	
	Method DrawJoint:Void(joint:b2Joint)
		
	End Method

	Method DrawShape:Void(shape:b2Shape, tx:b2Transform)

		Local drawScale:Int = 15

		Local type:Int = shape.GetType()
	
		Select type
			Case e_circleShape
			
				Local circle:b2CircleShape = b2CircleShape(shape)
				
				Local center:b2Vec2 = b2Math.MulX(tx, circle.GetLocalPosition())
				
				DrawCircle center.x() * drawScale, center.y() * drawScale, circle.GetRadius() * drawScale
			
			Case e_polygonShape
			
				Local poly:b2PolygonShape = b2PolygonShape(shape)
			
			
				Local count:Int = poly.GetVertexCount()

				Local startVec:b2Vec2
				Local lastVec:b2Vec2
				
				For Local i:Int = 0 Until count
					Local vec:b2Vec2 = b2Math.MulX(tx, poly.GetVertex(i))
					
					If Not startVec
						startVec = vec
					EndIf
					
					If lastVec
						DrawLine lastVec.x() * drawScale, lastVec.y() * drawScale, vec.x() * drawScale, vec.y() * drawScale
					EndIf

					lastVec = vec
				Next
				
				DrawLine lastVec.x() * drawScale, lastVec.y() * drawScale, startVec.x() * drawScale, startVec.y() * drawScale

		End Select

	End Method
	
End Class

Class StackTest Extends Test

	Method New()
		Init()
	End Method
	
	Method InitTest:Void()

		' Add bodies
		Local fd:b2FixtureDef = New b2FixtureDef()
		Local sd:b2PolygonShape = New b2PolygonShape()
		Local bd:b2BodyDef = New b2BodyDef()
		bd.SetType(b2Body.b2_dynamicBody)

		Local b:b2Body
		fd.SetDensity(1.0)
		fd.SetFriction(0.5)
		fd.SetRestitution(0.1)
		fd.SetShape(sd)


		' Create stack
		For Local i:Int = 0 Until 10
			sd.SetAsBox((10) / physScale, (10) / physScale)
			bd.position().Set((640/2+100) / physScale, (360-5-i*25) / physScale)
			b = world.CreateBody(bd)
			b.CreateFixture(fd)
		Next

		For Local i:Int = 0 Until 10
			sd.SetAsBox((10) / physScale, (10) / physScale)
			bd.position().Set((640/2-0+Rnd()*0.02 - 0.01) / physScale, (360-5-i*25) / physScale)
			b = world.CreateBody(bd)
			b.CreateFixture(fd)
		Next
		
		For Local i:Int = 0 Until 10
			sd.SetAsBox((10) / physScale, (10) / physScale)
			bd.position().Set((640/2+200+Rnd()*0.02 - 0.01) / physScale, (360-5-i*25) / physScale)
			b = world.CreateBody(bd)
			b.CreateFixture(fd)
		Next

		' Create ramp
		Local vxs:b2Vec2[] = [New b2Vec2(0, 0), New b2Vec2(0, -100 / physScale), New b2Vec2(200 / physScale, 0)]
		sd.SetAsArray(vxs, vxs.Length)
		fd.SetDensity(0)
		bd.SetType(b2Body.b2_staticBody)
		'bd.userData = "ramp"
		bd.position().Set(0, 360 / physScale)
		b = world.CreateBody(bd)
		b.CreateFixture(fd)

		' Create ball
		Local cd:b2CircleShape = New b2CircleShape()
		cd.SetRadius(40/physScale)
		fd.SetDensity(2)
		fd.SetRestitution(0.2)
		fd.SetFriction(0.5)
		fd.SetShape(cd)
		bd.SetType(b2Body.b2_dynamicBody)
		'bd.userData = "ball"
		bd.position().Set(50/physScale, 100 / physScale)
		b = world.CreateBody(bd)
		b.CreateFixture(fd)

	End Method
	
End Class
</textarea><br>It even supports Arrays for creation of polygons - this is easy for AS3, because Monkey fortunately uses the native Array type. Not sure how this will integrate with other languages...<br><br>Obviously, there's much more required to implement - joints, contacts, etc - but it gives a good idea of potential. <br><br></td></tr></table><br>
<a name="2006395"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >xzess</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Great work as always Brucey :)<br><br>Would you share you module or do you have a download link? <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
