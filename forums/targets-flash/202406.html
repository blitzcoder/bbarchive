<!DOCTYPE html><html lang="en" ><head ><title >How to add Preloader to Monkey - Plz add this Mark</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >How to add Preloader to Monkey - Plz add this Mark</h1><a href="forums.php" >Monkey Targets Forums</a>/<a href="topics.php?forum=503" >Flash</a>/<a href="#bottom" >How to add Preloader to Monkey - Plz add this Mark</a><br><br>
<a name="2023789"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> First we need to alter trans, open up src\trans\targets\flash.monkey.<br><br>Alter the Execute command (around line 72) so it looks like this:<br><pre class=code>Execute "mxmlc -static-link-runtime-shared-libraries=true -frame=MonkeyGame,MonkeyGame Preloader.as"</pre><br>And the FLASH_PLAYER execute (76):<br><pre class=code>Execute FLASH_PLAYER+" ~q"+RealPath( "Preloader.swf" )+"~q",False</pre><br>Compile trans, then move the exe to the bin folder.<br><br>Now go into the folder targets\flash and create the Preloader.as:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">package
{
	import flash.display.DisplayObject;
	import flash.display.Graphics;
	import flash.display.MovieClip;
	import flash.display.Sprite;
	import flash.display.StageAlign;
	import flash.display.StageScaleMode;
	import flash.events.Event;
	import flash.events.ProgressEvent;
	import flash.utils.getDefinitionByName;

	public class Preloader extends MovieClip
	{
		/** Bubble this event up to the stage to remove the preloader. **/
		public static const REMOVE_PRELOADER:String = "removePreloader";
		
		/** The fully-qualified name of the class we should instantiate after preloading. **/
		protected var applicationClassName:String;
		
		/** Container for our preloader graphics/animation. **/
		protected var preloader:Sprite;
		
		/** The application once it's instantiated. **/
		protected var application:DisplayObject;

		/** Constructor. **/
		public function Preloader(applicationClassName:String = "")
		{
			this.applicationClassName = applicationClassName;
			
			stop();

			loaderInfo.addEventListener(ProgressEvent.PROGRESS, onProgress);
			loaderInfo.addEventListener(Event.COMPLETE, onComplete);
			
			setupStage();
			addPreloader();
		}
		
		/** Set any stage properties or global playback settings, e.g. framerate. **/
		protected function setupStage():void
		{
			stage.align = StageAlign.TOP_LEFT;
			stage.scaleMode = StageScaleMode.NO_SCALE;
		}
		
		/** Add preloader -- override this to add your own animated elements. **/
		protected function addPreloader():void
		{
			preloader = new Sprite();
			addChild(preloader);
		}
		
		/** Update preload progress. **/
		protected function onProgress(event:ProgressEvent):void
		{
			var progress:Number = event.bytesLoaded / event.bytesTotal;
			updatePreloader(progress);
		}
		
		/** Update preloader -- override this to update your animated elements. **/
		protected function updatePreloader(progress:Number):void
		{
			// update the preloader with the loading progress
			var g:Graphics = preloader.graphics;
			g.clear();

			// draw a solid background so we can't see the app as it loads in the background
			g.beginFill(0x000000);
			g.drawRect(0, 0, stage.stageWidth, stage.stageHeight);
			g.endFill();
			
			// draw the outline of a progress bar
			g.lineStyle(3, 0xffffff, 1, true);
			g.drawRoundRect(100, 290, 600, 20, 10, 10);
			
			// fill the progress bar based on how many of our bytes have been loaded
			g.beginFill(0xffffff);
			g.drawRoundRect(100, 290, 600 * progress, 20, 10, 10);
			g.endFill();
		}
		
		/** Preload complete. **/
		protected function onComplete(event:Event):void
		{
			loaderInfo.removeEventListener(ProgressEvent.PROGRESS, onProgress);
			loaderInfo.removeEventListener(Event.COMPLETE, onComplete);
			
			nextFrame();
			
			createApplication();
		}
		
		/** Create the application. **/
		protected function createApplication():void
		{
			// we assume the current label's name is the name of the class,
			// unless overridden in the constructor
			//var className:String = applicationClassName || currentLabel;
			
			// must create the application by name so we don't have any static linkage to this class
			
			var appClass:Class = getDefinitionByName("MonkeyGame") as Class;
			addChild(new appClass() as DisplayObject);
		}

		/** Remove preloader. **/
		protected function onRemovePreloader(event:Event):void
		{
			application.removeEventListener(REMOVE_PRELOADER, onRemovePreloader);
			removePreloader();
		}

		/** Remove preloader -- override this to remove the elements added in addPreloader(). **/
		protected function removePreloader():void
		{
			removeChild(preloader);
			preloader = null;
		}
	}
}</textarea><br><br>Alter the MonkeyGame.html embed line to look like this:<br><pre class=code>&lt;embed src="Preloader.swf" type="application/x-shockwave-flash" width="640" height="480" wmode="direct"&gt;</pre><br><br>Alter MonkeyGame.as, add the following line after [SWF(width="640",height="480")]:<br><pre class=code>[Frame(factoryClass="Preloader")]</pre><br><br>Then compile your game... hopefully you will have a loading bar. Play around with the Preloader.as.<br><br>Test:<br><a href="http://www.therevillsgames.com/monkey/flash/MonkeyGame.html" target="_blank">http://www.therevillsgames.com/monkey/flash/MonkeyGame.html</a> <br><br></td></tr></table><br>
<a name="2023788"></a>

<a name="2023804"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >EdzUp</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> In safari the loading bar goes off the right side, any way of scaling it to the resolution of the window? <br><br></td></tr></table><br>
<a name="2023805"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> the loading bar goes off the right side cos its being drawn at xcoord 100 and its 600 pixels wide. So its an easy fix.<br><br>Good work Mr revills <br><br></td></tr></table><br>
<a name="2023807"></a>

<a name="2023808"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Foppy</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Very nice that you have figured this out. <br><br></td></tr></table><br>
<a name="2023847"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah the loading bar was quickly added ;)<br><br>I think I can streamline the process, by making mxmlc output the swf as MonkeyGame.swf, there is an "output" parameter.<br><br>So I guess we could do something like this in trans:<br><pre class=code>Execute "mxmlc -static-link-runtime-shared-libraries=true -frame=MonkeyGame,MonkeyGame -output=MonkeyGame Preloader.as"</pre><br><br>This <u>should</u> create a MonkeyGame.swf, so you dont have to alter the FLASH_PLAYER execute line or the MonkeyGame.html file.<br><br>*Not tested... <br><br></td></tr></table><br>
<a name="2023844"></a>

<a name="2023846"></a>

<a name="2023881"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just tested the above and I was wrong, it needed to be this:<br><br><pre class=code>Execute "mxmlc -static-link-runtime-shared-libraries=true -frame=MonkeyGame,MonkeyGame -output=MonkeyGame.swf Preloader.as"</pre><br><br>And I've moved the loading bar back a tad:<br><br>Preloader.as:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">package
{
	import flash.display.DisplayObject;
	import flash.display.Graphics;
	import flash.display.MovieClip;
	import flash.display.Sprite;
	import flash.display.StageAlign;
	import flash.display.StageScaleMode;
	import flash.events.Event;
	import flash.events.ProgressEvent;
	import flash.utils.getDefinitionByName;

	public class Preloader extends MovieClip
	{
		/** Bubble this event up to the stage to remove the preloader. **/
		public static const REMOVE_PRELOADER:String = "removePreloader";
		
		/** The fully-qualified name of the class we should instantiate after preloading. **/
		protected var applicationClassName:String;
		
		/** Container for our preloader graphics/animation. **/
		protected var preloader:Sprite;
		
		/** The application once it's instantiated. **/
		protected var application:DisplayObject;

		/** Constructor. **/
		public function Preloader(applicationClassName:String = "")
		{
			this.applicationClassName = applicationClassName;
			
			stop();

			loaderInfo.addEventListener(ProgressEvent.PROGRESS, onProgress);
			loaderInfo.addEventListener(Event.COMPLETE, onComplete);
			
			setupStage();
			addPreloader();
		}
		
		/** Set any stage properties or global playback settings, e.g. framerate. **/
		protected function setupStage():void
		{
			stage.align = StageAlign.TOP_LEFT;
			stage.scaleMode = StageScaleMode.NO_SCALE;
		}
		
		/** Add preloader -- override this to add your own animated elements. **/
		protected function addPreloader():void
		{
			preloader = new Sprite();
			addChild(preloader);
		}
		
		/** Update preload progress. **/
		protected function onProgress(event:ProgressEvent):void
		{
			var progress:Number = event.bytesLoaded / event.bytesTotal;
			updatePreloader(progress);
		}
		
		/** Update preloader -- override this to update your animated elements. **/
		protected function updatePreloader(progress:Number):void
		{
			// update the preloader with the loading progress
			var g:Graphics = preloader.graphics;
			g.clear();

			// draw a solid background so we can't see the app as it loads in the background
			g.beginFill(0x000000);
			g.drawRect(0, 0, stage.stageWidth, stage.stageHeight);
			g.endFill();
			
			// draw the outline of a progress bar
			g.lineStyle(3, 0xffffff, 1, true);
			g.drawRoundRect(20, 290, 600, 20, 10, 10);
			
			// fill the progress bar based on how many of our bytes have been loaded
			g.beginFill(0xffffff);
			g.drawRoundRect(20, 290, 600 * progress, 20, 10, 10);
			g.endFill();
		}
		
		/** Preload complete. **/
		protected function onComplete(event:Event):void
		{
			loaderInfo.removeEventListener(ProgressEvent.PROGRESS, onProgress);
			loaderInfo.removeEventListener(Event.COMPLETE, onComplete);
			
			nextFrame();
			
			createApplication();
		}
		
		/** Create the application. **/
		protected function createApplication():void
		{
			// we assume the current label's name is the name of the class,
			// unless overridden in the constructor
			//var className:String = applicationClassName || currentLabel;
			
			// must create the application by name so we don't have any static linkage to this class
			
			var appClass:Class = getDefinitionByName("MonkeyGame") as Class;
			addChild(new appClass() as DisplayObject);
		}

		/** Remove preloader. **/
		protected function onRemovePreloader(event:Event):void
		{
			application.removeEventListener(REMOVE_PRELOADER, onRemovePreloader);
			removePreloader();
		}

		/** Remove preloader -- override this to remove the elements added in addPreloader(). **/
		protected function removePreloader():void
		{
			removeChild(preloader);
			preloader = null;
		}
	}
}</textarea><br><br><a href="http://www.therevillsgames.com/monkey/flash/take2/MonkeyGame.html" target="_blank">http://www.therevillsgames.com/monkey/flash/take2/MonkeyGame.html</a> <br><br></td></tr></table><br>
<a name="2023880"></a>

<a name="2023999"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >h.humbert</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks! Since I don't have a clue about ActionScript you pretty much saved my life. <br><br></td></tr></table><br>
<a name="2025871"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >siread</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I had some trouble using this preloader after uploading to Kongregate. It worked fine in the latest IE and Chrome but lots of IE8 users complained that the intitial load worked fine, but after that they only saw white screen when they refreshed the page or restarted IE and went to the game page.<br><br>After endless head-banging I tried swapping the preloader.as code for the code here:<br><a href="http://flashpunk.net/forums/index.php?PHPSESSID=8a9aea7686a3cd31bcc6a4741be5be65&amp;topic=633.0" target="_blank">http://flashpunk.net/forums/index.php?PHPSESSID=8a9aea7686a3cd31bcc6a4741be5be65&amp;topic=633.0</a><br><br>I <i>think</i> all is good again. :) <br><br></td></tr></table><br>
<a name="2025876"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey Siread, what final preloader did you use? (As on that web page, there are a few pages with code on there?) <br><br></td></tr></table><br>
<a name="2025888"></a>

<a name="2025889"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >siread</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah sorry, I just used the one in the first post. I haven't tried the others. :) <br><br></td></tr></table><br>
<a name="2025890"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> No worries, so its this code:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">package 
{
	 import flash.display.*;
	 import flash.events.*;
	 import flash.text.*;
	 import flash.utils.getDefinitionByName;
	
	 [SWF(width = "640", height = "480")]
	
	 public class Preloader extends MovieClip 
	{
		 private var loader:Sprite = new Sprite();
		 private var border:Sprite = new Sprite();
		 private var text:TextField = new TextField();
		
		 private const loaderWidth:int = 320;
		 private const loaderHeight:int = 32;
		
		 private const loaderColor:uint = 0xffffff;
		 private const textColor:uint = 0xbbbbbb;
		 private const backgroundColor:uint = 0x000000;
		
		 private const main:String = "Main";
		 private var loaded:Number = 0;
		
		 public function Preloader() 
		{
			 // loader information
			 stage.addEventListener(Event.ENTER_FRAME, progress);
			
			 // show loader
			 addChild(loader);
			 loader.x = (stage.stageWidth / 2) - (loaderWidth / 2) + 4;
			 loader.y = (stage.stageHeight / 2) - (loaderHeight / 2) + 4;
			
			 addChild(border);
			 border.x = (stage.stageWidth / 2) - (loaderWidth / 2);
			 border.y = (stage.stageHeight / 2) - (loaderHeight / 2);
		
			 addChild(text);
			 text.x = (stage.stageWidth / 2) - (loaderWidth / 2);
			 text.y = (stage.stageHeight / 2) - (loaderHeight / 2) - 30;
			 text.textColor = textColor;
			
			 // render background
			 graphics.beginFill(backgroundColor, 1);
			 graphics.drawRect(0, 0, stage.stageWidth, stage.stageHeight);
			 graphics.endFill();
			
			 // render the border
			 border.graphics.clear();
			 border.graphics.lineStyle(2, loaderColor);
			 border.graphics.drawRect(0, 0, loaderWidth, loaderHeight);
		 }
		
		 private function progress(e:Event):void 
		{
			 // how much we've loaded thus far
			 loaded = loaderInfo.bytesLoaded / loaderInfo.bytesTotal;
			
			 // update loader graphic
			 loader.graphics.clear();
			 loader.graphics.beginFill(loaderColor);
			 loader.graphics.drawRect(0, 0, loaded * (loaderWidth - 8), loaderHeight - 8);
			 loader.graphics.endFill();
			
			 // update text
			 text.text = "Loading: " + Math.ceil(loaded * 100) + "%";
			
			 // done loading?
			 if (loaderInfo.bytesLoaded &gt;= loaderInfo.bytesTotal)
			 {
				 startup();
			 }
		 }
		
		 private function startup():void 
		{
			 // remove event listener(s)
			 stage.removeEventListener(Event.ENTER_FRAME, progress);
			
			 // hide loader
			 stop();
			
			 // remove all the children
			 var i:int = numChildren;
			 while (i --)
			 {
				 removeChildAt(i)
			 }
			
			 // go to the main class
			 var mainClass:Class = getDefinitionByName("Main") as Class;
			 parent.addChild(new mainClass as DisplayObject);
			
			 // remove self
			 parent.removeChild(this)
		 }
		
	 }
	
 }
</textarea><br><br>(Just to place it here on this forum) <br><br></td></tr></table><br>
<a name="2026424"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >siread</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> I did a little more work on my pre-loader, incorporating a background image and a third-party intro using FlashDevelop. You can see how in this thread...<br><a href="http://www.monkeycoder.co.nz/Community/posts.php?topic=2623" target="_blank">http://www.monkeycoder.co.nz/Community/posts.php?topic=2623</a> <br><br></td></tr></table><br>
<a name="2041794"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Paul - Taiphoz</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> did this ever get added? <br><br></td></tr></table><br>
<a name="2041815"></a>

<a name="2041816"></a>

<a name="2041817"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rushino</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Altering trans isnt a big deal. See the whole monkey package as an overall for your next game.. so you might have modified files for your target game such trans or the target directory template.. this is why i love monkey as it is extensible in many ways. I even think i could make an application to configure this and lets you use many instance of trans source in one click.. that would help. <br><br></td></tr></table><br>
<a name="2041814"></a>

<a name="2044702"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Raz</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just saying thanks for this. Makes my LD48 flash games seem a bit less shabby now! <br><br></td></tr></table><br>
<a name="2044726"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GW_</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Wouldn't it better if Monkey was set up to always use a dummy pre-loader? That way we wouldn't have to alter Monkey core files. <br><br></td></tr></table><br>
<a name="2044725"></a>

<a name="2044730"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Raz</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah I would agree with that. I can't really imagine a scenario where when using  flash you wouldn't want one. <br><br></td></tr></table><br>
<a name="2044888"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Soap</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> +1 to dummy pre-loader<br><br>HEY, HOW ABOUT THIS???<br><br>Make the dummy pre-loader an upsell to Monkey (Made With Monkey!). <br><br>Then we change it if we want to, no obligation to keep it that way, but on one hand it's more user friendly and the other it helps market Monkey.<br><br>We have used the pre-loader in this thread so also thank You &lt;3 <br><br></td></tr></table><br>
<a name="2061041"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Aymes</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> I don't suppose the dummy preloader ever got added and I just missed it?<br><br>Trying to create a web version of my latest game and all is well except for the fact it's got no preloader bar.<br><br>I've attempted to incorporate the one above. This is what I've done:<br><br>1. changed flash.monkey to incorporate the updated line 72 therevills mentioned.<br>2. compiled Trans (scary stuff!)<br>3. created Preloader.as<br>4. updated MonkeyGame.as<br><br>It all compiles but I run into a couple of difficulties:<br><br>1. no preloader bar! Just a white screen (when I load from a site. If I run locally it just loads the game as normal).<br>2. it changes the res of my game (I'm going for 800x600) and makes it look as if image filtering is turned off (it runs fast enough with it turned on).<br><br>I suspect I'm just being a dumbo. Any clues anyone? <br><br></td></tr></table><br>
<a name="2061030"></a>

<a name="2061027"></a>

<a name="2061081"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Paul - Taiphoz</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> Diddy needs a loading bar /me looks at them...<br><br>I don't think this works with the current version of monkey, might be wrong tho, I know I tried it a little while ago and couldn't get it working either. <br><br></td></tr></table><br>
<a name="2061090"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Aymes</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah phew, that makes me feel better! At least I wasn't doing anything silly then.<br><br>Can't see why this isn't already included as part of Monkey. I mean, when would you ever not want a loading bar of some description in Flash? <br><br></td></tr></table><br>
<a name="2062084"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Aymes</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> Agh, could really do with a preloader soon. <br><br>I've got some contract work involving making flash stuff and without the preloader it's going to look a bit shoddy :(<br><br>Unless anyone knows the answer to getting this preloader working? <br><br></td></tr></table><br>
<a name="2062529"></a>

<a name="2062530"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#23">[#23]</a></td></tr></table></td></tr><tr ><td class="posttext"> Okay using v72b:<br><br><b>1. Create Preloader.as</b><br><br>Create the file "Preloader.as" in the \targets\flash\template\ folder:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">package
{
	import flash.display.DisplayObject;
	import flash.display.Graphics;
	import flash.display.MovieClip;
	import flash.display.Sprite;
	import flash.display.StageAlign;
	import flash.display.StageScaleMode;
	import flash.events.Event;
	import flash.events.ProgressEvent;
	import flash.utils.getDefinitionByName;

	public class Preloader extends MovieClip
	{
		/** Bubble this event up to the stage to remove the preloader. **/
		public static const REMOVE_PRELOADER:String = "removePreloader";
		
		/** The fully-qualified name of the class we should instantiate after preloading. **/
		protected var applicationClassName:String;
		
		/** Container for our preloader graphics/animation. **/
		protected var preloader:Sprite;
		
		/** The application once it's instantiated. **/
		protected var application:DisplayObject;

		/** Constructor. **/
		public function Preloader(applicationClassName:String = "")
		{
			this.applicationClassName = applicationClassName;
			
			stop();

			loaderInfo.addEventListener(ProgressEvent.PROGRESS, onProgress);
			loaderInfo.addEventListener(Event.COMPLETE, onComplete);
			
			setupStage();
			addPreloader();
		}
		
		/** Set any stage properties or global playback settings, e.g. framerate. **/
		protected function setupStage():void
		{
			stage.align = StageAlign.TOP_LEFT;
			stage.scaleMode = StageScaleMode.NO_SCALE;
		}
		
		/** Add preloader -- override this to add your own animated elements. **/
		protected function addPreloader():void
		{
			preloader = new Sprite();
			addChild(preloader);
		}
		
		/** Update preload progress. **/
		protected function onProgress(event:ProgressEvent):void
		{
			var progress:Number = event.bytesLoaded / event.bytesTotal;
			updatePreloader(progress);
		}
		
		/** Update preloader -- override this to update your animated elements. **/
		protected function updatePreloader(progress:Number):void
		{
			// update the preloader with the loading progress
			var g:Graphics = preloader.graphics;
			g.clear();

			// draw a solid background so we can't see the app as it loads in the background
			g.beginFill(0x000000);
			g.drawRect(0, 0, stage.stageWidth, stage.stageHeight);
			g.endFill();
			
			// draw the outline of a progress bar
			g.lineStyle(3, 0xffffff, 1, true);
			g.drawRoundRect(20, 290, 600, 20, 10, 10);
			
			// fill the progress bar based on how many of our bytes have been loaded
			g.beginFill(0xffffff);
			g.drawRoundRect(20, 290, 600 * progress, 20, 10, 10);
			g.endFill();
		}
		
		/** Preload complete. **/
		protected function onComplete(event:Event):void
		{
			loaderInfo.removeEventListener(ProgressEvent.PROGRESS, onProgress);
			loaderInfo.removeEventListener(Event.COMPLETE, onComplete);
			
			nextFrame();
			
			createApplication();
		}
		
		/** Create the application. **/
		protected function createApplication():void
		{
			// we assume the current label's name is the name of the class,
			// unless overridden in the constructor
			//var className:String = applicationClassName || currentLabel;
			
			// must create the application by name so we don't have any static linkage to this class
			
			var appClass:Class = getDefinitionByName("MonkeyGame") as Class;
			addChild(new appClass() as DisplayObject);
		}

		/** Remove preloader. **/
		protected function onRemovePreloader(event:Event):void
		{
			application.removeEventListener(REMOVE_PRELOADER, onRemovePreloader);
			removePreloader();
		}

		/** Remove preloader -- override this to remove the elements added in addPreloader(). **/
		protected function removePreloader():void
		{
			removeChild(preloader);
			preloader = null;
		}
	}
}</textarea><br><br><b>2. Alter MonkeyGame.as</b><br><br>Add the following line after [SWF(width="640",height="480")]:<br><pre class=code>[Frame(factoryClass="Preloader")]</pre><br><b>3. Compile your app</b><br><br>Ensure that you have deleted the old build folder from your app to get the preloader files.<br><br><b>4. DONE!</b><br><br>Tested in FireFox and Chrome. <br><br></td></tr></table><br>
<a name="2062528"></a>

<a name="2062631"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Aymes</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#24">[#24]</a></td></tr></table></td></tr><tr ><td class="posttext"> therevills, I always said you was a genius! Thanks very much for that, appreciated :D <br><br></td></tr></table><br>
<a name="2062843"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grudlux</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#25">[#25]</a></td></tr></table></td></tr><tr ><td class="posttext"> Awesome, thanks for the clean and easy-to-add preloader! <br><br></td></tr></table><br>
<a name="2069319"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CGV</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#26">[#26]</a></td></tr></table></td></tr><tr ><td class="posttext"> Before I spend hours wrestling with it, please, does this still work with the latest Monkey, or at least V74a? <br><br></td></tr></table><br>
<a name="2069367"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CGV</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#27">[#27]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ive tried this preloader with V74a and V75d.<br><br>In both cases no preloader shows up at all in IE and the progress bar doesn't move in Chrome.<br><br>It works fine in Firefox, Safari and Opera though.<br><br>Can anyone else confirm this as I suspect it may be a problem with just my box.<br><br><br>UPDATE: I tried building with 72b which therevills specifically wrote the last preloader for and I got the same exact results so apparently it is my box.<br><br>I tried building with 3 different Flex SDK's and still the same results.<br><br>I'm at a loss to figure out what it is.<br><br>Other than the Flex SDK what else is involved in building Flash games with Monkey? <br><br></td></tr></table><br>
<a name="2069365"></a>

<a name="2069388"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >bazmonkey</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#28">[#28]</a></td></tr></table></td></tr><tr ><td class="posttext"> I use essentially the same code, with minor tweaks. <br><br><a href="http://goodreactions.com/preloader/MonkeyGame.html" target="_blank">http://goodreactions.com/preloader/MonkeyGame.html</a><br>(I bloated out the swf to ~6MB so you see the loader bar for longer).<br><br>For me, this works on Chrome, Firefox, Opera, mac+windows. + works in IE7, Safari. But, I havent done a wide release using this preloader yet (have been using mochi distribution till now). <br><br>I'm using flex 4.6.  My browsers + flash are generally up to date. I'm using monkey 72b. Actually, I dont edit any of the monkey trans/build commands, I just use mxmlc on the command line manually, but its the same command. <br><br></td></tr></table><br>
<a name="2069387"></a>

<a name="2069386"></a>

<a name="2069384"></a>

<a name="2069385"></a>

<a name="2069383"></a>

<a name="2069382"></a>

<a name="2069380"></a>

<a name="2069379"></a>

<a name="2069419"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CGV</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#29">[#29]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yep, your preloader works in every browser so it is my box.<br><br>Thanks for the confirmation.<br><br>I'm going to try an uninstall/reinstall of everything. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
