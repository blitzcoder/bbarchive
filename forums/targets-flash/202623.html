<!DOCTYPE html><html lang="en" ><head ><title >FLA files</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >FLA files</h1><a href="forums.php" >Monkey Targets Forums</a>/<a href="topics.php?forum=503" >Flash</a>/<a href="#bottom" >FLA files</a><br><br>
<a name="2026339"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >siread</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I notice that a lot of Flash sites request that you include their own start-up animations. These are generally .fla animation files. Has anyone tried incorporating something like this? Maybe by building it as a swf first, then building in FlashBuilder? <br><br></td></tr></table><br>
<a name="2026229"></a>

<a name="2026429"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >siread</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, I've managed to implement the intro animations into my pre-loader. First I had to build the FLA in CS4, making sure I added enough frames to the main layer (so that actual number of frames in Layer 1 matched the number of frame in the animations included within Layer 1 - I'm not really sure of the correct terminology here!) <br><br>Then put a stop(); command in the very last keyframe on Layer 1 (otherwise the anim will loop and play sound even beyond the life of the preloader). Now publish the intro as a swf. <br><br>Create a FlashBuilder project out of your MonkeyGame.as and data. Pop the swf in your src folder, allow with a background image if you wish.  Create a new file in the project called Preloader.as and use this code:<br><br><pre class=code>// My preloader
package 
{
	import flash.display.*;
	import flash.events.*;
	import flash.text.*;
	import flash.utils.getDefinitionByName;
	
	[SWF(width = "640", height = "480")]
	
	public class Preloader extends MovieClip 
	{
		[Embed(source = 'bg.png')] private const PICTURE:Class;		// PUT YOUR BACKGROUND IMAGE HERE
		[Embed(source = 'intro_logo.swf')] private var MOVIE:Class;	// CHANGE INTRO ANIM HERE
		
		privateprivate var myPicture:Bitmap = new PICTURE;
		private var myMovie:MovieClip = new MOVIE;

		private const allowedSite:String = "newstarsoccer.com";		// SITE LOCK HERE

		private var loader:Sprite = new Sprite();
		private const loaderColor:uint = 0xffffff;
		private var loaded:Number = 0;
		private var introdone:Boolean = false;
		private var count:int;
		
		public function Preloader() 
		{			
			// loader information
			stage.addEventListener(Event.ENTER_FRAME, progress);
			myMovie.addEventListener(Event.ENTER_FRAME, handleFrame);
			
			addChild(myPicture);
			addChild(myMovie);
			
			// SET THE POSITION OF YOUR ANIMATION
			myMovie.scaleX = stage.stageWidth / myMovie.width;
			myMovie.scaleY = myMovie.scaleX;
			myMovie.x = 0 - (((myMovie.width * myMovie.scaleX) - stage.stageWidth) / 2);
			myMovie.y = 120;
			
			// show loader
			addChild(loader);
			loader.x = 0;
			loader.y = stage.stageHeight - 32;
		}
		
		private function progress(e:Event):void 
		{			
			// how much we've loaded thus far
			loaded = loaderInfo.bytesLoaded / loaderInfo.bytesTotal;
			
			// update loader graphic
			loader.graphics.clear();
			loader.graphics.beginFill(loaderColor);
			loader.graphics.drawRect(0, 0, loaded * stage.stageWidth, 32);
			loader.graphics.endFill();
			
			// done loading?
			if (loaderInfo.bytesLoaded &gt;= loaderInfo.bytesTotal &amp;&amp; introdone == true)
			{
				startup();
			}
		}

		private function handleFrame(e:Event):void 
		{
			count += 1;

			if (count &gt;= 150)	// CHANGE INTRO LENGTH (Num of frames in intro swf)
			{
				introdone = true;
				
				if (loaderInfo.bytesLoaded &gt;= loaderInfo.bytesTotal)
				{
					startup();
				}
			}
		}
		
		private function startup():void 
		{
			// remove event listener(s)
			stage.removeEventListener(Event.ENTER_FRAME, progress);
			myMovie.removeEventListener(Event.ENTER_FRAME, handleFrame);
			
			// hide loader
			stop();
			
			// remove all the children
			var i:int = numChildren;
			
			while (i --)
			{
				removeChildAt(i);
			}
			
			// site lock
			if (this.root.loaderInfo.url.indexOf(allowedSite) == -1)
			{
				return;
			}
			
			// go to the main class
			var mainClass:Class = getDefinitionByName("MonkeyGame") as Class;
			parent.addChild(new mainClass as DisplayObject);
			
			// remove self
			parent.removeChild(this)
		}
		
	}
	
}
</pre><br><br>Then in MonkeyGame.as add this line after [SWF(width="640",height="480")]:<br><br><pre class=code>[Frame(factoryClass="Preloader")]</pre><br><br>Build in FlashBuilder and find your new swf in the bin folder. <br><br></td></tr></table><br>
<a name="2026428"></a>

<a name="2026427"></a>

<a name="2026426"></a>

<a name="2026425"></a>

<a name="2026423"></a>

<a name="2026422"></a>

<a name="2026421"></a>

<a name="2026420"></a>

<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
