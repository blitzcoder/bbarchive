<!DOCTYPE html><html lang="en" ><head ><title >Trans Failed with flash and not html5 and glfw</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Trans Failed with flash and not html5 and glfw</h1><a href="forums.php" >Monkey Targets Forums</a>/<a href="topics.php?forum=503" >Flash</a>/<a href="#bottom" >Trans Failed with flash and not html5 and glfw</a><br><br>
<a name="2098582"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pakz</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I installed the flash target today and got a error with my a* code. This does not happen with the other targets. Other code does work, but when inserted into blogger the input does not work (mouse/keyboard)<br><br>The error I get with this a* code is :Trans failed and says that a funcion does not return a value and then points to a } character.<br><br>Does this happen with anyone else with the flash target?<br><br>code where this happens with below :<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import mojo

Const mapwidth:Int=40
Const mapheight:Int=30
Const tilewidth:Int=16
Const tileheight:Int=16
Global sx:Int 'path start x
Global sy:Int 'path start y
Global ex:Int 'path end x
Global ey:Int 'path end y
Global map:Int[mapwidth][]
Global olmap:Int[mapwidth][]
Global clmap:Int[mapwidth][]
Class openlist
    Field x:Int
    Field y:Int
    Field f:Int
    Field g:Int
    Field h:Int
    Field px:Int
    Field py:Int
    Method New(_x=0,_y=0,_f=0,_g=0,_h=0,_px=0,_py=0)
        x=_x
        y=_y
        f=_f
        g=_g
        h=_h
        px=_px
        py=_py
    End Method
End Class
Class closedlist
    Field x:Int
    Field y:Int
    Field px:Int
    Field py:Int 
    Method New(_x,_y,_px,_py)
        x = _x
        y = _y
        px = _px
        py = _py
    End Method
End Class
Class pathnode
    Field x:Int
    Field y:Int
    Method New(_x,_y)
        x = _x
        y = _y
    End Method
End Class

Global ol:List&lt;openlist&gt; = New List&lt;openlist&gt;
Global cl:List&lt;closedlist&gt; = New List&lt;closedlist&gt;
Global path:List&lt;pathnode&gt; = New List&lt;pathnode&gt;

Class MyGame Extends App

    Method OnCreate()
        SetUpdateRate(1)
        For Local i=0 Until mapwidth
            map[i] = New Int[mapheight]
            olmap[i] = New Int[mapheight]
            clmap[i] = New Int[mapheight]
        Next
        makemap
    End Method
    Method OnUpdate()        
        sx = Rnd(mapwidth)
        sy = Rnd(mapheight)
        ex = Rnd(mapwidth)
        ey = Rnd(mapheight)
        findpath
    End Method
    Method OnRender()
        Cls 0,0,0 
        SetColor 255,255,255
        drawmap        
        drawpath
        SetColor 255,255,255
        DrawText "a Star Pathfinding example",0,0
    End Method
End Class

Function findpath:Bool()
    If sx = ex And sy = ey Then Return False
    For Local y=0 Until mapheight
    For Local x=0 Until mapwidth
        olmap[x][y] = 0
        clmap[x][y] = 0
    Next
    Next
    ol.Clear
    cl.Clear
    path.Clear
    ol.AddFirst(New openlist(sx,sy))
    Local tx:Int
    Local ty:Int
    Local tf:Int
    Local tg:Int
    Local th:Int
    Local tpx:Int
    Local tpy:Int
    Local newx:Int
    Local newy:Int
    Local lowestf:Int
    olmap[sx][sy] = 1
    Repeat
        If ol.IsEmpty() = True Then Return False
        lowestf = 100000
        For Local i:=Eachin ol
            If i.f &lt; lowestf
                lowestf = i.f
                tx = i.x
                ty = i.y
                tf = i.f
                tg = i.g
                th = i.h
                tpx = i.px
                tpy = i.py
            End If
        Next
        If tx = ex And ty = ey
            cl.AddLast(New closedlist(tx,ty,tpx,tpy))
            findpathback
            Return True
        Else
            removefromopenlist(tx,ty)
            olmap[tx][ty] = 0
            clmap[tx][ty] = 1
            cl.AddLast(New closedlist(tx,ty,tpx,tpy))
            For Local y=-1 To 1
            For Local x=-1 To 1
                newx = tx+x
                newy = ty+y
                If newx&gt;=0 And newy&gt;=0 And newx&lt;mapwidth And newy&lt;mapheight
                If olmap[newx][newy] = 0
                If clmap[newx][newy] = 0
                    olmap[newx][newy] = 1
                    Local gg = map[newx][newy]+1
                    Local hh = distance(newx,newy,ex,ey)
                    Local ff = gg+hh
                    ol.AddLast(New openlist(newx,newy,ff,gg,hh,tx,ty))
                End If
                End If
                End If
            Next
            Next
        End If
    Forever
    Return False
End Function

Function drawpath:Void()
    Local cnt:Int=1
    For Local i:=Eachin path
        SetColor 255,255,0
        DrawOval i.x*tilewidth,i.y*tileheight,4,4
        SetColor 255,255,255
        DrawText cnt,i.x*tilewidth,i.y*tileheight
        cnt+=1
    Next
End Function

Function findpathback:Bool()
    Local x=ex
    Local y=ey
    path.AddFirst(New pathnode(x,y))
    Repeat
        For Local i:=Eachin cl
            If i.x = x And i.y = y
                x = i.px
                y = i.py
                path.AddFirst(New pathnode(x,y))
            End If
        Next
        If x = sx And y = sy Then Return True
    Forever    
End Function

Function removefromopenlist:Void(x1:Int,y1:Int)
    For Local i:=Eachin ol
        If i.x = x1 And i.y = y1
            ol.Remove i
            Exit
        End If
    Next
End Function

Function drawmap:Void()
    For Local y=0 Until mapheight
    For Local x=0 Until mapwidth
        SetColor 0,map[x][y]*10,0
        DrawRect x*tilewidth,y*tileheight,tilewidth,tileheight
    Next
    Next
End Function

Function makemap:Void()
    For Local y=0 Until mapheight
    For Local x=0 Until mapwidth
        map[x][y] = 0
    Next
    Next
    Local lowest = 0
    While lowest &lt; 13
        Local x1 = Rnd(mapwidth)
        Local y1 = Rnd(mapheight)
        Local radius = Rnd(3,6)
        For Local y2=-radius To radius
        For Local x2=-radius To radius
            If ((x2*x2)+(y2*y2)) &lt;= radius*radius+radius*0.8
                Local x3 = x1+x2
                Local y3 = y1+y2
                If x3&gt;=0 And y3&gt;=0 And x3&lt;mapwidth And y3&lt;mapheight
                    map[x3][y3]=map[x3][y3]+1
                    If map[x3][y3] &gt; lowest Then lowest = map[x3][y3]
                End If
            End If
        Next
        Next
    Wend
End Function

Function distance:Int(x1:Int,y1:Int,x2:Int,y2:Int)
    Return Abs(x2-x1)+Abs(y2-y1)
End Function

Function Main()
    New MyGame()
End Function
</textarea> <br><br></td></tr></table><br>
<a name="2098593"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MikeHart</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I would say it is a bug. Everything after the FOREVER statement seems not to be translated. <br><br></td></tr></table><br>
<a name="2098594"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pakz</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I will put it in the bug forum. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
