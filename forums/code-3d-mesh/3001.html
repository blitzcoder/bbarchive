<!DOCTYPE html><html lang="en" ><head ><title >SetTextureAlphaWithThreshold() function</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Set the alpha value for the pixels in a texture based on a color level threshold., language=bb, category=3D Graphics - Mesh'><meta name='author' content='Zethrax'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=12>3D Graphics - Mesh</a>/SetTextureAlphaWithThreshold() function</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="3001.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >SetTextureAlphaWithThreshold() function by Zethrax</td><td align="right">2012 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This function basically allows you to set a color threshold value to be used to determine which of two possible pixel alpha values to apply to each pixel in a texture. If the color level of a pixel is above this threshold then the high color alpha value will be used to set its alpha. If the color level of a pixel is below or equal to this threshold then the low color alpha value will be used to set its alpha.<br><br>This can be useful if you have dark areas on a texture that you want to assign a particular alpha value to while still allowing the color areas to stand out.<br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Function SetTextureAlphaWithThreshold( texture, threshold, low_color_alpha, high_color_alpha )
	; Set the alpha value for the pixels in a texture based on a color level threshold.

	; 'texture' is the texture to set the alpha values for.

	; 'threshold' is the pixel color value above which the 'high_color_alpha' value is used
	; to set the pixel's alpha. If the threshold value isn't exceeded then the 'low_color_alpha'
	; value is used to set the pixel's alpha (Range: 0 to 255).

	; 'low_color_alpha' is the alpha value that will be used if a pixel's color value is below
	; or equal to the threshold (Range: 0 to 255).

	; 'high_color_alpha' is the alpha value that will be used if a pixel's color value is above
	; the threshold (Range: 0 to 255).

	; This function doesn't return anything.

	; Note that this will only work if the specified texture has the alpha flag set.

	texbuffer = TextureBuffer( texture )
	max_x = TextureWidth( texture ) - 1
	max_y = TextureHeight( texture ) - 1
	int_bank = CreateBank( 4 )
	For x = 0 To max_x
		For y = 0 To max_y
			PokeInt int_bank, 0, ReadPixel( x, y, texbuffer )
			If ( PeekByte( int_bank, 0 ) &gt; threshold ) Or ( PeekByte( int_bank, 1 ) &gt; threshold ) Or ( PeekByte( int_bank, 2 ) &gt; threshold )
				PokeByte int_bank, 3, high_color_alpha
			Else
				PokeByte int_bank, 3, low_color_alpha
			EndIf
			WritePixel x, y, PeekInt( int_bank, 0 ), texbuffer
		Next
	Next
	FreeBank int_bank
End Function


; *** DEMO ***


; Declare texture flag constants.
Const C_TEX_COLOR = 1 ; Color texture
Const C_TEX_ALPHA = 2 ; Alpha texture (Include alpha channel data)
Const C_TEX_MASK = 4 ; Masked texture (black is transparent)
Const C_TEX_MIPMAP = 8 ; Create texture mipmaps
Const C_TEX_CLAMPU = 16 ; Restrict U texture coords from "bleeding over"
Const C_TEX_CLAMPV = 32 ; Restrict V texture coords from "bleeding over"
Const C_TEX_ENVSHPERE = 64 ; Load texture as a spherical environment map
Const C_TEX_VRAM = 256 ; Force texture graphics to vram
Const C_TEX_HIGHCOLOR = 512 ; Forces texture graphics to be 32-bits per pixel


Graphics3D 800, 600, 0, 2
HidePointer

ClearTextureFilters


item_title$ = "Wizarding World of Harry Potter Hermione Rotating Time Turner 14k Gold Necklace."



; Create the image for the title text texture.
; Note that ebay listing titles have an 80 character limit.
title_image = CreateImage( 700, 22 )
SetBuffer ImageBuffer( title_image )
Color 254, 211, 41
Text 350, 11, item_title$, True, True
ResizeImage title_image, 1024, 32
SetBuffer ImageBuffer( title_image )
Color 255, 128, 0
Line 0, 0, 1023, 0
Line 0, 1, 1023, 1
Line 0, 30, 1023, 30
Line 0, 31, 1023, 31


title_texture = CreateTexture( 1024, 32, C_TEX_COLOR + C_TEX_ALPHA + C_TEX_MIPMAP )
CopyRect 0, 0, 1024, 32, 0, 0, ImageBuffer( title_image ), TextureBuffer( title_texture )
FreeImage title_image


;*********************************************************


SetTextureAlphaWithThreshold( title_texture, 64, 32, 255 )


;*********************************************************


; Create the title text tube mesh.
; Circumference = Pi * Diameter
; Diameter = Circumference / Pi
; The formula for the length of each side is: d * Tan( 180 / k), where d = inside diameter and k = number of sides.
; <a href="http://forum.onlineconversion.com/showthread.php?t=13436" target="_blank">http://forum.onlineconversion.com/showthread.php?t=13436</a>
Global G_base_title_mesh = CreateCylinder( 16, False ) ; The tube part of the cylinder is surface index 1.
ScaleMesh G_base_title_mesh, 0.5, 0.5, 0.5
ScaleMesh G_base_title_mesh, 1.5, 0.14824, 1.5
UpdateNormals G_base_title_mesh
HideEntity G_base_title_mesh

Global G_ebay_tube_surface = GetSurface( G_base_title_mesh, 1 )
Global G_max_ebay_tube_vert_index = CountVertices( G_ebay_tube_surface ) - 1

title_brush = CreateBrush()
BrushTexture title_brush, title_texture
BrushFX title_brush, 16

title_pivot = CreatePivot()
title_ring = CopyEntity( G_base_title_mesh, title_pivot )
PaintEntity title_ring, title_brush
PositionEntity title_pivot, 0.0, 1.6, 4.0


SetBuffer BackBuffer()


; -- Viewport.
Global viewport_center_x = GraphicsWidth () / 2
Global viewport_center_y = GraphicsHeight () / 2
;^^^^^^


; -- Mouselook.
Global mouselook_x_inc# = 0.4 ; This sets both the sensitivity and direction (+/-) of the mouse on the X axis.
Global mouselook_y_inc# = 0.4 ; This sets both the sensitivity and direction (+/-) of the mouse on the Y axis.
Global mouse_left_limit = 250 ; Used to limit the mouse movement to within a certain number of pixels (250 is used here) from the center of the screen. This produces smoother mouse movement than continuously moving the mouse back to the center each loop.
Global mouse_right_limit = GraphicsWidth () - 250 ; As above.
Global mouse_top_limit = 250 ; As above.
Global mouse_bottom_limit = GraphicsHeight () - 250 ; As above.
;^^^^^^

; -- Mouse smoothing que.
Global mouse_x_speed_1#
Global mouse_x_speed_2#
Global mouse_x_speed_3#
Global mouse_x_speed_4#
Global mouse_x_speed_5#
Global mouse_y_speed_1#
Global mouse_y_speed_2#
Global mouse_y_speed_3#
Global mouse_y_speed_4#
Global mouse_y_speed_5#
;^^^^^^

; -- User.
Global user_movement_speed# = 6.0 ; The user's movement speed.
Global user_camera_pitch#
Const USER_CENTERPOINT_HEIGHT# = 0.94
Const USER_CAMERA_HEIGHT# = USER_CENTERPOINT_HEIGHT# * 2.0 - 0.1
;^^^^^^


; -- Timing.
Global milli_secs ; Holds the value of the Millisecs() timer. Set at the start of the main loop.
Global old_time ; This must be set to the current 'MilliSecs()' time at the start of a new game and when returning from a pause.
Global game_time ; This holds a relative game time value which is used with timeouts so that they can be paused.
Global Delta_Time# ; Use this as a multiplier for all continuous game world events, to regulate game speed.
;^^^^^^

; -- Collision.
Const COLLTYPE_geometry = 1
Const COLLTYPE_user = 2
;^^^^^^


; == Setup ==

; -- Create user entity.
Global user = CreatePivot()
PositionEntity user, 0.0, USER_CENTERPOINT_HEIGHT#, 0.0, True
EntityRadius user, USER_CENTERPOINT_HEIGHT#
EntityType user, COLLTYPE_user
;^^^^^^


; -- Create a light;
light = CreateLight( 1 )
RotateEntity light, 45.0, 45.0, 0.0, True
;^^^^^^

; -- Create user's camera.
Global user_camera = CreateCamera( user )
CameraRange user_camera, 0.5, 500.0
CameraClsColor user_camera, 0.0, 106.0, 213.0
CameraZoom user_camera, 1.6 ; Set the camera focus to the correct value for the human eye.
PositionEntity user_camera, 0.0, USER_CAMERA_HEIGHT#, 0.0, True
;^^^^^^

; Create HUD camera.
Global hudcam = CreateHudCamera( -1, 0, -1000.0, 0 )

; -- Create user's gun.
Global gun = CreateCube()
ScaleMesh gun, 0.05, 0.05, 0.8
PositionMesh gun, 0.0, 0.0, 0.8
UpdateNormals gun
EntityParent gun, hudcam
PositionEntity gun, 0.3, -0.3, 0.3
;^^^^^^

; -- Create some meshes to use as a movement reference.
Global sphere = CreateSphere ()
UpdateNormals sphere
PositionEntity sphere, 15.0, 1.0, 0.0, True
EntityType sphere, COLLTYPE_geometry
Global cone = CreateCone ()
UpdateNormals cone
PositionEntity cone, -15.0, 1.0, 0.0, True
EntityType cone, COLLTYPE_geometry
Global cube = CreateCube ()
UpdateNormals cube
PositionEntity cube, 0.0, 1.0, 15.0, True
EntityType cube, COLLTYPE_geometry
Global cylinder = CreateCylinder ()
UpdateNormals cylinder
PositionEntity cylinder, 0.0, 1.0, -15.0, True
EntityType cylinder, COLLTYPE_geometry
	; -- Create grid texture.
	Color 255, 255, 255
	Global grid_2d_tex = CreateTexture ( 256, 256, 11 )
	SetBuffer TextureBuffer ( grid_2d_tex )
	For i = 0 To 4
		Rect i, i, 256 - i - i, 256 - i - i, False
	Next
	For y = 5 To 250
		For x = 5 To 250
			WritePixel x, y, 0
		Next
	Next
	SetBuffer BackBuffer ()
	;^^^^^^
	; -- Create 2D grid.
	Global grid_2D = CreatePlane ()
	EntityType grid_2D, COLLTYPE_geometry
	EntityTexture grid_2D, grid_2d_tex
	EntityFX grid_2D, 9
	;^^^^^^
;^^^^^^


InitializeNewMap()

; Code to translate and setup all map entities should go here.

InitializeInputAndGraphics()

; == Main Code ==


; -- Initialize the loop.
old_time = MilliSecs ()
Collisions COLLTYPE_user, COLLTYPE_geometry, 2, 2
;^^^^^^

; -- Main loop.
While Not KeyHit ( 1 ) ; Exit the main loop if the escape key is pressed.
	CalculateDeltatimeAndGametime()
	MouseLook()
	MoveUser()
	UpdateWorld
	RenderWorld

	; -- Frames Per Second counter.
	; Place into the main program loop where it will be executed each frame. The variables used here do not need to be declared globlly as this will occur automatically if the routine is placed in the main program. The value of 'milli_secs' should be set from the 'MilliSecs()' Blitz function at the start of the main program loop.
	FPS_framecounter = FPS_framecounter + 1
	If milli_secs &gt;= FPS_timeout
		FPS_timeout = milli_secs + 1000
		FPS_framecount = FPS_framecounter
		FPS_framecounter = 0
	EndIf
	Text 10, 10, "FPS"
	Text 40, 10, FPS_framecount
	;^^^^^^

	Text 10, 30, "Use W, A, S, D, SPACEBAR, LEFT-SHIFT to move and ESCAPE to exit."

	TurnEntity title_ring, 0.0, -30.0 * Delta_Time#, 0.0

	VWait ; Use this along with 'Flip False' so that the user can't disable the wait for vertical blank.
	Flip False

	Delay ( 1 ) ; Lets Windows do whatever housekeeping it needs to. Useful if the user is downloading something at the same time as playing a game, etc. Not sure if it's required for a windowed game.

Wend
;^^^^^^

End


; == Functions ==


Function MouseLook()

	; -- Update the smoothing que to smooth the movement of the mouse.
	mouse_x_speed_5# = mouse_x_speed_4#
	mouse_x_speed_4# = mouse_x_speed_3#
	mouse_x_speed_3# = mouse_x_speed_2#
	mouse_x_speed_2# = mouse_x_speed_1#
	mouse_x_speed_1# = MouseXSpeed ( )
	mouse_y_speed_5# = mouse_y_speed_4#
	mouse_y_speed_4# = mouse_y_speed_3#
	mouse_y_speed_3# = mouse_y_speed_2#
	mouse_y_speed_2# = mouse_y_speed_1#
	mouse_y_speed_1# = MouseYSpeed ( )
	the_yaw# = ( ( mouse_x_speed_1# + mouse_x_speed_2# + mouse_x_speed_3# + mouse_x_speed_4# + mouse_x_speed_5# ) / 5.0 ) * mouselook_x_inc#
	the_pitch# = ( ( mouse_y_speed_1# + mouse_y_speed_2# + mouse_y_speed_3# + mouse_y_speed_4# + mouse_y_speed_5# ) / 5.0 ) * mouselook_y_inc#
	;^^^^^^

	TurnEntity user, 0.0, -the_yaw#, 0.0 ; Turn the user on the Y (yaw) axis.
	user_camera_pitch# = user_camera_pitch# + the_pitch#
	; -- Limit the user's camera to within 180 degrees of pitch rotation. 'EntityPitch()' returns useless values so we need to use a variable to keep track of the camera pitch.
	If user_camera_pitch# &gt; 90.0 Then user_camera_pitch# = 90.0
	If user_camera_pitch# &lt; -90.0 Then user_camera_pitch# = -90.0
	;^^^^^^
	RotateEntity user_camera, user_camera_pitch#, 0.0, 0.0 ; Pitch the user's camera up and down.
	
	RotateEntity hudcam, EntityPitch# ( user_camera, True ), EntityYaw# ( user, True ), 0.0, True
	
	; -- Limit the mouse's movement. Using this method produces smoother mouselook movement than centering the mouse each loop.
	If ( MouseX() &gt; mouse_right_limit ) Or ( MouseX() &lt; mouse_left_limit ) Or ( MouseY() &gt; mouse_bottom_limit ) Or ( MouseY() &lt; mouse_top_limit )
		MoveMouse viewport_center_x, viewport_center_y
	EndIf
	;^^^^^^

End Function


Function MoveUser()

	If KeyDown( 32 ) Or KeyDown( 205 ) ; Right. The 'D' and 'CURSOR RIGHT' keys.
		x#=1.0
	Else If KeyDown( 30 ) Or KeyDown( 203 ) ; Left. The 'A' and 'CURSOR LEFT' keys.
		x#=-1.0
	EndIf

	If KeyDown( 57 ) Or KeyDown( 54 ) ; Up. The 'SPACEBAR' and 'RIGHT SHIFT' keys.
		y#=1.0
	Else If KeyDown( 42 ) Or KeyDown( 157 ) ; Down. The 'LEFT SHIFT' and 'RIGHT CONTROL' keys.
		y#=-1.0
	EndIf

	If KeyDown( 17 ) Or KeyDown( 200 ) ; Forward. The 'W' and 'CURSOR UP' keys.
		z#=1.0
	Else If KeyDown( 31 ) Or KeyDown( 208 ) ; Backward. The 'S' and 'CURSOR DOWN' keys.
		z#=-1.0
	EndIf

	MoveEntity user, x# * user_movement_speed# * Delta_Time#, y# * user_movement_speed# * Delta_Time#, z# * user_movement_speed# * Delta_Time# ; Move the user.

End Function


Function CalculateDeltatimeAndGametime()
; NOTES:
; The variable 'old_time' must be set to the current millisecs time at the start of a new game and when returning from a pause.

	milli_secs = MilliSecs () ; Store the 'MilliSecs ()' time in the 'milli_secs' variable so that you can use the stored time value without calling 'MilliSecs ()' again in this loop.

	the_time_taken = milli_secs - old_time ; Calculate the time the last loop took to execute.
	If the_time_taken &gt; 100 Then the_time_taken = 100 ; This stops disk accesses and the like from causing jumps in position.
	game_time = game_time + the_time_taken ; Calculate the value for the 'game_time' variable used with timeouts, etc.
	Delta_Time# = the_time_taken / 1000.0 ; Calculate the value for the 'Delta_Time#' variable used to regulate game speed.
	old_time = milli_secs ; Update the 'old_time' variable with the current time.

End Function


Function InitializeNewMap()
; NOTES:
 ; This function should only need to be run when a new map is first loaded. It should normally be executed by the map file loader routine.

	; -- Precache the game's 3D graphics media. This forces the media to be uploaded to the video card. This should be done before any of the media is hidden or has its alpha set to zero to turn off rendering. Note that the camera will end up pointing back to where it started, so there is no need to reset its orientation.
	For i = 1 To 4
		TurnEntity user_camera, 0.0, 90.0, 0.0, True
		RenderWorld
	Next
	;^^^^^^
	
End Function


Function InitializeInputAndGraphics()
; NOTES:
 ; This function should be run immediately before a game session begins and also after resuming from a pause.

	; *** At this point everything should be setup and ready to start/restart the game immediately. ***

	; Reset the input devices.
	MoveMouse viewport_center_x, viewport_center_y
	FlushMouse
	FlushKeys 
	MouseXSpeed()
	MouseYSpeed()
	MouseHit(1)
	MouseHit(2)
	MouseHit(3)

	; Set and render the backbuffer, and then flip it to the frontbuffer.
	SetBuffer BackBuffer()
	RenderWorld
	Flip
	
End Function


Function CreateHudCamera( order, x#, y#, z# )
; USAGE:
; Creates a camera to be used for a heads-up display.

; PARAMETERS:
; order - This sets the drawing order of the camera. 'order' should be a negative number, so that the HUD camera is drawn on top of the main camera graphics. The highest value negative number will be drawn last (eg. The '-2' camera will be drawn after the '-1' camera.).

; x#, y#, z# - Set the camera's absolute world position. The position should set so that the camera and its associated 3D graphic objects do not interfere with any other cameras.

; RETURNS:
; The new HUD camera's entity handle is returned.


	Local cam = CreateCamera()
	PositionEntity cam, x#, y#, z#, True ; 
	CameraRange cam, 0.2, 5.0
	CameraZoom cam, 1.6
	EntityOrder cam, order
	CameraClsMode cam, False, True ; This prevents the background graphics from being overwritten by the camera's clearscreen color.
	Return cam
End Function

</textarea> </td></tr><tr ><td class="cell"><pre class="code">; The code is at: http://www.blitzbasic.com/codearcs/codearcs.php?code=3001

; And by the way, codeboxes should be an option for code in the code archives.</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
