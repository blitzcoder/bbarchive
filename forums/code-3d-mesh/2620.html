<!DOCTYPE html><html lang="en" ><head ><title >Texture Splatting on Meshterrain</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Creates transparent mesh Layers with different textures, language=bb, category=3D Graphics - Mesh'><meta name='author' content='Krischan'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=12>3D Graphics - Mesh</a>/Texture Splatting on Meshterrain</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2620.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Texture Splatting on Meshterrain by Krischan</td><td align="right">2009 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This function creates a mesh terrain layer using a heightmap, a colormap and a alphamap for transparency (up to 128x128 vertices/pixel). All black pixels (0,0,0) of the alphamap are ignored. The trick is to create a full nontransparent base layer with FX2 and add the transparent FX2+32 mesh layers on top of it which results in nice faded transitions between two textures.<br><br>Here is a running demo showing the effect. With Keys 1-5 you can show/hide the base layer and the 4 transparent layers. Moving with arrows and Mouse, LMB/RMB = 2x faster, SPACE = Wireframe:<br><br><img src="http://www.christianhart.de/bb/texturesplatting/texturesplatting.jpg"><br><br><a href="http://www.christianhart.de/bb/texturesplatting/texturesplatting.zip" target="_blank">Download</a><br><br>Code of the demo (all images are 128x128 pixel except the textures):<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics3D 800,600,32,2

Include "texture_splatting.bb"


; Player movement speed
Global PlayerSpeed#=0.5


; which layers to show initially?
Global sl1%=1
Global sl2%=1
Global sl3%=1
Global sl4%=1
Global sl5%=1


; Frametweening
Global GameSpeed%=60
Global Screenwidth%=GraphicsWidth()
Global Screenheight%=GraphicsHeight()
Global FramePeriod%=1000/GameSpeed
Global FrameTime%=MilliSecs()-FramePeriod


; move variables
Global Camera,SpeedMultiplicator%,DeltaTimeOld


; world
Global Pivot%,Licht%


; central pivot
Pivot=CreatePivot()


; Camera
Camera=CreateCamera()
PositionEntity Camera,-32,64,-64
CameraZoom Camera,1
CameraRange Camera,0.1,256
CameraFogMode Camera,True
CameraFogRange Camera,96,192
CameraFogColor Camera,155,174,206

CameraClsColor Camera,155,174,206
PointEntity Camera,Pivot


; Light
Licht=CreateLight(2)
PositionEntity Licht,10000,10000,10000
LightRange Licht,20000
AmbientLight 16,16,16


; Load maps
hm=LoadImage("maps/hm.png")						; Heightmap
lm=LoadImage("maps/lm.png")						; Lightmap
cm=LoadImage("maps/cm.png")						; Colormap

; Load Alpha maps
alpha_snow=LoadImage("maps/alpha_snow.png")		; Alphamap Snow
alpha_rock=LoadImage("maps/alpha_rock.png")		; Alphamap Rock
alpha_grass=LoadImage("maps/alpha_grass.png")	; Alphamap Grass
alpha_road=LoadImage("maps/alpha_road.png")		; Alphamap Road
alpha_smile=LoadImage("maps/alpha_smile.png")	; Alphamap Smile


; create Meshterrain
Terrain1=LoadMeshTerrain(hm,16,cm   ,1.0,alpha_grass,False,False,False,True) 	: EntityFX Terrain1,2
Terrain2=LoadMeshTerrain(hm,16,lm   ,1.0,alpha_rock)							: EntityFX Terrain2,2+32
Terrain3=LoadMeshTerrain(hm,16,False,0.0,alpha_snow ,255,255,255)				: EntityFX Terrain3,2+32
Terrain4=LoadMeshTerrain(hm,16,False,1.0,alpha_road ,  0,255,  0)				: EntityFX Terrain4,2+32
Terrain5=LoadMeshTerrain(hm,16,False,0.0,alpha_smile,192,192,128)				: EntityFX Terrain5,2+32


; Load Textures
tex1=MyLoadTexture("textures/grass.jpg"  ,0,2.0/ 32,1.0/ 32,5,Terrain1)
tex2=MyLoadTexture("textures/rock.jpg"   ,0,1.0/ 32,1.0/ 32,2,Terrain2)
tex3=MyLoadTexture("textures/snow.jpg"   ,0,1.0/ 16,1.0/ 16,2,Terrain3)
tex4=MyLoadTexture("textures/road.jpg"   ,0,1.0/128,1.0/128,5,Terrain4)
tex5=MyLoadTexture("textures/crystal.jpg",0,1.0/  4,1.0/  4,1,Terrain5)


; Apply detail texture
detail=MyLoadTexture("textures/detail.jpg" ,0,1.0/ 64,1.0/ 64,5)
EntityTexture terrain1,detail,0,2
EntityTexture terrain2,detail,0,2
EntityTexture terrain3,detail,0,2
EntityTexture terrain4,detail,0,2
EntityTexture terrain5,detail,0,2


; centered pointer
MoveMouse GraphicsWidth()/2,GraphicsHeight()/2


; check which layers to show/hide
If sl1=0 Then HideEntity terrain1 Else ShowEntity terrain1
If sl2=0 Then HideEntity terrain2 Else ShowEntity terrain2
If sl3=0 Then HideEntity terrain3 Else ShowEntity terrain3
If sl4=0 Then HideEntity terrain4 Else ShowEntity terrain4
If sl5=0 Then HideEntity terrain5 Else ShowEntity terrain5


; Main loop
While Not KeyHit(1)
	
	Cls
	
	; Frametween calculation
	Local FrameElapsed%,FrameTicks%,FrameTween#,t%
	Repeat FrameElapsed=MilliSecs()-FrameTime Until FrameElapsed
	FrameTicks=FrameElapsed/FramePeriod
	FrameTween=Float(FrameElapsed Mod FramePeriod)/Float(FramePeriod)
	
	; Frametween loop
	For t=1 To FrameTicks
		
		; Frametween Captureworld
		FrameTime=FrameTime+FramePeriod : If t=FrameTicks Then CaptureWorld
	
		; Space = Wireframe on/off
		If KeyHit(57) Then wf%=1-wf : WireFrame wf
		
		; Key 1-5 = show/hide Layer 1-5
		If KeyHit(2) Then sl1=1-sl1 : If sl1=0 Then HideEntity terrain1 Else ShowEntity terrain1
		If KeyHit(3) Then sl2=1-sl2 : If sl2=0 Then HideEntity terrain2 Else ShowEntity terrain2
		If KeyHit(4) Then sl3=1-sl3 : If sl3=0 Then HideEntity terrain3 Else ShowEntity terrain3
		If KeyHit(5) Then sl4=1-sl4 : If sl4=0 Then HideEntity terrain4 Else ShowEntity terrain4
		If KeyHit(6) Then sl5=1-sl5 : If sl5=0 Then HideEntity terrain5 Else ShowEntity terrain5
		
		; Movement
		FreeCam()
		
		UpdateWorld
		
	Next
	
	; Frametween Render
	RenderWorld FrameTween
	
	Text 0,0,TrisRendered()
	
	; Bufferflip
	Flip 0
	
Wend

End


; customized Loadtexture Function
Function MyLoadTexture(filename$,flags%,uscale#=1.0,vscale#=1.0,blendmode%=False,entity%=False,layer%=0,frame%=0)
	
	Local tex%=LoadTexture(filename$,flags)
	ScaleTexture tex,uscale,vscale
	If blendmode Then TextureBlend tex,blendmode
	If entity Then EntityTexture entity,tex,frame,layer
	
	Return tex
	
End Function


; Freecam Movement
Function FreeCam(maxpitch#=85.0,rotspeed#=16.666,rotfloat#=8.0)
	
	Local movex#,movez#,dx#,dy#,dk#,dt%,t%
	Local movespeed#,pitch#
	
	SpeedMultiplicator=1
	
	; LMB/RMB = 2x faster
	If MouseDown(1) Or MouseDown(2) Then SpeedMultiplicator=2
	
	; Arrows = Move
	movex=KeyDown(205)-KeyDown(203)
	movez=KeyDown(200)-KeyDown(208)
	
	; Movespeed
	movespeed=PlayerSpeed*SpeedMultiplicator
	
	; smooth movement
	t=MilliSecs() : dt=t-DeltaTimeOld : DeltaTimeOld=t : dk=Float(dt)/rotspeed
	dx=(Screenwidth/2-MouseX())*0.01*dk : dy=(Screenheight/2-MouseY())*0.01*dk
	TurnEntity Camera,-dy,dx*0.1*dk*rotfloat,0
	
	; limit pitch
	pitch=EntityPitch(Camera,1) : If pitch&gt;maxpitch Then pitch=maxpitch Else If pitch&lt;-maxpitch Then pitch=-maxpitch
	
	; rotate and move
	RotateEntity Camera,pitch,EntityYaw(Camera,1),0,1	
	MoveEntity Camera,movex*movespeed,0,movez*movespeed
	
End Function</textarea> </td></tr><tr ><td class="cell"><pre class="code">Type triangle
	
	Field v%[2]
	Field alpha#
	
End Type

Global alphavertex#[65536]


; create Meshterrain
Function LoadMeshTerrain(hmap%,vscale#=16.0,cmap%,cscale#=1.0,amap%,r1%=False,g1%=False,b1%=False,base%=False)
	
	Local rgb%,r%,g%,b%,a#
	Local h#,x%,y%,vertex%
	Local vx#,vz#,u#,v#
	Local v0%,v1%,v2%,v3%
	
	; Heightmap
	If hmap Then
		Local hbuf%=ImageBuffer(hmap)
		Local size%=ImageWidth(hmap)-1
		Local verts%=size+1
	EndIf
	
	; Colormap
	If cmap Then 
		Local cbuf%=ImageBuffer(cmap)
		Local cwidth%=ImageWidth(cmap)
		Local cfactor#=cwidth*1.0/verts
	EndIf
	
	; Alphamap
	If amap Then Local abuf%=ImageBuffer(amap)
	
	; create the Mesh
	Local mesh%=CreateMesh()
	Local surf%=CreateSurface(mesh)
	
	; lock buffers
	LockBuffer hbuf
	If cmap Then LockBuffer cbuf
	If amap Then LockBuffer abuf
	
	For y=0 To size
		
		For x=0 To size
			
			; read height (only red channel) and normalize it
			h=Normalize((ReadPixelFast(x,size-y,hbuf) And $ff0000)/$10000,0,255,0.0,vscale)
			
			; read vertexcolor from colormap (R,G,B)
			If cmap Then
				rgb=ReadPixelFast(x*cfactor,(size-y)*cfactor,cbuf)
				r=Int(((rgb And $ff0000)/$10000)*cscale)
				g=Int(((rgb And $ff00)/$100)*cscale)
				b=Int((rgb And $ff)*cscale)
			Else
				r=128
				g=128
				b=128
			EndIf
			
			; read alpha value from alphamap (only red channel)
			If amap Then
				a=Normalize((ReadPixelFast(x,size-y,abuf) And $ff0000)/$10000,0,255,0,1)
			Else
				a=1.0
			EndIf
			
			; use forced colors if used
			If r1 Then r=r1
			If g1 Then g=g1
			If b1 Then b=b1
			
			; calculate vertex coordinates / texture coordinates
			vx=x-(size/2.0)
			vz=y-(size/2.0)
			u=x*1.0/size
			v=(size-y)*1.0/size
			
			; place vertex
			vertex=AddVertex(surf,vx,h,vz,u,v)
			
			; set vertex color and texture coordinates
			VertexColor surf,vertex,r,g,b,a
			
			; build alpha blitzarray for alpha check later
			alphavertex[vertex]=a
			
			; set triangles
			If y&lt;size And x&lt;size Then
				
				v0=x+((size+1)*y)
				v1=x+((size+1)*y)+(size+1)
				v2=(x+1)+((size+1)*y)
				v3=(x+1)+((size+1)*y)+(size+1)
				
				; add first triangle
				t.triangle = New triangle
				t\v[0]=v0
				t\v[1]=v1
				t\v[2]=v2
				
				; add second triangle
				t.triangle = New triangle
				t\v[0]=v2
				t\v[1]=v1
				t\v[2]=v3
				
			EndIf
			
		Next
		
	Next
	
	; flag all 100% transparent triangles (just add transparency values, higher than 0 = not transparent :-)
	For t.triangle = Each triangle
		
		t\alpha=alphavertex[t\v[0]]+alphavertex[t\v[1]]+alphavertex[t\v[2]]
		
	Next
	
	; create triangles
	For t.triangle = Each triangle
		
		; base layer = draw ALL triangles
		If base=True Then
			
			AddTriangle surf,t\v[0],t\v[1],t\v[2]
			
		Else
			
			; if they are not transparent
			If t\alpha&gt;0 Then
				
				AddTriangle surf,t\v[0],t\v[1],t\v[2]
				
			EndIf
			
		EndIf
		
		; delete triangle from to do list
		Delete t.triangle
		
	Next
	
	; unlock buffers
	If amap Then UnlockBuffer abuf
	If cmap Then UnlockBuffer cbuf
	UnlockBuffer hbuf
	
	UpdateNormals mesh
	
	Return mesh
	
End Function


; Normalize a value
Function Normalize#(value#=128.0,value_min#=0.0,value_max#=255.0,norm_min#=0.0,norm_max#=1.0)
	
	Return ((value-value_min)/(value_max-value_min))*(norm_max-norm_min)+norm_min
	
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RifRaf</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> Brilliant, thanks for sharing. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> Nice, works well here. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Beaker</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> My Samsung NC10 has a problem.  At certain angles and distances the grass layer pops in and out of existence showing the background color. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> Beaker, try it this way: I revised it and it works with only one mesh now, gaining 10% speed. Just replace the texture_splatting.bb with this one and run demo2.bb (or download the whole package again).<br><br><a href="http://www.christianhart.de/bb/texturesplatting/texturesplatting2.zip" target="_blank">Download Singlemesh-Version</a><br><br><b>demo2.bb</b><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics3D 800,600,32,2

Include "texture_splatting.bb"


; Player movement speed
Global PlayerSpeed#=0.5


; Frametweening
Global GameSpeed%=60
Global Screenwidth%=GraphicsWidth()
Global Screenheight%=GraphicsHeight()
Global FramePeriod%=1000/GameSpeed
Global FrameTime%=MilliSecs()-FramePeriod


; move variables
Global Camera,SpeedMultiplicator%,DeltaTimeOld


; world
Global Pivot%,Light%


; central pivot
Pivot=CreatePivot()


; Camera
Camera=CreateCamera()
PositionEntity Camera,-32,64,-64
CameraZoom Camera,1
CameraRange Camera,0.1,256
CameraFogMode Camera,True
CameraFogRange Camera,96,192
CameraFogColor Camera,155,174,206

CameraClsColor Camera,155,174,206
PointEntity Camera,Pivot


; Light
Light=CreateLight(2)
PositionEntity Light,10000,10000,10000
LightRange Light,20000
AmbientLight 16,16,16



; Load maps
hm=LoadImage("maps/hm.png")	; Heightmap
lm=LoadImage("maps/lm.png")	; Lightmap
cm=LoadImage("maps/cm.png")	; Colormap


; create two meshes
mesh=CreateMesh() : h#=24 ; height multiplier


; create Meshterrain (returns surface indexes) - the order must be descending top&gt;down with the base layer on very top
grass	= LoadMeshTerrain(mesh,hm,h,cm,1.0,LoadImage("maps/alpha_grass.png"),False,False,False,True)
smile	= LoadMeshTerrain(mesh,hm,h,False,1.0,LoadImage("maps/alpha_smile.png"),192,192,128)
road	= LoadMeshTerrain(mesh,hm,h,False,1.0,LoadImage("maps/alpha_road.png"),0,255,0)
snow	= LoadMeshTerrain(mesh,hm,h,False,1.0,LoadImage("maps/alpha_snow.png"),255,255,255)
rock	= LoadMeshTerrain(mesh,hm,h,lm,1.0,LoadImage("maps/alpha_rock.png"))


; apply textures to surface indexes
ApplyTexture("textures/grass.jpg"		,0,1.0/ 32,1.0/ 32,5,False,grass	,2)
ApplyTexture("textures/crystal.jpg"		,0,1.0/  4,1.0/  4,1,False,smile	,2+32)
ApplyTexture("textures/road.jpg"		,0,1.0/128,1.0/128,5,False,road		,2+32)
ApplyTexture("textures/rock.jpg"		,0,1.0/ 32,1.0/ 32,2,False,rock		,2+32)
ApplyTexture("textures/snow.jpg"		,0,1.0/ 16,1.0/ 16,2,False,snow		,2+32)


; Apply detail texture to meshes
detail=LoadTexture("textures/detail.jpg") : TextureBlend detail,5 : ScaleTexture detail,1.0/64,1.0/64
EntityTexture mesh,detail,0,1


; centered pointer
MoveMouse GraphicsWidth()/2,GraphicsHeight()/2


; Main loop
While Not KeyHit(1)
	
	Cls
	
	; Frametween calculation
	Local FrameElapsed%,FrameTicks%,FrameTween#,t%
	Repeat FrameElapsed=MilliSecs()-FrameTime Until FrameElapsed
	FrameTicks=FrameElapsed/FramePeriod
	FrameTween=Float(FrameElapsed Mod FramePeriod)/Float(FramePeriod)
	
	; Frametween loop
	For t=1 To FrameTicks
		
		; Frametween Captureworld
		FrameTime=FrameTime+FramePeriod : If t=FrameTicks Then CaptureWorld
	
		; Space = Wireframe on/off
		If KeyHit(57) Then wf%=1-wf : WireFrame wf
		
		; Movement
		FreeCam()
		
		UpdateWorld
		
	Next
	
	; Frametween Render
	RenderWorld FrameTween
	
	Text 0,0,TrisRendered()
	
	; Bufferflip
	Flip 0
	
Wend

End


; customized Loadtexture Function
Function ApplyTexture(filename$,flags%,uscale#=1.0,vscale#=1.0,blendmode%=False,mesh%,surf%=False,fx%=False)
	
	Local tex%=LoadTexture(filename$,flags)
	ScaleTexture tex,uscale,vscale
	If blendmode Then TextureBlend tex,blendmode
	
	Local brush%=CreateBrush(255,255,255)
	If tex Then BrushTexture brush,tex
	If fx Then BrushFX brush,fx
	
	If surf Then
		PaintSurface surf,brush
	Else 
		surf=CreateSurface(mesh)
		PaintSurface surf,brush
	EndIf
	
	FreeBrush brush
	
	Return tex
	
End Function


; Freecam Movement
Function FreeCam(maxpitch#=85.0,rotspeed#=16.666,rotfloat#=8.0)
	
	Local movex#,movez#,dx#,dy#,dk#,dt%,t%
	Local movespeed#,pitch#
	
	SpeedMultiplicator=1
	
	; LMB/RMB = 2x faster
	If MouseDown(1) Or MouseDown(2) Then SpeedMultiplicator=2
	
	; Arrows = Move
	movex=KeyDown(205)-KeyDown(203)
	movez=KeyDown(200)-KeyDown(208)
	
	; Movespeed
	movespeed=PlayerSpeed*SpeedMultiplicator
	
	; smooth movement
	t=MilliSecs() : dt=t-DeltaTimeOld : DeltaTimeOld=t : dk=Float(dt)/rotspeed
	dx=(Screenwidth/2-MouseX())*0.01*dk : dy=(Screenheight/2-MouseY())*0.01*dk
	TurnEntity Camera,-dy,dx*0.1*dk*rotfloat,0
	
	; limit pitch
	pitch=EntityPitch(Camera,1) : If pitch&gt;maxpitch Then pitch=maxpitch Else If pitch&lt;-maxpitch Then pitch=-maxpitch
	
	; rotate and move
	RotateEntity Camera,pitch,EntityYaw(Camera,1),0,1	
	MoveEntity Camera,movex*movespeed,0,movez*movespeed
	
End Function</textarea><br><br><b>texture_splatting.bb</b><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Type triangle
	
	Field v%[2]
	Field alpha#
	
End Type

Global alphavertex#[65536]


; create Meshterrain
Function LoadMeshTerrain(mesh%,hmap%,vscale#=16.0,cmap%,cscale#=1.0,amap%,r1%=False,g1%=False,b1%=False,base%=False)
	
	Local rgb%,r%,g%,b%,a#
	Local h#,x%,y%,vertex%
	Local vx#,vz#,u#,v#
	Local v0%,v1%,v2%,v3%
	
	; Heightmap
	If hmap Then
		Local hbuf%=ImageBuffer(hmap)
		Local size%=ImageWidth(hmap)-1
		Local verts%=size+1
	EndIf
	
	; Colormap
	If cmap Then 
		Local cbuf%=ImageBuffer(cmap)
		Local cwidth%=ImageWidth(cmap)
		Local cfactor#=cwidth*1.0/verts
	EndIf
	
	; Alphamap
	If amap Then Local abuf%=ImageBuffer(amap)
	
	; create the Mesh
	Local surf%=CreateSurface(mesh)
	
	; lock buffers
	LockBuffer hbuf
	If cmap Then LockBuffer cbuf
	If amap Then LockBuffer abuf
	
	For y=0 To size
		
		For x=0 To size
			
			; read height (only red channel) and normalize it
			h=Normalize((ReadPixelFast(x,size-y,hbuf) And $ff0000)/$10000,0,255,0.0,vscale)
			
			; read vertexcolor from colormap (R,G,B)
			If cmap Then
				rgb=ReadPixelFast(x*cfactor,(size-y)*cfactor,cbuf)
				r=Int(((rgb And $ff0000)/$10000)*cscale)
				g=Int(((rgb And $ff00)/$100)*cscale)
				b=Int((rgb And $ff)*cscale)
			Else
				r=128
				g=128
				b=128
			EndIf
			
			; read alpha value from alphamap (only red channel)
			If amap Then
				a=Normalize((ReadPixelFast(x,size-y,abuf) And $ff0000)/$10000,0,255,0,1)
			Else
				a=1.0
			EndIf
			
			; use forced colors if used
			If r1 Then r=r1
			If g1 Then g=g1
			If b1 Then b=b1
			
			; calculate vertex coordinates / texture coordinates
			vx=x-(size/2.0)
			vz=y-(size/2.0)
			u=x*1.0/size
			v=(size-y)*1.0/size
			
			; place vertex
			vertex=AddVertex(surf,vx,h,vz,u,v)
			
			; set vertex color and texture coordinates
			VertexColor surf,vertex,r,g,b,a
			
			; build alpha blitzarray for alpha check later
			alphavertex[vertex]=a
			
			; set triangles
			If y&lt;size And x&lt;size Then
				
				v0=x+((size+1)*y)
				v1=x+((size+1)*y)+(size+1)
				v2=(x+1)+((size+1)*y)
				v3=(x+1)+((size+1)*y)+(size+1)
				
				; add first triangle
				t.triangle = New triangle
				t\v[0]=v0
				t\v[1]=v1
				t\v[2]=v2
				
				; add second triangle
				t.triangle = New triangle
				t\v[0]=v2
				t\v[1]=v1
				t\v[2]=v3
				
			EndIf
			
		Next
		
	Next
	
	; flag all 100% transparent triangles (just add transparency values, higher than 0 = not transparent :-)
	For t.triangle = Each triangle
		
		t\alpha=alphavertex[t\v[0]]+alphavertex[t\v[1]]+alphavertex[t\v[2]]
		
	Next
	
	; create triangles
	For t.triangle = Each triangle
		
		; base layer = draw ALL triangles
		If base=True Then
			
			AddTriangle surf,t\v[0],t\v[1],t\v[2]
			
		Else
			
			; if they are not transparent
			If t\alpha&gt;0 Then
				
				AddTriangle surf,t\v[0],t\v[1],t\v[2]
				
			EndIf
			
		EndIf
		
		; delete triangle from to do list
		Delete t.triangle
		
	Next
	
	; unlock buffers
	If amap Then UnlockBuffer abuf
	If cmap Then UnlockBuffer cbuf
	UnlockBuffer hbuf
	
	UpdateNormals mesh
	
	Return surf
	
End Function


; Normalize a value
Function Normalize#(value#=128.0,value_min#=0.0,value_max#=255.0,norm_min#=0.0,norm_max#=1.0)
	
	Return ((value-value_min)/(value_max-value_min))*(norm_max-norm_min)+norm_min
	
End Function</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Beaker</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> Unfortunately, still the same problem. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> This is really very useful, thanks Krishan!<br><br>Compared to many other examples of multi-textured terrains this is the easiest to use, whilst still giving great results! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> Beaker: I dunno, perhaps the Intel GMA950 doesn't allow meshes displayed in this way - you could experiment with the order the meshes/surfaces are drawn. Here on my nVidia 9600M GT it works fine.<br><br>To top it - I experimented with normal maps to dramatically increase the detail level of the poor 128x128 vertex resolution and here is my solution, again texture splatted. I got it to work only using two meshes - one multitextured and one with the normalmap put on top of it. Lighting is a combination of vertex light and dot3 lighting.<br><br>You can change the sunlight angle (Y axis, 0...360°) with the Keys Q/W and the sun inclination angle with A/S (-90 to 90°). With the keys 1/2 you can show/hide the multitextured mesh and the normal map mesh to see the difference!<br><br>Beware: i am using a very hires 2048x2048 normal map texture here, so resize it if it doesn't work!<br><br><a href="http://www.christianhart.de/bb/texturesplatting/texturesplatting_normals.zip" target="_blank">Download</a><br><br><img src="http://www.christianhart.de/bb/texturesplatting/texturesplatting_normals.jpg"><br><br><b>demo_normals.bb</b> (the include is the same file like in my single-mesh-version)<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics3D 800,600,32,2


Include "texture_splatting.bb"


; Player movement speed
Global PlayerSpeed#=0.1
Global inclination#=60
Global sunangle#=0.0


; Frametweening
Global GameSpeed%=60
Global Screenwidth%=GraphicsWidth()
Global Screenheight%=GraphicsHeight()
Global FramePeriod%=1000/GameSpeed
Global FrameTime%=MilliSecs()-FramePeriod


; move variables
Global Camera,SpeedMultiplicator%,DeltaTimeOld


; world
Global Mesh1%,Mesh2%,Pivot%,Light%,Sun%


; central pivot
Pivot=CreatePivot()


; Camera
Camera=CreateCamera()
PositionEntity Camera,-56,12,-28
CameraZoom Camera,1
CameraRange Camera,0.1,1024
CameraFogMode Camera,True
CameraFogRange Camera,96,192
CameraFogColor Camera,155,174,206
CameraClsColor Camera,155,174,206
PointEntity Camera,Pivot
EntityType Camera,1
EntityRadius Camera,0.15

; Light
Sun=CreateSphere(8,Pivot)
EntityFX Sun,1+8
EntityColor Sun,255,255,0
ScaleEntity Sun,20,20,20
PositionEntity Sun,512,0,0
Light=CreateLight(2,Sun)
PositionEntity Light,0,0,0
LightRange Light,1024
AmbientLight 64,64,64
LightColor Light,128,128,128


; Load maps
hm=LoadImage("maps/hm.png")	; Heightmap
cm=LoadImage("maps/cm.png")	; Colormap


; create multitextured mesh
Mesh1=CreateMesh() : h#=24 ; height multiplier


; create normals mesh to put on top
Mesh2=CreateMesh()
EntityBlend Mesh2,2
EntityType Mesh2,2

; create Meshterrain (returns surface indexes) - the order must be descending top&gt;down with the base layer on very top
grass1lush	= LoadMeshTerrain(Mesh1,hm,h,cm,1.0,LoadImage("maps/1_grass1-lush.png"),False,False,False,True)
rock1b		= LoadMeshTerrain(Mesh1,hm,h,False,1.0,LoadImage("maps/2_rock1b.png"))
grass1		= LoadMeshTerrain(Mesh1,hm,h,False,1.0,LoadImage("maps/3_grass1.png"))
rock1		= LoadMeshTerrain(Mesh1,hm,h,False,1.0,LoadImage("maps/4_rock1.png"))
bump		= LoadMeshTerrain(Mesh2,hm,h,False,1.0,False)


; apply textures to surface indexes
ApplyTexture("textures/grass1-lush.jpg"	,0,1.0/ 32,1.0/ 32,2,False,grass1lush,2)
ApplyTexture("textures/rock1b.jpg"		,0,1.0/ 32,1.0/ 32,5,False,rock1b	,2+32)
ApplyTexture("textures/grass1.jpg"		,0,1.0/ 32,1.0/ 32,5,False,grass1	,2+32)
ApplyTexture("textures/rock1.jpg"		,0,1.0/ 32,1.0/ 32,5,False,rock1	,2+32)
ApplyTexture("maps/nm.jpg"				,0,1.0/  1,1.0/  1,4,False,bump,1)


; Apply detail texture to normals mesh
detail=LoadTexture("textures/detail.jpg") : TextureBlend detail,5 : ScaleTexture detail,1.0/64,1.0/64
EntityTexture Mesh2,detail,0,1


; centered pointer
MoveMouse GraphicsWidth()/2,GraphicsHeight()/2


Collisions 1,2,2,3

; Main loop
While Not KeyHit(1)
	
	Cls
	
	; Frametween calculation
	Local FrameElapsed%,FrameTicks%,FrameTween#,t%
	Repeat FrameElapsed=MilliSecs()-FrameTime Until FrameElapsed
	FrameTicks=FrameElapsed/FramePeriod
	FrameTween=Float(FrameElapsed Mod FramePeriod)/Float(FramePeriod)
	
	Local wf%,sl1%,sl2%
	
	; Frametween loop
	For t=1 To FrameTicks
		
		; Frametween Captureworld
		FrameTime=FrameTime+FramePeriod : If t=FrameTicks Then CaptureWorld
	
		; Space = Wireframe on/off
		If KeyHit(57) Then wf%=1-wf : WireFrame wf
		
		If KeyHit(2) Then sl1=1-sl1 : If sl1=1 Then HideEntity Mesh1 Else ShowEntity Mesh1
		If KeyHit(3) Then sl2=1-sl2 : If sl2=1 Then HideEntity Mesh2 Else ShowEntity Mesh2
		
		; Key Q/W: light angle
		If KeyDown(16) Then sunangle=sunangle+1 : If sunangle&gt;359 Then sunangle=0
		If KeyDown(17) Then sunangle=sunangle-1 : If sunangle&lt;0 Then sunangle=359
		
		; Key A/S: increase / decrease inclination
		If KeyDown(30) Then inclination=inclination+1 : If inclination&gt;90 Then inclination=90
		If KeyDown(31) Then inclination=inclination-1 : If inclination&lt;-90 Then inclination=-90
		
		; turn central pivot
		RotateEntity Pivot,0,sunangle,inclination
		
		; color normals mesh according to light position
		EntityColor Mesh2,(Cos(sunangle)+1)/2.0*255,(Sin(sunangle)+1)/2.0*255,Normalize(inclination,-90,90,0,255)
		
		; Movement
		FreeCam()
		
		UpdateWorld
		
	Next
	
	; Frametween Render
	RenderWorld FrameTween
	
	Text 0, 0,"Tris rendered..: "+TrisRendered()
	Text 0,15,"Sun Angle......: "+sunangle+"°"
	Text 0,30,"Sun Inclination: "+inclination+"°"
	
	; Bufferflip
	Flip 0
	
Wend

End


; customized Loadtexture Function
Function ApplyTexture(filename$,flags%,uscale#=1.0,vscale#=1.0,blendmode%=False,mesh%,surf%=False,fx%=False)
	
	Local tex%=LoadTexture(filename$,flags)
	ScaleTexture tex,uscale,vscale
	If blendmode Then TextureBlend tex,blendmode
	
	Local brush%=CreateBrush(255,255,255)
	If tex Then BrushTexture brush,tex
	If fx Then BrushFX brush,fx
	
	If surf Then
		PaintSurface surf,brush
	Else 
		surf=CreateSurface(mesh)
		PaintSurface surf,brush
	EndIf
	
	FreeBrush brush
	
	Return tex
	
End Function


; Freecam Movement
Function FreeCam(maxpitch#=85.0,rotspeed#=16.666,rotfloat#=8.0)
	
	Local movex#,movez#,dx#,dy#,dk#,dt%,t%
	Local movespeed#,pitch#
	
	SpeedMultiplicator=1
	
	; LMB/RMB = 2x faster
	If MouseDown(1) Or MouseDown(2) Then SpeedMultiplicator=2
	
	; Arrows = Move
	movex=KeyDown(205)-KeyDown(203)
	movez=KeyDown(200)-KeyDown(208)
	
	; Movespeed
	movespeed=PlayerSpeed*SpeedMultiplicator
	
	; smooth movement
	t=MilliSecs() : dt=t-DeltaTimeOld : DeltaTimeOld=t : dk=Float(dt)/rotspeed
	dx=(Screenwidth/2-MouseX())*0.01*dk : dy=(Screenheight/2-MouseY())*0.01*dk
	TurnEntity Camera,-dy,dx*0.1*dk*rotfloat,0
	
	; limit pitch
	pitch=EntityPitch(Camera,1) : If pitch&gt;maxpitch Then pitch=maxpitch Else If pitch&lt;-maxpitch Then pitch=-maxpitch
	
	; rotate and move
	RotateEntity Camera,pitch,EntityYaw(Camera,1),0,1	
	MoveEntity Camera,movex*movespeed,0,movez*movespeed
	
End Function</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RifRaf</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> I like the method, but is this texturesplatting? since you are still using vertex alpha for the transparency and not really a blendmode of textures. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
