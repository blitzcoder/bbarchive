<!DOCTYPE html><html lang="en" ><head ><title >iOS Xcode project (Anyone Willing II) ?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >iOS Xcode project (Anyone Willing II) ?</h1><a href="forums.php" >Monkey Targets Forums</a>/<a href="topics.php?forum=505" >iOS</a>/<a href="#bottom" >iOS Xcode project (Anyone Willing II) ?</a><br><br>
<a name="2083259"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >nivrig</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm currently evaluating Monkey (primarily for iOS) so I only have the free version.<br><br>Assuming it wouldn't give away any Pro secrets, would someone be willing to share an iOS Xcode project produced from Monkey so I can have a poke at it? One of the simple demo projects would be ideal.<br><br>Thanks :) <br><br></td></tr></table><br>
<a name="2084916"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >APC</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello sixecho,<br><br><br>This is my implementation of the Forces from the book "The Nature of Code" by Daniel Shiffman, <a href="http://natureofcode.com/book/chapter-2-forces/" target="_blank">Click here!</a><br>It is an implementation of vector forces that are attracted by an object. You can move the object with the mouse, by clicking on it.<br><br>Since you have a demo version of Monkey X you will not be able to compile this to run on your Xcode and iOS devices, but if you provide and e-mail address so I can send you the Xcode project compiled, so you can see how powerful Monkey is!<br><br>You still can compile on the free HTML 5 and then compare with the Xcode later.<br><br><b><u>This is the main Class</u></b><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Import mojo
Import pvector  ' !!!!! ------ Import classes need lowercase names ------ !!!!!!
Import mover
Import attractor

Class Forces2_7 Extends App
	Field width:Float = 0 '= DeviceWidth()
	Field height:Float = 0 '= DeviceHeight()
	
	Field m:Mover
	Field a:Attractor
	Field movers:Mover[10]
	'Field liquid:Liquid
	
	Field touching:Int
	Field touchmen:Int
	Field touchhit:Int
	Field touchX:Float
	Field touchY:Float
	
	Field accX:Float
	Field accY:Float
	Field accZ:Float

	Method OnCreate:Int()
		SetUpdateRate(60)

		width = DeviceWidth()
		height = DeviceHeight()
		If width = 0
			width = 640
		End 
		If height = 0
			height = 360
		End
		'm = New Mover(width, height)
		For Local i:= 0 Until movers.Length
			movers[i] = New Mover(Rnd(0.1, 2), Rnd(width), Rnd(height), width, height)
		End
		a = New Attractor(width, height)
		
		Return True
	End
	Method OnRender:Int()
		Cls(255, 255, 255)
		
		a.display()
		a.drag()
		a.hover(MouseX(), MouseY())
		
		For Local i:= 0 Until movers.Length
			Local force:PVector = New PVector(0, 0)
    		force = a.attract(movers[i])
			movers[i].applyForce(force)

			movers[i].update()
			movers[i].display1()

		End

		Return True
	End
	Method OnUpdate:Int()
		input()	' get User Touch screen or mouse
		If touching &gt; 0
		''Print "Drag"
			a.clicked(MouseX(), MouseY())
		Else
		''Print "Stop Dragging"
			a.stopDragging()
		End
		Return True
	End
	Method input:Int()

		touchhit = -1
	
		'For Local i:Int = 0 Until 64
		If TouchDown(0) And touchmen = 0
			touching = 1
			touchmen = 1
		End
		If
			TouchDown(0) = 0 And touchmen = 1	'Touch UP
			touching = 0
			touchmen = 0
		End

		'Print "Touching:" + touching + " touchmen:" + touchmen + " TD:" + TouchDown(0)
		'Next
		touchhit = TouchHit()

		touchX = TouchX(0)
		touchY = TouchY(0)
		accX = AccelX()
		accY = AccelY()
		accZ = AccelZ()
		Return True
	End
	Method reset:Void()
		For Local i:= 0 Until movers.Length
			movers[i] = New Mover(Rnd(0.5 * 2.25, 3 * 2.25), 20 * 2.25 + i * 40 * 2.25, 0.0, width, height)
		End	
	End
End

Function Main:Int()
	New Forces2_7()
	Return true
End
</textarea><br><br><b><u>This is PVector class</u></b><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Class PVector
 	Field x:Float
	Field y:Float
	
	Method New(x_:Float = 0, y_:Float = 0)
		x = x_
    	y = y_
	End
	Method New(v:PVector)
		x = v.x
    	y = v.y
	End
	'A function to add another PVector to this PVector. Simply add the x components and the y components together.
	Method get:PVector()
        Return New PVector(x, y)
	End
	Method add:Int(v:PVector)
	    y = y + v.y
	    x = x + v.x
		Return True
	End
	Method add:PVector(v1:PVector, v2:PVector)
		Return New PVector(v1.x - v2.x, v1.y - v2.y)
	End
	Function sub:PVector(A:PVector, B:PVector)
  		Return New PVector(A.x - B.x, A.y - B.y)
	End Function
	Method sub:Int(v:PVector)
   		x = x - v.x
    	y = y - v.y
		Return True
	End
'	Method sub:PVector(v1:PVector, v2:PVector)
'		Return New PVector(v1.x - v2.x, v1.y - v2.y)
'	End
	Method mult:Int(n:float)
		x = x * n
		y = y * n
		Return True
	End
	Method div:Int(n:float)
		x = x / n
		y = y / n
		Return True
	End
	Method div:PVector(v1:PVector, n:float)
		Return New PVector(v1.x / n, v1.y / n)
	End
	Method mag:Float()
  		Return Sqrt(x * x + y * y)
	End
	Method normalize:Void()
		Local m:Float = mag()
  		If (m &lt;&gt; 0)
			div(m)
		End
	End
	Method limit:Void(high:Float)
		If (Self.mag() &gt; high)
          normalize()
          mult(high)
		End
	End
End
</textarea><br><br><b><u>This is Mover class</u></b><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Import mojo
Import pvector

Class Mover
 	'Our object has two PVectors: location and velocity.
	Field location:PVector
	Field velocity:PVector
	Field acceleration:PVector
	Field f:PVector
	Field g:Float = 0.4
	Field mass:Float
	Field width:Float
	Field height:Float
	
	Field d:Float = 10	' Diameter
	Field r:Float = d / 2	' Radius
	
 
	Method New(m:Float, x:Float, y:Float, w:Float, h:Float)
		width = w
		height = h
		mass = m
		location = New PVector(x, y)
	    velocity = New PVector(0, 0)
	    acceleration = New PVector(0, 0)
	End
	Method New(w:Float, h:Float)
		width = w
		height = h
		'location = New PVector(w / 2, h / 2)
		location = New PVector(400, 50)
	    velocity = New PVector(1, 0)
	    acceleration = New PVector(0, 0)
	    mass = 1
	End
	Method applyForce:Void(force:PVector)
		'Newton's 2nd law: F = M * A or A = F / M
		f = New PVector(0, 0)
		'Divide by mass 
		f = f.div(force, mass)
		'Accumulate all forces in acceleration
    	acceleration.add(f)
	End
	Method update:Void()
		'Velocity changes according to acceleration
    	velocity.add(acceleration);
    	'Location changes by velocity
    	location.add(velocity)
		'We must clear acceleration each frame
		acceleration.mult(0)
	End
	Method display:Void()
	    SetAlpha(0.8)
		SetColor(50, 50, 50)
		Circle(location.x, location.y, d, d)
		SetAlpha(0.7)
		SetColor(127, 127, 127)
	    DrawEllipse(location.x, location.y, d, d)
		SetAlpha(1.0)
	End
	Method display1:Void()
	    SetAlpha(0.8)
		SetColor(50, 50, 50)
	    'DrawEllipse(location.x, location.y, mass * d, mass * d)
		Circle(location.x, location.y, mass * d, mass * d )
		 SetAlpha(0.7)
		SetColor(127, 127, 127)
		DrawEllipse(location.x, location.y, mass * d, mass * d)
		SetAlpha(1.0)
	End
 	Method Circle:Void(x:Float, y:Float, w:Float, h:Float)
		For Local phi:Float = 0.0 Until 360.0 Step 10
			DrawLine(x + Cos(phi - 10) * w, y - Sin(phi - 10) * h, x + Cos(phi) * w, y - Sin(phi) * h)
			'DrawPoint(x + Cos(phi) * w, y - Sin(phi) * h)
		Next
	End
	Method checkEdges:Void()
	    If (location.x &gt; width - d)
			location.x = width - d
			velocity.x *= -1
	    Else If (location.x &lt; 0)
			location.x = 0
			velocity.x *= -1
	    End
 
	    If (location.y &gt; height - d)
			velocity.y *= -1
			location.y = height-d
	    End
	End
	Method checkEdges1:Void()
		If (location.y &gt; height - d)
			velocity.y *= -0.9	'A little dampening when hitting the bottom
			location.y = height-d
	    End
	End
	Method attract:PVector(m:Mover)
		Local force:PVector = PVector.sub(location, m.location)		'Calculate direction of force
	    Local distance:float = force.mag()                                 'Distance between objects
	    distance = constrain(distance, 5.0, 25.0)                    'Limiting the distance to eliminate "extreme" results for very close or very far objects
	    force.normalize()                                           'Normalize vector (distance doesn't matter here, we just want this vector for direction
	
	    Local strength:Float = (g * mass * m.mass) / (distance * distance) 'Calculate gravitional force magnitude
		force.mult(strength)                                        'Get force vector --&gt; magnitude * direction
	    Return force
	End
	Method constrain:Float(aNumber:Float, aMin:Float, aMax:Float)
		If (aNumber &gt; aMax)
			Return aMax
		ElseIf (aNumber &lt; aMin)
			Return aMin
		Else
			Return aNumber
		End
	End
End
</textarea><br><br><b><u>This is Attractor class</u></b><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict
Import pvector
Import mover

Class Attractor
  Field mass:Float				'Mass, tied To size
  Field G:Float       			'Gravitational Constant
  Field location:PVector   		'Location
  Field dragging:Bool = False	'Is the object being dragged?
  Field rollover:Bool = False	'Is the mouse over the ellipse?
  Field dragOffset:PVector  	'holds the offset for when object is clicked on

	Method New(width:Float, height:Float)
	    location = new PVector(width/2,height/2);
	    mass = 20;
	    G = 1;
	    dragOffset = new PVector(0.0,0.0);
	End
	Method attract:PVector(m:Mover)
	    'Local force:PVector = New PVector(0, 0)  'Calculate direction of force
	    'force = force.sub(location, m.location);
		Local force:PVector = PVector.sub(location, m.location) 'Calculate direction of force
		Local d:Float = force.mag();                              'Distance between objects
	    d = constrain(d, 5.0, 25.0);                        'Limiting the distance to eliminate "extreme" results for very close or very far objects
	    force.normalize();                                  'Normalize vector (distance doesn't matter here, we just want this vector for direction)
	    Local strength:Float = (G * mass * m.mass) / (d * d);     'Calculate gravitional force magnitude
	    force.mult(strength) 								'Get force vector - - &gt; magnitude * direction
	    Return force
	End
	Method display:Void()
	    SetAlpha(0.8)
		SetColor(50, 50, 50)
		Circle(location.x, location.y, mass, mass)
		
		If (dragging)
			SetColor(255, 0, 0)
	    ElseIf(rollover)
			SetColor(255, 128, 0)
	    Else
			SetColor(0, 175, 0)
		End

		SetAlpha(0.7)
		'SetColor(127, 127, 127)
	    DrawEllipse(location.x, location.y, mass, mass)
		SetAlpha(1.0)
	    ' DrawEllipse(location.x, location.y, mass , mass)
	End
	Method constrain:Float(aNumber:Float, aMin:Float, aMax:Float)
		 'aNumber &gt; aMax ? aMax : aNumber &lt; aMin ? aMin : aNumber
		 If (aNumber &gt; aMax)
			Return aMax
		ElseIf (aNumber &lt; aMin)
			Return aMin
		Else
			Return aNumber
		End
	End
	Method dist:Float(mx:Float, my:Float, lx:Float, ly:Float)
		Local dx:Float
		Local dy:Float
		Local dz:Float
	    dx = mx - lx
	    dy = my - ly
	    Return Sqrt(dx * dx + dy * dy)
	End
	'The methods below are for mouse interaction
	Method clicked:Void(mx:Int, my:Int)
		'Print "clicked mx:" + mx + "my:" + my + " location.x:" + location.x + "location.y: " + location.y
	    Local d:float = dist(mx, my, location.x, location.y)
	    If (d &lt; mass)
			'Print "clicked mx:" + mx + "my:" + my
	      dragging = true;
	      dragOffset.x = location.x - mx
	      dragOffset.y = location.y - my
	    End
	End
	Method hover:Void(mx:Int, my:Int)
	    'Print "Hovering..."
		Local d:float = dist(mx, my, location.x, location.y)
	    If (d &lt; mass)
	      rollover = True
	    Else
	      rollover = False
	    End
	End
	Method stopDragging:Void()
	    dragging = False;
	End
	Method drag:Void()
	    If (dragging)
	      location.x = MouseX() +dragOffset.x
	      location.y = MouseY() +dragOffset.y
	    End
	End
	Method Circle:Void(x:Float, y:Float, w:Float, h:Float)
		For Local phi:Float = 0.0 Until 360.0 Step 10
			DrawLine(x + Cos(phi - 10) * w, y - Sin(phi - 10) * h, x + Cos(phi) * w, y - Sin(phi) * h)
			'DrawPoint(x + Cos(phi) * w, y - Sin(phi) * h)
		Next
	End
End
</textarea> <br><br></td></tr></table><br>
<a name="2084914"></a>

<a name="2084955"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >nivrig</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for the offer, though it's the Xcode source that I'd be interested in not the compiled version. Could you send that?<br>john -at- sixecho -dot- com if so. <br><br></td></tr></table><br>
<a name="2084982"></a>

<a name="2084983"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >APC</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> sixecho<br><br>I just sent you the e-mail.<br>Let me know if it worked for you. <br><br></td></tr></table><br>
<a name="2084984"></a>

<a name="2084985"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >APC</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> s <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
