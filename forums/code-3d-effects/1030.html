<!DOCTYPE html><html lang="en" ><head ><title >Depth of Field</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Simple depth of field effect!, language=bb, category=3D Graphics - Effects'><meta name='author' content='fredborg'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=11>3D Graphics - Effects</a>/Depth of Field</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1030.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Depth of Field by fredborg</td><td align="right">2004 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> A simple depth of field effect.<br><br>Depth of field, you ask? It is a simulation of the loss of focus a camera (or your eyes) has at a range beyond its focal point.<br><br>Should be more or less plug-and-pray!<br><br>Have fun! </td></tr><tr ><td class="cell"><pre class="code">;
; Depth of field
;
; Created by Mikkel Fredborg
; Use as you please!
;
Graphics3D 800,600,0,2
SetBuffer BackBuffer()

HidePointer

;
; Create a camera...
camera = CreateCamera()
CameraRange camera,0.1,1000.0
CameraFogMode camera,True
CameraFogRange camera,100,1000

;
; create some cubes
For i = 0 To 100
	cube = CreateCube()
	PositionEntity cube,Rnd(-100,100),Rnd(-100,20),Rnd(-100,100)
	RotateEntity cube,Rnd(-180,180),Rnd(-180,180),Rnd(-180,180)
	ScaleEntity cube,Rnd(1,10),Rnd(1,10),Rnd(1,10)
Next
			
;
; Light
light = CreateLight()
RotateEntity light,90,0,0

; Depth of Field setup
Type DepthOfField
	Field layers
	Field layer[99]
	Field texture
	Field tsize
	Field tbuffer
	Field near#,far#
	Field camera
End Type

dof.DepthOfField = DOF_Create(camera,3,2.0)

Repeat
	RotateEntity camera,MouseY(),-MouseX(),0
	MoveEntity camera,KeyDown(205)-KeyDown(203),0,KeyDown(200)-KeyDown(208)

	DOF_Update(dof)
	
	RenderWorld
	
	Flip False

Until KeyHit(1)

End

Function DOF_Update(dof.depthoffield)

	HideEntity dof\layer[0]

	CameraRange dof\camera,dof\near*0.95,1000
	CameraViewport dof\camera,0,0,dof\tsize,dof\tsize
	RenderWorld
	CopyRect 0,0,dof\tsize,dof\tsize,0,0,BackBuffer(),dof\tbuffer
	
	ShowEntity dof\layer[0]

	CameraRange dof\camera,0.1,1000	
	CameraViewport dof\camera,0,0,GraphicsWidth(),GraphicsHeight()
	
End Function

Function DOF_Create.DepthOfField(camera,layers,spread#=0.0)

	dof.depthoffield = New depthoffield

	dof\camera = camera

	dof\layers = layers

	dof\tsize	 = 512
	dof\near	 = 100.0
	dof\far		 = 300.0
	
	ClearTextureFilters
	dof\texture = CreateTexture(dof\tsize,dof\tsize,1+256+16+32)
	dof\tbuffer = TextureBuffer(dof\texture)
	
	ang# = 360.0/Float(dof\layers)
	For i = 0 To dof\layers-1
		dof\layer[i] = CreateFace(1)
			
		EntityAlpha dof\layer[i],1.0/Float(dof\layers)
		EntityFX	dof\layer[i],1+8
	
		ps# = dof\near+(i*((dof\far-dof\near)/Float(dof\layers)))

		px# = Sin(i*ang)*(i/Float(dof\layers))*spread
		py# = Cos(i*ang)*(i/Float(dof\layers))*spread
		
		PositionEntity dof\layer[i],px,py,ps
		ScaleEntity dof\layer[i],ps,ps,1.0		
	
		EntityTexture dof\layer[i],dof\texture
		
		If i = 0
			EntityParent dof\layer[i],dof\camera,True
		Else
			EntityParent dof\layer[i],dof\layer[i-1],True
		End If
	Next

	Return dof

End Function

Function CreateFace(segs=1,parent=0)

	mesh=CreateMesh( parent )
	surf=CreateSurface( mesh )
	stx#=-1.0
	sty#=stx
	stp#=Float(2)/Float(segs)
	y#=sty
	For a=0 To segs
		x#=stx
		v#=a/Float(segs)
		For b=0 To segs
			u#=b/Float(segs)
			AddVertex(surf,x,-y,0,u,v) ; swap these for a different start orientation
			x=x+stp
		Next
		y=y+stp
	Next
	For a=0 To segs-1
		For b=0 To segs-1
			v0=a*(segs+1)+b:v1=v0+1
			v2=(a+1)*(segs+1)+b+1:v3=v2-1
			AddTriangle( surf,v0,v2,v1 )
			AddTriangle( surf,v0,v3,v2 )
		Next
	Next
	
	FlipMesh mesh
	UpdateNormals mesh

	Return mesh
	
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> This is extremely slow for me.<br><br>After any input, such a moving the mouse, it takes about one second before I see any movement on screen.<br><br>But it all works beautifully if I switch from windowed graphics to full screen.<br>i.e. use mode 1 in Graphics3D, and change nothing else. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >fredborg</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Switching from 'Flip False' to 'Flip True' may fix it? <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CyBeRGoth</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Brilliant!<br><br>The effect is just as good as the one in Zelda the wind waker on the GC :)<br><br>Edit<br><br>Floyd try putting the line<br><br>EnableDirectInput 1 this seems to help a lot, I had a similar problem. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Just curious John - what does that do?  My help files are not up to date.<br><br>Cheers<br>Stevie <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CyBeRGoth</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> A while ago some people were having problems with direct input, it would work ok on their PC but on other peoples the mouse, keyboard, joysyick etc would not work correctly.<br><br>That command reverts the way blitz handles input to how it was before it was changed.<br><br>seems to fix a lot of problems like this tho, another way is to use windowed mode but that is not ideal for games <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jfk EO-11110</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> this version is trying to allow camerazooms other than 1.0:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
; ID: 1030
; Author: fredborg
; Date: 2004-05-14 18:19:02
; Title: Depth of Field
; Description: Simple depth of field effect!
; Attempt to handle CameraZoom added by jfk.

;
; Depth of field
;
; Created by Mikkel Fredborg
; Use as you please!
;
Graphics3D 800,600,0,2
SetBuffer BackBuffer()

HidePointer

;
; Create a camera...
camera = CreateCamera()
CameraRange camera,0.1,1000.0
CameraFogMode camera,True
CameraFogRange camera,100,1000
Global camzoom#=0.5
CameraZoom camera,camzoom#
;
; create some cubes
For i = 0 To 100
	cube = CreateCube()
	PositionEntity cube,Rnd(-100,100),Rnd(-100,20),Rnd(-100,100)
	RotateEntity cube,Rnd(-180,180),Rnd(-180,180),Rnd(-180,180)
	ScaleEntity cube,Rnd(1,10),Rnd(1,10),Rnd(1,10)
Next
			
;
; Light
light = CreateLight()
RotateEntity light,90,0,0

; Depth of Field setup
Type DepthOfField
	Field layers
	Field layer[99]
	Field texture
	Field tsize
	Field tbuffer
	Field near#,far#
	Field camera
End Type

dof.DepthOfField = DOF_Create(camera,3,4); 3,5    3,2.0

Repeat
	RotateEntity camera,MouseY(),-MouseX(),0
	MoveEntity camera,KeyDown(205)-KeyDown(203),0,KeyDown(200)-KeyDown(208)

	DOF_Update(dof)
	
	RenderWorld
	
	Flip False

Until KeyHit(1)

End

Function DOF_Update(dof.depthoffield)

	HideEntity dof\layer[0]

	CameraRange dof\camera,dof\near*0.95*camzoom,1000
	CameraViewport dof\camera,0,0,dof\tsize,dof\tsize
	RenderWorld
	CopyRect 0,0,dof\tsize,dof\tsize,0,0,BackBuffer(),dof\tbuffer
	
	ShowEntity dof\layer[0]

	CameraRange dof\camera,0.1*camzoom,1000	
	CameraViewport dof\camera,0,0,GraphicsWidth(),GraphicsHeight()
	
End Function

Function DOF_Create.DepthOfField(camera,layers,spread#=0.0)
spread=spread*(1.0/camzoom#)
	dof.depthoffield = New depthoffield

	dof\camera = camera

	dof\layers = layers

	dof\tsize	 = 512
	dof\near	 = 100.0
	dof\far		 = 300.0
	
	ClearTextureFilters
	dof\texture = CreateTexture(dof\tsize,dof\tsize,1+256+16+32)
	dof\tbuffer = TextureBuffer(dof\texture)
	
	ang# = 360.0/Float(dof\layers)
	For i = 0 To dof\layers-1
		dof\layer[i] = CreateFace(1)
			
		EntityAlpha dof\layer[i],1.0/Float(dof\layers)
		EntityFX	dof\layer[i],1+8
	
		ps# = dof\near+(i*((dof\far-dof\near)/Float(dof\layers)))

		px# = Sin(i*ang)*(i/Float(dof\layers))*spread
		py# = Cos(i*ang)*(i/Float(dof\layers))*spread
		
		PositionEntity dof\layer[i],px*camzoom#,py*camzoom#,ps*camzoom#
;		PositionEntity dof\layer[i],px,py,ps
		ScaleEntity dof\layer[i],ps,ps,1.0		
	
		EntityTexture dof\layer[i],dof\texture
		
		If i = 0
			EntityParent dof\layer[i],dof\camera,True
		Else
			EntityParent dof\layer[i],dof\layer[i-1],True
		End If
	Next

	Return dof

End Function

Function CreateFace(segs=1,parent=0)

	mesh=CreateMesh( parent )
	surf=CreateSurface( mesh )
	stx#=-1.0
	sty#=stx
	stp#=Float(2)/Float(segs)
	y#=sty
	For a=0 To segs
		x#=stx
		v#=a/Float(segs)
		For b=0 To segs
			u#=b/Float(segs)
			AddVertex(surf,x,-y,0,u,v) ; swap these for a different start orientation
			x=x+stp
		Next
		y=y+stp
	Next
	For a=0 To segs-1
		For b=0 To segs-1
			v0=a*(segs+1)+b:v1=v0+1
			v2=(a+1)*(segs+1)+b+1:v3=v2-1
			AddTriangle( surf,v0,v2,v1 )
			AddTriangle( surf,v0,v3,v2 )
		Next
	Next
	
	FlipMesh mesh
	UpdateNormals mesh

	Return mesh
	
End Function
</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Robert Cummings</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> The problems people are experiencing are typical of "mouse lag" and can be fixed by locking and unlocking the backbuffer. What happens is the card buffers ahead in situations with high fill rate and potentially high framerate. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ben(t)</td><td align="right"><font class=tiny>2007</font></td></tr></table></td></tr><tr ><td class="posttext"> sorry if I sound stupid but what is it supposed to do? <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mustang</td><td align="right"><font class=tiny>2007</font></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>what is it supposed to do? <br> <br></div><br><br>Live and learn, there are no stupid questions... and wikipedia or google is your best friend (if these forums fail which is rare):<br><br><a href="http://en.wikipedia.org/wiki/Depth_of_field" target="_blank">http://en.wikipedia.org/wiki/Depth_of_field</a> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >virtualjesus</td><td align="right"><font class=tiny>2012</font></td></tr></table></td></tr><tr ><td class="posttext"> This not working for 640x480<br>Please help me!!! <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
