<!DOCTYPE html><html lang="en" ><head ><title >render scene or render screenmesh+screentexture</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='This shows how to create a screenmesh+screentexture (useful for fullscreen effects), language=bb, category=3D Graphics - Effects'><meta name='author' content='RemiD'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=11>3D Graphics - Effects</a>/render scene or render screenmesh+screentexture</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="3256.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >render scene or render screenmesh+screentexture by RemiD</td><td align="right">2016 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This shows how to create a screenmesh+screentexture (useful for fullscreen effects)<br>If you want to remove the blured effect on the screenmesh+screentexture, you need to disable bilinear filtering ( see : <a href="http://www.blitzbasic.com/Community/posts.php?topic=103591" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=103591</a> ) </td></tr><tr ><td class="cell"><pre class="code">Graphics3D(1000,625,32,2)

SeedRnd(MilliSecs())

Camera = CreateCamera()
CameraRange(Camera,0.1,100)
CameraClsColor(Camera,000,000,000)

Global ThingsCount%
Dim ThingMesh(1000)

For n% = 1 To 1000 Step 1
 ThingsCount = ThingsCount + 1
 I% = ThingsCount
 ThingMesh(I) = CreateCube()
 ScaleMesh(ThingMesh(I),1.0/2,1.0/2,1.0/2)
 EntityColor(ThingMesh(I),Rand(025,255),Rand(025,255),Rand(025,255))
 PositionEntity(ThingMesh(I),Rnd(-15,15),Rnd(-15,15),Rnd(-15,15),True)
Next

OLight = CreateLight(2)
LightRange(OLight,3.0)
LightColor(OLight,224,224,224)

AmbientLight(032,032,032)

ScreenMesh = CreateMesh()
Surface = CreateSurface(ScreenMesh)
AddVertex(Surface,-1.0,0.625,0.0)
VertexTexCoords(Surface,0,Float(0)/1024,Float(0)/1024)
AddVertex(Surface,1.0,0.625,0.0)
VertexTexCoords(Surface,1,Float(1000)/1024,Float(0)/1024)
AddVertex(Surface,-1.0,-0.625,0.0)
VertexTexCoords(Surface,2,Float(0)/1024,Float(625)/1024)
AddVertex(Surface,1.0,-0.625,0.0)
VertexTexCoords(Surface,3,Float(1000)/1024,Float(625)/1024)
AddTriangle(Surface,0,1,2)
AddTriangle(Surface,2,1,3)
UpdateNormals(ScreenMesh)
EntityColor(ScreenMesh,255,255,255)
EntityFX(ScreenMesh,1)

ScreenTexture = CreateTexture(1024,1024,16+32+256)
SetBuffer(TextureBuffer(ScreenTexture))
 ClsColor(255,255,255)
 Cls()
;ScaleTexture(ScreenTexture,1.0,1.0)
TextureBlend(ScreenTexture,1)
EntityTexture(ScreenMesh,ScreenTexture)

PositionEntity(Camera,0,0,-16,True)

Const CScene% = 1
Const CScreen% = 2
RenderMode% = CScene

Repeat

 MXDiff% = MouseXSpeed()
 MYDiff% = MouseYSpeed()
 MoveMouse(GraphicsWidth()/2,GraphicsHeight()/2)
 TurnEntity(Camera,MYDiff*0.1,-MXDiff*0.1,0)

 If( KeyHit(57)=1 )
  If( RenderMode = CScene )
   RenderMode = CScreen
  Else If( RenderMode = CScreen )
   RenderMode = CScene
  EndIf
 EndIf

 PositionRotateEntityLikeOtherEntity(ScreenMesh,Camera)
 MoveEntity(ScreenMesh,0,0,1.0)

 WireFrame(False)
 If( KeyDown(2)=1 )
  WireFrame(True)
 EndIf

 If( RenderMode = CScene )

  For I% = 1 To ThingsCount Step 1
   ShowEntity(ThingMesh(I))
  Next
  HideEntity(ScreenMesh)
  SetBuffer(BackBuffer())
  RenderWorld()

  Color(255,255,255)
  CText("RenderMode = Scene",0,0)

 Else If( RenderMode = CScreen )

  For I% = 1 To ThingsCount Step 1
   ShowEntity(ThingMesh(I))
  Next
  HideEntity(ScreenMesh)
  SetBuffer(BackBuffer())
  RenderWorld()
  CopyRect(0,0,GraphicsWidth(),GraphicsHeight(),0,0,BackBuffer(),TextureBuffer(ScreenTexture))

  For I% = 1 To ThingsCount Step 1
   HideEntity(ThingMesh(I))
  Next
  ShowEntity(ScreenMesh)
  SetBuffer(BackBuffer())
  RenderWorld()

  Color(255,255,255)
  CText("RenderMode = ScreenMesh+ScreenTexture",0,0)

 EndIf

 Flip(1)

Until( KeyDown(1)=1 )

End()

Function CText(TextStr$,PX%,PY%)
 Text(PX,PY,TextStr,False,False)
End Function

Function PositionEntityLikeOtherEntity(Entity,OEntity)
 PositionEntity(Entity,EntityX(OEntity,True),EntityY(OEntity,True),EntityZ(OEntity,True),True)
End Function

Function RotateEntityLikeOtherEntity(Entity,OEntity)
 RotateEntity(Entity,EntityPitch(OEntity,True),EntityYaw(OEntity,True),EntityRoll(OEntity,True),True)
End Function

Function PositionRotateEntityLikeOtherEntity(Entity,OEntity)
 PositionEntity(Entity,EntityX(OEntity,True),EntityY(OEntity,True),EntityZ(OEntity,True),True)
 RotateEntity(Entity,EntityPitch(OEntity,True),EntityYaw(OEntity,True),EntityRoll(OEntity,True),True)
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bobysait</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> SetBuffer(TextureBuffer(ScreenTexture))<br>You should "SetBuffer backBuffer" once the texture is ready<br><br>Whatever, the filtering you're speaking of about bilinear filtering, it's just that you can't set a texture like this if you want a "pixel perfect" texture. You don't need to remove texture filtering, it's just a matter of UV settings.<br>&gt; Texels on texture are centered at half pixels (not at 0,0) which makes the texture blury if you start the UV at 0,0.<br><br>BTW "Float(0)/1024" ... that's 0<br><br>So, offset your UVs with half a texel and it will appears exactly like a direct renderworld<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Local TexSize%	=	1024
Local halfPixel#=	0.5/TexSize;
Local u0# = halfPixel
Local u1# = u0 + Float(GraphicsWidth())/TexSize
Local v0# = halfPixel
Local v1# = v0 + Float(GraphicsHeight())/TexSize
Local ScreenMesh=	CreateMesh		( )
Local Surface	=	CreateSurface	( ScreenMesh )
					AddVertex		( Surface, -1.0,0.625, 0.0, u0, v0 );
					AddVertex		( Surface, 1.0,0.625,0.0  , u1, v0 )
					AddVertex		( Surface, -1.0,-0.625,0.0, u0, v1 )
					AddVertex		( Surface, 1.0,-0.625,0.0 , u1, v1 )
					AddTriangle		( Surface, 0,1,2 )
					AddTriangle		( Surface, 2,1,3 )
					UpdateNormals	( ScreenMesh )
					EntityColor		( ScreenMesh, 255,255,255 )
					EntityFX		( ScreenMesh, 1 )

</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>You should "SetBuffer backBuffer" once the texture is ready<br> <br></div><br>There is no need to, since i don't need to draw on the backbuffer at this time...<br><br><br>About the pixel perfect texture same as the render image, yes i have noticed a slight distortion but it is not really important (but thanks for the example), especially if the screenmesh+screentexture is used to create a fullscreen effect like precise glow of some pixels (of some surfaces)<br>That's the purpose of this code and this other code :<br><a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=3257" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=3257</a> <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
