<!DOCTYPE html><html lang="en" ><head ><title >Milkyway Panorama</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Creates a stunning realistic lowpoly milkyway background object, language=bb, category=3D Graphics - Effects'><meta name='author' content='Krischan'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=11>3D Graphics - Effects</a>/Milkyway Panorama</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2753.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Milkyway Panorama by Krischan</td><td align="right">2010 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> My God - it's full of stars! This include creates a nice lowpoly milkyway background with a 360° panorama emission nebula, fixed stars and background stars combined. It uses a splitted texture where the upper part represents the panorama from 0-180° and the lower part from 180-360° put on an open cylinder object.<br><br><b>Screenshot</b><br><img src="http://www.christianhart.de/bb/milkyway/milkyway.jpg"><br><br>Here is a demo how to use, just save the code entry as "milkyway.bb" and run this, you can switch between two different milkyways using the LMB and fly around with the arrow keys:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics3D 800,600,32,2

Include "milkyway.bb"

Const MoveSpeed#=1

Global WIDTH%=GraphicsWidth()
Global HEIGHT%=GraphicsHeight()
Global TIMER%=CreateTimer(60)

Global CAM%,MILKYWAY1%,MILKYWAY2%,SUNTEX%,SUN%,SUN0%,SUN1%,SUN2%,SUN3%

CAM=CreateCamera()
CameraRange CAM,0.1,1000
PositionEntity CAM,0,0,0

; Example 1: Milkyway with 500 fixed stars, 80% up to 10° and 20% from 10° to 40°, red style, normal brightness
MILKYWAY1=InitMilkyway(500,0.2,10,40,500,0,0,0,0,255,192,128,1,0,0,0,0)

; Example 2: Milkyway with 5000 fixed stars, 50% up to 5° and 50% from 5° to 90° blue style, overbright
MILKYWAY2=InitMilkyway(5000,0.5,5,90,500,0,0,0,1,128,192,255,1,0,0,0,1)

InitSun()
PositionEntity SUN,-20,1,0

PointEntity CAM,SUN

MoveMouse WIDTH/2,HEIGHT/2
HidePointer

; -----------------------------------------------------------------------------
; main loop
; -----------------------------------------------------------------------------

While Not KeyHit(1)
	
	Local mw%,wf%
	
	Movement(CAM)
	
	If KeyHit(57) Then wf=1-wf : WireFrame wf
	
	; LMB = switch between Milkyways
	If MouseHit(1) Then mw=1-mw
	
	If mw=0 Then
		
		ShowEntity MILKYWAY1
		HideEntity MILKYWAY2
		
	Else
		
		HideEntity MILKYWAY1
		ShowEntity MILKYWAY2
		
	EndIf
	
	; uncomment to keep milkyway fixed to cam
	;PositionEntity MILKYWAY1,EntityX(CAM),EntityY(CAM),EntityZ(CAM)
	;PositionEntity MILKYWAY2,EntityX(CAM),EntityY(CAM),EntityZ(CAM)
	
	PointEntity SUN,CAM
	
	RenderWorld
	
	WaitTimer TIMER
	
	Flip 0
	
Wend

End

; -----------------------------------------------------------------------------
; move camera
; -----------------------------------------------------------------------------

Function Movement(cam)
	
	Local mxs#=MouseXSpeed()
	Local mys#=MouseYSpeed()
	Local pitch#=EntityPitch(cam)+(mys#/5)
	
	If pitch&gt;89.9 Then pitch=89.9 Else If pitch&lt;-89.9 Then pitch=-89.9
	
	RotateEntity cam,pitch,EntityYaw(cam)-(mxs#/5),0
	MoveEntity cam,(KeyDown(205)-KeyDown(203))*MoveSpeed,0,(KeyDown(200)-KeyDown(208))*MoveSpeed
	MoveMouse WIDTH/2,HEIGHT/2
	
End Function

; -----------------------------------------------------------------------------
; create a simple but stunning sun
; -----------------------------------------------------------------------------

Function InitSun(size#=1.0)
	
	SUNTEX=CreateSunTexture()
	SUN=CreatePivot()
	SUN0=CreateQuad(SUN,size*1.0,SUNTEX,3,1+8,255,255,255,1.00)
	SUN1=CreateQuad(SUN,size*1.5,SUNTEX,3,1+8,255,192,128,1.00)
	SUN2=CreateQuad(SUN,size*3.0,SUNTEX,3,1+8,255,255,224,0.75)
	SUN3=CreateQuad(SUN,size*6.0,SUNTEX,3,1+8,255,255,224,0.50)
	
End Function

; -----------------------------------------------------------------------------
; create a sun texture
; -----------------------------------------------------------------------------

Function CreateSunTexture()
	
	Local tex%=CreateTexture(512,512,3)
	Local tb%=TextureBuffer(tex)
	
	Local i#,j%,col%,rgb%
	
	SetBuffer tb
	LockBuffer tb
	
	For j=0 To 255
		
		col=255-j
		If col&gt;255 Then col=255
		rgb=col*$1000000+col*$10000+col*$100+col
		
		For i=0 To 360 Step 0.1
			
			WritePixelFast 256+(Sin(i)*j),256+(Cos(i)*j),rgb,tb
			
		Next
		
	Next
	
	UnlockBuffer tb
	SetBuffer BackBuffer()
	
	Return tex
	
End Function

; -----------------------------------------------------------------------------
; create a sun quad
; -----------------------------------------------------------------------------

Function CreateQuad(parent%=False,scale#=1.0,tex%=False,blend%=False,fx%=False,r%=255,g%=255,b%=255,a#=1.0)
	
	Local mesh%=CreateMesh()
	Local surf%=CreateSurface(mesh)
	
	Local v0%=AddVertex(surf, 1, 1,0,0,0)
	Local v1%=AddVertex(surf,-1, 1,0,1,0)
	Local v2%=AddVertex(surf,-1,-1,0,1,1)
	Local v3%=AddVertex(surf, 1,-1,0,0,1)
	
	AddTriangle surf,v0,v1,v2
	AddTriangle surf,v0,v2,v3
	
	If parent Then EntityParent mesh,parent
	If fx Then EntityFX mesh,fx
	If tex Then EntityTexture mesh,tex
	If blend Then EntityBlend mesh,blend
	
	EntityColor mesh,r,g,b
	EntityAlpha mesh,a
	
	VertexColor surf,v0,r,g,b,a
	VertexColor surf,v1,r,g,b,a
	VertexColor surf,v2,r,g,b,a
	VertexColor surf,v3,r,g,b,a
	
	ScaleEntity mesh,scale,scale,scale
	
	Return mesh
	
End Function</textarea><br><br>Media files:<br><br><b>nebula.jpg</b><br><img src="http://www.christianhart.de/bb/milkyway/nebula.jpg"><br><br><b>star.png</b><br><img src="http://www.christianhart.de/bb/milkyway/star.png"><br><br><b>stars.png</b><br><img src="http://www.christianhart.de/bb/milkyway/stars.png"> </td></tr><tr ><td class="cell"><pre class="code">Const StarSingleTexture$="star.png"		; single star Texture
Const StarFieldsTexture$="stars.png"	; background starfield texture
Const FogTexture$="nebula.jpg"			; 360° split panorama nebula texture

Const StarTexScale#=1.5					; texture scaling of background stars
Const PanoSegments%=32					; number of panorama segments (should be at least 8)

; -----------------------------------------------------------------------------
; Initialize
; -----------------------------------------------------------------------------
Function InitMilkyway(Stars%,Dist#,MinAngle#,MaxAngle#,Scale,R1%=0,G1%=0,B1%=0,A1#=0.0,R2%=0,G2%=0,B2%=0,A2#=0.0,R3%=0,G3%=0,B3%=0,A3#=0.0)
	
	Local MilkyWay%,StarBox%,FogPanorama%,StarSphere%
	Local StarTex%,StarsTex%,FogTex%
	
	MilkyWay=CreatePivot()
	
	; load textures
	StarTex=LoadTexture(StarSingleTexture,2)
	StarsTex=LoadTexture(StarFieldsTexture,2)
	FogTex=LoadTexture(FogTexture,16+32)
	ScaleTexture StarsTex,StarTexScale,StarTexScale
	
	; create stars
	StarBox=InitStarBox(Scale,StarsTex,1,0)
	FogPanorama=InitPanorama(FogTex,Scale,Scale*0.8,PanoSegments,1+2+32,R1,G1,B1,A1,R2,G2,B2,A2,R3,G3,B3,A3)
	StarSphere=InitStarSphere(StarTex,Stars,0.5*(Scale/100.0),0.75*(Scale/50.0),0.5,Dist,MinAngle,MaxAngle,Scale,1)
	
	EntityParent StarBox,MilkyWay
	EntityParent FogPanorama,MilkyWay
	EntityParent StarSphere,MilkyWay
	
	; reorder
	EntityOrder StarBox,3
	EntityOrder FogPanorama,2
	EntityOrder StarSphere,1
	
	; blending
	TextureBlend FogTex,5
	EntityBlend FogPanorama,3
	EntityBlend StarSphere,3
	;EntityBlend StarBox,3
	
	Return MilkyWay
	
End Function


; -----------------------------------------------------------------------------
; create milkyway nebula panorama
; -----------------------------------------------------------------------------
Function InitPanorama(texture%,radius#,h#,segmente,fx%=0,r1%=255,g1%=255,b1%=255,a1#=1.0,r2%=255,g2%=255,b2%=255,a2#=1.0,r3%=255,g3%=255,b3%=255,a3#=1.0)
	
	Local ang1#,ang2#,inc#,x1#,z1#,x2#,z2#
	Local uu1#,uu2#,uv1#,uv2#,uv3#
	Local v0%,v1%,v2%,v3%,v4%,v5%
	
	Local mesh=CreateMesh()
	Local surf=CreateSurface(mesh)
	
	inc=360.0/segmente
	
	ang1=0
	
	While ang1&lt;360
		
		ang2=(ang1+inc) Mod 360
		
		x1=radius*Cos(ang1)
		z1=radius*Sin(ang1)
		x2=radius*Cos(ang2)
		z2=radius*Sin(ang2)
		
		If ang1&lt;180 Then
			
			; use upper texture UV coordinates
			uu1=1.0-(ang1/180.0)
			uu2=1.0-(ang1+inc)/180.0
			uv1=0.50
			uv2=0.25
			uv3=0.00
			
		Else
			
			; use lower texture UV coordinates
			uu1=1-(((ang1/180.0))-1)
			uu2=1-(((ang1+inc)/180.0)-1)
			uv1=1.0
			uv2=0.75
			uv3=0.5
			
		EndIf
		
		v0=AddVertex(surf,x1,-h,z1,uu1,uv1) : VertexColor surf,v0,r1,g1,b1,a1
		v1=AddVertex(surf,x2,-h,z2,uu2,uv1) : VertexColor surf,v1,r1,g1,b1,a1
		v2=AddVertex(surf,x1, 0,z1,uu1,uv2) : VertexColor surf,v2,r2,g2,b2,a2
		v3=AddVertex(surf,x2, 0,z2,uu2,uv2) : VertexColor surf,v3,r2,g2,b2,a2
		v4=AddVertex(surf,x1, h,z1,uu1,uv3) : VertexColor surf,v4,r3,g3,b3,a3
		v5=AddVertex(surf,x2, h,z2,uu2,uv3) : VertexColor surf,v5,r3,g3,b3,a3
		
		AddTriangle surf,v0,v1,v3
		AddTriangle surf,v3,v2,v0
		AddTriangle surf,v2,v5,v4
		AddTriangle surf,v5,v2,v3
		
		ang1=ang1+inc
		
	Wend
	
	EntityFX mesh,fx
	
	EntityTexture mesh,texture
	
	Return mesh
	
End Function


; -----------------------------------------------------------------------------
; create starsphere
; -----------------------------------------------------------------------------
Function InitStarSphere(texture%,stars%,min#,max#,fix#,centered#,centermin#,centermax#,range#,scale#=1.0)
	
	Local star%,mesh%,surf%
	Local i%,size#,a#
	Local r%,g%,b%
	Local col#,maxy#
	
	star=CreateStarQuad()
	
	mesh=CreateMesh()
	surf=CreateSurface(mesh)
	EntityFX mesh,1+2
	
	For i=1 To stars
		
		; create new surface if there are too many stars
		If CountVertices(surf)&gt;=65532 Then surf=CreateSurface(mesh)
		
		; size
		size=min*scale
		If Rnd(1)&gt;fix Then size=Rnd(min,max)*scale
		ScaleEntity star,size,size,size
		
		; reset quad
		PositionEntity star,0,0,0
		
		; more stars centered or random?
		If Rnd(1)&gt;centered Then maxy=centermin Else maxy=centermax
		
		; turn and move starquad
		TurnEntity star,Rnd(-Rnd(0,maxy),Rnd(0,maxy)),Rnd(0,Rnd(0,360)),0
		MoveEntity star,0,0,range*scale
		PointEntity star,mesh
		
		; color
		col=Rnd(1)
		If col&gt;0.75 And col&lt;=1.00 Then r=255 : g=224 : b=192	; 25% orange stars
		If col&gt;0.50 And col&lt;=0.75 Then r=255 : g=255 : b=192	; 25% yellow stars
		If col&gt;0.25 And col&lt;=0.50 Then r=192 : g=224 : b=255	; 25% blue stars
		If col&gt;0.00 And col&lt;=0.25 Then r=255 : g=255 : b=255	; 25% white stars
		
		; alpha
		If size&gt;min*scale Then a#=1.0 Else a#=Rnd(0.5,1)
		
		; add to single surface mesh
		AddToSurface(star,surf,mesh,r,g,b,a)
		
	Next
	
	FreeEntity star
	
	EntityTexture mesh,texture
	
	FlipMesh mesh
	
	Return mesh
	
End Function


; -----------------------------------------------------------------------------
; simple cube starbox
; -----------------------------------------------------------------------------
Function InitStarBox(scale#,texture%,fx%,blend%)
	
	Local starbox%
	
	starbox=CreateCube()
	EntityTexture starbox,texture
	FlipMesh starbox
	ScaleEntity starbox,scale,scale,scale
	
	EntityFX starbox,fx
	EntityBlend starbox,blend
	
	Return starbox
	
End Function


; -----------------------------------------------------------------------------
; add a mesh to another mesh
; -----------------------------------------------------------------------------
Function AddToSurface(mesh,surf,singlesurfaceentity,r%,g%,b%,a#) 
	
	Local vert%[2],vr%[2],vg%[2],vb%[2],va#[2]
	Local surface%,oldvert%,i%,i2%
	
	surface = GetSurface(mesh,1) 
	
	For i = 0 To CountTriangles(surface)-1
		
		For i2 = 0 To 2 
			
			oldvert = TriangleVertex(surface,i,i2)
			
			vr[i2]=r
			vg[i2]=g
			vb[i2]=b
			va[i2]=a
			
			TFormPoint VertexX(surface,oldvert),VertexY(surface,oldvert),VertexZ(surface,oldvert), mesh,singlesurfaceentity 
			vert[i2] = AddVertex(surf,TFormedX(),TFormedY(),TFormedZ(),VertexU(surface,oldvert),VertexV(surface,oldvert)) 
			VertexColor surf,vert[i2],r,g,b,a
			
		Next 
		
		AddTriangle(surf,vert[0],vert[1],vert[2])
		
	Next 
	
End Function


; -----------------------------------------------------------------------------
; create star quad
; -----------------------------------------------------------------------------
Function CreateStarQuad(r%=255,g%=255,b%=255,a#=1.0,fx%=0)
	
	Local mesh%,surf%,v1%,v2%,v3%,v4%
	
	mesh=CreateMesh()
	surf=CreateSurface(mesh)
	
	v1=AddVertex(surf,-1,1,0,1,0)
	v2=AddVertex(surf,1,1,0,0,0)
	v3=AddVertex(surf,-1,-1,0,1,1)
	v4=AddVertex(surf,1,-1,0,0,1)
	
	VertexColor surf,v1,r,g,b,a
	VertexColor surf,v3,r,g,b,a
	VertexColor surf,v2,r,g,b,a
	VertexColor surf,v4,r,g,b,a
	
	AddTriangle(surf,0,1,2)
	AddTriangle(surf,3,2,1)
	
	EntityFX mesh,fx
	
	Return mesh
	
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MCP</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> Very nice indeed Krischan. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sung</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> Beautifull! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >*</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> Gorgeous, will look into using this for another project :) <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> It's really smooth and gives a great impression of depth. <br><br>I didnt see this before, but found it after the "Procedural Saturn" stuff you did.<br>I'm thinking, that, all this (excluding perhaps the very well defined star sprite') might be procedurally generated, giving dfifferent "shapes" and colours to the nebulae (within reason of course, the H-Alpha regions would likely be prominently red etc.)<br>Even ikf the separate pieces are generated individually, this actually gives a greater freedom of 'mix and match' variety.<br>Once i've finished playing with Saturn (I'm working on a means to seed a single, but different, AND RELATIVE value to make Neptune and Uranus so far....)<br>I'll look into playing with generating this too :) <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> Just to mention: the original PSD file for the Nebula has 170MB, 4096x1024 pixels and 18 Layers! I really want to see how to obtain the same result procedurally in a few seconds which costed me several days to create (mostly tweaking) :-D <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Just to mention: the original PSD file for the Nebula has 170MB, 4096x1024 pixels and 18 Layers! I really want to see how to obtain the same result procedurally in a few seconds which costed me several days to create (mostly tweaking) :-D <br><br> <br></div><br>Oh I certainly didn't mean to take anything from your artistic talent! Naturally an image like that you've worked on clearly so dedicatedly would be VERY difficult to even come close too. Perhaps I shouldn't have said |"it can be done procedurally" but rather, "something similar could be attempted [procedurally". <br><br>The method (I presume) of building the nebula involved wvarious different 'levels' of density of the fibrous dust/gas carefully laid over each other with various transparencies... <br>This same process can be achieved, but certainly not necesarily as well-defined or 'accurately' by (agaiun a noise function like perlin noise,- incidentally, did you use Photoshop's 'Difference Cloud' algorithm?', - to generate the structure of the dust-gas. This can be made more or less dense and then different levels applied over one another (by combining multiple alpha'd buffers into a final single buffer image)<br><br>That's the theory at leastm, at any rate, it's worth a try!<br>If that process can be reduced to some kind of algorithm or algorithms, then it need not even be done with a large 4096 image, but the algorithm can be repreated across smaller buffers to tile up to the large version.<br><br>Certainly, I can say without a doubt that your work has paid off since the images are truly stunning, and if I can get anything done procedurally, it wont be half as nice to look at, but as an exercise in ther theory, I wanna give it a try all the same. It helps me learn too :) <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> If you prefer a REAL panorama of our milkyway you should try this, I converted the <a href="http://www.hq.eso.org/public/images/eso0932a/" target="_blank">ESO Milkyway Panorama</a> (6000x3000 pixel!) for this. It is available in two DDS resolutions, 2048x2048 and 1024x1024 but I prefer the 2048 resolution, looks more realistic. To switch resolutions just change the line in the milkyway.bb.<br><br>Just for your info if you want to include own images: the color level range of the nebula panorama should be from 0...127, otherwise it will be too overbright and look weird.<br><br><a href="http://www.christianhart.de/bb/milkyway/Real_Milkyway.exe" target="_blank">Download Real Milkyway SFX [1.1MB]</a><br><br><b>Screenshot with my latest <a href="http://www.blitzbasic.com/Community/posts.php?topic=91575#1044584" target="_blank">Saturn demo</a></b><br><img src="http://www.christianhart.de/bb/milkyway/realmilkyway.jpg"> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cubed Inc.</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> damn, this is reach quality. great job. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >xlsior</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> If you do use the 'real' milky way images in a program: remember that while most of the ones you'll find are free to use, they do require attribution in the credits.<br>ESO, NASA, ESA, etc. all have similar requirements. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
