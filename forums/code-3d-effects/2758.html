<!DOCTYPE html><html lang="en" ><head ><title >Infinite Starfield / Infinite Asteroid Belt</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Creates an infinite starfield or asteroid belt using a single surface entity, language=bb, category=3D Graphics - Effects'><meta name='author' content='Krischan'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=11>3D Graphics - Effects</a>/Infinite Starfield / Infinite Asteroid Belt</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2758.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Infinite Starfield / Infinite Asteroid Belt by Krischan</td><td align="right">2010 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This creates an really infinite starfield or asteroid belt using a single surface entity. The stars are recycled when they are too far away from the camera. This can be used for 3D stars, space dust or even asteroid belts.<br><br>The main code shows the starfield which is cube-shaped in X/Y/Z dimensions while the asteroid belt is a starfield with very small Y dimensions, no recycling in Y direction and different Y random positioning. Movement with mouse/arrow keys, which is different in both demos.<br><br>Oh, and I am sure this can be improved!<br><br><b>Starfield</b><br><img src="http://www.christianhart.de/bb/starfield/starfield.jpg"><br><br><b>Asteroid Belt (see codebox below)</b><br><img src="http://www.christianhart.de/bb/starfield/asteroidbelt.jpg"><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">AppTitle "Infinite Asteroid Belt"

Graphics3D 800,600,32,2

Global SCALEX#		= 2.0				; starfield scale X
Global SCALEY#		= 0.2				; starfield scale Y
Global SCALEZ#		= 2.0				; starfield scale Z

Global TurnSpeed#	= 4.0				; cam turn speed
Global RollSpeed#	= 0.5				; cam roll speed
Global CameraSpeed#	= 0.01				; cam move speed

Global WIDTH%		= GraphicsWidth()	; grab screen width
Global HEIGHT%		= GraphicsHeight()	; grab screen height
Global TIMER%		= CreateTimer(60)	; timer

Type star
	
	Field col%
	Field x#,y#,z#
	
End Type

; camera
Global cam=CreateCamera()
CameraRange cam,0.01,10
PositionEntity cam,0,0,0

; starfield mesh
Global starfield=CreateMesh()
Global surf=CreateSurface(starfield)
Global star=CreateQuad()
EntityTexture starfield,CreateStarTexture()
EntityFX starfield,1+2+32
EntityBlend starfield,3

; add stars to starfield
AddStars(20000,0.001,0.005)

MoveMouse WIDTH/2,HEIGHT/2

; main loop
While Not KeyHit(1)
	
	; camera movement
	Movement(cam)
	
	; update stars
	UpdateStarfield(cam,2,1)

	RenderWorld()
	
	WaitTimer TIMER
	
	Flip 0
	
Wend

End

; create a simple star texture
Function CreateStarTexture(size%=256,flags%=3)
	
	Local tex%=CreateTexture(size,size,flags)
	Local tb%=TextureBuffer(tex)
	
	Local i#,j%,col%,rgb%
	
	SetBuffer tb
	LockBuffer tb
	
	For j=0 To 255
		
		col=255-j
		If col&gt;255 Then col=255
		rgb=col*$1000000+col*$10000+col*$100+col
		
		For i=0 To 360 Step 0.1
			
			WritePixelFast (size/2)+(Sin(i)*(j*size/512)),(size/2)+(Cos(i)*(j*size/512)),rgb,tb
			
		Next
		
	Next
	
	UnlockBuffer tb
	SetBuffer BackBuffer()
	
	Return tex
	
End Function

; rebuild starfield mesh
Function UpdateStarfield(parent%,maxdist#=2.0,fade%=False)
	
	Local s.star,px#,py#,pz#,d#,a#
	
	ClearSurface(surf)
	
	For s.star = Each star
		
		; calc star position
		px=EntityX(parent)-s\x
		py=EntityY(parent)-s\y
		pz=EntityZ(parent)-s\z
		
		; check if star must be moved
		If px&lt;-SCALEX Then s\x=s\x-(SCALEX*2)
		If px&gt;+SCALEX Then s\x=s\x+(SCALEX*2)
		;If py&lt;-SCALEY Then s\y=s\y-(SCALEY*2)
		;If py&gt;+SCALEY Then s\y=s\y+(SCALEY*2)
		If pz&lt;-SCALEZ Then s\z=s\z-(SCALEZ*2)
		If pz&gt;+SCALEZ Then s\z=s\z+(SCALEZ*2)
		
		; reposition star
		PositionEntity star,s\x,s\y,s\z
		
		; star is visible?
		If EntityInView(star,cam) Then
			
			; get distance
			d=EntityDistance(star,cam)
			
			; check if not to far away
			If d&lt;maxdist Then
				
				; align star to cam
				PointEntity star,cam
				
				; add alpha
				a=1.0 : If fade Then a=Normalize(d,maxdist*0.5,maxdist,1,0)
				
				; add star to starfield again
				AddToSurface(star,surf,starfield,s\col,s\col,s\col,a)
				
			EndIf
			
		EndIf
		
	Next
	
End Function

; add stars to starfield mesh
Function AddStars(amount%=1,min#=0.01,max#=0.02)
	
	Local i%,s.star,size#
	
	For i=1 To amount
		
		s.star = New star
		
		size#=Rnd(min,max)
		
		s\col=Rand(64,255)
		s\x=Rnd(-SCALEX,SCALEX)
		s\y=Rnd(Rnd(Rnd(-SCALEY)),Rnd(Rnd(SCALEY)))
		s\z=Rnd(-SCALEZ,SCALEZ)
		
		PositionEntity(star,s\x,s\y,s\z,1)
		ScaleEntity star,size,size,size
		AddToSurface(star,surf,starfield,255,255,255,1)
		
	Next
	
End Function

; simple spaceship freeflight
Function Movement(cam%,sensitivity#=1.0)
	
	Local roll#,cx#,cz#,tx#,ty#
	
	cx=(KeyDown(205)-KeyDown(203))*CameraSpeed
	cz=(KeyDown(200)-KeyDown(208))*CameraSpeed
	roll=(KeyDown(203)-KeyDown(205))*RollSpeed
	
	tx=Normalize(MouseX(),0,WIDTH , 1,-1)
	ty=Normalize(MouseY(),0,HEIGHT,-1, 1)
	
	If ty&lt;0 Then ty=(Abs(ty)^sensitivity)*-1 Else ty=ty^sensitivity
	If tx&lt;0 Then tx=(Abs(tx)^sensitivity)*-1 Else tx=tx^sensitivity
	
	TurnEntity cam,ty*TurnSpeed,tx*TurnSpeed,roll*TurnSpeed
	MoveEntity cam,0,0,cz
	
End Function

; normalize a value
Function Normalize#(value#=128.0,value_min#=0.0,value_max#=255.0,norm_min#=0.0,norm_max#=1.0)
	
	Return ((value-value_min)/(value_max-value_min))*(norm_max-norm_min)+norm_min
	
End Function

; add a mesh to another mesh's surface
Function AddToSurface(mesh,surf,singlesurfaceentity,r%,g%,b%,a#) 
	
	Local vert%[2],vr%[2],vg%[2],vb%[2],va#[2]
	Local surface%,oldvert%,i%,i2%
	
	surface = GetSurface(mesh,1) 
	
	For i = 0 To CountTriangles(surface)-1
		
		For i2 = 0 To 2 
			
			oldvert = TriangleVertex(surface,i,i2)
			
			vr[i2]=r
			vg[i2]=g
			vb[i2]=b
			va[i2]=a
			
			TFormPoint VertexX(surface,oldvert),VertexY(surface,oldvert),VertexZ(surface,oldvert), mesh,singlesurfaceentity 
			vert[i2] = AddVertex(surf,TFormedX(),TFormedY(),TFormedZ(),VertexU(surface,oldvert),VertexV(surface,oldvert)) 
			VertexColor surf,vert[i2],r,g,b,a
			
		Next 
		
		AddTriangle(surf,vert[0],vert[1],vert[2])
		
	Next 
	
End Function

; create a quad
Function CreateQuad(r%=255,g%=255,b%=255,a#=1.0)
	
	Local mesh%,surf%,v1%,v2%,v3%,v4%
	
	mesh=CreateMesh()
	surf=CreateSurface(mesh)
	
	v1=AddVertex(surf,-1,1,0,1,0)
	v2=AddVertex(surf,1,1,0,0,0)
	v3=AddVertex(surf,-1,-1,0,1,1)
	v4=AddVertex(surf,1,-1,0,0,1)
	
	VertexColor surf,v1,r,g,b,a
	VertexColor surf,v3,r,g,b,a
	VertexColor surf,v2,r,g,b,a
	VertexColor surf,v4,r,g,b,a
	
	AddTriangle(surf,0,1,2)
	AddTriangle(surf,3,2,1)
	
	FlipMesh mesh
	
	Return mesh
	
End Function</textarea> </td></tr><tr ><td class="cell"><pre class="code">AppTitle "Infinite Starfield"

Graphics3D 800,600,32,2

Global SCALEX#		= 2.0				; starfield scale X
Global SCALEY#		= 2.0				; starfield scale Y
Global SCALEZ#		= 2.0				; starfield scale Z

Global TurnSpeed#	= 4.0				; cam turn speed
Global RollSpeed#	= 0.5				; cam roll speed
Global CameraSpeed#	= 0.01				; cam move speed

Global WIDTH%		= GraphicsWidth()	; grab screen width
Global HEIGHT%		= GraphicsHeight()	; grab screen height
Global TIMER%		= CreateTimer(60)	; timer

Type star
	
	Field col%
	Field x#,y#,z#
	
End Type

; camera
Global cam=CreateCamera()
CameraRange cam,0.01,10
PositionEntity cam,0,0,0

; starfield mesh
Global starfield=CreateMesh()
Global surf=CreateSurface(starfield)
Global star=CreateQuad()
EntityTexture starfield,CreateStarTexture()
EntityFX starfield,1+2+32
EntityBlend starfield,3

; add stars to starfield
AddStars(20000,0.001,0.005)

MoveMouse WIDTH/2,HEIGHT/2

; main loop
While Not KeyHit(1)
	
	; camera movement
	Movement(cam)
	
	; update stars
	UpdateStarfield(cam,2,1)

	RenderWorld()
	
	WaitTimer TIMER
	
	Flip 0
	
Wend

End

; create a simple star texture
Function CreateStarTexture(size%=256,flags%=3)
	
	Local tex%=CreateTexture(size,size,flags)
	Local tb%=TextureBuffer(tex)
	
	Local i#,j%,col%,rgb%
	
	SetBuffer tb
	LockBuffer tb
	
	For j=0 To 255
		
		col=255-j
		If col&gt;255 Then col=255
		rgb=col*$1000000+col*$10000+col*$100+col
		
		For i=0 To 360 Step 0.1
			
			WritePixelFast (size/2)+(Sin(i)*(j*size/512)),(size/2)+(Cos(i)*(j*size/512)),rgb,tb
			
		Next
		
	Next
	
	UnlockBuffer tb
	SetBuffer BackBuffer()
	
	Return tex
	
End Function

; rebuild starfield mesh
Function UpdateStarfield(parent%,maxdist#=2.0,fade%=False)
	
	Local s.star,px#,py#,pz#,d#,a#
	
	ClearSurface(surf)
	
	For s.star = Each star
		
		; calc star position
		px=EntityX(parent)-s\x
		py=EntityY(parent)-s\y
		pz=EntityZ(parent)-s\z
		
		; check if star must be moved
		If px&lt;-SCALEX Then s\x=s\x-(SCALEX*2)
		If px&gt;+SCALEX Then s\x=s\x+(SCALEX*2)
		If py&lt;-SCALEY Then s\y=s\y-(SCALEY*2)
		If py&gt;+SCALEY Then s\y=s\y+(SCALEY*2)
		If pz&lt;-SCALEZ Then s\z=s\z-(SCALEZ*2)
		If pz&gt;+SCALEZ Then s\z=s\z+(SCALEZ*2)
		
		; reposition star
		PositionEntity star,s\x,s\y,s\z
		
		; star is visible?
		If EntityInView(star,cam) Then
			
			; get distance
			d=EntityDistance(star,cam)
			
			; check if not to far away
			If d&lt;maxdist Then
				
				; align star to cam
				PointEntity star,cam
				
				; add alpha
				a=1.0 : If fade Then a=Normalize(d,maxdist*0.5,maxdist,1,0)
				
				; add star to starfield again
				AddToSurface(star,surf,starfield,s\col,s\col,s\col,a)
				
			EndIf
			
		EndIf
		
	Next
	
End Function

; add stars to starfield mesh
Function AddStars(amount%=1,min#=0.01,max#=0.02)
	
	Local i%,s.star,size#
	
	For i=1 To amount
		
		s.star = New star
		
		size#=Rnd(min,max)
		
		s\col=Rand(64,255)
		s\x=Rnd(-SCALEX,SCALEX)
		s\y=Rnd(-SCALEY,SCALEY)
		s\z=Rnd(-SCALEZ,SCALEZ)
		
		PositionEntity(star,s\x,s\y,s\z,1)
		ScaleEntity star,size,size,size
		AddToSurface(star,surf,starfield,255,255,255,1)
		
	Next
	
End Function

; simple spaceship freeflight
Function Movement(cam%,sensitivity#=1.0)
	
	Local roll#,cx#,cz#,tx#,ty#
	
	cx=(KeyDown(205)-KeyDown(203))*CameraSpeed
	cz=(KeyDown(200)-KeyDown(208))*CameraSpeed
	roll=(MouseDown(2)-MouseDown(1))*RollSpeed
	
	tx=Normalize(MouseX(),0,WIDTH , 1,-1)
	ty=Normalize(MouseY(),0,HEIGHT,-1, 1)
	
	If ty&lt;0 Then ty=(Abs(ty)^sensitivity)*-1 Else ty=ty^sensitivity
	If tx&lt;0 Then tx=(Abs(tx)^sensitivity)*-1 Else tx=tx^sensitivity
	
	TurnEntity cam,ty*TurnSpeed,tx*TurnSpeed,roll*TurnSpeed
	MoveEntity cam,cx,0,cz
	
End Function

; normalize a value
Function Normalize#(value#=128.0,value_min#=0.0,value_max#=255.0,norm_min#=0.0,norm_max#=1.0)
	
	Return ((value-value_min)/(value_max-value_min))*(norm_max-norm_min)+norm_min
	
End Function

; add a mesh to another mesh's surface
Function AddToSurface(mesh,surf,singlesurfaceentity,r%,g%,b%,a#) 
	
	Local vert%[2],vr%[2],vg%[2],vb%[2],va#[2]
	Local surface%,oldvert%,i%,i2%
	
	surface = GetSurface(mesh,1) 
	
	For i = 0 To CountTriangles(surface)-1
		
		For i2 = 0 To 2 
			
			oldvert = TriangleVertex(surface,i,i2)
			
			vr[i2]=r
			vg[i2]=g
			vb[i2]=b
			va[i2]=a
			
			TFormPoint VertexX(surface,oldvert),VertexY(surface,oldvert),VertexZ(surface,oldvert), mesh,singlesurfaceentity 
			vert[i2] = AddVertex(surf,TFormedX(),TFormedY(),TFormedZ(),VertexU(surface,oldvert),VertexV(surface,oldvert)) 
			VertexColor surf,vert[i2],r,g,b,a
			
		Next 
		
		AddTriangle(surf,vert[0],vert[1],vert[2])
		
	Next 
	
End Function

; create a quad
Function CreateQuad(r%=255,g%=255,b%=255,a#=1.0)
	
	Local mesh%,surf%,v1%,v2%,v3%,v4%
	
	mesh=CreateMesh()
	surf=CreateSurface(mesh)
	
	v1=AddVertex(surf,-1,1,0,1,0)
	v2=AddVertex(surf,1,1,0,0,0)
	v3=AddVertex(surf,-1,-1,0,1,1)
	v4=AddVertex(surf,1,-1,0,0,1)
	
	VertexColor surf,v1,r,g,b,a
	VertexColor surf,v3,r,g,b,a
	VertexColor surf,v2,r,g,b,a
	VertexColor surf,v4,r,g,b,a
	
	AddTriangle(surf,0,1,2)
	AddTriangle(surf,3,2,1)
	
	FlipMesh mesh
	
	Return mesh
	
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> Here is an update, now you can set a density to simulate different amounts of objects, enter/leave a starfield/asteroid field or whatever. Use arrows and mouse to move, SHIFT=faster and the Mouse buttons increase/decrease the density in percent. Watch the radar to see how the density changes (only noticable while moving).<br><br>EDIT: I am not very happy with the Createquad / AddToSurface solution here, any suggestions how to improve it are welcome...<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">AppTitle "Infinite Starfield"

Graphics3D 800,600,32,2

Global SCALEX#		= 1.0				; starfield scale X
Global SCALEY#		= 1.0				; starfield scale Y
Global SCALEZ#		= 1.0				; starfield scale Z
Global FARDIST#		= 1.0				; fade out distance
Global DENSITY#		= 20.0				; 20% density

Global TurnSpeed#	= 4.0				; cam turn speed
Global CameraSpeed#	= 0.01				; cam move speed

Global WIDTH%		= GraphicsWidth()	; grab screen width
Global HEIGHT%		= GraphicsHeight()	; grab screen height
Global TIMER%		= CreateTimer(60)	; timer

Global MAXSTARS%	= 25000

Global visible%,stars%

Global imagesize%=128
Global image=CreateImage(imagesize,imagesize)
Global buff=ImageBuffer(image)

Type star
	
	Field col%
	Field scale#
	Field x#,y#,z#
	Field visible%
	
End Type

; camera
Global cam=CreateCamera()
CameraRange cam,0.01,10
PositionEntity cam,0,0,0

; star quad
Global star=CreateQuad()
HideEntity star

; starfield mesh
Global starfield=CreateMesh()
Global surf=CreateSurface(starfield)
EntityTexture starfield,CreateStarTexture()
EntityFX starfield,1+2+32
EntityBlend starfield,3

; add stars to starfield
AddStars(MAXSTARS,0.0025,0.005)

MoveMouse WIDTH/2,HEIGHT/2

; main loop
While Not KeyHit(1)
	
	; camera movement
	Movement(cam)
	
	DENSITY=DENSITY+((MouseDown(1)-MouseDown(2))/2.0)
	If DENSITY&gt;100 Then DENSITY=100 Else If DENSITY&lt;0 Then DENSITY=0
	
	; update stars
	UpdateStarfield(cam,True)
	
	RenderWorld()
	
	WaitTimer TIMER
	
	DrawImage image,0,0
	
	Color 255,255,255
	
	Text 0,imagesize+ 0,"Density:   "+DENSITY+"%"
	Text 0,imagesize+14,"Visible:   "+visible
	Text 0,imagesize+28,"Stars:     "+stars
	
	Flip 0
	
Wend

End

; rebuild starfield mesh
Function UpdateStarfield(parent%,fader%=False)
	
	Local s.star,px#,py#,pz#,d#,a#,rgb%,x%,y%
	
	Local cx#=EntityX(parent)
	Local cy#=EntityY(parent)
	Local cz#=EntityZ(parent)
	
	ClearSurface(surf)
	
	visible=0
	stars=0
	
	SetBuffer buff
	Color 0,0,0
	Rect 0,0,imagesize,imagesize,1
	SetBuffer BackBuffer()
	
	LockBuffer buff
	
	For s.star = Each star
		
		stars=stars+1
		
		; calc star position
		px=cx-s\x
		py=cy-s\y
		pz=cz-s\z
		
		; check if star must be moved
		If px&lt;-SCALEX Then s\x=s\x-(SCALEX*2) : s\visible=False : If Rnd(100)&lt;DENSITY Then s\visible=True
		If px&gt;+SCALEX Then s\x=s\x+(SCALEX*2) : s\visible=False : If Rnd(100)&lt;DENSITY Then s\visible=True
		If py&lt;-SCALEY Then s\y=s\y-(SCALEY*2) : s\visible=False : If Rnd(100)&lt;DENSITY Then s\visible=True
		If py&gt;+SCALEY Then s\y=s\y+(SCALEY*2) : s\visible=False : If Rnd(100)&lt;DENSITY Then s\visible=True
		If pz&lt;-SCALEZ Then s\z=s\z-(SCALEZ*2) : s\visible=False : If Rnd(100)&lt;DENSITY Then s\visible=True
		If pz&gt;+SCALEZ Then s\z=s\z+(SCALEZ*2) : s\visible=False : If Rnd(100)&lt;DENSITY Then s\visible=True
		
		If s\visible Then
		
			; reposition star
			PositionEntity star,s\x,s\y,s\z
			
			x=Int(Normalize(px,-SCALEX,SCALEX,imagesize/2,-imagesize/2))
			y=Int(Normalize(pz,-SCALEZ,SCALEZ,-imagesize/2,imagesize/2))
			
			rgb=64*$10000+64*$100+64
			
			; get distance
			d=EntityDistance(star,cam)
		
			; check if not to far away
			If d&lt;FARDIST Then
			
				; star is visible?
				If EntityInView(star,cam) Then
					
					rgb=255*$100
			
					; align star to cam
					PointEntity star,cam
				
					ScaleEntity star,s\scale,s\scale,s\scale
				
					; add alpha
					a=1.0 : If fader Then a=Normalize(d,FARDIST*0.5,FARDIST,1,0)
				
					; add star To starfield again
					AddToSurface(star,surf,starfield,s\col,s\col,s\col,a)
				
					visible=visible+1
				
				EndIf
			
			EndIf
			
			WritePixelFast ((imagesize/2)+x) Mod imagesize,((imagesize/2)+y) Mod imagesize Mod imagesize,rgb,buff
			
		
		EndIf
		
	Next
	
	UnlockBuffer buff
	
	Return DENSITY
	
End Function

; add stars to starfield mesh
Function AddStars(amount%=1,min#=0.01,max#=0.02)
	
	Local i%,s.star
	
	For i=1 To amount
		
		s.star = New star
		
		s\col=Rand(64,255)
		s\x=Rnd(-SCALEX,SCALEX)
		s\y=Rnd(-SCALEY,SCALEY)
		s\z=Rnd(-SCALEZ,SCALEZ)
		s\scale=Rnd(min,max)
		
		If Distance3D(0,0,0,s\x,s\y,s\z)&lt;=FARDIST Then
			
			If Rand(100)&lt;DENSITY Then s\visible=True
			
		Else
			
			If Rand(100)&lt;DENSITY Then s\visible=True
			
		EndIf
		
	Next
	
End Function

; simple 3D distance calculation
Function Distance3D#(x1#,y1#,z1#,x2#,y2#,z2#)
	
	Local x#=x1-x2
	Local y#=y1-y2
	Local z#=z1-z2
	
	Return Sqr(x*x+y*y+z*z)
	
End Function

; simple spaceship freeflight
Function Movement(cam%,sensitivity#=1.0)
	
	Local cx#,cz#,tx#,ty#,multi%=1
	
	cx=(KeyDown(205)-KeyDown(203))*CameraSpeed
	cz=(KeyDown(200)-KeyDown(208))*CameraSpeed
	If KeyDown(42) Or KeyDown(54) Then multi=3
	
	tx=Normalize(MouseX(),0,WIDTH , 1,-1)
	ty=Normalize(MouseY(),0,HEIGHT,-1, 1)
	
	If ty&lt;0 Then ty=(Abs(ty)^sensitivity)*-1 Else ty=ty^sensitivity
	If tx&lt;0 Then tx=(Abs(tx)^sensitivity)*-1 Else tx=tx^sensitivity
	
	TurnEntity cam,ty*TurnSpeed,tx*TurnSpeed,0
	MoveEntity cam,cx*multi,0,cz*multi
	
End Function

; create a simple star texture
Function CreateStarTexture(size%=256,flags%=3)
	
	Local tex%=CreateTexture(size,size,flags)
	Local tb%=TextureBuffer(tex)
	
	Local i#,j%,col%,rgb%
	
	SetBuffer tb
	LockBuffer tb
	
	For j=0 To 255
		
		col=255-j
		If col&gt;255 Then col=255
		rgb=col*$1000000+col*$10000+col*$100+col
		
		For i=0 To 360 Step 0.1
			
			WritePixelFast (size/2)+(Sin(i)*(j*size/512)),(size/2)+(Cos(i)*(j*size/512)),rgb,tb
			
		Next
		
	Next
	
	UnlockBuffer tb
	SetBuffer BackBuffer()
	
	Return tex
	
End Function

; normalize a value
Function Normalize#(value#=128.0,value_min#=0.0,value_max#=255.0,norm_min#=0.0,norm_max#=1.0)
	
	Return ((value-value_min)/(value_max-value_min))*(norm_max-norm_min)+norm_min
	
End Function

; add a mesh to another mesh's surface
Function AddToSurface(mesh,surf,singlesurfaceentity,r%,g%,b%,a#) 
	
	Local vert%[2],vr%[2],vg%[2],vb%[2],va#[2]
	Local surface%,oldvert%,i%,i2%
	
	surface = GetSurface(mesh,1) 
	
	For i = 0 To CountTriangles(surface)-1
		
		For i2 = 0 To 2 
			
			oldvert = TriangleVertex(surface,i,i2)
			
			vr[i2]=r
			vg[i2]=g
			vb[i2]=b
			va[i2]=a
			
			TFormPoint VertexX(surface,oldvert),VertexY(surface,oldvert),VertexZ(surface,oldvert), mesh,singlesurfaceentity 
			vert[i2] = AddVertex(surf,TFormedX(),TFormedY(),TFormedZ(),VertexU(surface,oldvert),VertexV(surface,oldvert)) 
			VertexColor surf,vert[i2],r,g,b,a
			
		Next 
		
		AddTriangle(surf,vert[0],vert[1],vert[2])
		
	Next 
	
End Function

; create a quad
Function CreateQuad(r%=255,g%=255,b%=255,a#=1.0)
	
	Local mesh%,surf%,v1%,v2%,v3%,v4%
	
	mesh=CreateMesh()
	surf=CreateSurface(mesh)
	
	v1=AddVertex(surf,-1,1,0,1,0)
	v2=AddVertex(surf,1,1,0,0,0)
	v3=AddVertex(surf,-1,-1,0,1,1)
	v4=AddVertex(surf,1,-1,0,0,1)
	
	VertexColor surf,v1,r,g,b,a
	VertexColor surf,v3,r,g,b,a
	VertexColor surf,v2,r,g,b,a
	VertexColor surf,v4,r,g,b,a
	
	AddTriangle(surf,0,1,2)
	AddTriangle(surf,3,2,1)
	
	FlipMesh mesh
	
	Return mesh
	
End Function</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ClayPigeon</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> It MAV's when you move. I'm assuming this is probably because when you move, it tries to draw pixels outside the buffer. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> Interesting, never happened here. I changed the writepixel line, should work now so please test it again. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >puki</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> It all works for me.  All very exciting.<br><br>I found it a bit grey so I just applied 'super-lighting' which is where you nonce the lighting to 512 rather than the "Andy Pandy" 255 that most people use. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MErren</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> very cool !! <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
