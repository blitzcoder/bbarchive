<!DOCTYPE html><html lang="en" ><head ><title >Aqua Effect on 3D Cube</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Aqua Effect, language=bb, category=3D Graphics - Effects'><meta name='author' content='Markus Rauch'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=11>3D Graphics - Effects</a>/Aqua Effect on 3D Cube</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="476.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Aqua Effect on 3D Cube by Markus Rauch</td><td align="right">2002 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Aqua is nice ! Test it !<br>The Code writing water hight fields in a Buffer and<br>copy them to a texture that is on a cube that simply<br>rotate in a 3D World . It has automatic rains drops but you can<br>hold down the left mouse button and move it !<br>The gray Pixel in the texture on the cube it your mouse pointer .<br>What you need is a Texture.bmp file in the same directory as the Code .<br>If you have a CPU &lt;= 400 MHz you need a Texture with size 128x128 .<br>The Texture you need must have the same x and y size .<br>First test it in debug mode (very very very slow) , when it works disable the<br>debug mode to run it fast . </td></tr><tr ><td class="cell"><pre class="code">; Aqua Effect (C) 2002 by M.Rauch from Germany

; If DebugMode = True Then it is very slow !

; MR 03.11.2002

.Top

Graphics3D 640,480,16,1 

SetBuffer BackBuffer()

;------------------------------------------------------------------------- Camera

.Cam

Global Camera =CreateCamera()

PositionEntity Camera,0,0,-10

CameraClsColor Camera,0,20,0

;------------------------------------------------------------------------- Texture

.Texture

;a Texture size 128 x 128 is very fast

Global ReflectImage=LoadTexture("Texture.bmp")    ;&lt;--- 
Global ReflectImageB=TextureBuffer(ReflectImage)

Global Output=CreateTexture(TextureWidth(ReflectImage),TextureHeight(ReflectImage),1) ;The same size 
Global OutputB=TextureBuffer(Output)

;-------------------------------------------------------------------------

.Entitys

 Global Cube=CreateCube()

 ScaleMesh Cube,3,3,3

 EntityTexture Cube,Output

;--------------------------------------------------------

.Light

 Global Light1 =CreateLight(1)
 Global Light2 =CreateLight(1)
 Global Light3 =CreateLight(1)

 LightRange Light1 ,50
 LightRange Light2 ,50
 LightRange Light3 ,50

 LightColor Light1, 64, 64, 64 
 LightColor Light2,255,255,255 
 LightColor Light3, 64, 64, 64 

 PositionEntity Light1 ,-10, 10,-10
 PositionEntity Light2 ,  0,  0,-10
 PositionEntity Light3 , 10,-10,-10

 AmbientLight 0,0,0 

;------------------------------------------------------------------------- WaterSettings

.WaterSettings

Global WATERSIZE=TextureWidth(ReflectImage) ;like 128 or 256

Global RainCount=0

Global DripRadius = 12
Global DripRadiusSqr = DripRadius * DripRadius
Global DampingFactor# = 0.04 ;Values For damping from 0.04 - 0.0001 look pretty good (the Buffer must in float)

.WaterBuffers

Global BufferSize=(WATERSIZE * WATERSIZE)

Dim ReadBuffer #(BufferSize)
Dim WriteBuffer#(BufferSize)
Dim TempBuffer #(BufferSize)

Local i

For i = 0 To BufferSize 
 TempBuffer (i) = 0
 ReadBuffer (i) = 0
 WriteBuffer(i) = 0
Next

;------------------------------------------------------------------------- MainLoop

.MainLoop

While Not KeyHit(1) ; 1=Escape 

 Local ti#

 ti=MilliSecs()

 SwapBuffers
 Show

 CheckMouse ;&lt;- Press left Button and move the Mouse

 TurnEntity Cube,1,-1.5,0

 RenderWorld

 Rain ;&lt;- automatic 

 ProcessWater

 While Abs(MilliSecs()-ti)&lt;10 
 Wend

 Flip
Wend
End

;-------------------------------------------------------------------------

.Buffers

;-------------------------------------------------------------------------

Function SetBufferR(x,y,value#)

 ReadBuffer(x+y*WATERSIZE)=value

End Function

;-------------------------------------------------------------------------

Function SetBufferW(x,y,value#)

 If value &gt;  32 Then value =  32
 If value &lt; -32 Then value = -32

 WriteBuffer(x+y*WATERSIZE)=value

End Function

;-------------------------------------------------------------------------

Function GetBufferR#(x,y)

 Return ReadBuffer(x+y*WATERSIZE)

End Function

;-------------------------------------------------------------------------

Function GetBufferW#(x,y)

 Return WriteBuffer(x+y*WATERSIZE)

End Function

;-------------------------------------------------------------------------

Function SwapBuffers() 

 Local i

 ;Swap the buffers !

 For i = 0 To BufferSize 
  TempBuffer(i) =ReadBuffer(i)  
  ReadBuffer(i) =WriteBuffer(i)  
  WriteBuffer(i)=TempBuffer(i)
 Next

End Function

;-------------------------------------------------------------------------

.RenderTexture

Function Show()

 Local x,y
 Local xoff,yoff
 Local xm,ym
 Local pix,pix2
 Local r,g,b,a
 Local bu 

 xm=GraphicsWidth() /2-WATERSIZE/2
 ym=GraphicsHeight()/2-WATERSIZE/2

 ;-----------------------------------------------

 LockBuffer ReflectImageB
 LockBuffer OutputB

 Local cnt=0 

  y=0
  While y &lt; WATERSIZE
	  x=0
    While x &lt; WATERSIZE

     xoff = x
		 If x &gt; 0 And x &lt; WATERSIZE - 1 Then
			xoff =xoff- (ReadBuffer(cnt - 1))
			xoff =xoff+ (ReadBuffer(cnt + 1))
		 EndIf

		 yoff = y
		 If y &gt; 0 And y &lt; WATERSIZE - 1 Then
			yoff =yoff- ReadBuffer(cnt - WATERSIZE)
			yoff =yoff+ ReadBuffer(cnt + WATERSIZE)
     EndIf

     If xoff &lt; 0 Then xoff = 0
     If yoff &lt; 0 Then yoff = 0
     If xoff &gt; WATERSIZE-1 Then xoff = WATERSIZE-1
     If yoff &gt; WATERSIZE-1 Then yoff = WATERSIZE-1

     pix=ReadPixelFast(xoff,yoff,ReflectImageB)
     r=(pix And $ff0000)/$10000
     g=(pix And $ff00)/$100
     b=(pix And $ff)

     ;r=128 ;&lt;- only color
     ;g=128
     ;b=128

     bu=ReadBuffer(cnt)
     r = r + bu
     g = g + bu
     b = b + bu
     If r &lt; 0 Then r = 0
     If g &lt; 0 Then g = 0
     If b &lt; 0 Then b = 0
     If r &gt; 255 Then r = 255
     If g &gt; 255 Then g = 255
     If b &gt; 255 Then b = 255

     pix2=ARGB(r,g,b)
     WritePixelFast x,y,pix2,OutputB

     cnt=cnt+1

     x=x+1
		Wend
   y=y+1
	Wend

 UnlockBuffer OutputB
 UnlockBuffer ReflectImageB

End Function

;-------------------------------------------------------------------------

.Helpers

Function ARGB(r,g,b)

 ;Return ((128 * $1000000) Or (r * $10000) Or (g * $100) Or b)
 Return ((r * $10000) Or (g * $100) Or b)

End Function

;-------------------------------------------------------------------------

Function SquaredDist(sx, sy, dx, dy)

 ;Find the Squared distance between two 2D points

 Return ((dx - sx) * (dx - sx)) + ((dy - sy) * (dy - sy))

End Function

;-------------------------------------------------------------------------

.MouseInput

Function CheckMouse()

 Local mx,my

 mx=MouseX()
 my=MouseY()

 If mx&lt;0 Then mx=0
 If my&lt;0 Then my=0

 If mx&gt;WATERSIZE-1 Then mx=WATERSIZE-1
 If my&gt;WATERSIZE-1 Then my=WATERSIZE-1

 WritePixel mx,my,ARGB(128,128,128),OutputB  

 If MouseDown(1) Then
  MakeDrip mx,my,4
 EndIf

End Function

;-------------------------------------------------------------------------

.RainInParadise

Function Rain()

 Local mx,my

 Local i

 RainCount=RainCount+1
 
 If RainCount &gt; 10 Then

  RainCount=0

  SeedRnd MilliSecs()

  mx=Rnd(0,WATERSIZE-1)
  my=Rnd(0,WATERSIZE-1)

  MakeDrip mx,my,4

 EndIf

End Function

;-------------------------------------------------------------------------

.WaterDrip

Function MakeDrip(xm , ym , depth) 

 ;Creates an initial drip in the water Field

 ;DebugLog "MakeDrip "+x+" "+y+" "+depth

	Local x,y
	Local dist,finaldepth#

  y=ym - DripRadius
  While y &lt; ym + DripRadius
   x=xm - DripRadius
   While x &lt; xm + DripRadius
		If x =&gt; 0 And y =&gt; 0 And x &lt; WATERSIZE And y &lt; WATERSIZE Then			
				dist = SquaredDist(x,y,xm,ym)
				If dist &lt; DripRadiusSqr Then				
					finaldepth = (depth * DripRadius - Sqr(dist))/DripRadius
					If finaldepth &gt;  127 Then finaldepth =  127
					If finaldepth &lt; -127 Then finaldepth = -127
					SetBufferW x,y,finaldepth
				EndIf
		EndIf
    x=x+1
	 Wend		
   y=y+1
  Wend

End Function

;-------------------------------------------------------------------------

.WaterInAction

Function ProcessWater() 

 ;Calculate New values For the water height Field

 Local x,y
 Local v#
	
  y=2
  While y &lt; WATERSIZE-2
	  x=2
    While x &lt; WATERSIZE-2
		
			;Sample a "circle" around the center point
      v =0
			v = v +	GetBufferR(x-2,y)
			v = v +	GetBufferR(x+2,y)
			v = v +	GetBufferR(x  ,y-2)
			v = v +	GetBufferR(x  ,y+2)
			v = v +	GetBufferR(x-1,y)
			v = v +	GetBufferR(x+1,y)
			v = v +	GetBufferR(x  ,y-1)
			v = v +	GetBufferR(x  ,y+1)
			v = v +	GetBufferR(x-1,y-1)
			v = v +	GetBufferR(x+1,y-1)
			v = v +	GetBufferR(x-1,y+1)
			v = v +	GetBufferR(x+1,y+1)

			v = v / 6.0 		
			v = v - GetBufferW(x,y)		
      v = v - (v * DampingFactor)

			SetBufferW (x,y,v)

     x=x+1
		Wend
   y=y+1
	Wend

End Function

;-------------------------------------------------------------------------</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> wow!!!! Nice water effect! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Trixx</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> One more "WOW" :) - Really nice ! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Clyde</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Nice effect mate :) <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >evil_1_24</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> WOW :) nice effect <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >puki</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> Heh, I don't think I ever saw that one before - very cool.<br><br>Finding the right texture is the key. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >puki</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> In fact, this is quite amazing.<br><br>This was done 4 years ago - my PC is well capable of running this quite fast.<br><br>You can even create pixel-shader-esqe water ripples like in Morrowind - like when you move through water and turn around to see the waves you created behind you.  Disable the rotation of the cube to look at this.<br><br>Very nice. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >bytecode77</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> i am working on a water eingine like your one with a bump mapping texture ("wateranim.png")...wait until first release :) <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
