<!DOCTYPE html><html lang="en" ><head ><title >Lens Flares</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Beautiful lens flares.  Complete with flare images and demo., language=bb, category=3D Graphics - Effects'><meta name='author' content='Barliesque'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=11>3D Graphics - Effects</a>/Lens Flares</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1107.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Lens Flares by Barliesque</td><td align="right">2004 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> ***DOWNLOAD THE DEMO*** complete with source code and media here:<br><a href="http://game.barliesque.com/blitz3d/sun_flares_demo.zip" target="_blank">http://game.barliesque.com/blitz3d/sun_flares_demo.zip</a> (1.7 MB)<br><br>This system produces one of the best lens flares I've seen, IMHO.  It's not quite perfect yet, but I'm working on <i>making</i> it perfect!  :)  <br>For now, I think this is a good start.<br><br>Features:<br>- Flares change size and intensity based on how directly they're viewed<br>- Flares are tinted, based on the colour of the light source (great for stained glass windows!)<br><br><img src="http://game.barliesque.com/blitz3d/flare_screenshot.jpg"><br><br>All code and media included here are available for you to use as you please.  If you do use it in a project, a small credit would be quite nice.  Also, if you produce your own flares image or come up with some good changes, please post them here.<br><br>Special Thanks:<br>- SyntaxError for "Sprite Control"<br>- Fredborg for the little landscape that appears in this demo<br>- BloodLocust for the simple lens flare code, which provided a start </td></tr><tr ><td class="cell"><pre class="code">;=======================================================================================
;  "SUN FLARES"
;  by David Barlia (a.k.a. Barliesque)
;  david[at]barliesque[dot]com
;
;  Please feel free to make use of this code as you wish.
;  If you do use it in a project, a little credit would be nice.
;  Also, if you should happen to make some exciting improvements,
;  don't hesitate to post your changes (or alternate flares image?)
;  to the code archives.
;
;  This code makes use of a modified version of "Sprite COntrol" by SyntaxError.
;  If you are already using "Sprite Control" in your project, and want to add
;  this code, you will probably have no problem changing to this modified version.
;  See "Sprite Control.bb" for further info.
;
;=======================================================================================

AppTitle = "Sun Flares Demo"
HidePointer


graphics3D 640,480,32,1


Include "Sprite Control.bb"


Global camera, player
Global cam_pitch#, cam_yaw#
Global mvx#,mvy#,mvz#,targetpitch#,targetyaw#

Global playerheight#=32		;height of collision sphere

Global FrameTime, FPS#=80.0

;-------------------------
; Set up the Camera/Player
;-------------------------
setbuffer Backbuffer()
player=CreatePivot()
camera=CreateCamera(player)
RotateEntity player,0,180,0
MoveEntity camera,0,playerheight,0

camerarange camera,1,5000
CameraFogMode camera,0
CameraFogColor camera,210,200,150
CameraFogRange camera,200,1000
AmbientLight 35,30,40

CameraClsMode camera,True,True

ClearTextureFilters

MoveMouse GraphicsWidth()/2,GraphicsHeight()/2


;-------------------------
; Set up Sprite Control
; and our flares
;-------------------------
Global ViewX=GraphicsWidth(),ViewY=GraphicsHeight()
Global ViewAspect# = float(viewx)/float(viewy)

global FlareRed, FlareGreen, FlareBlue
dim Flare(15)

spritecamera = camera
spritepivot = CreateSpritePivot(spritecamera,1.01)
SetupFlares("Media\lens-flares.jpg")


;-------------------------
;  Load the Scene
;-------------------------
Scene = LoadAnimMesh("Media\Scene.b3d")
Sun = FindChild(Scene,"Sun")
Sky = FindChild(Scene,"Sky")
SkyTurn# = 0.0


;----------------------------------------------
;  Set PickModes...
;    Use 1 for flare sources
;    Use 2 for objects that can block the sun
;----------------------------------------------
restore PickSettings
repeat
		read ChildName$, PickMode
		if ChildName&lt;&gt;"" then EntityPickMode FindChild(scene,ChildName$),PickMode
until ChildName=""



.PickSettings
data "Sun", 1
data "Landscape", 2
data "Stand 1 Upper", 2
data "Stand 1 Lower", 2
;data "Stand 1 Frame", 2
data "Stand 2 Upper", 2
data "Stand 2 Lower", 2
;data "Stand 2 Frame", 2
data "Stand 3 Upper", 2
data "Stand 3 Lower", 2
;data "Stand 3 Frame", 2
data "Stand 4 Upper", 2
data "Stand 4 Lower", 2
;data "Stand 4 Frame", 2
data "", 0



;MAIN LOOP * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

While Not KeyHit(1)
	PlayerControls()

	rate# = RatePerFrame#(0.0005, 240.0)	
	skyTurn = SkyTurn + rate
	if SkyTurn&gt;=360.0 then skyTurn=SkyTurn-360.0
	TurnEntity Sky,0,SkyTurn,0

	PointEntity Sun, Camera

	UpdateFlare(camera, Sun)

	Sync()

Wend

End

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

function Sync()

   UpdateWorld
	RenderWorld

	time = millisecs() - FrameTime
	FrameTime = millisecs()
	FPS = 1000.0/float(time)

	text 50,0,"FPS: " + FPS
	
	vwait : Flip false

end function


function RatePerFrame#(delta# = 1.0, secs# = 1.0)
	rate# = delta / float(secs * FPS)
	return rate#
end function

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *


Function PlayerControls()

	;***
	;***  Free Look
	;***

	mxspd# = MouseXSpeed()*0.25
	myspd# = MouseYSpeed()*0.25

	MoveMouse GraphicsWidth()/2,GraphicsHeight()/2

	targetpitch = targetpitch + myspd
	targetpitch = clampvaLUE(targetpitch, -85,85)
	targetyaw = targetyaw - mxspd	

	cam_pitch = cam_pitch + (targetpitch - cam_pitch)/8.0
	cam_yaw = cam_yaw + (targetyaw - cam_yaw)/8.0
	
	RotateEntity player,0,cam_yaw,0
	RotateEntity camera,cam_pitch,0,0

	;***
	;***  Movement
	;***
	
	If KeyDown(203) Then mvx=mvx-.25
	If KeyDown(205) Then mvx=mvx+.25
	If KeyDown(200) Then mvz=mvz+.25
	If KeyDown(208) Then mvz=mvz-.25
	
	If KeyDown(30) Then mvx=mvx-.25
	If KeyDown(32) Then mvx=mvx+.25
	If KeyDown(17) Then mvz=mvz+.25
	If KeyDown(31) Then mvz=mvz-.25
	
	TranslateEntity player,0,mvy,0
	MoveEntity player,mvx,0,mvz

	mvx=mvx/1.2
	mvy=mvy/1.2
	mvz=mvz/1.2
	
End Function


;-----------------------------------------------------

function ClampValue(Original#, low#, high#)
	if Original&lt;low  then return low
	if Original&gt;high then return high
	return Original
end function

;-----------------------------------------------------

Function UpdateFlare(cam_entity,source)

	cameraProject cam_entity,EntityX(source,True),EntityY(source,True),EntityZ(source,True)
	SourceX# = ProjectedX#()
	SourceY# = ProjectedY#()
	x# = SourceX/viewx
	y# = SourceY/viewy

	text 40,0,"X,Y:  " + X + ", " + Y

	SeeSource = camerapick(cam_entity,SourceX,SourceY)
	if ((SeeSource = Source) or (SeeSource = 0)) and entityinview(source,cam_entity) and (x&gt;0 And x&lt;=1) And (y&gt;0 And y&lt;=1)

      GetFlareColor(cam_entity, source, SourceX, SourceY)
      
		scale# = ViewX/800.0
		restore FlareData
		read TotalFlares		
		for f=1 to TotalFlares
			read Distance, FlareSize, ColorInfluence#, Alpha#, Frame
			flare_x# = SourceX - (((x-0.5)*2.0)*Distance)
			flare_y# = SourceY - (((y-0.5)*2.0)*(Distance/ViewAspect))

			r = (ColorInfluence * FlareRed   + 255.0*(1.0-ColorInfluence))
			g = (ColorInfluence * FlareGreen + 255.0*(1.0-ColorInfluence))
			b = (ColorInfluence * FlareBlue  + 255.0*(1.0-ColorInfluence))
			entitycolor Flare(f), r,g,b



			FlareSize = FlareSize * scale * (x + y + (cos(Distance*0.45)/2.0) + 0.5)
   		ResizeImage3D Flare(f),FlareSize,FlareSize

			if lowest#(x,y)&lt;0.1 then
				EntityAlpha Flare(f),Alpha * lowest#(x,y)/0.2
			else
				EntityAlpha Flare(f),Alpha
			endif

			DrawImage3D Flare(f),flare_x,flare_y,Frame
 	  		ShowEntity Flare(f)
		next
		
   else
 	   restore FlareData
 	   read TotalFlares
 	   for f=1 to TotalFlares
 	  		HideEntity Flare(f)
	   next

   endif

End Function



; FLARE DATA:   Distance, FlareSize, ColorInfluence#, Alpha#, Frame
;
; Distance       - Maximum offset from source
; FlareSize      - Maximum size of flare
; ColorInfluence - How strongly colour of source affects the flare's colour
; Alpha          - Maximum alpha level (0.0 to 1.0)
; Frame          - Frame of the lens flare texture (1 to 16)

.FlareData
Data 15		
Data -100, 400, 1.00, 0.40,  1  ;Red Crescent (50% to 0%)
Data  -95,  35, 0.80, 0.40,  2  ;Orange/Yellow Gradient (80% to 0%)
Data    0, 130, 0.60, 1.00,  3  ;Bright Flare Center (100%)
Data   95,  35, 0.35, 0.50,  4  ;Purple Disc (60%)
Data  150,  45, 0.50, 0.30,  5  ;Blue Disc (60%)
Data  120,  70, 0.60, 0.40,  6  ;Blue Gradient (50% to 20%)
Data  200,  20, 0.90, 0.40,  7  ;Orange Disc (80%)
Data  250,   8, 0.10, 1.00, 15  ;Sharp point (100%)
Data  280,  15, 0.15, 1.00,  8  ;Fuzzy Star (100%)
Data  345,  80, 0.90, 0.20,  7  ;Orange Disc (50%)
Data  390,  60, 0.80, 0.60,  7  ;Orange Disc (80%)
Data  395,  30, 0.40, 0.40,  9  ;Green Disc (80%)
Data  460, 100, 0.90, 0.40, 10  ;Orange Gradiant (90% to 0%)
Data  550, 160, 0.70, 0.50, 11  ;Yellow Ring (90%) with Green Gradient (60% to 0%)
Data  800, 350, 0.80, 0.30, 12  ;Rainbow Halo (outside to in:  Red,Orange/Yellow,Violet) (50%)

;-----------------------------------------------------------

function SetupFlares(filename$)

	FirstFlare = LoadAnimImage3D(filename$,4,4,1)
	restore FlareData
	read TotalFlares
	for i=1 to TotalFlares
 		 read Distance, FlareSize, ColorInfluence#, Alpha#, Frame

		 Flare(i) = CopyImage3D(FirstFlare)
		
		 ResizeImage3D Flare(i),FlareSize,FlareSize
		 EntityAlpha Flare(i),Alpha
		 EntityBlend Flare(i),3
		 EntityOrder Flare(i),-100-i
		 midhandle3D Flare(i)
		 entitycolor Flare(i),255,255,255
		 HideEntity Flare(i)
	next
	FreeImage3D(FirstFlare)

end function

;-----------------------------------------------------------

function GetFlareColor(cam_entity, source, SourceX, SourceY)

	;  This function is responsible for the majority of
	;  resource drain.  For just the sun, it may be an acceptable
	;  slowdown.  For things like candles and indoor lights
	;  this feature should probably not be used.

	CameraProjMode cam_entity, 0

	sample_cam = createcamera()

	positionentity sample_cam, EntityX(player,true),playerheight,EntityZ(player,true)
	pointentity sample_cam, source
	CameraProjMode sample_cam, 2
	
	if SourceX&lt;0 then SourceX=0
	if SourceY&lt;0 then SourceY=0
	cameraviewport sample_cam, SourceX, SourceY, 1,1
	
	for f=1 to 15 : hideentity Flare(f) : next
					
	updateworld
	RenderWorld
	
	getcolor SourceX,SourceY
	FlareRed   = ColorRed()
	FlareGreen = ColorGreen()
	FlareBlue  = ColorBlue()
	color 255,255,255

	CameraProjMode cam_entity, 1
	FreeEntity sample_cam
	
end function


;-----------------------------------------------------------

function lowest#(val1#, val2#)

   if val1&lt;val2 then
		return val1
   else
      return val2
	endif
	
end function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Very nice lenflare !!! thanks to release code !! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Barliesque</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Thank you!  :)<br><br>I just made a small change to correct an error in GetFlareColor() where it was off a little bit from the centre of the flare.<br><br><i>positionentity sample_cam, EntityX(camera,true),EntityY(camera,true),EntityZ(camera,true)</i><br><br>...changed to:<br><br><i>positionentity sample_cam, EntityX(player,true),playerheight,EntityZ(player,true)</i><br><br>...though I'm not clear why this would fix it!  :S <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >napole0n</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Very nice effect! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >elseano</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Best lens flare effect I have seen. Ever. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >aCiD2</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> This is incredible.... Wow *Attempts to shut gob smacked mouth* I can't believe this is open source :) Funkkkyyyy <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bot Builder</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Nice!!! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >carpman</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Hi Barliesque,<br>It didn't take you long to get used to Blitz then!!<br>A really nice piece of work.  I've given it a test in one of my space themes and it looks stunning.<br><br>I'm going to try and write it into my TrainSim, it should give some nice effects for early morning and late evening running.<br><br>I've managed to get a locomotive working and looking quite good on the Port Daniel line but no coaches yet, so passengers are still a bit irate!!!<br>Return tickets holders are now certain of being returned to where they started from because they never leave in the first place!!!<br><br>Thanks again.   Carpman <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kel</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Woah, i get 90 fps almost constantly on my pIII 800, GF MX440. quite impressive... i like the effect of the blending colors, superb stuff! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pedro</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> It is brillant.<br><br>I will use it and I will credit you.<br><br>thanks <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >WendellM</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Wow, *very* pretty - thanks for sharing! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Red</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Oh ! Another sweet effect ! <br><br>- reallistic lens FX<br>- paper pen FX<br>- black light FX<br>- sand of time FX<br>- motion blur FX<br>- glow FX <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Barliesque</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Thank you all!  Really great to read all your comments!<br>I look forward to seeing it in use in your projects.<br><br><div class="quote"> Woah, i get <b>90 fps</b> almost constantly on my pIII 800, GF MX440. quite impressive... i like the effect of the blending colors, superb stuff!  <br></div><br>Excellent news.  I was wondering how well the flare colouring effects would work with other systems.  I was amazed I could get away with rendering the scene a second time (even if it is just for a single pixel) and still manage a strong frame rate.  <b>I love Blitz3D!!!</b><br><br><div class="quote"> Return tickets holders are now certain of being returned to where they started from because they never leave in the first place!!! <br></div><br>Just like the real thing!  You are a stickler for realism, Carpman.  ;) <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Barliesque</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Recently, a user was having difficulty integrating the code into his project.  So here are some notes I wrote up to help him solve the trouble.  Maybe others will find this helpful:<br><br>1)  Be sure to include the file "Sprite Control.bb"<br><br>2)  Two globals are being used by <i>GetFlareColor()</i><br><pre class=code>      Global player
      Global playerheight#=32</pre><br>They'll need to be defined in your main program (or you may prefer to make them parameters of that function, rather than relying on globals.  Looking back at the code now, I'm not sure why I did it the way I did!)<br><br>3)  The following bit of code from the demo contains global declarations that are essential.  This code should come <b>AFTER</b> your graphics3D command, so that ViewX and ViewY are correctly set.<br><br><pre class=code>        ;-------------------------
        ; Set up Sprite Control
        ; and our flares
        ;-------------------------
        Global ViewX=GraphicsWidth(),ViewY=GraphicsHeight()
        Global ViewAspect# = float(viewx)/float(viewy)

        global FlareRed, FlareGreen, FlareBlue
        dim Flare(15)

        spritecamera = camera
        spritepivot = CreateSpritePivot(spritecamera,1.01)
        SetupFlares("Media\lens-flares.jpg")</pre><br>4) Of course, the last line above needs to be changed if the lens flare image is in a different location.<br><br>5)  In your main program loop, you need only one command:<br><pre class=code>        UpdateFlare(camera, Sun)  </pre><br>...where "camera" is the camera entity, and "Sun" is the entity of a pivot where the sun should be.  Note alse these lines from the intialisation part of the demo:<br><pre class=code>        Scene = LoadAnimMesh("Media\Scene.b3d")
        Sun = FindChild(Scene,"Sun")</pre><br>An object within the hierarchy of the sample scene model was named "Sun."  This statement finds that entity so that the flare functions can refer to it as a "light source" <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >shamanenCoder</td><td align="right"><font class=tiny>2005</font></td></tr></table></td></tr><tr ><td class="posttext"> great! thx a lot! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Viperfish</td><td align="right"><font class=tiny>2005</font></td></tr></table></td></tr><tr ><td class="posttext"> Awesome!<br><br>Here it is at work in my project;<br><img src="http://www.johnpitchers.com.au/traffika/screenshot.jpg"> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Barliesque</td><td align="right"><font class=tiny>2005</font></td></tr></table></td></tr><tr ><td class="posttext"> Ahhh, great!  Nice to see it in use.  Thanks for posting the pretty screenshot. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
