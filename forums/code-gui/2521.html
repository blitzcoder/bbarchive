<!DOCTYPE html><html lang="en" ><head ><title >Vector Editor</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Edit a vector with any number of components, language=bmx, category=BlitzPlus Gui'><meta name='author' content='JoshK'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=10>BlitzPlus Gui</a>/Vector Editor</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2521.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Vector Editor by JoshK</td><td align="right">2009 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Edit a vector with any number of components </td></tr><tr ><td class="cell"><pre class="code">SuperStrict

Import MaxGui.Drivers
Import "Spinner.bmx"

Rem
Local window:TGadget

window=CreateWindow("vectoredit Example",40,40,320,240)

Local label:TGadget
Local vectoredit:Tvectoredit
Local x:Int=4,y:Int=4

label=CreateLabel("vec3 with negative numbers:",x,y,200,18,window)
SetGadgetLayout label,1,0,1,0
y:+18
vectoredit=CreatevectorEdit(x,y,180,20,window)
SetVectorEditRange(vectoredit,-1,1)
SetVectorEditValue(vectoredit,[-1.0,0.0,0.5])
SetGadgetLayout vectoredit,1,1,1,0
y:+22

label=CreateLabel("vec4:",x,y,120,18,window)
SetGadgetLayout label,1,0,1,0
y:+18
vectoredit=CreatevectorEdit(x,y,180,20,window,4)
SetGadgetLayout vectoredit,1,1,1,0
y:+22


While True
	WaitEvent 
	Print CurrentEvent.ToString()
	Select EventID()
		Case EVENT_WINDOWCLOSE
			End
	End Select
Wend

EndRem

'-------------------------------------------------------------------


Type TVectorEdit Extends TProxygadget
	
	Field panel:TGadget
	Field spinner:TSpinner[]
	Field style:Int
		
	Method Cleanup()
		RemoveHook(EmitEventHook,EventHook,Self)
		Super.cleanup()
	EndMethod

	Method SetRange_(minimum:Double,maximum:Double,accuracy:Int=1)
		Local n:Int
		For n=0 To spinner.length-1
			spinner[n].SetRange_(minimum,maximum,accuracy)
		Next		
	EndMethod

	Method SetColor(r:Int,g:Int,b:Int)
		Local n:Int
		panel.SetColor(r,g,b)
		For n=0 To spinner.length-1
			spinner[n].SetColor(r,g,b)
		Next
	EndMethod
	
	Method SetTextColor(r:Int,g:Int,b:Int)
		Local n:Int
		panel.SetTextColor(r,g,b)
		For n=0 To spinner.length-1
			spinner[n].SetTextColor(r,g,b)
		Next
	EndMethod
	
	Method SetValue_(t:Float[])
		Local n:Int
		For n=0 To spinner.length-1
			SetSpinnerValue(spinner[n],t[n])
		Next
	EndMethod

	Method GetValue_:Float[]()
		Local t:Float[spinner.length]
		Local n:Int
		For n=0 To spinner.length-1
			t[n]=SpinnerValue(spinner[n])
		Next
		Return t
	EndMethod
	
	Function Create:TVectorEdit(x:Int,y:Int,width:Int,height:Int,group:TGadget,components:Int=3)
		Local vectoredit:Tvectoredit
		Local sw:Int
		Local k:Int
				
		vectoredit=New Tvectoredit
		'vectoredit.style=style
		
		vectoredit.panel=CreatePanel(x,y,width,height,group)
		
		sw=vectoredit.panel.ClientWidth()/components
		
		vectoredit.spinner=New TSpinner[components]
		
		For Local n:Int=0 To components-1
			vectoredit.spinner[n]=CreateSpinner(sw*n,0,sw,vectoredit.panel.ClientHeight(),vectoredit.panel)	
			SetSpinnerRange vectoredit.spinner[n],0,1,1
			Select n
			Case 0 SetGadgetLayout vectoredit.spinner[n],1,2,1,1
			Case components-1 SetGadgetLayout vectoredit.spinner[n],2,1,1,1
			Default SetGadgetLayout vectoredit.spinner[n],2,2,1,1
			EndSelect
		Next
				
		vectoredit.setproxy(vectoredit.panel)
		
		AddHook(EmitEventHook,EventHook,vectoredit)
		Return vectoredit
	EndFunction

	Function EventHook:Object(id:Int,data:Object,context:Object)
		Local event:TEvent
		Local vectoredit:Tvectoredit
		Local n:Int
		
		event=TEvent(data)
		If event
			vectoredit=Tvectoredit(context)
			If vectoredit
				Select event.id			
				Case EVENT_GADGETACTION
					For n=0 To vectoredit.spinner.length-1
						If event.source=vectoredit.spinner[n]
							EmitEvent CreateEvent(EVENT_GADGETACTION,vectoredit)
							Return Null
						EndIf
					Next
				EndSelect
			EndIf
		EndIf
		Return data
	EndFunction
	
EndType

Rem
bbdoc:
EndRem
Function CreateVectorEdit:TVectorEdit(x:Int,y:Int,width:Int,height:Int,group:TGadget,components:Int=3)
	Return TVectorEdit.Create(x,y,width,height,group,components)
EndFunction

Rem
bbdoc:
EndRem
Function VectorEditValue:Float[](vectoredit:TVectorEdit)
	Return vectoredit.GetValue_()
EndFunction

Rem
bbdoc:
EndRem
Function SetVectorEditRange(vectoredit:TVectorEdit,minimum:Double,maximum:Double,accuracy:Int=1)
	vectoredit.SetRange_(minimum,maximum,accuracy)
EndFunction

Rem
bbdoc:
EndRem
Function SetVectorEditValue:Int(vectoredit:TVectorEdit,t:Float[])
	Return vectoredit.SetValue_(t)
EndFunction</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
