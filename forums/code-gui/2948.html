<!DOCTYPE html><html lang="en" ><head ><title >Pixmap text panel</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='A proxy gadget panel with text on pixmap, language=bmx, category=BlitzPlus Gui'><meta name='author' content='Jur'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=10>BlitzPlus Gui</a>/Pixmap text panel</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2948.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pixmap text panel by Jur</td><td align="right">2012 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> A proxy gadget panel with text on pixmap </td></tr><tr ><td class="cell"><pre class="code">SuperStrict
Import MaxGui.Drivers
Import MaxGUI.ProxyGadgets


'rem
'DEMO

Local window:TGadget = CreateWindow( "Window",100, 100, 250, 250, Null, WINDOW_TITLEBAR|WINDOW_CLIENTCOORDS|WINDOW_RESIZABLE )
Local group:TGadget = CreatePanel(10,10,200,200,window,PANEL_GROUP,"Group Label")


Local Font:TImageFont = TImageFont.CreateDefault()
Local fontHeight:Int = ImageFontHeight(Font)   'because "Height" method doesnt give correct height
Local PixmapTextPanel:TPixmapTextPanel = New TPixmapTextPanel.Make("Test", 10,30,70,fontHeight,group,PANEL_ACTIVE,Font)
SetGadgetColor(PixmapTextPanel, 255,255,0)


Repeat
	WaitEvent()
	Print CurrentEvent.ToString()
	Select EventID()
		Case EVENT_WINDOWCLOSE, EVENT_APPTERMINATE
			End
		Case EVENT_MOUSEDOWN
			If EventSource()=PixmapTextPanel Then 
				Print "PixmapTextPanel clicked"
			EndIf
			
	End Select
Forever

'endrem



Type TPixmapTextPanel Extends TProxyGadget
	Field Panel:TGadget
	Field text:String
	Field font:TImageFont
	Field R:Int,G:Int,B:Int   'text color
	
	
	Method Make:TPixmapTextPanel(_text:String, x:Int, y:Int, w:Int, h:Int, Group:TGadget, style:Int, _font:TImageFont)
		
		Panel = CreatePanel(x,y,w,h,Group,style)
		SetProxy(Panel)
		font = _font
		SetText(_text)

		Return Self
	EndMethod
	
	
	Method SetTextColor(_R:Int,_G:Int,_B:Int)
		R=_R ; G=_G ; B=_B
		SetGadgetPixmap(Panel, MakeTextPixmap())
	EndMethod
	
	
	Method SetText(_text:String)
		text = _text
		If ImageFontTextWidth(text,font)&gt;GadgetWidth(Panel) Then
			text = ImageFontCutTextToWidth(text, GadgetWidth(Panel), font)
			text:+".."
		EndIf
		SetGadgetPixmap(Panel, MakeTextPixmap())
	EndMethod
	
	
	Method MakeTextPixmap:TPixmap()
	
		Local Pixmap:TPixmap = CreatePixmap(GadgetWidth(Panel), GadgetHeight(Panel), PF_RGBA8888)
		Pixmap.ClearPixels(0)
		
		Local xAdvance:Int
		For Local i:Int=0 Until text.Length
			Local n:Int = font.CharToGlyph(text[i])
			If n&lt;0 Continue
			
			Local Glyph:TImageGlyph = font.LoadGlyph(n)
			If Glyph._image Then
			 	Local GlyphPixmap:TPixmap = Glyph._image.Pixmaps[0]
				If GlyphPixmap Then
					Local tx:Float = glyph._x
					Local ty:Float = glyph._y
					
					For Local iy:Int = 0 Until GlyphPixmap.height
						For Local ix:Int = 0 Until GlyphPixmap.width
							If xAdvance + ix + tx &lt; Pixmap.width And iy + ty &lt; Pixmap.Height Then
								Local BGRA:Int = ReadPixel(GlyphPixmap, ix, iy)
								Local A:Int = (BGRA Shr 24) &amp; $FF
								BGRA = B | (G Shl 8) | (R Shl 16) | (A Shl 24)
								WritePixel(Pixmap, xAdvance + ix + tx, iy + ty, BGRA)
							EndIf
						Next
					Next
				EndIf
			EndIf
			xAdvance:+Glyph.Advance()
		Next
		
		Return Pixmap
	EndMethod
	
EndType



'some useful functions...

Function ImageFontHeight:Int(font:TImageFont)
	Local h:Int
	For Local ascii:Int=0 Until font._glyphs.length
		Local Glyph:TImageGlyph = font.LoadGlyph(ascii)
		If Glyph Then
			h=Max(h, Glyph._y+Glyph._h)
		EndIf
	Next
	Return h
EndFunction

Function ImageFontTextWidth:Int(text:String, Font:TImageFont)
	Local width:Int = 0
	For Local n:Int = 0 Until text.Length
		Local I:Int = font.CharToGlyph(Text[n])
		If I &lt; 0 Continue
		width:+font.LoadGlyph(I).Advance()
	Next
	Return width
End Function

Function ImageFontCutTextToWidth:String(text:String, widthMax:Int, Font:TImageFont)
	Local width:Int = 0
	For Local n:Int = 0 Until text.Length
		Local i:Int = font.CharToGlyph(Text[n])
		If i &lt; 0 Continue
		width:+font.LoadGlyph(I).Advance()
		If width&gt;widthMax Then
			Return text[..n-1]
		EndIf
	Next
	Return text
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
