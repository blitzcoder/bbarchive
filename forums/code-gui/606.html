<!DOCTYPE html><html lang="en" ><head ><title >StartupPlus  - UPDATED for BlitzPlus v1.34</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Simple 'Display Options' startup window, language=bb, category=BlitzPlus Gui'><meta name='author' content='EOF'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=10>BlitzPlus Gui</a>/StartupPlus  - UPDATED for BlitzPlus v1.34</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="606.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >StartupPlus  - UPDATED for BlitzPlus v1.34 by EOF</td><td align="right">2003 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Simple to use display options window.<br>Just place Include "StartupPlus.bb" at the beginning of the program and call one function.<br>The GUI will also display an optional 'logo.jpg' image if found in the current directory.<br>The image size viewed in the panel is 128x128.<br><br><img src="http://homepage.ntlworld.com/config/startupplus/startupplus.jpg"> <img src="http://homepage.ntlworld.com/config/startupplus/startupplus2.jpg"><br><br><br>Once the user chooses the display options and hits start, the window closes and the graphics mode will be set.<br>SetBuffer BackBuffer() is also called.<br><br><br>``````````````````````<br>Function<br>``````````````````````<br><br>NB: ALL PARAMETERS ARE OPTIONAL<br>Default values are:<br><br>title -&gt; BlitzPlus Example<br>graphics mode -&gt; 640x480 16 Bit<br>graphics driver -&gt; Native<br><br><br>result=SetDisplay(title$,width,height,depth,fullscreenflag,driver)<br><br>Parameters:<br>-----------<br> title$ - Displayed in the main window<br> width - default graphics width to use<br> height - default graphics height to use<br> depth - depth required<br> fullscreenflag - True means use full screen else windowed<br> driver - graphics driver to use as default<br><br>About<br>---------------------------------<br>Opens up a display options window.<br>If the user closes/quits the window the the result returned is 0.<br><br><br>Download <a href="http://homepage.ntlworld.com/config/startupplus/startupplus.zip" target="_blank">source code and example</a><br><br>Keyboard Shortcuts:<br>-------------------<br>RETURN is equivalent to clicking the [Start] button<br>ESC is equivalent to closing the window </td></tr><tr ><td class="cell"><pre class="code">EXAMPLE USAGE
=============



; StartupPlus - Example
; Syntax Error

Include "StartupPlus.bb"

If SetDisplay("SuperDemo 2003",512,384,16,False) = 0 End
sw=GraphicsWidth() : sh=GraphicsHeight() : sd=GraphicsDepth()

x=40 : y=90 : dx=4 : dy=3
size=sw*0.15
timer=CreateTimer(60)

While Not KeyHit(1) Or MouseHit(1)
	WaitTimer timer
	Cls
	x=x+dx : y=y+dy
	If x&lt;=0 Or x&gt;=sw-size Then dx=-dx
	If y&lt;=0 Or y&gt;=sh-size Then dy=-dy
	Rect x,y,size,size
	Text 5,5,"Driver: "+GfxDriverName$(GFXDriver)
	Text 5,25,"Width="+sw+" Height="+sh+" Depth="+sd
	Flip
Wend

End





THE INCLUDE (StartupPlus.bb)
============================



; StartupPlus by Syntax Error
; Updated for BlitzPlus v1.34

; include file 'StartupPlus.bb"

Global GFXDriver=1

; set program display graphics
Function SetDisplay(title$="BlitzPlus Example",w=640,h=480,d=16,full=0,driver=1)
	Local xo=160
	If FileType("logo.jpg")=0 Then xo=0
	win = CreateWindow(title$,307,322,xo+164,182,Desktop(),1)
	; Graphics Mode
	Label1 = CreateLabel("Graphics Mode",xo+36,8,74,14,win,0)
	Combo1 = CreateComboBox(xo+8,26,138,100,win,0)
	AddGadgetItem Combo1,"Choose Resolution..."
	For g=1 To CountGfxModes()
		mode$=Str$(GfxModeWidth(g))+" x "+Str$(GfxModeHeight(g))+"  "+Str$(GfxModeDepth(g))+"Bit"
		AddGadgetItem Combo1,mode$
		If w=GfxModeWidth(g) And h=GfxModeHeight(g) And d=GfxModeDepth(g)
			SelectGadgetItem Combo1,g
		EndIf
	Next
	If SelectedGadgetItem(Combo1)=-1 Then SelectGadgetItem Combo1,0
	; Graphics Driver
	Label2 = CreateLabel("Graphics Driver",xo+36,52,74,14,win,0)
	Combo2 = CreateComboBox(xo+8,70,138,100,win,0)
	For g=1 To CountGfxDrivers()
		AddGadgetItem Combo2,GfxDriverName$(g)
	Next
	If driver&lt;1 Then driver=1
	If driver&gt;CountGfxDrivers() Then driver=CountGfxDrivers()
	SelectGadgetItem Combo2,driver-1
	; FullScreen / Windowed
	RadioButton1 = CreateButton("FullScreen",xo+4,98,76,20,win,3)
	RadioButton2 = CreateButton("Windowed",xo+80,98,70,20,win,3)
	SetButtonState RadioButton1,full=True
	SetButtonState RadioButton2,full=False
	; Start Button
	Button = CreateButton("Start",xo+4,120,140,26,win,0)
	; Graphic Image
	If xo&gt;0
		panel = CreatePanel(16,14,128,128,win)
		SetPanelImage panel,"logo.jpg"
	EndIf
	
	; await user response
	Repeat
	Select WaitEvent()
		Case $103 ; key stroke
			If EventData()=27 Exit
			If EventData()=13
				If SelectedGadgetItem(Combo1)=0
					Notify "Please choose a Graphics mode"
				Else
					startflag=True : Exit
				EndIf
			EndIf
		Case $803 ; [x] close window
			Exit
		Case $401 ; gadget event
			If EventSource()=Button
				If SelectedGadgetItem(Combo1)=0
					Notify "Please choose a graphics mode"
				Else
					startflag=True : Exit
				EndIf
			EndIf
	End Select
	Forever

	; read combobox selections
	g=SelectedGadgetItem(Combo1)
	GFXDriver=SelectedGadgetItem(Combo2)+1
	If g&gt;0	
		w=GfxModeWidth(g) : h=GfxModeHeight(g) : d=GfxModeDepth(g)
		full=ButtonState(radiobutton1)
	EndIf
	
	; close GUI and created graphics display
	FreeGadget win
	If full=0 Then full=2 ; windowed
	If startflag=True
		AppTitle title$
		SetGfxDriver GFXDriver
		Graphics w,h,d,full
		SetBuffer BackBuffer()
	EndIf
	Return startflag
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
