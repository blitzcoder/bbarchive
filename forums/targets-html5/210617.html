<!DOCTYPE html><html lang="en" ><head ><title >OnUpdate() not being called often if...</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >OnUpdate() not being called often if...</h1><a href="forums.php" >Monkey Targets Forums</a>/<a href="topics.php?forum=504" >HTML5</a>/<a href="#bottom" >OnUpdate() not being called often if...</a><br><br>
<a name="2120696"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Salmakis</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> OnUpdate() not being called often if the canvas is not visible (i.e. if im in another tab in the browser (firefox in this case)), or if browser is minized etc.<br>i also found out that the javascript setInterval() function also is not working fine when the tab where the game is in is not visible, the time gets shifted compared to a tab that is all time open.<br>i think OnUpdate uses the setIntervall stuff? so this might be the reason then?<br><br>is there any trick to force it to keep it calling 30 updates per second even if the tab is not open/minimized/on other desktop?<br><br>Or is the workaround to count the updates and calculate them with the time, and let them all come after if the user returns to the tab with the game? <br><br></td></tr></table><br>
<a name="2120723"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImmutableOctet(SKNG)</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> The exact approach is up to you, but have you tried setting 'MOJO_AUTO_SUSPEND_ENABLED' to 'False'?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
#MOJO_AUTO_SUSPEND_ENABLED = False
</textarea> <br><br></td></tr></table><br>
<a name="2120737"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Salmakis</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> yes i have that setting on false <br><br></td></tr></table><br>
<a name="2120745"></a>

<a name="2120746"></a>

<a name="2120747"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >k.o.g.</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Use a Webworker :D<br><br>It is not 100% tested! and not fully finished<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict

#If MOJO_AUTO_SUSPEND_ENABLED = False And TARGET = "html5" Then
Private
Import dom.dom

Extern Private
#If CONFIG = "debug" Then
	Function log:Void(str:String) = "window.console.log"
#Else
	Function log:Void(str:String) = "(function(str){})"
#End

	'Function evalBlob:Blob(cmd:String) = "window.eval"
	Function createObjectURL:String(blob:Blob) = "window.URL.createObjectURL"
	Function revokeObjectURL:Void(url:String) = "window.URL.revokeObjectURL"
	
	Function GenerateBlob:Blob(script:String) = "(function(script){return new Blob([script], {type: 'text/javascript'});})"
	Function DelegateUpdate:Void(instance:BBGame) = "(function(game){if(game._debugExs){try{game._delegate.UpdateGame();}catch(ex){game.Die(ex);}}else{game._delegate.UpdateGame();}})"

	Class BBGame Extends Null = "BBGame"
		Function Game:BBGame()
		Field _started:Bool
		Field _updateRate:Int
	End
	
	Class Blob Extends Null = "Blob"
	End
	
	Class CmdObject Extends Null = "Object"
		Field cmd:String
		
		Field updaterate:Int
		Field active:Int
	End
	
	Class CustomEvent Extends Event = "Event"
		Field data:CmdObject = "data"
	End
	
	Function NewWorker:Worker(url:String) = "new Worker"
	Class Worker Extends EventTarget = "Worker"
		Method postMessage:Void(message:CmdObject)
	End

	Global MonkeyWindowIsActive:Bool
	Global MonkeyUpdateThreadInstance:UpdateThread
Public

Class UpdateThread Extends EventListener
Private
	Field _worker:Worker
	Field _url:String
	Field _game:BBGame
	
	Field _cmd:CmdObject = New CmdObject()
	
	Method New()
	End
	
	Function Instance:UpdateThread()
		If MonkeyUpdateThreadInstance = Null MonkeyUpdateThreadInstance = New UpdateThread()
		Return MonkeyUpdateThreadInstance
	End
	
	Method UpdateGame:Void()
		If _game = Null Return
		
		If _game._started DelegateUpdate(_game)
	End
	
	Method SendUpdate:Void()
		If _worker = Null Return
		
		_cmd.cmd = "change"
		_cmd.updaterate = _game._updateRate
		_cmd.active = 1 - MonkeyWindowIsActive
		_worker.postMessage(_cmd)
	End
	
	Method SetUpdateRate:Void(fake:Bool = False)
		If fake = False SendUpdate()
	End
	
	Method handleEvent:Int( event:Event )
		Local e:CustomEvent = CustomEvent(event)
		If e.type &lt;&gt; "message" log(e.type)
		Select event.type
			Case "loaded"
				
			Case "message"
				Select e.data.cmd
					Case "tick"
						UpdateGame()
				End
				
			Case "focus"
				MonkeyWindowIsActive = True
				SendUpdate()
			Case "blur"
				MonkeyWindowIsActive = False
				SendUpdate()
		End
		
		Return 1
	End
	
	Function WrapperScript:String()
		Local script:String = ""
		
		script += "/* Author: k.o.g. (c) 2016 */~n"
		script += "var MonkeyUpdateThreadInstance=null;"
		script += "var MonkeyWindowIsActive=" + document.hasFocus() * 1 + ";"
		script += "(function(){"
		script += "~quse strict~q;"
		script += "BBHtml5Game.prototype.UpdateGame_org=BBHtml5Game.prototype.UpdateGame;"
		script += "BBHtml5Game.prototype.UpdateGame=function(){"
		script += "if(MonkeyWindowIsActive) BBHtml5Game.prototype.UpdateGame_org.call(this);"
		script += "};"
		script += "BBHtml5Game.prototype.SetUpdateRate_org=BBHtml5Game.prototype.SetUpdateRate;"
		script += "BBHtml5Game.prototype.SetUpdateRate=function(updateRate){"
		script += "BBHtml5Game.prototype.SetUpdateRate_org.call(this, updateRate);"
		script += "MonkeyUpdateThreadInstance.p_SetUpdateRate();"
		script += "}"
		script += "})();"
		
		Return script
	End
	
	Function WorkerScript:String()
		Local script:String = ""
		
		script += "(function(){"
		script += "  ~quse strict~q;"
		script += "  var _timer = 0;"
		script += "  var _updaterate = 60;"
		script += "  var _active = 0;"
		script += "  function Setup(){"
		script += "    if(_timer) clearInterval(_timer);"
		script += "    _timer = 0;"
		script += "    if(_active) {"
		script += "      _timer = setInterval(function(){"
		script += "        self.postMessage({'cmd':'tick'});"
		script += "      }, (1000 / _updaterate) / 2);"
		script += "    }"
		script += "  }"
		script += "  self.addEventListener('message', function(event){"
		script += "    var data = event.data;"
		script += "    switch(data.cmd) {"
		script += "      case 'change':"
		script += "        _updaterate = data.updaterate;"
		script += "        _active = data.active;"
		script += "        Setup();"
		script += "      break;"
		script += "    }"
		script += "  });"
		script += ""
		script += "  self.postMessage({'cmd':'loaded'});"
		script += "})();"
		
		Return script
	End
	
	Method Do:Void()
		SetUpdateRate(True)
		
		_game = BBGame.Game()
		
		window.addEventListener("blur", Self, False)
		window.addEventListener("focus", Self, False)

		' Evil
		'Local s:String = "new Blob(['"+WorkerScript().Replace("'", "\'")+"'], {type: 'text/javascript'});"
		'Local data:Blob = evalBlob(s)
		
		Local data:Blob = GenerateBlob(WorkerScript())
		_url = createObjectURL(data)
		_worker = NewWorker(_url)
		_worker.addEventListener("message", Self, False)

		SendUpdate()
	End
	
	Method Clear:Void()
		revokeObjectURL(_url)
		_url = ""
	End
	
Public
	Function Initialize:Void()
		#If CONFIG = "debug" Then
		Local workerScriptElement:HTMLElement = HTMLElement(document.createElement("script"))
		workerScriptElement.setAttribute("id", "MonkeyUpdateThread")
		workerScriptElement.setAttribute("type", "javascript/worker")
		workerScriptElement.innerHTML = WorkerScript().Replace(";", ";~n")
		document.head.appendChild(workerScriptElement)
		#End

		Local wrapperScriptElement:HTMLElement = HTMLElement(document.createElement("script"))
		wrapperScriptElement.setAttribute("id", "MonkeyUpdateWrapper")
		wrapperScriptElement.setAttribute("type", "text/javascript")
		wrapperScriptElement.setAttribute("data-info", "http://www.monkey-x.com/")
		wrapperScriptElement.innerHTML = WrapperScript()
		document.body.appendChild(wrapperScriptElement)
		
		Instance().Do()
	End
	
	Function Free:Void()
		Instance().Clear()
	End
	
	Function ThreadActive:Bool()
		Return MonkeyWindowIsActive = 0
	End
End
#Else
Class UpdateThread
	Function Initialize:Void()
	End
	
	Method Free:Void()
	End
	
	Function ThreadActive:Bool()
		Return False
	End
End
#End</textarea><br><br>Use in Main() the function UpdateThread.Initialize() <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
