<!DOCTYPE html><html lang="en" ><head ><title >Catmull Rom splines</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='A short demo showing catmull rom splines, language=bmx, category=Graphics'><meta name='author' content='Warpy'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/Catmull Rom splines</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1522.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Catmull Rom splines by Warpy</td><td align="right">2005 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This is a short demo that shows how catmull rom splines work. The only bit that's specific to Catmull Rom splines is the For..Next loop; the rest is just bumf to make a demo that actually shows something. Quick explanation of how to get x,y values out of the textbook function at the end. </td></tr><tr ><td class="cell"><pre class="code">Rem
CATMULL ROM SPLINE

A Catmull Rom spline is a kind of curve. 
If you have a set of points , you can use Catmull Rom to construct
a nice curvy line joining them all up. It has the property of local
control , meaning that moving one point will only affect the piece of 
the line right next to it , and nothing further away than the next point.
It also doesn't require you to define a tangent at each point, unlike 
a bezier curve. Instead , the algorithm uses the points before and after
the segment it's working on to make a good curve.

THE ORIGINAL FUNCTION

Given 4 points on the spline P0 , P1 , P2 and P3 , q(t) gives you a point on
the bit of the curve between P1 and P2. q(0) is always equal to P1 and q(1)
is always equal to P2. It is not necessarily true that q(0.5) is halfway 
along the length of the curve, by distance.

q(t) = 0.5 *(
   				(2 * P1) +
  				(-P0 + P2) * t +
				(2*P0 - 5*P1 + 4*P2 - P3) * t2 +
				(-P0 + 3*P1- 3*P2 + P3) * t3
			)
			
Where P0 / 1 / 2 / 3 are the 4 control points on the spline , and q(t) is the point
on the curve at position t , where t = 0 is the start of the curve , and t = 1 is 
the end of the curve.

To actually use this, just split it into x and y parts, so:

qx(t) = 0.5 *(
   				(2 * P1x) +
  				(-P0x + P2x) * t +
				(2*P0x - 5*P1x + 4*P2x - P3x) * t2 +
				(-P0x + 3*P1x- 3*P2x + P3x) * t3
			)

qy(t) = 0.5 *(
   				(2 * P1y) +
  				(-P0y + P2y) * t +
				(2*P0y - 5*P1y + 4*P2y - P3y) * t2 +
				(-P0y + 3*P1y- 3*P2y + P3y) * t3
			)
			
Where each point PN has components PNx and PNy.

Clearly you can't compute every single point on the curve as there are infinitely many, so
what we do is compute the function for a certain number of values of t between 0 and 1, to
get a good enough approximation.


THE ALGORITHM

The way I've implemented the algorithm here is by defining two types: point and romspline.
A point is just an easy way of keeping track of points in 2d space - it has an x field
and a y field, that's all.
The romspline type holds all the information necessary to draw a spline. You create a romspline
object and then add points to it using the addpoint method. Calling the draw method draws the
spline on the screen.
What it does is start at the first 4 points in the spline. It then computes the catmull rom 
function at a number of values of t between 0 and 1, drawing a straight line between one and
the next. This gives a good approximation of the curve. Once that is done , it moves along one
point on the spline , so P0 becomes P1 , P1 becomes P2 , P2 becomes P3 , and P3 is the next point 
in the list. If there is no next point in the list , we're done and the function ends.
Because of the way the algorithm works , the part of the curve between the first and second 
points in the list and the penultimate and last points in the list won't be drawn. You can get 
around this by adding some more points to the start and end of the list.

EndRem


'This is just a little type to keep track of individual control points
Type point
	Field x#,y#
	
	Function create:point(x#,y#)
		p:point=New point
		p.x=x
		p.y=y
		Return p
	End Function
End Type

'Spline type
Type romspline

	Field points:TList 'the points list is a list of the control points to use in the spline
	
	Method New() '(This is called when the spline object is created, it just initialises the points list)
		points=New TList
	End Method
	
	Method addpoint(p:point) 'Call this to add a point to the end of the list
		points.addlast p
	End Method
	
	Method draw() 'Do the actual drawing!
	
		'Draw a rectangle at each control point so we can see them (not relevant to the algorithm)
		For p:point = EachIn points
			DrawRect p.x , p.y , 2 , 2
		Next
	
		num=points.count()
		If num&lt;4 Then Return 'Check there are enough points to draw a spline

		'Get the first three  TLinks in the list of points. This algorithm is going to work by 
		'working out the first three points, then getting the last point at the start of the
		'while loop. After the curve section has been drawn, every point is moved along one,
		'and the TLink is moved to the next one so we can see if it's null, and then get the next
		'p3 from it if not.
		pl:TLink = points.firstlink()
		p0:point = point(pl.value() )
		pl = pl.nextlink()
		p1:point = point(pl.value() )
		pl = pl.nextlink()
		p2:point = point(pl.value() )
		pl = pl.nextlink()
		'we'll work out what p3 is inside the loop
		
		While pl &lt;&gt; Null 'pl3 will be null when we've reached the end of the list
			
			'get the point objects from the TLinks
			p3:point = point(pl.value() )
			
			ox# = p1.x
			oy# = p1.y
			For t#=0 To 1 Step .01 'THE MEAT And BONES! Oddly, there isn't much to explain here, just copy the code.
				x# = .5 * ( (2 * p1.x) + (p2.x - p0.x) * t + (2 * p0.x - 5 * p1.x + 4 * p2.x - p3.x) * t * t + (3 * p1.x - p0.x - 3 * p2.x + p3.x) * t * t * t)
				y# = .5 * ( (2 * p1.y) + (p2.y - p0.y) * t + (2 * p0.y - 5 * p1.y + 4 * p2.y - p3.y) * t * t + (3 * p1.y - p0.y - 3 * p2.y + p3.y) * t * t * t)
				DrawLine ox , oy , x , y
				
				ox = x
				oy = y
				
			Next
			
			'Move one place along the list
			p0 = p1
			p1 = p2
			p2 = p3
			pl=pl.nextlink()
		Wend
	End Method
End Type

'Demo - create a spline and add points when the mouse is clicked

Graphics 800 , 800 , 0

s:romspline = New romspline

While Not KeyHit(KEY_ESCAPE)

	If MouseHit(1)
		s.addpoint(point.create(MouseX(),MouseY()))
	EndIf
	
	s.draw()
	Flip
	Cls
Wend</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>2005</font></td></tr></table></td></tr><tr ><td class="posttext"> Great ! many thanks ! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>2005</font></td></tr></table></td></tr><tr ><td class="posttext"> Might come in handy someday :-D <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> Here's an adapted version (same math) that lets you play with the splines a bit differently. Not quite the add-points-and-move-points, but you can step through each control point and move it with the mouse.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'Catmull rom splines
'Adapted from code by Warpy and Matt McFarland

'Press Space for a totally new random spline
'Press left arrow to control the previous control point
'Press right arrow to control the next control point
'Press Escape to exit
'Move  mouse to see it adapt

Strict

Type Point
	Field x:Int
	Field y:Int
End Type
Const Accuracy:Double=0.05		'Lower has more line segments
	
SeedRnd(MilliSecs())	'Different each time
SetGraphicsDriver GLMax2DDriver()
Graphics 640,480,0
SetBlend LIGHTBLEND
Local DoAnother:Int
Local ControlPoint:Int
Local Counter:Int
glEnable(GL_LINE_SMOOTH)	'Quick antaliasing hack
glHint(GL_LINE_SMOOTH_HINT,GL_NICEST)
glLineWidth(3.0)
Repeat
	DoAnother=False
	Local NumPoints:Int=Rand(4,10)	'Whatever you like &gt;3
	Local Points:Point[NumPoints]
	For Local p:Int=0 To NumPoints-1
		Points[p]=New Point
		Points[p].x=Rand(20,620)
		Points[p].y=Rand(20,460)
	Next
	ControlPoint=NumPoints/2
	Repeat
		Cls
		Points[ControlPoint].x=MouseX()
		Points[ControlPoint].y=MouseY()
		'Draw controls
		For Local p:Int=0 To NumPoints-1
			SetColor $88,$88,$88
			DrawLine Points[p].x-7,Points[p].y-7,Points[p].x+7,Points[p].y+7
			DrawLine Points[p].x-7,Points[p].y+7,Points[p].x+7,Points[p].y-7
			SetColor $00,$44,$88
			DrawLine Points[p].x,Points[p].y,Points[Min(NumPoints-1,p+1)].x,Points[Min(NumPoints-1,p+1)].y
			DrawText p,Points[p].x+5,Points[p].y+5
		Next
		'Draw segments
		Local PrevX:Double,PrevY:Double
		SetColor $FF,$FF,$FF
		Counter=0
		For Local S:Int=0 To NumPoints-4
			For Local T:Double=0 To 1 Step Accuracy
				Local X:Double=.5:Double*((2*Points[S+1].x)+(Points[S+2].x-Points[S].x)*T+(2*Points[S].x-5*Points[S+1].x+4*Points[S+2].x-Points[S+3].x)*T*T+(3*Points[S+1].x-Points[S].x-3*Points[S+2].x+Points[S+3].x)*T*T*T)
				Local Y:Double=.5:Double*((2*Points[S+1].y)+(Points[S+2].y-Points[S].y)*T+(2*Points[S].y-5*Points[S+1].y+4*Points[S+2].y-Points[S+3].y)*T*T+(3*Points[S+1].y-Points[S].y-3*Points[S+2].y+Points[S+3].y)*T*T*T)
				If PrevX=0 And PrevY=0
					PrevX=X
					PrevY=Y
				EndIf
				DrawLine PrevX,PrevY,X,Y,False
				PrevX=X
				PrevY=Y
				Counter:+1
			Next
		Next
		DrawLine PrevX,PrevY,Points[NumPoints-2].x,Points[NumPoints-2].y
		DrawText "Edges: "+String(Counter),0,10
		DrawText "Controls: "+String(NumPoints),0,20
		DrawText "Curves: "+String(NumPoints-3),0,30
		Flip
		If KeyHit(KEY_SPACE) Then DoAnother=True
		If KeyHit(KEY_LEFT) Then ControlPoint=Max(0,ControlPoint-1)
		If KeyHit(KEY_RIGHT) Then ControlPoint=Min(NumPoints-1,ControlPoint+1)
	Until KeyHit(KEY_ESCAPE) Or DoAnother=True
Until DoAnother=False
</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warpy</td><td align="right"><font class=tiny>2007</font></td></tr></table></td></tr><tr ><td class="posttext"> Updated with more explanation. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Blitzplotter</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> This is very useful, looking at how to apply it to my raw graph data &amp; smooth it before passing the BMax smoothed data into my B3D app. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Blitzplotter</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> Here is my slightly mod'd version of Catmull Rom Spline:-<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'Catmull Rom Spline v2.bmx

' added some code to read in a raw_speed.dat file. Please note,
' the raw_speed.dat file content is below this codebox. Just
' copy the data within the codebox below into a text editor and
' save the file in the same directory as Catmull Rom Spline v2.bmx

'This is just a little type to keep track of individual control points
Type point
	Field x#,y#
	
	Function Create:point(x#,y#)
		p:point=New point
		p.x=x
		p.y=y
		Return p
	End Function
End Type

'Spline type
Type romspline

	Field points:TList 'the points list is a list of the control points to use in the spline
	
	Method New() '(This is called when the spline object is created, it just initialises the points list)
		points=New TList
	End Method
	
	Method addpoint(p:point) 'Call this to add a point to the end of the list
		points.addlast p
	End Method
	
	Method draw() 'Do the actual drawing!
	
		'Draw a rectangle at each control point so we can see them (not relevant to the algorithm)
		For p:point = EachIn points
			DrawRect p.x , p.y , 2 , 2
		Next
	
		num=points.count()
		If num&lt;4 Then Return 'Check there are enough points to draw a spline

		'Get the first three  TLinks in the list of points. This algorithm is going to work by 
		'working out the first three points, then getting the last point at the start of the
		'while loop. After the curve section has been drawn, every point is moved along one,
		'and the TLink is moved to the next one so we can see if it's null, and then get the next
		'p3 from it if not.
		pl:TLink = points.firstlink()
		p0:point = point(pl.value() )
		pl = pl.nextlink()
		p1:point = point(pl.value() )
		pl = pl.nextlink()
		p2:point = point(pl.value() )
		pl = pl.nextlink()
		'we'll work out what p3 is inside the loop
		
		While pl &lt;&gt; Null 'pl3 will be null when we've reached the end of the list
			
			'get the point objects from the TLinks
			p3:point = point(pl.value() )
			
			ox# = p1.x
			oy# = p1.y
			For t#=0 To 1 Step .01 'THE MEAT And BONES! Oddly, there isn't much to explain here, just copy the code.
				x# = .5 * ( (2 * p1.x) + (p2.x - p0.x) * t + (2 * p0.x - 5 * p1.x + 4 * p2.x - p3.x) * t * t + (3 * p1.x - p0.x - 3 * p2.x + p3.x) * t * t * t)
				y# = .5 * ( (2 * p1.y) + (p2.y - p0.y) * t + (2 * p0.y - 5 * p1.y + 4 * p2.y - p3.y) * t * t + (3 * p1.y - p0.y - 3 * p2.y + p3.y) * t * t * t)
				DrawLine ox , oy , x , y
				
				ox = x
				oy = y
				
			Next
			
			'Move one place along the list
			p0 = p1
			p1 = p2
			p2 = p3
			pl=pl.nextlink()
		Wend
	End Method
End Type

'Demo - create a spline And add points from a dat file
'then also add points when the mouse is clicked

Graphics 800, 800 , 0

s:romspline = New romspline

While Not KeyHit(KEY_ESCAPE)

	If MouseHit(1)
		s.addpoint(point.Create(MouseX(),MouseY()))
	EndIf
	
	'####################################################################################################
	'okay, this is where i need to add the points from my raw (ish?) data
	
	If do_once=0
		
		in=ReadStream("raw_speed.dat")

		If Not in RuntimeError "Failed to open a ReadStream to file raw_speed.dat"

		While Not Eof(in)
	
			temp$ = ReadLine(in) 
			
			x_val = x_val + 1
			
			y_val = Int(temp$)
				
			Print "X:"+x_val
			Print "Y:"+y_val
			
			s.addpoint(point.Create(x_val,y_val))
			
			'Attempts at smoothing the data:-
			
			's.draw()   thought this might've needed to be in here, however no difference	
	
			'add the data, spreading the data out a bit To
			'attempt To let the smoothage kick in......
			'For smooth=1 To 6
			'		s.addpoint(point.Create(x_val,y_val))
			'		x_val=x_val + 1
			'Next
			
			'End attempts at smoothing data
		Wend
		
		CloseStream in
	
		do_once=1
	
	EndIf

	'end add data to method
	'######################################################################################################
	
	
	s.draw()
	Flip
	Cls
Wend

</textarea><br><br>the data for raw_speed.dat, needs to be in same directory as the catmull rom spline code:-<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
2
5
5
5
5
5
15
25
25
25
25
25
42
60
60
60
60
60
72
85
52
20
20
20
20
20
20
20
20
102
185
185
185
130
75
75
75
75
87
100
62
25
25
25
25
25
25
87
150
150
150
150
150
150
150
150
170
190
190
190
190
190
165
140
140
140
140
140
140
140
140
140
180
220
220
137
55
55
55
55
55
85
115
115
115
115
115
115
115
115
115
115
172
230
230
230
230
157
85
55
25
25
25
25
25
25
25
90
155
155
155
155
155
155
147
140
140
140
140
140
140
140
140
140
177
215
215
215
215
215
215
215
215
215
215
230
245
245
245
245
245
245
245
245
245
245
237
230
230
230
230
230
230
230
230
205
180
180
180
180
135
90
90
90
90
92
95
95
70
45
45
45
45
72
100
100
100
100
100
100
100
100
100
100
100
100
100
100
100
172
245
245
245
245
245
245
245
245
245
245
245
245
245
245
162
80
80
80
80
80
105
130
130
92
55
55
55
55
80
105
105
105
105
105
105
105
140
175
175
175
110
45
45
40
35
35
35
35
35
35
35
35
35
125
215
215
215
215
215
215
215
215
210
205
205
205
205
205
205
205
205
217
230
230
230
230
230
230
230
230
230
230
230
270
310
310
310
310
310
310
310
310
310
280
250
250
250
250
250
250
250
250
237
225
225
225
225
225
225
225
225
225
225
225
225
225
225
225
225
205
185
185
185
185
185
185
185
195
205
205
205
205
205
177
150
150
150
150
150
150
150
150
195
240
240
240
240
240
240
240
240
240
240
270
300
300
300
300
300
300
300
255
210
210
210
210
210
210
190
170
170
170
170
170
170
170
170
170
225
280
280
280
280
280
280
280
280
280
285
290
290
290
290
290
290
290
290
290
290


</textarea> <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
