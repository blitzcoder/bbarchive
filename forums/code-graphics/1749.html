<!DOCTYPE html><html lang="en" ><head ><title >Color Module</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Easy conversion between rgb and hsv, language=bmx, category=Graphics'><meta name='author' content='fredborg'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/Color Module</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1749.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Color Module by fredborg</td><td align="right">2006 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Make a folder "color" in the mod/pub/ directory and copy this there. It should be more or less self explanatory. </td></tr><tr ><td class="cell"><pre class="code">SuperStrict

Module Pub.Color

ModuleInfo "Version: 1.00"
ModuleInfo "Authors: Mikkel Fredborg"
ModuleInfo "License: Public Domain"

Import BRL.Math

Type TColor
	
	Method toARGB:Int() Abstract
		
End Type

Type TColorHSV Extends TColor
	
	Field h:Float,s:Float,v:Float,a:Float=1.0
	
	Method toRGB:TColorRGB()

		Local temph:Float = self.h
		Local temps:Float = self.s
		Local tempv:Float = self.v
	
		Local rgb:TColorRGB = New TColorRGB
	
		If temph=&gt;360.0 Or temph&lt;0.0 Then temph = 0.0
	
		If temps = 0 Then
			rgb.r = v
			rgb.g = v
			rgb.b = v
		Else
			temph = temph / 60.0
			
			Local i:Int   = Floor(temph)
			Local f:Float = temph - i
			Local p:Float = tempv * (1 - temps)
			Local q:Float = tempv * (1 - temps * f)
			Local t:Float = tempv * (1 - temps * (1 - f))

			Select i
				Case 0
					rgb.r = v
					rgb.g = t
					rgb.b = p
				Case 1
					rgb.r = q
					rgb.g = v
					rgb.b = p
				Case 2
					rgb.r = p
					rgb.g = v
					rgb.b = t
				Case 3
					rgb.r = p
					rgb.g = q
					rgb.b = v
				Case 4
					rgb.r = t
					rgb.g = p
					rgb.b = v
				Default
					rgb.r = v
					rgb.g = p
					rgb.b = q
			End Select		
		EndIf

		rgb.a = a

		Return rgb
	
	EndMethod
	
	Function fromARGB:TColorHSV(argb:Int)
		
		Return TColorRGB.fromARGB(argb).toHSV()
		
	EndFunction
	
	Method toARGB:Int()
		
		Return self.toRGB().toARGB()

	EndMethod
	
EndType

Type TColorRGB Extends TColor

	Field r:Float,g:Float,b:Float,a:Float=1.0
	
	Method toHSV:TColorHSV()
		
		Local tempr:Float = Min(1.0,Max(0.0,self.r))
		Local tempg:Float = Min(1.0,Max(0.0,self.g))
		Local tempb:Float = Min(1.0,Max(0.0,self.b))

		Local minVal:Float = Min(Min(tempr,tempg),tempb)
		Local maxVal:Float = Max(Max(tempr,tempg),tempb)
		
		Local diff:Float = maxVal - minVal
	
		Local hsv:TColorHSV = New TColorHSV
		hsv.v = maxVal
	
		If maxVal = 0.0 Then
			hsv.s = 0.0
			hsv.h = 0.0
		Else
			hsv.s = diff / maxVal
	
			If tempr = maxVal
				hsv.h = (tempg - tempb) / diff
			ElseIf tempg = maxVal
				hsv.h = 2.0 + (tempb - tempr) / diff
			Else
				hsv.h = 4.0 + (tempr - tempg) / diff
			EndIf
	
			hsv.h = hsv.h * 60.0
			If hsv.h &lt; 0 Then hsv.h = hsv.h + 360.0
		EndIf

		If hsv.h&lt;  0.0 Then hsv.h = 0.0
		If hsv.h&gt;360.0 Then hsv.h = 0.0
		
		hsv.a = a
		
		Return hsv
		
	EndMethod

	Function fromARGB:TColorRGB(argb:Int,alpha:Int=True)
	
		Local rgb:TColorRGB = New TColorRGB
	
		If alpha	
			rgb.a = ((argb Shr 24) &amp; $FF)/255.0
		EndIf
		
		rgb.r = ((argb Shr 16) &amp; $FF)/255.0
		rgb.g = ((argb Shr 8) &amp; $FF)/255.0
		rgb.b = (argb &amp; $FF)/255.0
	
		Return rgb
		
	EndFunction

	Function fromBGR:TColorRGB(argb:Int)
	
		Local rgb:TColorRGB = New TColorRGB
	
		rgb.r = (argb &amp; $000000FF)/255.0
		rgb.g = ((argb Shr 8) &amp; $000000FF)/255.0
		rgb.b = ((argb Shr 16) &amp; $000000FF)/255.0
	
		Return rgb
		
	EndFunction

	Method toARGB:Int()
		
		Local tempr:Int = Min(255,Max(0,Int(self.r*255)))
		Local tempg:Int = Min(255,Max(0,Int(self.g*255)))
		Local tempb:Int = Min(255,Max(0,Int(self.b*255)))
		Local tempa:Int = Min(255,Max(0,Int(self.a*255)))
						
		Return (tempa Shl 24) | (tempr Shl 16) | (tempg Shl 8) | tempb

	EndMethod
	
EndType</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Craig H. Nisbet</td><td align="right"><font class=tiny>2007</font></td></tr></table></td></tr><tr ><td class="posttext"> When I try to compile this, I get a "Module does not match commandline Module" <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>2007</font></td></tr></table></td></tr><tr ><td class="posttext"> You don't put modules in Pub any more. Create a directory in mods called 'fredborg' and change <pre class=code>Module Pub.Color </pre> to <pre class=code> Module fredborg.Color </pre> <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
