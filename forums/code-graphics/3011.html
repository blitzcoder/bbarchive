<!DOCTYPE html><html lang="en" ><head ><title >Catmull perimeter</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Uses Catmull splines to draw a closed shape, language=bmx, category=Graphics'><meta name='author' content='TAS'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/Catmull perimeter</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="3011.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Catmull perimeter by TAS</td><td align="right">2012 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Use the space bar to generate a new random perimeter and then drag any control point with the left mouse button. </td></tr><tr ><td class="cell"><pre class="code">'Catmull perimeter
'By Thomas Stevenson
'war-game-programming.com
'Adapted from code by ImaginaryHuman which was
'Adapted from code by Warpy and Matt McFarland

Type Point
	Field x:Int
	Field y:Int
End Type

Const Accuracy:Double=0.05		'Lower has more line segments
	
SeedRnd(MilliSecs())	'Different each time
SetGraphicsDriver GLMax2DDriver()
Graphics 800,600,0
SetBlend LIGHTBLEND
Local ControlPoint:Int
Local Counter:Int	
glEnable(GL_LINE_SMOOTH)	'Quick antaliasing hack
glHint(GL_LINE_SMOOTH_HINT,GL_NICEST)
glLineWidth(3.0)

Local NumPoints=Rand(4,30)	
Local Points:Point[]=Catmull_Create(NumPoints)

Repeat
	Cls
	If MouseDown(1)
		'check for point drag
		ControlPoint=Catmull_Find(Points)
		If ControlPoint
			'Catmull_Find() returns NumPoints for point 0
			ControlPoint=ControlPoint Mod NumPoints
			'if non 0 then mouse is near (+/-7) of a control point
			Points[ControlPoint].x=MouseX()
			Points[ControlPoint].y=MouseY()
		EndIf
	EndIf

	'Draw segments
	Counter=Catmull_Draw(Points,$00FFFFFF)
	'Info text
	DrawText "Control Points: "+String(NumPoints),10,5
	DrawText "Segiments (cps/0.05): "+String(Counter),10,20
	DrawText "Curves (=cps): "+String(NumPoints),10,35
	
	DrawText "Spacebar: New random shape",550,10
	DrawText "Escape:  Exit",550,30
	DrawText "Left Mouse: Drag points",550,50
	Catmull_Info(Points,60)
	Flip
	
	If KeyHit(KEY_SPACE) 
		NumPoints=Rand(7,30)
		Points=Catmull_Create(NumPoints)	'returns array of point types
	EndIf	
	If KeyHit(KEY_ESCAPE) Then Exit
	If AppTerminate() Then Exit
Forever 
End

Function Catmull_Draw(p:Point[],clr)
		Local PrevX:Double,PrevY:Double
		Local bytes:Byte Ptr = Varptr clr
		SetColor bytes[2],bytes[1],bytes[0]
		Local CM_Counter=0
		'Accuracy = constant set by Main
		a:Double=0.5
		For i=1 To p.length
			'calc indexs of four control points for curve from i to i+1
			'wrap index if Sj&gt;=p.length
			s0=(i-1); s1=(i+0) Mod p.length;	s2=(i+1) Mod p.length;	s3=(i+2) Mod p.length
			For T:Double=0 To 1 Step Accuracy
				x:Double=a*( (2*p[S1].x)+(p[S2].x-p[S0].x)*T..
				+(2*p[S0].x-5*p[S1].x+4*p[S2].x-p[S3].x)*T*T..
				+(3*p[S1].x-p[S0].x-3*p[S2].x+p[S3].x) *T*T*T)
				Y:Double=a*( (2*p[S1].y)+(p[S2].y-p[S0].y)*T +(2*p[S0].y-5*p[S1].y+4*p[S2].y-p[S3].y)*T*T +(3*p[S1].y-p[S0].y-3*p[S2].y+p[S3].y) *T*T*T)
				If PrevX=0 And PrevY=0
					PrevX=X; PrevY=Y
				EndIf
				DrawLine PrevX,PrevY,X,Y,False
				PrevX=X; PrevY=Y
				CM_Counter:+1
			Next
		Next
		Return CM_Counter
End Function

Function Catmull_Create:Point[](np,r1=80,r2=230)
	Local Pts:Point[]=New Point[np]
	For p:Int=0 To np-1
		Local deg:Int=P*(360/np) Mod 360
		Pts[p]=New Point
		Local h=Rand(r1,r2)
		Pts[p].x=400+h*Cos(deg)
		Pts[p].y=300+h*Sin(deg)
	Next
	Return Pts
End Function

Function Catmull_Info(p:Point[],y)
	'Draw controls excluding duplicates
	For i=0 To p.length-1
		SetColor $88,$88,$88	'mark the point
		DrawLine p[i].x-7,p[i].y-7,p[i].x+7,p[i].y+7
		DrawLine p[i].x-7,p[i].y+7,p[i].x+7,p[i].y-7
		SetColor 255,0,0	'point number
		DrawText i,p[i].x+5,p[i].y+5
		SetColor 255,255,255	'xy info
		DrawText RSet(i,3)+RSet(P[i].x,6)+RSet(P[i].y,6),10,y+i*15
	Next
End Function

Function Catmull_Find(p:Point[])
	'Point 0= Point n 
	If InsideRect(MouseX(),MouseY(),p[0].x-7,p[0].y-7,14,14) Then Return p.length
	For i=1 To p.length-1
		If InsideRect(MouseX(),MouseY(),p[i].x-7,p[i].y-7,14,14) Then Return i
	Next
	'Not found
	Return 0
End Function

Function InsideRect(x,y,x2,y2,w,h)
	If x&lt;x2 Then Return False	
	If x&gt;(x2+w) Then Return False	
	If y&lt;y2 Then Return False	
	If y&gt;(y2+h) Then Return False	
	Return True
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
