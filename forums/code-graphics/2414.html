<!DOCTYPE html><html lang="en" ><head ><title >Image Blending Functions</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Some Blendings like in Photoshop, language=bb, category=Graphics'><meta name='author' content='Krischan'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/Image Blending Functions</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2414.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Image Blending Functions by Krischan</td><td align="right">2009 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> A quick collection of Image Blending Functions like these in Photoshop. Very usable for blending heightmaps together to produce completely new terrains: I am not sure if all functions are translated correctly - feel free to comment, post fixes or simplify the code.<br><br>19 Blending Functions available: Average, Multiply, Screen, Darken, Lighten, Difference, Negation, Exclusion, Overlay, HardLight, SoftLight, ColorDodge, InverseColorDodge, SoftColorDodge, ColorBurn, InverseColorBurn, SoftColorBurn, Reflect, Glow<br><br>To see it in a working demo take a look at my Realtime Procedural Planet Generator: <a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=2416" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=2416</a> </td></tr><tr ><td class="cell"><pre class="code">; Image Blend Modes
;
; Idea Credit: http://www.pegtop.net/delphi/articles/blendmodes/intro.htm
; get the two images there or use your own (need to have the same size for this demo)

AppTitle "Image Blend Functions"


Graphics 400,300,32,2

image1=LoadImage("baseimage.jpg")
image2=LoadImage("blendimage.jpg")
image3=CreateImage(200,150)

ib1=ImageBuffer(image1)
ib2=ImageBuffer(image2)
ib3=ImageBuffer(image3)

start=MilliSecs()

LockBuffer ib1
LockBuffer ib2
LockBuffer ib3

For x=0 To 199
	
	For y=0 To 149
		
		rgb1=ReadPixelFast(x,y,ib1)
		rgb2=ReadPixelFast(x,y,ib2)
		
		r1%=(rgb1 And $ff0000)/$10000
		g1%=(rgb1 And $ff00)/$100
		b1%=(rgb1 And $ff)
		
		r2%=(rgb2 And $ff0000)/$10000
		g2%=(rgb2 And $ff00)/$100
		b2%=(rgb2 And $ff)
		
		r3=SoftLight(r1,r2)
		g3=SoftLight(g1,g2)
		b3=SoftLight(b1,b2)
		
		rgb3=r3*$10000+g3*$100+b3
		
		WritePixelFast x,y,rgb3,ib3
		
	Next
	
Next

UnlockBuffer ib3
UnlockBuffer ib2
UnlockBuffer ib1

ms=MilliSecs()-start

While Not KeyHit(1)
	
	DrawImage image1,0,0
	DrawImage image2,200,0
	DrawImage image3,100,150
	
	Text 0,0,ms+"ms"
	
	Flip
	
Wend

End

Function Average(a%,b%)
	
	Return (a+b) Shr 1
	
End Function

Function Multiply(a%,b%)
	
	Return (a*b) Shr 8
	
End Function

Function Screen(a%,b%)
	
	Return 255-((255-a)*(255-b) Shr 8)
	
End Function

Function Darken(a%,b%)
	
	If a&lt;b Then Return a Else Return b
	
End Function

Function Lighten(a%,b%)
	
	If a&gt;b Then Return a Else Return b
	
End Function

Function Difference(a%,b%)
	
	Return Abs(a-b)
	
End Function

Function Negation(a%,b%)
	
	Return 255-Abs(255-a-b)
	
End Function

Function Exclusion(a%,b%)
	
	Return a+b-(a*b Shr 7)
	
End Function

Function Overlay(a%,b%)
	
	If a&lt;128 Then Return (a*b) Shr 7 Else Return 255-((255-a)*(255-b) Shr 7)
	
End Function

Function HardLight(a%,b%)
	
	If b&lt;128 Then Return (a*b) Shr 7 Else Return 255-((255-b)*(255-a) Shr 7)
	
End Function

Function SoftLight(a%,b%)
	
	Local c%=a*b Shr 8
	Return (c+a*(255-((255-a)*(255-b) Shr 8)-c) Shr 8)
	
End Function

Function ColorDodge(a%,b%)
	
	If b=255 Then
		Return 255
	Else
		Local c%=Floor((a Shl 8)/(255-b))
		If c&gt;255 Then Return 255 Else Return c
	EndIf
	
End Function

Function InverseColorDodge(a%,b%)
	
	If a=255 Then
		Return 255
	Else
		Local c%=Floor((b Shl 8)/(255-a))
		If c&gt;255 Then Return 255 Else Return c
	EndIf
	
End Function

Function SoftColorDodge(a%,b%)
	
	Local c%
	
	If a+b&lt;256 Then
		If b=255 Then
			Return 255
		Else
			c=Floor((a Shl 7)/(255-b))
			If c&gt;255 Then Return 255 Else Return c
		EndIf
	Else
		c=255-Floor((((255-b) Shl 7)/a))
		If c&lt;0 Then Return 0 Else Return c
	EndIf
	
End Function

Function ColorBurn(a%,b%)
	
	If b=0 Then
		Return 0
	Else
		Local c%=255-Floor(((255-a) Shl 8)/b)
		If c&lt;0 Then Return 0 Else Return c
	EndIf
	
End Function

Function InverseColorBurn(a%,b%)
	
	If a=0 Then
		Return 0
	Else
		Local c%=255-Floor(((255-b) Shl 8)/a)
		If c&lt;0 Then Return 0 Else Return c
	EndIf
	
End Function

Function SoftColorBurn(a%,b%)
	
	Local c%
	
	If a+b&lt;256 Then
		If a=255 Then
			Return 255
		Else
			c=Floor((b Shl 7)/(255-a))
			If c&gt;255 Then Return 255 Else Return c
		EndIf
	Else
		c=255-Floor(((255-a) Shl 7)/b)
		If c&lt;0 Then Return 0 Else Return c
	EndIf
	
End Function

Function Reflect(a%,b%)
	
	If b = 255 Then
		Return 255
	Else
		Local c%=Floor(a*a/(255-b))
		If c&gt;255 Then Return 255 Else Return c
	EndIf
	
End Function

Function Glow(a%,b%)
	
	If a=255 Then
		Return 255
	Else
		Local c%=Floor(b*b/(255-a))
		If c&gt;255 Then Return 255 Else Return c
	EndIf
	
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >plash</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> BlitzMax version:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Framework brl.blitz

Import brl.glmax2d
Import brl.jpgloader

AppTitle = "Image Blend Functions"
SetGraphicsDriver(GLMax2DDriver())
Graphics(400, 300, 0)

Local start:Int, ms:Int
Local image1:TImage, image2:TImage, image3:TImage
Local pix1:TPixmap, pix2:TPixmap, pix3:TPixmap

image1 = LoadImage("baseimage.jpg")
image2 = LoadImage("blendimage.jpg")
image3 = CreateImage(200, 150)

pix1 = image1.Lock(0, True, True)
pix2 = image2.Lock(0, True, True)
pix3 = image3.Lock(0, True, True)

Local x:Int, y:Int
Local argb1:TARGB, argb2:TARGB
argb1 = New TARGB
argb2 = New TARGB

start = MilliSecs()
For x = 0 To image3.width - 1
	
	For y = 0 To image3.height - 1
		
		argb1.Set(pix1.ReadPixel(x, y))
		argb2.Set(pix2.ReadPixel(x, y))
		
		' Force full alpha
		pix3.WritePixel(x, y, TARGB.ConvToARGB(SoftLight(argb1.r, argb2.r), SoftLight(argb1.g, argb2.g), SoftLight(argb1.b, argb2.b), 255))
		
	Next
	
Next
ms = MilliSecs() - start

While KeyHit(KEY_ESCAPE) = False
	
	Cls()
	
	DrawImage(image1, 0.0, 0.0)
	DrawImage(image2, 200.0, 0.0)
	DrawImage(image3, 100.0, 150.0)
	
	DrawText(ms + "ms", 0.0, 0.0)
	
	Flip()
	
Wend


Type TARGB
	
	Field r:Int, g:Int, b:Int, a:Int
	
		Method Set(argb:Int)
			
			r = (argb Shr 16) &amp; $ff
			g = (argb Shr 8) &amp; $ff
			b = (argb &amp; $ff)
			
			a = (argb Shr 24) &amp; $ff
			
		End Method
		
		Method Get:Int()
			
			Return Int(a * $1000000) + Int(r * $10000) + Int(g * $100) + Int(b)
			
		End Method
		
		
		Function ConvToARGB:Int(r:Int, g:Int, b:Int, a:Int)
			
			Return Int(a * $1000000) + Int(r * $10000) + Int(g * $100) + Int(b)
			
		End Function
		
End Type


Function Average:Int(a:Int, b:Int)
	
	Return (a + b) Shr 1
	
End Function

Function Multiply:Int(a:Int, b:Int)
	
	Return (a * b) Shr 8
	
End Function

Function Screen:Int(a:Int, b:Int)
	
	Return 255 - ((255 - a) * (255 - b) Shr 8)
	
End Function

Function Darken:Int(a:Int, b:Int)
	
	If a &lt; b Then Return a Else Return b
	
End Function

Function Lighten:Int(a:Int, b:Int)
	
	If a &gt; b Then Return a Else Return b
	
End Function

Function Difference:Int(a:Int, b:Int)
	
	Return Abs(a - b)
	
End Function

Function Negation:Int(a:Int, b:Int)
	
	Return 255 - Abs(255 - a - b)
	
End Function

Function Exclusion:Int(a:Int, b:Int)
	
	Return a + b - (a * b Shr 7)
	
End Function

Function Overlay:Int(a:Int, b:Int)
	
	If a &lt; 128 Then Return (a * b) Shr 7 Else Return 255 - ((255 - a) * (255 - b) Shr 7)
	
End Function

Function HardLight:Int(a:Int, b:Int)
	
	If b &lt; 128 Then Return (a * b) Shr 7 Else Return 255 - ((255 - b) * (255 - a) Shr 7)
	
End Function

Function SoftLight:Int(a:Int, b:Int)
	Local c:Int
	
	c = a * b Shr 8
	Return (c + a * (255 - ((255 - a) * (255 - b) Shr 8) - c) Shr 8)
	
End Function

Function ColorDodge:Int(a:Int, b:Int)
	
	If b = 255
		
		Return 255
		
	Else
		Local c:Int
		
		c = Floor((a Shl 8) / (255 - b))
		If c &gt; 255 Then Return 255 Else Return c
		
	End If
	
End Function

Function InverseColorDodge:Int(a:Int, b:Int)
	
	If a = 255
		
		Return 255
		
	Else
		Local c:Int
		
		c = Floor((b Shl 8) / (255 - a))
		If c &gt; 255 Then Return 255 Else Return c
		
	End If
	
End Function

Function SoftColorDodge:Int(a:Int, b:Int)
	Local c:Int
	
	If a + b &lt; 256
		
		If b = 255
			
			Return 255
			
		Else
			
			c = Floor((a Shl 7) / (255 - b))
			If c &gt; 255 Then Return 255 Else Return c
			
		End If
		
	Else
		
		c = 255 - Floor((((255 - b) Shl 7) / a))
		If c &lt; 0 Then Return 0 Else Return c
		
	End If
	
End Function

Function ColorBurn:Int(a:Int, b:Int)
	
	If b = 0
		
		Return 0
		
	Else
		
		Local c:Int = 255 - Floor(((255 - a) Shl 8) / b)
		If c &lt; 0 Then Return 0 Else Return c
		
	End If
	
End Function

Function InverseColorBurn:Int(a:Int, b:Int)
	
	If a = 0
		
		Return 0
		
	Else
		Local c:Int
		
		c = 255 - Floor(((255 - b) Shl 8) / a)
		If c &lt; 0 Then Return 0 Else Return c
		
	End If
	
End Function

Function SoftColorBurn:Int(a:Int, b:Int)
	Local c:Int
	
	If a + b &lt; 256
		
		If a = 255
			
			Return 255
			
		Else
			
			c = Floor((b Shl 7) / (255 - a))
			If c &gt; 255 Then Return 255 Else Return c
			
		End If
		
	Else
		
		c = 255 - Floor(((255 - a) Shl 7) / b)
		If c &lt; 0 Then Return 0 Else Return c
		
	End If
	
End Function

Function Reflect:Int(a:Int, b:Int)
	
	If b = 255
		Return 255
	Else
		Local c:Int
		
		c = Floor(a * a / (255 - b))
		If c &gt; 255 Then Return 255 Else Return c
		
	End If
	
End Function

Function Glow:Int(a:Int, b:Int)
	
	If a = 255
		
		Return 255
		
	Else
		Local c:Int
		
		c = Floor(b * b / (255 - a))
		If c &gt; 255 Then Return 255 Else Return c
		
	End If
	
End Function</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>2012</font></td></tr></table></td></tr><tr ><td class="posttext"> Here is an update, I rewrote everything and cross-checked the results with real photoshop, so most blend modes should be identical now, and i added some additional blend modes I use. I wrote a little demo where you can browse all 22 blend modes with the UP/DOWN Arrow keys. You need two additional 256x256 PNG Files, use your own or use these two:<br><br><b>color.png</b><br><img src="http://www.christianhart.de/bb/blendmodes/color.png"><br><b>grey.png</b><br><img src="http://www.christianhart.de/bb/blendmodes/grey.png"><br><br><b>demo.bb</b><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Include "filters.bb"

Graphics3D 256,256,32,2

Global mode$
Local current%=1
Local timer%=CreateTimer(60)

Local img1%=LoadImage("color.png")
Local img2%=LoadImage("grey.png")

MoveMouse 128,128

While Not KeyHit(1)
	
	Local old%
	
	If old&lt;&gt;current Then
		
		AppTitle "Calculating..."
		
		old=current
		
		img3=Demo(img1,img2,current)
		
		Cls
		
	EndIf
	
	old%=current
	
	current=current+KeyHit(200)-KeyHit(208)
	current=LimitInt(current,1,22)
	
	LockBuffer ImageBuffer(img3)
	Local mx%=MouseX()
	Local my%=MouseY()
	Local rgb%=ReadPixelFast(mx,my,ImageBuffer(img3))
	UnlockBuffer ImageBuffer(img3)
	
	DrawImage img3,0,0
	
	Color 255,255,255
	Line mx,0,mx,255
	Line 0,my,255,my
	
	AppTitle mode+" ["+current+"]"
	Color 0,0,0
	Text 0, 0,"RGB: "+GetR(rgb)+","+GetG(rgb)+","+GetB(rgb)
	Text 0,15,"HSL: "+GetH(rgb)+","+GetS(rgb)+","+GetL(rgb)
	
	WaitTimer timer
	
	Flip 0
	
Wend

End

Function Demo(img1%,img2%,filter%=1)
	
	Local x%,y%
	Local r%,g%,b%
	Local rgb1%,rgb2%
	Local img3%
	
	If img3&lt;&gt;0 Then FreeImage img3 Else img3=CreateImage(256,256)
	
	LockBuffer ImageBuffer(img1)
	LockBuffer ImageBuffer(img2)
	LockBuffer ImageBuffer(img3)
	
	For x=0 To 255
		
		For y=0 To 255
			
			rgb1=ReadPixelFast(x,y,ImageBuffer(img1))
			rgb2=ReadPixelFast(x,y,ImageBuffer(img2))
			
			Select filter
					
				Case 1:
					mode="Darken"
					r=Darken(GetR(rgb1),GetR(rgb2))
					g=Darken(GetG(rgb1),GetG(rgb2))
					b=Darken(GetB(rgb1),GetB(rgb2))
					
				Case 2:
					mode="Multiply"
					r=Multiply(GetR(rgb1),GetR(rgb2))
					g=Multiply(GetG(rgb1),GetG(rgb2))
					b=Multiply(GetB(rgb1),GetB(rgb2))
					
				Case 3:
					mode="Color Burn"
					r=ColorBurn(GetR(rgb1),GetR(rgb2))
					g=ColorBurn(GetG(rgb1),GetG(rgb2))
					b=ColorBurn(GetB(rgb1),GetB(rgb2))
					
				Case 4
					mode="Linear Burn"
					r=LinearBurn(GetR(rgb1),GetR(rgb2))
					g=LinearBurn(GetG(rgb1),GetG(rgb2))
					b=LinearBurn(GetB(rgb1),GetB(rgb2))
					
				Case 5
					mode="Lighten"
					r=Lighten(GetR(rgb1),GetR(rgb2))
					g=Lighten(GetG(rgb1),GetG(rgb2))
					b=Lighten(GetB(rgb1),GetB(rgb2))
					
				Case 6
					mode="Screen"
					r=Screen(GetR(rgb1),GetR(rgb2))
					g=Screen(GetG(rgb1),GetG(rgb2))
					b=Screen(GetB(rgb1),GetB(rgb2))
					
				Case 7
					mode="Color Dodge"
					r=ColorDodge(GetR(rgb1),GetR(rgb2))
					g=ColorDodge(GetG(rgb1),GetG(rgb2))
					b=ColorDodge(GetB(rgb1),GetB(rgb2))
					
				Case 8
					mode="Linear Dodge"
					r=LinearDodge(GetR(rgb1),GetR(rgb2))
					g=LinearDodge(GetG(rgb1),GetG(rgb2))
					b=LinearDodge(GetB(rgb1),GetB(rgb2))
					
				Case 9
					mode="Overlay"
					r=Overlay(GetR(rgb1),GetR(rgb2))
					g=Overlay(GetG(rgb1),GetG(rgb2))
					b=Overlay(GetB(rgb1),GetB(rgb2))
					
				Case 10
					mode="Soft Light"
					r=SoftLight(GetR(rgb1),GetR(rgb2))
					g=SoftLight(GetG(rgb1),GetG(rgb2))
					b=SoftLight(GetB(rgb1),GetB(rgb2))
					
				Case 11
					mode="Hard Light"
					r=HardLight(GetR(rgb1),GetR(rgb2))
					g=HardLight(GetG(rgb1),GetG(rgb2))
					b=HardLight(GetB(rgb1),GetB(rgb2))
					
				Case 12
					mode="Vivid Light"
					r=VividLight(GetR(rgb1),GetR(rgb2))
					g=VividLight(GetG(rgb1),GetG(rgb2))
					b=VividLight(GetB(rgb1),GetB(rgb2))
					
				Case 13
					mode="Linear Light"
					r=LinearLight(GetR(rgb1),GetR(rgb2))
					g=LinearLight(GetG(rgb1),GetG(rgb2))
					b=LinearLight(GetB(rgb1),GetB(rgb2))
					
				Case 14
					mode="Pin Light"
					r=PinLight(GetR(rgb1),GetR(rgb2))
					g=PinLight(GetG(rgb1),GetG(rgb2))
					b=PinLight(GetB(rgb1),GetB(rgb2))
					
				Case 15
					mode="Hard Mix"
					r=HardMix(GetR(rgb1),GetR(rgb2))
					g=HardMix(GetG(rgb1),GetG(rgb2))
					b=HardMix(GetB(rgb1),GetB(rgb2))
					
				Case 16
					mode="Difference"
					r=Difference(GetR(rgb1),GetR(rgb2))
					g=Difference(GetG(rgb1),GetG(rgb2))
					b=Difference(GetB(rgb1),GetB(rgb2))
					
				Case 17
					mode="Exclusion"
					r=Exclusion(GetR(rgb1),GetR(rgb2))
					g=Exclusion(GetG(rgb1),GetG(rgb2))
					b=Exclusion(GetB(rgb1),GetB(rgb2))
					
				Case 18
					mode="Negation"
					r=Negation(GetR(rgb1),GetR(rgb2))
					g=Negation(GetG(rgb1),GetG(rgb2))
					b=Negation(GetB(rgb1),GetB(rgb2))
					
				Case 19
					mode="Glowing"
					r=Glowing(GetR(rgb1),GetR(rgb2))
					g=Glowing(GetG(rgb1),GetG(rgb2))
					b=Glowing(GetB(rgb1),GetB(rgb2))
					
				Case 20
					mode="Average"
					r=Average(GetR(rgb1),GetR(rgb2))
					g=Average(GetG(rgb1),GetG(rgb2))
					b=Average(GetB(rgb1),GetB(rgb2))
					
				Case 21
					mode="SoftOverlay"
					r=SoftOverlay(GetR(rgb1),GetR(rgb2))
					g=SoftOverlay(GetG(rgb1),GetG(rgb2))
					b=SoftOverlay(GetB(rgb1),GetB(rgb2))
					
				Case 22
					mode="SofterLight"
					r=SofterLight(GetR(rgb1),GetR(rgb2))
					g=SofterLight(GetG(rgb1),GetG(rgb2))
					b=SofterLight(GetB(rgb1),GetB(rgb2))
					
			End Select
			
			r=LimitInt(r,0,255)
			g=LimitInt(g,0,255)
			b=LimitInt(b,0,255)
			
			WritePixelFast x,y,CombineARGB(255,r,g,b),ImageBuffer(img3)
			
		Next
		
	Next
	
	UnlockBuffer ImageBuffer(img3)
	UnlockBuffer ImageBuffer(img2)
	UnlockBuffer ImageBuffer(img1)
	
	Return img3
	
End Function</textarea><br><br><b>filters.bb</b><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">; ----------------------------------------------------------------------------
; Photoshop filters
; ----------------------------------------------------------------------------

; Photoshop: Darken
Function Darken%(a%,b%)
	
	If a&lt;b Then Return a Else Return b
	
End Function

; Photoshop: Multiply
Function Multiply%(a%,b%)
	
	Return (a*b) Shr 8
	
End Function

; Photoshop: Color Burn
Function ColorBurn%(a%,b%)
	
	If a&lt;=0 Then Return 0 Else Return Max(255-((255-b)*255/a),0)
	
End Function

; Photoshop: Linear Burn
Function LinearBurn%(a%,b%)
	
	If (a+b)&lt;256 Return 0 Else Return (a+b-255)
	
End Function

; Photoshop: Lighten
Function Lighten%(a%,b%)
	
	If a&gt;b Then Return a Else Return b
	
End Function

; Photoshop: Screen
Function Screen%(a%,b%)
	
	Return 255-((255-a)*(255-b) Shr 8)
	
End Function

; Photoshop: Color Dodge
Function ColorDodge%(a%,b%)
	
	If a=255 Return 255 Else Return Min(255,((b Shl 8)/(255-a)))
	
End Function

; Photoshop: Linear Dodge
Function LinearDodge%(a%,b%)
	
	Return Min(a+b,255)
	
End Function

; Photoshop: Overlay
Function Overlay%(a%,b%)
	
	If b&lt;128 Then Return (2*a*b Shr 8) Else Return 255-(2*(255-b)*(255-a) Shr 8)
	
End Function

; Photoshop: Soft Light
Function SoftLight%(a%,b%)
	
	If a&gt;127.5 Then
		
		Return b+(255-b)*((a-127.5)/127.5)*(0.5-Abs(b-127.5)/255)
		
	Else
		
		Return b-b*((127.5-a)/127.5)*(0.5-Abs(b-127.5)/255)
		
	EndIf
	
End Function

; Photoshop: Hard Light
Function HardLight%(a%,b%)
	
	If a&gt;127.5 Then Return b+(255-b)*((a-127.5)/127.5) Else Return b*a/127.5
	
End Function

; Photoshop: Vivid Light
Function VividLight%(a%,b%)
	
	If a&lt;128 Then Return ColorBurn((2*a),b) Else Return ColorDodge(2*(a-128),b)
	
End Function

; Photoshop: Linear Light
Function LinearLight%(a%,b%)
	
	If a&lt;128 Then Return LinearBurn(b,(2*a)) Else Return LinearDodge(b,(2*(a-128)))
	
End Function

; Photoshop: Pin Light
Function PinLight%(a%,b%)
	
	If a&lt;128 Then Return Darken(b,(2*a)) Else Return Lighten(b,(2*(a-128)))
	
End Function

; Photoshop: Hard Mix
Function HardMix%(a%,b%)
	
	If VividLight(a,b)&lt;128 Then Return 0 Else Return 255
	
End Function

; Photoshop: Difference
Function Difference%(a%,b%)
	
	Return Abs(a-b)
	
End Function

; Photoshop: Exclusion
Function Exclusion%(a%,b%)
	
	Return a+b-(a*b Shr 7)
	
End Function



; ----------------------------------------------------------------------------
; Non-Photoshop additional filters
; ----------------------------------------------------------------------------

; Additional: Negation
Function Negation%(a%,b%)
	
	Return 255-Abs(255-a-b)
	
End Function

; Additional: Glowing
Function Glowing%(a%,b%)
	
	Local c%
	
	If a = 255 Then
		c=255
	Else
		c=b*b/(255-a)
		If c &gt; 255 Then Return 255
	EndIf
	
	Return c
	
End Function

; Additional: Average
Function Average%(a%,b%)
	
	Return (a+b) Shr 1
	
End Function

; Additional: Softer Overlay
Function SoftOverlay%(a%,b%)
	
	If a&lt;128 Then Return (a*b) Shr 7 Else Return 255-((255-a)*(255-b) Shr 7)
	
End Function

; Additional: Softer Softlight
Function SofterLight%(a%,b%)
	
	Local c%=a*b Shr 8
	
	Return (c+a*(255-((255-a)*(255-b) Shr 8)-c) Shr 8)
	
End Function



; ----------------------------------------------------------------------------
; help functions
; ----------------------------------------------------------------------------

; Limit integer value to a given range
Function LimitInt(value%,minimum%,maximum%)
	
	If value&gt;maximum Then value=maximum Else If value&lt;minimum Then value=minimum
	
	Return value
	
End Function

; return Min value out of two
Function Min%(a%,b%)
	
	If a&lt;b Then Return a Else Return b
	
End Function

; return Max value out of two
Function Max%(a%,b%)
	
	If a&gt;b Then Return a Else Return b
	
End Function

; return RGBA value out of given R,G,B,A values
Function GetRGB(r,g,b,a=255)
	
	Return b Or (g Shl 8) Or (r Shl 16) Or (a Shl 24)
	
End Function

; return Alpha value out of a RGB value
Function GetA(RGB)
	
	Return RGB Shr 24 And %11111111
	
End Function

; return Red value out of a RGB value
Function GetR(RGB)
	
    Return RGB Shr 16 And %11111111
	
End Function

; return Green value out of a RGB value
Function GetG(RGB)
	
	Return RGB Shr 8 And %11111111
	
End Function

; return Blue value out of a RGB value
Function GetB(RGB)
	
	Return RGB And %11111111
	
End Function

; combine Alpha, Red, Green, Blue values to a RGB value
Function CombineARGB(a%,r%,g%,b%)
	
	Return a*$1000000+r*$10000+g*$100+b
	
End Function

; return Hue value out of a RGB value
Function GetH#(rgb%)
	
	Return RGBtoHSL(GetR(rgb),GetG(rgb),GetB(rgb),1,0,0)
	
End Function

; return Saturation value out of a RGB value
Function GetS#(rgb%)
	
	Return RGBtoHSL(GetR(rgb),GetG(rgb),GetB(rgb),0,1,0)
	
End Function

; return Lightness value out of a RGB value
Function GetL#(rgb%)
	
	Return RGBtoHSL(GetR(rgb),GetG(rgb),GetB(rgb),0,0,1)
	
End Function

; return Hue, Saturation or Lightness value from given R,G,B values
Function RGBtoHSL#(r#,g#,b#,outh%=False,outs%=False,outl%=False)
	
	Local mmin#=Min(r,Min(g,b))
	Local mmax#=Max(r,Max(g,b))
	
	Local h#,s#,l#
	
	l=mmax*1.0/255*100
	
	Local delta#=(mmax-mmin)
	
	If mmax&lt;&gt;0 Then
		s=delta*1.0/mmax
	Else
		s=0
		h=-1
		Return
	EndIf
	
	If r=mmax
		h=(g-b)*1.0/delta
	ElseIf g=mmax
		h=2+(b-r)*1.0/delta
	Else
		h=4+(r-g)*1.0/delta
	EndIf
	
	h=h*60
	If h&lt;0 Then h=h+360
	
	If outh=True Then Return h
	If outs=True Then Return s
	If outl=True Then Return l
	
End Function</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>2012</font></td></tr></table></td></tr><tr ><td class="posttext"> Nice work :) <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >mv333</td><td align="right"><font class=tiny>2012</font></td></tr></table></td></tr><tr ><td class="posttext"> The updated code at the bottom (demo.bb) gives me a "duplicate identifier" error at Row:59 Col:9 <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
