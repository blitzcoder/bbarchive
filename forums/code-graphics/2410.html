<!DOCTYPE html><html lang="en" ><head ><title >Old School tunnel</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='graphics transformation, language=bmx, category=Graphics'><meta name='author' content='Jesse'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/Old School tunnel</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2410.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Old School tunnel by Jesse</td><td align="right">2009 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> creates a tunnel effect using image deformation.<br><br>use any image 256x256<br>or you can get the source, graphics and executable from my signature link </td></tr><tr ><td class="cell"><pre class="code">' Last iteration's tick value
'trancelation from C using SDL to bmax
'original code found at: http://sol.gfxile.net/gp/ch17.html
' by Jari Komppa AKA Sol. Translated by Jesse.

SuperStrict
' Screen surface
Global gScreen:TPixmap
Global GscreenPtr:Int Ptr
Global PITCH:Int
' Texture surface
Global gTexture:TPixmap
Global gTexturePtr:Int Ptr

' Look-up table
Global  gLut:Short[]

' Distance mask
Global gMask:Int[] 

' Screen width
Const WIDTH:Int = 480
' Screen height
Const HEIGHT:Int = 320

' Physics iterations per second
Const PHYSICSFPS:Int = 10

' Last iteration's tick value
Global gLastTick:Int


Function init()

  Local temp:TPixmap = LoadPixmap("texture17.bmp")
  gTexture = ConvertPixmap(temp,PF_RGBA8888)
  gTexturePtr = Int Ptr(gTexture.pixels)

  gLut = New Short[WIDTH * HEIGHT * 4]
  gMask = New Int[WIDTH * HEIGHT * 4]

  Local i:Int, j:Int
  Local distance:Int
    For i = 0 Until HEIGHT * 2
  
    For j = 0 Until WIDTH * 2
    
      Local xdist:Int = j - WIDTH
      Local ydist:Int = i - HEIGHT
     
      ' round
      distance:Int = Sqr(xdist * xdist + ydist * ydist)
  
      ' square
 
	 'If (Abs(xdist) &gt; Abs(ydist)) distance = Abs(xdist) Else distance = Abs(ydist)
	
      ' diamond
      'distance = (Abs(xdist) + Abs(ydist)) / 2
     
      ' flower
      'distance :+ (Sin(ATan2(xdist,ydist) * 5)*8)*57.2957795
     
      If (distance &lt;= 0) distance = 1
  
      Local d:Int = distance
      If (d &gt; 255) d = 255
      gMask[i * WIDTH * 2 + j] = d * $010101
     
      distance = (64 * 256 / distance) &amp; $ff
  
      Local angle:Int = (((ATan2(Float(xdist), Float(ydist)) / Pi*2) + 1.0) * 128)*0.0174532925
  
      gLut[i * WIDTH * 2 + j] = (distance Shl 8) + angle
    Next
  Next
End Function 


Function blend_mul:Int(source:Int, target:Int)

  Local sourcer:Int = (source Shr  0) &amp; $ff
  Local sourceg:Int = (source Shr  8) &amp; $ff
  Local sourceb:Int = (source Shr 16) &amp; $ff
  Local targetr:Int = (target Shr  0) &amp; $ff
  Local targetg:Int = (target Shr  8) &amp; $ff
  Local targetb:Int = (target Shr 16) &amp; $ff

  targetr = (sourcer * targetr) Shr 8
  targetg = (sourceg * targetg) Shr 8
  targetb = (sourceb * targetb) Shr 8

  Return (targetr Shl  0) | (targetg Shl  8) | (targetb Shl 16)
End Function 


Function render()
    
  ' Ask For the time in milliseconds
  Local tick:Int = MilliSecs()

  If (tick &lt;= gLastTick) 
  
    Delay(1)
    Return
  EndIf

  While (gLastTick &lt; tick)
  
    ' 'physics' here

    gLastTick :+ 100 / PHYSICSFPS
  Wend

  Local posx:Int = (Sin((tick * 0.000645234)*57.2957795) + 1) * WIDTH / 2
  Local posy:Int = (Sin(tick * 0.000445234*57.2957795) + 1) * HEIGHT / 2
  Local posx2:Int =(Sin(-tick * 0.000645234*57.2957795) + 1) * WIDTH / 2
  Local posy2:Int =(Sin(-tick * 0.000445234*57.2957795) + 1) * HEIGHT / 2

  Local i:Int, j:Int
  For i = 0 Until HEIGHT
  
    For j = 0 Until WIDTH
    
      Local lut:Int = gLut[(i + posy) * WIDTH * 2 + j + posx] - gLut[(i + posy2) * WIDTH * 2 + j + posx2]
      Local mask:Int = gMask[(i + posy) * WIDTH * 2 + j + posx]
      Local mask2:Int = gMask[(i + posy2) * WIDTH * 2 + j + posx2]

      gScreenPtr[(j) + (i) * PITCH] =.. 
        blend_mul(..
        blend_mul(..
        gTexturePtr[((lut + tick / 32) &amp; $ff) +.. 
                                         (((lut Shr 8) + tick / 8) &amp; $ff) *.. 
                                         (gTexture.pitch/4)],..
         mask),..
         mask2)
    Next
  Next

End Function 


  
' Attempt To Create a WIDTHxHEIGHT window with 32bit pixels.

Graphics width,height
gScreen = CreatePixmap(WIDTH, HEIGHT ,PF_RGBA8888)
gScreenPtr = Int Ptr(gScreen.pixels)
PITCH = gScreen.pitch/4
init()

' Main loop: loop Forever.

Repeat
  Cls
  render()
  DrawPixmap gScreen,0,0
  Flip(0)
Until KeyDown(KEY_ESCAPE)</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MrBill</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> More old school torus than tunnel, pretty spiffy. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> I just get a black window.<br><br>Using Windows Vista Home Premium 32 bit.<br>BFG GeForce 8800 GT OC <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> I don't know what might be the problem. what kind of image and size did you tryed? can you try the executable at  my signature link page under graphics_effects and let me if it works. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
