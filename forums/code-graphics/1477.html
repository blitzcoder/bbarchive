<!DOCTYPE html><html lang="en" ><head ><title >Draw Hollow Ellipse Pixel by Pixel with Integer Math</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Draws a hollow/unfilled ellipse one pixel at a time with integer math, language=bmx, category=Graphics'><meta name='author' content='ImaginaryHuman'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/Draw Hollow Ellipse Pixel by Pixel with Integer Math</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1477.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Draw Hollow Ellipse Pixel by Pixel with Integer Math by ImaginaryHuman</td><td align="right">2005 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This draws an unfilled ellipse one pixel at a time and uses only integer math to do it, except for one small part where two lines need to do a floating point calculation. Even so, there is no trigonometry involved. The ellipse is not rotatable unless the output method (ie Plot) can be drawn with rotated coordinates as part of the graphics system (ie rotating the camera, etc). </td></tr><tr ><td class="cell"><pre class="code">'Midpoint ellipse algorithm

Strict
Graphics 640,480,0

Local xCenter:Int=320
Local yCenter:Int=240
Local Rx,Ry:Int
Local p,px,py,x,y:Int
Local Rx2,Ry2,twoRx2,twoRy2:Int
Local pFloat:Float
Repeat
        Cls
        If MouseDown(1)
                xCenter=MouseX()
                yCenter=MouseY()
        EndIf
        Rx=Abs(xCenter-MouseX())
        Ry=Abs(yCenter-MouseY())
        DrawText String(Rx)+" x "+String(Ry),20,20
        Rx2=Rx*Rx
        Ry2=Ry*Ry
        twoRx2=Rx2 Shl 1
        twoRy2=Ry2 Shl 1
        'Region 1
        x=0
        y=Ry
        Plot xCenter+x,yCenter+y
        Plot xCenter-x,yCenter+y
        Plot xCenter+x,yCenter-y
        Plot xCenter-x,yCenter-y
        pFloat=(Ry2-(Rx2*Ry))+(0.25*Rx2)
        p=Int(pFloat)
        If pFloat Mod 1.0&gt;=0.5 Then p:+1
        px=0
        py=twoRx2*y
        While px&lt;py
                x:+1
                px:+twoRy2
                If p&gt;=0
                        y:-1
                        py:-twoRx2
                EndIf
                If p&lt;0 Then p:+Ry2+px Else p:+Ry2+px-py
                Plot xCenter+x,yCenter+y
                Plot xCenter-x,yCenter+y
                Plot xCenter+x,yCenter-y
                Plot xCenter-x,yCenter-y
        Wend
        'Region 2
        pFloat=(Ry2*(x+0.5)*(x+0.5))+(Rx2*(y-1.0)*(y-1.0))-(Rx2*(Float(Ry2)))
        p=Int(pFloat)
        If pFloat Mod 1.0&gt;=0.5 Then p:+1
        While y&gt;0
                y:-1
                py:-twoRx2
                If p&lt;=0
                        x:+1
                        px:+twoRy2
                EndIf
                If p&gt;0 Then p:+Rx2-py Else p:+Rx2-py+px
                Plot xCenter+x,yCenter+y
                Plot xCenter-x,yCenter+y
                Plot xCenter+x,yCenter-y
                Plot xCenter-x,yCenter-y
        Wend
        Flip
Until KeyHit(KEY_ESCAPE)
End</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>2005</font></td></tr></table></td></tr><tr ><td class="posttext"> This is meant to be in the BlitzMax code section. Sorry about that. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rck</td><td align="right"><font class=tiny>2005</font></td></tr></table></td></tr><tr ><td class="posttext"> <textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Midpoint ellipse algorithm

Graphics 640,480,0,2

Local xCenter% = 320
Local yCenter% = 240
Local Rx%, Ry%
Local p%, px%, py%, x%, y%
Local Rx2%, Ry2%, twoRx2%, twoRy2%
Local pFloat#

Repeat
        Cls
		WaitEvent()
		
		
        If MouseDown(1)
            xCenter = MouseX()
            yCenter = MouseY()
        EndIf
        Rx = Abs(xCenter - MouseX())
        Ry = Abs(yCenter - MouseY())
		
        Rx2 = Rx * Rx
        Ry2 = Ry * Ry
        twoRx2 = Rx2 Shl 1
        twoRy2 = Ry2 Shl 1
		
		msStart = MilliSecs()
		
        ;Region 1
        x = 0
        y = Ry

        Plot xCenter + x, yCenter + y
        Plot xCenter - x, yCenter + y
        Plot xCenter + x, yCenter - y
        Plot xCenter - x, yCenter - y
        pFloat = (Ry2 - (Rx2 * Ry)) + (0.25 * Rx2)
        p = Int(pFloat)
        If pFloat Mod 1.0 &gt;= 0.5 Then p = p + 1

        px = 0
        py = twoRx2 * y

        While px &lt; py
                x = x + 1
                px = px + twoRy2
                If p &gt;= 0
                        y = y - 1
                        py = py - twoRx2
                EndIf
                If p &lt; 0 Then p = p + Ry2 + px Else p = p + Ry2 + px - py
                Plot xCenter + x, yCenter + y
                Plot xCenter - x, yCenter + y
                Plot xCenter + x, yCenter - y
                Plot xCenter - x, yCenter - y
        Wend

        ;Region 2
        pFloat = (Ry2 * (x + 0.5) * (x + 0.5)) + (Rx2 * (y - 1.0) * (y - 1.0)) - (Rx2 * (Float(Ry2)))
        p = Int(pFloat)
        If pFloat Mod 1.0 &gt;= 0.5 Then p = p + 1
        While y &gt; 0
                y = y - 1
                py = py - twoRx2
                If p &lt;= 0
                        x = x +1
                        px = px + twoRy2
                EndIf
                If p &gt; 0 Then p = p + Rx2 - py Else p = p + Rx2 - py + px
                Plot xCenter + x, yCenter + y
                Plot xCenter - x, yCenter + y
                Plot xCenter + x, yCenter - y
                Plot xCenter - x, yCenter - y
        Wend

		timeToDraw = MilliSecs() - msStart
		Text 20, 20, Rx + " x " + Ry
		Text 20, 30, "Draw time " + timeToDraw + " ms"
		
        Flip(False)
Until KeyHit(1)
</textarea><br><br><br>Here is a port to BlitzPlus code for those interested (and an additional waitevent - oh how i love progs that aren't always 100% on my CPU). Oh, and I added an ms counter to see how long the calculations and drawing take. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rck</td><td align="right"><font class=tiny>2005</font></td></tr></table></td></tr><tr ><td class="posttext"> <textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Midpoint ellipse algorithm

Const GAME_WIDTH = 800
Const GAME_HEIGHT = 600
Graphics  GAME_WIDTH, GAME_HEIGHT, 0, 2

Local xCenter% = GAME_WIDTH / 2
Local yCenter% = GAME_HEIGHT / 2
Local Rx%, Ry%
Local p%, px%, py%, x%, y%
Local Rx2%, Ry2%, twoRx2%, twoRy2%
Local pFloat#

Const fillColor% = $FF

Repeat
        Cls
		WaitEvent()
		
		
        If MouseDown(1)
            xCenter = MouseX()
            yCenter = MouseY()
        EndIf
        Rx = Abs(xCenter - MouseX())
        Ry = Abs(yCenter - MouseY())
		
        Rx2 = Rx * Rx
        Ry2 = Ry * Ry
        twoRx2 = Rx2 Shl 1
        twoRy2 = Ry2 Shl 1
		
		msStart = MilliSecs()
		
        ;Region 1
        x = 0
        y = Ry
		
		LockBuffer(BackBuffer)
		
		wpf(xCenter + x, yCenter + y)
        wpf(xCenter - x, yCenter + y)
        wpf(xCenter + x, yCenter - y)
        wpf(xCenter - x, yCenter - y)

        pFloat = (Ry2 - (Rx2 * Ry)) + (0.25 * Rx2)
        p = Int(pFloat)
        If pFloat Mod 1.0 &gt;= 0.5 Then p = p + 1

        px = 0
        py = twoRx2 * y

        While px &lt; py
                x = x + 1
                px = px + twoRy2
                If p &gt;= 0
                        y = y - 1
                        py = py - twoRx2
                EndIf
                If p &lt; 0 Then p = p + Ry2 + px Else p = p + Ry2 + px - py
                wpf(xCenter + x, yCenter + y)
                wpf(xCenter - x, yCenter + y)
                wpf(xCenter + x, yCenter - y)
                wpf(xCenter - x, yCenter - y)
        Wend

        ;Region 2
        pFloat = (Ry2 * (x + 0.5) * (x + 0.5)) + (Rx2 * (y - 1.0) * (y - 1.0)) - (Rx2 * (Float(Ry2)))
        p = Int(pFloat)
        If pFloat Mod 1.0 &gt;= 0.5 Then p = p + 1
        While y &gt; 0
                y = y - 1
                py = py - twoRx2
                If p &lt;= 0
                        x = x +1
                        px = px + twoRy2
                EndIf
                If p &gt; 0 Then p = p + Rx2 - py Else p = p + Rx2 - py + px
                wpf(xCenter + x, yCenter + y)
                wpf(xCenter - x, yCenter + y)
                wpf(xCenter + x, yCenter - y)
                wpf(xCenter - x, yCenter - y)
        Wend

		UnlockBuffer(BackBuffer)
		

		timeToDraw = MilliSecs() - msStart
		Text 20, 20, Rx + " x " + Ry
		Text 20, 30, "Draw time " + timeToDraw + " ms"
		
        Flip(False)
Until KeyHit(1)


Function wpf(x,y)
  ;safe WritePixelFast
  If x &lt; 0 Or x &gt;= GAME_WIDTH Then Return 0
  If y &lt; 0 Or y &gt;= GAME_HEIGHT Then Return 0
  WritePixelFast(x, y, fillColor%)
End Function
</textarea><br><br>And here is virtually the same code just using writepixelfast on a locked buffer <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>2005</font></td></tr></table></td></tr><tr ><td class="posttext"> Cool, thanks. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
