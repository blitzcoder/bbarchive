<!DOCTYPE html><html lang="en" ><head ><title >Shadow from an image</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Creates a shadow from an image, language=bb, category=Graphics'><meta name='author' content='aCiD2'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/Shadow from an image</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="869.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Shadow from an image by aCiD2</td><td align="right">2003 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This function creates a shadow an image, by reading the "red" value and setting it to all three rgb values, amking a grey. its reasonably fast aswell. </td></tr><tr ><td class="cell"><pre class="code">Function GenerateShadow(img, r = 255, g = 0, b = 255)
	setbuffer imagebuffer(img)
	lockbuffer
	for x = 0 to imagewidth(img) - 1
		for y = 0 to imageheight(img) - 1
			col = readpixelfast (x, y)
			color 0, 0, col
			if not (colorred() = r and colorgreen() = g and colorblue() = 255)
				color colorred(), colorred(), colorred()
				plot x, y
			endif
		next
	next
	unlockbuffer
	setbuffer backbuffer()
	
	return img
end function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >bobbo</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Hmmm... it doesn't work on BlitzPlus.<br>First: you are writing on the img, it is better to create a copy.<br>Second: you should set the buffer back to the original Buffer, not to BackBuffer().<br>Third: you can't use Plot on a locked buffer, you must unlock it first.<br><br><pre class=code>
; SHADOW GENERATOR, the mask color is not affected
Function GenerateShadow(sourceImg, r = 0, g = 0, b =0)
	
	img=CopyImage(sourceImg)
	
	originalBuffer=GraphicsBuffer()
	
	SetBuffer ImageBuffer(img)
	LockBuffer
	For x = 0 To ImageWidth(img) - 1
		For y = 0 To ImageHeight(img) - 1
		
			col = ReadPixelFast (x, y)
			
			; convert to rgb values
			r1=(col Shl 8)Shr 24
			g1=(col Shl 16)Shr 24
			b1=(col Shl 24)Shr 24
						
			If Not (r1 = r And g1 = g And b1 = b)
			
				UnlockBuffer

				Color r1,r1,r1
								
				Plot x,y
				;WritePixelFast x,y,calculatenewcolor
				LockBuffer
				
			EndIf
		Next
	Next
	
	UnlockBuffer
	SetBuffer originalBuffer
	
	Return img
	
End Function
</pre><br><br>Use WritePixelFast would be much better than Plot of course, just let me find some time to code it.<br>This is more a grayscale converter than a real shadow... but it is easier to convert it into a shadow, you must use always the same r g b gray value instead of converting each rgb color to a different grayscale level. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >bobbo</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Here is the final version of the functions:<br>GenerateShadow() for genearting a shadow with a unique gray color, and GenerateGrayScale() for a grayscale version of the orignal image<br><br>There is also a small example to test it, just change the path and maskcolor of your image (I tried with a red maskcolor).<br><br><pre class=code>
Graphics 640,480,16,2
SetBuffer BackBuffer()

myImage=LoadImage("gfx\zelda1.bmp")
MaskImage myImage,255,0,0

myShadow=GenerateShadow(myImage,255,0,0,80)
myGrayScale=GenerateGrayScale(myImage,255,0,0)


While KeyDown(1)=0

 Cls
 
 
 DrawImage myShadow,MouseX(),MouseY()
 DrawImage myGrayScale,MouseX()-100,MouseY()-100
 DrawImage myImage,0,0
  
 Flip
 
Wend

End

; SHADOW GENERATOR, the mask color is not affected
; coded by Roberto Comuniello, 2004, www.sintetik.it
; sourceImg: source Image handle
; r: red mask color
; g: green mask color
; b: blue mask color
; shadowLevel: intensity of shadow (grey color) from 0 to 255
Function GenerateShadow(sourceImg, r = 0, g = 0, b =0, shadowLevel=100)

	img=CopyImage(sourceImg)
	myWidth=ImageWidth(img)-1
	myHeight=ImageHeight(img)-1
	myMask=GetRGB(r,g,b)
	myShadow=GetRGB(shadowLevel,shadowLevel,shadowLevel)
	
	originalBuffer=GraphicsBuffer()
	SetBuffer ImageBuffer(img)
	LockBuffer
	
	For x = 0 To myWidth
		For y = 0 To myHeight
		
			col = ReadPixelFast (x, y)
								
			If Not (col=myMask)
			
				WritePixelFast x,y,myShadow
								
			EndIf
		Next
	Next
	
	UnlockBuffer
	SetBuffer originalBuffer
	
	Return img
	
End Function

; GRAYSCALE GENERATOR, the mask color is not affected
; the grayscale is based on the red color of each pixels
; coded by Roberto Comuniello, 2004, www.sintetik.it
; it works fine even if you have a red mask (255,0,0)
; sourceImg: source Image handle
; r: red mask color
; g: green mask color
; b: blue mask color
Function GenerateGrayScale(sourceImg, r = 0, g = 0, b =0)

	img=CopyImage(sourceImg)
	myWidth=ImageWidth(img)-1
	myHeight=ImageHeight(img)-1
	myMask=GetRGB(r,g,b)
		
	originalBuffer=GraphicsBuffer()
	SetBuffer ImageBuffer(img)
	LockBuffer
	
	For x = 0 To myWidth
		For y = 0 To myHeight
		
			col = ReadPixelFast (x, y)
								
			If Not (col=myMask)
			
				r1=GetR(col)
				myNewColor=GetRGB(r1,r1,r1)		
				WritePixelFast x,y,myNewColor
								
			EndIf
		Next
	Next
	
	UnlockBuffer
	SetBuffer originalBuffer
	
	Return img
	
End Function

; general purpose RGB converter functions
Function GetRGB(R,G,B)
 Return $FF000000 Or R Shl 16 Or G Shl 8 Or B
End Function

Function GetR(RGB)
 Return RGB Shr 16 And %11111111
End Function

Function GetG(RGB)
 Return RGB Shr 8 And %11111111
End Function

Function GetB(RGB)
 Return RGB And %11111111
End Function
</pre> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >bobbo</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> Here is the final version, with shadow transparency as an option! Simple and quite good to see. Just try it with a tiled background as I did.<br>Maybe it should be optimized but it is already quite good.<br><br><pre class=code>
Graphics 640,480,16,2
SetBuffer BackBuffer()

myImage=LoadImage("gfx\zelda1.bmp")
MaskImage myImage,255,0,0

myShadow=GenerateShadow(myImage,255,0,0,80,1)
myGrayScale=GenerateGrayScale(myImage,255,0,0)

myTile=LoadImage("gfx\tile.bmp")



While KeyDown(1)=0

 TileBlock myTile
  
 DrawImage myShadow,MouseX(),MouseY()
 DrawImage myGrayScale,MouseX()-100,MouseY()-100
 DrawImage myImage,0,0
  
 Flip
 
Wend

End

; SHADOW GENERATOR, the mask color is not affected
; coded by Roberto Comuniello, 2004, www.sintetik.it
; sourceImg: source Image handle
; r: red mask color
; g: green mask color
; b: blue mask color
; shadowLevel: intensity of shadow (grey color) from 0 to 255
; transparent flag: true or false (a fake, cheap transparency effect made with mask color)
Function GenerateShadow(sourceImg, r = 0, g = 0, b =0, shadowLevel=100, transparent=False)

	img=CopyImage(sourceImg)
	myWidth=ImageWidth(img)-1
	myHeight=ImageHeight(img)-1
	myMask=GetRGB(r,g,b)
	myShadow=GetRGB(shadowLevel,shadowLevel,shadowLevel)
	
	originalBuffer=GraphicsBuffer()
	SetBuffer ImageBuffer(img)
	LockBuffer
	
	flipper=False
	
	For x = 0 To myWidth
	
		flipper=Not flipper
	
		For y = 0 To myHeight
		
			col = ReadPixelFast (x, y)
								
			If Not (col=myMask) Then
				
				If flipper Then
					myCondition=(y Mod 2)
				Else
					myCondition=(Not(y Mod 2))
				End If
						
				If transparent And myCondition Then
					theShadow=myMask
				Else
					theShadow=myShadow
				End If
				
				WritePixelFast x,y,theShadow
								
			EndIf
		Next
	Next
	
	UnlockBuffer
	SetBuffer originalBuffer
	
	Return img
	
End Function

; GRAYSCALE GENERATOR, the mask color is not affected
; the grayscale is based on the red color of each pixel
; coded by Roberto Comuniello, 2004, www.sintetik.it
; it works fine even if you have a red mask (255,0,0)
; sourceImg: source Image handle
; r: red mask color
; g: green mask color
; b: blue mask color
Function GenerateGrayScale(sourceImg, r = 0, g = 0, b =0)

	img=CopyImage(sourceImg)
	myWidth=ImageWidth(img)-1
	myHeight=ImageHeight(img)-1
	myMask=GetRGB(r,g,b)
		
	originalBuffer=GraphicsBuffer()
	SetBuffer ImageBuffer(img)
	LockBuffer
	
	For x = 0 To myWidth
		For y = 0 To myHeight
		
			col = ReadPixelFast (x, y)
								
			If Not (col=myMask)
			
				r1=GetR(col)
				myNewColor=GetRGB(r1,r1,r1)		
				WritePixelFast x,y,myNewColor
								
			EndIf
		Next
	Next
	
	UnlockBuffer
	SetBuffer originalBuffer
	
	Return img
	
End Function

; general purpose RGB converter functions
Function GetRGB(R,G,B)
 Return $FF000000 Or R Shl 16 Or G Shl 8 Or B
End Function

Function GetR(RGB)
 Return RGB Shr 16 And %11111111
End Function

Function GetG(RGB)
 Return RGB Shr 8 And %11111111
End Function

Function GetB(RGB)
 Return RGB And %11111111
End Function
</pre> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >aCiD2</td><td align="right"><font class=tiny>2004</font></td></tr></table></td></tr><tr ><td class="posttext"> wtf was i thinking using plot?! this was quite a whiel ago mind, and must of the comments in the first post would never have occoured :P thanks for the alpha shadow one (i had a function like that but never uploaded it :D) Should be useful to everyone else i think <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
