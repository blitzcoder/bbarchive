<!DOCTYPE html><html lang="en" ><head ><title >FloodFill - Really!!!</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='A real, working Blitz Basic Flood Fill routine., language=bb, category=Graphics'><meta name='author' content='Adam Novagen'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/FloodFill - Really!!!</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2157.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >FloodFill - Really!!! by Adam Novagen</td><td align="right">2007 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> <b><u>FLOODFILL</u></b><br>By Adam Novagen<br><br>After just an hour &amp; a half of typing, I've finally perfected a Flood Fill routine, like those used in paint programs! It can theoretically fill any size image region, and any shape too!<br>Depending on computer configuration, graphics mode, and the number of pixels to be filled, the fill can take anywhere from 5 to 1000 milliseconds (.005 to 1 seconds); just trust the code!<br><br><b><u>HOW TO USE FLOODFILL()</u></b><br>FloodFill() is declared like this (not verbatim):<br><div class="quote"> FloodFill(Image,Fill X,Fill Y,Fill Red,Fill Green,Fill Blue) <br></div><br><b>Image</b> is the image to be filled; I'm afraid the routine doesn't work directly on graphics buffers. <b>Fill X</b> &amp; <b>Y</b> are the starting coordinates for the fill, and <b>Fill Red, Green</b> and <b>Blue</b> are the RGB color values to fill with.<br><br><b><u>THE TESTING LOOP</u></b><br>This file contains a small testing loop at the end. It draws an image of your choice at 0,0, and shows the mouse location with a flashing pixel. Left-clicking on the image will fill that part with a random color; the FloodFill() RGB values in the testing loop are set to Rand(255). The Esc key ends the program, as usual.<br><br><b><u>OPTIONAL CODE</u></b><br>The FloodFill() routine contains two sets of optional code, which at the moment has been commented with a semicolon (;). The first line, the interrupt line, is a special "Panic Button" line that lets the Esc key end the program at any point during the execution of FloodFill(). You might want to uncomment this until you have faith in FloodFill's ability to avoid crashes &amp; freezes, but trust me, you won't need it forever!<br>The second bit of optional code lets you watch the fill in action (theoretically.) Depending on your computer configuration, this will probably slow FloodFill() down, but it's worth watching at least once; in a small image, that is!<br><br><b><u>ADDITIONAL FUNCTIONS: PIXELRED/GREEN/BLUE()</u></b><br>PixelRed(), PixelGreen(), and PixelBlue() are function used by FloodFill to obtain color information of certain pixels without changing the actual Color R,G,B settings. However, you can also use them like this:<br><div class="quote"> PixelRed(Pixel X,Pixel Y) <br></div><br>PixelGreen() &amp; Blue() are declared the same way, and all three return a value from 0 to 255.<br><br><b><u>FINAL NOTES</u></b><br>All the variables in the FloodFill routine, except for the Pixel type, are preceded by "Ghz_Lib_Var_". This is to minimize the possibility of you accidentally creating a variable with the same name, thus causing FloodFill() or your program to generate bugs. "Ghz_Lib_Var_" is used because FloodFill() and the Pixel functions are all used in the Gigahertz Games Command Library. Gigahertz_Library_Variable = Ghz_Lib_Var, get it? You can mess around with these variables if you <i>really</i> want to get into the FloodFill routine, but I only recommend that for expert Blitzers.<br><br>Enjoy! </td></tr><tr ><td class="cell"><pre class="code">Type Pixel
	Field X,Y
End Type


Function FloodFill(FillImage,FX,FY,FR,FG,FB);COMPLETE


If FX &lt; 0 Or FX &gt; ImageWidth(FillImage) - 1 Or FY &lt; 0 Or FY &gt; ImageHeight(FillImage) - 1
	RuntimeError("GHZLIB ERROR" + Chr(10) + "Invalid coordinates: " + FX + "," + FY + Chr(10) + "Coords must be within image boundaries")
EndIf


Local CurrentBuffer = GraphicsBuffer()
Local CurrentRed = ColorRed()
Local CurrentGreen = ColorGreen()
Local CurrentBlue = ColorBlue()


SetBuffer ImageBuffer(FillImage)
LockBuffer


Local TarR = PixelRed(FX,FY)
Local TarG = PixelGreen(FX,FY)
Local TarB = PixelBlue(FX,FY)


Color FR,FG,FB
WritePixelFast FX,FY,FB + (FG * 256) + (FR * (256 * 256))
Pixel.Pixel = New Pixel
Pixel\X = FX
Pixel\Y = FY


Repeat
Local PixelsRemaining = False
For Pixel.Pixel = Each Pixel
Local PixelLeft = False
Local PixelAbove = False
Local PixelRight = False
Local PixelBelow = False
Local PixelX = Pixel\X
Local PixelY = Pixel\Y
If Pixel\X &gt; 0;check left
	If PixelRed(Pixel\X - 1,Pixel\Y) = TarR And PixelGreen(Pixel\X - 1,Pixel\Y) = TarG And PixelBlue(Pixel\X - 1,Pixel\Y) = TarB
		PixelLeft = True
		PixelsRemaining = True
	EndIf
EndIf
If Pixel\Y &gt; 0;check above
	If PixelRed(Pixel\X,Pixel\Y - 1) = TarR And PixelGreen(Pixel\X,Pixel\Y - 1) = TarG And PixelBlue(Pixel\X,Pixel\Y - 1) = TarB
		PixelAbove = True
		PixelsRemaining = True
	EndIf
EndIf
If Pixel\X &lt; ImageWidth(FillImage) - 1;check right
	If PixelRed(Pixel\X + 1,Pixel\Y) = TarR And PixelGreen(Pixel\X + 1,Pixel\Y) = TarG And PixelBlue(Pixel\X + 1,Pixel\Y) = TarB
		PixelRight = True
		PixelsRemaining = True
	EndIf
Else
	PixelRight = False
EndIf
If Pixel\Y &lt; ImageHeight(FillImage) - 1;check below
	If PixelRed(Pixel\X,Pixel\Y + 1) = TarR And PixelGreen(Pixel\X,Pixel\Y + 1) = TarG And PixelBlue(Pixel\X,Pixel\Y + 1) = TarB
		PixelBelow = True
		PixelsRemaining = True
	EndIf
Else
	PixelBelow = False
EndIf
;If KeyDown(1) Then End
Delete Pixel
PixelNum = PixelNum - 1
;LockBuffer
If PixelLeft = True
	Pixel.Pixel = New Pixel
	Pixel\X = PixelX - 1
	Pixel\Y = PixelY
	PixelNum = PixelNum + 1
	PixelLeft = False
	WritePixelFast Pixel\X,Pixel\Y,FB + (FG * 256) + (FR * (256 * 256))
EndIf
If PixelAbove = True
	Pixel.Pixel = New Pixel
	Pixel\X = PixelX
	Pixel\Y = PixelY - 1
	PixelNum = PixelNum + 1
	PixelAbove = False
	WritePixelFast Pixel\X,Pixel\Y,FB + (FG * 256) + (FR * (256 * 256))
EndIf
If PixelRight = True
	Pixel.Pixel = New Pixel
	Pixel\X = PixelX + 1
	Pixel\Y = PixelY
	PixelNum = PixelNum + 1
	PixelRight = False
	WritePixelFast Pixel\X,Pixel\Y,FB + (FG * 256) + (FR * (256 * 256))
EndIf
If PixelBelow = True
	Pixel.Pixel = New Pixel
	Pixel\X = PixelX
	Pixel\Y = PixelY + 1
	PixelNum = PixelNum + 1
	PixelBelow = False
	WritePixelFast Pixel\X,Pixel\Y,FB + (FG * 256) + (FR * (256 * 256))
EndIf
;!UNCOMMENT THE FOLLOWING CODE TO SEE FLOODFILL IN ACTION!
;UnlockBuffer
;SetBuffer CurrentBuffer
;DrawImage FillImage,0,0
;Flip
;SetBuffer ImageBuffer(FillImage)
;LockBuffer
Next
Until PixelsRemaining = False

UnlockBuffer
SetBuffer CurrentBuffer
Color CurrentRed,CurrentGreen,CurrentBlue


End Function


Function PixelGreen(Ghz_Lib_Var_PixelX,Ghz_Lib_Var_PixelY)


Local Ghz_Lib_Var_CurrentRed = ColorRed()
Local Ghz_Lib_Var_CurrentGreen = ColorGreen()
Local Ghz_Lib_Var_CurrentBlue = ColorBlue()

GetColor(Ghz_Lib_Var_PixelX,Ghz_Lib_Var_PixelY)
Local Ghz_Lib_Var_Green = ColorGreen()

Color Ghz_Lib_Var_CurrentRed,Ghz_Lib_Var_CurrentGreen,Ghz_Lib_Var_CurrentBlue

Return Ghz_Lib_Var_Green


End Function


Function PixelRed(Ghz_Lib_Var_PixelX,Ghz_Lib_Var_PixelY)


Local Ghz_Lib_Var_CurrentRed = ColorRed()
Local Ghz_Lib_Var_CurrentGreen = ColorGreen()
Local Ghz_Lib_Var_CurrentBlue = ColorBlue()

GetColor(Ghz_Lib_Var_PixelX,Ghz_Lib_Var_PixelY)
Local Ghz_Lib_Var_Red = ColorRed()

Color Ghz_Lib_Var_CurrentRed,Ghz_Lib_Var_CurrentGreen,Ghz_Lib_Var_CurrentBlue

Return Ghz_Lib_Var_Red


End Function


Function PixelBlue(Ghz_Lib_Var_PixelX,Ghz_Lib_Var_PixelY)


Local Ghz_Lib_Var_CurrentRed = ColorRed()
Local Ghz_Lib_Var_CurrentGreen = ColorGreen()
Local Ghz_Lib_Var_CurrentBlue = ColorBlue()

GetColor(Ghz_Lib_Var_PixelX,Ghz_Lib_Var_PixelY)
Local Ghz_Lib_Var_Blue = ColorBlue()

Color Ghz_Lib_Var_CurrentRed,Ghz_Lib_Var_CurrentGreen,Ghz_Lib_Var_CurrentBlue

Return Ghz_Lib_Var_Blue


End Function


;!TESTING LOOP
;To use this mini-program, just click anywhere on the image.
;The cursor is represented by a flashing pixel.
Graphics 800,600,0,2
Global Image = LoadImage("sample image 1.bmp");replace "image.bmp" with a valid filename
SetBuffer BackBuffer()
SeedRnd MilliSecs()
While Not KeyDown(1)
Cls
If MouseHit(1) Then FloodFill(Image,MouseX(),MouseY(),Rand(255),Rand(255),Rand(255))
DrawImage Image,0,0
Color Rand(255),Rand(255),Rand(255)
Plot MouseX(),MouseY();the makeshift cursor
Flip
Wend
End</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_33</td><td align="right"><font class=tiny>2008</font></td></tr></table></td></tr><tr ><td class="posttext"> Very handy, thank you!<br><br>My pimped up version (for archival purpose):<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics 800,600,0,2
Global Image = LoadImage("apple_logo_640x480.jpg") ; &lt;---- put image filename here
SetBuffer BackBuffer()
SeedRnd MilliSecs()
While Not KeyDown(1)
	Cls
	If MouseHit(1) Then FloodFill(Image,MouseX(),MouseY(),Rand(255),Rand(255),Rand(255))
	DrawImage Image,0,0
	Color 255,255,255
	Plot MouseX(),MouseY()
	Flip
Wend
End


Type Pixel
	Field X,Y
End Type

Function FloodFill(FillImage, Fill_X, Fill_Y, FR, FG, FB)
	Local width% = ImageWidth(FillImage)
	Local height% = ImageHeight(FillImage)

	If Fill_X &lt; 0 Or Fill_X &gt; width - 1 Or Fill_Y &lt; 0 Or Fill_Y &gt; height - 1
		Return ; Coords ouside image boundaries
	EndIf

	Local CurrentBuffer = GraphicsBuffer()
	SetBuffer ImageBuffer(FillImage)
	LockBuffer

	Local Current_RGB% = ReadPixelFast(Fill_X, Fill_Y)

	Local RGB% = FB + FG Shl 8 + FR Shl 16
	Pixel.Pixel = New Pixel
	Pixel\X = Fill_X
	Pixel\Y = Fill_Y
	WritePixelFast Pixel\X,Pixel\Y,RGB

	Repeat
		Local PixelsRemaining = False
		For Pixel.Pixel = Each Pixel
			PixelX = Pixel\X
			PixelY = Pixel\Y
			PixelLeft = False
			PixelAbove = False
			PixelRight = False
			PixelBelow = False
			If Pixel\X &gt; 0;check left
				If Current_RGB = ReadPixelFast(Pixel\X - 1,Pixel\Y) Then
					PixelLeft = True
					PixelsRemaining = True
				EndIf
			EndIf
			If Pixel\Y &gt; 0;check above
				If Current_RGB = ReadPixelFast(Pixel\X,Pixel\Y - 1) Then
					PixelAbove = True
					PixelsRemaining = True
				EndIf
			EndIf
			If Pixel\X &lt; width - 1;check right
				If Current_RGB = ReadPixelFast(Pixel\X + 1,Pixel\Y) Then
					PixelRight = True
					PixelsRemaining = True
				EndIf
			EndIf
			If Pixel\Y &lt; height - 1;check below
				If Current_RGB = ReadPixelFast(Pixel\X,Pixel\Y + 1) Then
					PixelBelow = True
					PixelsRemaining = True
				EndIf
			EndIf
			Delete Pixel
			If PixelLeft = True
				Pixel.Pixel = New Pixel
				Pixel\X = PixelX - 1
				Pixel\Y = PixelY
				PixelLeft = False
				WritePixelFast Pixel\X,Pixel\Y,RGB
			EndIf
			If PixelAbove = True
				Pixel.Pixel = New Pixel
				Pixel\X = PixelX
				Pixel\Y = PixelY - 1
				PixelAbove = False
				WritePixelFast Pixel\X,Pixel\Y,RGB
			EndIf
			If PixelRight = True
				Pixel.Pixel = New Pixel
				Pixel\X = PixelX + 1
				Pixel\Y = PixelY
				PixelRight = False
				WritePixelFast Pixel\X,Pixel\Y,RGB
			EndIf
			If PixelBelow = True
				Pixel.Pixel = New Pixel
				Pixel\X = PixelX
				Pixel\Y = PixelY + 1
				PixelBelow = False
				WritePixelFast Pixel\X,Pixel\Y,RGB
			EndIf
		Next
	Until PixelsRemaining = False
	UnlockBuffer
	SetBuffer CurrentBuffer
End Function
</textarea> <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
