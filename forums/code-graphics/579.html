<!DOCTYPE html><html lang="en" ><head ><title >3D Maze</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Tiny Raycasting Engine, language=bb, category=Graphics'><meta name='author' content='jfk EO-11110'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/3D Maze</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="579.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >3D Maze by jfk EO-11110</td><td align="right">2003 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Simple 2.5D Raycasting Engine, enjoy. </td></tr><tr ><td class="cell"><pre class="code">Graphics 320,240,16,2
SetBuffer BackBuffer()

; read level data
Dim grid(15,15)
For j=0 To 15
 For i=0 To 15
  Read grid(i,j)
 Next
Next
Dim rgb(9,2)
For i=1 To 9
 Read rgb(i,0):Read rgb(i,1):Read rgb(i,2)
Next

Global px#=8.0
Global pz#=8.0
Global a#, pxold#=0.0, pzold#=0.0, premind#, info$
;__________________________MAINLOOP_________________________
While KeyDown(1)=0
 Cls
 If KeyDown(200) Then ; up
  pxold=px
  pzold=pz
  px=px+Sin(a)/5.0
  pz=pz+Cos(a)/5.0
  sliding()
 EndIf
 If KeyDown(208) Then ; down
  pxold=px
  pzold=pz
  px=px-Sin(a)/5.0
  pz=pz-Cos(a)/5.0
  sliding()
 EndIf
 a=(a-mxs#) Mod 360 ; use mouse to steer
 raycast() ; do magic (well, actually not)
 Color 0,255,0 ; map info onscreen...
 Line (px*5-Sin(a)*2),(pz*5-Cos(a)*2),(px*5+Sin(a)*2),(pz*5+Cos(a)*2)
 Color 255,0,0
 Oval (px*5+(Sin(a)*2))-1,(pz*5+(Cos(a)*2))-1,2,2
 Color 127,127,127
 For j=0 To 15
  For i=0 To 15
   If grid(i,j)&lt;&gt;0
    Rect i*5-3,j*5-3,5,5,0
   EndIf
  Next
 Next
 If info$&lt;&gt;"" Then Text GraphicsWidth()/2,GraphicsHeight()/2,info$,1,1
 Flip
 mxs#=MouseXSpeed()/3.0 ; used by steer
 MoveMouse GraphicsWidth()/2,GraphicsHeight()/2
Wend
End
;________________________eo mainloop__________________________

Function sliding()
  If grid(px,pz)&lt;&gt;0 ; sliding collision...
   premind=px
   px=pxold
   If grid(px,pz)&lt;&gt;0
    px=premind
    pz=pzold
    If grid(px,pz)&lt;&gt;0
     px=pxold
    EndIf
   EndIf
  EndIf
End Function

Function raycast()
 For i=-160 To 159
  rayx#=px
  rayz#=pz
  stepx#=Sin(a+0.125*i)/50.0  ; pretty small steps - would be way to slow on old machines
  stepz#=Cos(a+0.125*i)/50.0
  count=0
  touched=0
  While touched=0 And count&lt;1000
   rayx#=rayx+stepx
   rayz#=rayz+stepz
   count=count+1
   If grid(rayx,rayz)&lt;&gt;0 Then
    touched=grid(rayx,rayz)
   EndIf
  Wend
  If touched&lt;&gt;0 Then
   h#=7000.0/count
   bright#=(h#+20.0)/127.0
   If bright&gt;1.0 Then bright=1.0
   Color rgb(touched,0)*bright,rgb(touched,1)*bright,rgb(touched,2)*bright
   Line 160-i,120-h,160-i,120+h
   If touched=9
    If info$=""
     info$="Level Exit found!!! Congratzz!!!"
    EndIf
   EndIf
  EndIf
 Next
End Function

; map
Data 1,2,1,2,1,2,1,2,1,2,1,2,1,2,1,2
Data 2,9,0,0,1,0,0,0,0,0,0,0,0,0,0,1
Data 1,0,0,0,0,0,4,3,5,6,5,6,0,0,0,2
Data 2,0,1,3,4,2,2,0,0,0,0,2,3,0,0,1
Data 1,2,1,0,0,0,2,1,4,0,0,1,2,2,0,2
Data 2,0,0,0,4,0,0,0,7,0,0,0,1,0,0,1
Data 1,0,8,4,3,0,0,0,0,0,0,0,1,2,0,2
Data 2,0,7,0,0,0,0,0,0,4,5,4,1,0,0,1
Data 1,0,6,0,0,1,0,0,0,1,0,0,2,2,0,2
Data 2,0,5,1,2,1,3,0,1,1,0,0,0,0,0,1
Data 1,0,4,0,0,0,0,0,2,4,2,4,2,2,0,2
Data 2,0,3,1,0,0,0,0,0,0,3,0,0,1,0,1
Data 1,0,2,1,1,0,0,0,0,0,0,0,0,1,0,2
Data 2,0,1,2,3,4,5,4,5,4,5,5,6,3,0,1
Data 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,2
Data 2,1,2,1,2,1,2,1,2,1,2,1,2,1,2,1

; wall colors: 1,2,3...
Data 127,127,255
Data 64,64,255
Data 255,127,255
Data 255,0,0
Data 0,255,0
Data 0,0,255
Data 255,0,255
Data 0,255,255
Data 255,255,0
Data 255,255,255</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >impixi</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> BlitzMax port and modification:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Graphics 320,240

Local MV:TMazeView = New TMazeView
MV.loadMap
MV.loadColors

Local a:Float
Local mxs:Float

Local OldTime:Int = 0
Local CurTime:Int

HideMouse

' *** MAIN LOOP ***

While Not KeyHit(KEY_ESCAPE)

	Local mxs:Float = (MouseX() - GraphicsWidth() / 2) / 3.0
	MoveMouse (GraphicsWidth() / 2), (GraphicsHeight() / 2)

	Cls
 
	CurTime = 1000 / (MilliSecs() - OldTime)
 	OldTime = MilliSecs()

	If KeyDown(KEY_W)
		MV.moveForward a
	EndIf
	
	If KeyDown(KEY_S) 
		MV.moveBackward a
	EndIf
	
	If KeyDown(KEY_Q) 'turn left 
		a :+ 2
 	EndIf

	If KeyDown(KEY_E) 'turn right
		a :- 2
 	EndIf
	
	a = (a - mxs) Mod 360 
	
 	MV.raycast a 

	MV.drawMap a 

 	If MV.info &lt;&gt; "" Then DrawText MV.info, 1, (GraphicsHeight() - 20)

 	DrawText "WQSE and mouse to move. FPS:" + CurTime, 1, (GraphicsHeight() - (2 * 20))

	Flip 1

Wend

End


Type TMazeView

	Field Cols:Int
	Field Rows:Int
	Field Grid:Int[,]
	Field RGB:Byte[,]

	Field px:Float 
	Field pxold:Float
	Field pz:Float
	Field pzold:Float
	Field premind:Float
	
	Field info:String

	Method New()
	
		Cols = 16
		Rows = 16
		Grid = New Int[cols, rows]
		RGB = New Byte[10, 3]
	
		px = 8.0
		pxold = 0.0
		pz = 8.0
		pzold = 0.0
		
		info = ""	
	
	EndMethod

	Method loadMap()

		RestoreData Map

		For Local j:Int = 0 To Rows - 1
			For Local i:Int = 0 To Cols - 1
				ReadData Grid[i, j]
 			Next
		Next
	
	EndMethod
	
	Method loadColors()
	
		RestoreData Colors 
		For Local i:Int = 0 To 9
			ReadData RGB[i, 0]
 			ReadData RGB[i, 1]
			ReadData RGB[i, 2]
		Next
	
	EndMethod

	Method checkCollision()
	
		If Grid[px, pz] &lt;&gt; 0 
  			premind = px
			px = pxold
   			If Grid[px, pz] &lt;&gt; 0
				px = premind
				pz = pzold
			    If Grid[px, pz] &lt;&gt; 0
					px = pxold
				EndIf
			EndIf
		 EndIf

	EndMethod

	Method drawMap(a:Float)
				
		SetColor 0,255,0
	
 		DrawLine (px * 5 - Sin(a) * 2), (pz * 5 - Cos (a) * 2), (px * 5 + Sin(a) * 2), (pz * 5 + Cos(a) * 2)
		SetColor 255, 0, 0
	
		DrawOval (px * 5 + (Sin(a) * 2)) - 1, (pz * 5 + (Cos(a) * 2)) - 1, 2, 2
	
		SetColor 127, 127, 127

		SetBlend ALPHABLEND
		SetAlpha 0.5

		For Local j:Int = 0 To (Rows - 1)
  			For Local i:Int = 0 To (Cols - 1)
   				If Grid[i, j] &lt;&gt; 0
    				DrawRect (i * 5 - 3), (j * 5 - 3), 5, 5
				EndIf
			Next
		Next
	
		SetBlend SOLIDBLEND
	
	EndMethod

	Method moveForward(a:Float)

  		pxold = px
  		pzold = pz
  		px :+ Sin(a) / 5.0
  		pz :+ Cos(a) / 5.0
  		CheckCollision

	EndMethod

	Method moveBackward(a:Float)
	
		pxold = px
		pzold = pz
		px :- Sin(a) / 5.0
		pz :- Cos(a) / 5.0
		CheckCollision
		
	EndMethod

	Method raycast(a:Float)
	
		For Local i:Int = -160 To 159
		
			Local rayx:Float = px
			Local rayz:Float = pz
			Local stepx:Float = Sin(a + 0.125 * i) / 50.0  
  			Local stepz:Float = Cos( a + 0.125 * i) / 50.0
  			Local count:Int = 0
  			Local touched:Int = 0
			
			While touched = 0 And count &lt; 1000
		  		rayx :+ stepx
				rayz :+ stepz
				count :+ 1
				If Grid[rayx, rayz] &lt;&gt; 0 Then touched = Grid[rayx, rayz]
			Wend
			
			If touched &lt;&gt; 0 
				Local h:Float = 7000.0 / count
				Local bright:Float = (h + 20.0) / 127.0
				If bright &gt; 1.0 Then bright = 1.0
				SetColor RGB[touched, 0] * bright, RGB[touched, 1] * bright, RGB[touched, 2] * bright
				DrawLine 160 - i, 120 - h, 160 - i, 120 + h
				If touched = 9
				    If info = ""
						info = "Level Exit found!!! Congratzz!!!"
					EndIf
				EndIf
			EndIf
			
		Next
	
	EndMethod

EndType


#Map
DefData 1,2,1,2,1,2,1,2,1,2,1,2,1,2,1,2
DefData 2,9,0,0,1,0,0,0,0,0,0,0,0,0,0,1
DefData 1,0,0,0,0,0,4,3,5,6,5,6,0,0,0,2
DefData 2,0,1,3,4,2,2,0,0,0,0,2,3,0,0,1
DefData 1,2,1,0,0,0,2,1,4,0,0,1,2,2,0,2
DefData 2,0,0,0,4,0,0,0,7,0,0,0,1,0,0,1
DefData 1,0,8,4,3,0,0,0,0,0,0,0,1,2,0,2
DefData 2,0,7,0,0,0,0,0,0,4,5,4,1,0,0,1
DefData 1,0,6,0,0,1,0,0,0,1,0,0,2,2,0,2
DefData 2,0,5,1,2,1,3,0,1,1,0,0,0,0,0,1
DefData 1,0,4,0,0,0,0,0,2,4,2,4,2,2,0,2
DefData 2,0,3,1,0,0,0,0,0,0,3,0,0,1,0,1
DefData 1,0,2,1,1,0,0,0,0,0,0,0,0,1,0,2
DefData 2,0,1,2,3,4,5,4,5,4,5,5,6,3,0,1
DefData 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,2
DefData 2,1,2,1,2,1,2,1,2,1,2,1,2,1,2,1

#Colors
DefData 127,127,255
DefData 64,64,255
DefData 255,127,255
DefData 255,0,0
DefData 0,255,0
DefData 0,0,255
DefData 255,0,255
DefData 0,255,255
DefData 255,255,0
DefData 255,255,255

</textarea><br><br>PS: JFK EO-11110, you're a legend. Thanks for the code. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jfk EO-11110</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> Thank you :) <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
