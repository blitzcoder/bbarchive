<!DOCTYPE html><html lang="en" ><head ><title >Guided missile trail</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content=':), language=bmx, category=Graphics'><meta name='author' content='Jesse'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/Guided missile trail</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2045.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guided missile trail by Jesse</td><td align="right">2007 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> I have been tring to create a better guided missile logic but do to my lack of physics knowledge  I have hit a wall. what I am pleased with, is the trail it creates. it's a smooth trail that fades out. Any way I hope somebody finds use for it.  If somebody wants to improve the guiding logic I would  much apreciate if it is posted here. </td></tr><tr ><td class="cell"><pre class="code">SuperStrict
Framework BRL.GLMax2D
Import BRL.Random



Const Acceleration# 	= 00.5
Const TopSpeed# 		= 05.0
Const TurnAcceleration#	= 01.0
Const TurnMax# 		= 10.0
Const MaxPoints#		= 50.0
Const MissileSpeed%	= 2
Local Missiles% = 20
Local Missile:TMisile[Missiles] ' create missiles
Local streak:tstreak[Missiles]  ' create streak for missiles


Graphics 800,600,32,60
Global width% = GraphicsWidth()
Global height% = GraphicsHeight()

SetBlend alphablend
glEnable(GL_LINE_SMOOTH)	'Quick antaliasing hack
glHint(GL_LINE_SMOOTH_HINT,GL_NICEST)
glLineWidth(3.0)

' create missiles and streaks.

For Local i% = 0 To Missiles-1
	Missile[i] = TMisile.create(Rand(700)+50,Rand(500)+50,Rand(360))
	streak[i] =tstreak.create(Missile[i].x,Missile[i].y,Rand(255),Rand(255),Rand(255))
Next

SeedRnd MilliSecs()

Repeat
	SetColor 20,200,40
	DrawText "Press (esc) to exit",300,30
	For Local i% = 0 To Missiles-1
		For Local s% = 1 To MissileSpeed
			Missile[i].update(MouseX(),MouseY())
			streak[i].add(Missile[i].x,Missile[i].y)
		Next 
		streak[i].draw()
	Next
	Flip()
	Cls 
Until KeyHit(key_escape)


'
'				Resources
'-----------------------------------------------------------------------------------
'

Type Tpoint
	Field x#
	Field y#
End Type

' used to draw a streak. 
'Note:  does become jugged If points are separated To far And
'		angles are set sharp.

Type Tstreak
	Field PointList:TList 
	Field lastpoint:tpoint
	Field active%
	Field Red%,Green%,Blue%
	Field Tint#
	Function create:tstreak(x#,y#,Red%,Green%,Blue%)
		Local s:tstreak = New tstreak
		Local p:tpoint  = New tpoint
		If Not s.PointList Then 
			s.PointList      = CreateList()
			s.lastpoint = New tpoint
			s.active    = True
		EndIf
		p.x = x
		p.y = y
		s.PointList.addlast(p)
		s.lastpoint.x = p.x
		s.lastpoint.y = p.y
		s.Red    = Red
		s.Green  = Green
		s.Blue   = Blue
		s.tint 	= 1.0 /MaxPoints
		Return s
	End Function
	' adds a segment to a streak.
	
	Method add(x#,y#)
		If PointList.count()&lt;Int(MaxPoints) 
			Local p:tpoint = New tpoint
			p.x = x
			p.y = y
			PointList.addlast(p)
			lastpoint.x = p.x
			lastpoint.y = p.y 
		Else
			PointList.remove(PointList.first())
		EndIf
	End Method
	
	'
	
	Method draw()
		
		If Not PointList.count() Return 	
		If PointList.count() &gt; 1 Then 
			Local Alpha# = 0.0
			Local p1:Tpoint = Tpoint(PointList.first())
			SetColor Red,Green,Blue
			For Local p2:tpoint = EachIn PointList
				If p2&lt;&gt;p1 Then 
					SetAlpha alpha
					DrawLine(p1.x,p1.y,p2.x,p2.y,False)
					p1 = p2	
				EndIf
				alpha :+Tint
			Next		
		EndIf 
	End Method
End Type


Type TMisile 	
	Field Red%
	Field Green%
	Field Blue%
	Field x#,y#
	Field DirectionX#,DirectionY#
	Field XS#,YS#
	Field Done:Int
	Field Degree#
	Field Direction#
	Field TurnSpeed#
	Function create:TMisile(x#,y#,dir#)
	
		Local s:TMisile = New TMisile
		s.x = x
		s.y = y
		s.Direction = Dir 
		s.Done = False
		Return s
		
	End Function
	
	'Used To Draw Missile (currently unused)
	
	Method draw() ' used to draw missile
	' use from field in type
	' x, y -- is the current missile position also used for head of streak
	'direction -- is the angle the missile is facing in degrees. 
	End Method
	
	' Target chassing logic
	
	Method Update%(nx%,ny%)
		
		'Set acceleration
	
		XS:+ Cos(Direction)*Acceleration
		YS:+ Sin(Direction)*Acceleration
		
		Local CurrentSpeed# = Sqr(XS*XS + YS*YS)
		
		If CurrentSpeed &gt; TopSpeed 
			XS:+ (XS/CurrentSpeed)*(TopSpeed - CurrentSpeed)
			YS:+ (YS/CurrentSpeed)*(TopSpeed - CurrentSpeed)
		EndIf
		
		X:+ XS 
		Y:+ YS 
				
		'Set Rotation
										
		Local distance# = Sqr((x-nx)^2+(y-ny)^2) '
		Local TargetAngle# = (ATan2(y-ny,x-nx)+180.0) Mod 360.0		
		Local difference# = Abs(TargetAngle-Direction)

		'turn toward target

		If TargetAngle &lt; Direction				
			If difference &gt; 180.0 TurnSpeed:+TurnAcceleration Else TurnSpeed:-TurnAcceleration
		ElseIf TargetAngle &gt; Direction
			If difference &gt; 180.0 TurnSpeed:-TurnAcceleration Else TurnSpeed:+TurnAcceleration
		EndIf
		
		'If found stop turning
		
		If difference &lt; 1.0 TurnSpeed = 0.0			
				
		'Limit TurnSpeed

		If TurnSpeed &gt;  TurnMax TurnSpeed =  TurnMax
		If TurnSpeed &lt; -TurnMax TurnSpeed = -TurnMax
		Direction = (Direction+TurnSpeed+360) Mod 360
		
	EndMethod
End Type</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >PGF</td><td align="right"><font class=tiny>2007</font></td></tr></table></td></tr><tr ><td class="posttext"> Neat.  More like constant speed point following.<br><br>I have done missile turn and thrust to intersect a moving target with realistic physics including gravity of planets and stars.<br><br>The missile control wants to reach the target object at a given relative speed so may actually be boosting at an angle or slowing down in order to achieve this goal.<br><br>If I get some free time I'll create a demo.<br><br>Anyway your trails are nice. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>2007</font></td></tr></table></td></tr><tr ><td class="posttext"> &gt; More like constant speed point following.<br>yea you are right. But that can be fixed easily. <br><br>I am looking forward to seeing that demo.<br><br>Thank you for your comments.<br><br>P.S:  I purposely left out a test. that is why it gives a nonrepetitive circular turn. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
