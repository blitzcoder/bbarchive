<!DOCTYPE html><html lang="en" ><head ><title >Correct Filled Polygon</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='A quick and dirty way of building a filled polygon where Blitzmax's own FillPoly() does not work effectively., language=bmx, category=Graphics'><meta name='author' content='dw817'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/Correct Filled Polygon</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="3236.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Correct Filled Polygon by dw817</td><td align="right">2016 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> <img src="http://www.writerscafe.org/uploads/rte/c814d2a4685b147d13a34e7456124d1a.png"><br><br>A quick and dirty way of building a filled polygon where FillPoly() provided by BlitzMAX makes a mess of it for concave angles.<br><br>Does not require any additional libs, dlls, or mods.<br><br><b>Any questions or comments, especially about the BlitzMAX commands or functions used in this code, please address them below.</b> </td></tr><tr ><td class="cell"><pre class="code">'    ___________________________________________
'   //                                        //
'  //    "Perfect" Filled Polygon            //
' //    Written by David W (dw817) 01/12/16 //
'//________________________________________//

' What's up ?? Added boundary check, thanks to Bobysait

Strict
SetGraphicsDriver(GLMax2DDriver()) ' no problems w intensive pixel reading
Graphics 640,480
SetBlend alphablend ' allow for transparency

Global vec$="3U^jV\lf]7RGN3P" ' all vector images are in 8x8 grid
Global typ

typ=0
' 0 (zero) to use my polygon plotter
' 1 (one) to use BlitzMAX's own flawed plotter

If typ=0 Then drawvec vec$
If typ=1 Then drawvec2 vec$
Flip
WaitKey

Function drawvec(t$)
Local x,y,x2,y2,p,a,i,j,k,m,z=Len(t$),ok,pic:TPixmap
  For p=1 To z
    a=Asc(Mid$(t$,p))-40 ' pull out a vector
    x=a Mod 9*16 ' retrieve horizontal
    y=a/9*16 ' retrieve vertical
    If p&gt;1 And p&lt;z ' if not first iteration nor last, draw vector
      DrawLine x2,y2,x,y
    ElseIf p=z ' last iteration, let's do our fill
      pic=GrabPixmap(0,0,128,128) ' grab image for massive pixel updates
      WritePixel pic,x,y+8,$fffffffe ' +8 as paint vector falls on existing edge
' needs to be $fe to differentiate between edges that are $ff
      Repeat
        ok=1 ' flag that no changes have been made
        For i=0 To 127 ' field of 8x8 grid, snap on 16
          For j=0 To 127
            If j+m&gt;=0 And i+k&gt;=0 ' confirm within range of screen
              If ReadPixel(pic,j,i)=$fffffffe ' found our paint pixel
                For k=-1 To 1
                  For m=-1 To 1
                    If j+m&gt;=0 And i+k&gt;=0 And j+m&lt;=127 And i+k&lt;=127 ' confirm within ange
                      If ReadPixel(pic,j+m,i+k)=$ff000000 And(k=0 Or m=0) ' ensure search is cross and not square
                        WritePixel pic,j+m,i+k,$fffffffe ' it's empty so fill it in
                        ok=0 ' flag for cannot exit just yet
                      EndIf
                    EndIf
                  Next
                Next
              EndIf
            EndIf
          Next
        Next
      Until ok ' no changes above means we're finally done
      DrawPixmap pic,0,0 ' draw back the changes
    EndIf
    x2=x
    y2=y
  Next
EndFunction

Function drawvec2(t$) ' System's own polygon plotter
Local vec#[Len(t$)*2],a,i
  For i=1 To Len(t$)
    a=Asc(Mid$(t$,i))-40
    vec#[i*2-2]=a Mod 9*16+20 ' store X
    vec#[i*2-1]=a/9*16+20 ' store Y
  Next
  DrawPoly vec# ' use system's own plotter, flawed
EndFunction</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SimonasQu</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> Why pix-mapping it? I've the code to draw filled textured polygons with native openGL/DirectX functions. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bobysait</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> I won't comment on the "writepixel" method, it can be usefull for writing concave polygons to textures.<br><br>I just will mention this :<br><pre class=code>
for i=1 To Len(t$)
    a=Asc(Mid$(t$,i))-40
    [...]
</pre><br>you'll get a more user-friendly syntax (and improved performances) by using the byte buffer attached to the string.<br><br><pre class=code>
For i = 0 Until t.length
   a = t[i] - 40;
Next
</pre><br>This will give the same result.<br><br>ps :<br>BTW, your code won't compile on debug because you try to write pixel outside of the pixmap range.<br>For j = 0 to 127<br>For m = -1 to 1<br>ReadPixel(pic,j+m,i+k)<br>-&gt; your "j+m" so as the "i+k" coordinates are in the range [-1, 128] and it will crash on debug mode. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bobysait</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> This is the ear-clipping method for filling a concave polygon<br>It should computes polygons fast enough to be used in realtime.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Function extractPolys:Float[][](t:String)
	
	' generate the points list
	Local pt:Float[][] = New Float[][t.length-1];
	For Local p:Int = 0 Until t.length-1
		pt[p] = [VecX(t[p]-40), VecY(t[p]-40)];
	Next;
	
	' initialize polygon list
	Local polys:Float[][] = New Float[][0];
	
	' current vertex to test.
	Local v:Int = 0;
	
	While pt.length&gt;3
		
		Local ptl:Int = pt.length;
		Local v0:Int = v Mod(ptl), v1:Int = (v+1) Mod (ptl), v2:Int = (v1+1) Mod (ptl);
		
		' coordinates of the triangle to test
		Local x0:Float = pt[v0][0], y0:Float = pt[v0][1];
		Local x1:Float = pt[v1][0], y1:Float = pt[v1][1];
		Local x2:Float = pt[v2][0], y2:Float = pt[v2][1];
		
		' cross product -&gt; Z Front (Front =-1)
		If (x2-x0)*(y2-y1)-(y2-y0)*(x2-x1)&gt;=0 ' if triangle backward -&gt; skip point
			v = v1;
		Else
			' test no point are in the triangle v0,v1,v2
			Local t:Int=False;
			For Local p__:Int = 0 Until pt.length
				' don't test vertices of the triangle ^^
				If p__&lt;&gt;v0 And p__&lt;&gt;v1 And p__&lt;&gt;v2
					' point in the triangle -&gt; skip this triangle.
					If InTriangle( pt[p__][0], pt[p__][1], x0,y0, x1,y1, x2,y2 ) Then t = True; v = v1; Exit;
				EndIf;
			Next;
			
			If t=False ' no point the triangle
				' output new triangle
				polys = polys+[[pt[v0][0],pt[v0][1],..
							 	pt[v1][0],pt[v1][1],..
							 	pt[v2][0],pt[v2][1]]];
				
				' remove v1 from points list
				If v1=0 ' first point -&gt; points = points after v1
					pt = pt[v1+1..];
				ElseIf v1=pt.length-1 ' last point -&gt; points = points before v1
					pt = pt[..v1];
				Else ' points = start of points until v1 + points after v1
					pt = pt[..v1]+pt[v1+1..];
				EndIf;
			EndIf;
			
		EndIf;
		
	Wend;
	
	Return polys;
	
End Function

Function InTriangle:Byte ( x0:Float,y0:Float, x1:Float,y1:Float, x2:Float,y2:Float, x3:Float,y3:Float )
	Local b0:Float =  1.0 / (x2 - x1) * (y3 - y1) - (x3 - x1) * (y2 - y1);
	If ( ( ((x2 - x0) * (y3 - y0) - (x3 - x0) * (y2 - y0)) * b0 ) &lt;= 0) Then Return False;
	If ( ( ((x3 - x0) * (y1 - y0) - (x1 - x0) * (y3 - y0)) * b0 ) &lt;= 0) Then Return False;
	Return ( ( ((x1 - x0) * (y2 - y0) - (x2 - x0) * (y1 - y0)) * b0 ) &gt; 0)
End Function


' small sdk to convert string data to coordinates
Function VecX:Float(a:Int)
	Return a Mod(9) * 16
End Function
Function VecY:Float(a:Int)
	Return a/9 * 16
End Function


SetGraphicsDriver(GLMax2DDriver()) ' no problems w intensive pixel reading
Graphics (640,480, 0, 0)

Local vec:String="3U^jV\lf]7RGN3P";
vec = vec[..vec.length-1]; ' remove last vertex -&gt; it overrides the first vertex.

' generates a list of polygons ( each polygon is in fact a triangle -&gt; float[x0,y0,x1,y1,x2,y2] )
Local polys:Float[][] = extractPolys(vec);

Cls
SetColor 150,150,150

For Local p:Float[] = EachIn polys
	DrawPoly(p);
Next

Flip True
WaitKey()
End
</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> Ok ... I am finally understanding what is going on here. You are taking my polygon image and 'shaping' it so the flawed DrawPoly() can finally draw it the correct way. You are essentially adding crutches, and - if that's what is needed.<br><br>Quite a bit of work - nice job of that ! :)<br><br>As for the coordinates, yes - I need to add a check boundaries.<br><br><b>Corrected - Code Updated</b> <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
