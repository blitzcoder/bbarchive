<!DOCTYPE html><html lang="en" ><head ><title >Edit palette in png images</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='How to change colors in a paletted png image without loading image in memory, language=bb, category=Graphics'><meta name='author' content='Massimo32'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/Edit palette in png images</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1326.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Edit palette in png images by Massimo32</td><td align="right">2005 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> If you use png paletted images and you need to change the colors you can do that without loading the image in memory and the change each pixel.<br><br>In paletted png images there's a palette chunk, ending with a crc. <br>You can change the colors inside the palette provided that you update the crc.<br><br>This sample code works this way:<br><br>1) load the file .png<br>2) load the new palette (JASC-PAL format)<br>3) find the palette chunk in the .png<br>3) change the palette colors<br>4) find the new crc of the chunk &amp; overwrite the old one<br>5) save the new file<br><br>Very fast!<br><br>Thanks to MrCredo for crc code.<br><br>For png specification, see: <br><a href="http://www.libpng.org/pub/png/spec/iso/" target="_blank">http://www.libpng.org/pub/png/spec/iso/</a><br><br>Ciao. </td></tr><tr ><td class="cell"><pre class="code">Dim crc_table(255)
crc_init()

image$ = Input("Image (.png): ")
If Lower(Right(image,4)) &lt;&gt; ".png" Then image = image + ".png"

palette$ = Input("Palette (.pal): ")
If Lower(Right(palette,4)) &lt;&gt; ".pal" Then palette = palette + ".pal"

file_image = OpenFile(image)
file_palette = OpenFile(palette)

If file_image = 0 Then RuntimeError(image + " not found!")
If file_palette = 0 Then RuntimeError(palette + " not found!")


;--- READ PALETTE INSIDE THE IMAGE ---

;search for the palette 'chunk'

i0 = ReadByte(file_image)
i1 = ReadByte(file_image)
i2 = ReadByte(file_image)
i3 = ReadByte(file_image)

palette_found=False

While Not Eof 

	If Chr(i0)+Chr(i1)+Chr(i2)+Chr(i3) = "PLTE"
		palette_found = True
		Exit
	EndIf

	i0 = i1
	i1 = i2
	i2 = i3
	i3 = ReadByte(file_image)

Wend

If Not palette_found
	RuntimeError("Palette not found inside image!")
EndIf

palette_offset = FilePos(file_image) - 8

;read data size
SeekFile(file_image, palette_offset)
data_size = 0
For i = 0 To 3
	data_size = 256*data_size + ReadByte(file_image) 
Next

;create and fill buffer
buf = CreateBank(4 + data_size)
For i = 0 To BankSize(buf)-1
	PokeInt(buf, i, ReadByte(file_image))
Next


;--- READ NEW PALETTE AND CHANGE BUFFER ---


;check palette heading
row$ = ReadLine(file_palette)
If Upper(row) &lt;&gt; "JASC-PAL" Then RuntimeError("Palette type not recognized (should be JASC-PAL): " + row)

;check color depth
row$ = ReadLine(file_palette)
If row &lt;&gt; "0100" Then RuntimeError("New palette should be 8 bit/channel!")

;check size of palettes
row$ = ReadLine(file_palette)
palette_size = Int(row)
If 3*palette_size &lt;&gt; data_size Then RuntimeError("Size of new palette: " + 6*Int(row) + " Size of image palette: " + data_size)

For i = 0 To palette_size-1
	
	row$ = ReadLine(file_palette)

	;red
	green_offset = Instr(row, " ")
	red$ = (Mid(row,1,green_offset-1))
	
	;green
	blue_offset = Instr(row, " ", green_offset+1)
	green$ = (Mid(row,green_offset+1,blue_offset-green_offset-1))
	
	;blue
	blue$ = (Mid(row, blue_offset+1, Len(row)-blue_offset))

	PokeByte(buf, 4 +3*i, Int(red))
	PokeByte(buf, 4 +3*i +1, Int(green))
	PokeByte(buf, 4 +3*i +2, Int(blue))

Next


;calculate crc		
crc32% = crc_bank(buf)

Print (" ")
Print ("New crc: " + Hex(crc32))

;write new file
file_image_out = WriteFile("tmp_"+image)

SeekFile(file_image, 0)

While Not Eof(file_image)

	pos% = FilePos(file_image)
	If pos &lt; palette_offset + 4 
		WriteByte(file_image_out, ReadByte(file_image))
	Else If pos &lt; (palette_offset +4 +4 +data_size)
		WriteByte(file_image_out, PeekByte(buf, pos -(palette_offset +4)))
		SeekFile(file_image, FilePos(file_image)+1)
	Else If pos &lt; palette_offset +4 +4 +data_size +4
		shift = palette_offset +4 +4 +data_size +4 -pos -1
;		Print Hex(crc32 Shr 8*shift)
		WriteByte(file_image_out, (crc32 Shr 8*shift) And $FF)
		SeekFile(file_image, FilePos(file_image)+1)
	Else
		WriteByte(file_image_out, ReadByte(file_image))
	EndIf

Wend

CloseFile(file_image_out)
CloseFile(file_image)	

Print (" ")
Print("New image saved as: tmp_"+image)	




;_____________________________________


Function crc_init()
  Local i
  Local j
  Local value

  For i=0 To 255
    value=i
    For j=0 To 7
      If (value And $1) Then 
        value=(value Shr 1) Xor $EDB88320
      Else
        value=(value Shr 1)
      EndIf
    Next
    crc_table(i)=value
  Next
End Function



Function crc_bank(bank)
  Local byte
  Local crc
  Local i
  Local size

  crc=$FFFFFFFF
  size=BankSize(bank)-1
  For i=0 To size
    byte=PeekByte(bank,i)
    crc=(crc Shr 8) Xor crc_table(byte Xor (crc And $FF))
  Next
  Return ~crc
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
