<!DOCTYPE html><html lang="en" ><head ><title >Texture Fill</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='At last, a texture fill routine!, language=bb, category=Graphics'><meta name='author' content='EOF'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/Texture Fill</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="151.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Texture Fill by EOF</td><td align="right">2001 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Function - TextureFill(x,y,texture)<br><br>Takes an image and texture fills an area on the front buffer.  The fill routine uses whatever color it finds at x,y to fill with. </td></tr><tr ><td class="cell"><pre class="code">; Texture Fill routine by Jim Brown
; ##########################################
; Modified Steven Smiths Flood Fill code.
; Adapted from code by Petter Holmberg.
; Get the original from www.basicguru.com/abc/graphics.htm


; 	USAGE: TextureFill(X,Y,TEXTURE)

; fills an area of same colour (on the front buffer) with a texture
;
; X/Y      - coordinates to fill from
; TEXTURE  - supplied image texture


Const sw=640, sh=480	; screen width/height
Graphics sw,sh,32

texture=LoadImage(&quot;brick.bmp&quot;)  ; &lt;---- Supply your own here!!
If texture=0 ; make one if no texture found
	texture=CreateImage(64,64)
	SetBuffer ImageBuffer(texture)
	ClsColor 0,20,100 : Cls : Color 60,140,0
	Oval 32-28,32-28,56,56,1 : Rect 1,1,62,62,0
EndIf

; include this part in your code
; ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
; ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Const fillstack=2350 ; increase this if you get holes in complex fills
; type to hold pixel coords
Type point
	Field x,y
End Type
;Create a stack of point types
Dim pixdata.point(fillstack)
For fstack=0 To fillstack : pixdata(fstack)=New point : Next
; ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
; ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

SeedRnd MilliSecs()
SetBuffer FrontBuffer() : ClsColor 0,0,0

While Not KeyHit(1)
	Cls : Color Rand(10,200),Rand(10,200),Rand(10,200)
	For d=1 To Rand(50,80)
		xp=Rand(100,sw-100) : yp=Rand(100,sh-100)
		radx=Rand(20,180) : rady=Rand(20,150)
		Oval xp-radx/2,yp-rady/2,radx,rady,True
	Next
	Oval sw/2-25,sh/2-25,50,50
	TextureFill(sw/2,sh/2,texture)
	Delay 400
Wend
End


; ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
; Texture Fill routine
; ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Function TextureFill(x,y,tex)
	If tex=0 Then Return 0 			; exit if no texture
	Local fillwidth=GraphicsWidth(), fillheight=GraphicsHeight()
	Local twidth=ImageWidth(tex), theight=ImageHeight(tex)
	LockBuffer FrontBuffer() : LockBuffer ImageBuffer(tex)
	;clear the coord stack
	For fstack=0 To fillstack : pixdata(fstack)\x=0 : pixdata(fstack)\y=0 : Next
	bcol=ReadPixel(x,y) 			; get the background colour
	firstentry=0 : lastentry=1
	Repeat
		; fill to the right
		fx=pixdata(firstentry)\x : fy=pixdata(firstentry)\y
		Repeat
			fillcol=ReadPixelFast(x+fx,y+fy) : flag=True
			If fillcol = bcol And (x+fx&gt;=0 And x+fx&lt;fillwidth) And (y+fy&gt;=0 And y+fy&lt;fillheight)
				CopyPixelFast (x+fx) Mod twidth,(y+fy) Mod theight, ImageBuffer(tex), x+fx,y+fy , FrontBuffer()
				If ReadPixelFast(x+fx,y+fy-1) = bcol ; above
					pixdata(lastentry)\x=fx : pixdata(lastentry)\y=fy-1
					lastentry=lastentry+1 : If lastentry=fillstack+1 Then lastentry=0
				EndIf
				If ReadPixelFast(x+fx,y+fy+1) = bcol ; below
					pixdata(lastentry)\x=fx : pixdata(lastentry)\y=fy+1
					lastentry=lastentry+1 : If lastentry=fillstack+1 Then lastentry=0
				EndIf
			Else
				flag=False
			EndIf
			fx=fx+1
		Until flag=False
		fx=pixdata(firstentry)\x-1 : fy=pixdata(firstentry)\y
		; fill to the left
		Repeat
			fillcol=ReadPixelFast(x+fx,y+fy) : flag=True
			If fillcol = bcol And (x+fx&gt;=0 And x+fx&lt;fillwidth) And (y+fy&gt;=0 And y+fy&lt;fillheight)
				CopyPixelFast (x+fx) Mod twidth,(y+fy) Mod theight, ImageBuffer(tex), x+fx,y+fy , FrontBuffer()
				If ReadPixelFast(x+fx,y+fy-1) = bcol ; above
					pixdata(lastentry)\x=fx : pixdata(lastentry)\y=fy-1
					lastentry=lastentry+1 : If lastentry=fillstack+1 Then lastentry=0
				EndIf
				If ReadPixelFast(x+fx,y+fy+1) = bcol ; below
					pixdata(lastentry)\x=fx : pixdata(lastentry)\y=fy+1
					lastentry=lastentry+1 : If lastentry=fillstack+1 Then lastentry=0
				EndIf
			Else
				flag=False
			EndIf
			fx=fx-1
		Until flag=False
		firstentry=firstentry+1 : If firstentry=fillstack+1 Then firstentry=0
	Until firstentry=lastentry
	UnlockBuffer FrontBuffer() : UnlockBuffer ImageBuffer(tex)
	Return
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
