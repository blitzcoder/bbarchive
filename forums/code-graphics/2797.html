<!DOCTYPE html><html lang="en" ><head ><title >Text Shaper</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Displays text to conform to the shape on an image., language=bmx, category=Graphics'><meta name='author' content='zambani'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/Text Shaper</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2797.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Text Shaper by zambani</td><td align="right">2010 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> The textshaper takes an image and then a text.<br>It then then displays the text in such a a way that it is bound by the  totally transparent area of the image.<br><br>Text will flow based on the alpha cutout of the image </td></tr><tr ><td class="cell"><pre class="code">SuperStrict
Graphics(800, 600, 0, 30)


'SetBlend(ALPHABLEND)
Local imagePath:String = "shape1.png"	'path to shaper image. Text will conform to location where the alpha &gt; 0
Local txt:String						'Text to shape
txt = "We've got a few SIRIUS struggling to survive in one tank. Unfortunately for then, in the other tank is a thriving  colony of IRIDESA. We need to eradicate the IRIDESA so that the SIRIUS colony."
'txt = LoadString("Sample.txt")

Global st:objTextShaper = objTextShaper.createNew(txt, LoadImage(imagePath))

While Not KeyHit(KEY_ESCAPE) And Not AppTerminate()
	Cls()
	st.render()		'Draw textshaper
	Flip(-1)
Wend

'========================================= TEXT SHAPER ============================================

Type objTextShaper		'renders a text  base on the alpha shape of  an image
	Field _line:Int[]
	Field _text:String
	Field _font:TImageFont
	Field _lineHeight:Int
	Field _worldsArray:String[]
	Field _linesArray:String[]
	Field _startIndex:Int
	Field _lineIndex:Int = 1
	Field _yStart:Int
	Field _skip:Int
	Field xOffset:Int
	Field yOffset:Int
	
	
	Function createNew:objTextShaper(t:String, img:TImage, font:TImageFont = Null)
		'position of text is specified in the render method
		Local ts:objTextShaper = New objTextShaper
		ts._text = t
		ts._font = font
		Local tt:TImageFont
		If ts._font &lt;&gt; Null
			tt:TImageFont = GetImageFont() 'save current font
			SetImageFont(ts._font)
		End If
		ts._lineHeight = TextHeight(ts._text)
		ts._worldsArray = ts._text.Split(" ")
		ts._line = ts._getLinesLimit(img)
	
		ts.SetText(ts._text)
		If ts._font &lt;&gt; Null Then SetImageFont(tt)		'restore current font	
		Return ts
	End Function
	
	Method SetText(t:String)
		_text = t.Replace("~n", " ~n ")	'create space between lines
		Local ss:String
		_worldsArray = _text.Split(" ")
		While _startIndex &lt; _worldsArray.Length
			_linesArray = _linesArray[.._lineIndex]
			ss = _getFullLine()
			_linesArray[_lineIndex - 2] = ss
			
			'Skip additional lines if required
			_lineIndex:+_skip
			If _skip &gt; 0 Then _linesArray = _linesArray[.._lineIndex]
			
			If _lineIndex &gt;= (_line.Length / 2) Then Exit		'end of image
		Wend		
	End Method
	
	Method _getLinesLimit:Int[] (i:TImage)		'returns  an array of line limis based on image i
		Local pix:TPixmap = LockImage(i)
		Local x:Int, y:Int, ar:Int[], p1:Int = 1, p2:Int = 1
		Local st:Int = False
		
		For Local cy:Int = 0 Until pix.Height
			If st = 1		'Force shut any line that started but not completed
				ar = ar[..(ar.length + 1)]
				ar[ar.length - 1] = pix.width
				st = 0
			End If
			For Local cx:Int = 0 Until pix.width
				p1 = pix.ReadPixel(cx, cy) Shr 24
				If p1 &gt; 0 Then p1 = 1
				If p1 &lt;&gt; p2
					If st = 0
						ar = ar[..(ar.length + 1)]
						ar[ar.length - 1] = cx
						st = 1
						If _yStart = 0 Then _yStart = cy-_lineHeight
					Else
						ar = ar[..(ar.length + 1)]
						ar[ar.length - 1] = cx
						st = 0
						Exit
					End If
					p2 = p1
				End If
			Next
			p1 = 1
			p2 = 1
			cy:+(_lineHeight - 1)
		Next
		Return ar
	End Method
	
	Method _getFullLine:String()
		Local l:String
		Local l2:String
		Local a:Int
		Local cr:Int, cro:Int, ex:Int = False
		_skip = -1
		For a:Int = _startIndex Until _worldsArray.Length
			l:+_worldsArray[a] + " "
			If TextWidth(l) &gt; (_line[_lineIndex * 2 + 1] - _line[_lineIndex * 2]) Then Exit
			cr = _worldsArray[a].find("~n", cro)
			While cr &lt;&gt; - 1
				ex = True
				cro = cr + 1
				_skip:+1
				cr = _worldsArray[a].find("~n", cro)
			Wend			
			If ex
				a:+1
				Exit
			End If
			l2 = l
		Next
		_skip = Max(0, _skip)
		_startIndex = a
		_lineIndex:+1
		Return l2
		End
	End Method
	
	Method render(xo:Float = 0, yo:Float = 0)
		If _font &lt;&gt; Null Then SetImageFont(_font)
		For Local c:Int = 0 Until _linesArray.Length
			DrawText(_linesArray[c], xo + _line[c * 2]+xOffset, (_lineHeight * c) + yo+_yStart+yOffset)
		Next
	End Method
End Type</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
