<!DOCTYPE html><html lang="en" ><head ><title >Progress Bars</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Simple Rect Progress Bars, language=bb, category=Graphics'><meta name='author' content='xmlspy'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/Progress Bars</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1424.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Progress Bars by xmlspy</td><td align="right">2005 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Simple Progress bars using Rect command.<br>Three styles: Horizontal, Vertical, and Vertical Inverted </td></tr><tr ><td class="cell"><pre class="code">Graphics 640, 480, 0, 2
SetBuffer BackBuffer()

b1value# = 25
Bar1 = Create_Bar( 0, 0, GraphicsWidth(), 20 , b1value#)

b2value# = 50
Bar2 = Create_Bar( 0, 21, GraphicsHeight() - 21, 20, b2value#, 0)

b3value# = 75
Bar3 = Create_Bar( GraphicsWidth() - 21, 21, 20, GraphicsHeight() - 21, b3value#, 2)

Bar4 = Create_Bar( 21, GraphicsHeight() - 21, GraphicsWidth() - 43, 20 , b1value#)


timer = CreateTimer(60)
Repeat
	Cls
	
	If KeyDown(16) Then b1value# = b1value# - 1 ; Q
	If KeyDown(17) Then b1value# = b1value# + 1 ; W
	If KeyDown(30) Then b2value# = b2value# - 1 ; A
	If KeyDown(44) Then b2value# = b2value# + 1 ; Z
	If KeyDown(45) Then b3value# = b3value# - 1 ; S
	If KeyDown(31) Then b3value# = b3value# + 1 ; X
	
	
	If ob1value# &lt;&gt; b1value# Then
		b1value# = Update_BarValue( Bar1, b1value# )
		Update_BarValue( Bar4, b1value# )
		ob1value# = b1value#
	EndIf
	
	If ob2value# &lt;&gt; b2value# Then
		b2value# = Update_BarValue( Bar2, b2value# )
		ob2value# = b2value#
	EndIf
	
	If ob3value# &lt;&gt; b3value# Then
		b3value# = Update_BarValue( Bar3, b3value# )
		ob3value# = b3value#
	EndIf
	
	WaitTimer( timer )
	Render_Bar( )
	
	Color 255, 255, 255
	Text 100, 100, "Press QW for horizontal  | Value= " + b1value#
	Text 100, 120, "Press AZ for vertical 1  | Value= " + b2value#
	Text 100, 140, "Press SX for vertical 2  | Value= " + b3value#
	
	Flip(0)

Until KeyHit(1)
End


Type Bar
	Field x#, y#, width#, height#
	Field value#, horizontal
	Field Red, Green, Blue
	Field bRed, bGreen, bBlue
	Field bStyle
End Type

Function Create_Bar( x#, y#, width# = 100, height# = 20, value# = 100, horizontal = 1, Red = 0, Green = 255, Blue = 0, bRed = 255, bGreen = 255, bBlue = 255, bStyle = 0 )
	Bar.Bar = New Bar
	Bar\x# = x#
	Bar\y# = y#
	Bar\width# = width#
	Bar\height# = height#
		If value# &lt;= 0 Then
			value# = 0
		ElseIf value# &gt;= 100 Then
			value# = 100
		EndIf
	Bar\value# = value# : Bar\horizontal = horizontal
	Bar\Red = Red : Bar\Green = Green : Bar\Blue = Blue
	Bar\bRed = bRed : Bar\bGreen = bGreen : Bar\bBlue = bBlue : 
	Bar\bStyle = bStyle
	Return Handle( Bar )
End Function


Function Delete_Bar( ID )
	Bar.Bar = Object.Bar( ID )
	If Bar.Bar &lt;&gt; Null Then
		Delete Bar
	EndIf
End Function

Function Render_Bar( )
	For Bar.Bar = Each Bar
		If Bar\horizontal = 0
			Color Bar\bRed, Bar\bGreen, Bar\bBlue
			Rect Bar\x#, Bar\y#, Bar\height#, Bar\width#, Bar\bStyle
			Color Bar\Red, Bar\Green, Bar\Blue
			Rect Bar\x#+1, Bar\y#+1, Bar\height# - 2, ((Bar\value# * .01) * Bar\width#) - 2
		ElseIf Bar\horizontal = 1
			Color Bar\bRed, Bar\bGreen, Bar\bBlue
			Rect Bar\x#, Bar\y#, Bar\width#, Bar\height#, Bar\bStyle
			Color Bar\Red, Bar\Green, Bar\Blue
			Rect Bar\x#+1, Bar\y#+1, ((Bar\value# * .01) * Bar\width#) - 2, Bar\height# - 2
		ElseIf Bar\horizontal = 2
			Color Bar\bRed, Bar\bGreen, Bar\bBlue
			Rect Bar\x#, Bar\y#, Bar\width#, Bar\height#, Bar\bStyle
			Color Bar\Red, Bar\Green, Bar\Blue
			tTotal1# = Bar\height# + Bar\y# + 1
			val1# = tTotal1 - ((Bar\value# * .01) * Bar\height#)
			val2# = (tTotal1 - val1#) - 2		
			Rect Bar\x#+1, val1#, Bar\width# - 2, val2#
		EndIf
	Next
End Function

Function Update_BarValue( ID, value# )
	Bar.Bar = Object.Bar( ID )
	If Bar.Bar &lt;&gt; Null Then
		If value# &lt;= 0.0 Then value# = 0.0
		If value# &gt;= 100.0 Then value# = 100.0
		Bar\value# = value#
		Return value#
	EndIf
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
