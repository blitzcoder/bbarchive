<!DOCTYPE html><html lang="en" ><head ><title >Draw Filled Circle Row by Row with Integer Math</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Draws a filled circle one row at a time using integer math, language=bmx, category=Graphics'><meta name='author' content='ImaginaryHuman'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/Draw Filled Circle Row by Row with Integer Math</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1478.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Draw Filled Circle Row by Row with Integer Math by ImaginaryHuman</td><td align="right">2005 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This draws a filled circle row by row and does not use any floating point math or trigonometry - it is entirely integer based. The rows are filled using calls to DrawRect and each is drawn separately. This example actually draws a filled circle and a hollow circle at the same time. You can comment out the Plot commands to see only the filled circle, or comment out the DrawRect commands to see only the hollow circle. By both drawing at once in the same loop, they somewhat overwrite each other. </td></tr><tr ><td class="cell"><pre class="code">'Midpoint Circle algorithm

Strict
Graphics 640,480,0

Local xCenter:Int=320
Local yCenter:Int=240
Local radius:Int
Local p,x,y,prevy:Int
Repeat
	Cls
	If MouseDown(1)
		xCenter=MouseX()
		yCenter=MouseY()
	EndIf
	radius=Abs(xCenter-MouseX())
	x=0
	y=radius
	SetColor $FF,$88,$00
	DrawRect xCenter-y,yCenter+x,y Shl 1,1
	SetColor $FF,$FF,$FF
	Plot xCenter+x,yCenter+y
	Plot xCenter-x,yCenter+y
	Plot xCenter+x,yCenter-y
	Plot xCenter-x,yCenter-y
	Plot xCenter+y,yCenter+x
	Plot xCenter-y,yCenter+x
	Plot xCenter+y,yCenter-x
	Plot xCenter-y,yCenter-x
	p=1-radius
	While x&lt;y-1
		prevy=y
		If p&lt;0
			x:+1
		Else
			x:+1
			y:-1
		EndIf
		If p&lt;0
			p=p+(x Shl 1)+1
		Else
			p=p+((x-y) Shl 1)+1
		EndIf
		If y&lt;prevy And x&lt;y
			SetColor $FF,$88,$00
			DrawRect xCenter-x,yCenter+y,x Shl 1,1
			DrawRect xCenter-x,yCenter-y,x Shl 1,1
			SetColor $FF,$FF,$FF
			Plot xCenter+x,yCenter+y
			Plot xCenter-x,yCenter+y
			Plot xCenter+x,yCenter-y
			Plot xCenter-x,yCenter-y
		EndIf
		SetColor $FF,$88,$00
		DrawRect xCenter-y,yCenter+x,y Shl 1,1
		DrawRect xCenter-y,yCenter-x,y Shl 1,1
		SetColor $FF,$FF,$FF
		Plot xCenter+y,yCenter+x
		Plot xCenter-y,yCenter+x
		Plot xCenter+y,yCenter-x
		Plot xCenter-y,yCenter-x
	Wend
	Flip
Until KeyHit(KEY_ESCAPE)
End</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>2005</font></td></tr></table></td></tr><tr ><td class="posttext"> The previous code was inefficient in that it drew some of the rows more than once. I've now edited and uploaded modified code which makes sure to only draw each row once. It therefore works better with modes like LIGHTBLEND and is faster.<br><br>This is meant to be in the BlitzMax code archives. You also don't have to use Plot or DrawRect - those are just a way of outputting the circle `data` to show that it works. You could write to memory pointers, to an array, whatever. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rck</td><td align="right"><font class=tiny>2005</font></td></tr></table></td></tr><tr ><td class="posttext"> <textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Midpoint Circle algorithm

Graphics 640,480,0,2

Local xCenter% = 320
Local yCenter% = 240
Local radius%
Local p%, x%, y%, prevy%

Repeat
	WaitEvent()
	Cls
	If MouseDown(1)
		xCenter = MouseX()
		yCenter = MouseY()
	EndIf
	
	radius = Abs(xCenter - MouseX())
	x = 0
	y = radius
	
	msStart = MilliSecs()
	
	Color $FF,$88,$00
	Rect xCenter - y, yCenter + x, y Shl 1,1
	Color $FF,$FF,$FF
	
	Plot xCenter + x, yCenter + y
	Plot xCenter - x, yCenter + y
	Plot xCenter + x, yCenter - y
	Plot xCenter - x, yCenter - y
	Plot xCenter + y, yCenter + x
	Plot xCenter - y, yCenter + x
	Plot xCenter + y, yCenter - x
	Plot xCenter - y, yCenter - x
	
	p = 1 - radius
	
	While x &lt; y - 1
	
		prevy = y
		
		If p &lt; 0
			x = x + 1
		Else
			x = x + 1
			y = y - 1
		EndIf
		
		If p &lt; 0
			p = p + (x Shl 1) + 1
		Else
			p = p + ((x - y) Shl 1) + 1
		EndIf
		
		If y &lt; prevy And x &lt; y Then
			Color $FF,$88,$00
			Rect xCenter - x, yCenter + y, x Shl 1,1
			Rect xCenter - x, yCenter - y, x Shl 1,1
			Color $FF,$FF,$FF
			Plot xCenter + x, yCenter + y
			Plot xCenter - x, yCenter + y
			Plot xCenter + x, yCenter - y
			Plot xCenter - x, yCenter - y
		EndIf
		
		Color $FF,$88,$00
		Rect xCenter - y, yCenter + x, y Shl 1, 1
		Rect xCenter - y, yCenter - x, y Shl 1, 1
		Color $FF,$FF,$FF 
		
		Plot xCenter + y, yCenter + x
		Plot xCenter - y, yCenter + x
		Plot xCenter + y, yCenter - x
		Plot xCenter - y, yCenter - x
	Wend
	
	drawTime = MilliSecs() - msStart
	Text 20, 20, "Radius " + radius
	Text 20, 30, "Draw time " + drawTime + " ms"
	
	Flip(False)
Until KeyHit(1)
</textarea><br><br>Port to BlitzPlus <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>2005</font></td></tr></table></td></tr><tr ><td class="posttext"> Cool! <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
