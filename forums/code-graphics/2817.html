<!DOCTYPE html><html lang="en" ><head ><title >Bresenham like Circle and Ellipse functions</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Bresenham: Circle &amp;amp; Ellipse, language=bb, category=Graphics'><meta name='author' content='Andy_A'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/Bresenham like Circle and Ellipse functions</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2817.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bresenham like Circle and Ellipse functions by Andy_A</td><td align="right">2011 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> After looking at 'furbrain's Circle plotter and MCP asking the question: "How difficult would it be to modify the Circle code above to draw elipses quickly?"<br>( <a href="http://www.blitzmax.com/codearcs/codearcs.php?code=2809" target="_blank">http://www.blitzmax.com/codearcs/codearcs.php?code=2809</a> )<br><br>During my research into these routines I found that adding scan line fill code was just a couple of lines, so both routines have the option of drawing regular and filled circles/ellipses.<br><br>One other thing of note is that the Blitz filled oval is faster than an unfilled oval (approx. 33% faster!)<br><br>P.S. There are other Bresenham like circle algos here in the code archives<br><a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=428" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=428</a><br><a href="http://www.blitzmax.com/codearcs/codearcs.php?code=1451" target="_blank">http://www.blitzmax.com/codearcs/codearcs.php?code=1451</a><br>But this is the first Bresenham like ellipse plotter<br><br>Here are the results:<br><br>Circle<br><pre class=code>;Source:
;	<a href="http://homepage.smc.edu/kennedy_john/papers.htm" target="_blank">http://homepage.smc.edu/kennedy_john/papers.htm</a>
;	<a href="http://homepage.smc.edu/kennedy_john/bcircle.pdf" target="_blank">http://homepage.smc.edu/kennedy_john/bcircle.pdf</a>

;	A Fast Bresenham Type Algorithm For Drawing Circles
;	by 
;	John Kennedy

;	Blitzplus/Blitz 3D port by Andy_A


AppTitle "Bresenham circle"
Graphics 800,600,32,2
SetBuffer BackBuffer()

numIterations% = 100

LockBuffer GraphicsBuffer()

st = MilliSecs()
For i = 1 To numIterations
	PlotCircle(400, 300, 290, $FFE020, 1)
Next
et1# = MilliSecs()-st

st = MilliSecs()
For i = 1 To numIterations
	Oval 110, 10, 580, 580, 1
Next
et2# = MilliSecs()-st

UnlockBuffer

Text 5,	5,"Avg/Circle: "+(et1/Float(numIterations))+"ms"
Text 5,20,"  Avg/Oval: "+(et2/Float(numIterations))+"ms"

Flip
WaitMouse()
End

Function PlotCircle(CX%, CY%, R%, colr%, fill% = 0)
	Local X%, Y%
	Local XChange%, YChange%
	Local RadiusError%
	Color (colr And $FF0000) Shr 16, (colr And $FF00) Shr 8, colr And $FF
	X = R
	Y = 0
	XChange = 1 - (R Shl 1)
	YChange = 1
	RadiusError = 0

	While  X &gt;= Y

		If fill &lt;&gt; 0 Then
			Line(CX-X, CY+Y, CX+X, CY+Y)		;used calc'd values to draw
			Line(CX-X, CY-Y, CX+X, CY-Y)		;scan lines from points
			Line(CX-Y, CY+X, CX+Y, CY+X)		;in opposite octants
			Line(CX-Y, CY-X, CX+Y, CY-X)
		Else
			WritePixelFast(CX+X, CY+Y, colr)	; {point in octant 1}
			WritePixelFast(CX-X, CY+Y, colr)	; {point in octant 4}
			WritePixelFast(CX-X, CY-Y, colr)	; {point in octant 5}
			WritePixelFast(CX+X, CY-Y, colr)	; {point in octant 8}
			WritePixelFast(CX+Y, CY+X, colr)	; {point in octant 2}
			WritePixelFast(CX-Y, CY+X, colr)	; {point in octant 3}
			WritePixelFast(CX-Y, CY-X, colr)	; {point in octant 6}
			WritePixelFast(CX+Y, CY-X, colr)	; {point in octant 7}
		End If

		Y = Y + 1
		RadiusError = RadiusError + YChange
		YChange = YChange + 2

		If (RadiusError Shl 1) + XChange &gt; 0 Then
			X = X - 1
			RadiusError = RadiusError + XChange
			XChange = XChange + 2
		End If
	Wend
End Function</pre><br><br><br>Ellipse: </td></tr><tr ><td class="cell"><pre class="code">;Source:
;	http://homepage.smc.edu/kennedy_john/papers.htm
;	http://homepage.smc.edu/kennedy_john/belipse.pdf

;	A Fast Bresenham Type Algorithm For Drawing Ellipses
;	by 
;	John Kennedy

;	Blitzplus/Blitz 3D port by Andy_A

AppTitle "Bresenham Ellipse"
Graphics 800,600,32,2
SetBuffer BackBuffer()


numRepeats% = 100

LockBuffer GraphicsBuffer()

st = MilliSecs()
For i = 1 To numRepeats
	Ellipse(400, 300, 390, 290,$FFE020, 0)
Next
et1# = MilliSecs()-st

st = MilliSecs()
For i = 1 To numRepeats
	Oval 10, 10, 780, 580, 1
Next
et2# = MilliSecs()-st

UnlockBuffer


Text 5,	5,"Avg/Ellipse: "+(et1/Float(numRepeats))+"ms"
Text 5,20,"   Avg/Oval: "+(et2/Float(numRepeats))+"ms"


Flip
WaitMouse()
End

Function Ellipse(CX%, CY%, XRadius%, YRadius%, colr%, fill%);
	Local X%, Y%
	Local XChange%, YChange%
	Local EllipseError%
	Local TwoASquare%, TwoBSquare%
	Local StoppingX%, StoppingY%
	Color (colr And $FF0000) Shr 16, (colr And $FF00) Shr 8, colr And $FF

	TwoASquare = (XRadius*XRadius) Shl 1
	TwoBSquare = (YRadius*YRadius) Shl 1
	X = XRadius
	Y = 0
	XChange = YRadius*YRadius*(1-(XRadius Shl 1))
	YChange = XRadius*XRadius
	EllipseError = 0
	StoppingX = TwoBSquare*XRadius
	StoppingY = 0

	While StoppingX &gt;= StoppingY 				; do {1st set of points, y' &gt; -1}

		If fill &lt;&gt; 0 Then
			Line(CX-X, CY+Y, CX+X, CY+Y)		; used calc'd points to draw scan
			Line(CX-X, CY-Y, CX+X, CY-Y)		; lines from opposite quadrants
		Else
			WritePixelFast(CX+X, CY+Y, colr)	; {point in quadrant 1}
			WritePixelFast(CX-X, CY+Y, colr)	; {point in quadrant 2}
			WritePixelFast(CX-X, CY-Y, colr)	; {point in quadrant 3}
			WritePixelFast(CX+X, CY-Y, colr)	; {point in quadrant 4}
		End If

		Y = Y + 1
		StoppingY = StoppingY + TwoASquare
		EllipseError = EllipseError + YChange
		YChange = YChange + TwoASquare

		If (EllipseError Shl 1) + XChange &gt; 0 Then
			X = X - 1
			StoppingX = StoppingX - TwoBSquare
			EllipseError = EllipseError + XChange
			XChange = XChange + TwoBSquare
		End If
	Wend
	
	;{ 1st set of points is done; start the 2nd set of points }
	X = 0
	Y = YRadius
	XChange = YRadius*YRadius
	YChange = XRadius*XRadius*(1 - (YRadius Shl 1))
	EllipseError = 0
	StoppingX = 0
	StoppingY = TwoASquare*YRadius

	While StoppingX &lt;= StoppingY 				;do {2nd set of points, y' &lt; -1}

		If fill &lt;&gt; 0 Then					
			Line(CX-X, CY+Y, CX+X, CY+Y)		; used calc'd points to draw scan
			Line(CX-X, CY-Y, CX+X, CY-Y)		; lines from opposite quadrants
		Else
			WritePixelFast(CX+X, CY+Y, colr)	; {point in quadrant 1}
			WritePixelFast(CX-X, CY+Y, colr)	; {point in quadrant 2}
			WritePixelFast(CX-X, CY-Y, colr)	; {point in quadrant 3}
			WritePixelFast(CX+X, CY-Y, colr)	; {point in quadrant 4}
		End If

		X = X + 1
		StoppingX = StoppingX + TwoBSquare
		EllipseError = EllipseError + XChange
		XChange = XChange + TwoBSquare

		If (EllipseError Shl 1) + YChange &gt; 0 Then
			Y = Y - 1
			StoppingY = StoppingY - TwoASquare
			EllipseError = EllipseError + YChange
			YChange = YChange + TwoASquare
		End If
	Wend
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MCP</td><td align="right"><font class=tiny>2011</font></td></tr></table></td></tr><tr ><td class="posttext"> Excellent stuff. Thanks for spending your time on this! :)<br><br>BTW: With debug off I get 14ms to draw an elipse with your code vs 38ms with Blitz Oval. Nice. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Andy_A</td><td align="right"><font class=tiny>2011</font></td></tr></table></td></tr><tr ><td class="posttext"> No worries,<br><br>I was researching some other math's when I stumbled on John Kennedy's site.<br><br>I posted because it may help others (until processors are so fast that there isn't any difference in speed). <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>2011</font></td></tr></table></td></tr><tr ><td class="posttext"> not fast but useful I think.<br><pre class=code>
Function Ellipse(CX%, CY%, XRadius%, YRadius%,angle:Float)'
	Local X%, Y%
	Local XChange%, YChange%
	Local EllipseError%
	Local TwoASquare%, TwoBSquare%
	Local StoppingX%, StoppingY%
	Local xdiameter% = XRadius*XRadius
	Local ydiameter% = YRadius*YRadius
	Local xc#,ys#,yc#,xs#
	TwoASquare = (xdiameter) Shl 1
	TwoBSquare = (ydiameter) Shl 1
	X = XRadius
	Y = 0
	XChange = ydiameter*(1-(XRadius Shl 1))
	YChange = xdiameter
	EllipseError = 0
	StoppingX = TwoBSquare*XRadius
	StoppingY = 0
	Local c:Float=Cos(angle)
	Local s:Float=Sin(angle)
	While StoppingX &gt;= StoppingY 				
		xc = c*x
		ys = s*y
		yc = c*y
		xs = s*x
		Plot CX+xc-ys, CY+yc+xs	
		Plot CX-xc-ys, CY+yc-xs	
		Plot CX-xc+ys, CY-yc-xs	
		Plot CX+xc+ys, CY-yc+xs	
		Y = Y + 1
		StoppingY = StoppingY + TwoASquare
		EllipseError = EllipseError + YChange
		YChange = YChange + TwoASquare

		If (EllipseError Shl 1) + XChange &gt; 0 Then
			X = X - 1
			StoppingX = StoppingX - TwoBSquare
			EllipseError = EllipseError + XChange
			XChange = XChange + TwoBSquare
		End If
	Wend
	
	
	X = 0
	Y = YRadius
	XChange = YDiameter
	YChange = XDiameter*(1 - (YRadius Shl 1))
	EllipseError = 0
	StoppingX = 0
	StoppingY = TwoASquare*YRadius
	While StoppingX &lt;= StoppingY 				
		xc = c*x
		ys = s*y
		yc = c*y
		xs = s*x
		Plot CX+xc-ys, CY+yc+xs	
		Plot CX-xc-ys, CY+yc-xs	
		Plot CX-xc+ys, CY-yc-xs	
		Plot CX+xc+ys, CY-yc+xs	
		X = X + 1
		StoppingX = StoppingX + TwoBSquare
		EllipseError = EllipseError + XChange
		XChange = XChange + TwoBSquare

		If (EllipseError Shl 1) + YChange &gt; 0 Then
			Y = Y - 1
			StoppingY = StoppingY - TwoASquare
			EllipseError = EllipseError + YChange
			YChange = YChange + TwoASquare
		End If
	Wend
End Function
</pre> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Andy_A</td><td align="right"><font class=tiny>2011</font></td></tr></table></td></tr><tr ><td class="posttext"> That's useful if the ovals are small and gaps between pixels is acceptable.<br><br>On my machine the <i>WritePixelFast</i> version is about 70x faster!<br><pre class=code>Function Ellipse(CX%, CY%, XRadius%, YRadius%, colr%, angle#)
	Local X%, Y%
	Local XChange%, YChange%
	Local EllipseError%
	Local TwoASquare%, TwoBSquare%
	Local StoppingX%, StoppingY%
	Local xdiameter% = XRadius*XRadius
	Local ydiameter% = YRadius*YRadius
	Local xc#,ys#,yc#,xs#, ca#, sa#
	
	TwoASquare = (xdiameter) Shl 1
	TwoBSquare = (ydiameter) Shl 1
	X = XRadius
	Y = 0
	XChange = ydiameter*(1-(XRadius Shl 1))
	YChange = xdiameter
	EllipseError = 0
	StoppingX = TwoBSquare*XRadius
	StoppingY = 0
	ca = Cos(angle)
	sa = Sin(angle)
	While StoppingX &gt;= StoppingY 				
		xc = ca*x
		ys = sa*y
		yc = ca*y
		xs = sa*x
		WritePixelFast(CX+xc-ys, CY+yc+xs, colr)	
		WritePixelFast(CX-xc-ys, CY+yc-xs, colr)	
		WritePixelFast(CX-xc+ys, CY-yc-xs, colr)	
		WritePixelFast(CX+xc+ys, CY-yc+xs, colr)	
		Y = Y + 1
		StoppingY = StoppingY + TwoASquare
		EllipseError = EllipseError + YChange
		YChange = YChange + TwoASquare

		If (EllipseError Shl 1) + XChange &gt; 0 Then
			X = X - 1
			StoppingX = StoppingX - TwoBSquare
			EllipseError = EllipseError + XChange
			XChange = XChange + TwoBSquare
		End If
	Wend
	
	
	X = 0
	Y = YRadius
	XChange = YDiameter
	YChange = XDiameter*(1 - (YRadius Shl 1))
	EllipseError = 0
	StoppingX = 0
	StoppingY = TwoASquare*YRadius
	While StoppingX &lt;= StoppingY 				
		xc = ca*x
		ys = sa*y
		yc = ca*y
		xs = sa*x
		WritePixelFast(CX+xc-ys, CY+yc+xs, colr)
		WritePixelFast(CX-xc-ys, CY+yc-xs, colr)	
		WritePixelFast(CX-xc+ys, CY-yc-xs, colr)	
		WritePixelFast(CX+xc+ys, CY-yc+xs, colr)	
		X = X + 1
		StoppingX = StoppingX + TwoBSquare
		EllipseError = EllipseError + XChange
		XChange = XChange + TwoBSquare

		If (EllipseError Shl 1) + YChange &gt; 0 Then
			Y = Y - 1
			StoppingY = StoppingY - TwoASquare
			EllipseError = EllipseError + YChange
			YChange = YChange + TwoASquare
		End If
	Wend
End Function</pre> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>2011</font></td></tr></table></td></tr><tr ><td class="posttext"> I was just trying to illustrate rotation principles. I didn't try to make it exact or fast. if speed would have been my concern I would have given an example with Blitzmax and drawn to an image and simply hardware rotate it.  it would have been faster than... and I didn't do it with writepixelfast because blitzbasic don't work on Macs and therefore couldn't test it. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Andy_A</td><td align="right"><font class=tiny>2011</font></td></tr></table></td></tr><tr ><td class="posttext"> I just wrote the B+ version so that those with B+/B3d can use a faster version. But since I didn't mention it in my prior post, thanks for making the modification. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jimmy</td><td align="right"><font class=tiny>2011</font></td></tr></table></td></tr><tr ><td class="posttext"> Bresenham like code for rotated ellipses <br><br>Uses crunches huge numbers more than can be handled which introduces the jumpy bug at some sizes..<br><br>But it produces silky smooth ellipses, which is rather scarce to see these days.<br><br><pre class=code>
Graphics  800,600,16,2:SetBuffer BackBuffer()
Repeat:Cls:xx=MouseX():yy=MouseY()
Color 0,255,0:ellipse(512,384,1+yy/2.0,200,xx/200.0)
Color 255,255,255:Plot 512,384:Plot 512+100,384:Plot 512-100,384:Plot 512,384+200:Plot 512,384-200
Flip
Until MouseDown(2)

Function ellipse (iXC#,iYC#,A#,B#,angle#)
;
; General ellipse
;
; Known bugs:
; jumpy movement sometimes, as it uses huge numbers, too large To fit storage
; It has few lines with trigonometry, these could very possibly be replaced with integer table or 
; tables of degrees Or similiar. It wraps the angle into an acceptable range, and sets xflag accordingly.
;
R2D# = 180.0/Pi:D2R# = Pi/180.0
If angle# &lt; Pi/2.0
xflag#=1
ElseIf angle#&gt;Pi/2 And angle#&lt;Pi
temp#=angle#-Pi/2.0:angle#=Pi/2.0-temp#:xflag#=-1
ElseIf angle#&gt;=Pi And angle#&lt;Pi*1.5
angle#=angle#-Pi:xflag#=1
ElseIf angle#&gt;=Pi*1.5 And angle#&lt;Pi*2.0
angle#=angle#-Pi:temp#=angle#-Pi/2.0:angle#=Pi/2.0-temp#:xflag#=-1
EndIf

; ...To output these 4 integer points. (and the said xflag), These are where major and minor axis of ellipse ends (determines shape as in aligned axis algorithms).
ixa#=Int(Cos(angle#*R2D#)*A#)
iya#=Int(Sin(angle#*R2D#)*A#)
ixb#=Int(Cos((angle#+(Pi/2.0))*R2D#)*B#)
iyb#=Int(Sin((angle#+(Pi/2.0))*R2D#)*B#)

; rest of code uses only variables:
; ixa,iya,ixb,iyb
; ixc, iyc, xflag
; these are as every one else from now on, integers

Plot ixc#-ixa#,iyc#-iya#
Plot ixc#-ixb#,iyc#-iyb#
Plot ixc#-ixb#,iyc#-iya#
Plot ixc#-ixa#,iyc#-iyb#

; From now on, integers only, it uses multiplication much, and needs very large integers (in large resolutions even more than 64bits)
; therefor single precision are used to alloud the space, but theres no fixed point Or floating point involved.

   ixa2#=ixa#*ixa#
   iya2#=iya#*iya#
   ixb2#=ixb#*ixb#
   iyb2#=iyb#*iyb#
   ixaya#=ixa#*iya#
   ixbyb#=ixb#*iyb#
   ila2#=ixa2#+iya2#
   ila4#=ila2#*ila2#
   ilb2#=ixb2#+iyb2#
   ilb4#=ilb2#*ilb2#
   ia#=ixa2#*ilb4#+ixb2#*ila4#
   ib#=ixaya#*ilb4#+ixbyb#*ila4#
   ic#=iya2#*ilb4#+iyb2#*ila4#
   id#=ila4#*ilb4#

   If iYA# &lt;= iXA#
       ; Start AT (-xA,-yA) 
       iX# = -iXA#:iY# = -iYA#:iDx# = -(iB#*iXA#+iC#*iYA#):iDy# = iA#*iXA#+iB#*iYA# 

       ; Arc FROM (-xA,-yA) TO point (x0,y0) where dx/dy = 0 
       While iDx# &lt;= 0
           Plot iXC#+iX#*xflag#,iYC#+iY#
           Plot iXC#-iX#*xflag#,iYC#-iY#
           iY#=iY#+1
           iSigma# = iA#*iX#*iX#+2*iB#*iX#*iY#+iC#*iY#*iY#-iD# 
           If iSigma# &lt; 0 Then iDx#=iDx#-iB#:iDy#=iDy#+iA#:iX#=iX#-1
           iDx#=iDx# + iC# 
           iDy#=iDy# - iB# 
       Wend 

       ; Arc FROM (x0,y0) TO point (x1,y1) where dy/dx = 1 
       While iDx# &lt;= iDy# 
           Plot iXC#+iX#*xflag#,iYC#+iY# 
           Plot iXC#-iX#*xflag#,iYC#-iY# 
           iY#=iY#+1
           iXp1# = iX#+1 
           iSigma# = iA#*iXp1#*iXp1#+2*iB#*iXp1#*iY#+iC#*iY#*iY#-iD# 
           If iSigma# &gt;= 0 Then iDx#=iDx# + iB#:iDy#=iDy# - iA#:iX# = iXp1# 
           iDx#=iDx# + iC# 
           iDy#=iDy# - iB# 
       Wend 

       ; Arc FROM (x1,y1) TO point (x2,y2) where dy/dx = 0 
       While iDy# &gt;= 0
           Plot iXC#+iX#*xflag#,iYC#+iY#
           Plot iXC#-iX#*xflag#,iYC#-iY#
           iX=iX+1 
           iSigma# = iA#*iX#*iX#+2*iB#*iX#*iY#+iC#*iY#*iY#-iD# 
           If iSigma# &lt; 0 Then iDx#=iDx# + iC# : iDy#=iDy# - iB# : iY#=iY#+1 
           iDx#=iDx# + iB# 
           iDy#=iDy# - iA# 
       Wend 

       ; Arc FROM (x2,y2) TO point (x3,y3) where dy/dx = -1 
       While iDy# &gt;= -iDx# 
           Plot iXC#+iX#*xflag#,iYC#+iY#
           Plot iXC#-iX#*xflag#,iYC#-iY#
           iX#=iX#+1 
           iYm1# = iY#-1 
           iSigma# = iA#*iX#*iX#+2*iB#*iX#*iYm1#+iC#*iYm1#*iYm1#-iD# 
           If iSigma# &gt;= 0 Then iDx#=iDx# - iC#:iDy#=iDy# + iB#:iY# = iYm1# 
           iDx#=iDx# + iB# 
           iDy#=iDy# - iA# 
       Wend 

       ; Arc FROM (x3,y3) TO (xa,ya) 
       While iY# &gt;= iYA# 
           Plot iXC#+iX#*xflag#,iYC#+iY#
           Plot iXC#-iX#*xflag#,iYC#-iY#
           iY#=iY#-1
           iSigma# = iA#*iX#*iX#+2*iB#*iX#*iY#+iC#*iY#*iY#-iD# 
           If iSigma# &lt; 0 Then iDx#=iDx# + iB#:iDy#=iDy# - iA#:iX#=iX#+1 
           iDx#=iDx# - iC# 
           iDy#=iDy# + iB# 
       Wend 

   Else 
       ; Start AT (-xa,-ya) 
       iX# = -iXA#:iY# = -iYA#:iDx# = -(iB#*iXA#+iC#*iYA#):iDy# = iA#*iXA#+iB#*iYA#

       ; Arc FROM (-xa,-ya) TO point (x0,y0) where dy/dx = -1 
       While -iDx# &gt;= iDy# 
           Plot iXC#+iX#*xflag#,iYC#+iY#
           Plot iXC#-iX#*xflag#,iYC#-iY# 
           iX#=iX#-1
           iYp1# = iY#+1 
           iSigma# = iA#*iX#*iX#+2*iB#*iX#*iYp1#+iC#*iYp1#*iYp1#-iD# 
           If iSigma# &gt;= 0 Then iDx#=iDx# + iC# : iDy#=iDy# - iB#:iY# = iYp1# 
           iDx#=IDx# - iB# 
           iDy#=Idy# +iA# 
       Wend 

       ; Arc FROM (x0,y0) TO point (x1,y1) where dx/dy = 0 
       While iDx# &lt;= 0
           Plot iXC#+iX#*xflag#,iYC#+iY#
           Plot iXC#-iX#*xflag#,iYC#-iY#
           iY#=iY#+1
           iSigma# = iA#*iX#*iX#+2*iB#*iX#*iY#+iC#*iY#*iY#-iD# 
           If iSigma# &lt; 0 Then iDx#=iDx# - iB#:iDy#=iDy# + iA#:iX#=iX#-1
           iDx#=iDx# + iC# 
           iDy#=iDy# - iB# 
       Wend 

       ; Arc FROM (x1,y1) TO point (x2,y2) where dy/dx = 1 
       While iDx# &lt;= iDy# 
           Plot iXC#+iX#*xflag#,iYC#+iY#
           Plot iXC#-iX#*xflag#,iYC#-iY#
           iY#=iY#+1
           iXp1# = iX#+1 
           iSigma# = iA#*iXp1#*iXp1#+2*iB#*iXp1#*iY#+iC#*iY#*iY#-iD# 
           If iSigma# &gt;= 0 Then iDx#=IDx# + iB# :iDy#=Idy# - iA# : iX# = iXp1# 
           iDx#=iDx# + iC# 
           iDy#=iDy# - iB# 
       Wend 

       ; Arc FROM (x2,y2) TO point (x3,y3) where dy/dx = 0 
       While iDy# &gt;= 0
           Plot iXC#+iX#*xflag#,iYC#+iY#
           Plot iXC#-iX#*xflag#,iYC#-iY# 
           iX#=iX#+1 
           iSigma# = iA#*iX#*iX#+2*iB#*iX#*iY#+iC#*iY#*iY#-iD# 
           If iSigma# &lt; 0 Then iDx#=iDx# + iC# : iDy#=iDy# - iB# : iY#=iY#+1 
           iDx#=iDx# + iB# 
           iDy#=iDy# - iA# 
       Wend 

       ; Arc FROM (x3,y3) TO (xa,ya) 
       While iX# &lt;= iXA#
           Plot iXC#+iX#*xflag#,iYC#+iY# 
           Plot iXC#-iX#*xflag#,iYC#-iY# 
           iX#=iX#+1 
           iYm1# = iY#-1 
           iSigma# = iA#*iX#*iX#+2*iB#*iX#*iYm1#+iC#*iYm1#*iYm1#-iD# 
           If iSigma# &gt;= 0 Then iDx#=iDx# - iC# : iDy#=iDy# + iB# :iY# = iYm1# 
           iDx#=iDx# + iB# 
           iDy#=iDy# - iA# 
       Wend 

   EndIf
End Function
</pre> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MCP</td><td align="right"><font class=tiny>2011</font></td></tr></table></td></tr><tr ><td class="posttext"> Fantastic stuff Jimmy :) <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Andy_A</td><td align="right"><font class=tiny>2011</font></td></tr></table></td></tr><tr ><td class="posttext"> Jimmy that's Brilliant!<br><br>I tested to see what a difference it would make to lock the buffer and use WritePixelFast, the results were over 50 times the original speed!  <br><br>It approaches Bresenham's speed, with rotated ellipses no less.<br><br>Here's the WritePixelFast code I used to test:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Bresenham like rotated ellipses by Jimmy
;http://www.blitzmax.com/codearcs/codearcs.php?code=2817#comments
Graphics  800,600,16,2
SetBuffer BackBuffer()


pi2# = Pi * 2.0
numOvals# = 1000.0
rate# = pi2/numOvals

Cls
Color 0,255,0
i# = 0.0
st = MilliSecs()
LockBuffer
While i &lt;= pi2
	ellipse(400.0, 300.0, 280.0, 80.0, i#, $00ff00)
	i = i + rate
Wend
UnlockBuffer
et = MilliSecs() - st
Text 5,5,Int(numOvals)+" ovals in "+et+"ms = "+et/numOvals +" ms/oval"
Flip
WaitMouse()

End


Function ellipse (iXC#,iYC#,A#,B#,angle#,hexColor%)
;
; General ellipse
;
; Known bugs:
; jumpy movement sometimes, as it uses huge numbers, too large to fit storage
; It has few lines with trigonometry, these could very possibly be replaced with integer table or 
; tables of degrees or similiar.
; It wraps the angle into an acceptable range, and sets xflag accordingly.
;
R2D# = 180.0/Pi:D2R# = Pi/180.0
If angle# &lt; Pi/2.0 Then
	xflag#=1
Else
	If angle#&gt;Pi/2 And angle#&lt;Pi Then
		temp#=angle#-Pi/2.0:angle#=Pi/2.0-temp#:xflag#=-1
	Else
		If angle#&gt;=Pi And angle#&lt;Pi*1.5 Then 
			angle#=angle#-Pi:xflag#=1
		Else
			If angle#&gt;=Pi*1.5 And angle#&lt;Pi*2.0 Then
				angle#=angle#-Pi:temp#=angle#-Pi/2.0:angle#=Pi/2.0-temp#:xflag#=-1
			End If
		End If
	End If
End If

; ...To output these 4 integer points. (and the said xflag), 
; These are where major &amp; minor axis of ellipse ends (determines shape as in aligned axis algorithms).
ixa#=Int(Cos(angle#*R2D#)*A#)
iya#=Int(Sin(angle#*R2D#)*A#)
ixb#=Int(Cos((angle#+(Pi/2.0))*R2D#)*B#)
iyb#=Int(Sin((angle#+(Pi/2.0))*R2D#)*B#)

; rest of code uses only variables:
; ixa,iya,ixb,iyb
; ixc, iyc, xflag
; these are as every one else from now on, integers

;WritePixelFast(ixc#-ixa#,iyc#-iya#,$00ff00) ;these lines created artifacts ???
;WritePixelFast(ixc#-ixb#,iyc#-iyb#,$00ff00)
;WritePixelFast(ixc#-ixb#,iyc#-iya#,$00ff00)
;WritePixelFast(ixc#-ixa#,iyc#-iyb#,$00ff00)

; From now on, integers only, it uses multiplication much, and needs very large integers
; (in large resolutions even more than 64bits)
; therefor single precision are used to alloud the space, 
; but theres no fixed point or floating point involved.

   ixa2#=ixa#*ixa#
   iya2#=iya#*iya#
   ixb2#=ixb#*ixb#
   iyb2#=iyb#*iyb#
   ixaya#=ixa#*iya#
   ixbyb#=ixb#*iyb#
   ila2#=ixa2#+iya2#
   ila4#=ila2#*ila2#
   ilb2#=ixb2#+iyb2#
   ilb4#=ilb2#*ilb2#
   ia#=ixa2#*ilb4#+ixb2#*ila4#
   ib#=ixaya#*ilb4#+ixbyb#*ila4#
   ic#=iya2#*ilb4#+iyb2#*ila4#
   id#=ila4#*ilb4#

   If iYA# &lt;= iXA#
       ; Start AT (-xA,-yA) 
       iX# = -iXA#:iY# = -iYA#:iDx# = -(iB#*iXA#+iC#*iYA#):iDy# = iA#*iXA#+iB#*iYA# 

       ; Arc FROM (-xA,-yA) TO point (x0,y0) where dx/dy = 0 
       While iDx# &lt;= 0
           WritePixelFast(iXC#+iX#*xflag#,iYC#+iY#,hexColor)
           WritePixelFast(iXC#-iX#*xflag#,iYC#-iY#,hexColor)
           iY#=iY#+1
           iSigma# = iA#*iX#*iX#+2*iB#*iX#*iY#+iC#*iY#*iY#-iD# 
           If iSigma# &lt; 0 Then iDx#=iDx#-iB#:iDy#=iDy#+iA#:iX#=iX#-1
           iDx#=iDx# + iC# 
           iDy#=iDy# - iB# 
       Wend 

       ; Arc FROM (x0,y0) TO point (x1,y1) where dy/dx = 1 
       While iDx# &lt;= iDy# 
           WritePixelFast(iXC#+iX#*xflag#,iYC#+iY#,hexColor) 
           WritePixelFast(iXC#-iX#*xflag#,iYC#-iY#,hexColor) 
           iY#=iY#+1
           iXp1# = iX#+1 
           iSigma# = iA#*iXp1#*iXp1#+2*iB#*iXp1#*iY#+iC#*iY#*iY#-iD# 
           If iSigma# &gt;= 0 Then iDx#=iDx# + iB#:iDy#=iDy# - iA#:iX# = iXp1# 
           iDx#=iDx# + iC# 
           iDy#=iDy# - iB# 
       Wend 

       ; Arc FROM (x1,y1) TO point (x2,y2) where dy/dx = 0 
       While iDy# &gt;= 0
           WritePixelFast(iXC#+iX#*xflag#,iYC#+iY#,hexColor)
           WritePixelFast(iXC#-iX#*xflag#,iYC#-iY#,hexColor)
           iX=iX+1 
           iSigma# = iA#*iX#*iX#+2*iB#*iX#*iY#+iC#*iY#*iY#-iD# 
           If iSigma# &lt; 0 Then iDx#=iDx# + iC# : iDy#=iDy# - iB# : iY#=iY#+1 
           iDx#=iDx# + iB# 
           iDy#=iDy# - iA# 
       Wend 

       ; Arc FROM (x2,y2) TO point (x3,y3) where dy/dx = -1 
       While iDy# &gt;= -iDx# 
           WritePixelFast(iXC#+iX#*xflag#,iYC#+iY#,hexColor)
           WritePixelFast(iXC#-iX#*xflag#,iYC#-iY#,hexColor)
           iX#=iX#+1 
           iYm1# = iY#-1 
           iSigma# = iA#*iX#*iX#+2*iB#*iX#*iYm1#+iC#*iYm1#*iYm1#-iD# 
           If iSigma# &gt;= 0 Then iDx#=iDx# - iC#:iDy#=iDy# + iB#:iY# = iYm1# 
           iDx#=iDx# + iB# 
           iDy#=iDy# - iA# 
       Wend 

       ; Arc FROM (x3,y3) TO (xa,ya) 
       While iY# &gt;= iYA# 
           WritePixelFast(iXC#+iX#*xflag#,iYC#+iY#,hexColor)
           WritePixelFast(iXC#-iX#*xflag#,iYC#-iY#,hexColor)
           iY#=iY#-1
           iSigma# = iA#*iX#*iX#+2*iB#*iX#*iY#+iC#*iY#*iY#-iD# 
           If iSigma# &lt; 0 Then iDx#=iDx# + iB#:iDy#=iDy# - iA#:iX#=iX#+1 
           iDx#=iDx# - iC# 
           iDy#=iDy# + iB# 
       Wend 

   Else 
       ; Start AT (-xa,-ya) 
       iX# = -iXA#:iY# = -iYA#:iDx# = -(iB#*iXA#+iC#*iYA#):iDy# = iA#*iXA#+iB#*iYA#

       ; Arc FROM (-xa,-ya) TO point (x0,y0) where dy/dx = -1 
       While -iDx# &gt;= iDy# 
           WritePixelFast(iXC#+iX#*xflag#,iYC#+iY#,hexColor)
		   WritePixelFast(iXC#-iX#*xflag#,iYC#-iY#,hexColor) 
           iX#=iX#-1
           iYp1# = iY#+1 
           iSigma# = iA#*iX#*iX#+2*iB#*iX#*iYp1#+iC#*iYp1#*iYp1#-iD# 
           If iSigma# &gt;= 0 Then iDx#=iDx# + iC# : iDy#=iDy# - iB#:iY# = iYp1# 
           iDx#=IDx# - iB# 
           iDy#=Idy# +iA# 
       Wend 

       ; Arc FROM (x0,y0) TO point (x1,y1) where dx/dy = 0 
       While iDx# &lt;= 0
           WritePixelFast(iXC#+iX#*xflag#,iYC#+iY#,hexColor)
           WritePixelFast(iXC#-iX#*xflag#,iYC#-iY#,hexColor)
           iY#=iY#+1
           iSigma# = iA#*iX#*iX#+2*iB#*iX#*iY#+iC#*iY#*iY#-iD# 
           If iSigma# &lt; 0 Then iDx#=iDx# - iB#:iDy#=iDy# + iA#:iX#=iX#-1
           iDx#=iDx# + iC# 
           iDy#=iDy# - iB# 
       Wend 

       ; Arc FROM (x1,y1) TO point (x2,y2) where dy/dx = 1 
       While iDx# &lt;= iDy# 
           WritePixelFast(iXC#+iX#*xflag#,iYC#+iY#,hexColor)
           WritePixelFast(iXC#-iX#*xflag#,iYC#-iY#,hexColor)
           iY#=iY#+1
           iXp1# = iX#+1 
           iSigma# = iA#*iXp1#*iXp1#+2*iB#*iXp1#*iY#+iC#*iY#*iY#-iD# 
           If iSigma# &gt;= 0 Then iDx#=IDx# + iB# :iDy#=Idy# - iA# : iX# = iXp1# 
           iDx#=iDx# + iC# 
           iDy#=iDy# - iB# 
       Wend 

       ; Arc FROM (x2,y2) TO point (x3,y3) where dy/dx = 0 
       While iDy# &gt;= 0
           WritePixelFast(iXC#+iX#*xflag#,iYC#+iY#,hexColor)
           WritePixelFast(iXC#-iX#*xflag#,iYC#-iY#,hexColor) 
           iX#=iX#+1 
           iSigma# = iA#*iX#*iX#+2*iB#*iX#*iY#+iC#*iY#*iY#-iD# 
           If iSigma# &lt; 0 Then iDx#=iDx# + iC# : iDy#=iDy# - iB# : iY#=iY#+1 
           iDx#=iDx# + iB# 
           iDy#=iDy# - iA# 
       Wend 

       ; Arc FROM (x3,y3) TO (xa,ya) 
       While iX# &lt;= iXA#
           WritePixelFast(iXC#+iX#*xflag#,iYC#+iY#,hexColor) 
           WritePixelFast(iXC#-iX#*xflag#,iYC#-iY#,hexColor) 
           iX#=iX#+1 
           iYm1# = iY#-1 
           iSigma# = iA#*iX#*iX#+2*iB#*iX#*iYm1#+iC#*iYm1#*iYm1#-iD# 
           If iSigma# &gt;= 0 Then iDx#=iDx# - iC# : iDy#=iDy# + iB# :iY# = iYm1# 
           iDx#=iDx# + iB# 
           iDy#=iDy# - iA# 
       Wend 

   EndIf
End Function
</textarea><br><br>I used this code at the top of the listing to modify your original post in making the speed comparisons.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;http://www.blitzmax.com/codearcs/codearcs.php?code=2817#comments
;Bresenham like code for rotated ellipses by Jimmy

Graphics  800,600,16,2
SetBuffer BackBuffer()
pi2# = Pi * 2.0
numOvals# = 1000.0
rate# = pi2/numOvals


Cls
Color 0,255,0
st = MilliSecs()

i# = 0.0
While i &lt;= pi2
	ellipse(400.0, 300.0, 280.0, 80.0, i)
	i = i + rate
Wend
et = MilliSecs() - st
Text 5,5,Int(numOvals)+" ovals in "+et+"ms = "+et/numOvals +" ms/oval"
Flip
WaitMouse()

End

;...
; Jimmy's originally posted function goes here
;...

</textarea> <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
