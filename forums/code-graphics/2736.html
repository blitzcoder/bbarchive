<!DOCTYPE html><html lang="en" ><head ><title >fixedmap font</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='display bmax fonts as bitmap font, language=bmx, category=Graphics'><meta name='author' content='Jesse'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/fixedmap font</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2736.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >fixedmap font by Jesse</td><td align="right">2010 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> this is for those who keep on complaining that BlitzMax font module is slow.<br>this program copies the loaded glyph images and makes copies of equal size images and display them directly or rotated into the display. there are two ways to display them.<br>if you are planning to use rotation and/or scaling use the draw method "fast"<br>else if you are not planning to use "SetRotation" to display them use the DrawFixed method  "faster". </td></tr><tr ><td class="cell"><pre class="code">Type TBitMap
	Field image		:TImage
	Field gx			:Int
	Field gy			:Int
	Field advance		:Int
	Function Create:TBitMap(image:TImage,x:Float,y:Float,adv:Int,width:Int,height:Int)
		
		If width  = 0 Return Null 
		If height = 0  Return Null
		Local b:TbitMap = New TBitMap
		If image = Null
			b.gx = x
			b.gy = y
			b.advance = adv
			Return b	
		EndIf
		
		
		Local from:TPixmap = image.lock(0,True,False)
		
		b.image = CreateImage(width,height)
		b.gx = x
		b.gy = y
		b.advance = adv
		
		Local inTo:TPixmap = b.image.lock(0,True,True)
		into.ClearPixels(0)
		Local w:Int
		Local h:Int
		If into.width  &gt;= from.width+x Then  w = from.width  Else w = inTo.width - x
		If into.height &gt;= from.height+x Then h = from.height Else h = inTo.height- y

		For Local d:Int = 0 Until h
			For Local a:Int = 0  Until w
					Local n:Int = from.ReadPixel(a,d)
					inTo.WritePixel(a+x+2,d+y+2,n)
			Next
		Next

		Return b
		
	End Function
	
	
	Method Draw(x:Float,y:Float)
		If image DrawImage image,x,y
	End Method
	
End Type		

Type TBitMapFont
	
	Field bitMap		:Tbitmap[96]
	Field gfx			:TMax2dGraphics
	
	Function Create:TBitmapFont(url:String,size:Int)
		Local oldFont:TImageFont = GetImageFont()
		Local imgFont:TImageFont=LoadImageFont(url,size)
		Local b:TbitMapFont = New TbitMapFont
		If imgfont=Null Return Null
		SetImageFont(imgfont)
		
		b.gfx = tmax2dgraphics.Current()
		
		For Local i:Int = 0 Until 96
			Local n:Int = imgFont.CharToGlyph(i+32)
			If n &lt; 0 Continue
			Local glyph:TImageGlyph = imgFont.LoadGlyph(n)
			If glyph
				b.bitMap[i] = TBitMap.Create(glyph._image,glyph._x,glyph._y,glyph._advance,size*2,size*2)			
			Else
			 	b.bitmap[i] = New TBitMap
			EndIf
		Next
		SetImageFont(oldFont)
		Return b
	End Function
	
	Method draw(text:String,x:Float,y:Float)
		For Local i:Int = 0 Until text.length
			Local a:Int = text[i]-32
			Local bm:TBitMap = bitmap[a]
			bm.Draw(x,y)
			x :+ bm.advance * gfx.tform_ix
			y :+ bm.advance * gfx.tform_jx
		Next
	End Method
	
	Method drawFixed(text:String,x:Float,y:Float)
		For Local i:Int = 0 Until text.length
			Local a:Int = text[i]-32
			bitmap[a].draw(x,y)
			x:+bitmap[a].advance
		Next
			
	End Method
End Type</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> or this is if you want to display the blitzmax font images directly. again fast and faster<br><pre class=code>
SuperStrict
Graphics 800,600
SetBlend Alphablend
Local myfont:TBitmapFont = TBitmapFont.Create(GetEnv_("systemroot")+"\fonts\COURBD.TTF",32)
myfont.drawfixed("this is a drawfixed test no rotation",100,100)
myfont.draw("this is a draw  test no rotation",100,150)
SetRotation 30
myfont.drawfixed("this is a drawfixed test rotated",100,200)
myfont.draw("this is a draw test rotated",100,250)
Flip()
WaitKey()

Type TBitMap
	Field gx			:Int
	Field gy			:Int
	Field gImg			:TImage
	Field advance		:Int
	Function Create:TBitMap(image:TImage,x:Float,y:Float,adv:Int)
		
		Local b:TbitMap = New TBitMap
		b.gImg = image
		b.gx = x
		b.gy = y
		b.advance = adv
		Return b
	End Function
	
	Method Draw(x:Float,y:Float)
			If gImg DrawImage gImg,x,y
	End Method	
End Type		

Type TBitMapFont
	
	Field bitMap		:Tbitmap[96]
	Field gfx			:TMax2dGraphics
	
	Function Create:TBitmapFont(url:String,size:Int)
		Local oldFont:TImageFont = GetImageFont()
		Local imgFont:TImageFont=LoadImageFont(url,size)
		Local b:TbitMapFont = New TbitMapFont
		If imgfont=Null Return Null
		SetImageFont(imgfont)
		
		b.gfx = tmax2dgraphics.Current()
		
		For Local i:Int = 0 Until 96
			Local n:Int = imgFont.CharToGlyph(i+32)
			If n &lt; 0 Continue
			Local glyph:TImageGlyph = imgFont.LoadGlyph(n)
			If glyph
				b.bitMap[i] = TBitMap.Create(glyph._image,glyph._x,glyph._y,glyph._advance)			
			Else
			 	b.bitmap[i] = New TBitMap
			EndIf
		Next
		SetImageFont(oldFont)
		Return b
	End Function
	
	Method draw(text:String,x:Float,y:Float)
		For Local i:Int = 0 Until text.length
			Local bm:TBitMap = bitmap[text[i]-32]
			Local tx:Float = bm.gx * gfx.tform_ix + bm.gy * gfx.tform_iy
			Local ty:Float = bm.gx * gfx.tform_jx + bm.gy * gfx.tform_jy 
			bm.Draw(x+tx,y+ty)
			x :+ bm.advance * gfx.tform_ix
			y :+ bm.advance * gfx.tform_jx
		Next
	End Method
	
	Method drawfixed(text:String,x:Float,y:Float)
		For Local i:Int = 0 Until text.length
			Local bm:TBitMap = bitmap[text[i]-32]
			bm.Draw(x+bm.gx,y+bm.gy)
			x :+ bm.advance
		Next
	End Method

End Type</pre> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>2011</font></td></tr></table></td></tr><tr ><td class="posttext"> Why isn't Field bitMap:Tbitmap[96] extended to 256?<br>With a length of only 96, our Swedish ÅÄÖ aren't supported.<br>Extended Ascii is quite useful you know. ;)<br><a href="http://www.asciitable.com/" target="_blank">http://www.asciitable.com/</a> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>2011</font></td></tr></table></td></tr><tr ><td class="posttext"> I know. I am horrible. I just picked the most common english standard  codes. :)<br><br>you can even extend it to uni-code if the font support it.  Everything is there. ;).<br>simple enough. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
