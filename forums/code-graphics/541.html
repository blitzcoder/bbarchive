<!DOCTYPE html><html lang="en" ><head ><title >ReadImageInfo()</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Retrieve width, height, depth and alpha from a image file without loading it., language=bb, category=Graphics'><meta name='author' content='Marcelo'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/ReadImageInfo()</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="541.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >ReadImageInfo() by Marcelo</td><td align="right">2003 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Retrieve information about an image file without loading it, currently it works only on png, bmp and jpg.<br><br><br>Example usage:<br><br><pre class=code>
Info.TImageInfo = ReadImageInfo("teste.png")

DebugLog Info\Width
DebugLog Info\Height
DebugLog Info\Depth
DebugLog Info\HasAlpha

Delete Info
</pre> </td></tr><tr ><td class="cell"><pre class="code">; Image info
Type TImageInfo
	Field Width
	Field Height
	Field Depth
	
	Field HasAlpha
End Type

Function ReadImageInfo.TImageInfo(imgfile$)
	imgfile$ = Trim(Lower(imgfile$))
	
	file = ReadFile(imgfile)
	If Not file Then Return Null
	
	Width = 0 : Height = 0 : Depth = 0 : HasAlpha = False
	
	If Instr(imgfile, ".png")

		; Check signature
		Sig$ = Chr(137) + "PNG" + Chr(13) + Chr(10) + Chr(26) + Chr(10)
		
		If ReadByteString(file, Len(Sig)) &lt;&gt; Sig
			CloseFile(file)
			Return Null
		EndIf
		
		; Check for valid IHDR chunk
		Size = Int_SwapEndian(ReadInt(file))
		Chunk$ = ReadByteString(file, 4)
		If Chunk &lt;&gt; "IHDR" Or Size &lt;&gt; 13
			CloseFile(file)
			Return Null
		EndIf
		
		; Read IHDR chunk
		Width = Int_SwapEndian(ReadInt(file))
		Height = Int_SwapEndian(ReadInt(file))
		Depth = ReadByte(file)
		
		Col = ReadByte(file)
		
		Select Col
			Case 2
				Depth = Depth * 3 ; RGB  (red + green + blue)
				
			Case 4
				Depth = Depth * 2 ; GA   (gray + alpha)
				HasAlpha = True
				
			Case 5
				Depth = Depth * 4 ; ARGB (Alpha + RGB)
				HasAlpha = True
		End Select
		
	Else If Instr(imgfile, ".bmp")
	
		; Check for bmp file
		If ReadByteString(file, 2) &lt;&gt; "BM"
			CloseFile(file)
			Return Null
		EndIf
		
		; Some non-used stuff
		ReadInt(file) ; File size
		ReadShort(file) ; reserved1
		ReadShort(file) ; reserved2
		ReadInt(file)	; data offset from this point

		Size = ReadInt(file) ; BMPINFO chunk size
		If Size &lt;&gt; 40
			CloseFile(file)
			Return Null
		EndIf
		
		Width = ReadInt(file)
		Height = ReadInt(file)
		
		ReadShort(file) ; planes
		Depth = ReadInt(file)

	Else If Instr(imgfile, ".jpg")
	
		; SOI + APP0
		Sig$ = Chr($FF) + Chr($D8) + Chr($FF) + Chr($E0)
		
		If ReadByteString(file, Len(Sig)) &lt;&gt; Sig
			CloseFile(file)
			Return Null
		EndIf
		
		Size = Short_SwapEndian(ReadShort(file)) - 2
		Pos = FilePos(file)
		
		Ident$ = ReadByteString(file)
		Version = Short_SwapEndian(ReadShort(file))
		
		; Check for JFIF version 1.2
		If (Ident &lt;&gt; "JFIF") Or ( (((Version And $FF00) Shr 8) &lt;&gt; 1) And ((Version And $FF) &lt;&gt; 2) )
			CloseFile(file)
			Return Null
		EndIf
		
		; Search for markers
		SeekFile(file, Pos + Size)

		While Not Eof(file)
			If ReadByte(file) = $FF
				BType = ReadByte(file)
				Size = Short_SwapEndian(ReadShort(file)) - 2
				Pos = FilePos(file)
				
				; if the type is from SOF0 to SOF3
				If (BType &gt;= $C0) And (BType &lt;= $C3)
					Prec = ReadByte(file)
					
					Height = Short_SwapEndian(ReadShort(file))
					Width = Short_SwapEndian(ReadShort(file))
					
					Exit
				EndIf
				
				; Goto next marker
				SeekFile(file, Pos + Size)
			EndIf		
		Wend
		
		Depth = 24	
	EndIf

	CloseFile(file)
	
	Info.TImageInfo = New TImageInfo
	Info\Width = Width
	Info\Height = Height
	Info\Depth = Depth
	Info\HasAlpha = HasAlpha
	
	Return Info
End Function



; Utils
Function Int_SwapEndian%(n%)
	Return ((n And $FF) Shl 24) Or ((n And $FF00) Shl 8) Or ((n And $FF0000) Shr 8) Or ((n And $FF000000) Shr 24)
End Function

Function Short_SwapEndian%(n%)
	Return ((n And $FF) Shl 8) Or ((n And $FF00) Shr 8)
End Function

Function ReadByteString$(file, count = 0)
	Ret$ = ""
	
	If count = 0
		Char = ReadByte(file)
		While Char &lt;&gt; 0
			Ret = Ret + Chr(Char)
			Char = ReadByte(file)
		Wend
	Else
		For i = 1 To count
			Ret = Ret + Chr(ReadByte(file))
		Next
	EndIf
	
	Return Ret
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
