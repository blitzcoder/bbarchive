<!DOCTYPE html><html lang="en" ><head ><title >Image sheet and strip maker</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Image sheet maker, language=bmx, category=Graphics'><meta name='author' content='AdamRedwoods'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/Image sheet and strip maker</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2861.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Image sheet and strip maker by AdamRedwoods</td><td align="right">2011 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Quick Image strip/sheet maker for BMax+MaxGUI.<br>exports PNG files.<br><br>Assumes the first image loaded is the size of all the images.<br>Hold shift to multi-select files.<br><br>You could change this for mac or linux by changing the top MaxGUI import.<br><br>You may not sell this program or code for profit, but you may use the files it makes for commercial uses. Makes sense. </td></tr><tr ><td class="cell"><pre class="code">SuperStrict

''image strip maker
'' copyright 2011 Adam Piette
'' You may not sell this program or code for profit, but you may use the files it makes for commercial uses. Makes sense. 
'
'makes image strips For animated textures
'assumes sheet and strip images are all same sizes of first image loaded
'only does PNG so far

Framework MaxGUI.Win32MaxGUIEx
Import BRL.pngloader
Import BRL.jpgloader
Import BRL.tgaloader
Import BRL.bmploader
Import BRL.Event
Import BRL.EventQueue
Import "multifilerequestor.bmx"

Global window:TGadget =CreateWindow("Image Strip Maker",100,40,700,500)

Global panel:TGadget = CreatePanel(10,10,690,150,window,0,"")
Global loadbtn:TGadget  = CreateButton("Load",10,10,100,30,panel,BUTTON_PUSH)
Global filelabel:TGadget  = CreateLabel(".",130,10,520,30,panel,LABEL_LEFT|LABEL_SUNKENFRAME)
CreateLabel("Save File: ",10,50,100,30,panel,LABEL_RIGHT)
Global file2label:TGadget = CreateLabel(".",130,50,520,30,panel,LABEL_LEFT|LABEL_SUNKENFRAME)

Global combobox:TGadget = CreateComboBox(10,90,200,25,panel)
AddGadgetItem combobox, "Make Image Strip", True
AddGadgetItem combobox, "Make Image Sheet", False



Global runbtn:TGadget  = CreateButton("RUN",10,120,100,30,panel,BUTTON_PUSH)

Global imagepanel:TGadget = CreatePanel(10,170,690,200,window,PANEL_BORDER,"")

Global files:String[] = New String[101]
Global currDir:String = ""
Global outfile:String = ""
Global ext:String=""

Global imageFinal:TPixmap

SetGadgetLayout( panel,EDGE_ALIGNED,0,EDGE_ALIGNED,0 )
SetGadgetLayout( imagepanel,EDGE_ALIGNED,0,EDGE_ALIGNED,0 )

Repeat
	Select WaitEvent()
		Case EVENT_WINDOWCLOSE, EVENT_APPTERMINATE
			End
		Case EVENT_GADGETACTION
			''
			DoGadget(TGadget(EventSource()), EventData() )
	EndSelect
	
Forever

End

Function DoGadget(tg:TGadget, edata:Int)
	If tg=loadbtn
		files = RequestMultiFile( "Open Files", "jpg,png,bmp,tga")
		If (files.length&gt;2)
			Print
			Print "dir:"+files[0]
			currDir = files[0]
			SetGadgetText( filelabel, (StripDir(files[1])+"..."+StripDir(files[files.length-1])))
			
			outfile = files[1]
			ext:String = "."+ExtractExt(outfile).toLower()
			outfile = StripExt(outfile)
			For Local i:Int = outfile.length-1 To 1 Step -1
				If outfile[i] &gt;=48 And outfile[i] &lt;=57
					outfile = outfile[..i]
				Else
					Exit
				EndIf
			Next
			outfile :+ ext
			SetGadgetText( file2label, outfile)
			outfile = currDir+outfile
		EndIf
	EndIf
	If tg=runbtn
		Local action:Int = SelectedGadgetItem(combobox)
		If files.length&gt;2 And outfile&lt;&gt;""
			Local err:Int

			If action = 0 Then  Print "image strip" ; err = MakeImageStrip()
			If action = 1 Then Print "image sheet" ; err = MakeImageSheet()
			If err =-1 Then Notify("First Image load failed.")
			If err =-2 Then Notify("Multi Image load failed.")
			If Not err
				Print "done."
				SetGadgetPixmap( imagepanel, imageFinal, PANELPIXMAP_FIT )
				
				If ext = ".png"
					SavePixmapPNG(imageFinal,outfile,7)
				EndIf
				Print"Saved."
			EndIf
		EndIf
	EndIf
EndFunction

Function MakeImageStrip:Int()
	Local firstpix:TPixmap = LoadPixmap(currDir+files[1])
	
	If Not firstpix Then Return -1
	
	Local w:Int = firstpix.width
	Local h:Int = firstpix.height
	Local p:Int = firstpix.pitch
	Local total:Int = files.length-1
	Print "total:"+total+" width:"+w+" p:"+p
	Local newpix:TPixmap = CreatePixmap(w*total,h,PF_RGBA8888)
	Local srcpix:TPixmap
	Local destptr:Byte Ptr, srcptr:Byte Ptr
	
	For Local j:Int = 0 To total-1
		srcpix = LoadPixmap(currDir+files[j+1])
		Print currDir+files[j+1]
		If Not srcpix Then Return -2
		srcpix = ConvertPixmap( srcpix,PF_RGBA8888)
		srcptr = srcpix.pixelptr(0,0)
		
		For Local y:Int = 0 To h-1
			destptr = newpix.pixelptr(w*j,y)
			srcptr = srcpix.pixelptr(0,y) 
			'MemCopy (destptr, srcptr, w*3)
			For Local x:Int = 0 To w*4 ''use width since we're reading from source, pasting to dest
				destptr[x] = srcptr[x]
			Next
			
			
		Next
	Next
	
	imageFinal = newpix

EndFunction

Function MakeImageSheet:Int()
	Local firstpix:TPixmap = LoadPixmap(currDir+files[1])
	
	If Not firstpix Then Return -1
	
	Local w:Int = firstpix.width
	Local h:Int = firstpix.height
	Local p:Int = firstpix.pitch
	Local total:Int = files.length-1
	
	Local sheetsize:Int = Ceil(Sqr(Float(total) ))
	
	Print "total:"+total+" width:"+w+" p:"+p+" sqsize:"+sheetsize
	Local newpix:TPixmap = CreatePixmap(sheetsize*w,sheetsize*h,PF_RGBA8888)
	Local srcpix:TPixmap
	Local destptr:Byte Ptr, srcptr:Byte Ptr
	
	Local offx:Int=0, offy:Int=0
	
	For Local j:Int = 0 To total-1
		srcpix = LoadPixmap(currDir+files[j+1])
		Print currDir+files[j+1]
		If Not srcpix Then Return -2
		srcpix = ConvertPixmap( srcpix,PF_RGBA8888)
		srcptr = srcpix.pixelptr(0,0)
		
		For Local y:Int = 0 To h-1
			destptr = newpix.pixelptr(w*offx,y+h*offy)
			srcptr = srcpix.pixelptr(0,y) 
			'MemCopy (destptr, srcptr, w*3)
			For Local x:Int = 0 To w*4 ''use width since we're reading from source, pasting to dest
				destptr[x] = srcptr[x]
			Next
			
			
		Next
		
		offx=offx+1
		If offx&gt;sheetsize-1
			offy=offy+1
			offx=0
		EndIf
	Next
	
	imageFinal = newpix

EndFunction</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamRedwoods</td><td align="right"><font class=tiny>2011</font></td></tr></table></td></tr><tr ><td class="posttext"> Forogt the MULTIFILEREQUESTOR.bmx<br><br>Not sure if that's available on MacOS, Linux.<br><br>multifilerequestor.bmx:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
?Win32
Import PUB.Win32

Private

Const MAX_BUFFER_SIZE:Int = 8192

Const OFN_ALLOWMULTISELECT:Int = 512
Const OFN_CREATEPROMPT:Int = $2000
Const OFN_ENABLEHOOK:Int = 32
Const OFN_ENABLESIZING:Int = $800000
Const OFN_ENABLETEMPLATE:Int = 64
Const OFN_ENABLETEMPLATEHANDLE:Int = 128
Const OFN_EXPLORER:Int = $80000
Const OFN_EXTENSIONDIFFERENT:Int = $400
Const OFN_FILEMUSTEXIST:Int = $1000
Const OFN_HIDEREADONLY:Int = 4
Const OFN_LONGNAMES:Int = $200000
Const OFN_NOCHANGEDIR:Int = 8
Const OFN_NODEREFERENCELINKS:Int = $100000
Const OFN_NOLONGNAMES:Int = $40000
Const OFN_NONETWORKBUTTON:Int = $20000
Const OFN_NOREADONLYRETURN:Int = $8000
Const OFN_NOTESTFILECREATE:Int = $10000
Const OFN_NOVALIDATE:Int = 256
Const OFN_OVERWRITEPROMPT:Int = 2
Const OFN_PATHMUSTEXIST:Int = $800
Const OFN_READONLY:Int = 1
Const OFN_SHAREAWARE:Int = $4000
Const OFN_SHOWHELP:Int = 16
Const OFN_SHAREFALLTHROUGH:Int = 2
Const OFN_SHARENOWARN:Int = 1
Const OFN_SHAREWARN:Int = 0

Type TOpenFileNameA
	Field lStructSize:Int
	Field hwndOwner:Int
	Field hInstance:Int
	Field lpstrFilter:Byte Ptr
	Field lpstrCustomFilter:Int
	Field nMaxCustFilter:Int
	Field nFilterIndex:Int
	Field lpstrFile:Byte Ptr
	Field nMaxFile:Int
	Field lpstrFileTitle:Byte Ptr
	Field nMaxFileTitle:Int
	Field lpstrInitialDir:Byte Ptr
	Field lpstrTitle:Byte Ptr
	Field Flags:Int
	Field nFileOffset:Short
	Field nFileExtension:Short
	Field lpstrDefExt:Byte Ptr
	Field lCustData:Int
	Field lpfnHook:Byte Ptr
	Field lpTemplateName:Byte Ptr
EndType

Extern "Win32"
	Function GetOpenFileName:Int( of:Byte Ptr) = "GetOpenFileNameA@4"	
EndExtern

Extern "C"
	Function memcpy( dst:Byte Ptr, src$z, sz:Int)
EndExtern

Public
?

Function RequestMultiFile:String[]( text:String, exts:String = Null, path:String = Null)
	?MacOS | Linux	
	Local res:String = RequestFile( test, exts, False, path)
	If res.Length &lt;= 0 Then Return Null
	Return [res]
	?
	?Win32	
	Global hwndFocus:Int
	
	' prepare filename / path (ripped from BRL's RequestFile())
	Local file:String, dir:String
	path = path.Replace( "/","\" )	
	Local i:Int = path.FindLast( "\" )
	If i &lt;&gt; -1 Then
		dir = path[..i]
		file = path[i+1..]
	Else
		file = path
	EndIf
	' calculate default index of extension in extension list from path name
	Local ext:String, defext:Int,p:Int,q:Int
	p = path.Find(".")
	If (p&gt;-1) Then
		ext = "," + path[p+1..].toLower() + ","
		Local exs:String = exts.toLower()
		exs = exs.Replace(":",":,")
		exs = exs.Replace(";",",;")
		p = exs.find(ext)
		If p &gt;-1 Then
			Local q:Int = -1
			defext = 1
			While True
				q = exs.find(";",q+1)
				If q &gt; p Then Exit
				If q = -1 Then 
					defext = 0
					Exit
				EndIf
				defext :+ 1
			Wend
		EndIf
	EndIf
	If exts Then
		If exts.Find(":") = -1 Then
			exts = "Files~0*." + exts
		Else
			exts = exts.Replace(":","~0*.")
		EndIf
		exts = exts.Replace(";","~0")
		exts = exts.Replace(",",";*.") + "~0"
	EndIf
	
	' allocate cstrings
	Local textp:Byte Ptr = text.ToCString()
	Local extsp:Byte Ptr = exts.ToCString()
	Local dirp:Byte Ptr
	If dir.Length &gt; 0 Then dirp = dir.ToCString()
	
	' prepare file buffer
	Local buf:Byte[MAX_BUFFER_SIZE]
	memcpy( buf, file, file.Length)
	
	' initialize dialog options
	Local of:TOpenFileNameA = New TOpenFileNameA
	of.lStructSize = SizeOf(TOpenFileNameA)
	of.hwndOwner = GetActiveWindow()
	of.lpstrTitle = textp
	of.lpstrFilter = extsp
	of.nFilterIndex = defext
	of.lpstrFile = buf
	of.lpstrInitialDir = dirp
	of.nMaxFile = buf.Length
	of.Flags = OFN_HIDEREADONLY | OFN_NOCHANGEDIR | OFN_PATHMUSTEXIST | OFN_FILEMUSTEXIST | OFN_ALLOWMULTISELECT | OFN_EXPLORER ' | OFN_LONGNAMES
	
	' display dialog
	hwndFocus = GetFocus()
	Local n:Int = GetOpenFileName( of)
	SetFocus( hwndFocus)
	
	' free cstrings
	MemFree textp
	MemFree extsp
	If dirp Then MemFree dirp

	' failure ?
	If n &lt;= 0 Then Return Null
	
	' count the number of files
	Local s:Byte Ptr = buf
	Local count:Int = 0
	While s[0] &lt;&gt; 0
		If s[1] = 0 Then
			count :+ 1
			s :+ 2
			If s[0] = 0 Then Exit
		EndIf
		s :+ 1
	Wend
	If count &lt;= 0 Then Return Null
	
	' extract filenames into String array
	If count = 1 Then 
		'MARK: im following RequestFile() convention here, and returing "\" path seperators #1
		'Return [ String.FromCString( buf).Replace( "\", "/") ]
		Return [ String.FromCString( buf) ]
	Else
		Local result:String[] = New String[count]	
		s = buf
		For Local i:Int = 0 Until count
			result[i] = String.FromCString( s)
			s :+ result[i].Length + 1
		Next	
		'MARK: im following RequestFile() convention here, and returing "\" path seperators #2
		'result[0] = result[0].Replace( "\", "/") + "/"
		result[0] :+ "\"
		Return result
	EndIf
EndFunction
</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matty</td><td align="right"><font class=tiny>2011</font></td></tr></table></td></tr><tr ><td class="posttext"> You did read this right at the top of the code archives didn't you:<br><br>This code has been declared by its author to be Public Domain code.<br><br>Not sure how that fits with:<br><br><div class="quote"> You may not sell this program or code for profit, but you may use the files it makes for commercial uses. Makes sense. <br></div> <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
