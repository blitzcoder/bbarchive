<!DOCTYPE html><html lang="en" ><head ><title >Use VLC DLL to play video</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Simple VLC-based video player, language=bmx, category=Graphics'><meta name='author' content='BlitzSupport'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/Use VLC DLL to play video</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="3230.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Use VLC DLL to play video by BlitzSupport</td><td align="right">2015 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Basic proof-of-concept VLC-based player.<br><br>This very simple example is Windows-specific, but the same methods of LibVLC usage should apply on other platforms.<br><br><b>Please note that you need to place this code in a folder alongside copies of libvlc.dll and libvlccore.dll, <u>plus the /plugins folder and contents</u>, from a <u>32-bit</u> VLC installation (or build yourself from sources if so inclined). This applies even on 64-bit Windows.</b><br><br>The installation should look something like this:<br><br><img src="http://www.hi-toro.com/blitz/images/libvlcfolder.png"><br><br>Known problems/limitations:<br><br>1) LibVLC writes out any debug/error messages to console; these would not appear in a GUI-built application;<br><br>2) Only seems to work with non-Unicode filenames, not sure if this is down to :String or the use of String.ToCString, though String.ToWString didn't work.<br><br>3) Licensing: warning -- I am not a lawyer, so this note is only my understanding written out:<br><br><i>LibVLC is LGPL-licensed, and so can be distributed in DLL form alongside your program, without the need to release your program's own source code. However, the codec-specific plugins are a licensing minefield, so you should probably research which ones are also LGPL, encode in one of these formats, and distribute only the specific plugins you need.</i> [EDIT: Note that VLC itself, the player application, is licensed under the GPL.)<br><br>Tested with 32-bit LibVLC DLLs, version 2.2.0, copied from VLC Media Player 2.2.1, on Windows 7 64-bit only. </td></tr><tr ><td class="cell"><pre class="code">' MaxGUI/Win32-specific example!

' See description -- some setup required!

' Resources:

' https://wiki.videolan.org/LibVLC_Tutorial/
' https://www.videolan.org/developers/vlc/doc/doxygen/html/modules.html
' Search engine + VLC function names!

SuperStrict

Import maxgui.drivers

' LibVLC functions...

Global libvlc_new:Byte Ptr							(argc:Int, argv:Byte Ptr)			"win32"
Global libvlc_release:Byte Ptr						(instance:Byte Ptr)					"win32"	' Void return

Global libvlc_media_new_path:Byte Ptr				(instance:Byte Ptr, path:Byte Ptr)	"win32"
Global libvlc_media_release:Int						(media:Byte Ptr)					"win32"	' Void return

Global libvlc_media_player_new_from_media:Byte Ptr	(media:Byte Ptr)					"win32"
Global libvlc_media_player_set_hwnd:Int				(player:Byte Ptr, hwnd:Int)			"win32"	' Void return
Global libvlc_media_player_play:Int					(player:Byte Ptr)					"win32"
Global libvlc_media_player_stop:Int					(player:Byte Ptr)					"win32"	' Void return
Global libvlc_media_player_release:Int				(player:Byte Ptr)					"win32"	' Void return

Local file:String = RequestFile ("Select a media file...")

If Not file Then End

' Assign functions from DLL...

Global libvlc:Int = LoadLibraryA ("libvlc.dll")

If libvlc

	libvlc_new							= GetProcAddress (libvlc, "libvlc_new")
	libvlc_release						= GetProcAddress (libvlc, "libvlc_release")
	
	libvlc_media_new_path				= GetProcAddress (libvlc, "libvlc_media_new_path")
	libvlc_media_player_new_from_media	= GetProcAddress (libvlc, "libvlc_media_player_new_from_media")
	libvlc_media_release				= GetProcAddress (libvlc, "libvlc_media_release")
	
	libvlc_media_player_set_hwnd		= GetProcAddress (libvlc, "libvlc_media_player_set_hwnd")
	libvlc_media_player_play			= GetProcAddress (libvlc, "libvlc_media_player_play")
	libvlc_media_player_stop			= GetProcAddress (libvlc, "libvlc_media_player_stop")
	libvlc_media_player_release			= GetProcAddress (libvlc, "libvlc_media_player_release")
	
	Local instance:Byte Ptr
	Local media:Byte Ptr
	Local player:Byte Ptr
	
	' Create VLC instance...
	
	instance = libvlc_new (0, Null)
	
	If instance
	
		' Load media from filename...
		
		media = libvlc_media_new_path (instance, file.ToCString ())
		
		If media
		
			' Create VLC player...
			
			player = libvlc_media_player_new_from_media (media)
			
			If player
			
				' Blitz window...
			
				Local window:TGadget = CreateWindow ("Media player", 320, 200, 640, 480)
				
				If window
				
					' Inner client area of window...
					
					Local hwnd:Int = QueryGadget (window, QUERY_HWND_CLIENT)
					
					' Tell VLC to render into client area...
					
					libvlc_media_player_set_hwnd player, hwnd
					
					' Play!
					
					libvlc_media_player_play player
	
					Repeat
						
						Select WaitEvent ()
							
							Case EVENT_WINDOWCLOSE
								Exit

						End Select
						
					Forever
					
					' Stop and release VLC player...
					
					If player
						libvlc_media_player_stop	player
						libvlc_media_player_release	player
					EndIf
					
				EndIf
			
			EndIf
		
			' Release media...
			
			libvlc_media_release media

		EndIf
		
		' Release VLC instance...
		
		libvlc_release instance
		
	EndIf
	
EndIf</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>2015</font></td></tr></table></td></tr><tr ><td class="posttext"> Minor tweak to play from URL; note that same limitations will apply as per my note above on strings (may need to un-hex URLs encoded with %20 and so on)...<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

' MaxGUI/Win32-specific example!

' See description -- some setup required!

' Resources:

' <a href="https://wiki.videolan.org/LibVLC_Tutorial/" target="_blank">https://wiki.videolan.org/LibVLC_Tutorial/</a>
' <a href="https://www.videolan.org/developers/vlc/doc/doxygen/html/modules.html" target="_blank">https://www.videolan.org/developers/vlc/doc/doxygen/html/modules.html</a>
' Search engine + VLC function names!

SuperStrict

Import maxgui.drivers

' LibVLC functions...

Global libvlc_new:Byte Ptr							(argc:Int, argv:Byte Ptr)			"win32"
Global libvlc_release:Byte Ptr						(instance:Byte Ptr)					"win32"	' Void return

Global libvlc_media_new_path:Byte Ptr				(instance:Byte Ptr, path:Byte Ptr)	"win32"
Global libvlc_media_new_location:Byte Ptr			(instance:Byte Ptr, path:Byte Ptr)	"win32"
Global libvlc_media_release:Int						(media:Byte Ptr)					"win32"	' Void return

Global libvlc_media_player_new_from_media:Byte Ptr	(media:Byte Ptr)					"win32"
Global libvlc_media_player_set_hwnd:Int				(player:Byte Ptr, hwnd:Int)			"win32"	' Void return
Global libvlc_media_player_play:Int					(player:Byte Ptr)					"win32"
Global libvlc_media_player_stop:Int					(player:Byte Ptr)					"win32"	' Void return
Global libvlc_media_player_release:Int				(player:Byte Ptr)					"win32"	' Void return

Local file:String = "http://www.quirksmode.org/html5/videos/big_buck_bunny.mp4"

' Assign functions from DLL...

Global libvlc:Int = LoadLibraryA ("libvlc.dll")

If libvlc

	libvlc_new							= GetProcAddress (libvlc, "libvlc_new")
	libvlc_release						= GetProcAddress (libvlc, "libvlc_release")
	
	libvlc_media_new_path				= GetProcAddress (libvlc, "libvlc_media_new_path")
	libvlc_media_new_location			= GetProcAddress (libvlc, "libvlc_media_new_location")
	libvlc_media_player_new_from_media	= GetProcAddress (libvlc, "libvlc_media_player_new_from_media")
	libvlc_media_release				= GetProcAddress (libvlc, "libvlc_media_release")
	
	libvlc_media_player_set_hwnd		= GetProcAddress (libvlc, "libvlc_media_player_set_hwnd")
	libvlc_media_player_play			= GetProcAddress (libvlc, "libvlc_media_player_play")
	libvlc_media_player_stop			= GetProcAddress (libvlc, "libvlc_media_player_stop")
	libvlc_media_player_release			= GetProcAddress (libvlc, "libvlc_media_player_release")
	
	Local instance:Byte Ptr
	Local media:Byte Ptr
	Local player:Byte Ptr
	
	' Create VLC instance...
	
	instance = libvlc_new (0, Null)
	
	If instance
	
		' Load media from filename...
		
		media = libvlc_media_new_location (instance, file.ToCString ())
		
		If media
		
			' Create VLC player...
			
			player = libvlc_media_player_new_from_media (media)
			
			If player
			
				' Blitz window...
			
				Local window:TGadget = CreateWindow ("Media player", 320, 200, 640, 480)
				
				If window
				
					' Inner client area of window...
					
					Local hwnd:Int = QueryGadget (window, QUERY_HWND_CLIENT)
					
					' Tell VLC to render into client area...
					
					libvlc_media_player_set_hwnd player, hwnd
					
					' Play!
					
					libvlc_media_player_play player
	
					Repeat
						
						Select WaitEvent ()
							
							Case EVENT_WINDOWCLOSE
								Exit

						End Select
						
					Forever
					
					' Stop and release VLC player...
					
					If player
						libvlc_media_player_stop	player
						libvlc_media_player_release	player
					EndIf
					
				EndIf
			
			EndIf
		
			' Release media...
			
			libvlc_media_release media

		EndIf
		
		' Release VLC instance...
		
		libvlc_release instance
		
	EndIf
	
EndIf
</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >atv</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> James can you send me an email, i'm trying to contact you in regards to this. Thank you very much. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >wmaass</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> Hmm, that works nicely for Youtube videos as well. Cool... <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> Can we please get a Blitz3D version of this? I would very much like to use 1080p HD in my demos! :)<br><br>Thanks a lot! =)<br><br>~GF <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> Here you go Guy :)  Been many years since i used Blitz3D, surprisingly i remembered most of it hehe<br><br><a href="/Community/posts.php?topic=105999" target="_blank">LIBVLC + video frame capture userlib</a> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> Thanks alot, grable! :)<br><br>~GF <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> How would I apply this to a Texture? <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> How would I apply this to a Texture? <br></div>Since VLC draws on top of whatever blitz does, you cant do it directly. But its possible using another window and copying from that window to a bitmap.<br><br>I was bored, so i did just that ;)<br>This requires a custom userlib though, and so as to not clutter up this thread im posting a link: <a href="https://drive.google.com/open?id=0BzVLNZSckvfhY3JPeU5BdzhPbFE" target="_blank">hwndbuffer.7z</a><br><br>EDIT: Fixed crash and rendering bugs. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> Brother. It's still crashing... :/<br><br>With userlib not found for the function, "libvlc_new ( )". Even though I SPECIFICALLY put the DLL in the same folder as the code, and added the up-to-date libvlc.bb to the same folder as the code. and added the DECLs to my DECLs folder :(<br><br>~GF <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> Hmm thats weird, it works here..<br><br><strike>What version of Blitz3D?<br>Which function does it fail on?</strike><br>EDIT: Dont forget to do as the top post says, copy both vlc dlls AND the plugins folder.<br><br>Lets continue this in another thread <a href="/Community/posts.php?topic=105999" target="_blank">here</a>. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> For completeness heres how to get the current frame as a TPixmap or TImage in BlitzMax using callbacks.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' MaxGUI/Win32-specific example!

' See description -- some setup required!

' Resources:

' <a href="https://wiki.videolan.org/LibVLC_Tutorial/" target="_blank">https://wiki.videolan.org/LibVLC_Tutorial/</a>
' <a href="https://www.videolan.org/developers/vlc/doc/doxygen/html/modules.html" target="_blank">https://www.videolan.org/developers/vlc/doc/doxygen/html/modules.html</a>
' Search engine + VLC function names!

SuperStrict

Import maxgui.drivers

' LibVLC functions...

Global libvlc_new:Byte Ptr							(argc:Int, argv:Byte Ptr Ptr)			"win32"
Global libvlc_release:Byte Ptr						(instance:Byte Ptr)					"win32"	' Void return

Global libvlc_media_new_path:Byte Ptr				(instance:Byte Ptr, path$z)	"win32"
Global libvlc_media_new_location:Byte Ptr		(instance:Byte Ptr, path$z)	"win32"
Global libvlc_media_release:Int						(media:Byte Ptr)					"win32"	' Void return

Global libvlc_media_parse:Int( media:Byte Ptr) "win32"

Global libvlc_media_player_new_from_media:Byte Ptr	(media:Byte Ptr)					"win32"
Global libvlc_media_player_set_hwnd:Int				(player:Byte Ptr, hwnd:Int)			"win32"	' Void return

Global libvlc_media_player_play:Int					(player:Byte Ptr)					"win32"
Global libvlc_media_player_stop:Int					(player:Byte Ptr)					"win32"	' Void return
Global libvlc_media_player_release:Int				(player:Byte Ptr)					"win32"	' Void return

Global libvlc_video_get_size:Int( player:Byte Ptr, num:Int, width:Int Var, height:Int Var) "win32"

Global libvlc_video_set_callbacks:Int( player:Byte Ptr, lock:Byte Ptr, unlock:Byte Ptr, display:Byte Ptr, ctx:Byte Ptr) "win32"
Global libvlc_video_set_format:Int( player:Byte Ptr, chroma$z, width:Int, height:Int, pitch:Int) "win32"

Global libvlc_log_set( instance:Byte Ptr, callback:Byte Ptr) "win32"
Global libvlc_log_unset( instance:Byte Ptr) "win32"


'Local file:String = "c:\dump\Creating a Doom-style 3D engine in C-HQYsFshbkYw.mp4"
'Local file:String = "e:\torrents\[HorribleSubs] World Trigger - 70 [1080p].mkv"
Local file:String = "http://www.quirksmode.org/html5/videos/big_buck_bunny.mp4"
'Local file:String = RequestFile ("Select a media file...")
If Not file Then Throw "no file!"

' Assign functions from DLL...

Global libvlc:Int = LoadLibraryA ("libvlc.dll")

If libvlc

	libvlc_new							= GetProcAddress (libvlc, "libvlc_new")
	libvlc_release						= GetProcAddress (libvlc, "libvlc_release")
	
	libvlc_media_new_path				= GetProcAddress (libvlc, "libvlc_media_new_path")
	libvlc_media_new_location			= GetProcAddress (libvlc, "libvlc_media_new_location")	
	libvlc_media_release				= GetProcAddress (libvlc, "libvlc_media_release")
	libvlc_media_parse					= GetProcAddress (libvlc, "libvlc_media_parse")

	libvlc_media_player_new_from_media	= GetProcAddress (libvlc, "libvlc_media_player_new_from_media")
	
	libvlc_media_player_set_hwnd		= GetProcAddress (libvlc, "libvlc_media_player_set_hwnd")
	
	libvlc_media_player_play			= GetProcAddress (libvlc, "libvlc_media_player_play")
	libvlc_media_player_stop			= GetProcAddress (libvlc, "libvlc_media_player_stop")
	libvlc_media_player_release			= GetProcAddress (libvlc, "libvlc_media_player_release")
	
	libvlc_video_get_size = GetProcAddress( libvlc, "libvlc_video_get_size")
	
	libvlc_video_set_callbacks = GetProcAddress( libvlc, "libvlc_video_set_callbacks")
	libvlc_video_set_format = GetProcAddress( libvlc, "libvlc_video_set_format")
	
	libvlc_log_unset = GetProcAddress( libvlc, "libvlc_log_unset")
	libvlc_log_set = GetProcAddress( libvlc, "libvlc_log_set")
	
	
	Local instance:Byte Ptr
	Local media:Byte Ptr
	Local player:Byte Ptr
	
	' Create VLC instance...
	
	Local args:Byte Ptr[] = [ "--quiet".ToCString() ]
	instance = libvlc_new( args.Length, args)
	For Local p:Byte Ptr = EachIn args
		MemFree p
	Next
	
	If instance
	
		' Load media from filename...
		
		If file.Find("://") &lt;&gt; -1 Then
			media = libvlc_media_new_location (instance, file)
		Else
			media = libvlc_media_new_path (instance, file)
		EndIf
		
		If media
		
			' Create VLC player...
			
			player = libvlc_media_player_new_from_media (media)
			
			If player
				Const PLAYER_WIDTH:Int = 800
				Const PLAYER_HEIGHT:Int = 600
							
				Local window:TGadget = CreateWindow ("Media player", 100, 200, PLAYER_WIDTH, PLAYER_HEIGHT, Null, WINDOW_TITLEBAR | WINDOW_CLIENTCOORDS)				
				Global canvas:TGadget = CreateCanvas( 0,0, ClientWidth(window),ClientHeight(window), window)
				
				If file.Find("://") = -1 Then
					' need to parse the video to get its stats, like the size
					libvlc_media_parse(media)
				Else
					' if its a remote video, parse wont work. so we play it a few frames and restart it.
					libvlc_media_player_set_hwnd( player, window.Query(QUERY_HWND_CLIENT))
					libvlc_media_player_play(player)
					Delay 200
					libvlc_media_player_stop(player)
				EndIf
				Local w:Int,h:Int
				libvlc_video_get_size( player, 0, w,h)
				Print "size=" + w + "x" + h
				
				' aspect correct resize
				If w &gt; 0 And h &gt; 0 Then
					If w &lt;&gt; PLAYER_WIDTH Or h &lt;&gt; PLAYER_HEIGHT Then
						Local ratio:Float = Min( PLAYER_WIDTH / Float(w), PLAYER_HEIGHT / Float(h))
						w :* ratio
						h :* ratio
					EndIf
				Else
					' wasnt able to get the video size, use player size instead
					w = PLAYER_WIDTH
					h = PLAYER_HEIGHT
				EndIf
				Print "newsize=" + w + "," + h
								
				' Blitz window...
				
				' destination pixmap/image
				' only reason to use TImage here is for fancy drawing, but it can be a bit slower than using a TPixmap alone (especially if MIMPAPPED)
				Global pixmap:TPixmap = CreatePixmap( w,h, PF_BGRA8888)
				Global image:TImage = LoadImage( pixmap, FILTEREDIMAGE | DYNAMICIMAGE)
				SetImageHandle image, w/2,h/2

				' setup own rendering (RV32 = BGRA8888)
				libvlc_video_set_format( player, "RV32", w,h, w*4)
				libvlc_video_set_callbacks( player, lock_cb, Null, display_cb, Null)
					
				Function lock_cb:Byte Ptr( ctx:Byte Ptr, planes:Byte Ptr Ptr)
					' only one plane since we are doing RGB
					'planes[0] = pixmap.pixels
					planes[0] = LockImage(image).pixels
				EndFunction

				Function display_cb:Int( ctx:Byte Ptr, pixels:Byte Ptr)
					RedrawGadget canvas
				EndFunction

				
				If window
				
					' Inner client area of window...

					' disabled since we are doing the rendering our selves					
'					Local hwnd:Int = QueryGadget (window, QUERY_HWND_CLIENT)
'					libvlc_media_player_set_hwnd player, hwnd
					
					' Play!
					
					libvlc_media_player_play player
	
					Repeat
						
						Select WaitEvent ()
							Case EVENT_GADGETPAINT
								SetGraphics CanvasGraphics(canvas)
								Global angle:Float
								SetRotation angle
								If angle &gt;= 360 Then angle = 0 Else angle :+ 1
								SetColor 255,255,255
								'DrawPixmap pixmap, 0,0
								DrawImage image, ClientWidth(canvas) / 2, ClientHeight(canvas) / 2
								Flip 1
								Cls
								
							Case EVENT_WINDOWCLOSE
								Exit

						End Select
						
					Forever
					
					' Stop and release VLC player...
					
					If player
						libvlc_media_player_stop	player
						libvlc_media_player_release	player
					EndIf
					
				EndIf
			
			EndIf
		
			' Release media...
			
			libvlc_media_release media

		EndIf
		
		' Release VLC instance...
		
		libvlc_release instance
		
	EndIf
	
EndIf
</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> Nice one, grable! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> I'm getting quite a lot of crashes when streaming video.<br>It keeps throwing me to random parts of the code each time too, so it's real hard to track down where the problem comes from...<br><br>Is it possible to make libvlc_video_set_callbacks accept a Method instead of a Function for lock_cb? <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> It's not possible, because a Function in BlitzMax is like a "static" function from C++, they are not associated with an object. Methods operate on an object, so you have direct access to the fields of the object -- you can use the Self keyword, for example, something that you can't do with Functions.<br><br>I think you should be able to use Functions contained within types, like this:<br><pre class=code>Type VLCCallbacks

	Function lock:Byte Ptr( ctx:Byte Ptr, planes:Byte Ptr Ptr )

		'...

	End Function

End Type

libvlc_video_set_callbacks( player, VLCCallbacks.lock, ... )</pre>...if it helps with organisation.<br><br>EDIT: If you really need to use a Method, you can send the BlitzMax object as the "ctx" (context) parameter, which is a private data value that you use for whatever you want.<br><br><pre class=code>Type VLCCallbacks

	Function lock:Byte Ptr( ctx:Byte Ptr, planes:Byte Ptr Ptr )

		myObject:MyClass = MyClass( ctx[ 0 ] ) 'Cast the pointer to an object.
		Assert( myObject &lt;&gt; Null ) 'Good programming practice. When built on Release, this line does nothing.

		myObject.bla( planes ) 'Call the method of the object.

	End Function

End Type

Type MyClass

	Method bla( planes:Byte Ptr Ptr )
		'...
	End Method

End Type</pre>I don't know if the casting is done like that, I can't test it at the moment. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> Casting from anything not an Object wont work im afraid, but you can define the argument as an Object and it should work.<br>And since the context is the first argument you can even use the function pointer of the method directly.<br><pre class=code>

Import BRL.Reflection

Type TTest
	Method Lock:Byte Ptr( planes:Byte Ptr Ptr)
	EndMethod
	
	Method UnLock:Byte Ptr( picture:Byte Ptr, planes:Byte Ptr Ptr)
	EndMethod
EndType

Local o:Test = New TTtest
Local t:TTypeId = TTypeId.ForObject(o)
Local lock:Byte Ptr = bbRefMethodPtr( o, t.FindMethod("Lock")._index)
Local unlock:Byte Ptr = bbRefMethodPtr( o, t.FindMethod("UnLock")._index)

Extern "C"
	' from BRL.Reflection to get method pointer
	Function bbRefMethodPtr:Byte Ptr( obj:Object, index:Int)
EndExtern

' needed to pass the object pointer through unchanged
Global libvlc_video_set_callbacks_object( player:Byte Ptr, lock:Byte Ptr, unlock:Byte Ptr, display:Byte Ptr, ctx:Object) = Byte Ptr libvlc_video_set_callbacks

libvlc_video_set_callbacks_object( player, lock, unlock, Null, o)
</pre>libvlc wont know about object ownership though, so you have to make sure the object is kept around in some other place (or bump its refcount)<br><br>Note that im using reflection here, but you could just as well use pointers to get at the class and the method yourself if you know its index beforehand. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hezkore</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> I'm having some trouble understanding all this heh.<br>Pointers and reflections isn't something I'm very familiar with.<br><br>Basically what I'm trying to do is create a Type of this, one that does all the work for you.<br>So in the end you'd just have to call something like LoadVideo(file)<br><br>Here's what I've got so far.<br>Notice that I'm using a global image atm. since I couldn't set a Method as the callback.<br><br>easyvlc.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

SuperStrict

Import maxgui.drivers
Import brl.max2d

Function LoadVideo:TEasyVLC(file:String)
	Local nEVLC:TEasyVLC = New TEasyVLC
	nEVLC.Play(file)
	Return nEVLC
EndFunction

Type TEasyVLC
	Global libvlc:Int
	
	' LibVLC functions...
	Global libvlc_new:Byte Ptr (argc:Int, argv:Byte Ptr Ptr) "win32"
	Global libvlc_release:Byte Ptr (instance:Byte Ptr) "win32"	' Void return
	
	Global libvlc_media_new_path:Byte Ptr (instance:Byte Ptr, path$z) "win32"
	Global libvlc_media_new_location:Byte Ptr (instance:Byte Ptr, path$z) "win32"
	Global libvlc_media_release:Int (media:Byte Ptr) "win32"	' Void return
	
	Global libvlc_media_parse:Int(media:Byte Ptr) "win32"
	
	Global libvlc_media_player_new_from_media:Byte Ptr (media:Byte Ptr) "win32"
	Global libvlc_media_player_set_hwnd:Int (player:Byte Ptr, hwnd:Int) "win32"	' Void return
	
	Global libvlc_media_player_play:Int (player:Byte Ptr) "win32"
	Global libvlc_media_player_stop:Int (player:Byte Ptr) "win32"	' Void return
	Global libvlc_media_player_release:Int (player:Byte Ptr) "win32"	' Void return
	
	Global libvlc_video_get_size:Int(player:Byte Ptr, num:Int, width:Int Var, height:Int Var) "win32"
	
	Global libvlc_video_set_callbacks:Int(player:Byte Ptr, lock:Byte Ptr, unlock:Byte Ptr, display:Byte Ptr, ctx:Byte Ptr) "win32"
	Global libvlc_video_set_format:Int(player:Byte Ptr, chroma$z, width:Int, height:Int, pitch:Int) "win32"
	
	Global libvlc_log_set(instance:Byte Ptr, callback:Byte Ptr) "win32"
	Global libvlc_log_unset(instance:Byte Ptr) "win32"
	
	Field instance:Byte Ptr
	Field media:Byte Ptr
	Field player:Byte Ptr
	
	Global Image:TImage
	
	Field width:Int
	Field height:Int
		
	Method New()
		If Not TEasyVLC.libvlc Then
			libvlc = LoadLibraryA ("libvlc.dll")
			
			If Not libvlc Then
				Print "Unable to find libvlc.dll"
			Else
				libvlc_new = GetProcAddress (libvlc, "libvlc_new")
				libvlc_release						= GetProcAddress (libvlc, "libvlc_release")
				
				libvlc_media_new_path				= GetProcAddress (libvlc, "libvlc_media_new_path")
				libvlc_media_new_location			= GetProcAddress (libvlc, "libvlc_media_new_location")	
				libvlc_media_release				= GetProcAddress (libvlc, "libvlc_media_release")
				libvlc_media_parse					= GetProcAddress (libvlc, "libvlc_media_parse")
			
				libvlc_media_player_new_from_media	= GetProcAddress (libvlc, "libvlc_media_player_new_from_media")
				
				libvlc_media_player_set_hwnd		= GetProcAddress (libvlc, "libvlc_media_player_set_hwnd")
				
				libvlc_media_player_play			= GetProcAddress (libvlc, "libvlc_media_player_play")
				libvlc_media_player_stop			= GetProcAddress (libvlc, "libvlc_media_player_stop")
				libvlc_media_player_release			= GetProcAddress (libvlc, "libvlc_media_player_release")
				
				libvlc_video_get_size = GetProcAddress( libvlc, "libvlc_video_get_size")
				
				libvlc_video_set_callbacks = GetProcAddress( libvlc, "libvlc_video_set_callbacks")
				libvlc_video_set_format = GetProcAddress( libvlc, "libvlc_video_set_format")
				
				libvlc_log_unset = GetProcAddress( libvlc, "libvlc_log_unset")
				libvlc_log_set = GetProcAddress( libvlc, "libvlc_log_set")
			EndIf
		EndIf
	EndMethod
	
	Method Play(file:String)
		' Create VLC instance...
		Local args:Byte Ptr[] = ["--quiet".ToCString() ]
		Self.instance = libvlc_new(args.Length, args)
		For Local p:Byte Ptr = EachIn args
			MemFree(p)
		Next
		
		If Self.instance Then
			' Load media from filename...
			If file.Find("://") &lt;&gt; - 1 Then
				Self.media = libvlc_media_new_location(Self.instance, file)
			Else
				Self.media = libvlc_media_new_path(Self.instance, file)
			EndIf
			
			If Self.media Then
				' Create VLC player...
				Self.player = libvlc_media_player_new_from_media(Self.media)
				
				If Self.player Then
					'Check the size of the video			
					Print "Checking size of video..."
					libvlc_media_player_play(Self.player)
					While width &lt;= 0 And height &lt;= 0
						libvlc_video_get_size(player, 0, width, height)
						Delay(10)
					Wend
					libvlc_media_player_stop(Self.player)
					
					Print "Size = " + width + "x" + height
					Image = LoadImage(CreatePixmap(width, height, PF_BGRA8888))
					
					' setup own rendering (RV32 = BGRA8888)
					libvlc_video_set_format(Self.player, "RV32", width, height, width * 4)
					libvlc_video_set_callbacks(Self.player, lock_cb, unlock_cb, display_cb, Null)
					
					libvlc_media_player_play(Self.player)
				EndIf
			EndIf
		EndIf
	EndMethod
	
	Function lock_cb:Byte Ptr(ctx:Byte Ptr, planes:Byte Ptr Ptr)
		'Only one plane since we are doing RGB
		PLANES[0] = LockImage(Image).pixels
	EndFunction
	
	Function unlock_cb:Byte Ptr()
	EndFunction
	
	Function display_cb:Byte Ptr(ctx:Byte Ptr, pixels:Byte Ptr)
	EndFunction
	
	Method Draw(x:Int, y:Int, w:Int = 0, h:Int = 0)
		If Image Then
			If w = 0 And h = 0 Then w = width;h = height
			DrawImageRect(Image, x, y, w, h)
		EndIf
	EndMethod
	
	Method Free()
		If media Then libvlc_media_release(media)
		If instance Then libvlc_release(instance)
	EndMethod
EndType</textarea><br><br>Here's an example using it.<br>This crashes all the time for me though, don't know why.<br>And it keeps saying that the error is something different each time.<br>Sometimes it just crashes and doesn't even give me an error.<br>I have gotten "bad refs:obj=$2f403d0 refs=$0" a few times though.<br>But it seems the problem is with either locking the image, or drawing the image.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

SuperStrict

Framework maxgui.drivers
Import brl.eventqueue
Import brl.glmax2d
Import brl.timer
Import "easyvlc.bmx"

Local file:String = "http://www.quirksmode.org/html5/videos/big_buck_bunny.mp4"
If Not file Then Print "No video file!"

Graphics(1280, 720)

Local myVideo:TEasyVLC = LoadVideo(file)

While Not KeyDown(KEY_ESCAPE)
	myVideo.Draw(0, 0, 640 + Sin(MilliSecs() * 0.1) * 100, 360)
	
	Flip(1)
	Cls()
Wend
myVideo.Free()</textarea><br><br>I'm guessing the crash is because I'm not actually drawing the image at the callback, so I'm accessing the image while VLC is drawing to it. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> Hmmm that example of yours has some weird behaviors for sure.<br><br>Yeah, some of the crashes are the result of reading and writing to the image at the same time. Doing some locking and/or using a second buffer should get rid of them.<br><br>It seems to be the mixing of a Type and libvlc that causes random crashes elsewhere though...<br>I managed to get rid of some of them by moving things out of the methods (like Draw) and into the main loop, <br>which is odd to say the least!<br><br>And there are differences between debug-mode and release-mode.. In Debug i get "scope stack underflow", but using GCEnter/GCLeave in the callback functions fixes that.<br><br>It would appear that the threaded nature of libvlc and blitzmax gc does not like each other very much.<br>No matter what i try i get crashes somewhere, but my other tests that doesnt use any types works perfectly.<br>Go figure :/<br><br>Heres what i ended up with, they arent pretty and dont work perfectly but maybe youl figure something out...<br><br>This one only works in Debug mode and NON-threaded mode and still has issues.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

SuperStrict

'Local file:String = "http://www.quirksmode.org/html5/videos/big_buck_bunny.mp4"
Local file:String = "c:\dump\Creating a Doom-style 3D engine in C-HQYsFshbkYw.mp4"
If Not file Then Print "No video file!"

Graphics 1280, 720

Local myVideo:TEasyVLC = LoadVideo(file)

While Not KeyDown(KEY_ESCAPE)
	myVideo.Draw(0, 0, 640 + Sin(MilliSecs() * 0.1) * 100, 360)

		If myVideo.Image Then
?threaded
			LockMutex(myVideo.FrameMutex)
?
			DrawImageRect(myVideo.Image, 0, 0, 640 + Sin(MilliSecs() * 0.1) * 100, 360)
?threaded
			UnlockMutex(myVideo.FrameMutex)
?
		EndIf

	Flip(1)
	Cls()
Wend
myVideo.Free()
' just to play nice
Extern "Win32"
	Function FreeLibrary( lib:Int)
EndExtern
FreeLibrary(myVideo.libvlc)
End



Function LoadVideo:TEasyVLC(file:String)
	Local nEVLC:TEasyVLC = New TEasyVLC
	nEVLC.Play(file)
	Return nEVLC
EndFunction

	
Type TEasyVLC
	Global libvlc:Int
	
	' LibVLC functions...
	Global libvlc_new:Byte Ptr (argc:Int, argv:Byte Ptr Ptr) "win32"
	Global libvlc_release:Byte Ptr (instance:Byte Ptr) "win32"	' Void return
	
	Global libvlc_media_new_path:Byte Ptr (instance:Byte Ptr, path$z) "win32"
	Global libvlc_media_new_location:Byte Ptr (instance:Byte Ptr, path$z) "win32"
	Global libvlc_media_release:Int (media:Byte Ptr) "win32"	' Void return
	
	Global libvlc_media_parse:Int(media:Byte Ptr) "win32"
	
	Global libvlc_media_player_new_from_media:Byte Ptr (media:Byte Ptr) "win32"
	Global libvlc_media_player_set_hwnd:Int (player:Byte Ptr, hwnd:Int) "win32"	' Void return
	
	Global libvlc_media_player_play:Int (player:Byte Ptr) "win32"
	Global libvlc_media_player_stop:Int (player:Byte Ptr) "win32"	' Void return
	Global libvlc_media_player_release:Int (player:Byte Ptr) "win32"	' Void return
	
	Global libvlc_video_get_size:Int(player:Byte Ptr, num:Int, width:Int Var, height:Int Var) "win32"
	
	Global libvlc_video_set_callbacks:Int(player:Byte Ptr, lock:Byte Ptr, unlock:Byte Ptr, display:Byte Ptr, ctx:Byte Ptr) "win32"
	Global libvlc_video_set_format:Int(player:Byte Ptr, chroma$z, width:Int, height:Int, pitch:Int) "win32"
	
	Global libvlc_log_set(instance:Byte Ptr, callback:Byte Ptr) "win32"
	Global libvlc_log_unset(instance:Byte Ptr) "win32"
	
	Field instance:Byte Ptr
	Field media:Byte Ptr
	Field player:Byte Ptr
	
	Global Image:TImage
?threaded
	Global FrameMutex:TMutex
?
	
	Field width:Int
	Field height:Int
	
	Field lockPtr:Byte Ptr(ctx:Byte Ptr, planes:Byte Ptr Ptr)
		
	Method  New()
?threaded
		If Not FrameMutex Then FrameMutex = CreateMutex()
?
		If Not TEasyVLC.libvlc Then
			libvlc = LoadLibraryA ("libvlc.dll")
			
			If Not libvlc Then
				Print "Unable to find libvlc.dll"
			Else
				libvlc_new = GetProcAddress (libvlc, "libvlc_new")
				libvlc_release						= GetProcAddress (libvlc, "libvlc_release")
				
				libvlc_media_new_path				= GetProcAddress (libvlc, "libvlc_media_new_path")
				libvlc_media_new_location			= GetProcAddress (libvlc, "libvlc_media_new_location")	
				libvlc_media_release				= GetProcAddress (libvlc, "libvlc_media_release")
				libvlc_media_parse					= GetProcAddress (libvlc, "libvlc_media_parse")
			
				libvlc_media_player_new_from_media	= GetProcAddress (libvlc, "libvlc_media_player_new_from_media")
				
				libvlc_media_player_set_hwnd		= GetProcAddress (libvlc, "libvlc_media_player_set_hwnd")
				
				libvlc_media_player_play			= GetProcAddress (libvlc, "libvlc_media_player_play")
				libvlc_media_player_stop			= GetProcAddress (libvlc, "libvlc_media_player_stop")
				libvlc_media_player_release			= GetProcAddress (libvlc, "libvlc_media_player_release")
				
				libvlc_video_get_size = GetProcAddress( libvlc, "libvlc_video_get_size")
				
				libvlc_video_set_callbacks = GetProcAddress( libvlc, "libvlc_video_set_callbacks")
				libvlc_video_set_format = GetProcAddress( libvlc, "libvlc_video_set_format")
				
				libvlc_log_unset = GetProcAddress( libvlc, "libvlc_log_unset")
				libvlc_log_set = GetProcAddress( libvlc, "libvlc_log_set")
			EndIf
		EndIf
	EndMethod
	
	Method Play(file:String)
		' Create VLC instance...
		Local args:Byte Ptr[] = ["--no-audio".ToCString() ]
		Self.instance = libvlc_new(args.Length, args)
		For Local p:Byte Ptr = EachIn args
			MemFree(p)
		Next
		
		If Self.instance Then
			' Load media from filename...
			If file.Find("://") &lt;&gt; - 1 Then
				Self.media = libvlc_media_new_location(Self.instance, file)
			Else
				Self.media = libvlc_media_new_path(Self.instance, file)
			EndIf
			
			If Self.media Then
				' Create VLC player...
				Self.player = libvlc_media_player_new_from_media(Self.media)
				
				If Self.player Then
					'Check the size of the video			
					Print "Checking size of video..."
'					libvlc_media_player_play(Self.player)
					libvlc_media_parse(media)
					While width &lt;= 0 And height &lt;= 0						
						libvlc_video_get_size(player, 0, width, height)
						Delay(10)
					Wend
'					libvlc_media_player_stop(Self.player)
					
					Print "Size = " + width + "x" + height
					Image = LoadImage(CreatePixmap(width, height, PF_BGRA8888))
					
					' setup own rendering (RV32 = BGRA8888)
					libvlc_video_set_format(Self.player, "RV32", width, height, width * 4)
'					libvlc_video_set_callbacks(Self.player, lock_cb, unlock_cb, display_cb, Null)
Extern "C"
	Function bbGCRetain(o:Object)
EndExtern
bbGCRetain(Self)
					libvlc_video_set_callbacks(Self.player, lock_cb, unlock_cb, Null, Null)
					
					libvlc_media_player_play(Self.player)
				EndIf
			EndIf
		EndIf
	EndMethod
	
	Function lock_cb:Byte Ptr(ctx:Byte Ptr, planes:Byte Ptr Ptr)
?threaded
		LockMutex(FrameMutex)
?
		'Only one plane since we are doing RGB
		PLANES[0] = LockImage(Image).pixels
	EndFunction
	
	Function unlock_cb:Byte Ptr()
		UnlockImage(image)
?threaded
		UnlockMutex(FrameMutex)
?
	EndFunction
	
	Function display_cb:Byte Ptr(ctx:Byte Ptr, pixels:Byte Ptr)
	EndFunction
	
	Method Draw(x:Int, y:Int, w:Int = 0, h:Int = 0)
		If Image Then
			If w = 0 And h = 0 Then w = width;h = height
?threaded
			LockMutex(FrameMutex)
?
			DrawImageRect(Image, x, y, w, h)
?threaded
			UnlockMutex(FrameMutex)
?
		EndIf
	EndMethod
	
	Method Free()
		If player Then libvlc_media_player_stop(player)
		If media Then libvlc_media_release(media)
		If player Then libvlc_media_player_release(player)
		If instance Then libvlc_release(instance)
?threaded
		CloseMutex(FrameMutex)
?
	EndMethod
EndType
</textarea><br>This test works in Release+Threaded mode and requires a module i made a while back <a href="https://drive.google.com/open?id=0BzVLNZSckvfhOTdqSWtMYnhCVlk" target="_blank">grb.libvlc.7z</a> (it has one sample: "libvlc_trackdesc_test.bmx").<br>It still crashes on exit though...<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

SuperStrict

Import GRB.libvlc

SetGraphicsDriver D3D7Max2DDriver()

'Local file:String = "http://www.quirksmode.org/html5/videos/big_buck_bunny.mp4"
Local file:String = "c:\dump\Creating a Doom-style 3D engine in C-HQYsFshbkYw.mp4"
If Not file Then Print "No video file!"

Graphics 1280, 720

Local myVideo:TEasyVLC = LoadVideo(file)
Global keep:TEasyVLC = myVideo

Print "Video: " + Hex(Int Byte Ptr myVideo)
Print "Image: " + Hex(Int Byte Ptr myVideo.Image)
?threaded
Print "Mutex: " + Hex(Int Byte Ptr myVideo.FrameMutex)
?

While Not KeyDown(KEY_ESCAPE)
'	myVideo.Draw(0, 0, 640 + Sin(MilliSecs() * 0.1) * 100, 360)

?threaded
	If TryLockMutex(myVideo.FrameMutex)
?
		Local pix:TPixmap = LockImage(myVideo.Image)
		MemCopy pix.pixels, myVideo.Buffer, myVideo.BufferSize
		UnlockImage(myVideo.Image)
?threaded
		UnlockMutex(myVideo.FrameMutex)
	EndIf
?
	DrawImageRect(myVideo.Image, 0, 0, 640 + Sin(MilliSecs() * 0.1) * 100, 360)

	Flip
	Cls
Wend
myVideo.Free()
End



Function LoadVideo:TEasyVLC(file:String)
	Local nEVLC:TEasyVLC = New TEasyVLC
	nEVLC.Play(file)
	Return nEVLC
EndFunction

	
Type TEasyVLC
	Field instance:TVLCInstance
	Field media:TVLCMedia
	Field player:TVLCMediaPlayer
	
	Global Image:TImage
	Global Buffer:Byte Ptr
	Global BufferSize:Int
?threaded
	Global FrameMutex:TMutex
?
	
	Field width:Int
	Field height:Int
		
	Method  New()
?threaded
		If Not FrameMutex Then FrameMutex = CreateMutex()
?
	EndMethod
	
	Method Play(file:String)
		' Create VLC instance...
		Local args:Byte Ptr[] = ["--no-audio".ToCString() ]
		Self.instance = libvlc_new(args.Length, args)
		For Local p:Byte Ptr = EachIn args
			MemFree(p)
		Next
		
		If Self.instance Then
			' Load media from filename...
			If file.Find("://") &lt;&gt; - 1 Then
				Self.media = libvlc_media_new_location(Self.instance, file)
			Else
				Self.media = libvlc_media_new_path(Self.instance, file)
			EndIf
			
			If Self.media Then
				' Create VLC player...
				Self.player = libvlc_media_player_new_from_media(Self.media)
				
				If Self.player Then
					'Check the size of the video			
					Print "Checking size of video..."
'					libvlc_media_player_play(Self.player)
					libvlc_media_parse(media)
					While width &lt;= 0 And height &lt;= 0						
						libvlc_video_get_size(player, 0, width, height)
						Delay(10)
					Wend
'					libvlc_media_player_stop(Self.player)
					
					Print "Actial-Size = " + width + "x" + height					
					Image = LoadImage(CreatePixmap(width, height, PF_BGRA8888), DYNAMICIMAGE)
'					Image = create_scaled_image( width,height, 400,400)
'					width = ImageWidth(Image)
'					height = ImageHeight(Image)
'					Print "Target-Size = " + width + "x" + height
					
					BufferSize = width * height * 4
					Buffer = MemAlloc(BufferSize)
					
					' setup own rendering (RV32 = BGRA8888)
					libvlc_video_set_format(Self.player, "RV32", width, height, width * 4)
'					libvlc_video_set_callbacks(Self.player, lock_cb, unlock_cb, display_cb, Null)
					libvlc_video_set_callbacks(Self.player, lock_cb, unlock_cb, Null, Null)
					
					libvlc_media_player_play(Self.player)
				EndIf
			EndIf
		EndIf
	EndMethod
	
	Function lock_cb:Byte Ptr(ctx:Byte Ptr, planes:Byte Ptr Ptr)
		GCEnter
?threaded
		LockMutex(FrameMutex)
?
		'Only one plane since we are doing RGB
'		PLANES[0] = LockImage(Image).pixels
		planes[0] = Buffer
		GCLeave
		Return Buffer
	EndFunction
	
	Function unlock_cb:Byte Ptr( ctx:Byte Ptr, picture:Byte Ptr, planes:Byte Ptr Ptr)
		GCEnter
		UnlockImage(Image)
?threaded
		UnlockMutex(FrameMutex)
?
		GCLeave
	EndFunction
	
	Function display_cb:Byte Ptr(ctx:Byte Ptr, planes:Byte Ptr Ptr)
	EndFunction
	
	Method Draw(x:Int, y:Int, w:Int = 0, h:Int = 0)
		If Image Then
			If w = 0 And h = 0 Then w = width;h = height
?threaded
			LockMutex(FrameMutex)
?
			Local pix:TPixmap = LockImage(Image)
			MemCopy pix.pixels, Buffer, BufferSize
			UnlockImage(Image)
			DrawImageRect(Image, x, y, w, h)
?threaded
			UnlockMutex(FrameMutex)
?
		EndIf
	EndMethod
	
	Method Free()
		libvlc_media_player_stop(player)
		If media Then libvlc_media_release(media)
		If player Then libvlc_media_player_release(player)
		If instance Then libvlc_release(instance)
?threaded
		CloseMutex(FrameMutex)
?
	EndMethod
EndType


' create scaled image from actual size (aw,ah) to target size (tw,th)
Function create_scaled_image:TImage( aw:Int, ah:Int, tw:Int, th:Int, flags:Int = -1)
	Local pix:TPixmap
	If aw = 0 Or ah = 0 Then
		pix = CreatePixmap( tw, th, PF_BGRA8888)
	Else
		Local ratio:Float = Min( tw / Float(aw), th / Float(ah))
		pix = CreatePixmap( aw * ratio, ah * ratio, PF_BGRA8888)
	EndIf
	If flags = -1 Then flags = DYNAMICIMAGE Else flags :| DYNAMICIMAGE
	Return LoadImage( pix, flags)
EndFunction


</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> It would be lovely to have a non-crashing video stream from webcam to internet. =)<br><br>~GF <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LT</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> Have not found a way to get the video size (or aspect ratio).  It always returns 0 x 0.  :/<br><br>EDIT: Getting the video size (but not aspect ratio) works after a few seconds of playing.  Looking for a better way... <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> Been having a go at getting the width/height data by parsing the media before trying to play, but I'm running into a crash -- see below for *** CRASH HERE ***<br><br>It looks to me as if libvlc_media_tracks_get should allocate the necessary data structures and fill in my pointer (tracks:Byte Ptr), but that just crashes. If I allocate a random lump of memory myself, it gets the data... but then crashes with two illegal instruction errors!<br><br>Anyone have any idea what's up? (grable, Kryzon?!)<br><br>I'm a bit stumped at this point...<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Type libvlc_video_track_t

	Field i_height:Int
	Field i_width:Int
	Field i_sar_num:Int
	Field i_sar_den:Int
	Field i_frame_rate_num:Int
	Field i_frame_rate_den:Int

End Type

Type libvlc_media_track_t

	Field i_codec:Int
	Field i_original_fourcc:Int
	Field i_id:Int
	Field i_type:Int
	Field i_profile:Int
	Field i_level:Int
	Field track_union:Byte Ptr	' Represents one of these, depending on context:
	
								' union {libvlc_audio_track_t * audio,
								' libvlc_video_track_t * video,
								' libvlc_subtitle_track_t * subtitle

	Field i_bitrate:Int
	Field psz_language:Byte Ptr
	Field psz_description:Byte Ptr
	
End Type

' LibVLC functions...

Global libvlc_new:Byte Ptr							(argc:Int, argv:Byte Ptr)			"win32"
Global libvlc_release:Byte Ptr						(instance:Byte Ptr)					"win32"	' Void return

Global libvlc_media_new_path:Byte Ptr				(instance:Byte Ptr, path:Byte Ptr)	"win32"
Global libvlc_media_new_location:Byte Ptr			(instance:Byte Ptr, path:Byte Ptr)	"win32"
Global libvlc_media_release:Int						(media:Byte Ptr)					"win32"	' Void return

Global libvlc_media_parse:Int						(media:Byte Ptr)
Global libvlc_media_tracks_get:Int					(media:Byte Ptr, tracks:Byte Ptr)
Global libvlc_media_tracks_release:Int				(media:Byte Ptr, count:Int)

Global libvlc_media_player_new_from_media:Byte Ptr	(media:Byte Ptr)					"win32"
Global libvlc_media_player_set_hwnd:Int				(player:Byte Ptr, hwnd:Int)			"win32"	' Void return
Global libvlc_media_player_play:Int					(player:Byte Ptr)					"win32"
Global libvlc_media_player_stop:Int					(player:Byte Ptr)					"win32"	' Void return
Global libvlc_media_player_release:Int				(player:Byte Ptr)					"win32"	' Void return

Local file:String = RequestFile ("Select a media file...")
If Not file Then End

' URL loading:

' Local file:String = "http://www.quirksmode.org/html5/videos/big_buck_bunny.mp4"

' Assign functions from DLL...

Global libvlc:Int = LoadLibraryA ("libvlc.dll")

If libvlc

	libvlc_new							= GetProcAddress (libvlc, "libvlc_new")
	libvlc_release						= GetProcAddress (libvlc, "libvlc_release")
	
	libvlc_media_new_path				= GetProcAddress (libvlc, "libvlc_media_new_path")
	libvlc_media_new_location			= GetProcAddress (libvlc, "libvlc_media_new_location")
	libvlc_media_release				= GetProcAddress (libvlc, "libvlc_media_release")

	libvlc_media_parse					= GetProcAddress (libvlc, "libvlc_media_parse")
	libvlc_media_tracks_get				= GetProcAddress (libvlc, "libvlc_media_tracks_get")
	libvlc_media_tracks_release			= GetProcAddress (libvlc, "libvlc_media_tracks_release")
	
	Local instance:Byte Ptr
	Local media:Byte Ptr
	Local player:Byte Ptr
	
	' Create VLC instance...
	
	instance = libvlc_new (0, Null)
	
	If instance
	
		' Load media from filename...
		
		media = libvlc_media_new_path (instance, file.ToCString ())
		
		' URL loading:
		
'		media = libvlc_media_new_location (instance, file.ToCString ())
		
		If media
		
			libvlc_media_parse media
			
			Local tracks:Byte Ptr' = MemAlloc (102400) - gets count, and double-crash!
			
			' *** CRASH HERE ***
			
			Print "Trying..."
			Local count:Int = libvlc_media_tracks_get (media, tracks)
			Print count
			Print "Done"
			
			If count

				' This part, in theory...
				
				' 1) Parse 'count' libvlc_media_track_t structures;
				' 2) Interpret 'track_union' offset as libvlc_video_track_t;
				' 3) Read 'i_height' and 'i_width';
				' 4) Calculate ratio from these...

				' Now tell libvlc to free up allocated structures:
				
				libvlc_media_tracks_release media, count

			EndIf
						
			' Release media...
			
			libvlc_media_release media

		EndIf
		
		' Release VLC instance...
		
		libvlc_release instance
		
	EndIf
	
EndIf
</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> It would be lovely to have a non-crashing video stream from webcam to internet. =)<br><br>Thanks guys! =)<br><br>~GF <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> Hey guys,<br><br>Just found this thread and immediately thought that I had the exact same symtpoms with the FMod encoder using callbacks into Blitzmax code.<br><br>The problem I had was that the callback is being called from a different thread that was created inside FMod and the BlitzMax garbage collector doesn't know anything about that thread. It caused havoc and random crashes. The only way to fix my issue was to move the callback code into c++ ( or c ) where there is no garbage collector.<br><br>I'm not saying it's the issue - just that I had the same symptoms and it *may* be related.<br><br>EDIT: After reading grables last message with the stack mentioned then I'd say that it the same issue that I had. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> BlitzSupport...<br>I couldn't resist taking a stab with your latest posted example.<br><br>I'm testing in NG x32 on Windows10 and it crashes if I have the functions use the "win32" calling convention. I'm using the lastest version of vlc from <a href="http://videolan.mirrors.uk2.net/vlc/2.2.3/win32/vlc-2.2.3-win32.zip" target="_blank">http://videolan.mirrors.uk2.net/vlc/2.2.3/win32/vlc-2.2.3-win32.zip</a><br>If it's not crashing for you on legacy max then oh well :D we'll ignore that bit for the time being.<br><br>Also the line trying to get count and tracks needs to pass the address of the tracks variable into it and not its value so use<br><br><pre class=code>Local count:Int = libvlc_media_tracks_get (media, <b>Varptr</b> tracks)</pre><br><br>Hope it helps! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> Hmm, interesting, thanks Col -- will have another look with all this in mind! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> @BlitzSupport or somebody here currently, can you build a small demo of a server &amp; client that is able to use this to stream from a webcam, an MP4 real time? I'd like to use that Blitz3D example grable made, but he says it's unstable. :(<br><br>Thanks!<br><br>~GF <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> @Guy Fawkes<br>I speak for myself and no I won't <div class="quote"> build a small demo of a server &amp; client that is able to use this to stream from a webcam <br></div>. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
