<!DOCTYPE html><html lang="en" ><head ><title >Sprite class</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Sprite class, language=bmx, category=Graphics'><meta name='author' content='ozak'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/Sprite class</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1394.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sprite class by ozak</td><td align="right">2005 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Frame rate independant animatione/movement sprite class. </td></tr><tr ><td class="cell"><pre class="code">' Frame independent sprite class with animation support by Odin Jensen (www.furi.dk)
' Free to use as you please :)

' Sprite class
Type Sprite

	' Position
	Field x:Float = 0
	Field y:Float = 0
	
	' Velocity in pixels
	Field velx:Float = 0
	Field vely:Float = 0
	
	' Sprite image
	Field Image:TImage
	
	' Current animation frame
	Field CurAnimFrame:Int = 0
	
	' Width and height of sprite
	Field Width:Int = 0
	Field Height:Int = 0
	
	' Animation frame list
	Field FrameIndices:Int[] = Null
	
	' Animation timer vars
	Field AnimDelay:Int = 0
	Field AnimTime:Int = 1000
	Field CurAnimIndex:Int = 0
	
	' Load sprite
	Method Load(URL:Object, TileWidth:Int, TileHeight:Int, FirstTile:Int, NumTiles:Int)
	
		' Load image
		Image = LoadAnimImage(URL, TileWidth, TileHeight, FirstTile, NumTiles)
		
		' Save dimensions
		Width = TileWidth
		Height = TileHeight
		
		' Set cur anim frame
		CurAnimFrame = 0
		
	EndMethod
	
	' Clone sprite
	Method Clone:Sprite()
	
		' Create new copy
		Local NewSprite:Sprite = New Sprite
		
		' Copy pointers to indices/image
		NewSprite.SetFrameIndices(FrameIndices)
		NewSprite.SetImage(Image, Width, Height)
		NewSprite.SetAnimDelay(AnimDelay)
		NewSprite.SetX(x)
		NewSprite.SetY(y)
		NewSprite.SetVelocityX(velx)
		NewSprite.SetVelocityY(vely)
		
		Return NewSprite		
	
	EndMethod
	
	' Update sprite
	Method Update(DeltaTime:Int)
	
		' Update movement
		x = x + (velx * DeltaTime)
        y = y + (vely * DeltaTime)

		' Update animation
		If (FrameIndices)
		
			' Increase anim timer
			AnimTime = AnimTime + DeltaTime
			
			' Is it time?
			If (AnimTime &gt; AnimDelay)
			
				' Reset anim time
				AnimTime = 0
				
				' Ok to advance?
				If (CurAnimIndex &lt; (FrameIndices.length-1))	
				
					CurAnimIndex = CurAnimIndex + 1
									
				Else
				
					CurAnimIndex = 0
				
				EndIf
				
				CurAnimFrame = FrameIndices[CurAnimIndex]
			EndIf
		
		EndIf
	
	EndMethod
	
	' Set image
	Method SetImage(Image:TImage, Width:Int, Height:Int)
	
		self.Image = Image
		self.Width = Width
		self.Height = Height
	
	EndMethod
	
	' Draw sprite
	Method Draw()
	
		DrawImage(Image, x, y, CurAnimFrame)
	
	EndMethod
	
	' Set current frame
	Method SetCurFrame(Frame:Int)
	
		CurAnimFrame = Frame
	
	EndMethod
	
	' Set animation delay
	Method SetAnimDelay(NewDelay:Int)
	
		AnimDelay = NewDelay
	
	EndMethod
	
	' Set frame index list
	Method SetFrameIndices(Indices:Int[])
	
		FrameIndices = Indices
	
	EndMethod
	
	' Get image for manipulation
	Method GetImage:TImage()
	
		Return Image
	
	EndMethod
	
	' Set X
	Method SetX(x:Float)
	
		self.x = x	
	
	EndMethod
	
	' Set Y
	Method SetY(y:Float)
	
		self.y = y
	
	EndMethod
	
	' Get X
	Method GetX:Float()
	
		Return x
	
	EndMethod
	
	' Get Y
	Method GetY:Float()
	
		Return y
	
	EndMethod
	
	' Set x velocity
	Method SetVelocityX(vx:Float)
	
		velx = vx
	
	EndMethod
	
	' Set y velocity
	Method SetVelocityY(vy:Float)
	
		vely = vy
	
	EndMethod

	' Get width of sprite
	Method GetWidth:Int()
	
		Return Width
	
	EndMethod
	
	' Get height of sprite
	Method GetHeight:Int()
	
		Return Height
	
	EndMethod		

EndType</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>2005</font></td></tr></table></td></tr><tr ><td class="posttext"> Hmmm Delta time is rarely an Int, I think a double would be more accurate. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ozak</td><td align="right"><font class=tiny>2005</font></td></tr></table></td></tr><tr ><td class="posttext"> Works fine for me, but yes. I usually use a long too, so it's a bummer when porting from older java code :) <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
