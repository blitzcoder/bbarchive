<!DOCTYPE html><html lang="en" ><head ><title >Deformable terrain</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Quick and un-optimised deformable terrain test, language=bmx, category=Graphics'><meta name='author' content='matibee'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/Deformable terrain</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2691.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Deformable terrain by matibee</td><td align="right">2010 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> The terrain is made up of horizontal line segments while a second, smaller shape is used as an eraser and some falling particles are added for effect.<br><br>Subtracting one line segment from another is really simple..  Depending on how they intersect; the start could be changed, the end could be changed, it could be deleted altogether or split into two.<br><br><strike>It's completely unoptimised but still runs quite well.</strike><br><br>Edit:  Now updated to sort the line segments by height, allowing the subtract and collision tests to exit once they've reached lines higher than the test height.<br><br>Edit 2:  I've uploaded a textured and slightly updated version <a href="http://www.matibee.co.uk/wpsite/?p=39" target="_blank">here</a>.<br><br>Cheers<br>Matt </td></tr><tr ><td class="cell"><pre class="code">SuperStrict 

' Horizontal line type
Type hline
	Field _start:Int
	Field _end:Int
	Field y:Int
	
	Method draw()
		DrawLine( _start, y, _end, y )
	End Method 
	
	Function Create:hline ( s:Int, e:Int, y:Int )
		Local hl:hline = New hline
		hl._start = s
		hl._end = e
		hl.y = y
		Return hl
	End Function 
	
	' Subtract another hline from this one, if this line is
	' deleted completely this function returns true.
	'
	' If this line is split, hlDest contains the newly created
	' line.
	Method Split:Int ( hl:hline, hlDest:hline Var )
		If ( hl.y &lt;&gt; y ) Return False 
		If ( _start &lt; hl._start )
			If ( _end &gt; hl._end )
				hlDest = hline.Create( hl._end, _end, y )
				_end = hl._start
			Else If ( _end &gt; hl._start )
				_end = hl._start
			End If 
		Else If ( _start &lt; hl._end )
			If ( _end &lt; hl._end )
				Return True
			Else 
				_start = hl._end
			End If 
		End If 
		Return False 
	End Method 
	
	Method Move( _x:Int, _y:Int )
		_start :+ _x
		_end :+ _x
		y :+ _y
	End Method 
	
	Method Collide:Int ( _x:Int, _y:Int )
		If ( _y = y )
			If ( _x &gt;= _start And _x &lt;= _end )
				Return True 
			End If 
		End If 
		Return False 
	End Method 
	
End Type 

' A shape created from the horizontal lines..
Type hshape
	Field lines:TList
	Field x:Int, y:Int
	
	Function Create:hshape ()
		Local hs:hshape = New hshape
		hs.lines = New TList
		Return hs
	End Function 
	
	Method Draw()
		For Local hl:hline = EachIn lines
			hl.Draw()
		Next 
	End Method 
	
	Method AddLine( s:Float, e:Float, y:Float )
		lines.AddLast( hline.Create( s, e, y ) )
		SortList( lines, True, hshape.SortLinesByHeight )
	End Method 
	
	Function SortLinesByHeight:Int ( h0:Object, h1:Object )
		Return hline(h1).y &lt; hline(h0).y
	End Function 
	
	Method Position( _x:Int, _y:Int )
		Local xmove:Int = _x - x
		Local ymove:Int = _y - y
		x :+ xmove
		y :+ ymove
		If ( xmove Or ymove )
			For Local hlThis:hline = EachIn lines
				hlThis.Move( xmove, ymove )
			Next
		End If 
	End Method 
	
	Method Subtract( hs:hshape )
		For Local hlOther:hline = EachIn hs.lines
			For Local hlThis:hline = EachIn lines
				Local hlNew:hline
				If ( hlOther.y &lt; hlThis.y ) Exit 
				If ( hlThis.Split( hlOther, hlNew ) )
					lines.Remove( hlThis )
				End If 
				If ( hlNew ) 
					lines.AddLast( hlNew )
					SortList( lines, True, hshape.SortLinesByHeight )
				End If 
			Next
		Next 
	End Method 
	
	Method CollidePixel:Int ( x:Int, y:Int )
		For Local hlThis:hline = EachIn lines
			If ( hlThis.y &gt; y ) 
				Return False 
			Else If ( hlThis.y = y And hlThis.Collide( x, y ) )
				Return True 
			End If 
		Next 
		Return False 
	End Method 
	
End Type 

Type phyxel ' simple physics pixel
	Field x:Float
	Field y:Float 
	Field vel:Float
	
	Function Create:phyxel ( x:Float, y:Float )
		Local p:phyxel = New phyxel
		p.x = x
		p.y = y
		Return p
	End Function 
	
	Method Draw()
		DrawRect( x, y, 1, 1 )
	End Method 
	
	Method Update( terrain:hshape )
		If ( Not terrain.CollidePixel( x, y + vel + 0.5 ) )
			vel :+ 0.1
			If ( vel &gt; 0.98 ) vel = 0.98
		Else 
			vel :* -( 0.4 + RndFloat() * 0.5 )
		End If 
		y :+ vel
		If ( y &gt; 600 ) y = 600
	End Method 
	
End Type 



' Simple test
Graphics 800, 600

' The first shape is the terrain
Local hs1:hshape = hshape.Create()
For Local t:Int = 300 To 599
	hs1.AddLine( 0, 799, t )
Next 

' The second shape is the eraser
Local hs2:hshape = hshape.Create()
For Local t:Int = 0 To 30
	hs2.AddLine( 0, 40, t )
Next 
hs2.AddLine( 1, 39, 31 )
hs2.AddLine( 2, 38, 32 )
hs2.AddLine( 4, 36, 33 )
hs2.AddLine( 6, 34, 34 )
hs2.AddLine( 8, 32, 35 )
hs2.AddLine( 11, 29, 36 )
hs2.AddLine( 15, 25, 37 )

' a list of falling pixels.
Local phyxelList:TList = New TList
For Local t:Int = 0 To 200
	phyxelList.AddLast( phyxel.Create( Rand( 5, 795 ), Rand( 20, 80 ) ) )
Next 


While Not AppTerminate()
	hs1.Subtract( hs2 )
	Cls
	SetColor( 0,0,255 )
	hs1.draw()
	SetColor( 255, 0, 0 )
	hs2.Position( MouseX(), MouseY() )
	hs2.draw()
	DrawText( "Line segments: " + hs1.lines.Count() , 0, 0 )
	SetColor( 0, 255, 0 )
	For Local p:phyxel = EachIn phyxelList
		p.Update( hs1 )
		p.Draw()
	Next 
	Flip	
Wend</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AndrewT</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> Awesome! I'm very tempted to make a Worms clone now :P. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ethan</td><td align="right"><font class=tiny>2012</font></td></tr></table></td></tr><tr ><td class="posttext"> Which program did you do this in. In my blitz plus it does not work? <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>2012</font></td></tr></table></td></tr><tr ><td class="posttext"> This is BlitzMax code. It's not compatible with BlitzPlus.<br><br>BlitzPlus can use those Archive entries that are marked as ".bb" format in the index, as opposed to ".bmx" like this one (at least, when people remember to tag them correctly). <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>2012</font></td></tr></table></td></tr><tr ><td class="posttext"> As Yasha says it is Blitzmax but the description of how it works is here: <a href="http://www.matibee.co.uk/wpsite/2010/04/04/2d-deformable-terrain-in-blitzmax/" target="_blank">http://www.matibee.co.uk/wpsite/2010/04/04/2d-deformable-terrain-in-blitzmax/</a><br><br>(Not sure why the original link above isn't working) <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
