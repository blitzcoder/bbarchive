<!DOCTYPE html><html lang="en" ><head ><title >Bitmap Font</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Another bitmap font method., language=bmx, category=Graphics'><meta name='author' content='Ked'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/Bitmap Font</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2722.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bitmap Font by Ked</td><td align="right">2010 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Uses BlitzMax's DrawSubImageRect, so it's faster than AnimImage. Have fun! </td></tr><tr ><td class="cell"><pre class="code">SuperStrict

Type TBitmapFontViewer
	Field img:TImage
	Field x:Float,y:Float
	Field w:Int,h:Int
	Field holding:Int=False
	Field holdx:Float,holdy:Float
	
	Field path:String="n/a"
	Field chardata:TBitmapCharData[96]
	
	Method Open:TBitmapFontViewer(url:Object)
		img=LoadImage(String(url)+"_img.png")
		If img=Null Return Null
		
		x=0
		y=0
		w=ImageWidth(img)
		h=ImageHeight(img)
		
		path=String(url)
		
		Return Self
	EndMethod
	
	Method CreateNew:TBitmapFontViewer(furl:Object,size:Int)
		Local imgfont:TImageFont=LoadImageFont(furl,size)
		If imgfont=Null Return Null
		SetImageFont(imgfont)
		
		x=0
		y=0
		
		path="n/a"
		
		Local spacing:Int=(size*2)
		Local dx:Int=0,dy:Int=0
		
		Cls()
		SetBlend ALPHABLEND
		
		For Local i:Int=32 To 127
			DrawText(Chr(i),dx,dy)
			
			Local c:TBitmapCharData=New TBitmapCharData
			c.ind=i
			c.x=dx
			c.y=dy
			c.w=TextWidth(Chr(i))
			c.h=TextHeight(Chr(i))
			
			chardata[(i-32)]=c
			
			dx:+spacing
			If dx=(16*spacing)
				dx=0
				dy:+spacing
			EndIf
		Next
		
		Flip()
		
		dy:+spacing
		
		w=(16*spacing)
		h=dy
		
		img=CreateImage(w,h,1,DYNAMICIMAGE|FILTEREDIMAGE)
		GrabImage(img,0,0)
		
		Return Self
	EndMethod
	
	Method Save(name:String)
		If path&lt;&gt;"n/a" Return
		If img=Null Return
		
		Local pix:TPixmap=img.pixmaps[0]
		SavePixmapPNG(pix,name+"_img.png")
		pix=Null
		
		Local dat:TStream=WriteStream(name+"_data.dat")
		For Local i:Int=0 To 94
			Local c:TBitmapCharData=chardata[i]
			WriteInt(dat,c.ind)
			WriteInt(dat,c.x)
			WriteInt(dat,c.y)
			WriteInt(dat,c.w)
			WriteInt(dat,c.h)
		Next
		CloseStream(dat)
		
		path=name
	EndMethod
	
	Method Update()
		Local md:Int=MouseDown(MOUSE_LEFT)
		Local mx:Int=MouseX()
		Local my:Int=MouseY()
		
		If (mx&gt;x And my&gt;y And mx&lt;(x+w) And my&lt;(y+h))=False
			If md=True md=False
		EndIf
		
		If md=True
			If holding=False
				holding=True
				holdx=MouseX()
				holdy=MouseY()
			EndIf
			
			If holding=True
				Local xdist:Float=(MouseX()-holdx)
				Local ydist:Float=(MouseY()-holdy)
				
				x:+xdist
				y:+ydist
				
				holdx:+xdist
				holdy:+ydist
			EndIf
		Else
			If holding=True
				holding=False
				holdx=0
				holdy=0
			EndIf
		EndIf
	EndMethod
	
	Method Draw()
		SetBlend ALPHABLEND
		SetScale 1,1
		SetAlpha 1.0
		SetRotation 0
		SetColor 255,255,255
		
		DrawImage img,x,y
	EndMethod
EndType

Type TBitmapCharData
	Field ind:Int
	Field x:Int
	Field y:Int
	Field w:Int
	Field h:Int
EndType

Type TBitmapFont
	Field img:TImage
	Field chardata:TBitmapCharData[96]
	
	Method Load:TBitmapFont(url:Object)
		img=LoadImage(String(url)+"_img.png")
		If img=Null Return Null
		
		Local dat:TStream=ReadStream(String(url)+"_data.dat")
		If dat=Null Return Null
		
		For Local i:Int=0 To 94
			Local c:TBitmapCharData=New TBitmapCharData
			c.ind=ReadInt(dat)
			c.x=ReadInt(dat)
			c.y=ReadInt(dat)
			c.w=ReadInt(dat)
			c.h=ReadInt(dat)
			chardata[i]=c
		Next
		CloseStream(dat)
		
		Return Self
	EndMethod
	
	Method Draw(t:String,x:Float,y:Float)
		Local dx:Float=x,dy:Float=y
		Local h:Float=GetHeight(" ")
		Local xscale:Float,yscale:Float
		Local rot:Float
		
		GetScale(xscale,yscale)
		rot=GetRotation()
		SetRotation 0
		
		For Local i:Int=0 To t.length-1
			Local ind:Int=t[i]
			If Chr(ind)="~n"
				dx=x
				dy:+(h*yscale)
			Else
				Local c:TBitmapCharData=chardata[ind-32]
				DrawSubImageRect(img,dx,dy,c.w,c.h,c.x,c.y,c.w,c.h)
				dx:+(c.w*xscale)
			EndIf
		Next
		
		SetRotation rot
	EndMethod
	
	Method GetHeight:Int(t:String)
		Local h:Int=0
		
		For Local i:Int=0 To t.length-1
			Local ind:Int=t[i]
			If Chr(ind)="~n" Continue
			
			Local c:TBitmapCharData=chardata[ind-32]
			If c.h&gt;h h=c.h
		Next
		
		Return h
	EndMethod
	
	Method GetWidth:Int(t:String)
		Local w:Int=0
		
		For Local i:Int=0 To t.length-1
			Local ind:Int=t[i]
			If Chr(ind)="~n" Continue
			
			Local c:TBitmapCharData=chardata[ind-32]
			If c.w&gt;w w=c.w
		Next
		
		Return w
	EndMethod
	
	Method Delete()
		chardata=Null
		img=Null
	EndMethod
EndType

Rem
'Example 1 : Create a bitmap font
Graphics 800,600

Global fview:TBitmapFontViewer=New TBitmapFontViewer.CreateNew(GetEnv_("systemroot")+"/fonts/trebuc.ttf",16)
fview.Save("TrebuchetMS16")

Repeat
	If AppTerminate() End
	If KeyHit(KEY_ESCAPE) End
	
	fview.Update()
	
	Cls
	
	fview.Draw()
	
	Flip
Forever
EndRem

Rem
'Example 2 : Use a bitmap font
Graphics 800,600

Global f:TBitmapFont=New TBitmapFont.Load("TrebuchetMS16")

Repeat
	If AppTerminate() End
	If KeyHit(KEY_ESCAPE) End
	
	Cls
	
	SetScale 1,1
	SetRotation 90
	f.Draw("Hello! This is a bitmap font routine for BlitzMax.~nI hope you enjoy it.~n~nOr else...",MouseX(),MouseY())
	
	Flip()
Forever
EndRem</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >rs22</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> Very nice. :) <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ked</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> Thanks! I have a bugfix but won't be able to update the above code until I can get to my computer. (I'm on my iPod right now.) <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
