<!DOCTYPE html><html lang="en" ><head ><title >OpenGL Render2Texture (FBOs)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='A small but simple solution for  real r2t with opengl, language=bmx, category=Graphics'><meta name='author' content='klepto2'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/OpenGL Render2Texture (FBOs)</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2222.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >OpenGL Render2Texture (FBOs) by klepto2</td><td align="right">2008 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Render2Texture with Alpha and so on for OpenGL. </td></tr><tr ><td class="cell"><pre class="code">SuperStrict


Type TImageBuffer
	Field Image:TImage
	Field rb:Int[1]
	Field fb:Int[1]
	Field Imageframe:TGLImageframe
	Field Frame:Int = 0
	Field OrigX:Int
	Field OrigY:Int
	Field OrigW:Int
	Field OrigH:Int

	Function SetBuffer:TImageBuffer(Image:TImage,Frame:Int = 0 )
		Local IB:TImageBuffer = New TImageBuffer
		IB.Image = Image
		IB.Frame = Frame
		IB.GenerateFBO()
		IB.BindBuffer()
		Return IB
	End Function
	
	Function Init(Width:Int,Height:Int,Bit:Int=0,Mode:Int=60)
		SetGraphicsDriver(GLMax2DDriver())
		Graphics Width , Height,bit,Mode
		glewInit()
	End Function
	
	Method GenerateFBO()
		ImageFrame = TGLImageFrame(Image.frame(Frame) )
			
		imageframe.v0 = imageframe.v1
		imageframe.v1 = 0.0
	
		Local W:Int = Image.width
		Local H:Int = Image.Height
		
		AdjustTexSize(W , H) 

		
		glGenFramebuffersEXT(1, fb )
	    glGenRenderbuffersEXT(1 , rb) 
	   
	    glBindTexture(GL_TEXTURE_2D, Imageframe.name);
	    glBindFramebufferEXT(GL_FRAMEBUFFER_EXT , fb[0]) ; 
	   
	   
	 	glFramebufferTexture2DEXT(GL_FRAMEBUFFER_EXT, GL_COLOR_ATTACHMENT0_EXT, GL_TEXTURE_2D,  Imageframe.name, 0);
	    glBindRenderbufferEXT(GL_RENDERBUFFER_EXT, rb[0]);
	    glRenderbufferStorageEXT(GL_RENDERBUFFER_EXT, GL_DEPTH_COMPONENT24, W, H);
	    glFramebufferRenderbufferEXT(GL_FRAMEBUFFER_EXT , GL_DEPTH_ATTACHMENT_EXT , GL_RENDERBUFFER_EXT , rb[0])
	   
	    Local status:Int =  glCheckFramebufferStatusEXT(GL_FRAMEBUFFER_EXT)
	   
	    Select status
	       Case GL_FRAMEBUFFER_COMPLETE_EXT 
	          Print "all right" + " : " + Status
	       Case GL_FRAMEBUFFER_UNSUPPORTED_EXT
	          Print "choose different formats"
	       Default
	          End 
	    EndSelect 
   
	End Method
	
	Method BindBuffer()
		GetViewport(OrigX,OrigY,OrigW,OrigH)
		glBindFramebufferEXT(GL_FRAMEBUFFER_EXT , fb[0])
		glMatrixMode GL_PROJECTION
		glLoadIdentity
		glOrtho 0,Image.Width,Image.Width,0,-1,1
		glMatrixMode GL_MODELVIEW 
		glViewport(0 , 0 , Image.Width , Image.Height)
		glScissor 0,0, Image.Width , Image.Height
	End Method
	
	Method UnBindBuffer()
		glBindFramebufferEXT(GL_FRAMEBUFFER_EXT , 0)
		glMatrixMode GL_PROJECTION
		glLoadIdentity
		glOrtho 0,OrigW ,Origh,0,-1,1
		glMatrixMode GL_MODELVIEW 
		glViewport(0 , 0 , OrigW, OrigH)
		glScissor 0,0, OrigW ,OrigH
	End Method
	
	Method Cls(r#=0.0,g#=0.0,b#=0.0,a#=1.0)
		glClearColor r,g,b,a
		glClear GL_COLOR_BUFFER_BIT|GL_DEPTH_BUFFER_BIT
	End Method
	
	Method BufferWidth:Int()
		Return Image.Width
	End Method
	
	Method BufferHeight:Int()
		Return Image.Height
	End Method

End Type


Function AdjustTexSize( width:Int Var,height:Int Var )
	'calc texture size
	width=Pow2Size( width )
	height=Pow2Size( height )
	Repeat
		Local t:Int
		glTexImage2D GL_PROXY_TEXTURE_2D,0,4,width,height,0,GL_RGBA,GL_UNSIGNED_BYTE,Null
		glGetTexLevelParameteriv GL_PROXY_TEXTURE_2D,0,GL_TEXTURE_WIDTH,Varptr t
		If t Return
		If width=1 And height=1 RuntimeError "Unable to calculate tex size"
		If width&gt;1 width:/2
		If height&gt;1 height:/2
	Forever
End Function

Function Pow2Size:Int( n:Int )
	Local t:Int=1
	While t&lt;n
		t:*2
	Wend
	Return t
End Function




TImageBuffer.Init(800,600) 'Same as Graphics but set to GLDriver + glewinit


Global Img:TImage = CreateImage(512, 512)
MidHandleImage Img

Local IB:TImageBuffer = TImageBuffer.SetBuffer(Img) 

Local x:Float = 0
Local Speed:Float = 0.1

Local Angle:Float = 0

While Not KeyHit(Key_Escape)
	SetClsColor 0 , 0 , 255

	Cls
		SetColor 0,255,0
		DrawRect 272,172,256,256
		SetBlend ALPHABLEND
		SetAlpha 1.0
		SetColor 255,255,255
		SetRotation Angle
		DrawImageRect Img , 256+144,256+44,512,512
		SetRotation 0
		SetAlpha 1.0
		Angle:+0.3 
	Flip	
	
	
		IB.BindBuffer()
		IB.Cls(1.0,,,0.4)
		SetColor 255,255,255
		DrawLine 0,0,512,512
		DrawLine 0,512,512,0
		
		IB.UnBindBuffer()
		SetViewport 0,0,800,600
		
Wend</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pete Rigz</td><td align="right"><font class=tiny>2008</font></td></tr></table></td></tr><tr ><td class="posttext"> I moved this question to the openGL forum: <a href="http://www.blitzbasic.com/Community/posts.php?topic=77025" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=77025</a> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tesuji</td><td align="right"><font class=tiny>2011</font></td></tr></table></td></tr><tr ><td class="posttext"> Apologies for digging this up.<br><br>Thanks for posting this snippet as it helped me get my head around FBOs and is nice and lightweight.<br><br>Hope you don't mind but I've refactored it slightly and couldn't resist adding a simple feedback example :)<br>Not sure exactly why the image is mirrored. Guess it might just be max2d uv coords.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Type TImageBuffer

    Field rb:Int[1]
    Field fb:Int[1]
    Field OrigX:Int,OrigY:Int
    Field OrigW:Int,OrigH:Int
    Field width:Int,height:Int
    Field glTextureId:Int

    Method Create:TImageBuffer(image:TImage,frameNo:Int=0)
        initBuffer(image.width, image.height, TGLImageFrame(Image.frame(frameNo)).name)
        Return Self
    End Method
	
    Method initBuffer(width:Int,height:Int,glTextureId:Int)
        Self.width = width
        Self.height = height
        Self.glTextureId = glTextureId
        generateFBO()
    End Method
	
    Function initGraphics(width:Int,height:Int,bit:Int=0,mode:Int=60)
        SetGraphicsDriver(GLMax2DDriver())
        Graphics width,height,bit,mode
        glewInit()
     End Function
	
     Method generateFBO()
	
        Local W:Int = width
        Local H:Int = height
		
        AdjustTexSize(W , H) 

        glGenFramebuffersEXT(1, fb )
        glGenRenderbuffersEXT(1 , rb) 
	   
        glBindTexture(GL_TEXTURE_2D, glTextureId);
        glBindFramebufferEXT(GL_FRAMEBUFFER_EXT , fb[0]) ; 
	   	   
        glFramebufferTexture2DEXT(GL_FRAMEBUFFER_EXT, GL_COLOR_ATTACHMENT0_EXT, GL_TEXTURE_2D,  glTextureId, 0);
        glBindRenderbufferEXT(GL_RENDERBUFFER_EXT, rb[0]);
        glRenderbufferStorageEXT(GL_RENDERBUFFER_EXT, GL_DEPTH_COMPONENT24, W, H);
        glFramebufferRenderbufferEXT(GL_FRAMEBUFFER_EXT , GL_DEPTH_ATTACHMENT_EXT , GL_RENDERBUFFER_EXT , rb[0])
	   
        Local status:Int =  glCheckFramebufferStatusEXT(GL_FRAMEBUFFER_EXT)
	   
        Select status
            Case GL_FRAMEBUFFER_COMPLETE_EXT 
                Print "OK" + " : " + Status
            Case GL_FRAMEBUFFER_UNSUPPORTED_EXT
                Print "GL Framebuffer Unsupported - choose different formats"
            Default
                End 
        EndSelect 
   
    End Method
	
    Method enableBuffer()
        GetViewport(OrigX,OrigY,OrigW,OrigH)
        glBindFramebufferEXT(GL_FRAMEBUFFER_EXT , fb[0])
        glMatrixMode GL_PROJECTION
        glLoadIdentity
        glOrtho 0,width,0,width,-1,1
        glMatrixMode GL_MODELVIEW 
        glViewport 0,0,width,height
        glScissor 0,0,width,height
    End Method
	
    Method disableBuffer()
        glBindFramebufferEXT(GL_FRAMEBUFFER_EXT , 0)
        glMatrixMode GL_PROJECTION
        glLoadIdentity
        glOrtho 0,OrigW,Origh,0,-1,1
        glMatrixMode GL_MODELVIEW 
        glViewport 0,0,OrigW,OrigH
        glScissor 0,0,OrigW,OrigH
    End Method
	
End Type

Function AdjustTexSize( width:Int Var,height:Int Var )
   'calc texture size
    width=Pow2Size( width )
    height=Pow2Size( height )
    Repeat
        Local t:Int
        glTexImage2D GL_PROXY_TEXTURE_2D,0,4,width,height,0,GL_RGBA,GL_UNSIGNED_BYTE,Null
        glGetTexLevelParameteriv GL_PROXY_TEXTURE_2D,0,GL_TEXTURE_WIDTH,Varptr t
        If t Return
        If width=1 And height=1 RuntimeError "Unable to calculate tex size"
        If width&gt;1 width:/2
        If height&gt;1 height:/2
    Forever
End Function

Function Pow2Size:Int( n:Int )
    Local t:Int=1
    While t&lt;n
        t:*2
    Wend
    Return t
End Function

' ------------------------------------------------

TImageBuffer.initGraphics(1024,768,32,0) 'Same as Graphics but set to GLDriver + glewinit

Global img:TImage = CreateImage(512,512)
MidHandleImage img

Local fbo:TImageBuffer = New TImageBuffer.Create(img)

Local xpos:Float = 0.0
Local ypos:Float = -1.5
Local zoom:Float = 1.01
Local rotation:Float = -0.5

While Not KeyHit(KEY_ESCAPE)

    ' render to texture
    fbo.enableBuffer()

    SetColor 254,255,254
    SetBlend ALPHABLEND
    SetAlpha .99
    SetScale zoom,zoom
    SetRotation rotation
    DrawImage img , 256+xpos,256+ypos ' feedback texture
    If Rnd(2) &lt; .2
        SetRotation Rand(0,1)*90
        SetScale Rnd(3),Rnd(3)
        Local x:Int = Rnd(512)
        Local y:Int = Rnd(512)
        SetColor 0,0,0
        DrawText "f e e d b a c k",x+1,y+1
        SetColor 255,255,255
        DrawText "f e e d b a c k",x,y
    End If

    fbo.disableBuffer()

    Cls
    SetRotation 0
    SetColor 255,255,255
    SetAlpha 1.0
    Local xscale:Float = GraphicsWidth()/Float(ImageWidth(img))
    Local yscale:Float = GraphicsHeight()/Float(ImageHeight(img))
    SetScale xscale,yscale
    DrawImage img , GraphicsWidth()/2,GraphicsHeight()/2 ' draw screen size quad
    SetScale 1.0,1.0
    DrawText "OpenGL Framebuffer Object (FBO) Test",80,100
    DrawText "Cursors keys - move feedback area, W &amp; S - zoom in + out, A &amp; D - rotate",80,120
    Flip True

     xpos :+ ((KeyDown(KEY_LEFT)-KeyDown(KEY_RIGHT))*.25)
     ypos :+ ((KeyDown(KEY_UP)-KeyDown(KEY_DOWN))*.25)
     zoom :+ ((KeyDown(KEY_W)-KeyDown(KEY_S))*.0025)
     rotation :+ ((KeyDown(KEY_A)-KeyDown(KEY_D))*.04)

Wend
End
</textarea><br><br>*Updated - included y mirror fix. Thanks for that GW.<br>**Updated - added some interactivity. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GW</td><td align="right"><font class=tiny>2011</font></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>Changing the glortho command in 'Enablebuffer' to <br>glortho 0,width,0,width,-1,1<br>should fix the y flipping issue<br> <br></div><br>I spoke too soon, <br>I think the real way to fix it is to to do a translation when you bind and unbind the buffer.<br>After glLoadIdentity add glScalef(1,-1,1.0)<br>and of course undo it in the unbind function with glScalef(1,1,1)<br><br>EDIT#2<br>It works and fixes the original FBO post, but not with the second. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
