<!DOCTYPE html><html lang="en" ><head ><title >Basic ASCII text module</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Module for drawing text using a fixed width, ASCII ordered image font., language=bmx, category=Graphics'><meta name='author' content='Oddball'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/Basic ASCII text module</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2713.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Basic ASCII text module by Oddball</td><td align="right">2010 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Module for drawing text using a fixed width, ASCII ordered image font. It's very basic, but some might find it useful.<br><br><b>Updated 18 NOV 2010:</b> Now includes block text functionality.<br><b>Updated 19 NOV 2010:</b> Draw now recognises the new line escape character(~n) and Null strings.<br><br>You can also download it <a href="http://www.gooeyblob.com/mods/asciifontmodule.zip" target="_blank">here</a> complete with example. </td></tr><tr ><td class="cell"><pre class="code">'Ascii bitmap font rendering
SuperStrict

Rem
bbdoc: Gaphics/AsciiFont
End Rem
Module ODD.AsciiFont

ModuleInfo "Version: 1.3"
ModuleInfo "Author: David Williamson"
ModuleInfo "License: Public Domain"

ModuleInfo "History: 1.3"
ModuleInfo "       : Draw now recognises the newline escape character and Null strings."
ModuleInfo "History: 1.2"
ModuleInfo "       : Added block text and line-spacing."
ModuleInfo "History: 1.1"
ModuleInfo "       : Added letter-spacing."
ModuleInfo "History: 1.0"
ModuleInfo "       : Initial release."

Import BRL.Max2D

Const ASCII_PADDED:Int=16
Const ASCII_EXTENDED:Int=32

Const ALIGN_LEFT:Int=-1
Const ALIGN_RIGHT:Int=1
Const ALIGN_TOP:Int=-1
Const ALIGN_BOTTOM:Int=1
Const ALIGN_CENTER:Int=0

Type TAsciiFont
	Field img:TImage, _width:Int, _height:Int, _letterSpacing:Int, _lineSpacing:Int
	Field frontPad:Int, extended:Int
	
	Rem
	bbdoc: Loads an ascii font from an image file
	returns: An ascii font object
	about: Loads a fixed width ascii ordered font for rendering.
	
	@flags are the standard #LoadImage flags with two ascii font specific additions
	[ @ASCII_PADDED | The image contains the first 32 unprintable characters
	* @ASCII_EXTENDED | The image contains the extended ascii characters(128-255)
	]
	End Rem
	Function Load:TAsciiFont( url:Object, width:Int, height:Int, flags:Int=0 )
		Local af:TAsciiFont=New TAsciiFont
		af.frontPad=flags&amp;ASCII_PADDED
		af.extended=flags&amp;ASCII_EXTENDED
		Local fCount:Int=96
		If af.frontPad Then fCount:+32
		If af.extended Then fCount:+128
		af.img=LoadAnimImage(url,width,height,0,fCount,flags)
		af._width=width;af._height=height;af._letterSpacing=0;af._lineSpacing=0
		If af.img Then Return af Else Return Null
	End Function
	
	Rem
	bbdoc: Sets the font's letter-spacing and line-spacing
	about: Sets the letter-spacing and line-spacing of the font. A negative spacing will cause the letters to overlap.
	EndRem
	Method SetSpacing( letterSpacing:Int, lineSpacing:Int=0 )
		_letterSpacing=letterSpacing
		_lineSpacing=lineSpacing
	End Method
	
	Rem
	bbdoc: Draws text on the screen
	about: Draws text at position @x, @y. @horAlign and @vertAlign discribes how the text is aligned. The values are @ALIGN_LEFT, @ALIGN_RIGHT, @ALIGN_TOP, @ALIGN_BOTTOM, @ALIGN_CENTER.
	
	If a width, @w, is specified then the text exeeding that value will be displayed over multiple lines, otherwise text is displayed on a single line.
	
	Draw ignores the current Max2D rotation.
	EndRem
	Method Draw( text:String, x:Float, y:Float, horAlign:Int=ALIGN_LEFT, vertAlign:Int=ALIGN_TOP, w:Int=0 )
		If text="" Then Return
		If text.Contains("~n")
			Local blocks:String[]=text.Split("~n")
			Local xScale:Float, yScale:Float
			GetScale xScale, yScale
			Local h:Int[]=New Int[blocks.length]
			Local align:Float
			For Local count:Int=0 Until blocks.length
				h[count]=height(blocks[count],w)
				align:+(h[count]+_lineSpacing)*yScale
			Next
			align:-_lineSpacing*yScale
			align:*.5
			y:-(align+align*vertAlign)
			For Local count:Int=0 Until blocks.length
				Draw blocks[count],x,y,horAlign,vertAlign,w
				y:+h[count]*yScale
			Next
		ElseIf w&gt;0
			Local lineWidth:Int
			lineWidth=Floor(w/Float(_width+_letterSpacing))
			If lineWidth&lt;2 Then Return
			Local words:String[]=text.Split(" ")
			Local lines:String[]=New String[text.length]
			Local lIndex:Int=0
			Local wIndex:Int=0
			While wIndex&lt;words.length
				lines[lIndex]=words[wIndex]
				While lines[lIndex].length&gt;lineWidth
					lines[lIndex+1]=lines[lIndex][lineWidth-1..]
					lines[lIndex]=lines[lIndex][..lineWidth-1]+"-"
					lIndex:+1
				Wend
				wIndex:+1
				While wIndex&lt;words.length And lines[lIndex].length+words[wIndex].length+1&lt;lineWidth
					lines[lIndex]:+" "+words[wIndex]
					wIndex:+1
				Wend
				lIndex:+1
			Wend
			
			Local xScale:Float, yScale:Float
			GetScale xScale, yScale
			Local align:Float=(((_height+_lineSpacing)*lIndex)-_lineSpacing)*.5*yScale
			y:-(align+align*vertAlign)
			For Local count:Int=0 To lIndex
				Draw lines[count],x,y+(count*(_height+_lineSpacing)*yScale),horAlign
			Next
		Else
			Local rot:Float=GetRotation()
			Local xScale:Float, yScale:Float
			GetScale xScale, yScale
			Local fOffset:Int=0
			If Not frontPad Then fOffset=-32
			Local a:Int
			Local align:Float=Width(text)*.5*xScale
			x:-(align+align*horAlign)
			align=_height*.5*yScale
			y:-(align+align*vertAlign)
			For Local count:Int=0 Until text.length
				a=text[count]
				If Not(extended) And a&gt;=128 Then Continue
				DrawImage img,x+(count*(_width+_letterSpacing)*xScale),y,a+fOffset
			Next
			SetRotation rot
		EndIf
	End Method
	
	Rem
	bbdoc: Gets the total width of text
	returns: The pixel width of text
	about: If @text is blank then #Width returns the width of a single character.
	EndRem
	Method Width:Int( text:String="" )
		If text Then Return ((_width+_letterSpacing)*text.length)-_letterSpacing Else Return _width
	End Method
	
	Rem
	bbdoc: Gets the total height of text
	returns: The pixel height of text
	about: If @text is blank or @w=0 then #Height returns the height of a single character.
	EndRem
	Method Height:Int( text:String="", w:Int=0 )
		If text="" Or w=0 Then Return _height
		
		If text.Contains("~n")
			Local blocks:String[]=text.Split("~n")
			Local h:Int
			For Local count:Int=0 Until blocks.length
				height(blocks[count],w)
				h:+height(blocks[count],w)+_lineSpacing
			Next
			Return h-_lineSpacing
		Else
			Local lineWidth:Int
			lineWidth=Floor(w/Float(_width+_letterSpacing))
			If lineWidth&lt;2 Then Return 0
			Local words:String[]=text.Split(" ")
			Local lines:String[]=New String[text.length]
			Local lIndex:Int=0
			Local wIndex:Int=0
			While wIndex&lt;words.length
				lines[lIndex]=words[wIndex]
				While lines[lIndex].length&gt;lineWidth
					lines[lIndex+1]=lines[lIndex][lineWidth-1..]
					lines[lIndex]=lines[lIndex][..lineWidth-1]+"-"
					lIndex:+1
				Wend
				wIndex:+1
				While wIndex&lt;words.length And lines[lIndex].length+words[wIndex].length+1&lt;lineWidth
					lines[lIndex]:+" "+words[wIndex]
					wIndex:+1
				Wend
				lIndex:+1
			Wend
			Return ((_height+_lineSpacing)*lIndex)-_lineSpacing
		EndIf
	End Method
End Type

Rem
bbdoc: Loads an ascii font from an image file
returns: An ascii font object
about: Loads a fixed width ascii ordered font for rendering.

@flags are the standard #LoadImage flags with two ascii font specific additions
[ @ASCII_PADDED | The image contains the first 32 unprintable characters
* @ASCII_EXTENDED | The image contains the extended ascii characters(128-255)
]
End Rem
Function LoadAsciiFont:TAsciiFont( url:Object, width:Int, height:Int, flags:Int=0 )
	Return TAsciiFont.Load(url,width,height,flags)
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Oddball</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> An example of it in use<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

Import ODD.AsciiFont

Graphics 800,600

Local font:TAsciiFont=TAsciiFont.Load("asciifont.png",8,8,ASCII_PADDED|ASCII_EXTENDED)

Repeat
	Cls
	SetScale 1,1
	SetColor 255,255,255
	font.Draw "POSITION 0,0 WITH TOP/LEFT ALIGNMENT",0,0,ALIGN_LEFT,ALIGN_TOP
	font.Draw "POSITION 400,300 WITH CENTER/CENTER ALIGNMENT",400,300,ALIGN_CENTER,ALIGN_CENTER
	font.Draw "POSITION 800,600 WITH BOTTOM/RIGHT ALIGNMENT",800,600,ALIGN_RIGHT,ALIGN_BOTTOM
	SetScale 2,2
	SetColor 255,127,127
	font.Draw "X2 SCALED RED TEXT",400,50,ALIGN_CENTER,ALIGN_TOP
	SetScale 3,3
	SetColor 127,255,127
	font.Draw "X3 SCALED GREEN TEXT",400,70,ALIGN_CENTER,ALIGN_TOP
	SetScale 4,4
	SetColor 127,127,255
	font.Draw "X4 SCALED BLUE TEXT",400,100,ALIGN_CENTER,ALIGN_TOP
	
	Flip
Until KeyHit(KEY_ESCAPE) Or AppTerminate()</textarea><br>Requires an ascii font image like the one below.<br><a href="http://www.gooeyblob.com/temp/asciifont.png" target="_blank">asciifont.png<br><img src="http://www.gooeyblob.com/temp/asciifont.png"></a> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Oddball</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> I have updated this to include block text functionality. Here's an example of it in use<textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

Import ODD.AsciiFont

Graphics 800,600

Local font:TAsciiFont=TAsciiFont.Load("asciifont.png",8,8,ASCII_PADDED|ASCII_EXTENDED)

Repeat
	Cls
	SetScale 1,1
	SetColor 255,255,255
	font.Draw "POSITION 0,0 WITH TOP/LEFT ALIGNMENT",0,0,ALIGN_LEFT,ALIGN_TOP
	font.Draw "POSITION 400,300 WITH CENTER/CENTER ALIGNMENT",400,300,ALIGN_CENTER,ALIGN_CENTER
	font.Draw "POSITION 800,600 WITH BOTTOM/RIGHT ALIGNMENT",800,600,ALIGN_RIGHT,ALIGN_BOTTOM
	font.Draw "THIS IS A BLOCK OF TEXT AT POSITION 400,400 WITH TOP/CENTER ALIGNMENT AND A WIDTH OF 300. TEXT BLOCKS AUTOMATICALLY ALIGN LINES OF TEXT FOR YOU GIVEN A SET WIDTH.",400,400,ALIGN_CENTER,ALIGN_TOP,300
	SetScale 2,2
	SetColor 255,127,127
	font.Draw "X2 SCALED RED TEXT",400,50,ALIGN_CENTER,ALIGN_TOP
	SetScale 3,3
	SetColor 127,255,127
	font.Draw "X3 SCALED GREEN TEXT",400,70,ALIGN_CENTER,ALIGN_TOP
	SetScale 4,4
	SetColor 127,127,255
	font.Draw "X4 SCALED BLUE TEXT",400,100,ALIGN_CENTER,ALIGN_TOP
	
	Flip
Until KeyHit(KEY_ESCAPE) Or AppTerminate()</textarea><br>Requires the same font image in the post above. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Oddball</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> Another update. Draw now recognises the new line escape character(~n) and Null strings. And here's the obligatory example of it in use<textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

Import ODD.AsciiFont

Graphics 800,600

Local font:TAsciiFont=TAsciiFont.Load("asciifont.png",8,8,ASCII_PADDED|ASCII_EXTENDED)

Local textBlock:String="THIS IS A BLOCK OF TEXT AT POSITION 400,400 WITH TOP/CENTER ALIGNMENT AND A WIDTH OF 300. TEXT BLOCKS AUTOMATICALLY BREAK STRINGS INTO MULTIPLE LINES OF TEXT FOR YOU GIVEN A SET WIDTH, AND ALIGNS THEM FOR YOU.~n~nNOW THEY ALSO RECOGNISE THE NEW LINE ESCAPE CHARACTER, AND NULL STRINGS."

Repeat
	Cls
	SetScale 1,1
	SetColor 255,255,255
	font.Draw "POSITION 0,0 WITH TOP/LEFT ALIGNMENT",0,0,ALIGN_LEFT,ALIGN_TOP
	font.Draw "POSITION 400,300 WITH CENTER/CENTER ALIGNMENT",400,300,ALIGN_CENTER,ALIGN_CENTER
	font.Draw "POSITION 800,600 WITH BOTTOM/RIGHT ALIGNMENT",800,600,ALIGN_RIGHT,ALIGN_BOTTOM
	SetColor 64,64,64
	DrawRect 245,395,310,font.Height(textBlock,300)+10
	SetColor 255,255,255
	font.Draw textBlock,400,400,ALIGN_CENTER,ALIGN_TOP,300
	SetScale 2,2
	SetColor 255,127,127
	font.Draw "X2 SCALED RED TEXT",400,50,ALIGN_CENTER,ALIGN_TOP
	SetScale 3,3
	SetColor 127,255,127
	font.Draw "X3 SCALED GREEN TEXT",400,70,ALIGN_CENTER,ALIGN_TOP
	SetScale 4,4
	SetColor 127,127,255
	font.Draw "X4 SCALED BLUE TEXT",400,100,ALIGN_CENTER,ALIGN_TOP
	
	Flip
Until KeyHit(KEY_ESCAPE) Or AppTerminate()</textarea> <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
