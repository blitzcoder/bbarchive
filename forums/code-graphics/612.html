<!DOCTYPE html><html lang="en" ><head ><title >Gaussian Blur</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='A gaussian blur function, language=bb, category=Graphics'><meta name='author' content='elias_t'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=1>Graphics</a>/Gaussian Blur</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="612.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gaussian Blur by elias_t</td><td align="right">2003 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Yoda. </td></tr><tr ><td class="cell"><pre class="code">;GAUSSIAN BLUR FUNCTION
;by elias_t
;----------------------


;example
Graphics 640,480,32,2

image=LoadImage("frame.png");your image here 

image=gaussian_blur(image,5,5);[3,5,7,9]

DrawImage image,0,0
Flip

WaitKey()
End







;needed arrays
Dim maskData#(0)
Dim texel#(0,0,0)
Dim result#(0,0,0)

;ww and hh values
;should always be uneven numbers, [3,5,7,9]

Function gaussian_blur(input_image,ww,hh)

Width=ImageWidth(input_image)
Height=ImageHeight(input_image)

Dim texel#(Width,Height,2) 
Dim result#(Width,Height,2) 

SetBuffer ImageBuffer (input_image)
LockBuffer ImageBuffer (input_image)

For x=0 To Width-1
 For y=0 To Height-1
  rgb=ReadPixelFast (x,y,ImageBuffer(input_image))
  texel#(x,y,0)=(rgb Shr 16) And $ff;
  texel#(x,y,1)=(rgb Shr 8) And $ff;
  texel#(x,y,2)=rgb And $ff;
 Next
Next


maskWidth#  = ww;
maskHeight# = hh;

Dim maskData#(maskWidth#*maskHeight#);

mult# = 0.0;

For  ym# = 0  To   maskHeight#-1
	For xm# = 0  To  maskWidth#-1
		 cx# = xm# - (maskWidth# - 1) / 2.0;
		 cy# = ym# - (maskHeight# - 1) / 2.0;
		 rt# = cx# * cx# + cy# * cy#;
		 mult# =mult#+ Exp(-0.35 * rt#);
	Next 
Next 

mult# = 1.0 / mult#;

For ym# = 0 To  maskHeight#-1
	For xm# = 0  To  maskWidth#-1
		cx# = xm# - (maskWidth# - 1) / 2.0;
		cy# = ym# - (maskHeight# - 1) / 2.0;
		rt# = cx# * cx# + cy# * cy#;
		maskData#(ym# * maskWidth# + xm#) = mult# * Exp(-0.35 * rt#);
	Next 
Next 


Dim result#(Width,Height,3)

For ym# = 0 To Height-1
	For  xm# = 0 To Width-1
		rr#=0.0;
		gg#=0.0;
		bb#=0.0;
		For yy = 0 To maskHeight# -1
			For xx = 0 To maskWidth#-1  
				If (xm + xx - Floor(maskWidth# / 2.0)&lt;0) Or (ym + yy - Floor(maskHeight# / 2)&lt;0) Or (xm + xx - Floor(maskWidth# / 2.0)&gt;Width) Or (ym + yy - Floor(maskHeight# / 2)&gt;Height)
					rl#=0;
					gl#=0;
					bl#=0;
				Else
					rl#=texel#(xm + xx - Floor(maskWidth# / 2.0),	ym + yy - Floor(maskHeight# / 2),0)
					gl#=texel#(xm + xx - Floor(maskWidth# / 2.0),	ym + yy - Floor(maskHeight# / 2),1)
					bl#=texel#(xm + xx - Floor(maskWidth# / 2.0),	ym + yy - Floor(maskHeight# / 2),2)
				EndIf 
					
				rr# =rr#+rl# * maskData#(xx + yy * maskWidth#);
				gg# =gg#+gl# * maskData#(xx + yy * maskWidth#);
				bb# =bb#+bl# * maskData#(xx + yy * maskWidth#);
			Next
		Next
		result#(xm#,ym#,0)=rr#;
		result#(xm#,ym#,1)=gg#;
		result#(xm#,ym#,2)=bb#;
	Next
Next




For x=0 To Width-1
 For y=0 To Height-1
  WritePixelFast ( x,y,((result#(x,y,0) Shl 16)+(result#(x,y,1) Shl 8)+result#(x,y,2)),ImageBuffer(input_image) )
 Next
Next

UnlockBuffer ImageBuffer(input_image)

SetBuffer BackBuffer()

Dim maskData#(0)
Dim texel#(0,0,0)
Dim result#(0,0,0)

Return input_image

End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Andy_A</td><td align="right"><font class=tiny>2015</font></td></tr></table></td></tr><tr ><td class="posttext"> I don't know if anyone has tried this, but I found it to be extremely slow. More than 16 seconds to perform the blur when not in Debug mode on a 1024x768 image.<br><br>After looking at the code, I found that some portions (inner loops) were good places to optimize. After the optimizations it only takes about 1 second (1024x768 image) to perform the blur.<br><br>Included is a default demo screen to see the Gaussian blur in action. To use your own image set the 'blitzVersion' variable to 1 or 2  for BlitzPlus or Blitz3D respectively, otherwise the default image will be displayed.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">;---------------------------------
;GAUSSIAN BLUR FUNCTION
;by elias_t
;---------------------------------
;Speed optimization of over 800%
;by Andres Amaya Jr.
;---------------------------------

AppTitle "Gaussian Blur Function by elias_t"
Global sw%, sh%
sw = 1024	;screen width
sh =  768	;screen height

;required arrays
Dim maskData#(0)
Dim texel#(0,0,0)
Dim result#(0,0,0)


Dim a#(2),b#(2),c#(2),d#(2),e#(2),f#(2)
Dim p#(2)
Dim pr%(24), pg%(24), pb%(24), hxc%(24)
Dim hx8$(3)
palette()

Graphics sw,sh,32,2
Cls


;=================================================================
;Set version number for Blitz3D/Classic or BlitzPlus or Default
;=================================================================
	;set to 0 for Default  set to 1 for B+  anything else for B3D
	blitzVersion = 0
;=================================================================

;create default image to perform Gaussian blur
If blitzVersion = 0 Then image = makeImage()

;load image using file requestor (B+)
If blitzVersion = 1 Then 
	fileName$ = RequestFile("Load image...","bmp;*.png;*.jpg",False)
	If fileName$ = "" Then RuntimeError "Image load cancelled."
	image = LoadImage(filename$)
	If image = 0 Then RuntimeError "Image file not found."
End If

;load image using explicit file name (B3D)
If blitzVersion &lt;&gt; 0 And blitzVersion &lt;&gt; 1 Then
	;===========================================================
	;	If Blitz3D or Classic then assign file name here.
	;===========================================================
	image = LoadImage("[your image here].png")
	If image = 0 Then RuntimeError "Image file not found."
End If

picFlag = 1

DrawBlock image,0,0
Color 255,255,255
Text 512,sh-20,"Click to perform Gaussian Blur",True
Flip
WaitMouse()


st = MilliSecs()
image = gaussian_blur(image, 1, 1)	;Valid arg values are [1,2,3,4]
picFlag = 1
Cls
DrawBlock image,0,0
Color 215,215,215
Rect 5, sh-24, 80,25
Color 0,48,160
Text 5, sh-20, "et:"+(MilliSecs()-st)+"ms"
Flip

WaitMouse()
If picFlag = 1 Then FreeImage image: picFlag = 0
End

Function gaussian_blur(input_image,ww,hh)
	;ww and hh values
	;must always be between 1 and 4 (inclusive)
	If ww&gt;0 And ww&lt;5 And hh&gt;0 And hh&lt;5 Then
		ww = ww + ww + 1
		hh = hh + hh + 1
	Else
		RuntimeError "Blur radius invalid. Must be 1 - 4."
		FreeImage image
		End
	End If

	Width=ImageWidth(input_image)
	Height=ImageHeight(input_image)

	Dim texel#(Width,Height,2) 
	Dim result#(Width,Height,2) 

	SetBuffer ImageBuffer (input_image)
	LockBuffer ImageBuffer (input_image)

	For x=0 To Width-1
		For y=0 To Height-1
			rgb=ReadPixelFast (x,y,ImageBuffer(input_image))
			texel#(x,y,0)=(rgb Shr 16) And $ff
			texel#(x,y,1)=(rgb Shr 8) And $ff
			texel#(x,y,2)=rgb And $ff
		Next
	Next

	maskWidth#  = ww
	maskHeight# = hh

	Dim maskData#(maskWidth#*maskHeight#)
	mult# = 0.0
	;======================================================================
	;These values remain constant during this first step
	;======================================================================
	kmaskW# = ( maskWidth# - 1.0)/2.0
	kmaskH# = (maskHeight# - 1.0)/2.0
	;======================================================================
	For  ym# = 0  To   maskHeight#-1
		;==================================================================
		; Take these two calcs out of the inner loop
		;==================================================================
		cy# = ym# - kmaskH#
		cy22# = cy# * cy#
		;==================================================================
		For xm# = 0  To  maskWidth#-1
			 cx# = xm# - kmaskW#
			 rt# = cx# * cx# + cy22#
			 mult# =mult#+ Exp(-0.35 * rt#)
		Next 
	Next 
	;======================================================================
	;This value remains constant during this next step
	;======================================================================
	mult# = 1.0 / mult#
	;======================================================================
	For ym# = 0 To  maskHeight#-1
		;==================================================================
		; Take these three calcs out of the inner loop
		;==================================================================
		cy# = ym# - kmaskH#
		ymMaskW# = ym# * maskWidth#
		cy22# = cy# * cy#
		;==================================================================
		For xm# = 0  To  maskWidth#-1
			cx# = xm# - kmaskW#
			rt# = cx# * cx# + cy22#
			maskData#(ymMaskW# + xm#) = mult# * Exp(-0.35 * rt#)
		Next 
	Next 

	Dim result#(Width,Height,3)
	;======================================================================
	; These two values remain constant during this most intensive step.
	; Since Blitz is NOT an optimizing compiler, this helps immensely.
	;======================================================================
	kmw = Floor(maskWidth#/2.0)
	kmh = Floor(maskWidth#/2.0)
	;======================================================================
	For ym# = 0 To Height-1
		For xm# = 0 To Width-1
			rr#=0.0;
			gg#=0.0;
			bb#=0.0;
			For yy = 0 To maskHeight# -1
				;==========================================================
				; take this calc out of the inner-most loop
				;==========================================================
				kymyy = ym+yy-kmh  ;&lt;-----------------------------------------+
				;==========================================================  ;|
				For xx = 0 To maskWidth#-1	  								 ;|
					kxmxx = xm+xx-kmw		  								 ;|
					;kymyy = ym+yy-kmh ;--------------------------------------+
	
					If (kxmxx &lt;0)Or (kymyy &lt;0) Or (kxmxx &gt;Width) Or (kymyy &gt;Height)
						rl#=0;
						gl#=0;
						bl#=0;
					Else
						rl#=texel#(kxmxx, kymyy, 0)
						gl#=texel#(kxmxx, kymyy, 1)
						bl#=texel#(kxmxx, kymyy, 2)
					EndIf 

					kxxyy = xx + yy * maskWidth#
					rr# =rr#+rl# * maskData#(kxxyy )
					gg# =gg#+gl# * maskData#(kxxyy )
					bb# =bb#+bl# * maskData#(kxxyy )
				Next
			Next
			result#(xm#,ym#,0) = rr#
			result#(xm#,ym#,1) = gg#
			result#(xm#,ym#,2) = bb#
		Next
	Next
	For x=0 To Width-1
		For y=0 To Height-1
			WritePixelFast ( x,y,((result#(x,y,0) Shl 16)+(result#(x,y,1) Shl 8)+result#(x,y,2)),ImageBuffer(input_image) )
		Next
	Next
	UnlockBuffer ImageBuffer(input_image)

	SetBuffer BackBuffer()

	Dim maskData#(0)	;free memory allocated to arrays
	Dim texel#(0,0,0)
	Dim result#(0,0,0)

	Return input_image
End Function


Function makeImage()
	Local k%, j%, i%, xp%, yp%, clr%, rep%, xOffset%, yOffset%, numRows%
	Local xMult#, yMult#, xalt#, yalt#, x#, y#, pct#, cx#, cy#, band#, coff#

	; aa078.ifs
	; Andres Amaya Jr  2015.02.18	
	a(1)=0.6: b(1)= 0.5: c(1)=-0.3: d(1)= 0.6: e(1)=-1.1: f(1)=-0.3: p(1)=0.5368
	a(2)=0.4: b(2)=-0.4: c(2)=-0.6: d(2)=-0.5: e(2)= 0.3: f(2)=-0.4: p(2)=0.4632
	xMult=275.: yMult=330.: xOffset=763: yOffset=-337

;	; aa305.ifs
;	; Andres Amaya Jr  2015.02.19
;	a(1)=-0.5: b(1)=-0.1: c(1)=0.2: d(1)=-0.3: e(1)=-0.2: f(1)=-0.9: p(1)=0.1683
;	a(2)= 0.6: b(2)=-0.6: c(2)=0.4: d(2)= 1.0: e(2)=-1.1: f(2)= 0.5: p(2)=0.8317
;	xMult=240.: yMult=290.: xOffset=707: yOffset=-575

	
	numRows=2: cx = 512.0: cy = 384.0: band = 23.0: coff = 15.0
	xalt=0.5 : yalt=0.5
	LockBuffer()
	For k = 1 To 30000
		For j = 1 To 10
			pct=Rnd(0.0, 1.0)
			For i = 1 To numRows
				If pct&gt;p(i-1) And pct &lt;= p(i) Then Exit
			Next
			If i &gt; numRows Then i = numRows
			x = a(i) * xalt + b(i) * yalt + e(i)
			y = c(i) * xalt + d(i) * yalt + f(i)
			xp = Int(x*xMult) + xOffset 
			yp = (768-Int(y*yMult)) + yOffset
			clr = Int(Sqr((cx-xp)*(cx-xp) + (cy-yp)*(cy-yp))/band + coff) Mod 24
			If xp&gt;=0 And xp&lt;sw And yp&gt;=0 And yp&lt;sh Then
				WritePixelFast(xp,yp,hxc(clr))
			End If
	        xalt = x : yalt = y
    	Next
	Next
	UnlockBuffer()
	SetBuffer BackBuffer()
	Flip
	image = CreateImage(sw, sh)
	GrabImage image,0,0
	Return image
End Function


Function h2d$(hx$)
	Local hi%, lo%
	hx$ = Upper(hx$)
	hi = Asc( Left(hx$,1))-48
	lo = Asc(Right(hx$,1))-48
	If hi&gt;10 Then hi=hi-7
	If lo&gt;10 Then lo=lo-7
	Return hi Shl 4 Or lo
End Function

Function palette()
	Local i%, j%, count%
	Local triplet$
	hx8$(1)="0000FF4000FF7D00FFBE00FFFF00FFFF00BEFF007DFF0040"
	hx8$(2)="FF0000FF4000FF7D00FFBE00FFFF00BEFF007DFF0040FF00"
	hx8$(3)="00FF0000FF4000FF7D00FFBE00FFFF00BEFF007DFF0040FF"
	count = 0
	For i = 1 To 3
		For j = 1 To 43 Step 6
			triplet$ = Mid(hx8$(i),j,6)
			pr(count) = h2d$(Mid(triplet$,1,2))
			pg(count) = h2d$(Mid(triplet$,3,2))
			pb(count) = h2d$(Mid(triplet$,5,2))
			hxc(count) = pr(count) Shl 16 Or pg(count) Shl 8 Or pb(count)
			count = count + 1
		Next
	Next
End Function</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>2015</font></td></tr></table></td></tr><tr ><td class="posttext"> Wow, it was extremely slow before -- nice speedup! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Andy_A</td><td align="right"><font class=tiny>2015</font></td></tr></table></td></tr><tr ><td class="posttext"> I don't remember the exact number but the number of extraneous calculations in the inner loop was in the millions. <br><br>Yes, it made a big difference in speed. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
