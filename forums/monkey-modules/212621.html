<!DOCTYPE html><html lang="en" ><head ><title >Async Image(Down)loader</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Async Image(Down)loader</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=524" >User Modules</a>/<a href="#bottom" >Async Image(Down)loader</a><br><br>
<a name="2123869"></a>

<a name="2123870"></a>

<a name="2123871"></a>

<a name="2123872"></a>

<a name="2123873"></a>

<a name="2123874"></a>

<a name="2123875"></a>

<a name="2123876"></a>

<a name="2123877"></a>

<a name="2123878"></a>

<a name="2123879"></a>

<a name="2123881"></a>

<a name="2123883"></a>

<a name="2123884"></a>

<a name="2123886"></a>

<a name="2123887"></a>

<a name="2123888"></a>

<a name="2123903"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >k.o.g.</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello All<br><br>Here is a version of my current Code, from my Async Image (Down)loader.<br><br>AsyncImageLib.js<br><a href="http://kognetwork.ch/monkeyview/?file=monkeyview/imageasync/AsyncImageLib.js" target="_blank">http://kognetwork.ch/monkeyview/?file=monkeyview/imageasync/AsyncImageLib.js</a><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">BBAsyncImageLoaderThread.prototype.Start = function(){
	var thread=this;

	thread._surface=null;
	thread._result=false;
	thread._running=true;

	var image=new Image();

	image.onload=function( e ){
		image.meta_width=image.width;
		image.meta_height=image.height;
		thread._surface=new gxtkSurface( image,thread._device )
		thread._result=true;
		thread._running=false;
	};
	
	image.onerror=function( e ){
		thread._running=false;
	};
	image.crossOrigin = "anonymous";
	image.src=BBGame.Game().PathToUrl( thread._path );
};</textarea><br><br>AsyncImageLib.monkey<br><a href="http://kognetwork.ch/monkeyview/?file=monkeyview/imageasync/AsyncImageLib.monkey" target="_blank">http://kognetwork.ch/monkeyview/?file=monkeyview/imageasync/AsyncImageLib.monkey</a><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' Author: k.o.g. 2016
Strict

Private
Import mojo.asyncloaders
Import mojo.graphics

#If TARGET = "html5" Then
Import "AsyncImageLib.js"
	
#Else
Import brl.socket
Import brl.url
Import brl.filestream
Import brl.filesystem
	
#End

Public

#If TARGET = "html5" Then
Class AsyncImage Implements IOnLoadImageComplete
	Private
#Else
Class AsyncImage Implements IOnLoadImageComplete, IOnConnectComplete, IOnSendComplete, IOnReceiveComplete
	Private
	Field _socket:Socket = Null
	Field _url:Url = Null
	Field _bytesReceived:Int = 0
	Field _buffer:DataBuffer = Null
	Field _headers:StringStack = Null
	Field _etag:String = ""
	
#End
	Field _failedCode:Int = 0
	Field _instance:Image
	Field _saveDirectory:String = ""
	Field _path:String = ""
	Field _originalPath:String = ""
	Field _savePath:String = ""
	
	Field _rows:Int = 0
	Field _columns:Int = 0
	Field _flags:Int = Image.DefaultFlags
	Field _frameWidth:Int = 0
	Field _frameHeight:Int = 0
	Field _frameCount:Int = 1
	
	Method OnLoadImageComplete:Void ( image:Image, path:String, source:IAsyncEventSource )
		_instance = image
		
		If _instance = Null Then
			_failedCode = -5
		Else If _frameWidth &gt; 0 And _frameHeight &gt; 0 Then
			_rows = _instance.Width() / _frameWidth
			_columns = _instance.Height() / _frameHeight
		End
	End
	
	#If TARGET &lt;&gt; "html5" Then
	Method Finish:Void()
		_path = _savePath
		If _buffer Then
			If _bytesReceived &gt;= _buffer.Length Then
				Local strip:Int = 0
				If _saveDirectory.StartsWith("monkey://") Then
					strip += 9
				End
				If _saveDirectory.StartsWith("monkey://internal/") &gt; -1 Then
					strip += 9
				Else If _saveDirectory.StartsWith("monkey://data/") &gt; -1 Then
					strip += 5
				End
				
				Local base:String = _saveDirectory[0..strip]
				Local check:String = _saveDirectory[strip ..]
				Local dirOk:Bool = True
				If check.Length &gt; 0 Then
					If check.EndsWith("/") check = check[0 .. - 1]
					Local chunks:String[] = check.Split("/")
					
					For Local dir:String = Eachin chunks
						base += dir
						If CreateDir(base) = False dirOk = False
						base += "/"
					Next
				End
				
				If dirOk Then
					Local stream:FileStream = FileStream.Open(_path, "w")
					If stream Then
						stream.WriteAll(_buffer, 0, _buffer.Length())
						stream.Close()
						
						If _etag.Length() &gt; 0 Then
							stream = FileStream.Open(_path + ".etag", "w")
							stream.WriteByte($FE) ' FAKE :D
							stream.WriteByte($FF)
							stream.WriteString(_etag)
							stream.Close()
						End
					Else
						DebugLog("[AsyncImage] Failed to create image file")
						_failedCode = -6
					End
				Else
					DebugLog("[AsyncImage] Failed to create directory " + base)
					_failedCode = -7
				End
			Else
				_failedCode = -8
			End
			
			_buffer.Discard()
		End
		If _headers _headers.Clear()
		If _socket _socket.Close()
		
		_headers = Null
		_socket = Null
		_buffer = Null
			
		' All fine?
		If _failedCode = 0 LoadImage()
	End
	
	' Modified Version from HttpRequest
	Method OnConnectComplete:Void( connected:Bool, source:Socket )
		If Not connected
			_failedCode = -4
			Finish()
			Return
		End
		
		Local stream:FileStream = Null
		Try
			stream = FileStream.Open(_savePath + ".etag", "r")
		Catch e:Throwable
		End
		If stream Then
			Local etag:String = ""
			Try
				stream.Seek(2)
				etag = stream.ReadString().Trim()
			Catch e:Throwable
			End
			stream.Close()
			
			If etag.Length() &gt; 0 _headers.Push("If-None-Match: ~q" + etag + "~q")
		End
		
		_headers.Push("")
		_headers.Push("")
		
		Local t:String = _headers.Join("~r~n")
		Local buffer:DataBuffer = New DataBuffer(t.Length() * 2)
		Local n:Int = buffer.PokeString( 0, t, "ascii")
		
		_headers.Clear()
		_socket.SendAsync(buffer, 0, n, Self)
	End
	
	Method OnSendComplete:Void( buf:DataBuffer, offset:Int, count:Int, source:Socket )
		buf.Discard()
		
		BufferSize = Max(BufferSize, 128)
		
		Local rbuf:DataBuffer = New DataBuffer(BufferSize)
		_socket.ReceiveAsync(rbuf, 0, rbuf.Length(), Self)
	End
	
	Method OnReceiveComplete:Void( buf:DataBuffer, offset:Int, count:Int, source:Socket )
		If count &lt;= 0 Then
			buf.Discard()
			Finish()
			Return
		End
		
		If _buffer Then
			buf.CopyBytes(offset, _buffer, _bytesReceived, count)
			_bytesReceived += count
			
			If _bytesReceived &gt;= _buffer.Length() Then
				' Close
				OnReceiveComplete(buf, 0, 0, source)
			Else
				_socket.ReceiveAsync( buf, 0, buf.Length, Self )
			End
			Return
		End
		
		Local start:Int = 0
		Local i:Int = offset
		Local term:Int = i+count
		
		While i &lt; term
			i += 1
			If buf.PeekByte(i-1) &lt;&gt; 10 Continue
			
			Local len:Int = i - start - 1
			Local t:String = buf.PeekString(start, len, "ascii").Trim()
			start = i
			
			If t Then
				_headers.Push(t)
				Continue
			Endif
			
			If _headers.Length Then
				Local bits:String[] = _headers.Get( 0 ).ToUpper().Split(" ")
				If bits.Length &gt; 2 And bits[0].StartsWith( "HTTP/" ) Then
					Local status:Int = Int( bits[1] )
				
					If status = 200 Then
						' OK
					Else
						If status &lt;&gt; 304 _failedCode = -1
						OnReceiveComplete(buf, 0, 0, source)
						Return
					End
				End
				
				Local length:Int = 0
				For Local header:String = Eachin _headers
					Local chunk:String[] = header.Split(":")
					If chunk.Length &lt; 2 Continue
					
					chunk[1] = chunk[1].Trim()
					Select chunk[0].Trim().ToLower()
						Case "content-length"
							length = Int(chunk[1].Trim())
							
						Case "etag"
							_etag = chunk[1]
							If _etag.StartsWith("~q") Then
								_etag = _etag[1..-1]
							End
					End
				Next
				
				'ERROR!
				If length &lt;= 0 Then
					_failedCode = -2
					OnReceiveComplete(buf, 0, 0, source)
					Return
				End
				
				_buffer = New DataBuffer(length)
				If start &lt; term Then
					buf.CopyBytes(start, _buffer, _bytesReceived, term - start)
					_bytesReceived += term - start
				Endif
				_socket.ReceiveAsync(buf, 0, buf.Length, Self)
				
				Return
			End
			
			
			'ERROR!
			_failedCode = -3
			OnReceiveComplete(buf, 0, 0, source)
			Return
			
		Wend
		
		Local n:Int = term - start
		If start buf.CopyBytes(start, buf, 0, n)
		_socket.ReceiveAsync(buf, n, buf.Length - n, Self)
	End
	'------------
	#End

	Method LoadImage:Void()
		If _frameWidth &gt; 0 And _frameHeight &gt; 0 Then
			Local flags:Int = _flags
			If flags &amp; Image.MidHandle = Image.MidHandle flags ~= Image.MidHandle
			LoadImageAsync(_path, 1, flags, Self)
		Else
			LoadImageAsync(_path, _frameCount, _flags, Self)
		End
	End

	Method StartLoading:Void(path:String)

		_path = path
		_originalPath = path
		
		#If TARGET &lt;&gt; "html5" Then
		If SaveDirectory.StartsWith("monkey://") = False Then
			SaveDirectory = "monkey://internal/" + SaveDirectory
		End
		If SaveDirectory[SaveDirectory.Length - 1] &lt;&gt; "/"[0] Then
			SaveDirectory += "/"
		End
		_savePath = path
		_saveDirectory = SaveDirectory
		
		If _path.Find("http") &gt; -1 Or _path.Find("ftp") &gt; -1 Then
			_headers = New StringStack()
			_url = New Url(_path, "http", 80 )
			
			Local chunks:String[] = _url.Path().Split("/")
			Local filename:String = chunks[chunks.Length - 1]
			_savePath = _saveDirectory + filename

			_headers.Push("GET /" +_url.FullPath() + " HTTP/1.0")
			_headers.Push("Host: " + _url.Domain())
			_headers.Push("Connection: close")
			_headers.Push("Accept: image/*")
			_headers.Push("X-Monkey-X: http://www.monkey-x.com")
					
			_socket = New Socket("stream")
			_socket.ConnectAsync(_url.Domain(), _url.Port(), Self)
			Return
		End
		#End
		LoadImage()
	End

	Public
	Global BufferSize:Int = 4096
	Global SaveDirectory:String = "monkey://internal/"
	
	Method Draw:Void(x:Float, y:Float, frame:Int = 0)
		If _instance = Null Return
		
		If _frameWidth &gt; 0 And _frameHeight &gt; 0 Then
			Local srcX:Int = 0, srcY:Int = 0
			
			srcX = (frame Mod _rows) * _frameWidth
			srcY = Int(frame / _rows) * _frameHeight
			
			DrawImageRect(_instance, x, y, srcX, srcY, _frameWidth, _frameHeight)
		Else
			DrawImage(_instance, x, y, frame)
		End
	End
	
	Method Draw:Void(x:Float, y:Float, rotation:Float, scaleX:Float, scaleY:Float, frame:Int = 0)
		If _instance = Null Return
		
		If _frameWidth &gt; 0 And _frameHeight &gt; 0 Then
			Local srcX:Int = 0, srcY:Int = 0
			
			srcX = (frame Mod _rows) * _frameWidth
			srcY = Int(frame / _rows) * _frameHeight
			
			PushMatrix()
			Translate(x, y)
			Rotate(rotation)
			Scale(scaleX, scaleY)
			Translate(-_frameWidth/2, -_frameHeight/2)
			
			DrawImageRect(_instance, 0, 0, srcX, srcY, _frameWidth, _frameHeight)
			PopMatrix()
		Else
			DrawImage(_instance, x, y, rotation, scaleX, scaleY, frame)
		End
	End
	
	Method Loaded:Bool() Property
		Return _instance &lt;&gt; Null
	End
	
	Method Failed:Bool() Property
		Return _failedCode &lt;&gt; 0
	End
	
	Method FailedCode:Int() Property
		Return _failedCode
	End
	
	Function Load:AsyncImage(path:String, frameCount:Int = 1, flags:Int = Image.DefaultFlags)
		Local img:AsyncImage = New AsyncImage()
		img._frameWidth = 0
		img._frameHeight = 0
		img._frameCount = frameCount
		img._flags = flags
		
		img.StartLoading(path)

		Return img
	End
	
	Function Load:AsyncImage(path:String, frameWidth:Int, frameHeight:Int, frameCount:Int, flags:Int = Image.DefaultFlags)
		Local img:AsyncImage = New AsyncImage()
		img._frameWidth = frameWidth
		img._frameHeight = frameHeight
		img._frameCount = frameCount
		img._flags = flags
		
		img.StartLoading(path)
		
		Return img
	End
End

Function DrawImageAsync:Void(image:AsyncImage, x:Float, y:Float, frame:Int = 0)
	If image = Null Return
	
	image.Draw(x, y, frame)
End

Function DrawImageAsync:Void(image:AsyncImage, x:Float, y:Float, rotation:Float, scaleX:Float, scaleY:Float, frame:Int = 0)
	If image = Null Return
	
	image.Draw(x, y, rotation, scaleX, scaleY, frame)
End</textarea><br><br>Usage:<br><pre class=code>local image:AsyncImage = AsyncImage.Load("http://localhost:80/image.jpg")
image.Draw(x, y, frame)
image.Draw(x, y, rotation, scaleX, scaleY, rotation, frame)
image.Loaded
image.Failed
image.FailedCode

AsyncImage.SaveDirectory 'Sets the Directory for saving the downloaded image
AsyncImage.BufferSize 'Sets the Size of downloading buffer
</pre><br><br>On HTML5:<br>- The Server, which holds the Image must allow CORS Requests!<br><br>More Informations following.. <br><br></td></tr></table><br>
<a name="2123889"></a>

<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
