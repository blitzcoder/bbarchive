<!DOCTYPE html><html lang="en" ><head ><title >freeaudio streaming</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='generate dynamic waveforms and stream them directly to a freeaudio device, language=bmx, category=Audio'><meta name='author' content='skidracer'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=3>Audio</a>/freeaudio streaming</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="3129.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >freeaudio streaming by skidracer</td><td align="right">2014 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> generate dynamic waveforms and stream them directly to a freeaudio device </td></tr><tr ><td class="cell"><pre class="code">' freestream.bmx
' mono 8 bit streaming example for 

Strict
Import pub.freeaudio

Const FRAG=4096

Print "freestream is free streaming..."

fa_Init(0)	' brl.freefreeaudio usually does this

Local buffer:Byte[FRAG*8]
Local writepos

Local sound
Local channel

sound=fa_CreateSound( FRAG*8,8,1,44100,buffer,$80000000)
Print "Sound:"+sound

channel=fa_PlaySound( sound, FA_CHANNELSTATUS_STREAMING ,0)
Print "PlaySound:"+channel

Local streaming
Local lfo#
Local osc1#

While True
'	Print "Status:"+fa_ChannelStatus( channel )	
	Local readpos=fa_ChannelPosition( channel )
	Local write=readpos+FRAG*4-writepos
	Local frags=write/FRAG	
	While frags&gt;0	
		Print "Write to "+writepos
		Local pos=writepos Mod (FRAG*8)
		For Local f=0 Until FRAG				
			Local t=writepos+f	
			lfo=Sin(0.001*t)
			osc1=Sin(t*(lfo+2))
			buffer[pos+f]=128+10*lfo*osc1
		Next
		writepos:+FRAG
		frags:-1
	Wend
	
	If Not streaming And writepos&gt;=FRAG*4
		fa_SetChannelPaused( channel, False )
		streaming=True
	EndIf
	
	Print "."	

	Delay 50
Wend</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>2014</font></td></tr></table></td></tr><tr ><td class="posttext"> this version uses an int buffer to supply the left and right channels of a stereo 16 bit freeaudio stream <br><pre class=code>

' freestream16.bmx

' stereo 16 bit streaming example

Strict

Import pub.freeaudio

Const FRAG=1024

Print "freestream is free streaming..."

fa_Init(0)	' brl.freefreeaudio usually does this

Local buffer:Int[FRAG*8]
Local writepos

Local sound
Local channel

sound=fa_CreateSound( FRAG*8,16,2,44100,buffer,$80000000)
Print "Sound:"+sound

channel=fa_PlaySound( sound, FA_CHANNELSTATUS_STREAMING ,0)
Print "PlaySound:"+channel

Local streaming
Local lfo#
Local osc1#

While True
'	Print "Status:"+fa_ChannelStatus( channel )	
	Local readpos=fa_ChannelPosition( channel )
	Local write=readpos+FRAG*4-writepos
	Local frags=write/FRAG	
	While frags&gt;0	
		Print "Write to "+writepos
		Local pos=writepos Mod (FRAG*8)
		For Local f=0 Until frag				
			Local t=writepos+f	
			lfo=Sin(0.001*t)
			osc1=Sin(t*(lfo+2))

 		Local l16=$ffff &amp; Int(1000*lfo*osc1)
 		Local r16=$ffff &amp; Int(2000*Sin(t))
	
			buffer[pos+f]=L16|(R16 Shl 16)

		Next
		writepos:+FRAG
		frags:-1
	Wend
	
	If Not streaming And writepos&gt;=FRAG*4
		fa_SetChannelPaused( channel, False )
		streaming=True
	EndIf
	
	Print "."	

	Delay 50
Wend

</pre> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>2014</font></td></tr></table></td></tr><tr ><td class="posttext"> this version streams a stereo 16 bit ogg file<br><br><pre class=code>
' freestreamogg.bmx
' streaming stereo 16 bit OGG decoder to freeaudio 

Strict

Import pub.freeaudio
Import pub.oggvorbis

Type TAudioStream
	Const FRAG=8000

	Field buffer:Int[FRAG*8]
	Field writepos
	
	Field sound
	Field channel
	Field streaming
	
	Method PlayStereo16(freq)
		sound=fa_CreateSound( FRAG*8,16,2,freq,buffer,$80000000)
		Print "Sound:"+sound		
		channel=fa_PlaySound( sound, FA_CHANNELSTATUS_STREAMING ,0)
		Print "Channel:"+channel
		streaming=False
	End Method
	
	Method Poll()
		Local readpos=fa_ChannelPosition( channel )
		Local write=readpos+FRAG*4-writepos
		Local frags=write/FRAG	

		While frags&gt;0	
			Print "Write to "+writepos
			Local pos=writepos Mod (FRAG*8)
			Local res=ReadBuffer(Byte Ptr(buffer)+pos*4,FRAG*4)
			Print "ReadBuffer="+res
			writepos:+FRAG
			frags:-1
		Wend
			
		If Not streaming And writepos&gt;=FRAG*4
			fa_SetChannelPaused( channel, False )
			streaming=True
		EndIf
	End Method

	Method ReadBuffer%(buffer:Byte Ptr,bytes)
	End Method
End Type

Type TOggStream Extends TAudioStream
	
	Field source:Object
	Field stream:TStream
	Field ogg:Byte Ptr

	Field samples
	Field channels
	Field freq
	Field size

	Method Open( url:Object )
		source=url
		stream=ReadFile(url)
		ogg=Decode_Ogg(stream,readfunc,seekfunc,closefunc,tellfunc,samples,channels,freq)
		size=samples*2*channels		
		PlayStereo16(freq)
	End Method
		
	Method ReadBuffer%(buffer:Byte Ptr,bytes)	
		If Not ogg Return		
		If bytes&gt;size bytes=size
		If bytes=0 Return 0
		Local err=Read_Ogg( ogg,buffer,bytes )
		If err Return -1
		size:-bytes
'		If size=0 Open source
		Return bytes
	End Method

	Function readfunc( buf@Ptr,size,nmemb,src:Object )
		Local bytes=TStream(src).Read(buf,size*nmemb)
		Return bytes/size
	End Function
	
	Function seekfunc( src_obj:Object,off0,off1,whence )
		Local off
		Local src:TStream=TStream(src_obj)
		off=off0	'WARNING 32 bit BADNESS
		Local res=-1
		Select whence
			Case 0
				res=src.Seek(off)			'SEEK_SET
			Case 1
				res=src.Seek(src.Pos()+off)	'SEEK_CUR
			Case 2
				res=src.Seek(src.Size()+off)	'SEEK_END
		End Select
		If res&gt;=0 Return 0
		Return -1
	End Function
	
	Function closefunc( src:Object )
	End Function
	
	Function tellfunc( src:Object )
		Return TStream(src).Pos()
	End Function

End Type

fa_Init(0)	' brl.freefreeaudio usually does this

Local ogg:TOggStream

ogg=New TOggStream

ogg.Open("bouncy.ogg")

Print "ogg.size="+ogg.size+" ogg.channels="+ogg.channels+" ogg.freq="+ogg.freq

Print "freestream is free streaming..."

While True
	ogg.Poll
	Print "."	
	Delay 50
Wend

</pre> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>2014</font></td></tr></table></td></tr><tr ><td class="posttext"> This is cool, but both the 8-bit and the ogg versions have a 'fluttering' sound throughout for me -- the sound plays but has a constant "p-p-p-p-p-p-p" playing through it while doing so. I did find one ogg that played without flutter*, but all three that I tried are 16-bit according to Audacity.<br><br>The 16-bit demo was also flutter-free.<br><br>* Actually, on headphones, that has the flutter too, just quieter.<br><br>Just tried reducing FRAG to 1000 on that quiet flutter track and it was a lot better during a couple of plays, but now after trying the 'noisy tracks' (they stayed noisy) the 'quiet' track is doing it too!<br><br>They all play fine with this:<br><br><pre class=code>
ogg$ = "xyz.ogg"
sound:TSound = LoadSound (ogg$)
PlaySound sound
Delay 5000
</pre><br><br>For what it's worth, sound card is "Creative SB Audigy 2 (WDM)". <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>2014</font></td></tr></table></td></tr><tr ><td class="posttext"> Oh, and right now the first demo is flutter-free and the 16-bit version is fluttering! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>2014</font></td></tr></table></td></tr><tr ><td class="posttext"> James, you may want to make FRAG bigger not smaller,  I have modified the first example to more realistic size. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>2014</font></td></tr></table></td></tr><tr ><td class="posttext"> I used 8k or bigger to make cracklings go away.<br><br>Reason is: If you eg. only update the streams buffer 30 times a second, this means your buffer must in all cases be longer than 1/33 = 33ms. If your buffer is slightly smaller you have chances to play some milliseconds of "old sound".<br><br>In the examples using "Delay 50" this means that at least 50 ms are gone till the next buffer refill.<br><br>Maybe there is one having time and knowledge to write it in a threaded way so some kind of autopolling can be achieved (regardless of delays, or occupation of the mainthread like resource loading).<br><br>To make it more convertible I wrapped the code in a way so that TChannel/TSound get used.<br><br><br>I do not post the code here because it is not PD but zlib/libpng-licenced:<br><a href="https://github.com/GWRon/Dig/blob/3ea47f1e85680dc4403c490751fdd069a2d28047/base.sfx.soundstream.bmx" target="_blank">https://github.com/GWRon/Dig/blob/3ea47f1e85680dc4403c490751fdd069a2d28047/base.sfx.soundstream.bmx</a><br><br>Code contains some simple "Clone"-Method to enable crossfading to the same sound (as you cannot share the buffers but the source bank/stream). <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
