<!DOCTYPE html><html lang="en" ><head ><title >Play sound on midi (using MidiLib.dll)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Midi sound function addon, language=bb, category=Audio'><meta name='author' content='Dan'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=3>Audio</a>/Play sound on midi (using MidiLib.dll)</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="3224.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Play sound on midi (using MidiLib.dll) by Dan</td><td align="right">2015 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Here are 3 Functions for playing a sound over Midi channels, <br>using MidiLib.dll from <a href="http://www.blitzforum.de/forum/viewtopic.php?t=33817" target="_blank">http://www.blitzforum.de/forum/viewtopic.php?t=33817</a><br><br>You will need to set this at the start of your program:<br><pre class=code>
Global hOut = MidiOutOpen( 0 ) ; Microsoft GS Wavetable SW Synth is most probably ID zero
MInstrument(10)
</pre><br><br>and at the end <br><pre class=code>
MidiOutClose(hOut)
</pre><br><br>The functions are:<br><br>MInstrument(nr,ch=0) - sets an music instrument nr(0-127) on the channel (0-15) <br>need to be set before the Mplay Call but only once (if it doesnt change during program execution)<br><br>MPlay(note,oktave²,ch²,durr²) Plays a note * oktave² on a channel with the durration<br>Mstop(note,oktave²,ch²,durr²) To stop certain music instruments from playing. (need to be called with the previous Mplay variables.<br>The ² marked fuction variables have their default, and don't need to be set. <br><br>Notes are ranged from 1-7 (C-H) 8-12 (C#-A#) see the function.<br>Use 0 to skip a note (see the demo)<br><br>There are 10 octaves which you can choose from 0-9. (the 11th was omitted because it has only 8 notes to choose from)<br>see <a href="http://www.zem-college.de/midi/mc_taben.htm" target="_blank">midi compendium</a> for more info (german)<br><br>or <a href="https://translate.google.com/translate?sl=de&amp;tl=en&amp;js=y&amp;prev=_t&amp;hl=en&amp;ie=UTF-8&amp;u=http%3A%2F%2Fwww.zem-college.de%2Fmidi%2Fmc_taben.htm&amp;edit-text=" target="_blank"> Midi Compendium translated with Google </a> (english) </td></tr><tr ><td class="cell"><pre class="code">;====================================================================
; Project:     Play sound on midi channels using Midilib.dll
; Version:     1.0
; Author:      Dan
; Email:       -.-
; Copyright:   PD
; Description: Play sound without external media (needs midilib.dll)
;              from http://www.blitzforum.de/forum/viewtopic.php?t=33817
;              A set of 3 functions written for use with the MidiLib.dll
;              MInstrument(n,ch²) sets an music instrument (0-127) for one channel
;              MPlay(note,oktave²,ch²,durr²) Plays a note * oktave²
;                  on a channel² (0-15) with durration² (0-127)
;              Mstop(note,oktave²,ch²,durr²) same as Mplay, 
;                  needs to be called with the previous MPlay variables !
;                  (only for some instruments)
;                  ² = optional parameters 
;====================================================================
; Demo Numpad +- change the instrument nr
;      Numpad enter sets the instrument

Graphics 800,600,32,2
Graphics 800,600,32,3

Global hOut = MidiOutOpen( 0 ); Microsoft GS Wavetable SW Synth is most probably ID zero (needed at the start)
Print "instrument code"+Hex$(MInstrument(10)) ; Sets an instrument to play (needed !)

;Demo starts here
Delay 1000
Mtime=MilliSecs()
Ktime=MilliSecs()
z=0
Repeat

	Color Rand($0,$ff),Rand($0,$ff),Rand($0,$ff)
	Line Rand(1,GraphicsWidth()),Rand(1,GraphicsHeight()),Rand(1,GraphicsWidth()),Rand(1,GraphicsHeight())
	Color $0,$0,$0
	Rect Rand(1,GraphicsWidth()),Rand(1,GraphicsHeight()),Rand(1,30),Rand(1,30)
	Color $0,0,0
	Rect 0,0,620,16
	Color $ff,$ff,$ff
	Text 0,0,MilliSecs()+"  Note="+x+" InstrNr:"+z+".. "+Right$(Hex$(j),6)
	
	If KeyDown(78) And MilliSecs()-Ktime&gt;100 ;(numpad +)
		z=z+1 : If z&gt;$80 Then z=$80
		Ktime=MilliSecs()
	EndIf
	
	If KeyDown(74) And MilliSecs()-Ktime&gt;100 ;(numpad -)
		z=z-1 : If z&lt;=0 Then z=0
		Ktime=MilliSecs()
	EndIf
	
	If KeyDown(156) And MilliSecs()-Ktime&gt;150 ; Enter (numpad)
	
	MInstrument(z)
	Ktime=MilliSecs()
	EndIf
	
	If MilliSecs()-Mtime&gt;Mdelay    ; Automatic play !
	    
	    J=MStop(x,3,0,0)  ;Stops the previous note, needed for some long playing instruments
		Read x
		If x=-1 
			Restore ende
		Else
			Mdelay=MPlay(x) ; plays the next note and sets the delay untill next one is played
		EndIf
		Mtime=MilliSecs()
	EndIf
	Flip
Until KeyDown(1)

.ende
Data 1,2,3,5,5,0,6,5,3,1,0,2,3,3,2,1,0,2,1,2,3,5,5,6,5,3,1,2,3,3,2,2,1,4,4,4,6,6,6,5,5,4,2,1,0,0
;Data 1,2,1,3,1,3,3,4,3,2,4,2,1,2,3,1,2,1,4,3,5,3,1,2,3,1,2,1,4,3,2,1,2,4,5,6,5,6,4,6,3,2,1,2  
Data -1

;Demo ends here
MidiOutClose(hOut)  ; Closes the midi handle ! 
End



Function MInstrument(nr,ch=0)
;Sets an instrument NR for one Channel
;instrument nr from 0 to 127 (128)
	If ch&lt;0
		ch=-ch Mod 16
	Else
		ch=ch Mod 16
	EndIf
	
	If nr&lt;0 
		nr=-nr Mod $80
	Else
		nr=nr Mod $80
	EndIf
	
	Message=(($C0+ch)*$100+(nr))
	MidiOutShortMsg(hOut,Message)
	Return Message ; for debugging purpose 
End Function

Function MPlay(note,oktave=3,ch=0,durr=$60)
	;Plays a note (1-12)*oktave on channel with durration
    ;oktave range 0-9
	;Durration range 128 (0-127)
	oktave=oktave Mod 10
	ch=ch Mod $10  ; 16 channels maximum ! 0-15 - standard ch 0
	If durr&lt;$10 Then durr=$10
	durr=durr Mod $80
    note=note Mod 13
	;Cnote=note Mod 12
    Select note
		Case 1			;C
			n=0 
		Case 2			;D
			n=2
		Case 3			;E
			n=4
		Case 4			;F
			n=5
		Case 5			;G
			n=7
		Case 6			;A
			n=9
		Case 7			;H
		    n=11
		Case 8			;C#
		    n=1
		Case 9			;D#
		    n=3
		Case 10			;F#
		    n=6
		Case 11			;G#
		    n=8
		Case 12			;A#
		    n=10
	End Select
	;Print "Note= "+note+" / "+n+" ... "+Right$(Hex$(((n+(oktave*12)))*$100),4)
	If note&gt;0
		Message=(($90+ch)*$10000)+((n+(oktave*12)-1)*$100)+durr
	;Print Hex$(Message)
		MidiOutShortMsg(hOut,Message)
    Else
	 Message=0
	EndIf 
	Return durr*6     ;returns the durration*6 (as miliseconds to wait, for example)
End Function 

Function MStop(note,oktave=3,ch=0,durr=0)
    ;oktave 0-9
	oktave=oktave Mod 10
	ch=ch Mod $10  ; 16 channels maximum ! 0-15 - standard ch 0
	durr=durr Mod $80
    note=note Mod 13
	;Cnote=note Mod 12
    Select note
		Case 1			;C
			n=0 
		Case 2			;D
			n=2
		Case 3			;E
			n=4
		Case 4			;F
			n=5
		Case 5			;G
			n=7
		Case 6			;A
			n=9
		Case 7			;H
		    n=11
		Case 8			;C#
		    n=1
		Case 9			;D#
		    n=3
		Case 10			;F#
		    n=6
		Case 11			;G#
		    n=8
		Case 12			;A#
		    n=10
	End Select
Message=(($80+ch)*$10000)+((n+(oktave*12)-1)*$100)+durr
MidiOutShortMsg(hOut,Message)
Return Message
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
