<!DOCTYPE html><html lang="en" ><head ><title >Better OpenAL</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Lightweight OpenAL command set., language=bmx, category=Audio'><meta name='author' content='JoshK'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=3>Audio</a>/Better OpenAL</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2307.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Better OpenAL by JoshK</td><td align="right">2008 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This bypasses the BRL sound and audio driver classes and creates a lightweight sound API that functions very close to the OpenAL command set.  3D spatialization features are supported, including Doppler shifting. </td></tr><tr ><td class="cell"><pre class="code">SuperStrict

Framework brl.audiosample
Import pub.OpenAL
Import brl.wavloader
Import brl.oggloader


'===============================================================
'Test
Local sound:TSound
Local source:TSource
sound=TSound.Load("impact_metal07.wav")

TListener.SetMatrix([1.0,0.0,0.0,0.0, 0.0,1.0,0.0,0.0, 0.0,0.0,1.0,0.0, 1.0,0.0,0.0,1.0])

source=TSource.Create(sound,1)
source.Resume()

Delay 1000
source.stop()
source.resume()
source.setvelocity([100.0,0.0,0.0])
Delay 20000

RuntimeError alGetError()

End
'===============================================================


Private

Global ALDevice:Int
Global ALContext:Int

Function StartOpenAL()
	If Not ALContext
		ALDevice=alcOpenDevice(Null) 
		ALContext=alcCreateContext(ALDevice, Null) 
		alcMakeContextCurrent(ALContext)
	EndIf
EndFunction

Function StopOpenAL()
	alcDestroyContext(ALContext)
	alcCloseDevice(ALDevice)
	ALContext=0
	ALDevice=0
EndFunction

OnEnd StopOpenAL

Public


Type TSource
	
	Field id:Int
	Field sound:TSound
	
	Method New()
		StartOpenAL()
		alGenSources 1,Varptr id
		alSourcei id,AL_SOURCE_RELATIVE,False
		alSourcef id,AL_REFERENCE_DISTANCE,0.0
		alSourcef id,AL_ROLLOFF_FACTOR,1.0
	EndMethod
	
	Method Delete()
		Stop()
		If ALContext alDeleteBuffers 1,Varptr id
	EndMethod
	
	'Source controls
	Method Resume()
		alSourcePlay id
	EndMethod
		
	Method Pause()
		alSourcePause id
	EndMethod
	
	Method Stop()
		alSourceStop id
	EndMethod
	
	Method Playing:Int()
		Local state:Int
		alGetSourcei id,AL_SOURCE_STATE,Varptr state
		Return state=AL_PLAYING
	EndMethod
	
	'Source settings
	Method SetVolume(volume:Float)
		alSourcef id,AL_GAIN,volume
	EndMethod
	
	Method SetPitch(pitch:Float)
		alSourcef id,AL_PITCH,pitch
	EndMethod
	
	Method SetPosition(position:Byte Ptr)
		Local positionf:Float[3]
		If position=Null
			alSourcei id,AL_SOURCE_RELATIVE,True
			alSource3f id,AL_POSITION,0,0,0
		Else
			MemCopy positionf,position,12
			alSourcei id,AL_SOURCE_RELATIVE,False
			alSource3f id,AL_POSITION,positionf[0],positionf[1],positionf[2]
		EndIf
	EndMethod
	
	Method SetRange(range:Float)
		alSourcef id,AL_MAX_DISTANCE,range	
	EndMethod
	
	Method SetVelocity(velocity:Byte Ptr)
		Local velocityf:Float[3]
		MemCopy velocityf,velocity,12
		alSource3f id,AL_VELOCITY,velocityf[0],velocityf[1],velocityf[2]
	EndMethod
	
	'Flags
	'1 - looping
	Function Create:TSource(sound:TSound,flags:Int=0)
		If Not sound Return Null
		Local source:TSource
		source=New TSource
		source.sound=sound
		alSourcei source.id,AL_BUFFER,sound.buffer
		If (1 &amp; flags) alSourcei source.id,AL_LOOPING,True
		Return source
	EndFunction
	
EndType


Type TSound
	
	Field buffer:Int
	
	Method New()
		StartOpenAL()
		If ALContext alGenBuffers 1,Varptr buffer
	EndMethod
	
	Method Delete()
		If ALContext alDeleteBuffers 1,Varptr buffer
	EndMethod
	
	Function Load:TSound(url:Object)
		Local sample:TAudioSample
		Local sound:TSound
		Local alfmt:Int
		sample=LoadAudioSample(url)
		If Not sample Return Null
		sound=New TSound		
		Select sample.format
			Case SF_MONO8
				alfmt=AL_FORMAT_MONO8
			Case SF_MONO16LE
				alfmt=AL_FORMAT_MONO16
				?BigEndian
					sample=sample.Convert( SF_MONO16BE )
				?
			Case SF_MONO16BE
				alfmt=AL_FORMAT_MONO16
				?LittleEndian
					sample=sample.Convert( SF_MONO16LE )
				?
			Case SF_STEREO8
				alfmt=AL_FORMAT_STEREO8
			Case SF_STEREO16LE
				alfmt=AL_FORMAT_STEREO16
				?BigEndian
					sample=sample.Convert( SF_STEREO16BE )
				?
			Case SF_STEREO16BE
				alfmt=AL_FORMAT_STEREO16
				?LittleEndian
					sample=sample.Convert( SF_STEREO16LE )
				?
		End Select
		alBufferData sound.buffer,alfmt,sample.samples,sample.length*BytesPerSample[sample.format],sample.hertz
		Return sound
	EndFunction
	
EndType


Type TListener
	
	Function SetMatrix(mat:Byte Ptr)
		StartOpenAL()
		Local matf:Float[16]
		MemCopy matf,mat,64
		alListener3f AL_POSITION,matf[12],matf[13],matf[14]
		alListenerfv AL_ORIENTATION,[matf[4],matf[5],matf[6],matf[8],matf[9],matf[10]]
	EndFunction
	
EndType</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>2008</font></td></tr></table></td></tr><tr ><td class="posttext"> Leadwerks, you've put this code under ".bb" format, but it's for BMax. Please do follow the appropriate format specification, for the sake of organization.<br>Thank you.<br><br>PS: Nice system you got there! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chroma</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> Argh!!  Yeah it's under .bb and I thought this was the B3D ticket to great 3d sound... boo! <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
