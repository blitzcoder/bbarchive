<!DOCTYPE html><html lang="en" ><head ><title >MaxGUI strange idea...</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >MaxGUI strange idea...</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >MaxGUI strange idea...</a><br><br>
<a name="1041504"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I just wrote this 'strange' MaxGUI 'self-contained-definition' gadgets...<br>Just for fun (or for my lazyness)<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' createwindow.bmx

Import MaxGui.Drivers

Strict 

AppTitle = "CreateWindow() Example"

Global FLAGS:Int

' Comment/uncomment any of the following lines to experiment with the different styles.

FLAGS:| WINDOW_TITLEBAR
FLAGS:| WINDOW_RESIZABLE
'FLAGS:| WINDOW_MENU
FLAGS:| WINDOW_STATUS
FLAGS:| WINDOW_CLIENTCOORDS
'FLAGS:| WINDOW_HIDDEN
FLAGS:| WINDOW_ACCEPTFILES
'FLAGS:| WINDOW_TOOL
FLAGS:| WINDOW_CENTER

Local window:TGadget = CreateWindow( AppTitle, 100, 100, 320, 370, Null, FLAGS )

If (FLAGS &amp; WINDOW_STATUS) Then
	SetStatusText( window, "Left aligned~tCenter aligned~tRight aligned" )
EndIf

Local btn:tgadget=CreateButtonX("Hallo{=pos:;size:50%,25%;title:I'm special!;fcolor:200,50,50;bcolor:255,200,125;layout:2,2,2,2=}",10,10,100,20,window)

Local btn2:tgadget=CreatebuttonX("My{=pos:100,100;activate:;layout:2,2,2,2=}",,,80,30 ,window)

Local btn3:tgadget=CreateListBoxX("{=size:50%;fcolor:200,250,50;bcolor:128,128,128;layout:2,2,2,2;items:One,Two,Three,Four,Five;select:2=}",10,150,100,180,window)

Repeat
	WaitEvent()
	Print CurrentEvent.ToString()
	Select EventID()
		Case EVENT_APPTERMINATE, EVENT_WINDOWCLOSE
			End
	End Select
Forever

Function CreateButtonX:tgadget(tx$,x:Int=0,y:Int=0,w:Int=0,h:Int=0,win:tgadget=Null,style:Int=BUTTON_PUSH)
	
	Local p1:Int,p2:Int,recode:Int,cod:String
	Local gad:tgadget
	
	p1=Instr(tx,"{=")
	p2=Instr(tx,"=}",p1)
	
	If p1&gt;0 And p2&gt;0
		cod=tx[p1+1..p2-1]
		recode=1
		tx=tx[..p1-1]
	End If
	
	gad=CreateButton(tx,x,y,w,h,win,style)
	
	If recode=1 And gad&lt;&gt;Null Special(gad,cod)
	
	Return gad
End Function

Function CreateListBoxX:tgadget(tx$,x:Int=0,y:Int=0,w:Int=0,h:Int=0,win:tgadget=Null,style:Int=BUTTON_PUSH)
	
	Local p1:Int,p2:Int,recode:Int,cod:String
	Local gad:tgadget
	
	p1=Instr(tx,"{=")
	p2=Instr(tx,"=}",p1)
	
	If p1&gt;0 And p2&gt;0
		cod=tx[p1+1..p2-1]
		recode=1
		tx=tx[..p1-1]
	End If
	
	gad=CreateListBox(x,y,w,h,win,style)
	
	If recode=1 And gad&lt;&gt;Null Special(gad,cod)
	
	Return gad
End Function


	Function Special(gad:tgadget,cod:String)
		Local cmd:String[],pars:String[]
		Local cs:String,pss:String
		cmd=cod.split(";")
		For Local s$=EachIn cmd
			cs=s.tolower()
			cs=cs[..cs.find(":")]
			pss=s[s.find(":")+1..]
			pars=pss.split(",")
			
			Select cs
				Case "pos"
					pars=pars[..2]
					SetGadgetShape gad,Int(pars[0]),Int(pars[1]),GadgetWidth(gad),GadgetHeight(gad)
				
				Case "size"
					pars=pars[..2]
					Local ww:Int,hh:Int,done:Int
					ww=Int(pars[0])
					hh=Int(pars[1])
			
					If Instr(pars[0],"%")&gt;0 
						pars[0]=Replace(pars[0],"%","")
						ww=GadgetWidth(gad.parent)*(Float(pars[0])/100)-gad.xpos
					End If
					
					If Instr(pars[1],"%")&gt;0 
						pars[1]=Replace(pars[1],"%","")
						hh=GadgetHeight(gad.parent)*(Float(pars[1])/100)-gad.ypos

					End If
			
					If ww=0 Or ww=-1 ww=GadgetWidth(gad.parent)-gad.xpos
					If hh=0 Or hh=-1 hh=GadgetHeight(gad.parent)-((gad.parent.style &amp; WINDOW_STATUS&lt;&gt;0)*1)-gad.ypos
					
					
					
					SetGadgetShape gad,gad.xpos,gad.ypos,ww,hh
					
					
				Case "title"
					pars=pars[..1]
					SetGadgetText gad,pars[0]
				Case "fcolor"
					pars=pars[..3]
					SetGadgetColor gad,Int(pars[0]),Int(pars[1]),Int(pars[2]),False
				Case "bcolor"
					pars=pars[..3]
					SetGadgetColor gad,Int(pars[0]),Int(pars[1]),Int(pars[2]),True
				Case "disable"
					DisableGadget gad
				Case "enable"
					EnableGadget gad
				Case "layout"
					pars=pars[..4]
					SetGadgetLayout gad,Int(pars[0]),Int(pars[1]),Int(pars[2]),Int(pars[3])
				Case "status"
					pars=pars[..1]
					SetButtonState gad,Int(pars[0])
				Case "activate"
					ActivateGadget gad
					
				Case "items"
					For Local it$=EachIn pars
						AddGadgetItem gad,it
					Next
				Case "select"
					SelectGadgetItem gad,Int(pars[0])
					

			End Select
		Next
	End Function
</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
