<!DOCTYPE html><html lang="en" ><head ><title >SetProxy()</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >SetProxy()</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >SetProxy()</a><br><br>
<a name="891139"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have some problems to understand how SetProxy() exactly works.<br>With SetProxy you can setup what is the 'main gadget' in your own-gadget.<br>But...<br><br>I have an user-gadget (like in the following example) that is composed by 2 (or more) sub-gadgets; I have problems to extends the standard gadget commands to work with my user-gadget.<br>In this case DISABLEGADGET what it really does? It disable the panel gadget? And the others?<br><br>I think I'm a little lost....<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import maxgui.drivers
Import maxgui.proxygadgets

Global wndMain:TGadget = CreateWindow( AppTitle, 100, 100, 300, 200, Null, WINDOW_TITLEBAR|WINDOW_CLIENTCOORDS|WINDOW_STATUS )

Global temp:tgadget = CreateMultiGadget("Hello" , 10 , 10 , 100 , 100 , wndMain) 
DisableGadget temp 'it disables the gadgets(panel) but all the rest is not greyed-out

Repeat
	WaitEvent()
	SetStatusText wndMain, CurrentEvent.ToString()
	Select EventID()
		Case EVENT_WINDOWCLOSE, EVENT_APPTERMINATE;End
	EndSelect
Forever

Function CreateMultiGadget:TGadget( url$,x:Int,y:Int,w:Int,h:Int,group:TGadget,style=0,customtext$ = "" )
		Return New Tmulti.Create(url,x,y,w,h,group,style,customtext)
EndFunction

Type tmulti Extends  TProxyGadget

	Global multi_list:TList
	
	Field panel:tgadget
	Field button:tgadget

	Method New()
		If multi_list=Null Initialize()
	End Method
	
	Method Create:tmulti(tit$ , x:Int , y:Int , w:Int , h:Int , group:tgadget , style:Int = 0 , ct$ = "")
		panel:tgadget = CreatePanel(x , y , w , h , group , PANEL_ACTIVE) 
		button:tgadget = CreateButton(tit , 4,1 , w , 18, panel , BUTTON_CHECKBOX) 
	
		SetProxy(panel ) 
		SetGadgetColor panel,188,188,188
		multi_list.AddLast Self

		Return Self
	End Method
	
	Method EventHook:TEvent( pEvent:TEvent )
		
		Select pEvent.id
		
			Case EVENT_MOUSEENTER
				button.settextcolor(255,0,0)
				SetPointer(POINTER_HAND)
			Case EVENT_MOUSELEAVE
				button.settextcolor(0,0,0)
				SetPointer(POINTER_DEFAULT)
			Case EVENT_MOUSEDOWN
				
		EndSelect
		
		Return Null
	
	EndMethod
	

	Method SetTextColor(r,g,b)
		Super.SetTextColor(r,g,b)
	EndMethod
	
	Method Free()
		multi_list.Remove(Self)
		Super.Free()
	EndMethod
	
	Function Initialize()
		multi_list = New TList
		AddHook EmitEventHook, eventHandler, Null, 1
		
	EndFunction
	
	Function eventHandler:Object( pID%, pData:Object, pContext:Object )
		Local pEvent:TEvent = TEvent(pData)
		
		If pEvent Then
			For Local tmp:Tmulti = EachIn multi_list
				If tmp.proxy = pEvent.source
					Return tmp.EventHook( pEvent ) 
				End If
			Next
		EndIf
	Return pData
	EndFunction
End Type
</textarea> <br><br></td></tr></table><br>
<a name="891149"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> The <b>TProxyGadget</b> simply allows you to set-up custom gadgets so that they can be used with the standard set of MaxGUI commands, such as <b>DisableGadget()</b>, <b>SetGadgetColor()</b> etc.<br><br>This is done by setting a real gadget as the <i>proxy gadget</i>, which in your case appears to be <b>panel</b>, to which all commands are forwarded on to. Therefore, any time you call a MaxGUI command (such as <b>DisableGadget()</b>) on your <b>TMulti</b> custom gadget, it is effectively the same as calling it on <b>TMulti.panel</b>.<br><br>The behaviour you are experiencing is as expected - the panel is disabled  which on Windows means that the panel becomes greyed out, and the children are also disabled (but don't appear so, this is just the OS standard - only the parent appears greyed out, although the children become desensitized).<br><br>If you really want the button to be greyed out too, then you'll need to override the <b>SetEnabled(bool)</b> by adding a new method somewhere in <b>TMulti</b> so that is passes the message onto both the panel and the button. For example,<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Import maxgui.drivers
'Import maxgui.proxygadgets

Global wndMain:TGadget = CreateWindow( AppTitle, 100, 100, 300, 200, Null, WINDOW_TITLEBAR|WINDOW_CLIENTCOORDS|WINDOW_STATUS )

Global temp:tgadget = CreateMultiGadget("Hello" , 10 , 10 , 100 , 100 , wndMain) 
DisableGadget temp 'it disables the gadgets(panel) but all the rest is not greyed-out

Repeat
	WaitEvent()
	SetStatusText wndMain, CurrentEvent.ToString()
	Select EventID()
		Case EVENT_WINDOWCLOSE, EVENT_APPTERMINATE;End
	EndSelect
Forever

Function CreateMultiGadget:TGadget( url$,x:Int,y:Int,w:Int,h:Int,group:TGadget,style=0,customtext$ = "" )
		Return New Tmulti.Create(url,x,y,w,h,group,style,customtext)
EndFunction

Type tmulti Extends  TProxyGadget

	Global multi_list:TList
	
	Field panel:tgadget
	Field button:tgadget

	Method New()
		If multi_list=Null Initialize()
	End Method
	
	Method Create:tmulti(tit$ , x:Int , y:Int , w:Int , h:Int , group:tgadget , style:Int = 0 , ct$ = "")
		panel:tgadget = CreatePanel(x , y , w , h , group , PANEL_ACTIVE) 
		button:tgadget = CreateButton(tit , 4,1 , w , 18, panel , BUTTON_CHECKBOX) 
	
		SetProxy(panel ) 
		SetGadgetColor panel,188,188,188
		multi_list.AddLast Self

		Return Self
	End Method
	
	Method EventHook:TEvent( pEvent:TEvent )
		
		Select pEvent.id
		
			Case EVENT_MOUSEENTER
				button.settextcolor(255,0,0)
				SetPointer(POINTER_HAND)
			Case EVENT_MOUSELEAVE
				button.settextcolor(0,0,0)
				SetPointer(POINTER_DEFAULT)
			Case EVENT_MOUSEDOWN
				
		EndSelect
		
		Return Null
	
	EndMethod
	
	Method SetEnabled(bool)
		button.SetEnabled(bool)
		Super.SetEnabled(bool)	'This will forward the command onto the proxy gadget (i.e. the panel)
	EndMethod
	
	'You shouldn't need this
	'Method SetTextColor(r,g,b)
	'	Super.SetTextColor(r,g,b)
	'EndMethod
	
	Method Free()
		multi_list.Remove(Self)
		Super.Free()
	EndMethod
	
	Function Initialize()
		multi_list = New TList
		AddHook EmitEventHook, eventHandler, Null, 1
		
	EndFunction
	
	Function eventHandler:Object( pID%, pData:Object, pContext:Object )
		Local pEvent:TEvent = TEvent(pData)
		
		If pEvent Then
			For Local tmp:Tmulti = EachIn multi_list
				If tmp.proxy = pEvent.source
					Return tmp.EventHook( pEvent ) 
				End If
			Next
		EndIf
	Return pData
	EndFunction
End Type</textarea><br><br>Finally, I perhaps should also mention that you don't need to import <b>MaxGUI.ProxyGadgets</b> to make your own custom <b>TProxyGadget</b>s - all this is defined in <b>MaxGUI.MaxGUI</b> which is imported by <b>MaxGUI.Drivers</b>. <b>MaxGUI.ProxyGadgets</b> simply contains standard MaxGUI custom gadgets such as the hyperlink control (which wraps a label). If you aren't using the hyperlink control, you don't need to import it.<br><br>Edit: However, you might want to take a look into <b>MaxGUI.ProxyGadget</b>'s source, as you will see how I do proxy gadgets. <br><br></td></tr></table><br>
<a name="891185"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oh many thanks. Now it's more clear.<br>I think I will checkout better the gadget.bmx+magui.bmx type structure to know what methods are used.<br>Thank you very much.<br><br>PS:as for <b>import Maxgui.Proxygadgets</b> I made a copy&amp;paste of the hyperlink gadget example: this means that this example must be changed... <br><br></td></tr></table><br>
<a name="891231"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> PS:as for import Maxgui.Proxygadgets I made a copy&amp;paste of the hyperlink gadget example: this means that this example must be changed... <br></div><br>Well that's not quite right - the hyperlink example needs to import <b>MaxGUI.ProxyGadgets</b> as it is using the hyperlink proxy gadget control. ;-) For your own proxy gadgets however, you do not need to import it. <br><br></td></tr></table><br>
<a name="891312"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> (Homer Simpson's voice)<b><i>Doh!</i></b> <br>I'm very stupid...sorry :D <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
