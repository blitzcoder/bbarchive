<!DOCTYPE html><html lang="en" ><head ><title >standard spinner gadget</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >standard spinner gadget</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >standard spinner gadget</a><br><br>
<a name="820171"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dmaz</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> allows for negative and floating point<br><br>windows only.... needs to be modified quite a bit for the mac<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict 

'-----------------------------------------------------------------------------------------------------
' Example program
'-----------------------------------------------------------------------------------------------------

Local Window:TGadget=CreateWindow("My Window",0,0,240,240,Null,WINDOW_TITLEBAR)


' default spinner (0 to 10 step 1) with trackbar
CreateSpinner(15,15,65,30,Window,SLIDER_TRACKBAR)

' spinner (-10 to 10 step .1)
Local spinner:TSpinner = CreateSpinner(15,65,65,20,Window)
SetSpinnerRange spinner,-10,10
SetSpinnerPrecision spinner,.1
SetSpinnerValue spinner,-5

' % spinner (0 to 1 step .01)
Local spinner2:TSpinner = CreateSpinner(15,100,65,20,Window)
SetSpinnerRange spinner2,0,1
SetSpinnerPrecision spinner2,.01
SetSpinnerValue spinner2,.5


While WaitEvent()
	Local event:TEvent = CurrentEvent

	Print event.ToString()
	Select event.id
		Case EVENT_GADGETACTION
			Print "GadgetText: " + GadgetText(TGadget(event.source)) + "~tEventExtra: " + String(event.extra)
		
		Case EVENT_WINDOWCLOSE
			End
	End Select
Wend



'-----------------------------------------------------------------------------------------------------
' TSpinner Gadget
' by dmaz
'-----------------------------------------------------------------------------------------------------

Function CreateSpinner:TSpinner( x:Int, y:Int, w:Int, h:Int, group:TGadget, style:Int=0 )
	Return New TSpinner.Create(x,y,w,h,group,style)
End Function

Function SetSpinnerRange( spinner:TSpinner, low:Double, high:Double )
	spinner.SetSpinnerRange low,high
End Function

Function SetSpinnerPrecision( spinner:TSpinner, precision:Double )
	spinner.SetSpinnerPrecision precision
End Function

Function SetSpinnerValue( spinner:TSpinner, value:Double )
	spinner.SetSpinnerValue value
End Function



Type TSpinner Extends TProxyGadget
	Field panel	:TGadget
	Field textField:TGadget
	Field stepper	:TGadget
	Field trackbar	:TGadget
	Field range	:Double
	Field value	:Double = 0.0
	Field low		:Double = 0.0
	Field high	:Double = 10.0
	Field precision:Double = 1.0
	
	Method New()
		AddHook EmitEventHook,MyEventHook,Self
	End Method
	
	Method Create:TSpinner( x:Int, y:Int, w:Int, h:Int, group:TGadget, style:Int=0 )
		panel = CreatePanel(x,y,w,h,group,PANEL_BORDER)
		If style &amp; SLIDER_TRACKBAR
			stepper = CreateSlider(w-h+14,0,h-18,h-14,panel,SLIDER_STEPPER)
			textField = CreateTextField(-2,-2,w-h+18,h-10,panel)
			trackbar = CreateSlider(-2,h-12,w+1,10,panel,SLIDER_HORIZONTAL|SLIDER_TRACKBAR)
		Else
			stepper = CreateSlider(w-h+4,0,h-8,h-4,panel,SLIDER_STEPPER)
			textField = CreateTextField(-2,-2,w-h+8,h,panel)
		EndIf
		SetGadgetFilter textField,NumFilter
		SetProxy textField
		
		SetSpinnerRange low,high
		SetSpinnerPrecision precision
		SetSpinnerValue low
		
		Return Self
	End Method

	Method Free()
		RemoveHook EmitEventHook,MyEventHook,Self
		'textField is freed in super
		If trackbar Then FreeGadget trackbar
		FreeGadget stepper
		FreeGadget panel
		Super.Free
	End Method

	Method SetSpinnerRange( low:Double, high:Double )
		Self.low = low
		Self.high = high
		range = Round((high-low)/precision)
		SetSpinnerValue low
		SetSliderRange stepper,0,range
		SetSliderValue stepper,0
		If trackbar
			SetSliderRange trackbar,0,range
			SetSliderValue trackbar,0
		EndIf
	End Method
	
	Method SetSpinnerPrecision( precision:Double )
		Self.precision = precision
		range = Round((high-low)/precision)
		SetSliderRange stepper,0,range
		If trackbar Then SetSliderRange trackbar,0,range
	End Method

	Method SetSpinnerValue:Int( value:Double )
		Self.value = Min(Max(low,value),high)
		SetSliderValue stepper,Round((Self.value-low)/precision)
		If trackbar Then SetSliderValue trackbar,Round((Self.value-low)/precision)
		SetGadgetText Self,PrettyFloat(Self.value)
		If value &lt; low Or value &gt; high Then Return False Else Return True
	End Method

	Method GetSpinnerValue:String()
		Return PrettyFloat(value)
	End Method


	'------------------------------------------------------------------------------------------------
	
	Function MyEventHook:Object( id:Int, data:Object, context:Object )
		If TSpinner(context) Then data=TSpinner(context).MyEvents(TEvent(data))
		Return data
	End Function
		
	Method MyEvents:TEvent( event:TEvent )
		If event
			Select event.id
				Case EVENT_GADGETACTION
					If event.source = stepper Or event.source = trackbar
						ActivateGadget TGadget(event.source)
						If low+(event.data/precision) &lt;&gt; value
							SetSpinnerValue low+(event.data*precision)
							EmitEvent CreateEvent(EVENT_GADGETACTION,Self,0,0,0,0,GetSpinnerValue())
						EndIf
						event = Null
					EndIf
	
				Case EVENT_GADGETLOSTFOCUS
					If event.source = Self
						If GadgetText(Self).ToDouble() &lt;&gt; value
							SetSpinnerValue GadgetText(Self).ToDouble()
							EmitEvent CreateEvent(EVENT_GADGETACTION,Self,0,0,0,0,GetSpinnerValue())
						EndIf
					EndIf
				
			End Select
		EndIf
		Return event
	End Method


	'------------------------------------------------------------------------------------------------

	Function NumFilter:Int(event:TEvent, context:Object)
		If event.ID = EVENT_KEYCHAR
			If event.data = KEY_BACKSPACE Then Return True
			If event.data = 47 Then Return False
			If event.data &lt; 45 Or event.data &gt; 57 Then Return False
		EndIf
		
		Return True
	End Function

	Function PrettyFloat:String( value:Double )	'by grable
		Extern "C"
			Function modf:Double( value:Double, intpart:Double Var)
			Function snprintf_i:Int( buf:Byte Ptr, szbuf:Int, fmtstr$z, val:Int) = "snprintf"
			Function snprintf_f:Int( buf:Byte Ptr, szbuf:Int, fmtstr$z, val:Double) = "snprintf"	
		EndExtern
		Local i:Double
		Local buf:Byte[32]
		Local l:Int
		' check for whole number
		If modf( value, i) = 0.0 Then 
			l = snprintf_i( buf, SizeOf(buf), "%d", i)
			Return String.FromBytes( buf, l)
		Else
			' eliminate ending zeros except the one after "."
			l = snprintf_f( buf, SizeOf(buf), "%f", value)
			While (l &gt; 0) And (buf[l-1] = Asc("0")) And (buf[l-2] &lt;&gt; Asc("."))
				l :- 1
			Wend
			' terminate it, just in case
			buf[l+1] = 0
			Return String.FromBytes( buf, l)
		EndIf
	EndFunction
	
	Function Round:Double(x:Double)
		Return Int(x + (Sgn(x) * 0.5))
	End Function
	
End Type


</textarea> <br><br></td></tr></table><br>
<a name="820181"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Excellent ! i Really need this ! Thanks :) <br><br></td></tr></table><br>
<a name="820184"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I get <br><div class="quote"> <br> Unhandled Exception : Attempt to access field or method of a null object.<br> <br></div><br>when changing the value in debug mode although it seems OK in release mode. <br>Error is listed against <br><pre class=code>Select event.id</pre> in the tspinner myevents method. <br><br></td></tr></table><br>
<a name="820232"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dmaz</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> my fault.... I was working with debug off....  I will fix.<br>[edit] first post fixed for debug. <br><br></td></tr></table><br>
<a name="857191"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> This code has fundamental problems because you are comparing double floats, and there will always be some inaccuracy. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
