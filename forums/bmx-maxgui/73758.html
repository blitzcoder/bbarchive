<!DOCTYPE html><html lang="en" ><head ><title >Self-contained About window</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Self-contained About window</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >Self-contained About window</a><br><br>
<a name="824152"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> The main program just needs to call CreateAboutWindow() and ShowAboutWindow(), and this routine will take care of the rest, including deactivating the parent and all siblings.<br><br>I find this sort of thing very interesting, because it is no longer linear programming.  You can mix and match all kinds of gadgets and windows to make modular applications.<br><br>Menu action events now contain the menu as the event source. You can use that instead of the event id.  That means you can created sub-windows with menus and their own menu actions in the hook.  The main program doesn't need to care or know what the subwindow is doing.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Global GADGET_ABOUT_WINDOW:TGadget
Global GADGET_ABOUT_OK:TGadget
Global GADGET_ABOUT_ACTIVE:TGadget

Function CreateAboutWindow(text$,group:TGadget)
	Local w,h,x,y
	w=284
	h=192
	GADGET_ABOUT_WINDOW:TGadget=CreateWindow("About",(GadgetWidth(group)-w)/2,(GadgetHeight(group)-h)/2,w,h,group,WINDOW_TITLEBAR|WINDOW_HIDDEN|WINDOW_DIALOG)
	x=8
	y=4
	panel:TGadget=CreatePanel(x,y,GADGET_ABOUT_WINDOW.ClientWidth()-2*x,GADGET_ABOUT_WINDOW.ClientHeight()-y-8-23-8,GADGET_ABOUT_WINDOW,PANEL_GROUP)
	CreateLabel(text$,panel.ClientWidth()/2-60,panel.ClientHeight()/2-32,panel.ClientWidth(),panel.ClientHeight(),panel)
	w=80
	h=23
	GADGET_ABOUT_OK=CreateButton("OK",(GADGET_ABOUT_WINDOW.ClientWidth()-w)/2,GADGET_ABOUT_WINDOW.ClientHeight()-h-8,w,h,GADGET_ABOUT_WINDOW,BUTTON_OK)
	AddHook EmitEventHook,AboutWindowHook
	SetHotKeyEvent KEY_ESCAPE,0,CreateEvent(EVENT_WINDOWCLOSE,GADGET_ABOUT_WINDOW)
EndFunction

Function ShowAboutWindow()
	Local gadget:TGadget
	GADGET_ABOUT_ACTIVE=ActiveGadget()
	ShowGadget GADGET_ABOUT_WINDOW
	ActivateGadget GADGET_ABOUT_OK
	DisableGadget GadgetGroup(GADGET_ABOUT_WINDOW)
	For gadget=EachIn GadgetGroup(GADGET_ABOUT_WINDOW).kids
		If gadget&lt;&gt;GADGET_ABOUT_WINDOW
			DisableGadget gadget
		EndIf
	Next
EndFunction

Function AboutWindowHook:Object(id:Int,data:Object,context:Object)
	If data=Null Return Null
	Local event:TEvent=TEvent(data)
	Select event.id
		
		Case EVENT_GADGETACTION
			Select event.source
				
				Case GADGET_ABOUT_OK
					EmitEvent CreateEvent(EVENT_WINDOWCLOSE,GADGET_ABOUT_WINDOW)
					Return Null
					
			EndSelect

		Case EVENT_WINDOWCLOSE
			Select event.source
				
				Case GADGET_ABOUT_WINDOW
					Local gadget:TGadget
					For gadget=EachIn GadgetGroup(GADGET_ABOUT_WINDOW).kids
						If gadget&lt;&gt;GADGET_ABOUT_WINDOW
							EnableGadget gadget
						EndIf
					Next
					EnableGadget GadgetGroup(GADGET_ABOUT_WINDOW)
					ActivateGadget GADGET_ABOUT_ACTIVE
					HideGadget GADGET_ABOUT_WINDOW
					Return Null
					
			EndSelect
			
	EndSelect
	Return data
EndFunction</textarea> <br><br></td></tr></table><br>
<a name="828828"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's an updated version.  All you do is:<br><br>Import "About.bmx"<br>CreateAboutWindow(MainWindow:TGadget)<br>Create a menu like this:<br><br>GADGET_MENU_ABOUT=CreateMenu("About",0,menu)<br><br>The code will automate everything else for you, including activating the last active window and hiding/showing the about window.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict

Import brl.maxgui

Private

Global GADGET_ABOUT_WINDOW:TGadget
Global GADGET_ABOUT_OK:TGadget
Global GADGET_ABOUT_CANCEL:TGadget
Global GADGET_ABOUT_ACTIVE:TGadget

Public

Global GADGET_MENU_ABOUT:TGadget

Function CreateAboutWindow(text$,group:TGadget)
	Local w,h,x,y,flags
	Local panel:TGadget
	flags=WINDOW_TITLEBAR|WINDOW_HIDDEN'|WINDOW_DIALOG
	w=284
	h=192
	GADGET_ABOUT_WINDOW:TGadget=CreateWindow("About",(GadgetWidth(group)-w)/2,(GadgetHeight(group)-h)/2,w,h,group,flags)
	x=8
	y=4
	panel:TGadget=CreatePanel(x,y,GADGET_ABOUT_WINDOW.ClientWidth()-2*x,GADGET_ABOUT_WINDOW.ClientHeight()-y-8-23-8,GADGET_ABOUT_WINDOW,PANEL_GROUP)
	CreateLabel(text$,panel.ClientWidth()/2-60,panel.ClientHeight()/2-32,panel.ClientWidth(),panel.ClientHeight(),panel)
	w=80
	h=23
	GADGET_ABOUT_OK=CreateButton("OK",(GADGET_ABOUT_WINDOW.ClientWidth()-w)/2,GADGET_ABOUT_WINDOW.ClientHeight()-h-8,w,h,GADGET_ABOUT_WINDOW,BUTTON_OK)
	GADGET_ABOUT_CANCEL=CreateButton("",GADGET_ABOUT_WINDOW.ClientWidth(),0,0,0,GADGET_ABOUT_WINDOW,BUTTON_CANCEL)	
	AddHook EmitEventHook,AboutWindowHook
EndFunction

Private

Function ShowAboutWindow()
	Local gadget:TGadget
	GADGET_ABOUT_ACTIVE=ActiveGadget()
	ShowGadget GADGET_ABOUT_WINDOW
	ActivateGadget GADGET_ABOUT_OK
	DisableGadget GadgetGroup(GADGET_ABOUT_WINDOW)
	For gadget=EachIn GadgetGroup(GADGET_ABOUT_WINDOW).kids
		If gadget&lt;&gt;GADGET_ABOUT_WINDOW
			DisableGadget gadget
		EndIf
	Next
EndFunction

Function AboutWindowHook:Object(id:Int,data:Object,context:Object)
	If data=Null Return Null
	Local event:TEvent=TEvent(data)
	Select event.id
		
		Case EVENT_GADGETACTION
			Select event.source
				
				Case GADGET_ABOUT_OK,GADGET_ABOUT_CANCEL
					EmitEvent CreateEvent(EVENT_WINDOWCLOSE,GADGET_ABOUT_WINDOW)
					Return Null
				
			EndSelect

		Case EVENT_MENUACTION
			Select event.source
				Case GADGET_MENU_ABOUT
					ShowAboutWindow
					Return Null
					
			EndSelect

		Case EVENT_WINDOWCLOSE
			Select event.source
				
				Case GADGET_ABOUT_WINDOW
					Local gadget:TGadget
					For gadget=EachIn GadgetGroup(GADGET_ABOUT_WINDOW).kids
						If gadget&lt;&gt;GADGET_ABOUT_WINDOW
							EnableGadget gadget
						EndIf
					Next
					EnableGadget GadgetGroup(GADGET_ABOUT_WINDOW)
					ActivateGadget GADGET_ABOUT_ACTIVE
					HideGadget GADGET_ABOUT_WINDOW
					Return Null
					
			EndSelect
			
	EndSelect
	Return data
EndFunction

Public</textarea> <br><br></td></tr></table><br>
<a name="876628"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >rs22</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sorry to bring up an old post, but I just wanted to thank you for this. I've been trying to work out a good way to implement dialog boxes in my application, and this seems like it'll work perfectly. Thanks! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
