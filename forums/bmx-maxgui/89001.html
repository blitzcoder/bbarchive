<!DOCTYPE html><html lang="en" ><head ><title >Latest addidition of THelpPanel ?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Latest addidition of THelpPanel ?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >Latest addidition of THelpPanel ?</a><br><br>
<a name="1010467"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Crovean</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've found some old source-code for MaxIDE which I am playing around with... But I can't seem to get the treeview to categorize help topics properly... atm it looks just like the "Index" node in the current version of MaxIDE.<br><br>Can anyone provide me with the code that categorize all the documents?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Type THelpPanel Extends TToolPanel

	Field	host:TCodePlay
	Field	htmlview:TGadget
	Field root:TNode
	Field user:TNode
	Field  lang:TNode
	Field mods:TNode
	
	Method AddLink:TNode(parent:TNode,name$,href$)
		Local	n:TNode
		n=parent.AddNode(name)
		If href href=RealPath(href)
		n.SetAction(Self,TOOLNAVIGATE,href)
		Return n
	End Method

	Method FindLink:TNode(parent:TNode, name$)
		Local n:TNode
		For n = EachIn parent.kids
			If n.name = name Return n
		Next
		Return Null
	EndMethod

	Method ImportLinks(root:TNode,path$)
		Local	stream:TStream
		Local	c$,l$,p,q,a$,n$,cat
		Local	node:TNode
		
		stream=ReadFile(path)
		If Not stream Return
		path=ExtractDir(path)
		node=root
		While Not Eof(stream)
			c=ReadLine(stream)
			If c="" Continue
			If c.Find("Alphabetic index")&lt;&gt;-1
				node=AddLink(root,"Index","")
				Continue
			EndIf
			l=c.ToLower()
			l=l.Replace(QUOTES$,"'")
			p=l.Find("&lt;a")
			If p=-1 Continue
			a$=""
			q=l.find("href=",p)+1
			If q
				If l[q+4]=39	'"'"
					p=q+5
					q=l.find("'",p)
				Else
					p=q+4
					q=l.find(" ",p)
				EndIf
				If q&gt;p 
					a$=path+"/"+c[p..q]
				EndIf
			EndIf
			p=c.Find("&gt;",q)+1
			If p And c[p]=60 p=c.Find("&gt;",p)+1
			q=c.Find("&lt;",p)
			If q&lt;=p Continue
			n$=c[p..q]
'			Print n$
			If l.find("onclick=toggle")&lt;&gt;-1
				node=AddLink(root,n$,a$)
			Else
				AddLink(node,n$,a$)
			EndIf
		Wend
		stream.Close
	End Method
	
	Method SyncDocs()
		mods.FreeKids
		AddLink(mods,"Welcome",host.bmxpath+"/docs/html/index.html")
		ImportLinks mods,host.bmxpath+"/doc/bmxmods/navbar.html"
		mods.Refresh
	End Method

	Method BuildTree()
		root=host.helproot
		user=root.AddNode("UserGuide")
		ImportLinks user,host.bmxpath+"/docs/html/user guide/index.html"
		lang=root.AddNode("Language")
		ImportLinks lang,host.bmxpath+"/docs/html/language/index.html"
		mods=root.AddNode("Modules")
		SyncDocs
		AddLink root,"Samples",host.bmxpath+"/samples/index.html"
		root.refresh
	End Method

	Method Invoke(command,argument:Object=Null)
		Local	href$
		If Not htmlview Return
		Select command
			Case TOOLCUT
				GadgetCut htmlview
			Case TOOLCOPY
				GadgetCopy htmlview
			Case TOOLPASTE
				GadgetPaste htmlview
			Case TOOLSHOW 
				ActivateGadget htmlview	
				host.SetTitle
			Case TOOLNAVIGATE
				href$=String(argument)
				If href Go href
			Case TOOLPRINT
				GadgetPrint htmlview
		End Select
	End Method

	Method OnEvent()
		Local	url$,p,t$
		If EventSource()=htmlview
			Select EventID()			
				Case EVENT_GADGETACTION				'NAVIGATEREQUEST
					url$=String( EventExtra() )
					If url[..5]="http:"
						OpenURL url
					Else
						p=url.findlast(".")
						If p&gt;-1
							t$=url[p..].tolower()
							If t$=".bmx"
								If url.Find( "file://" )=0
									url=url[7..]
									?Win32
									url=url[1..]
									?
								EndIf 
								url=url.Replace("%20"," ")
								Local source:TOpenCode=host.OpenSource(url)
								If source source.MakePathTemp
							Else 
								url=url.Replace("\","/")
								url=url.Replace("user/index","user/index")
								url=url.Replace("lang/index","lang/index")
								url=url.Replace("mods/index","mods/index")
								Go url$
							EndIf
						EndIf
					EndIf
			End Select			
		EndIf
	End Method
	
	Method Go(url$,internal=False)
		Local	node:TNode

		If host.options.externalhelp And Not internal
			PollSystem
			OpenURL url
			MinimizeWindow host.window
			PollSystem
			Return
		EndIf

		HtmlViewGo htmlview,url$
		host.SelectPanel Self
		node=host.helproot.FindArgument(RealPath(url))
		If node
			node.Highlight
		Else
'			print "node not found"
		EndIf		
		ActivateGadget htmlview			
	End Method
	
	Method Home()
		Go host.bmxpath+HOMEPAGE,True
	End Method
	
	Method Forward()
		HtmlViewForward htmlview
	End Method

	Method Back()
		HtmlViewBack htmlview
	End Method

	Function Create:THelpPanel(host:TCodePlay)
		Local	p:THelpPanel
		Local	root,style		
		p=New THelpPanel
		p.host=host
		p.name="Help"
		codeplay.addpanel(p)
		style=HTMLVIEW_NONAVIGATE		'HTMLVIEW_NOCONTEXTMENU
		p.htmlview=CreateHTMLView(0,0,ClientWidth(p.panel),ClientHeight(p.panel),p.panel,style)		
		SetGadgetLayout p.htmlview,1,1,1,1
'		p.Home
		p.buildtree
		Return p
	End Function

End Type
</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
