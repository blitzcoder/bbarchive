<!DOCTYPE html><html lang="en" ><head ><title >WindowMenu refresh on a canvas</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >WindowMenu refresh on a canvas</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >WindowMenu refresh on a canvas</a><br><br>
<a name="804939"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kistjes</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Help!!! I have a very strange problem here with window menus. <br>I have 4 menus: file, edit, view &amp; help.<br>If I have an empty window, there are no problems but if I make a canvas the size of the window, then something strange happens: the first menu I select appears but when I move the mouse cursor over another menu the first will not disappear (only the dropshadow disappears). With all the other menus there are no problems, just with the first one (no matter which menu is the first). I found out that the whole application is 'on hold' when the mouse is on a menu, so there is no possibility of refreshing the canvas, or something and the application does not receive any EventID's.<br><br>Is this a bug? <br><br></td></tr></table><br>
<a name="804944"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kistjes</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here is an example illustrating the bug.<br>Any ideas how to solve this?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' createcanvas.bmx

Strict 

Const MENU_NEW%=101
Const MENU_OPEN%=102
Const MENU_SAVE%=103
Const MENU_CLOSE%=104
Const MENU_PRINT%=105
Const MENU_EXIT%=106

Const MENU_CUT%=107
Const MENU_COPY%=108
Const MENU_PASTE%=109

Const MENU_VIEW1% = 110
Const MENU_VIEW2% = 111
Const MENU_VIEW3% = 112
Const MENU_VIEW4% = 113

Const MENU_ABOUT%=114

Global GAME_WIDTH=640
Global GAME_HEIGHT=480

' create a centered window with client size GAME_WIDTH,GAME_HEIGHT

Local wx=(ClientWidth(Desktop())-GAME_WIDTH)/2
Local wy=(ClientHeight(Desktop())-GAME_HEIGHT)/2

Local window:TGadget=CreateWindow("My Canvas",wx,wy,GAME_WIDTH,GAME_HEIGHT,Null,WINDOW_TITLEBAR|WINDOW_CLIENTCOORDS|WINDOW_MENU)
Local filemenu:TGadget=CreateMenu("&amp;File",0,WindowMenu(window))
CreateMenu"&amp;New",MENU_NEW,filemenu,KEY_N,MODIFIER_COMMAND
CreateMenu"&amp;Open",MENU_OPEN,filemenu,KEY_O,MODIFIER_COMMAND
CreateMenu"&amp;Close",MENU_CLOSE,filemenu,KEY_W,MODIFIER_COMMAND
CreateMenu"",0,filemenu
CreateMenu"&amp;Save",MENU_SAVE,filemenu,KEY_S,MODIFIER_COMMAND
CreateMenu"",0,filemenu
CreateMenu"&amp;Print...", MENU_PRINT,filemenu,KEY_P,MODIFIER_COMMAND
CreateMenu"",0,filemenu
CreateMenu"E&amp;xit",MENU_EXIT,filemenu,KEY_F4,MODIFIER_COMMAND

Local editmenu:TGadget=CreateMenu("&amp;Edit",0,WindowMenu(window))
CreateMenu "Cu&amp;t",MENU_CUT,editmenu,KEY_X,MODIFIER_COMMAND
CreateMenu "&amp;Copy",MENU_COPY,editmenu,KEY_C,MODIFIER_COMMAND
CreateMenu "&amp;Paste",MENU_PASTE,editmenu,KEY_V,MODIFIER_COMMAND

Local viewmenu:TGadget=CreateMenu("&amp;View",0,WindowMenu(window))
CreateMenu("Drawing A",MENU_VIEW1,viewmenu,KEY_1,MODIFIER_COMMAND)
CreateMenu("Drawing B",MENU_VIEW2,viewmenu,KEY_2,MODIFIER_COMMAND)
CreateMenu("Drawing C",MENU_VIEW3,viewmenu,KEY_3,MODIFIER_COMMAND)
CreateMenu("Drawing D",MENU_VIEW4,viewmenu,KEY_4,MODIFIER_COMMAND)

Local helpmenu:TGadget=CreateMenu("&amp;Help",0,WindowMenu(window))
CreateMenu "&amp;About",MENU_ABOUT,helpmenu

UpdateWindowMenu window


' create a canvas for our game

Local canvas:TGadget=CreateCanvas(0,0,320,240,window)

' create an update timer

CreateTimer 60

While WaitEvent()
	Select EventID()
		Case EVENT_TIMERTICK
			RedrawGadget canvas

		Case EVENT_GADGETPAINT
			SetGraphics CanvasGraphics(canvas)
			SetOrigin 160,120
			SetLineWidth 5
			Cls
			Local t=MilliSecs()
			DrawLine 0,0,120*Cos(t),120*Sin(t)
			DrawLine 0,0,80*Cos(t/60),80*Sin(t/60)
			Flip

		Case EVENT_MOUSEMOVE
			Print "MOVE!"

		Case EVENT_WINDOWCLOSE
			FreeGadget canvas
			End

		Case EVENT_APPTERMINATE
			End
	End Select
Wend</textarea> <br><br></td></tr></table><br>
<a name="804953"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> This can be solved by using hooks.<br><br>Look at the <b>paint</b> function ;)<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict 

Const MENU_NEW%=101
Const MENU_OPEN%=102
Const MENU_SAVE%=103
Const MENU_CLOSE%=104
Const MENU_PRINT%=105
Const MENU_EXIT%=106

Const MENU_CUT%=107
Const MENU_COPY%=108
Const MENU_PASTE%=109

Const MENU_VIEW1% = 110
Const MENU_VIEW2% = 111
Const MENU_VIEW3% = 112
Const MENU_VIEW4% = 113

Const MENU_ABOUT%=114

Global GAME_WIDTH=640
Global GAME_HEIGHT=480

' create a centered window with client size GAME_WIDTH,GAME_HEIGHT

Local wx=(ClientWidth(Desktop())-GAME_WIDTH)/2
Local wy=(ClientHeight(Desktop())-GAME_HEIGHT)/2

Local window:TGadget=CreateWindow("My Canvas",wx,wy,GAME_WIDTH,GAME_HEIGHT,Null,WINDOW_TITLEBAR|WINDOW_CLIENTCOORDS|WINDOW_MENU)
Local filemenu:TGadget=CreateMenu("&amp;File",0,WindowMenu(window))
CreateMenu"&amp;New",MENU_NEW,filemenu,KEY_N,MODIFIER_COMMAND
CreateMenu"&amp;Open",MENU_OPEN,filemenu,KEY_O,MODIFIER_COMMAND
CreateMenu"&amp;Close",MENU_CLOSE,filemenu,KEY_W,MODIFIER_COMMAND
CreateMenu"",0,filemenu
CreateMenu"&amp;Save",MENU_SAVE,filemenu,KEY_S,MODIFIER_COMMAND
CreateMenu"",0,filemenu
CreateMenu"&amp;Print...", MENU_PRINT,filemenu,KEY_P,MODIFIER_COMMAND
CreateMenu"",0,filemenu
CreateMenu"E&amp;xit",MENU_EXIT,filemenu,KEY_F4,MODIFIER_COMMAND

Local editmenu:TGadget=CreateMenu("&amp;Edit",0,WindowMenu(window))
CreateMenu "Cu&amp;t",MENU_CUT,editmenu,KEY_X,MODIFIER_COMMAND
CreateMenu "&amp;Copy",MENU_COPY,editmenu,KEY_C,MODIFIER_COMMAND
CreateMenu "&amp;Paste",MENU_PASTE,editmenu,KEY_V,MODIFIER_COMMAND

Local viewmenu:TGadget=CreateMenu("&amp;View",0,WindowMenu(window))
CreateMenu("Drawing A",MENU_VIEW1,viewmenu,KEY_1,MODIFIER_COMMAND)
CreateMenu("Drawing B",MENU_VIEW2,viewmenu,KEY_2,MODIFIER_COMMAND)
CreateMenu("Drawing C",MENU_VIEW3,viewmenu,KEY_3,MODIFIER_COMMAND)
CreateMenu("Drawing D",MENU_VIEW4,viewmenu,KEY_4,MODIFIER_COMMAND)

Local helpmenu:TGadget=CreateMenu("&amp;Help",0,WindowMenu(window))
CreateMenu "&amp;About",MENU_ABOUT,helpmenu

UpdateWindowMenu window


' create a canvas for our game

Global canvas:TGadget=CreateCanvas(0,0,320,240,window)

' create an update timer

CreateTimer 60

AddHook EmitEventHook, paint

Function paint:Object( id:Int, data:Object, context:Object)
	Local ev:TEvent = TEvent(data)
	If Not ev Then Return data
	If ev.ID = EVENT_TIMERTICK Then
		RedrawGadget canvas
	ElseIf ev.ID = EVENT_GADGETPAINT Then
		SetGraphics CanvasGraphics( canvas)
		SetOrigin 160,120
		SetLineWidth 5
		Cls
		Local t=MilliSecs()
		DrawLine 0,0,120*Cos(t),120*Sin(t)
		DrawLine 0,0,80*Cos(t/60),80*Sin(t/60)
		Flip
		Return Null
	EndIf
	Return data
EndFunction

While WaitEvent()
	Select EventID()
		Case EVENT_WINDOWCLOSE
			FreeGadget canvas
			End

		Case EVENT_APPTERMINATE
			End
	End Select
Wend
</textarea> <br><br></td></tr></table><br>
<a name="804959"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kistjes</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks a lot!<br>I'll look at the code more closer later to see how it works. <br><br></td></tr></table><br>
<a name="805558"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kistjes</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> The problem is not completely solved yet :(<br><br>Run the code below. It's part of a project I'm doing. I have an application window with a canvas and I have a (fake) printer driver window (also with a canvas). Via the file-menu you can call the printer driver window or by pressing Ctrl-P.<br>If you do not use any setScale, setRotation or setTransform code it seems to work. But I have to do a lot of imaging stuff within the draw method of the printer window. And within the application window I need to be able to offer 4 different views (in this example I use colors, but that will be 4 images).<br><br>Modify the code as explained in the code to see the strange effects.<br>Tell me... what am I doing wrong???<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict 

Global gApplicationWindow:TApplicationWindow = TApplicationWindow.create()
Global gPrinterDriver:TPrinterDriver = TPrinterDriver.create()


CreateTimer(60)

EnablePolledInput()

AddHook EmitEventHook, paint

gApplicationWindow.open()

Local iEventID%


While True
	PollEvent()
	iEventID = EventID()
	If gPrinterDriver.isOpen() Then
		gPrinterDriver.update(iEventID)
		gPrinterDriver.draw()
	Else
		gApplicationWindow.update(iEventID)
		gApplicationWindow.draw()
	End If
Wend

End


Function paint:Object( id:Int, data:Object, context:Object)
	Local ev:TEvent = TEvent(data)
	If Not ev Then Return data
	If ev.ID = EVENT_TIMERTICK Then
		gApplicationWindow.update(ev.ID)
		gApplicationWindow.draw()
		Return Null
	EndIf
	Return data
End Function



Type TColor
	Field ir%, ig%, ib%
	
	Function create:TColor(r%, g%, b%)
		Local c:TColor = New TColor
		c.ir = r
		c.ig = g
		c.ib = b
		Return c
	End Function

	Method setThisColor()
		SetColor(ir, ig, ib)
	End Method
	
End Type


Type TWindow
	Field poWindow:TGadget
	Field poCanvas:TGadget
	
	Method open()
		draw()
		ActivateWindow(poWindow)
		ShowGadget(poWindow)
	End Method
	
	Method close()
		HideGadget(poWindow)
	End Method

	Method isOpen:Byte()
		Return (Not GadgetHidden(poWindow))
	End Method
	
	Method getWindow:TGadget()
		Return poWindow
	End Method
	
	Method getCanvas:TGadget()
		Return poCanvas
	End Method

	Method update(iEventID%) Abstract
	
	Method draw() Abstract
	
End Type


Type TApplicationWindow Extends TWindow

	Const MENU_NEW%=101
	Const MENU_OPEN%=102
	Const MENU_SAVE%=103
	Const MENU_CLOSE%=104
	Const MENU_PRINT%=105
	Const MENU_EXIT%=106
	
	Const MENU_CUT%=107
	Const MENU_COPY%=108
	Const MENU_PASTE%=109
	
	Const MENU_VIEW1% = 110
	Const MENU_VIEW2% = 111
	Const MENU_VIEW3% = 112
	Const MENU_VIEW4% = 113
	
	Const MENU_ABOUT%=114

	Field ploViewMenu:TGadget[4]
	Field ploBgnd:TColor[4]
	Field piBgnd% = 0

	Function create:TApplicationWindow()
		Local a:TApplicationWindow = New TApplicationWindow 
		Local filemenu:TGadget
		Local editmenu:TGadget
		Local viewmenu:TGadget
		Local helpmenu:TGadget
		
		Local style% = WINDOW_TITLEBAR | WINDOW_CLIENTCOORDS | WINDOW_MENU| WINDOW_HIDDEN
		a.poWindow = CreateWindow("Application", 400, 150, 1024, 768, Null, style)
		
		filemenu=CreateMenu("&amp;File",0,WindowMenu(a.poWindow))
		CreateMenu"&amp;New",MENU_NEW,filemenu,KEY_N,MODIFIER_COMMAND
		CreateMenu"&amp;Open",MENU_OPEN,filemenu,KEY_O,MODIFIER_COMMAND
		CreateMenu"&amp;Close",MENU_CLOSE,filemenu,KEY_W,MODIFIER_COMMAND
		CreateMenu"",0,filemenu
		CreateMenu"&amp;Save",MENU_SAVE,filemenu,KEY_S,MODIFIER_COMMAND
		CreateMenu"",0,filemenu
		CreateMenu"&amp;Print...", MENU_PRINT,filemenu,KEY_P,MODIFIER_COMMAND
		CreateMenu"",0,filemenu
		CreateMenu"E&amp;xit",MENU_EXIT,filemenu,KEY_F4,MODIFIER_COMMAND
		
		editmenu=CreateMenu("&amp;Edit",0,WindowMenu(a.poWindow))
		CreateMenu "Cu&amp;t",MENU_CUT,editmenu,KEY_X,MODIFIER_COMMAND
		CreateMenu "&amp;Copy",MENU_COPY,editmenu,KEY_C,MODIFIER_COMMAND
		CreateMenu "&amp;Paste",MENU_PASTE,editmenu,KEY_V,MODIFIER_COMMAND
		
		viewmenu=CreateMenu("&amp;View",0,WindowMenu(a.poWindow))
		a.ploViewMenu[0] = CreateMenu("Background 1",MENU_VIEW1,viewmenu,KEY_1,MODIFIER_COMMAND)
		a.ploViewMenu[1] = CreateMenu("Background 2",MENU_VIEW2,viewmenu,KEY_2,MODIFIER_COMMAND)
		a.ploViewMenu[2] = CreateMenu("Background 3",MENU_VIEW3,viewmenu,KEY_3,MODIFIER_COMMAND)
		a.ploViewMenu[3] = CreateMenu("Background 4",MENU_VIEW4,viewmenu,KEY_4,MODIFIER_COMMAND)
		
		helpmenu=CreateMenu("&amp;Help",0,WindowMenu(a.poWindow))
		CreateMenu "&amp;About",MENU_ABOUT,helpmenu
		
		Local dw% = GadgetWidth(a.poWindow)
		Local dh% = GadgetHeight(a.poWindow)
		a.poCanvas:TGadget = CreateCanvas(0, 0, dw, dh, a.poWindow) 

		a.ploBgnd[0] = TColor.create(250, 250, 200)
		a.ploBgnd[1] = TColor.create(200, 250, 200)
		a.ploBgnd[2] = TColor.create(200, 200, 250)
		a.ploBgnd[3] = TColor.create(250, 200, 200)
		
		a.selectBgnd(0) 
		Return a
	End Function


	Method update(iEventID%)
		Select iEventID
			Case EVENT_WINDOWCLOSE
				End
			Case EVENT_MENUACTION
				Select EventData()
					Case MENU_EXIT
						End
					Case MENU_PRINT
						gPrinterDriver.open()
					Case MENU_ABOUT
						draw()
						Notify("My First Multiple Window Application", False)
					Case MENU_VIEW1
						selectBgnd(0)
					Case MENU_VIEW2
						selectBgnd(1)
					Case MENU_VIEW3
						selectBgnd(2)
					Case MENU_VIEW4
						selectBgnd(3)
				End Select
		End Select
	End Method
	
	Method draw()
		SetGraphics CanvasGraphics(poCanvas)
		Cls	
' **** function like setTransform result in strange behavior (rem the next line to see the correct behavior)
		SetTransform 1, 0, 0

		ploBgnd[piBgnd].setThisColor()
		DrawRect(0, 0, GadgetWidth(poCanvas), GadgetHeight(poCanvas))
		Flip
	End Method
	
	
	Method selectBgnd(iBgnd%)
		piBgnd = iBgnd
		For Local i% = 0 To 3
			If i = piBgnd Then
				CheckMenu(ploViewMenu[i])
			Else
				UncheckMenu(ploViewMenu[i])
			End If
		Next		
		UpdateWindowMenu poWindow
	End Method
	
End Type


Type TPrinterDriver Extends TWindow

	Field poImage:TImage
	
	Function create:TPrinterDriver()
		Local p:TPrinterDriver = New TPrinterDriver
		Local dw% = GadgetWidth(Desktop())
		Local dh% = GadgetHeight(Desktop())
		Local dww% = 800
		Local dwh% = 600
		
		p.poWindow = CreateWindow("Print",(dw - dww) / 2, (dh -dwh) / 2, dww, dwh, Null, WINDOW_TITLEBAR|WINDOW_CLIENTCOORDS|WINDOW_HIDDEN) 
		p.poCanvas:TGadget = CreateCanvas(0, 0, dww, dwh, p.poWindow)

		p.poImage = CreateImage(100, 100)
		Return p
	End Function
	
	
	Method update(iEventID%)
		Select iEventID
			Case EVENT_WINDOWCLOSE
				HideGadget(poWindow)
		End Select
	End Method

	
	Method draw()
		SetGraphics CanvasGraphics(poCanvas)
		SetClsColor 255, 255, 255
		Cls
		SetColor 0, 0, 0
		DrawText String(MouseX()) +","+String(MouseY()),10,10
'Rem   **** if you remove te next 4 lines you see that the screen-refresh works ok
		SetColor 255, 255, 255
		SetTransform(45, 2, 2)
		DrawImage(poImage, 100, 100)
		SetTransform(1, 0, 0)
'End Rem		
		Flip
	End Method
		
End Type
</textarea> <br><br></td></tr></table><br>
<a name="805590"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> It locks up on my puter, but adding the EVENT_GADGETPAINT to the hook makes it responsive.<br><br>About the SetTransform.. I assume your resetting it here?<pre class=code>SetTransform 1, 0, 0
' should be
SetTransform 0, 1,1
' or just
SetTransform</pre>Scaling something to 0,0 just makes it disappear ;)<br><br>And when your doing SetTransform (or any of the other transform commands), its a good idea to reset it each loop so you know your starting point.<br><br>Heres the modified code:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict 

Global gApplicationWindow:TApplicationWindow = TApplicationWindow.create()
Global gPrinterDriver:TPrinterDriver = TPrinterDriver.create()


CreateTimer(60)

EnablePolledInput()

AddHook EmitEventHook, paint

gApplicationWindow.open()

Local iEventID%


While True
	PollEvent()
	iEventID = EventID()
	If gPrinterDriver.isOpen() Then
		gPrinterDriver.update(iEventID)
		gPrinterDriver.draw()
	Else
		gApplicationWindow.update(iEventID)
		gApplicationWindow.draw()
	End If
Wend

End


Function paint:Object( id:Int, data:Object, context:Object)
	Local ev:TEvent = TEvent(data)
	If Not ev Then Return data
	If ev.ID = EVENT_TIMERTICK Then
		gApplicationWindow.update(ev.ID)
	ElseIf ev.ID = EVENT_GADGETPAINT Then  '&lt;--
		gApplicationWindow.draw()
	Else
		Return data
	EndIf
	Return Null
End Function



Type TColor
	Field ir%, ig%, ib%
	
	Function create:TColor(r%, g%, b%)
		Local c:TColor = New TColor
		c.ir = r
		c.ig = g
		c.ib = b
		Return c
	End Function

	Method setThisColor()
		SetColor(ir, ig, ib)
	End Method
	
End Type


Type TWindow
	Field poWindow:TGadget
	Field poCanvas:TGadget
	
	Method open()
		draw()
		ActivateWindow(poWindow)
		ShowGadget(poWindow)
	End Method
	
	Method close()
		HideGadget(poWindow)
	End Method

	Method isOpen:Byte()
		Return (Not GadgetHidden(poWindow))
	End Method
	
	Method getWindow:TGadget()
		Return poWindow
	End Method
	
	Method getCanvas:TGadget()
		Return poCanvas
	End Method

	Method update(iEventID%) Abstract
	
	Method draw() Abstract
	
End Type


Type TApplicationWindow Extends TWindow

	Const MENU_NEW%=101
	Const MENU_OPEN%=102
	Const MENU_SAVE%=103
	Const MENU_CLOSE%=104
	Const MENU_PRINT%=105
	Const MENU_EXIT%=106
	
	Const MENU_CUT%=107
	Const MENU_COPY%=108
	Const MENU_PASTE%=109
	
	Const MENU_VIEW1% = 110
	Const MENU_VIEW2% = 111
	Const MENU_VIEW3% = 112
	Const MENU_VIEW4% = 113
	
	Const MENU_ABOUT%=114

	Field ploViewMenu:TGadget[4]
	Field ploBgnd:TColor[4]
	Field piBgnd% = 0

	Function create:TApplicationWindow()
		Local a:TApplicationWindow = New TApplicationWindow 
		Local filemenu:TGadget
		Local editmenu:TGadget
		Local viewmenu:TGadget
		Local helpmenu:TGadget
		
		Local style% = WINDOW_TITLEBAR | WINDOW_CLIENTCOORDS | WINDOW_MENU| WINDOW_HIDDEN
		a.poWindow = CreateWindow("Application", 400, 150, 1024, 768, Null, style)
		
		filemenu=CreateMenu("&amp;File",0,WindowMenu(a.poWindow))
		CreateMenu"&amp;New",MENU_NEW,filemenu,KEY_N,MODIFIER_COMMAND
		CreateMenu"&amp;Open",MENU_OPEN,filemenu,KEY_O,MODIFIER_COMMAND
		CreateMenu"&amp;Close",MENU_CLOSE,filemenu,KEY_W,MODIFIER_COMMAND
		CreateMenu"",0,filemenu
		CreateMenu"&amp;Save",MENU_SAVE,filemenu,KEY_S,MODIFIER_COMMAND
		CreateMenu"",0,filemenu
		CreateMenu"&amp;Print...", MENU_PRINT,filemenu,KEY_P,MODIFIER_COMMAND
		CreateMenu"",0,filemenu
		CreateMenu"E&amp;xit",MENU_EXIT,filemenu,KEY_F4,MODIFIER_COMMAND
		
		editmenu=CreateMenu("&amp;Edit",0,WindowMenu(a.poWindow))
		CreateMenu "Cu&amp;t",MENU_CUT,editmenu,KEY_X,MODIFIER_COMMAND
		CreateMenu "&amp;Copy",MENU_COPY,editmenu,KEY_C,MODIFIER_COMMAND
		CreateMenu "&amp;Paste",MENU_PASTE,editmenu,KEY_V,MODIFIER_COMMAND
		
		viewmenu=CreateMenu("&amp;View",0,WindowMenu(a.poWindow))
		a.ploViewMenu[0] = CreateMenu("Background 1",MENU_VIEW1,viewmenu,KEY_1,MODIFIER_COMMAND)
		a.ploViewMenu[1] = CreateMenu("Background 2",MENU_VIEW2,viewmenu,KEY_2,MODIFIER_COMMAND)
		a.ploViewMenu[2] = CreateMenu("Background 3",MENU_VIEW3,viewmenu,KEY_3,MODIFIER_COMMAND)
		a.ploViewMenu[3] = CreateMenu("Background 4",MENU_VIEW4,viewmenu,KEY_4,MODIFIER_COMMAND)
		
		helpmenu=CreateMenu("&amp;Help",0,WindowMenu(a.poWindow))
		CreateMenu "&amp;About",MENU_ABOUT,helpmenu
		
		Local dw% = GadgetWidth(a.poWindow)
		Local dh% = GadgetHeight(a.poWindow)
		a.poCanvas:TGadget = CreateCanvas(0, 0, dw, dh, a.poWindow) 

		a.ploBgnd[0] = TColor.create(250, 250, 200)
		a.ploBgnd[1] = TColor.create(200, 250, 200)
		a.ploBgnd[2] = TColor.create(200, 200, 250)
		a.ploBgnd[3] = TColor.create(250, 200, 200)
		
		a.selectBgnd(0) 
		Return a
	End Function


	Method update(iEventID%)
		Select iEventID
			Case EVENT_WINDOWCLOSE
				End
			Case EVENT_MENUACTION
				Select EventData()
					Case MENU_EXIT
						End
					Case MENU_PRINT
						gPrinterDriver.open()
					Case MENU_ABOUT
						draw()
						Notify("My First Multiple Window Application", False)
					Case MENU_VIEW1
						selectBgnd(0)
					Case MENU_VIEW2
						selectBgnd(1)
					Case MENU_VIEW3
						selectBgnd(2)
					Case MENU_VIEW4
						selectBgnd(3)
				End Select
		End Select
	End Method
	
	Method draw()
		SetGraphics CanvasGraphics(poCanvas)
		Cls	
		SetTransform  '&lt;--
' **** function like setTransform result in strange behavior (rem the next line to see the correct behavior)
		'SetTransform 1, 0, 0

		ploBgnd[piBgnd].setThisColor()
		DrawRect(0, 0, GadgetWidth(poCanvas), GadgetHeight(poCanvas))
		Flip
	End Method
	
	
	Method selectBgnd(iBgnd%)
		piBgnd = iBgnd
		For Local i% = 0 To 3
			If i = piBgnd Then
				CheckMenu(ploViewMenu[i])
			Else
				UncheckMenu(ploViewMenu[i])
			End If
		Next		
		UpdateWindowMenu poWindow
	End Method
	
End Type


Type TPrinterDriver Extends TWindow

	Field poImage:TImage
	
	Function create:TPrinterDriver()
		Local p:TPrinterDriver = New TPrinterDriver
		Local dw% = GadgetWidth(Desktop())
		Local dh% = GadgetHeight(Desktop())
		Local dww% = 800
		Local dwh% = 600
		
		p.poWindow = CreateWindow("Print",(dw - dww) / 2, (dh -dwh) / 2, dww, dwh, Null, WINDOW_TITLEBAR|WINDOW_CLIENTCOORDS|WINDOW_HIDDEN) 
		p.poCanvas:TGadget = CreateCanvas(0, 0, dww, dwh, p.poWindow)

		p.poImage = CreateImage(100, 100)
		Return p
	End Function
	
	
	Method update(iEventID%)
		Select iEventID
			Case EVENT_WINDOWCLOSE
				HideGadget(poWindow)
		End Select
	End Method

	
	Method draw()
		SetGraphics CanvasGraphics(poCanvas)
		SetClsColor 255, 255, 255
		Cls
		SetTransform '&lt;--
		SetColor 0, 0, 0
		DrawText String(MouseX()) +","+String(MouseY()),10,10
'Rem   **** if you remove te next 4 lines you see that the screen-refresh works ok
		SetColor 255, 255, 255
		SetTransform(45, 2, 2)
		DrawImage(poImage, 100, 100)
		SetTransform(1, 0, 0)
'End Rem		
		Flip
	End Method
		
End Type
</textarea> <br><br></td></tr></table><br>
<a name="805596"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kistjes</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes, you're right about setTransform. Stupid mistake!!<br><br>First of all, thanks again for your advice. I <b>have</b> to solve this stupid problem!<br>But about the modification you made. As soon as I open the printer driver it draws the image and mouse location in both windows. When I move the Printer driver window the Application windows refreshes.<br><br>I'll try to solve this myself, but I hope you can give me a clue. I still do not really understand how it works with the hook-thing (BlitzMax help is very poor on this item). <br><br></td></tr></table><br>
<a name="805601"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oh, didnt notice that. But its only the first frame just before the window is opened it seems.<br><br>Hooks work by intercepting Events (and other stuff) before they reach their destination.<br>The hook can also stop the event from reaching the event queue by returning Null.<br><br>Make sure both Canvases are updated each timer tick, and that they are separated properly.<br>Passing the whole TEvent object to your Application class instead of just the ID might help, as it has a Source field containing the Gadget the event originated from and some other goodies. <br><br></td></tr></table><br>
<a name="805605"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kistjes</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> I tried my best but I'm getting more and more confused about the behavior of the events. <br>As long as I do not move or close the printer driver window the content of both windows remains the same. Why? <br><br></td></tr></table><br>
<a name="805609"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> I modified the code to my suggestions above, so now the Update method takes a TEvent and filters its actiond depending on the Source field.<br><br>I also changed the Hook to reflect this, and removed the code from the main loop as its no longer needed.<br><br>Hope this helps =)<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict 

Global gApplicationWindow:TApplicationWindow = TApplicationWindow.create()
Global gPrinterDriver:TPrinterDriver = TPrinterDriver.create()


CreateTimer(60)

EnablePolledInput()

AddHook EmitEventHook, paint

gApplicationWindow.open()

Local iEventID%


While WaitEvent()
Wend

End


Function paint:Object( id:Int, data:Object, context:Object)
	Local ev:TEvent = TEvent(data)
	If Not ev Then Return data
	gApplicationWindow.update(ev)
	If gPrinterDriver.IsOpen() Then gPrinterDriver.update(ev)
	Return data
End Function



Type TColor
	Field ir%, ig%, ib%
	
	Function create:TColor(r%, g%, b%)
		Local c:TColor = New TColor
		c.ir = r
		c.ig = g
		c.ib = b
		Return c
	End Function

	Method setThisColor()
		SetColor(ir, ig, ib)
	End Method
	
End Type


Type TWindow
	Field poWindow:TGadget
	Field poCanvas:TGadget
	
	Method open()
		draw()
		ActivateWindow(poWindow)
		ShowGadget(poWindow)
	End Method
	
	Method close()
		HideGadget(poWindow)
	End Method

	Method isOpen:Byte()
		Return (Not GadgetHidden(poWindow))
	End Method
	
	Method getWindow:TGadget()
		Return poWindow
	End Method
	
	Method getCanvas:TGadget()
		Return poCanvas
	End Method

	Method update(event:TEvent) Abstract
	
	Method draw() Abstract
	
End Type


Type TApplicationWindow Extends TWindow

	Const MENU_NEW%=101
	Const MENU_OPEN%=102
	Const MENU_SAVE%=103
	Const MENU_CLOSE%=104
	Const MENU_PRINT%=105
	Const MENU_EXIT%=106
	
	Const MENU_CUT%=107
	Const MENU_COPY%=108
	Const MENU_PASTE%=109
	
	Const MENU_VIEW1% = 110
	Const MENU_VIEW2% = 111
	Const MENU_VIEW3% = 112
	Const MENU_VIEW4% = 113
	
	Const MENU_ABOUT%=114

	Field ploViewMenu:TGadget[4]
	Field ploBgnd:TColor[4]
	Field piBgnd% = 0

	Function create:TApplicationWindow()
		Local a:TApplicationWindow = New TApplicationWindow 
		Local filemenu:TGadget
		Local editmenu:TGadget
		Local viewmenu:TGadget
		Local helpmenu:TGadget
		
		Local style% = WINDOW_TITLEBAR | WINDOW_CLIENTCOORDS | WINDOW_MENU| WINDOW_HIDDEN
		a.poWindow = CreateWindow("Application", 400, 150, 1024, 768, Null, style)
		
		filemenu=CreateMenu("&amp;File",0,WindowMenu(a.poWindow))
		CreateMenu"&amp;New",MENU_NEW,filemenu,KEY_N,MODIFIER_COMMAND
		CreateMenu"&amp;Open",MENU_OPEN,filemenu,KEY_O,MODIFIER_COMMAND
		CreateMenu"&amp;Close",MENU_CLOSE,filemenu,KEY_W,MODIFIER_COMMAND
		CreateMenu"",0,filemenu
		CreateMenu"&amp;Save",MENU_SAVE,filemenu,KEY_S,MODIFIER_COMMAND
		CreateMenu"",0,filemenu
		CreateMenu"&amp;Print...", MENU_PRINT,filemenu,KEY_P,MODIFIER_COMMAND
		CreateMenu"",0,filemenu
		CreateMenu"E&amp;xit",MENU_EXIT,filemenu,KEY_F4,MODIFIER_COMMAND
		
		editmenu=CreateMenu("&amp;Edit",0,WindowMenu(a.poWindow))
		CreateMenu "Cu&amp;t",MENU_CUT,editmenu,KEY_X,MODIFIER_COMMAND
		CreateMenu "&amp;Copy",MENU_COPY,editmenu,KEY_C,MODIFIER_COMMAND
		CreateMenu "&amp;Paste",MENU_PASTE,editmenu,KEY_V,MODIFIER_COMMAND
		
		viewmenu=CreateMenu("&amp;View",0,WindowMenu(a.poWindow))
		a.ploViewMenu[0] = CreateMenu("Background 1",MENU_VIEW1,viewmenu,KEY_1,MODIFIER_COMMAND)
		a.ploViewMenu[1] = CreateMenu("Background 2",MENU_VIEW2,viewmenu,KEY_2,MODIFIER_COMMAND)
		a.ploViewMenu[2] = CreateMenu("Background 3",MENU_VIEW3,viewmenu,KEY_3,MODIFIER_COMMAND)
		a.ploViewMenu[3] = CreateMenu("Background 4",MENU_VIEW4,viewmenu,KEY_4,MODIFIER_COMMAND)
		
		helpmenu=CreateMenu("&amp;Help",0,WindowMenu(a.poWindow))
		CreateMenu "&amp;About",MENU_ABOUT,helpmenu
		
		Local dw% = GadgetWidth(a.poWindow)
		Local dh% = GadgetHeight(a.poWindow)
		a.poCanvas:TGadget = CreateCanvas(0, 0, dw, dh, a.poWindow) 

		a.ploBgnd[0] = TColor.create(250, 250, 200)
		a.ploBgnd[1] = TColor.create(200, 250, 200)
		a.ploBgnd[2] = TColor.create(200, 200, 250)
		a.ploBgnd[3] = TColor.create(250, 200, 200)
		
		a.selectBgnd(0) 
		Return a
	End Function


	Method update(event:TEvent)
		Select event.ID
			Case EVENT_TIMERTICK
				RedrawGadget poCanvas
			Case EVENT_WINDOWCLOSE
				If event.Source = poWindow Then End
			Case EVENT_MENUACTION
				Select event.Data
					Case MENU_EXIT
						End
					Case MENU_PRINT
						gPrinterDriver.open()
					Case MENU_ABOUT
						draw()
						Notify("My First Multiple Window Application", False)
					Case MENU_VIEW1
						selectBgnd(0)
					Case MENU_VIEW2
						selectBgnd(1)
					Case MENU_VIEW3
						selectBgnd(2)
					Case MENU_VIEW4
						selectBgnd(3)
				End Select
			Case EVENT_GADGETPAINT
				If event.Source = poCanvas Then draw()
		End Select
	End Method
	
	Method draw()
		SetGraphics CanvasGraphics(poCanvas)
		Cls	
		SetTransform  '&lt;--
' **** function like setTransform result in strange behavior (rem the next line to see the correct behavior)
		'SetTransform 1, 0, 0

		ploBgnd[piBgnd].setThisColor()
		DrawRect(0, 0, GadgetWidth(poCanvas), GadgetHeight(poCanvas))
		Flip
	End Method
	
	
	Method selectBgnd(iBgnd%)
		piBgnd = iBgnd
		For Local i% = 0 To 3
			If i = piBgnd Then
				CheckMenu(ploViewMenu[i])
			Else
				UncheckMenu(ploViewMenu[i])
			End If
		Next		
		UpdateWindowMenu poWindow
	End Method
	
End Type


Type TPrinterDriver Extends TWindow

	Field poImage:TImage
	
	Function create:TPrinterDriver()
		Local p:TPrinterDriver = New TPrinterDriver
		Local dw% = GadgetWidth(Desktop())
		Local dh% = GadgetHeight(Desktop())
		Local dww% = 800
		Local dwh% = 600
		
		p.poWindow = CreateWindow("Print",(dw - dww) / 2, (dh -dwh) / 2, dww, dwh, Null, WINDOW_TITLEBAR|WINDOW_CLIENTCOORDS|WINDOW_HIDDEN) 
		p.poCanvas:TGadget = CreateCanvas(0, 0, dww, dwh, p.poWindow)

		p.poImage = CreateImage(100, 100)
		Return p
	End Function
	
	
	Method update(event:TEvent)
		Select event.ID
			Case EVENT_TIMERTICK
				RedrawGadget poCanvas
			Case EVENT_WINDOWCLOSE
				If event.Source = poWindow Then HideGadget(poWindow)
			Case EVENT_GADGETPAINT
				If event.Source = poCanvas Then draw()
		End Select
	End Method

	
	Method draw()
		SetGraphics CanvasGraphics(poCanvas)
		SetClsColor 255, 255, 255
		Cls
		SetTransform '&lt;--
		SetColor 0, 0, 0
		DrawText String(MouseX()) +","+String(MouseY()),10,10
'Rem   **** if you remove te next 4 lines you see that the screen-refresh works ok
		SetColor 255, 255, 255
		SetTransform(45, 2, 2)
		DrawImage(poImage, 100, 100)
		SetTransform(1, 0, 0)
'End Rem		
		Flip
	End Method
		
End Type
</textarea> <br><br></td></tr></table><br>
<a name="805615"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kistjes</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> I checked the code and everythings works the way it should. You are a hero! <br>I have to go now. I'll try your modifications in my real project to night and let you know the results!<br>Thanks again! <br><br></td></tr></table><br>
<a name="805672"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kistjes</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok. Modified my project conform your suggestions and now everything works great!<br>It all started with a windows menu that did not refresh properly. I still wonder if needs to be that complicated to accomplish what I want, although the finel result shows some nice clean code.<br>Many thanks for all your effort! Now I can focus on the rest of the project. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
