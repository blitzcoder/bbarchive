<!DOCTYPE html><html lang="en" ><head ><title >Living Gadgets</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Living Gadgets</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >Living Gadgets</a><br><br>
<a name="755734"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Fetze</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I was just playing with the GUI - but I think, you could create some kind of game using the idea. Go on if you want to develop something out of it, unfortunately I don't have the time to - other projects are more important ^^<br><br><pre class=code>
SuperStrict
SeedRnd MilliSecs()

Global MainTimer:TTimer = CreateTimer(60)
Global MainWindow:TGadget = CreateWindow("Buttonwars", 200, 200, 640, 480, Null, WINDOW_TITLEBAR)

Local lbTemp:LivingButton
For Local iLoop:Int = 1 To 50
	lbTemp = LivingButton.Create("Muahar", Rand(0, 640), Rand(0, 480), 20, 40, MainWindow)
	lbTemp.fSpeed = Rnd(0.1, 1.0)
	lbTemp.bAngle = Rand(0, 3)
Next

Repeat
	LivingGadget.UpdateAll()
	WaitTimer(MainTimer)
	PollEvent()
Until EventID() = EVENT_WINDOWCLOSE

End



Type LivingGadget Abstract
	Global listAll	:TList = New TList
	
	Field gadObject	:TGadget
	Field gadParent	:TGadget
	Field sName		:String	'Name
	Field fX		:Float	'Position
	Field fY		:Float	
	Field fSpeed	:Float	'Speed
	Field fWidth	:Float	'Size
	Field fHeight	:Float	
	Field bAngle	:Byte	'Angle. Actually, there's just 0, 1, 2 and 3 for 0°, 90°, 180° and 270°
	Field bIsDeleted:Byte	'Scheduled for Removal?
	
	Method New()
		listAll.AddLast Self
	End Method
	
	Method Remove:Byte()
		listAll.Remove Self
		FreeGadget(gadObject)
		gadObject = Null
		bIsDeleted = True
		Return True
	End Method
	
	Method GetEdgeCoords:Byte(fVarX:Float Var, fVarY:Float Var, fVarW:Float Var, fVarH:Float Var)
		Select bAngle
			Case 0 '0°
				fVarX = fX - (fWidth / 2.0)
				fVarY = fY - (fHeight / 2.0)
				fVarW = fWidth
				fVarH = fHeight
			Case 1 '90°
				fVarX = fX - (fHeight / 2.0)
				fVarY = fY - (fWidth / 2.0)
				fVarW = fHeight
				fVarH = fWidth
			Case 2 '180°
				fVarX = fX - (fWidth / 2.0)
				fVarY = fY - (fHeight / 2.0)
				fVarW = fWidth
				fVarH = fHeight
			Case 3 '270°
				fVarX = fX - (fHeight / 2.0)
				fVarY = fY - (fWidth / 2.0)
				fVarW = fHeight
				fVarH = fWidth
		End Select
		Return True
	End Method
	
	Method Update:Byte()
		Local fXTmp:Float
		Local fYTmp:Float
		Local fWTmp:Float
		Local fHTmp:Float
		Select bAngle
			Case 0 '0°
				fY:- fSpeed
			Case 1 '90°
				fX:+ fSpeed
			Case 2 '180°
				fY:+ fSpeed
			Case 3 '270°
				fX:- fSpeed
		End Select
		GetEdgeCoords(fXTmp, fYTmp, fWTmp, fHTmp)
		SetGadgetShape(gadObject, fXTmp, fYTmp, fWTmp, fHTmp)
		RedrawGadget(gadObject)
		Return True
	End Method
	
	
	Function UpdateAll:Byte()
		Local lgTemp:LivingGadget
		
		For lgTemp = EachIn listAll
			lgTemp.Update()
		Next
		
		Return True
	End Function
End Type

Type LivingButton Extends LivingGadget
	Field bMoveMode		:Byte	'What movement-phase?
	Field fOrigWidth	:Float	'Real Size
	Field fOrigHeight	:Float
	
	Method GetEdgeCoords:Byte(fVarX:Float Var, fVarY:Float Var, fVarW:Float Var, fVarH:Float Var)
		Select bAngle
			Case 0 '0°
				fVarX = fX - (fOrigWidth / 2.0)
				fVarY = fY - (fOrigHeight / 2.0)
				fVarW = fWidth
				fVarH = fHeight
			Case 1 '90°
				fVarX = fX - (fOrigHeight / 2.0)
				fVarY = fY - (fOrigWidth / 2.0)
				fVarW = fHeight
				fVarH = fWidth
			Case 2 '180°
				fVarX = fX - (fOrigWidth / 2.0)
				fVarY = fY - (fOrigHeight / 2.0)
				fVarW = fWidth
				fVarH = fHeight
			Case 3 '270°
				fVarX = fX - (fOrigHeight / 2.0)
				fVarY = fY - (fOrigWidth / 2.0)
				fVarW = fHeight
				fVarH = fWidth
		End Select
		Return True
	End Method
	
	Method Update:Byte()
		Local fXTmp:Float
		Local fYTmp:Float
		Local fWTmp:Float
		Local fHTmp:Float
		Select bAngle
			Case 0 '0°
				If bMoveMode = 0 Then
					fHeight:- fSpeed
					If fHeight &lt;= fOrigHeight / 2.0 Then
						fHeight = fOrigHeight / 2.0
						bMoveMode = 1
					End If
				ElseIf bMoveMode = 1 Then
					fHeight:+ fSpeed
					fY:- fSpeed
					If fHeight &gt;= fOrigHeight Then
						fHeight = fOrigHeight
						bMoveMode = 0
					End If
				End If
			Case 1 '90°
				If bMoveMode = 0 Then
					fHeight:- fSpeed
					fX:+ fSpeed
					If fHeight &lt;= fOrigHeight / 2.0 Then
						fHeight = fOrigHeight / 2.0
						bMoveMode = 1
					End If
				ElseIf bMoveMode = 1 Then
					fHeight:+ fSpeed
					If fHeight &gt;= fOrigHeight Then
						fHeight = fOrigHeight
						bMoveMode = 0
					End If
				End If
			Case 2 '180°
				If bMoveMode = 0 Then
					fHeight:- fSpeed
					fY:+ fSpeed
					If fHeight &lt;= fOrigHeight / 2.0 Then
						fHeight = fOrigHeight / 2.0
						bMoveMode = 1
					End If
				ElseIf bMoveMode = 1 Then
					fHeight:+ fSpeed
					If fHeight &gt;= fOrigHeight Then
						fHeight = fOrigHeight
						bMoveMode = 0
					End If
				End If
			Case 3 '270°
				If bMoveMode = 0 Then
					fHeight:- fSpeed
					If fHeight &lt;= fOrigHeight / 2.0 Then
						fHeight = fOrigHeight / 2.0
						bMoveMode = 1
					End If
				ElseIf bMoveMode = 1 Then
					fHeight:+ fSpeed
					fX:- fSpeed
					If fHeight &gt;= fOrigHeight Then
						fHeight = fOrigHeight
						bMoveMode = 0
					End If
				End If
		End Select
		GetEdgeCoords(fXTmp, fYTmp, fWTmp, fHTmp)
		SetGadgetShape(gadObject, fXTmp, fYTmp, fWTmp, fHTmp)
		RedrawGadget(gadObject)
		
		Return True
	End Method
	
	Function Create:LivingButton(sParName:String, fParX:Float, fParY:Float, fParWidth:Float, fParHeight:Float, gadParParent:TGadget)
		If gadParParent = Null Then Return Null
		Local lbTemp:LivingButton = New LivingButton
		
		lbTemp.sName = sParName
		lbTemp.fX = fParX
		lbTemp.fY = fParY
		lbTemp.fWidth = fParWidth
		lbTemp.fHeight = fParHeight
		lbTemp.fOrigWidth = fParWidth
		lbTemp.fOrigHeight = fParHeight
		lbTemp.gadParent = gadParParent
		lbTemp.gadObject = CreateButton("", lbTemp.fX - (lbTemp.fWidth / 2.0), lbTemp.fY - (lbTemp.fHeight / 2.0), lbTemp.fWidth, lbTemp.fHeight, lbTemp.gadParent)
		
		Return lbTemp
	End Function
End Type
</pre> <br><br></td></tr></table><br>
<a name="755774"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >WendellM</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Heh, that's wonderfully creepy-looking. :) <br><br></td></tr></table><br>
<a name="755891"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jake L.</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice ;) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
