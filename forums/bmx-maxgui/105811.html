<!DOCTYPE html><html lang="en" ><head ><title >Bound Together Forever</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Bound Together Forever</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >Bound Together Forever</a><br><br>
<a name="1295134"></a>

<a name="1295135"></a>

<a name="1295136"></a>

<a name="1295137"></a>

<a name="1295138"></a>

<a name="1295139"></a>

<a name="1295140"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Now that I am starting to work on my Carryall 750k interface, I am noticing apparently you cannot bind either global or local variables to any of the gadgets, I.E.:<pre class=code>g_slide=CreateSlider(h,v,x,y,zvideo,SLIDER_HORIZONTAL|SLIDER_TRACKBAR)</pre>While I can modify this control, I must use this to read its value:<pre class=code>slidepos=slidervalue(g_slide)</pre>Isn't there a way of building a Gadget and hardwiring a variable to it so I don't have to manually read the value of every gadget I need for later ? I.E.:<pre class=code>g_slide=CreateSlider(link(slidepos),h,v,x,y,zvideo,SLIDER_HORIZONTAL_SLIDER_TRACKBAR)</pre>What would be a good example in coding this ? <br><br></td></tr></table><br>
<a name="1295153"></a>

<a name="1295154"></a>

<a name="1295156"></a>

<a name="1295157"></a>

<a name="1295159"></a>

<a name="1295160"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Why would you need to?<br><br>Its like this because the value is kept not by MaxGUI but by the system itself.<br>Windows GUI stuff is a kernel thing, so all state a window can have is kept internally.<br>Meaning you have to call an API function to get any value from it.<br><br>If its speed related it would actually be slower, since that "link" would have to call the relevant API in addition to the abstraction cost itself.<br><br>You could build something like that link yourself though, by using function pointers. But i see no value in doing that.<br>It would just be an abstraction on top of another abstraction ;)<br><br>EDIT: And even then it would still be a function call. Its impossible to make it look like a variable, since that would require language level support for it.<br><br>EDIT2: And if that link was to be totally generic, it would need some way of figuring out which gadget it was operating on. Leading to the very same "API" as the original.<br>Unless of course you created functions at runtime, JIT style... <br><br></td></tr></table><br>
<a name="1295197"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> That's quite a brainweaver there, Grable. Just saying it should be an available option - and was hoping it could be done. :)<br><br>Actually, it can be done w virtual variables. I didn't see that until you you mentioned, "Its impossible to make it look like a variable."<br><br>You do remember my function ?<pre class=code>addmenu "file,n@new state,o@open state,z@close state,-,s@save as ...,q@exit"
addmenu "help,instructions,-,about,author,credits,-,website"</pre>Certainly a lot faster than manually typing out for each of them:<pre class=code>CreateMenu "New State",menuid+n,gadget[menuid],"N",modifier_command</pre> <br><br></td></tr></table><br>
<a name="1295211"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> That is still a function call. What you want is to type "something" and have that something not only act like a function but also return a value.<br>Which is something you can only do IF the language supports it.<br><br>The furthest you could reduce it down to would be<pre class=code>
something()
</pre>But that would require you to hardcode each and every function to directly call the API on a global.<br><br>IE not very generic ;)<br><br>And for just saving you some typing, i really see no value in doing it in the language proper.<br>I would instead build a parser/compiler and interpreter/virtual machine, then i could decide the rules of how everything works myself.<br><br>IE what you seem to want is a scripting language. <br><br></td></tr></table><br>
<a name="1295217"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Writing a Game Making programming language is in my list. But I have many other projects to finish first.<br><br>(1) Carryall 750k Method (this is the one using MaxGUI)<br>(2) Vector Editor &amp; Builder (make your own vector images with arc ability)<br>(3) sample game based on Bill Budge's Bomber game (to demonstrate vectors)<br>(4) Tilemaster 2016 (comprehensive at-the-pixel level editor)<br>(5) Back on course, S3 or - a game programming language. Perhaps both. <br><br></td></tr></table><br>
<a name="1295220"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jsp</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Not sure if I correctly understood what you wanted to do, but couldn't you just set your variable when the event from the slider occured?<br><br>Every time the event pops up the variable is set.<br>You can use even a hook instead of the Wait-Event-Queue.<br><br><pre class=code>
SuperStrict

Import MaxGui.Drivers

Global SliderPos:Int

Local Window1:TGadget = CreateWindow("Window1",404,205,335,211,Null,WINDOW_TITLEBAR|WINDOW_RESIZABLE |WINDOW_STATUS |WINDOW_CLIENTCOORDS )
	Local SL2:TGadget = CreateSlider(38,28,247,40,Window1,SLIDER_HORIZONTAL | SLIDER_TRACKBAR )
		SetSliderRange( SL2,1 ,10 )
		SetSliderValue( SL2,1 )

Repeat
	WaitEvent()
	Select EventID()
		Case EVENT_WINDOWCLOSE
			Select EventSource()
				Case Window1	Window1_WC( Window1 )
			End Select

		Case EVENT_GADGETACTION
			Select EventSource()
				Case SL2	SL2_GA( SL2 , EventData() )
			End Select

	End Select
Forever

Function Window1_WC( Window:TGadget )
	DebugLog "Window Window1 wants to be closed"
	End
End Function

Function SL2_GA( Slider:TGadget , Number:Int )
'Slider Range was initially set to: 1 as Start and: 10 as End Value
	DebugLog "Slider SL2 changed to " + Number
	SliderPos=SliderValue( Slider )
End Function
</pre> <br><br></td></tr></table><br>
<a name="1295280"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think dw817 would like to 'transmit' the value to other gadgets, automatically (sort of).<br><br>Imagine to have a slider: when you move the knob, a label_text is changed accordingly AND a (for example) a ListBox() is moving its content to show a specific 'range' of value (don't know if this is possible!).<br>It's sure you can do this like in the example posted by jsp (just put a SetGadgetText(label,SliderPos) and other in the SL2_GA() function.<br>An 'automatic' solution - like <b>SetGadgetLink(my_slider,this_label)</b> (and you can add more item to the 'queue' to receivers) is 'good to see'<br><br>I've done something similar for one of my GUI (just graphics, in game).<br>I've tried to do something in the past, the idea was to implement in MaxGUI, but then I gave up (no time!), mainly because many gadgets have not 'exposed' their inner value (ie: progressBar position, you need to find where is stored the current value, same for ListBox() etc)<br><br>This was my experiment<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

'BlitzMax program : 29 Dec 2013 19:19:17
'Template         : template_maxgui.bmx
'				Gadget LINK
Rem
				link together 2 (or more) gadgets
				when act on ONE gadget, the effect are on the other(s)
End Rem


SuperStrict
Import maxgui.drivers


Global window:tgadget=		CreateWindow("Main",100,100,300,300,,WINDOW_TITLEBAR|WINDOW_CENTER)

Global lbl_result:tgadget=	CreateLabel("",10,10,80,20,window,LABEL_FRAME|LABEL_CENTER)
Global lbl_2:tgadget=		CreateLabel("",100,10,80,20,window,LABEL_SUNKENFRAME|LABEL_CENTER)
Global lbl_3:tgadget=		CreateLabel("",120,100,40,20,window,LABEL_SUNKENFRAME|LABEL_CENTER)

Global slider:tgadget=		CreateSlider(10,40,100,20,window,SLIDER_HORIZONTAL)
Global bar:tgadget=			CreateProgBar(10,100,100,10,window)
Global bar2:tgadget=		CreateProgBar(10,112,100,10,window)
Global lb:tgadget=			CreateComboBox(120,150,100,100,window)

For Local s:Int=0 Until 50
	AddGadgetItem lb,"Item "+s
Next

SetSliderRange slider,0,100
SetGadgetColor lbl_2,200,200,200
SetGadgetColor lbl_result,200,100,100
CreateLink(slider,lbl_result)
AddGadgetLink(slider,lbl_2)
AddGadgetLink(slider,bar2)
RemoveGadgetLink(slider,lbl_2)
'ClearGadgetLink(slider)

CreateLink(bar,lbl_3)
CreateLink(lb,lbl_2)
'CreateLink(bar,slider)

Local tmr:Ttimer=CreateTimer(20)
Local n:Int
Repeat
	WaitEvent()
	
	Select EventID()
		Case EVENT_APPTERMINATE, EVENT_WINDOWCLOSE
			End
		Case EVENT_TIMERTICK
			UpdateProgBar bar,Float(n)/100
			n=(n+1) Mod 100
			Tgadgetlink.Check(bar)
		
		Case EVENT_GADGETACTION
			Select EventSource()
				Case slider
					Tgadgetlink.Check(slider)
				Case lb
					tgadgetlink.Check(lb)
					
			End Select
	End Select
Forever

Function AddGadgetLink:Int(s:tgadget,d:tgadget)
	tgadgetlink.Target(s,d,0)
End Function
Function RemoveGadgetLink:Int(s:Tgadget,d:Tgadget)
	tgadgetlink.Target(s,d,1)
End Function

Function CreateLink:Int(source:Tgadget,dest:Tgadget)
		Local ln:TgadgetLink=Tgadgetlink.Add(source)
		ln.AddTarget(dest)
End Function

Function ClearGadgetLink:Int(source:Tgadget)
	TgadgetLink.Clear(source)
End Function

Type Tgadgetlink

	Global map_link:Tmap
	
	Field source:Tgadget
	Field list_target:TList	'a list of possible target
	
	Method AddTarget:Int(_dest:Tgadget)
		list_target.addlast _dest
	End Method
	
	Method ClearLinks:Int()
		ClearList list_target
	End Method
	
	Method Destroy:Int()
		ClearList list_target
		ClearMap map_link
		source=Null
		list_target=Null
		
	End Method
	
	Method RemoveTarget:Int(_dest:Tgadget)
		ListRemove list_target,_dest
	End Method
	
	Method UpdateLink:Int()
		If list_target=Null Return 0
		If list_target.count()=0 Return 0
		
		Local gc:Int,gv:String
		
		gc=GadgetClass(source)
		
		Select gc
			Case GADGET_SLIDER
				gv=SliderValue(source)
			Case GADGET_PROGBAR,GADGET_COMBOBOX
				'gv=Int(source.value)
		End Select
		
		
		For Local g:Tgadget=EachIn list_target
			If g
			Select GadgetClass(g)	
				Case GADGET_LABEL,GADGET_BUTTON,GADGET_TEXTFIELD 
					SetGadgetText g,gv
				Case GADGET_PROGBAR
					UpdateProgBar g,Float(gv)/100
				Case GADGET_SLIDER
					SetSliderValue g,Int(gv)
			End Select
			
				'recursion 
				
				Check(g)
			
			End If
		Next
		
	End Method
	
	Function Clear:Int(s:tgadget=Null)
		If s=Null Return 0
		
		Local l:Tgadgetlink
		L=Tgadgetlink(MapValueForKey(map_link,s))
		If L
			L.ClearLinks()
			L.Destroy()
			L=Null
		Else
			DebugLog "ERROR: no source-link found!"
		End If	

	End Function
	
	Function Check:Int(s:tgadget=Null)
		If s=Null Return 0

		Local l:Tgadgetlink
		L=Tgadgetlink(MapValueForKey(map_link,s))
		If L
			L.UpdateLink()
		End If

	End Function
	
	Function Target:Int(s:Tgadget,d:Tgadget,tipo:Int=0)
		Local l:TgadgetLink
			l=Tgadgetlink(MapValueForKey(map_link,s))
		If l
			Select tipo
				Case 0	l.AddTarget(d)'add
				Case 1	l.RemoveTarget(d)'remove
			End Select
		Else
			DebugLog "Error: Source link not found"
		End If
	End Function

		
	Function Add:Tgadgetlink(_s:tgadget)
		Local g:Tgadgetlink=New Tgadgetlink
		g.source=_s
		g.list_target=New TList
		
		If map_link=Null map_link=CreateMap()
		
		MapInsert map_link,_s,g	'add to a LINK map the 'source' gadget
		Return g
	End Function

End Type

</textarea> <br><br></td></tr></table><br>
<a name="1295583"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Degac. I tried your program.  I'm not really understanding the source-code, but you are correct, I want to have a value I can read or set. Just by changing a variable it immediately appears as an effect in the gadgets - or I can read the variable solely to retrieve the status and position of the gadget.<br><br>I have also already written code to this effect and am continuing on.<br><br>Thanks for your submit. <br><br></td></tr></table><br>
<a name="1295586"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> It looks to be an Event Multiplexer of sorts. Getting a single event from a Source gadget and "sending" it to other gadgets by updating them with the state from the event.<br><br>Not exactly what your looking for if my understanding of your post is correct, but interesting nonetheless. <br><br></td></tr></table><br>
<a name="1295590"></a>

<a name="1295591"></a>

<a name="1295592"></a>

<a name="1295593"></a>

<a name="1295594"></a>

<a name="1295595"></a>

<a name="1295596"></a>

<a name="1295597"></a>

<a name="1295598"></a>

<a name="1295600"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Building on the concepts of degacs post, heres something using variables as intermediaries.<br>Its not automatic though. One has to register and link gadgets with variables explicitly, and periodically call a Sync() method to update gadgets-&gt;variables or variables-&gt;gadgets depending on the current event.<br>But i think its pretty close to your original requirement ;)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

SuperStrict

Import MaxGUI.Drivers


Global window:TGadget = CreateWindow( "GadgetLinker", 0,0,640,480, Null, WINDOW_TITLEBAR | WINDOW_CLIENTCOORDS | WINDOW_CENTER)
Global label:TGadget = CreateLabel( "Test Gadgets", 8,8, 640-16,25, window, LABEL_CENTER)
Global textfield:TGadget = CreateTextField( 8,8+25+8, 640-16,25, window)
Global slider:TGadget = CreateSlider( 8,8+25+8+25+8, 640-16,25, window, SLIDER_HORIZONTAL | SLIDER_TRACKBAR)
Global progbar:TGadget = CreateProgBar( 8,8+25+8+25+8+25+8, 640-16,25, window)
slider.SetRange(10,100)

Global help:TGadget = CreateLabel( "Input some data here and click the buttons below", 8,8+25+8+25+8+25+8+75, 640-16,25, window, LABEL_CENTER)
Global value:TGadget = CreateTextField( 8,8+25+8+25+8+25+8+100, 640-16,25, window)
Local w:Int = (640-(8*5)) / 3
Global setint:TGadget = CreateButton( "Update Int", 8,8+25+8+25+8+25+8+100+25+8, w,25, window)
Global setfloat:TGadget = CreateButton( "Update Float", 8+w+8,8+25+8+25+8+25+8+100+25+8, w,25, window)
Global setstring:TGadget = CreateButton( "Update String", 8+w+8+w+8,8+25+8+25+8+25+8+100+25+8, w,25, window)
Global labelint:TGadget = CreateLabel( "0 - 100 == 0", 8,8+25+8+25+8+25+8+100+25+8+25+8, w,25, window, LABEL_CENTER)
Global labelfloat:TGadget = CreateLabel( "0.0 - 1.0 == 0.0", 8+w+8,8+25+8+25+8+25+8+100+25+8+25+8, w,25, window, LABEL_CENTER)
Global labelstring:TGadget = CreateLabel( "anything == ~q~q", 8+w+8+w+8,8+25+8+25+8+25+8+100+25+8+25+8, w,25, window, LABEL_CENTER)


Global linker:TGadgetLinker = New TGadgetLinker

Global thevaluei:Int = 50
Global thevaluef:Float = 0.5
Global thevalues:String = "some value"

linker.RegisterInt( slider, thevaluei)
linker.RegisterFloat( progbar, thevaluef)
linker.RegisterString( textfield, thevalues)

Repeat
	WaitEvent()
	Select EventID()
		Case EVENT_GADGETACTION
			' update value in variables
			Select EventSource()
				Case setint			thevaluei = value.GetText().ToInt()
				Case setfloat		thevaluef = value.GetText().ToFloat()
				Case setstring		thevalues = value.GetText()
			EndSelect			

		Case EVENT_WINDOWCLOSE
			Exit
	EndSelect
	
	' sync the gadgets
	linker.Sync(CurrentEvent)
	
	' show current value in variables
	labelint.SetText "0 - 100 == " + thevaluei
	labelfloat.SetText "0.0 - 1.0 == " + thevaluef
	labelstring.SetText "anything == ~q" + thevalues + "~q"
Forever
End


Function GetProgbarValue:Float( gadget:TGadget)
?Win32
	' since progbars have no GetValue() we do it manually
	Local value:Int
	SendMessageW gadget.Query(QUERY_HWND),PBM_GETPOS,value*100, Int Varptr value
	Return Float(value) / 100.0
?
EndFunction



Const LINK_INT:Int = 0
Const LINK_FLOAT:Int = 1
Const LINK_STRING:Int = 2

Const LINK_FLAG_TOGADGET:Int = 1 Shl 0
Const LINK_FLAG_TOVARIABLE:Int = 1 Shl 1
Const LINK_FLAG_TWOWAY:Int = LINK_FLAG_TOGADGET | LINK_FLAG_TOVARIABLE

Type TGadgetLink
	Field Tag:Int
	Field Source:TGadget
	Field Variable:Byte Ptr
	Field Cache:Int
	Field SCache:String
	Field Flags:Int = LINK_FLAG_TWOWAY
	Field Link:TLink

	Method GetInt:Int()
		Select Tag
			Case LINK_INT		Return Int Ptr(Variable)[0]
			Case LINK_FLOAT	Return Float Ptr(Variable)[0]
			Case LINK_STRING Return ReadStringVariable(Variable).ToInt()
		EndSelect
	EndMethod
	
	Method GetFloat:Float()
		Select Tag
			Case LINK_INT		Return Int Ptr(Variable)[0]
			Case LINK_FLOAT	Return Float Ptr(Variable)[0]
			Case LINK_STRING	Return ReadStringVariable(Variable).ToFloat()
		EndSelect
	EndMethod
	
	Method GetString:String()
		Select Tag
			Case LINK_INT		Return Int Ptr(Variable)[0]
			Case LINK_FLOAT	Return Float Ptr(Variable)[0]
			Case LINK_STRING Return ReadStringVariable(Variable)
		EndSelect
	EndMethod
	
	Method SetInt( value:Int)
		Select Tag
			Case LINK_INT		Int Ptr(Variable)[0] = value
			Case LINK_FLOAT	Float Ptr(Variable)[0] = value
			Case LINK_STRING WriteStringVariable( Variable, value)
		EndSelect
	EndMethod
	
	Method SetFloat( value:Float)
		Select Tag
			Case LINK_INT		Int Ptr(Variable)[0] = value
			Case LINK_FLOAT	Float Ptr(Variable)[0] = value
			Case LINK_STRING WriteStringVariable( Variable, value)
		EndSelect
	EndMethod
	
	Method SetString( value:String)
		Select Tag
			Case LINK_INT		Int Ptr(Variable)[0] = value.ToInt()
			Case LINK_FLOAT	Float Ptr(Variable)[0] = value.ToFloat()
			Case LINK_STRING WriteStringVariable( Variable, value)
		EndSelect
	EndMethod

	Method UpdateGadget()
		If Not(Flags &amp; LINK_FLAG_TOGADGET) Then Return
		
		Local isdirty:Int
		If Tag = LINK_STRING Then
			Local currstr:String = ReadStringVariable(Variable)
			If SCache &lt;&gt; currstr Then
				SCache = currstr
				isdirty = True
			EndIf
		Else		
			Local currval:Int = Int Ptr(Variable)[0]
			If Cache &lt;&gt; currval Then
				Cache = currval
				isdirty = True
			EndIf
		EndIf
		
		If isdirty Then
			Select Source.Class()
				Case GADGET_SLIDER
					Source.SetProp( GetInt())
					
				Case GADGET_PROGBAR
					Source.SetValue( GetFloat())
					
				Case GADGET_WINDOW, GADGET_BUTTON, GADGET_TEXTFIELD, GADGET_LABEL, GADGET_WINDOW
					Source.SetText( GetString())
			EndSelect
		EndIf
	EndMethod
	
	Method UpdateVariable()
		If Not(Flags &amp; LINK_FLAG_TOVARIABLE) Then Return
		
		Select Source.Class()
			Case GADGET_SLIDER
				SetInt( Source.GetProp())
				
			Case GADGET_PROGBAR
				SetFloat( GetProgbarValue(progbar))
				
			Case GADGET_WINDOW, GADGET_BUTTON, GADGET_TEXTFIELD, GADGET_LABEL, GADGET_WINDOW	
				SetString( Source.GetText())
		EndSelect
	EndMethod

	Method Sync( event:TEvent = Null)
		If Not event Then
			UpdateGadget()
		Else
			If event.ID = EVENT_GADGETACTION And Source = event.Source Then
				UpdateVariable()
			Else
				UpdateGadget()
			EndIf
		EndIf
	EndMethod

	Method Remove()
		Link.Remove()
	EndMethod
	
	Function ReadStringVariable:String( variable:Byte Ptr)
		' a little trickery to get string from pointer
		Function get:String( variable:String Var)
			Return variable
		EndFunction
		Local get_str:String( variable:Byte Ptr) = Byte Ptr get
		Return get_str(variable)
	EndFunction
	
	Function WriteStringVariable( variable:Byte Ptr, value:String)
		' a little trickery to set string to pointer
		Function set:String( variable:String Var, value:String)
			variable = value
		EndFunction
		Local set_str:String( variable:Byte Ptr, value:String) = Byte Ptr set
		set_str( variable, value)
	EndFunction	
EndType

Type TGadgetLinker
	Field Links:TList = New TList
	
	Method RegisterVariable:TGadgetLink( source:TGadget, tag:Int, variable:Byte Ptr, flags:Int = LINK_FLAG_TWOWAY)
		Local link:TGadgetLink = New TGadgetLink
		link.Tag = tag
		link.Source = source
		link.Variable = variable
		link.Flags = flags
		link.Link = Links.AddLast(link)
		Return link
	EndMethod
	
	Method RemoveVariable( source:TGadget, variable:Byte Ptr)
		Local n:TLink = Links.FirstLink()
		While n
			Local link:TGadgetLink = TGadgetLink( n.Value())
			If link.Source = source And link.Variable = variable Then
				n.Remove()
				Exit
			EndIf
			n = n.NextLink()
		WEnd
	EndMethod
	
	Method RegisterInt:TGadgetLink( source:TGadget, variable:Int Var, flags:Int = LINK_FLAG_TWOWAY)
		Return RegisterVariable( source, LINK_INT, Varptr variable, flags)
	EndMethod
	
	Method RegisterFloat:TGadgetLink( source:TGadget, variable:Float Var, flags:Int = LINK_FLAG_TWOWAY)
		Return RegisterVariable( source, LINK_FLOAT, Varptr variable, flags)
	EndMethod

	Method RegisterString:TGadgetLink( source:TGadget, variable:String Var, flags:Int = LINK_FLAG_TWOWAY)
		Return RegisterVariable( source, LINK_STRING, GetStringVariablePointer(variable), flags)
	EndMethod

	Method RemoveInt( source:TGadget, variable:Int Var)
		RemoveVariable( source, Varptr variable)
	EndMethod
	
	Method RemoveFloat( source:TGadget, variable:Float Var)
		RemoveVariable( source, Varptr variable)
	EndMethod
	
	Method RemoveString( source:TGadget, variable:String Var)
		RemoveVariable( source, GetStringVariablePointer(variable))
	EndMethod
	
	Method Sync( event:TEvent = Null)
		For Local link:TGadgetLink = EachIn Links
			link.Sync(event)
		Next
	EndMethod
	

	Function GetStringVariablePointer:Byte Ptr( variable:String Var)	
		' a little trickery to get pointer of string variable
		Function get:Byte Ptr( variable:Byte Ptr)
			Return variable
		EndFunction		
		Local get_str_ptr:Byte Ptr( variable:String Var) = Byte Ptr get
		Return get_str_ptr(variable)
	EndFunction	
EndType

</textarea><br><br>EDIT: Fixed some bugs and added some more abstractions. <br><br></td></tr></table><br>
<a name="1295632"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Grable<br><br>Wow... just read your code... vodoo magic in some places :D (pointer &amp; co)<br>Finding the progressbar value seems so easy now! :)<br>Thanks a lot, something interesting to explore! <br><br></td></tr></table><br>
<a name="1295693"></a>

<a name="1295694"></a>

<a name="1295696"></a>

<a name="1295697"></a>

<a name="1295698"></a>

<a name="1295710"></a>

<a name="1295712"></a>

<a name="1295713"></a>

<a name="1295714"></a>

<a name="1295715"></a>

<a name="1295717"></a>

<a name="1295938"></a>

<a name="1295939"></a>

<a name="1295941"></a>

<a name="1295942"></a>

<a name="1295943"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> An abstraction that helped me a lot when working on a personal software was "properties", which would be something like a "smart variable" or a class that wraps a variable.<br>You can have Bool properties, Int properties, String properties etc. These properties belong to the document that the user is editing, like a "scene" for example.<br>The GUI of your application serves two purposes: to represent the state of properties visually to the user and to allow the user to manipulate these properties.<br>In the case of an integer value, a slider type of gadget can be used for that.<br>In the case of a boolean value, a check box is used for that. When the user toggles the check box, internally that boolean property is being modified.<br><br>When a property changes, you may want to run some specific code to do something.<br>You subscribe an object and a function from that object to the property. If I'm not mistaken this association takes the form of a "delegate", an object that can invoke that function.<br>When the property changes, it invokes each delegate that was assigned to it (with each delegate representing functions that want to be run when the property changes), so the functions 'run' and do what they're meant to.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

Import maxgui.drivers

Type Delegate

	Function Create:Delegate( target:Object, fn( obj:Object ) )
	
		Local d:Delegate = New Delegate
		d.target = target
		d.fn = fn
		
		Return d
		
	End Function
	
	
	Method invoke()
	
		fn( target ) 'Invoke the object function, submitting the object itself as parameter.
	
	End Method
	
	
	Field target:Object
	Field fn( obj:Object )

End Type


Type BaseProperty

	Method New()
		
		subscribers = New TList
		
	End Method


	Method subscribe( target:Object, fn( obj:Object ) )
		
		subscribers.AddLast( Delegate.Create( target, fn ) )
		
	End Method
	
	
	'PRIVATE.
	
	Method _notify()
	
		For Local d:Delegate = EachIn subscribers
			d.invoke()
		Next
	
	End Method


	Field subscribers:TList
	Field value:Int

End Type


'Integer specialisation of 'BaseProperty'.

Type IntProperty Extends BaseProperty

	Method setValue( newValue:Int )
	
		If value &lt;&gt; newValue Then
			value = newValue 'There was a change to the value of this property.
			_notify() 'Notify subscribers.
		EndIf
	
	End Method
	
	
	Method getValue:Int()
	
		Return value
	
	End Method
	
End Type


Type PropertyGadget

	Method handleEvent( event:TEvent ) Abstract

End Type


Type MyIntLabel Extends PropertyGadget
	
	Function Create:MyIntLabel( property:IntProperty, x:Int, y:Int, w:Int, h:Int, group:TGadget=Null, style:Int=LABEL_LEFT )
	
		Local label:MyIntLabel = New MyIntLabel
		
		label.baseLabel = CreateLabel( "", x, y, w, h, group, style )
		SetGadgetExtra( label.baseLabel, label ) 'Used in the main loop, when processing events.
		
		label.property = property
		
		'Subscribe this MyIntLabel object to changes to the Integer property.
		
		property.subscribe( label, onPropertyChange )
				
		Return label
			
	End Function
	
	
	Method handleEvent( event:TEvent )
	
		'Do nothing.

	End Method
	
	
	Function onPropertyChange( obj:Object )
	
		Local label:MyIntLabel = MyIntLabel( obj )
	
		'The property changed. What should the label do?
		'---&gt; Change its text to represent the value in the property.
		
		SetGadgetText( label.baseLabel, label.property.getValue() )
		
	End Function

	
	Field baseLabel:TGadget
	Field property:IntProperty

End Type


Type MyIntSlider Extends PropertyGadget
	
	Function Create:MyIntSlider( property:IntProperty, x:Int, y:Int, w:Int, h:Int, group:TGadget=Null, style:Int=0 )
	
		Local slider:MyIntSlider = New MyIntSlider
		
		slider.baseSlider = CreateSlider( x, y, w, h, group, style )
		SetGadgetExtra( slider.baseSlider, slider )
		
		slider.property = property
				
		Return slider
	
	End Function


	Method handleEvent( event:TEvent )
	
		If event.id = EVENT_GADGETACTION

			'Change the property value.
			
			property.setValue( event.data )
			
		EndIf
	
	End Method
	
	
	Field baseSlider:TGadget
	Field property:IntProperty

End Type


Local window:TGadget = CreateWindow( "Property Example", 0, 0, 400, 300, Null, WINDOW_TITLEBAR | WINDOW_RESIZABLE | WINDOW_CENTER )

'The properties represent values useful to your program and the document it's operating on.

Local amountOfBirds:IntProperty = New IntProperty

'Create gadgets that are SPECIALISED in manipulating a certain type of property.
'You could also have a 'MyDoubleSlider', 'MyByteSlider' for example, as gadgets
'that manipulate other types of properties.

Local slider:MyIntSlider = MyIntSlider.Create( amountOfBirds, 20, 20, 200, 20, window, SLIDER_HORIZONTAL )
SetSliderRange( slider.baseSlider, 1, 100 )

Local label:MyIntLabel = MyIntLabel.Create( amountOfBirds, 230, 20, 50, 32, window )


Repeat
	WaitEvent()
	
	Select EventID()
		Case EVENT_GADGETACTION
			Local gadget:TGadget = TGadget( EventSource() )
			If gadget And GadgetExtra( gadget ) Then PropertyGadget( GadgetExtra( gadget ) ).handleEvent( CurrentEvent )
				
		Case EVENT_WINDOWCLOSE, EVENT_APPTERMINATE
			End
	End Select
Forever</textarea><br>EDIT: Added a 'BaseProperty' type with essential functionality that all properties should have (the subscriber list and notification), and all properties 'extend' this base property type.<br>There's plenty that can be improved in that code since it's just an example, it's not a final product.<br><br>The benefit of this abstraction is that it's got your back when your program becomes complex. You can assign the same property to several gadgets, like for example, having different colour pickers that all change the same R, G and B byte-value properties.<br>BlitzMax doesn't have many language features to help with this, we need to use static functions. You can do more elegant (and efficient) solutions with C++ by using templates (i.e <a href="http://www.codeproject.com/Articles/11015/The-Impossibly-Fast-C-Delegates" target="_blank">like this</a>).<br><br>Further reading:<br>- <a href="https://en.wikipedia.org/wiki/Model%E2%80%93view%E2%80%93controller" target="_blank">https://en.wikipedia.org/wiki/Model–view–controller</a> (and its variations)<br>- <a href="https://en.wikipedia.org/wiki/Delegate_(CLI)" target="_blank">https://en.wikipedia.org/wiki/Delegate_(CLI)</a> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
