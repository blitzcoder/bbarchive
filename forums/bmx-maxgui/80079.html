<!DOCTYPE html><html lang="en" ><head ><title >CreateHyperLink suggested changes</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >CreateHyperLink suggested changes</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >CreateHyperLink suggested changes</a><br><br>
<a name="899731"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I just noticed this aspect of CreateHyperLink (taken from the help page...)<br><div class="quote"> <br>The optional customtext$ parameter allows you to set friendly text that is displayed instead of the URL in the label. If this is set then the label's tooltip is automatically generated with the URL the link refers to. Although this masking text can be changed at any time by calling SetGadgetText, <b>the url$ that the hyperlink gadget opens cannot be altered once the gadget has been created</b>.<br> <br></div><br><br>As I don't like the last sentence (and I want to be free to change the URL without destroying and recreate a new gadget) I suggest this little change to the CreateHyperLink() code<br><pre class=code>
	Method SetText(_url$) 
		url = _url
		Super.settext(_url) 
	End Method

	Method SetToolTip(_url$) 
		proxy.settooltip(_url) 
	End Method
</pre><br><br>This will allow to change the URL via a SetGadgetText command. The tooltip (if any) must be defined with SetGadgetToolTip()<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Import MaxGUI.Drivers
Import MaxGUI.ProxyGadgets

AppTitle = "Hyperlink Test Window"

Global wndMain:TGadget = CreateWindow( AppTitle, 100, 100, 300, 59, Null, WINDOW_TITLEBAR|WINDOW_CLIENTCOORDS|WINDOW_STATUS )
	
	'Standard Hyperlink Gadget
	Global hypLeft:TGadget = CreateHyperlink( "http://www.blitzbasic.com/", 2, 2, ClientWidth(wndMain)-4, 15, wndMain, LABEL_LEFT )
	
	'Center Aligned Hyperlink Gadget with alternate text
	Global hypCenter:TGadget = CreateHyperlink( "http://www.blitzbasic.com/", 2, 21, ClientWidth(wndMain)-4, 17, wndMain, LABEL_CENTER|LABEL_FRAME, "Alternate Text" )
	
	'Right Aligned Sunken Hyperlink Gadget with custom rollover colors set
	Global hypRight:TGadget = CreateHyperlink( "http://www.blitzbasic.com/", 2, 42, ClientWidth(wndMain)-4, 15, wndMain, LABEL_RIGHT, "Custom Rollover Colors" )
		SetGadgetTextColor(hypRight,128,128,128)	'Set normal text color to grey.
		SetGadgetColor(hypRight,255,128,0)			'Set rollover color to orange.

	SetGadgetText hypCenter , "http://www.graphio.net" '&lt;---- change the URL (keep the same initial tooltip)
	'SetGadgetToolTip hypCenter,"my site" ' &lt;---- change the tooltip

Repeat
	
	WaitEvent()
	
	SetStatusText wndMain, CurrentEvent.ToString()
	
	Select EventID()
		Case EVENT_WINDOWCLOSE, EVENT_APPTERMINATE;End
	EndSelect
	
Forever

</textarea><br><br>I think in this way the HyperLink gadget is more usable<br><br>ps: wow! I just noticed there is a new <b>CreateSplitter</b> gadget! <br><br></td></tr></table><br>
<a name="899733"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I just noticed that my suggested changes have a negative impact as you can not more use the 'alternate text' (if any).<br>So there is a complete rewrite of HyperLink.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Import MaxGUI.MaxGUI

Rem
bbdoc: Creates a basic hyperlink gadget that opens the specified @url$ in the default browser when clicked.
about: The underlying gadget is a label, and so the @style parameter can take all the #CreateLabel flags apart from LABEL_SEPARATOR.

Both the default and roll-over text color can be set using #SetGadgetTextColor and #SetGadgetColor respectively.

The optional @customtext$ parameter allows you to set friendly text that is displayed instead of the URL in the label. If this is set
then the label's tooltip is automatically generated with the URL the link refers to. Although this masking text can be changed at any
time by calling #SetGadgetText, the @url$ that the hyperlink gadget opens cannot be altered once the gadget has been created.
End Rem
Function CreateHyperlink:TGadget( url$,x,y,w,h,group:TGadget,style=0,customtext$ = "" )
	
	Return New THyperlinkGadget.Create(url,x,y,w,h,group,style,customtext)
	
EndFunction

Type THyperlinkGadget Extends TProxyGadget
	
	Global lstHyperlinkGadgets:TList
	
	Global fntDefault:TGuiFont

	Field tmpLabel:tgadget ' --- now we keep the TmpLabel to further changes
	Field hyperlinkstyle%, url$
	Field colors[][] = [[0,0,255],[255,0,0]]
	
	Field lastclick[] = [-1, -1]
	
	Method New()
		If Not lstHyperlinkGadgets Then Initialize()
	EndMethod
	
	Method Create:THyperlinkGadget(pUrl$,x,y,w,h,group:TGadget,style,customtext$)
		
		If Not customtext Then customtext = pUrl$
		If (style&amp;LABEL_SEPARATOR) = LABEL_SEPARATOR Then style:&amp;~LABEL_SEPARATOR
		
		tmpLabel:TGadget = CreateLabel( customtext, x, y, w, h, group, style&amp;31 )
		If Not tmpLabel Then Return Null Else SetGadgetSensitivity(tmpLabel, SENSITIZE_MOUSE)
		
		SetGadgetFont(tmpLabel,fntDefault)		'Let's underline our hyperlink
		
		If customtext &lt;&gt; pUrl Then SetGadgetToolTip( tmpLabel, pUrl )
		
		SetProxy( tmpLabel ) 
		Super.SetTextColor(colors[0][0], colors[0][1], colors[0][2])
		
		hyperlinkstyle = style
		url = pUrl
		lstHyperlinkGadgets.AddLast Self
		
		Return Self
		
	EndMethod
	
	Method EventHook:TEvent( pEvent:TEvent )
		
		Select pEvent.id
		
			Case EVENT_MOUSEENTER;Super.SetTextColor(colors[1][0], colors[1][1], colors[1][2]);SetPointer(POINTER_HAND)
			Case EVENT_MOUSELEAVE;Super.SetTextColor(colors[0][0], colors[0][1], colors[0][2]);SetPointer(POINTER_DEFAULT)
			Case EVENT_MOUSEDOWN;If lastclick[0] &lt;&gt; pEvent.x Or lastclick[1] &lt;&gt; pEvent.y Then lastclick = [pEvent.x,pEvent.y];OpenURL(url)
		
		EndSelect
		
		Return Null
	
	EndMethod
	
	Method SetText(_url$) 
		url = _url ' --- we change ONLY the url (not the text)
	End Method
	
	Method SetToolTip(_url$) 
		tmpLabel.settooltip(_url) '--- we refer to the label
	End Method

	Method SetColor(r,g,b)
		colors[1][0] = r;colors[1][1] = g;colors[1][2] = b
	EndMethod
	
	Method SetTextColor(r,g,b)
		colors[0][0] = r;colors[0][1] = g;colors[0][2] = b
		Super.SetTextColor(colors[0][0], colors[0][1], colors[0][2])
	EndMethod
	
	Method Free()
		lstHyperlinkGadgets.Remove(Self)
		Super.Free()
	EndMethod
	
	Function Initialize()
		lstHyperlinkGadgets = New TList
		AddHook EmitEventHook, eventHandler, Null, 1
		fntDefault = LookupGuiFont( GUIFONT_SYSTEM, 0, FONT_UNDERLINE )
	EndFunction
	
	Function eventHandler:Object( pID%, pData:Object, pContext:Object )
		Local pEvent:TEvent = TEvent(pData)
		
		If pEvent Then
			For Local tmpHyperlinkGadget:THyperlinkGadget = EachIn lstHyperlinkGadgets
				If tmpHyperlinkGadget.proxy = pEvent.source Then Return tmpHyperlinkGadget.EventHook( pEvent )
			Next
		EndIf
		
		Return pData
	EndFunction
	
EndType
</textarea><br><br>With <b>SetGadgetText()</b> it is possibile to change <u>ONLY the URL</u>, the AlternateText still remain. (Unless we use SetGadgetToolTip to change both ToolTip and alternate_text...)<br><br>edit:<br><br>I'm playing with SetGadgetExtra(gadget,object). At the moment I can change everything (URL, tooltip, label) with standard MaxGUI commands.<br><br>edit2:<br><br>A third - not conventional &amp; strange - method I tested<br><br><pre class=code>
	Method SetText(_url$) 
		
		If _URL.find("||")&lt;0
	             	url = _url
		        If String(Self.extra)&lt;&gt;"" Super.settext(String(Self.extra) ) 
		Else
		        Local turl$[2]
			turl= _url.split("||") 
			If turl[1] &lt;&gt; "" Super.settext turl[1]
			url=turl[0]
	        End If
	
	End Method
</pre><br><br><pre class=code>
SetGadgetText hypCenter,"www.graphio.net||To my site"
</pre><br>If there is not "||" SetGadgetText changes the only the URL, else the string is splitted in two parts: the URL and the new Label/Text. <br><br></td></tr></table><br>
<a name="899887"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> I just noticed that my suggested changes have a negative impact as you can not more use the 'alternate text' (if any). <br></div><br>Exactly the reason why I implemented that behaviour.<br><br>I like the <b>SetGadgetExtra()</b> idea though... I'll take a look at some point, but I think I might keep the current behaviour 'as is' (with <b>SetGadgetText()</b> affecting the displayed text), and use <b>SetGadgetExtra()</b> to set the URL.<br><br>Sounds fair enough? <br><br></td></tr></table><br>
<a name="899948"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Perfect.<br>What is important it's a way to allow the user to change the URL. <br><br></td></tr></table><br>
<a name="902803"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've just committed a new version (rev. 84).<br><br>You should now be able to set the URL using <b>SetGadgetExtra()</b> and retrieve it using <b>String( GadgetExtra() )</b>.<br><br>Let me know if this is OK... <br><br></td></tr></table><br>
<a name="902828"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Great. It works perfectly.<br>Thanks.<br><br><u>ps: semi-related problem</u><br><br>I've tested on MacOS X (PPC - 10.3.9) the old and the new version of CreateHyperLink() gadget, and I get the same problem: OpenURL doesn't open the brower (the default one Firefox, neither Safari). If FF is already opened clicking on a HyperLink gadget works as expected.<br><br>I just checked with a simple program<br><pre class=code>OpenURL("http://www.google.com")</pre><br>but neither this works.<br>So the problem is not in MaxGUI, but resides in the OpenURL command. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
