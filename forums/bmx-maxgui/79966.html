<!DOCTYPE html><html lang="en" ><head ><title >New Dialog!</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >New Dialog!</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >New Dialog!</a><br><br>
<a name="898377"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ked</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have created a username and password dialog for MaxGUI. Let me know what you think. I am currently working on more. (Let me know of any you would like. I'd be happy to put something together!)<br><br>Seb/Mark Tiffany: Could this be a possible addition to the ProxyGadgets module?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict
Import maxgui.maxgui
'Import maxgui.win32maxguiex

'CREATED BY KEDRIC SIDDONS'
'COPYRIGHT 2008 KEDRIC SIDDONS'

Type TUserPassDialog
	Global _window:TGadget
	Global _usernamelab:TGadget
	Global _passwordlab:TGadget
	Global _usernamefield:TGadget
	Global _passwordfield:TGadget
	Global _okbutton:TGadget
	
	Global _username:String
	Global _password:String
	
	Function Create:String[](parent:TGadget,username:String="",password:String="")
		If parent=Null
			RuntimeError "TUserPassDialog: Need parent gadget!"
			End
		EndIf
		If GadgetClass(parent)&lt;&gt;GADGET_WINDOW
			RuntimeError "TUserPassDialog: Parent gadget must be a window!"
			End
		EndIf
		
		DisableGadget parent
		_window=CreateWindow(AppTitle,0,0,300,120,parent,WINDOW_TITLEBAR|WINDOW_CLIENTCOORDS|WINDOW_CENTER)
		_usernamelab=CreateLabel("Username:",10,14,80,20,_window)
		_usernamefield=CreateTextField(90,10,ClientWidth(_window)-(90+10),20,_window)
		_passwordlab=CreateLabel("Password:",10,44,80,20,_window)
		_passwordfield=CreateTextField(90,40,ClientWidth(_window)-(90+10),20,_window,TEXTFIELD_PASSWORD)
		_okbutton=CreateButton("Submit",ClientWidth(_window)-(70+10),ClientHeight(_window)-(24+10),70,24,_window)
		
		ActivateGadget _usernamefield
		
		SetGadgetText _usernamefield,username
		SetGadgetText _passwordfield,password
		
		Repeat
			WaitEvent()
			Select EventID()
				Case EVENT_WINDOWCLOSE
					Select EventSource()
						Case _window
							EnableGadget parent
							FreeGadget _window
							Return ["",""]
							Exit
					EndSelect
				
				Case EVENT_GADGETACTION
					Select EventSource()
						Case _okbutton
							_username=TextFieldText(_usernamefield)
							_password=TextFieldText(_passwordfield)
							
							If Len(_username)=0
								Notify "Username required!",True
							Else
								If Len(_password)=0
									Notify "Password required!",True
								Else
									EnableGadget parent
									FreeGadget _window
									Return [_username,_password]
									Exit
								EndIf
							EndIf
					EndSelect
			EndSelect
		Forever
	EndFunction
EndType

Rem
bbdoc: Creates a username and password dialog.
returns: A string array. Index 0 is username, index 1 is password
EndRem
Function RequestUsernamePassword:String[](parent:TGadget,username:String="",password:String="")
	Return TUserPassDialog.Create(parent,username,password)
EndFunction

Rem
'EXAMPLE'
Global window:tgadget=CreateWindow("Dialog Test",50,50,640,480,Desktop(),1+2)
Global button:tgadget=CreateButton("Dialog",10,10,80,24,window)

Repeat
	WaitEvent()
	Select EventID()
		Case event_windowclose
			Select EventSource()
				Case window
					End
			EndSelect
		
		Case event_gadgetaction
			Select EventSource()
				Case button
					Local info:String[]=RequestUsernamePassword(window)
					Local username:String=info[0]
					Local password:String=info[1]
					
					If username="" And password=""
						Notify "Dialog closed.",True
					Else
						Notify "USERNAME: "+username+"~nPASSWORD: "+password,False
					EndIf
			EndSelect
	EndSelect
Forever
EndRem</textarea> <br><br></td></tr></table><br>
<a name="898605"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ked</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here is a reminder dialog.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict
Import maxgui.maxgui
'Import maxgui.drivers

'CREATED BY KEDRIC SIDDONS'
'COPYRIGHT 2008 KEDRIC SIDDONS'

Type TReminderDialog
	Global _window:TGadget
	Global _imgpanel:TGadget
	Global _infolabel:TGadget
	Global _okbutton:TGadget
	
	Function Create(parent:TGadget,remindertxt:String,image:TPixmap=Null)
		If GadgetClass(parent)&lt;&gt;GADGET_WINDOW
			RuntimeError "TReminderDialog: Parent needs to be a window!"
			End
		EndIf
		
		DisableGadget parent
		_window=CreateWindow("Reminder",0,0,300,150,parent,WINDOW_TITLEBAR|WINDOW_CENTER|WINDOW_CLIENTCOORDS)
		_imgpanel=CreatePanel(10,10,64,64,_window)
		If image&lt;&gt;Null
			SetPanelPixmap _imgpanel,image,PANELPIXMAP_FIT
		EndIf
		_infolabel=CreateLabel(remindertxt,84,10,ClientWidth(_window)-(84+10),ClientHeight(_window)-(24+10+10),_window)
		_okbutton=CreateButton("OK",ClientWidth(_window)-(70+10),ClientHeight(_window)-(24+10),70,24,_window)
		
		Repeat
			WaitEvent()
			Select EventID()
				Case EVENT_WINDOWCLOSE
					Select EventSource()
						Case _window
							EnableGadget parent
							FreeGadget _window
							Exit
					EndSelect
					
				Case EVENT_GADGETACTION
					Select EventSource()
						Case _okbutton
							EnableGadget parent
							FreeGadget _window
							Exit
					EndSelect
			EndSelect
		Forever
	EndFunction
EndType

Rem
bbdoc: Creates a reminder dialog.
EndRem
Function Reminder(parent:TGadget,remindertxt:String,image:TPixmap=Null)
	TReminderDialog.Create(parent,remindertxt,image)
EndFunction

Rem
'EXAMPLE'
Local window:TGadget=CreateWindow("Dialog",50,50,640,480,Desktop(),1+2)
Local button:TGadget=CreateButton("Dialog",10,10,80,24,window)
Local image:TPixmap=LoadPixmap("F:\Blitzmax.png")

Repeat
	WaitEvent()
	Select EventID()
		Case event_windowclose
			Select EventSource()
				Case window
					End
			EndSelect
		
		Case event_gadgetaction
			Select EventSource()
				Case button
					Reminder(window,"Hey!~nThis is to remind you that this "+..
					"magnificent piece of work is fantastic and that you should "+..
					"always use it.~n~nThank you for your time.",image)
			EndSelect
	EndSelect
Forever
EndRem</textarea> <br><br></td></tr></table><br>
<a name="898627"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey Kev,<br><br>Nice work ;-)<br><br>If you are serious about wanting them to be added to <b>MaxGUI.ProxyGadgets</b>, we first need to make sure that they behave as expected on all platforms. We also need to establish some sort of convention of <b>ProxyGadget</b> requesters, but I'm sure it will pay off as many people will find these things really useful to have quick access to.<br><br>I was thinking along the lines of <b>TRequester</b> from <i>maxide.bmx</i>. This is the base type from which all of MaxIDE's Find/Find &amp; Replace/IDE Options/Command Line/Goto windows extend. As you can see it has an <b>InitRequester()</b> method that initializes the window in a standardized way, according to the style parameters specified in <i>flags</i>. This allows the control to be much more flexible - the user can quickly specify which button(s) they want on the requester, the text of the OK button, as well as things like whether we have a divider line, and whether it appears as a tool window.<br><br>Have you any thoughts? <br><br></td></tr></table><br>
<a name="898662"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ked</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Hey Kev, <br></div><br>Ked. :)<br><br><div class="quote"> Nice work ;-) <br></div><br>Thanks.<br><br><div class="quote"> If you are serious about wanting them to be added to MaxGUI.ProxyGadgets, we first need to make sure that they behave as expected on all platforms. <br></div><br>Agreed.<br><br><div class="quote"> We also need to establish some sort of convention of ProxyGadget requesters, but I'm sure it will pay off as many people will find these things really useful to have quick access to. <br></div><br>I also agree.<br><br><div class="quote"> I was thinking along the lines of TRequester from maxide.bmx. This is the base type from which all of MaxIDE's Find/Find &amp; Replace/IDE Options/Command Line/Goto windows extend. As you can see it has an InitRequester() method that initializes the window in a standardized way, according to the style parameters specified in flags. This allows the control to be much more flexible - the user can quickly specify which button(s) they want on the requester, the text of the OK button, as well as things like whether we have a divider line, and whether it appears as a tool window.<br><br>Have you any thoughts? <br></div><br>I will definitely have a good look at the TRequester from the MaxIDE. <br><br></td></tr></table><br>
<a name="898663"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ked</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oooo... I don't know. I really don't know how to do OOP like that.<br><br>EDIT: But I'll try anyway! :P <br><br></td></tr></table><br>
<a name="898853"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ked</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I was looking at the TRequester from MaxIDE and it's not what I'm trying to make. I'm trying to do stuff like Notify, Confirm, Proceed, RequestColor, RequestDir, and RequestFile. <br><br></td></tr></table><br>
<a name="899169"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ked, great work.<br>I just changed your Reminder to manage better the text height (on MacOS the last line in your original example is cut off).<br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict
'Import maxgui.maxgui
Import maxgui.drivers

'CREATED BY KEDRIC SIDDONS'
'COPYRIGHT 2008 KEDRIC SIDDONS'

Type TReminderDialog
	Global _window:TGadget
	Global _imgpanel:TGadget
	Global _infolabel:TGadget
	Global _okbutton:TGadget
	
	Function Create(parent:TGadget,remindertxt:String,image:TPixmap=Null)
		If GadgetClass(parent)&lt;&gt;GADGET_WINDOW
			RuntimeError "TReminderDialog: Parent needs to be a window!"
			End
		EndIf
		
		
		Local hpa:Int=150
		Local ac$
		
		For Local c:Int=0 Until Len(remindertxt)
			ac=remindertxt[c]
	
			If ac=10 Or ac=13 hpa:+10			
		Next
		
		
		DisableGadget parent
		_window=CreateWindow("Reminder",0,0,300,hpa,parent,WINDOW_TITLEBAR|WINDOW_CENTER|WINDOW_CLIENTCOORDS)
		_imgpanel=CreatePanel(10,10,64,64,_window)
		If image&lt;&gt;Null
			SetPanelPixmap _imgpanel,image,PANELPIXMAP_FIT
		EndIf
		_infolabel=CreateLabel(remindertxt,84,10,ClientWidth(_window)-(84+10),ClientHeight(_window)-(24+10+10),_window)
		_okbutton=CreateButton("OK",ClientWidth(_window)-(70+10),ClientHeight(_window)-(24+10),70,24,_window)
		
		Repeat
			WaitEvent()
			Select EventID()
				Case EVENT_WINDOWCLOSE
					Select EventSource()
						Case _window
							EnableGadget parent
							FreeGadget _window
							Exit
					EndSelect
					
				Case EVENT_GADGETACTION
					Select EventSource()
						Case _okbutton
							EnableGadget parent
							FreeGadget _window
							Exit
					EndSelect
			EndSelect
		Forever
	EndFunction
EndType

Rem
bbdoc: Creates a reminder dialog.
EndRem
Function Reminder(parent:TGadget,remindertxt:String,image:TPixmap=Null)
	TReminderDialog.Create(parent,remindertxt,image)
EndFunction

'EXAMPLE'
Local window:TGadget=CreateWindow("Dialog",50,50,640,480,Desktop(),1+2)
Local button:TGadget=CreateButton("Dialog",10,10,80,24,window)
Local image:TPixmap=LoadPixmap("F:\Blitzmax.png")

Repeat
	WaitEvent()
	Select EventID()
		Case event_windowclose
			Select EventSource()
				Case window
					End
			EndSelect
		
		Case event_gadgetaction
			Select EventSource()
				Case button
					Reminder(window,"Hey!~nThis is to remind you that this "+..
					"magnificent piece of work is fantastic and that you should "+..
					"always use it.~n~nThank you for your time.",image)
			EndSelect
	EndSelect
Forever
</textarea> <br><br></td></tr></table><br>
<a name="899184"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ked</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> OK, cool. Thanks, degac. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
