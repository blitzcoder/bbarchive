<!DOCTYPE html><html lang="en" ><head ><title >CheckListBox</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >CheckListBox</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >CheckListBox</a><br><br>
<a name="979480"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> As there is any solution to manage a listbox + checkable items (and I can't found anymore the Leadwerks(?) hack to the 'standard' MaxGUI ListBox...) I made this gadget...<br><br>At the moment is tested ONLY on Windows XP<br><br><img src="http://www.graphio.net/download/maxgui_checklistbox_example.png"><br><br>Not very exciting, but it seems to works well<br><br>You need (at the moment, until I find a way to 'codify-in-the-source-code' the checkbox images) the following image-iconstrip<br><br><img src="http://www.graphio.net/download/check.png"><br><br>And now the incredible code<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
Import MaxGUI.drivers

	Local window:tgadget	=CreateWindow("test",100,100,240,410,,window_titlebar|window_center)
	Local checklist:tgadget=CreateCheckListBox(10,10,100,300,window)
	Local checklist2:tgadget=CreateCheckListBox(120,10,100,300,window)
	
	Local btn_clear:tgadget	=CreateButton("Clear all",10,320,100,25,window)
	Local btn_copy:tgadget	=CreateButton("Copy selected",10,350,100,25,window)
	
	For Local t:Int=0 Until 10
		AddGadgetItem checklist,String(t)
	Next
	
	InsertGadgetItem checklist,2,"New 2",1
	
	RemoveGadgetItem checklist,3
	
	ModifyGadgetItem checklist,5,"5 - changed",1,,"This has been changed"
	
	
	SetGadgetColor checklist,200,190,150
	SetGadgetTextColor checklist,20,90,200
	

	
	While True
	WaitEvent 


	Select EventID()
		Case event_gadgetaction
			Select EventSource()
				Case btn_clear
					ClearGadgetItems checklist
						For Local t:Int=0 Until 10
							AddGadgetItem checklist,String(t)+" "+MilliSecs(),Rand(0,1)
						Next
				
				Case btn_copy
						ClearGadgetItems checklist2
						Local result:Int[]=SelectedGadgetItems(checklist)
						For Local i:Int=EachIn result
							AddGadgetItem checklist2,GadgetItemText(checklist,i),checklist.ItemState(i),,checklist.ItemTip(i),GadgetItemExtra(checklist,i)
						Next
			End Select
		
		Case EVENT_WINDOWCLOSE
			End
	End Select
Wend



Function CreateCheckListBox:TGadget(x:Int,y:Int,w:Int,h:Int,group:TGadget,style:Int=0)
	
	Return New TcheckBoxList .Create(x,y,w,h,group,style)
	
EndFunction

Type TcheckBoxList Extends TProxyGadget
	
	Global list_checkboxlist:TList
	Global iconstrip_check:ticonstrIP=LoadIconStrip("check.png")

	

		
	Field style:Int
	Field list:tgadget
	Field status:Int[],count:Int
	
	?win32
		Field _os:Int=0
	?
	?macos
		Field _os:Int=6
	?
	?linux
		Field _os:Int=2
	?

'ethod InsertItem(index,text$,tip$,icon,extra:Object,flags)
	Method InsertItem(_index:Int,_text:String="",_tip:String="",_icon:Int=-1,extra:Object=Null,flags:Int=0)
	
	
		items=items[..items.length+1]
		status=status[..status.length+1]
		status[_index]=flags
		Super.INSERTItem(_index,_text,_tip,flags+_OS,extra,flags)
		
	End Method
	
	Method RemoveItem(index:Int)
	'	Print "Remove an item : "+index
		For Local i:Int=index Until status.length-1
			status[i]=status[i+1]
		Next
		status=status[..status.length-1]
		Super.removeItem(index)

	End Method
	
	Method SetItem(index:Int,text$,tip$,icon:Int,extra:Object,flags:Int)
		Super.SetItem(index,text,tip,icon+_OS,extra,flags)
		status[index]=flags
		ModifyGadgetItem(list,index,GadgetItemText(list,index),0,status[index]+_OS,list.itemTip(index),GadgetItemExtra(list,index))
	End Method
	

	Method ItemState:Int(index:Int)
		?debug
				If index&lt;0 Or index&gt;=items.length Throw "Index out of range"
		?
		Return status[index]
	End Method
	
	Method New()
		If Not list_checkboxLIst
			list_checkBoxList=New TList
			AddHook EmitEventHook, eventHandler, Null, -1
			
		End If
	EndMethod
	
	Method Clear()
		list.ClearListItems()
		list.items=Null
		status=Null
	End Method
	
	Method Invert()
	'
		
	
	End Method

	Method SelectedItems:Int[]()
		Local	index:Int,count:Int,array:Int[items.length]
		For index=0 Until status.length
			If Status[index]=1 Then
				array[count] = index
				count:+1
			EndIf
		Next
		If count&gt;0 Then Return array[..count]
	End Method
	
	
	Method Create:TcheckBoxList (x:Int,y:Int,w:Int,h:Int,group:TGadget,_style:Int=0)
		list=CreateListBox(x,y,w,h,group,style)
		SetGadgetIconStrip list,iconstrip_check

		SetProxy( list )
		style = _style
		LIST_CHECKBOXLIST.AddLast Self
		Return Self
	EndMethod
	
	Method EventHook:TEvent( pEvent:TEvent )
		Local state:Int
		Local selected:Int
		
		Select pEvent.id
		
			Case EVENT_GADGETACTION
				selected=SelectedGadgetItem(list)
				If selected&gt;-1
					status[selected]=1-status[selected]
					ModifyGadgetItem(list,selected,GadgetItemText(list,selected),0,status[selected]+_OS,"",GadgetItemExtra(list,selected))
				End If
				
		
		EndSelect
		
		Return Null
	
	EndMethod
	
		
	Method CleanUp()
		list_checkBoxList.remove(Self)
		Super.CleanUp()
		status=Null
		'list_checkboxlist=Null
		'iconstrip_check=Null
		
	EndMethod
	
	Method Free()
		list_checkboxlist=Null
		iconstrip_check=Null
	End Method

	Function eventHandler:Object( pID%, pData:Object, pContext:Object )
		Local pEvent:TEvent = TEvent(pData)
		
		If pEvent Then
			For Local lstcheck:TcheckBoxList = EachIn list_checkboxList
				If lstcheck= pEvent.source Then Return lstcheck.EventHook( pEvent )
			Next
		EndIf
		
		Return pData
	EndFunction
	
EndType
</textarea><br>I tried to use the MaxGUI-built in State_button, but I failed, so I prefered to use my own status[] array to keep this information.<br><br>I'm planning to add some other function like Invert() to change the check-state with a single command, but at the moment I've not found a MaxGUI method-function to map it.<br><br>Any comments, improvements, suggestions or critics are welcomed. <br><br></td></tr></table><br>
<a name="979490"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just discovered FLTK ListBox doesnt' support icons...so my experiment is already ended.<br>I will check with new ways (panel+checkbox gadgets...) <br><br></td></tr></table><br>
<a name="979519"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ked</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Why can't you just use a panel, some labels, scrollbars, and checkboxes? <br><br></td></tr></table><br>
<a name="979524"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>Why can't you just use a panel, some labels, scrollbars, and checkboxes? <br> <br></div><br><strike>I'm trying this way...</strike><br><br>Edited: see my last post <br><br></td></tr></table><br>
<a name="979607"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm feel very, very stupid...<br><br><a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=2290" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=2290</a><br><br>I've already made times ago a checkboxlist (...sort of) and made it public in the code archives...<br><br>I will re-work it to create a standard MaxGUI interface... <br><br></td></tr></table><br>
<a name="979688"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> <strike>WIP (95% - it misses RemoveGadgetItem, Extra gadget full supports, and icon supports at the moment)</strike><br><br>Now completed (I think), tested only on WIN32 - Linux (Ubuntu 9.04)<br><br><img src="http://www.graphio.net/Images/ListCheckBox.png"><br><br><img src="http://www.graphio.net/Images/CheckListBox_linux.png"><br><br>Plain MaxGUI code, standard interface, so it should works on every platform without too much problems.<br><br>Supported 'commands':<br><br>AddGadgetItem (the FLAGS=1 means Checked)<br>ModifyGadgetItem<br>ClearGadgetItems<br>SelectedGadgetItems<br>CountGadgetItems<br>Disable/EnableGadget<br>Setgadgetcolor<br>GadgetItemText<br>GadgetItemExtra<br>RemoveGadgetItem<br>SetGadgetIconStrip <br>SelectGadgetItem *<br>DeselectGadgetItem *<br>ToggleGadgetItem *<br><br>* Index= -1 means select all the items<br><br>It supports now Pixmaps/Iconstrip<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Rem
	checklistbox
	
	version 2.0 - degac
	
	18 agosto 2009
	
	2.1
		+ added IconStrip support
		+ added RemoveGadgetItem
		+ added SelectGadgetItem (-1 as index means ALL the items)
		+ added DeselectGadgetItem (-1 as index means ALL the items)
		+ added ToggleGadgetItem (-1 as index means ALL the items)

		
	
End Rem

SuperStrict
Import maxgui.drivers


	Local window:tgadget=CreateWindow("test 2",100,100,350,340,,WINDOW_TITLEBAR|WINDOW_CENTER)
	
	Local list1:tgadget=CreateCheckListBox( 10,10,150,200,window)
	Local list2:tgadget=CreateCheckListBox(180,10,150,200,window)
	
	Local but1:tgadget	=CreateButton("Clear all"	,10,220,100,25,window)
	Local but2:tgadget	=CreateButton("Copy"		,10,250,100,25,window)
	Local but3:tgadget	=CreateButton("Destroy"	,120,220,100,25,window)
	Local but4:tgadget	=CreateButton("Enable"	,120,250,100,25,window)
	Local but5:tgadget	=CreateButton("Disable"	,230,250,100,25,window)
	Local but6:tgadget	=CreateButton("Check all"	, 10,280,100,25,window)
	Local but7:tgadget	=CreateButton("UnCheck all"	,120,280,100,25,window)
	Local but8:tgadget	=CreateButton("Toggle all"	,230,280,100,25,window)
	
	Local ic:ticonstrip=LoadIconStrip("D:\_BlitzMax\MaxGUI_extra\toolbar.png")
	
	SetGadgetIconStrip list1,ic
	SetGadgetIconStrip list2,ic
	SetGadgetColor list1,200,100,90

	For Local ii:Int=1 To 20
		AddGadgetItem list1,"Item "+ii,Rand(0,1),(ii Mod 11),"Tip "+ii
	Next

	Print "Item in List1: "+CountGadgetItems(list1)
	
	ModifyGadgetItem list1,3,"---New 4",1,8,"This is new..."
	RemoveGadgetItem list1,4 ' it's the 5th items in the list (starting from 0...)
	
	Print GadgetItemText(list1,3)

	'DisableGadget list1
	'DeselectGadgetItem list1,-1
	'ToggleGadgetItem list1,1
	SelectGadgetItem list1,22
	
	
	While True
	WaitEvent 
	'Print CurrentEvent.ToString()
	Select EventID()
		Case EVENT_GADGETACTION	
			Select EventSource()
				Case but6 'check all
					SelectGadgetItem(list1,-1)
				Case but7 'uncheck all
					DeselectGadgetItem(list1,-1)	
				Case but8 'toggle item
					ToggleGadgetItem(list1,-1)			
			
				Case but1
					ClearGadgetItems list1
				Case but2
					
					ClearGadgetItems list2
					Local result:Int[]=SelectedGadgetItems(list1)
					For Local i:Int=EachIn result
							AddGadgetItem list2,GadgetItemText(list1,i),list1.ItemState(i),list1.ItemIcon(i),list1.ItemTip(i),GadgetItemExtra(list1,i)
					Next

				Case but3
					FreeGadget list1
					
				Case but5
					DisableGadget list1
				Case but4
					EnableGadget list1
				
					
				
			End Select
	
		Case EVENT_WINDOWCLOSE
			End
	End Select
Wend

End 


Function CreateCheckListBox:Tchecklistbox (x:Int , y:Int , w:Int , h:Int , win:tgadget , style:Int = 0) 
	Return Tchecklistbox .Create(x , y , w , h , win ,  style) 
End Function


Type SingleItem
	Field panel:tgadget
	Field check:tgadget
	Field label:tgadget
	Field icon:tgadget
	Field iconNumber:Int
	Field parent:tchecklistbox
	
	
		
	Function Create:SingleItem(parent:tchecklistbox,txt$,tip$="",icon:Int=-1,state:Int=0)
		Local ss:singleItem=New singleItem
		Local yy:Int=parent.it.length*20-20
			
		ss.panel=CreatePanel(0,yy,ClientWidth(parent.panel),20,parent.panel,PANEL_ACTIVE)
		ss.check=CreateButton("",5,0,16,19,ss.panel,BUTTON_CHECKBOX)
		ss.label=CreateLabel(txt,40,2,ClientWidth(ss.PANEL),20,SS.PANEL)
		ss.icon=CreatePanel(20,1,18,18,ss.panel)
		
		ss.parent=parent
		
		SetGadgetToolTip ss.label,tip
		SetButtonState ss.check,state
		Return SS
	End Function
	
	Method Change(txt$,tip$,_iconNumber:Int=-1,state:Int=-1)
	
		SetGadgetText label,txt
		SetGadgetToolTip label,tip
		If _iconNumber&gt;-1
				Local px:TPixmap=PixmapFromIconStrip(parent.myiconstrip, _iconNumber)
				SetGadgetPixmap icon,px,PANELPIXMAP_CENTER
				iconNumber=_iconNumber
				
		End If

		
		If state&lt;&gt;-1 SetButtonState check,state	
	
	End Method
	
	Method Clear()
		If label FreeGadget label
		If check FreeGadget check
		If panel FreeGadget panel
		If icon FreeGadget icon
		
		icon=Null
		label=Null
		check=Null
		panel=Null	
	End Method

	Method SetEnabled(status:Int=True)
		
		If status=True
			EnableGadget label
			EnableGadget check
			EnableGadget panel
			EnableGadget icon
		Else
			DisableGadget label
			DisableGadget check
			DisableGadget panel
			DisableGadget icon

		End If
	
	End Method
	
End Type


Type Tchecklistbox Extends TProxyGadget

	Global list_checklistbox:TList

	Field myiconstrip:ticonstrip
	Field panel:tgadget
	Field slider:tgadget
	Field style:Int
	Field current_position:Int,visible:Int
	Field panel_width:Int
	Field it:singleItem[]
	
	Method InsertItem(index:Int,text:String,tip:String,icon:Int,extra:Object,flags:Int)
		
		?debug
		If index&lt;0 Or index&gt;it.length Throw "Index out of range"
		?	
		it=it[..it.length+1]
		For Local i:Int=it.length-2 To index Step -1
			it[i+1]=it[i]
		Next
		If it[index]&lt;&gt;Null 
				SetGadgetText it[index].label,text
				SetGadgetExtra it[index].label,extra
				If icon&gt;-1
					Local px:TPixmap=PixmapFromIconStrip(myiconstrip, icon)
					SetGadgetPixmap it[index].icon,px,PANELPIXMAP_CENTER
					it[index].iconNumber=icon
				End If
		Else
			it[index]=SingleItem.Create(Self,text$,tip$,icon,flags)
			it[index].label.Extra=Extra
			If icon&gt;-1
				Local px:TPixmap=PixmapFromIconStrip(myiconstrip, icon)
				SetGadgetPixmap it[index].icon,px,PANELPIXMAP_CENTER
				it[index].iconNumber=icon
			End If
		End If
		
		Local num:Int = it.length - visible + 1
		If slider SetSliderRange slider , 0 , num

		
	End Method
	
	Method SetIconStrip(_iconstrip:ticonStrip)
		myiconstrip=_iconStrip
	End Method
	
	Method SelectItem(index:Int=-1,op:Int=1)
		Rem
			if index=-1 SELECT ALL the items
			
			OP = 1	set CHECKED
			OP = 0	set UNCHEKED
			OP = 2	TOGGLE
		End Rem
		
		If index=-1
			If op=0 Or op=1
				For Local ss:singleitem=EachIn it
					If ss	SetButtonState(ss.check,OP)
				Next
			Else
				For Local ss:singleitem=EachIn it
					If ss	SetButtonState(ss.check,1-ButtonState(ss.check))
				Next
			
			End If
		Else
			
			If index&lt;it.length 
				If OP=0 Or OP=1 
					SetButtonState(it[index].check,OP)
				Else
					SetButtonState(it[index].check,1-ButtonState(it[index].check))

				End If
			End If
			
		End If
					
		
	
	End Method
	
	
	
	Method SetEnabled(status:Int=True)
	
		For Local ss:singleitem=EachIn it
			ss.SetEnabled(status)
		Next

		If status=True
			EnableGadget slider
			EnableGadget panel
		Else
			DisableGadget slider
			DisableGadget panel
		End If
	End Method
	
	Method ItemCount:Int()
		Return it.length
	End Method
	
	Method RemoveItem:Int(index:Int)
		?debug
		If index&lt;0 Or index&gt;it.length Throw "Index out of range"
		?	
		
		it[index].Clear()
		it[index]=Null
		
		For Local t:Int=index+1 Until it.length
			it[t-1]=it[t]
		
		Next
		it=it[..it.length-1]
		
		'Replace position

		For Local t:Int=index Until it.length
			SetGadgetShape it[t].panel , 0 , t * 20, panel_width , 20
		Next
		If slider SetSliderRange slider , 0 , it.length - visible + 1


	End Method
	
	Method SetItem(index:Int,text:String,tip:String,icon:Int,extra:Object,flags:Int)
		?debug
		If index&lt;0 Or index&gt;it.length Throw "Index out of range"
		?	
		it[index].Change(text,tip,icon,flags)	
	End Method
	
	Method ItemText:String(index:Int)
		?debug
		If index&lt;0 Or index&gt;it.length Throw "Index out of range"
		?
		Return GadgetText(it[index].label)
	End Method
	
	Method ItemTip:String(index:Int)
		?debug
		If index&lt;0 Or index&gt;it.length Throw "Index out of range"
		?
		Return it[index].label.GetTooltip()
	End Method
	
	Method ItemExtra:Object(index:Int)
		?debug
		If index&lt;0 Or index&gt;it.length Throw "Index out of range"
		?
		Return GadgetExtra(it[index].label)

	End Method
	
	Method ItemIcon:Int(index:Int)
		?debug
				If index&lt;0 Or index&gt;=it.length Throw "Index out of range"
		?
		Return it[index].iconNumber
	End Method
	
	Method ItemState:Int(index:Int)
		?debug
		If index&lt;0 Or index&gt;it.length Throw "Index out of range"
		?
		Return ButtonState(it[index].check)
	End Method
	
	Method Clear()
		For Local ss:singleItem=EachIn it
			ss.clear()
			ss=Null
		Next
		it=Null
		
	End Method
	
	Method SetColor(r:Int,g:Int,b:Int)
		SetGadgetColor panel,r,g,b
		For Local si:singleitem=EachIn it
			SetGadgetColor si.panel,r,g,b
			SetGadgetColor si.label,r,g,b
			SetGadgetColor si.check,r,g,b
		Next	
	End Method
	
	Method SelectedItems:Int[]()
		'SELECED (=CHECKED) ITEMS
		Local count:Int,i:Int,array:Int[it.length]
		For i=0 Until it.length
			If it[i]
				If ButtonState(it[i].check)=True
					array[count]=i
					count:+1
				End If
			End If
		Next
		If count&gt;0 Return array[..count]	

	End Method
	
	Method CleanUp()
		Clear()
		If slider FreeGadget slider
		If panel FreeGadget panel
		
		list_checklistbox.remove(Self)
		
	
	End Method
	
	Method Free()
		
		ClearList list_checklistbox
		list_checklistbox=Null
	End Method
	


	Function Create:Tchecklistbox ( x:Int, y:Int, w:Int, h:Int, group:TGadget,style:Int=0 )
		Local mb:Tchecklistbox =New Tchecklistbox 
		mb.panel = CreatePanel(x , y , w-20 , h , group , PANEL_ACTIVE) 
		mb.style = style
		mb.slider=CreateSlider(x+w-20,y,20,h,group,SLIDER_SCROLLBAR|SLIDER_VERTICAL)
		SetGadgetColor mb.panel,255,255,255
		ActivateGadget mb.panel
		AddHook EmitEventHook,_Hook,mb
		mb.setproxy(mb.panel)
		If list_checklistbox=Null list_checklistbox=New TList
		list_checklistbox.addlast mb
		
		mb.panel_width=ClientWidth(mb.panel)
		mb.visible=Int(h/20)
		
		Return mb
	EndFunction

	Function _Hook:Object( id:Int, data:Object, context:Object )
		Local mb:Tchecklistbox 
		Local event:TEvent
		event=TEvent(data)
		If event
			mb=Tchecklistbox (context)	
			If mb Return mb.HandleEvents(event)
		EndIf
		Return data
	EndFunction
	
	Method HandleEvents:TEvent( event:TEvent ) 
		Local what:Tchecklistbox 
		Local con:Int = 0
		Local datamove:Int
		
		
		Select event.id
			'mouse_wheel for changing position?
									
			Case EVENT_GADGETACTION
							
				If EventSource() = slider
				
					current_position = SliderValue(slider)'EventData() 
					
					If current_position &lt; 0 current_position = 0
					If current_position &gt; it.length current_position=it.length
					Local con:Int=0
					For Local what:singleitem=EachIn it
						SetGadgetShape what.panel , 0 , con * 20 - current_position* 20 , panel_width , 20
						con:+1
					Next

				
				End If
				
		EndSelect
	Return event
	EndMethod

End Type

</textarea> <br><br></td></tr></table><br>
<a name="1167272"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MacSven</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Does not work under MacOS X. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
