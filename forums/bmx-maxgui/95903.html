<!DOCTYPE html><html lang="en" ><head ><title >Tweak for proper combobox event handling</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Tweak for proper combobox event handling</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >Tweak for proper combobox event handling</a><br><br>
<a name="1107429"></a>

<a name="1107430"></a>

<a name="1107435"></a>

<a name="1107436"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skn3</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have made a tweak for the maxgui combo box event handling. The tweak should be backwards compatible with old maxgui behaviour. The tweak allows:<br><br>- Gadget lost event on combobox<br>- EVENT_GADGETACTION mods field now is false for an updating change and true for an updated change<br><br>What this means if a user opens a combobox ... browsers around with keyboard and then clicks/hits return .. the final EVENT_GADGETACTION will have a mods True and all other previous EVENT_GADGETACTION mods false.<br><br>So if you want to only capture a combobox change when its a final change, look for mods = True. Useful for processing something upon selection instead of on change.<br><br>win32maxguiex.bmx<br>Replace your old TWindowsComboBox type with this<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Type TWindowsComboBox Extends TWindowsGadget

	Field _icons:TWindowsIconStrip
	Field _editHwnd, _comboHwnd
	Field _selected = -1
	Field _selecting = -1
	Field _open = False
	
	Method Create:TWindowsGadget(group:TGadget,style,Text$="")
		Local	xstyle,wstyle,hotkey,hwnd
		Local	parent,editstyle,combostyle
		
		Self.style = style
		wstyle=WS_CHILD|WS_TABSTOP|WS_CLIPSIBLINGS|WS_CLIPCHILDREN|CBS_AUTOHSCROLL
		If (style &amp; COMBOBOX_EDITABLE) Then wstyle:|CBS_DROPDOWN Else wstyle:|CBS_DROPDOWNLIST
		
		parent=group.query(QUERY_HWND_CLIENT)
		hwnd=CreateWindowExW(xstyle,"ComboBoxEx32","",wstyle,0,0,0,180,parent,hotkey,GetModuleHandleW(Null),Null)
		
		If (style &amp; COMBOBOX_EDITABLE) Then
			_editHwnd=SendMessageW(hwnd,CBEM_GETEDITCONTROL,0,0)
			If _editHwnd Then
				editstyle=GetWindowLongW(_editHwnd,GWL_STYLE)
				SetWindowLongW _editHwnd,GWL_STYLE,editstyle|WS_TABSTOP
			EndIf
		EndIf

		_comboHwnd=SendMessageW(hwnd,CBEM_GETCOMBOCONTROL,0,0)
		comboStyle=GetWindowLongW(_comboHwnd,GWL_STYLE)
		SetWindowLongW _comboHwnd,GWL_STYLE,comboStyle|WS_TABSTOP
		
		Register GADGET_COMBOBOX,hwnd
		
		TWindowsGUIDriver.RegisterHwnd(_combohwnd,Self)
		If _edithwnd Then TWindowsGUIDriver.RegisterHwnd(_edithwnd,Self)
		
		SetColor(255,255,255)
		
		Return Self	
		
	EndMethod
	
	Method SetText(Text$)
		If Not _editHwnd Then
			Local tmpWString:Short Ptr = Text.ToWString()
			Local tmpResult = SendMessageW(_comboHwnd, CB_SETCUEBANNER, 0, Int(tmpWString))
			MemFree tmpWString;Return tmpResult
		Else
			Return Super.SetText(Text)
		EndIf
	EndMethod
	
	Method GetText$()
		If Not _editHwnd Then
			If _selected &gt; -1 Then Return items[_selected].Text Else Return ""
		Else
			Return Super.GetText()
		EndIf
	EndMethod
	
	Method Activate(cmd)
		If _editHwnd Then
			Select cmd
				Case ACTIVATE_CUT	
					SendMessageW _editHwnd,WM_CUT,0,0
				Case ACTIVATE_COPY	
					SendMessageW _editHwnd,WM_COPY,0,0
					SetFocus _hwnd
				Case ACTIVATE_PASTE
					SendMessageW _editHwnd,WM_PASTE,0,0
				Case ACTIVATE_FOCUS
					SendMessageW _editHwnd,EM_SETSEL,0,-1
			End Select
		EndIf
		Return Super.Activate(cmd)
	EndMethod
	
	Method SetIconStrip(iconstrip:TIconStrip)
		Local imagelist
		_icons=TWindowsIconStrip(iconstrip)
		If _icons Then imagelist = _icons._imagelist
		SendMessageW _hwnd,CBEM_SETIMAGELIST,LVSIL_SMALL,imagelist
	EndMethod

	Method ClearListItems()
		_selected=-1
		_selecting=-1
		Desensitize()
		SendMessageW _hwnd,CB_RESETCONTENT,0,0
		Sensitize()
	EndMethod

	Method InsertListItem(index,Text$,tip$,icon,tag:Object)
		Local it:COMBOBOXEXITEMW = New COMBOBOXEXITEMW
		it.mask=CBEIF_TEXT
		it.iItem=index
		it.pszText=Text.toWString()
		If icon&gt;=0
			it.mask:|CBEIF_IMAGE|CBEIF_SELECTEDIMAGE
			it.iImage=icon
			it.iSelectedImage=icon
		EndIf
		Desensitize()
		SendMessageW(_hwnd,CBEM_INSERTITEMW,0,Int Byte Ptr(it))
		Sensitize()
		MemFree it.pszText
	EndMethod
	
	Method SetListItem(index,Text$,tip$,icon,tag:Object)
		Local it:COMBOBOXEXITEMW = New COMBOBOXEXITEMW
		it.mask=CBEIF_TEXT
		it.iItem=index
		it.pszText=Text.toWString()
		If _icons And icon&gt;-1
			it.mask:|CBEIF_IMAGE|CBEIF_SELECTEDIMAGE
			it.iImage=icon
			it.iSelectedImage=icon
		EndIf
		Desensitize()
		SendMessageW(_hwnd,CBEM_SETITEMW,0,Int Byte Ptr(it))
		Sensitize()
		MemFree it.pszText
	EndMethod
	
	Method RemoveListItem(index)
		Desensitize()
		SendMessageW _hwnd,CBEM_DELETEITEM,index,0
		Sensitize()
	EndMethod
	
	Method SetListItemState(index,state)
		If State&amp;STATE_SELECTED
			_selected=index
			_selecting=index
		Else
			If _selected=index
				_selected=-1
				_selecting=-1
			EndIf
			index=-1
		EndIf
		Desensitize()
		SendMessageW _hwnd,CB_SETCURSEL,index,0
		Sensitize()
	EndMethod
	
	Method ListItemState(index)
		Local Current,state
		Current=SendMessageW(_hwnd,CB_GETCURSEL,0,0)
		If Current=CB_ERR Current=-1
		If Current=index state=STATE_SELECTED
		Return state
	EndMethod
	
	Method OnCommand(msg,wp)
		Local index
		Select wp Shr 16
			Case CBN_SELENDOK
				'treat this as a UPDATE
				'keep track of open state
				_open = False
				
				'the combo box was closed so lets check for a change
				'this is a new function as we are going to fire gadget select
				index=SendMessageW(_hwnd,CB_GETCURSEL,0,0)
				If index=CB_ERR
					index=-1
				Else
					If _selected&lt;&gt;index	'user generated event
						_selecting=index
						_selected=index
						Local extra:Object
						If index&gt;=0 And index&lt;items.length extra=items[index].extra
						PostGuiEvent EVENT_GADGETACTION,index,True,0,0,extra
					EndIf
				EndIf
				
			Case CBN_DROPDOWN
				'keep track of open state
				_open = True
				
			Case CBN_KILLFOCUS
				'combo box lost focus
				PostGuiEvent EVENT_GADGETLOSTFOCUS
			 
			Case CBN_SELCHANGE
				'only process sel change if open (to emulate old max behaviour)
				If _open
					index=SendMessageW(_hwnd,CB_GETCURSEL,0,0)
					If index=CB_ERR
						index=-1
					Else
						If _selecting&lt;&gt;index	'user generated event
							_selecting = index
							Local extra:Object
							If index&gt;=0 And index&lt;items.length extra=items[index].extra
							PostGuiEvent EVENT_GADGETACTION,index,False,0,0,extra
						EndIf
					EndIf
				EndIf
			Case CBN_EDITCHANGE
				_selecting=-1
				PostGuiEvent EVENT_GADGETACTION,-1

		End Select
	EndMethod

	Method Class()
		Return GADGET_COMBOBOX
	EndMethod

EndType</textarea><br><br>I'll get a macOS version done soon!<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1108249"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Impi</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks a lot. Exactly what I need. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
