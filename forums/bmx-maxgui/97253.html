<!DOCTYPE html><html lang="en" ><head ><title >Side on tab view</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Side on tab view</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >Side on tab view</a><br><br>
<a name="1128614"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I need a tall narrow tab view and I don't like the way multiple tabs bunch up and scroll on the standard tab view.  So, inspired by <a href="/posts.php?topic=84882" target="_blank">this</a>, I've had a go at making my own side tabber..<br><br><img src="http://www.matibee.co.uk/temp/sidetabtest.jpg"><br><br><a href="http://www.matibee.co.uk/temp/sidetab.zip" target="_blank">Source here</a> (it's a first draft so don't expect too much).<br><br>I'm here to ask for your help in making it window re-size friendly.<br><br>It's just a collection of panels and I don't seem to be able to use  SetGadgetLayout to correctly control the width and height after a re-size.<br><br>I'm not planning on adding the ability to scroll through the tabs when the window is too small in height, instead I might stick it in a scroll panel for when it reaches a set minimum size.<br><br>I'm prepared to recalculate all the panel positions if that's what it takes, but this also has the drawback that if it's done on a WINDOW_SIZE message, it will be distorted while the window is being re-sized.<br><br>Any help greatly appreciated, and the final version will go into the code archives.<br><br>Cheers<br>Matt <br><br></td></tr></table><br>
<a name="1128692"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Quick update.<br><br>Looking more into JoshK's source, I've added an event hook and adjusting the position of the panels on the fly.  It's not finished, but so far so good :) <br><br></td></tr></table><br>
<a name="1128696"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jsp</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> For the layout, copy the stuff over I added:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict 
Import maxgui.drivers

Incbin "incbin//sidetab_leftedge.png"
Incbin "incbin//sidetab_rightedge.png"
Incbin "incbin//sidetab_topedge.png"
Incbin "incbin//sidetab_btmedge.png"

Incbin "incbin//sidetab_coltop_active.png"
Incbin "incbin//sidetab_coltop_inactive.png"

Incbin "incbin//sidetab_colleft_active.png"
Incbin "incbin//sidetab_colleft_inactive.png"

Incbin "incbin//sidetab_colmidtop_active.png"
Incbin "incbin//sidetab_colmidbtm_active.png"

Incbin "incbin//sidetab_colmid_inactive.png"

Incbin "incbin//sidetab_colbtm_active.png"
Incbin "incbin//sidetab_colbtm_inactive.png"

Incbin "incbin//sidetab_corner_tl.png"
Incbin "incbin//sidetab_corner_tr.png"
Incbin "incbin//sidetab_corner_bl.png"
Incbin "incbin//sidetab_corner_br.png"

'----------------------------------------------------------------------------------------------------------------------
Type sideTabPixmapPair
	Field activePixmap:TPixmap
	Field inactivePixmap:TPixmap
End Type 
'----------------------------------------------------------------------------------------------------------------------
Type sideTab
	
	Global stImages:TPixmap[17]

	Global STIC_LEFT_EDGE%		= 	0
	Global STIC_RIGHT_EDGE%		= 	1
	Global STIC_TOP_EDGE%		= 	2
	Global STIC_BTM_EDGE%		= 	3
	Global STIC_COL_TOP_ACTIVE%	= 	4
	Global STIC_COL_TOP_INACTIVE% = 	5
	Global STIC_COL_LEFT_ACTIVE%	= 	6
	Global STIC_COL_LEFT_INACTIVE% = 	7
	Global STIC_COL_MID_TOP_ACTIVE% = 	8
	Global STIC_COL_MID_BTM_ACTIVE% = 	9
	Global STIC_COL_MID_INACTIVE%	= 	10
	Global STIC_COL_BTM_ACTIVE% 	= 	11
	Global STIC_COL_BTM_INACTIVE% = 	12
	Global STIC_TL_CORNER%		=	13
	Global STIC_TR_CORNER%		=	14
	Global STIC_BL_CORNER%		=	15
	Global STIC_BR_CORNER%		= 	16
	
	Global borderSize:Int
	Global tabWidth:Int

	Field parent:TGadget
	Field xPos:Int
	Field yPos:Int
	Field currentTab:Int = -1
	Field leftBorderPanel:TGadget
	Field tabs:TList
	Field tabPanels:TList
	Field tabPixmaps:TList
	Field client:TGadget
	
	'-----------------------------------------------------------------------------------------------------------------	
	Function create:sideTab( parent:TGadget, X:Int, Y:Int, Width:Int, Height:Int, r:Int, g:Int, b:Int ) 
		
		Local st:sideTab = New sideTab
		st.parent = parent

		st.tabs = New TList 
		st.tabPanels = New TList
		st.tabPixmaps = New TList 
		st.xPos = X
		st.yPos = Y
		
		If ( stImages[0] = Null )
			LoadImages()
		End If 
		
		Local x2:Int = X + tabWidth		' start of first corner
		Local x3:Int = x2 + borderSize	' start of client area
		Local x4:Int = X + Width - bordersize	' end of client area
		
		Local y2:Int = Y + borderSize		' start of client area
		Local y3:Int = Height - bordersize
		
		Local cliHeight:Int = Height - bordersize * 2
		Local cliWidth:Int = Width - tabWidth - bordersize * 2
		
		
		Local p:Tgadget = CreatePanel( x2, Y, borderSize, borderSize, parent ) ' top left corner
		SetGadgetLayout( p, 1,0,1,0 )
		SetGadgetPixmap( p, stImages[STIC_TL_CORNER] )
				
		p = CreatePanel( x3, Y, cliWidth, bordersize, parent ) ' top border
		SetGadgetLayout( p, 1,1,1,0 )
		SetGadgetPixmap( p, stImages[STIC_TOP_EDGE] )
				
		p = CreatePanel( x4, Y, bordersize, bordersize, parent ) ' top right corner
		SetGadgetLayout( p, 0,1,1,0 )
		SetGadgetPixmap( p, stImages[STIC_TR_CORNER] )
				
		st.client = CreatePanel( x3, y2, cliWidth, cliHeight, parent ) ' client area
		SetGadgetLayout( st.client, 1,1,1,1 )
		'SetPanelColor( st.client, r, g, b )
		'SetPanelColor( st.client, 128, 0, 0 )
				
		st.leftBorderPanel = CreatePanel( x2, y2, bordersize, cliHeight, parent ) ' left border
		SetGadgetLayout( st.leftBorderPanel, 1,0,1,1 )
		SetGadgetPixmap( st.leftBorderPanel, stImages[STIC_LEFT_EDGE] )
		'SetPanelColor( st.leftBorderPanel , 128, 0, 0 )
		
		p = CreatePanel( x4, y2, bordersize, cliHeight, parent ) ' rught border
		SetGadgetLayout( p, 0,1,1,1 )
		SetGadgetPixmap( p, stImages[STIC_RIGHT_EDGE] )
		
		p = CreatePanel( x2, y3, bordersize, bordersize, parent ) ' bottom left corner
		SetGadgetLayout( p, 1,0,0,1 )
		SetGadgetPixmap( p, stImages[STIC_BL_CORNER] )

		p = CreatePanel( x4, y3, bordersize, bordersize, parent ) ' bottom right corner
		SetGadgetLayout( p, 0,1,0,1 )
		SetGadgetPixmap( p, stImages[STIC_BR_CORNER] )
		
		p = CreatePanel( x3, y3, cliWidth, bordersize, parent ) ' bottom border
		SetGadgetLayout( p, 1,1,0,1 )
		SetGadgetPixmap( p, stImages[STIC_BTM_EDGE] )
		
		Return st
	End Function 
	'-----------------------------------------------------------------------------------------------------------------	
	Function LoadImages()
		stImages[STIC_LEFT_EDGE] = LoadPixmap( "incbin::incbin//sidetab_leftedge.png" )
		stImages[STIC_RIGHT_EDGE] = LoadPixmap( "incbin::incbin//sidetab_rightedge.png" )
		stImages[STIC_TOP_EDGE] = LoadPixmap( "incbin::incbin//sidetab_topedge.png" )
		stImages[STIC_BTM_EDGE] = LoadPixmap( "incbin::incbin//sidetab_btmedge.png" )
		stImages[STIC_COL_TOP_ACTIVE] = LoadPixmap( "incbin::incbin//sidetab_coltop_active.png" )
		stImages[STIC_COL_TOP_INACTIVE] = LoadPixmap( "incbin::incbin//sidetab_coltop_inactive.png" )
		stImages[STIC_COL_LEFT_ACTIVE] = LoadPixmap( "incbin::incbin//sidetab_colleft_active.png" )
		stImages[STIC_COL_LEFT_INACTIVE] = LoadPixmap( "incbin::incbin//sidetab_colleft_inactive.png" )
		stImages[STIC_COL_MID_TOP_ACTIVE] = LoadPixmap( "incbin::incbin//sidetab_colmidtop_active.png" )
		stImages[STIC_COL_MID_BTM_ACTIVE] = LoadPixmap( "incbin::incbin//sidetab_colmidbtm_active.png" )
		stImages[STIC_COL_MID_INACTIVE] = LoadPixmap( "incbin::incbin//sidetab_colmid_inactive.png" )
		stImages[STIC_COL_BTM_ACTIVE] = LoadPixmap( "incbin::incbin//sidetab_colbtm_active.png" )
		stImages[STIC_COL_BTM_INACTIVE] = LoadPixmap( "incbin::incbin//sidetab_colbtm_inactive.png" )
		stImages[STIC_TL_CORNER] = LoadPixmap( "incbin::incbin//sidetab_corner_tl.png" )
		stImages[STIC_TR_CORNER] = LoadPixmap( "incbin::incbin//sidetab_corner_tr.png" )
		stImages[STIC_BL_CORNER] = LoadPixmap( "incbin::incbin//sidetab_corner_bl.png" )
		stImages[STIC_BR_CORNER] = LoadPixmap( "incbin::incbin//sidetab_corner_br.png" )
		borderSize = PixmapWidth( stImages[STIC_TL_CORNER] ) ' borders must be square
		tabWidth = PixmapWidth( stImages[STIC_COL_BTM_ACTIVE] ) - borderSize
	End Function 
	'-----------------------------------------------------------------------------------------------------------------	
	Method AddTab( activePixmap:TPixmap, inactivePixmap:TPixmap )
		If ( currentTab &lt; 0 ) currentTab = 0
		Local p:TGadget
		Local yStart:Int = yPos + borderSize
		For Local p:TGadget = EachIn tabPanels
			yStart :+ GadgetHeight( p )
		Next 
		Local yEnd:Int = yStart + borderSize
		If ( tabPanels.Count() = 0 )
			p = CreatePanel( xPos, yStart, tabwidth + borderSize, borderSize, parent )
					SetGadgetLayout( p, 1,0,1,0 )

					'SetPanelColor( p , 0, 0, 196 )

			tabPanels.AddLast( p )
		Else 
			yStart :- borderSize
			yEnd :- borderSize
		End If 
		p = CreatePanel( xPos, yStart + bordersize, bordersize, PixmapHeight( activePixmap ), parent )
					SetGadgetLayout( p, 1,0,1,10)
					'SetPanelColor( p , 0, 0, 196 )
		yEnd :+ PixmapHeight( activePixmap )
		tabPanels.AddLast( p )
		
		Local pp:sideTabPixmapPair = New sideTabPixmapPair
		pp.activePixmap = activePixmap
		pp.inactivePixmap = inactivePixmap
		tabPixmaps.AddLast( pp )
		
		p = CreatePanel( xpos, yStart + borderSize + PixmapHeight( activePixmap ), tabWidth + borderSize, ..
						borderSize, parent )
					SetGadgetLayout( p, 1,0,1,0 ) '??????????????????????
					'SetPanelColor( p , 0, 0, 196 )
		yEnd :+ borderSize
		tabPanels.AddLast( p )
		p = CreatePanel( xpos + borderSize, yStart + borderSize, tabWidth + bordersize, ..
						PixmapHeight( activePixmap ), parent, PANEL_ACTIVE  )
					SetGadgetLayout( p, 1,0,1,0 )
					'SetPanelColor( p , 0, 0, 196 )
		
		If ( tabs.Count() = 0 )
			SetPanelPixmap( p, activePixmap )
		Else 
			SetPanelPixmap( p, activePixmap )
		End If 
		tabs.AddLast( p )
		
		SetGadgetShape( leftBorderPanel, xpos + tabWidth, yEnd, borderSize, ..
						yPos + GadgetHeight( client ) - yEnd + borderSize )
		
		AdjustActiveImages()
	End Method 
	'-----------------------------------------------------------------------------------------------------------------	
	Method AdjustActiveImages()
		Local tab:Int = 0
		Local c:Int = 0
		Local lasttab:Int = tabs.Count() - 1
		For Local p:TGadget = EachIn tabPanels
			Select c
			Case 0
				If tab = 0
					If currentTab = tab
						SetPanelPixmap( p, stImages[STIC_COL_TOP_ACTIVE] )
					Else 
						SetPanelPixmap( p, stImages[STIC_COL_TOP_INACTIVE] )
					End If 
				End If 
			Case 1
				If currentTab = tab
					SetPanelPixmap( p, stImages[STIC_COL_LEFT_ACTIVE] )
				Else 
					SetPanelPixmap( p, stImages[STIC_COL_LEFT_INACTIVE] )
				End If 
			Case 2
				If currentTab = tab
					If ( tab = lasttab )
						SetPanelPixmap( p, stImages[STIC_COL_BTM_ACTIVE] )
					Else 
						SetPanelPixmap( p, stImages[STIC_COL_MID_BTM_ACTIVE] )
					End If 
				Else If currentTab = tab + 1
					SetPanelPixmap( p, stImages[STIC_COL_MID_TOP_ACTIVE] )
				Else 
					If ( tab &lt; lasttab )
						SetPanelPixmap( p, stImages[STIC_COL_MID_INACTIVE] )
					Else 
						SetPanelPixmap( p, stImages[STIC_COL_BTM_INACTIVE] )
					End If 
				End If 	
			End Select 
			
			c :+ 1
			If ( c = 3 ) 
				c = 1
				tab :+ 1
			End If 
			
		Next 	
		Local i:Int = 0
		For Local p:TGadget = EachIn tabs
			If ( currentTab = i )
				SetGadgetPixmap( p, sideTabPixmapPair(tabPixmaps.ValueAtIndex( i )).activePixmap )
			Else 
				SetGadgetPixmap( p, sideTabPixmapPair(tabPixmaps.ValueAtIndex( i )).inactivePixmap )
			End If 
			i :+ 1
		Next 
	End Method 
	'-----------------------------------------------------------------------------------------------------------------	
	Method Poll( e:TEvent )
		Local i:Int = 0
		For Local p:TGadget = EachIn tabs
			If EventSource() = p
				Select EventID()
				Case EVENT_MOUSEDOWN
					ActivateTab( i )	
				End Select 
			End If 
			i :+ 1
		Next 
	End Method 
	'-----------------------------------------------------------------------------------------------------------------	
	Method ActivateTab( tabID:Int )
		currentTab = tabID
		AdjustActiveImages()
		Local e:TEvent = New TEvent
		e.id = EVENT_GADGETACTION
		e.source = Self 
		e.data = tabID		
		PostEvent( e )
	End Method 
	'-----------------------------------------------------------------------------------------------------------------	
	Method ActiveTab:Int( )
		Return currentTab
	End Method 
	'-----------------------------------------------------------------------------------------------------------------	
	Method ClientArea:TGadget()
		Return client
	End Method
	'-----------------------------------------------------------------------------------------------------------------	
	Method AdjustLayout()
		
	End Method 
	
End Type 
		

'----------------------------------------------------------------------------------------------------------------------
' DEMO
'----------------------------------------------------------------------------------------------------------------------
Global window:TGadget = CreateWindow( "Side tab test", 400, 200, 600, 600, Null, ..
								WINDOW_TITLEBAR | WINDOW_RESIZABLE | WINDOW_CLIENTCOORDS )
SetMinWindowSize( window, 600, 600 )
						
								
Global st:sideTab = sideTab.create( window, 4, 4, 290, 550, 255, 255, 255 )
st.AddTab( LoadPixmap( "incbin//sidetab_label_active.png" ), LoadPixmap( "incbin//sidetab_label_inactive.png" )  )
st.AddTab( LoadPixmap( "incbin//sidetab_label_active.png" ), LoadPixmap( "incbin//sidetab_label_inactive.png" )  )
st.AddTab( LoadPixmap( "incbin//sidetab_label_active.png" ), LoadPixmap( "incbin//sidetab_label_inactive.png" )  )
st.AddTab( LoadPixmap( "incbin//sidetab_label_active.png" ), LoadPixmap( "incbin//sidetab_label_inactive.png" )  )
st.AddTab( LoadPixmap( "incbin//sidetab_label_active.png" ), LoadPixmap( "incbin//sidetab_label_inactive.png" )  )

Global tabViews:TGadget[5]
Global lbl:Tgadget
tabViews[0] = CreatePanel( 0, 0, ClientWidth( st.ClientArea() ), ClientHeight( st.ClientArea() ), st.ClientArea() )
					SetGadgetLayout( tabViews[0], 1,1,1,1 )

lbl=CreateLabel( "Tab 1", 0, 0, 40, 20, tabViews[0] )
SetGadgetLayout( lbl, 1,0,1,0 )

tabViews[1] = CreatePanel( 0, 0, ClientWidth( st.ClientArea() ), ClientHeight( st.ClientArea() ), st.ClientArea() )
					SetGadgetLayout( tabViews[1], 1,1,1,1 )

lbl=CreateLabel( "Tab 2", 0, 0, 40, 20, tabViews[1] )
SetGadgetLayout( lbl, 1,0,1,0 )

tabViews[2] = CreatePanel( 0, 0, ClientWidth( st.ClientArea() ), ClientHeight( st.ClientArea() ), st.ClientArea() )
					SetGadgetLayout( tabViews[2], 1,1,1,1 )

lbl=CreateLabel( "Tab 3", 0, 0, 70, 20, tabViews[2] )
SetGadgetLayout( lbl, 1,0,1,0 )

tabViews[3] = CreatePanel( 0, 0, ClientWidth( st.ClientArea() ), ClientHeight( st.ClientArea() ), st.ClientArea() )
					SetGadgetLayout( tabViews[3], 1,1,1,1 )

lbl=CreateLabel( "Tab 4", 0, 0, 40, 20, tabViews[3] )
SetGadgetLayout( lbl, 1,0,1,0 )

tabViews[4] = CreatePanel( 0, 0, ClientWidth( st.ClientArea() ), ClientHeight( st.ClientArea() ), st.ClientArea() )
					SetGadgetLayout( tabViews[4], 1,1,1,1 )

lbl=CreateLabel( "Tab 5", 0, 0, 70, 20, tabViews[4] )
SetGadgetLayout( lbl, 1,0,1,0 )

For Local i:Int = 1 To 4
	HideGadget( tabViews[i] )
Next 

Repeat
	WaitEvent()
	st.Poll( CurrentEvent )
	Select EventID()
		Case EVENT_GADGETACTION
			Select EventSource()
			Case st
				For Local i:Int = 0 To 4
					HideGadget( tabviews[i] )
				Next
				ShowGadget( tabviews[ EventData() ] )
			End Select 		
		Case EVENT_WINDOWCLOSE
			End
		Case event_WINDOWSIZE
			st.AdjustLayout()
	EndSelect
Forever

			
			
		

</textarea> <br><br></td></tr></table><br>
<a name="1128697"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Perfect!<br><br>And you did it all with SetGadgetLayout? I was convinced that wasn't even possible.  <br><br>Great job, thanks. <br><br></td></tr></table><br>
<a name="1128698"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jsp</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Biggest problem was to figure out which panel was meant, thus some SetGadgetColor inside the code to identify them :) <br><br></td></tr></table><br>
<a name="1128707"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Agreed, the code's a bit of a mess, but it was really a proof of concept at that stage.<br><br>Thanks to you, I'm now making it safe for human consumption ;) <br><br></td></tr></table><br>
<a name="1128715"></a>

<a name="1128716"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Okay, everything updated for a final check over before I drop it in the code archives.<br><br>It now has its own event handler, and managed panels so it acts kind of autonomously (no more Poll() or switching views required).<br><br>Original image and download overwritten.<br><br>Thanks again jsp.  I really hope this community stays around for a while yet.  BMax is a great language but the lack of activity here does worry me.<br><br>If anyone wants to test on other OS's and post a screen shot I'd be really grateful (the above pic from WinXP).<br><br>Cheers<br>Matt<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1128718"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jsp</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Works fine here and looks quite cool!<br>Well done. <br><br></td></tr></table><br>
<a name="1128801"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks again jsp.  A code archive entry submitted, and the download zip now contains the photoshop doc I made and took the sample images from.<br><br>Looking at your additions I've realised why I was having no luck with setgadgetlayout :/  My experience of setgadgetlayout was limited to using EDGE_ALIGNED on all four sides and I always assumed the arguments were left,top,right,bottom (as in x,y,w,h). Doh! <br><br></td></tr></table><br>
<a name="1128809"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamRedwoods</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Very nice!<br><br>does this also support other orientations (ie horizontal alignment) in a top and bottom arrangement? <br><br></td></tr></table><br>
<a name="1128814"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Other orientations?? :)  No, I'm afraid not.  I simply needed this orientation for my app and wasn't really planning on taking it any further.<br><br>A mirrored version should be easy enough (ie tabs on the right), but top and bottom tabs would require a bit of work.  My biggest fear is that its limitations will quickly become a major PITA if it's taken out of its comfort zone. <br><br></td></tr></table><br>
<a name="1129082"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well...  I've just tried this on my fresh Ubuntu install :/<br><br>It looks like FLTK requires gadgets to start and end on certain pixel boundaries (say 8x8) or power of 2, so it's stretching all the panels.  Does anyone know if that's usual?  Going back to windows now with the fltk drivers to see what happens. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
