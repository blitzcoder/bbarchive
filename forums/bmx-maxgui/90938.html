<!DOCTYPE html><html lang="en" ><head ><title >Menus not working in Linux...</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Menus not working in Linux...</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >Menus not working in Linux...</a><br><br>
<a name="1034874"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jondecker76</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm starting on a small skin editor for a program that I have just made.  I started programming it in windows and everything worked fine. However, when I build it in Linux, the Menus don't show up.<br><br>Below is the code so far...  Is this a bug?<br><br><pre class=code>
SuperStrict
Import MaxGui.Drivers

Const MENU_NEW:Int=101
Const MENU_OPEN:Int=102
Const MENU_SAVE:Int=103
Const MENU_SAVE_AS:Int=104
Const MENU_CLOSE:Int=105
Const MENU_EXIT:Int=106

Const MENU_ABOUT:Int=201

'Make the window
Global win:TGadget=CreateWindow("Skin Editor",0,0,640,600,Null,WINDOW_TITLEBAR|WINDOW_MENU|WINDOW_STATUS|WINDOW_CENTER|WINDOW_ACCEPTFILES)

'Make the file menu
Global filemenu:TGadget=CreateMenu("&amp;File",0,win)
CreateMenu("&amp;New",MENU_NEW,filemenu)
CreateMenu("&amp;Open",MENU_OPEN,filemenu)
CreateMenu("&amp;Save",MENU_SAVE,filemenu)
CreateMenu("Save &amp;As",MENU_SAVE_AS,filemenu)
CreateMenu("&amp;Close",MENU_CLOSE,filemenu)
CreateMenu("E&amp;xit",MENU_EXIT,filemenu)

'Make the help menu
Global helpmenu:TGadget=CreateMenu("&amp;Help",0,win)
CreateMenu("&amp;About",MENU_ABOUT,helpmenu)

'Now update the window to use the menus
UpdateWindowMenu(win)

Global tabber:TGadget=CreateTabber(0,0,ClientWidth(win),ClientHeight(win),win)
SetGadgetLayout(tabber,1,1,1,1)
AddGadgetItem(tabber,"General",False,-1,"General skin properties")
AddGadgetItem(tabber,"Vending",False,-1,"Options for the Vending screen")
AddGadgetItem(tabber,"Waiting",False,-1,"Options for the Waiting screen")
AddGadgetItem(tabber,"Countdown",False,-1,"Options for the Countdown")
AddGadgetItem(tabber,"Photograph",False,-1,"Options for the Photograph screen")
AddGadgetItem(tabber,"Developing",False,-1,"Options for the Developing screen")
AddGadgetItem(tabber,"Review",False,-1,"Options for the Review screen")
AddGadgetItem(tabber,"Gallery",False,-1,"OPtions for the Gallery screen")
AddGadgetItem(tabber,"Thankyou",False,-1,"Options for the Thankyou screen")
AddGadgetItem(tabber,"Preview",False,-1,"Preview this skin")
Global GENERAL:Int=0
Global VENDING:Int=1
Global WAITING:Int=2
Global COUNTDOWN:Int=3
Global PHOTOGRAPH:Int=4
Global DEVELOPING:Int=5
Global REVIEW:Int=6
Global GALLERY:Int=7
Global THANKYOU:Int=8
Global PREVIEW:Int=9

Global TabPanel:TGadget[11]
Global CurrentTabPanel:TGadget
TabPanel[GENERAL]=CreateTabPanel(tabber)
TabPanel[VENDING]=CreateTabPanel(tabber)
TabPanel[WAITING]=CreateTabPanel(tabber)
TabPanel[COUNTDOWN]=CreateTabPanel(tabber)
TabPanel[PHOTOGRAPH]=CreateTabPanel(tabber)
TabPanel[DEVELOPING]=CreateTabPanel(tabber)
TabPanel[REVIEW]=CreateTabPanel(tabber)
TabPanel[GALLERY]=CreateTabPanel(tabber)
TabPanel[THANKYOU]=CreateTabPanel(tabber)
TabPanel[PREVIEW]=CreateTabPanel(tabber)
'===========
'GENERAL TAB
'===========
Local lblPosx:Int=10
Local lblPosy:Int=400
Local lblWidth:Int=175
Local lblHeight:Int=20
Local txtPosx:Int=175
Local txtPosY:Int=400
Local txtWidth:Int=330
Local txtHeight:Int=20
Local btnPosx:Int=500
Local btnPosy:Int=400
Local btnWidth:Int=50
Local btnHeight:Int=20
Local vertOffset:Int=20
Global lblBackgroundImage:TGadget=CreateLabel("Background Image:",lblPosx,lblPosy,lblWidth,lblHeight,TabPanel[GENERAL])
Global txtBackgroundImage:TGadget=CreateTextField(txtPosX,txtPosy,txtWidth,txtHeight,TabPanel[GENERAL])
Global btnGeneralBackground:TGadget=CreateButton("Browse",btnPosX,btnPosy,btnWidth,btnHeight,TabPanel[GENERAL])
lblPosy=lblPosy+vertOffset
txtPosY=txtPosy+vertOffset
btnPosY=btnPosy+vertOffset
Global lblSavepathOriginals:TGadget=CreateLabel("Save Original Photos To:",lblPosx,lblPosy,lblWidth,lblHeight,TabPanel[GENERAL])
Global txtSavePathOriginals:TGadget=CreateTextField(txtPosX,txtPosy,txtWidth,txtHeight,TabPanel[GENERAL])
Global btnSavePathOriginals:TGadget=CreateButton("Browse",btnPosX,btnPosy,btnWidth,btnHeight,TabPanel[GENERAL])
lblPosy=lblPosy+vertOffset
txtPosY=txtPosy+vertOffset
btnPosY=btnPosy+vertOffset
Global lblSavePathPrintouts:TGadget=CreateLabel("Save Printout Photos To:",lblPosx,lblPosy,lblWidth,lblHeight,TabPanel[GENERAL])
Global txtSavePathPrintouts:TGadget=CreateTextField(txtPosX,txtPosy,txtWidth,txtHeight,TabPanel[GENERAL])
Global btnSavePathPrintouts:TGadget=CreateButton("Browse",btnPosX,btnPosy,btnWidth,btnHeight,TabPanel[GENERAL])
lblPosy=lblPosy+vertOffset
txtPosY=txtPosy+vertOffset
btnPosY=btnPosy+vertOffset
Global lblNumPics:TGadget=CreateLabel("Number of pictures:",lblPosx,lblPosy,lblWidth,lblHeight,TabPanel[GENERAL])
Global txtNumPics:TGadget=CreateTextField(txtPosX,txtPosy,txtWidth/8,txtHeight,TabPanel[GENERAL])
lblPosy=lblPosy+vertOffset
txtPosY=txtPosy+vertOffset
btnPosY=btnPosy+vertOffset
Global lblEnableVending:TGadget=CreateLabel("Enable Vending Mode:",lblPosx,lblPosy,lblWidth,lblHeight,TabPanel[GENERAL])
Global btnEnableVending:TGadget=CreateButton("",txtPosX,btnPosy,btnWidth,btnHeight,TabPanel[GENERAL],BUTTON_CHECKBOX)

'Global lblMessageBGColor:TGadget=CreateLabel()
'===========
'VENDING TAB
'===========
Global lblMessage:TGadget[11]
Global txtMessage:TGadget[11]

Global lblAudio:TGadget[11]
Global txtAudio:TGadget[11]
Global btnAudio:TGadget[11]
lblMessage[VENDING]=CreateLabel("On Screen Message:",10,300,150,20,TabPanel[VENDING])
txtMessage[VENDING]=CreateTextField(160,300,330,20,TabPanel[VENDING])
lblAudio[VENDING]=CreateLabel("Audio File:",10,320,330,20,TabPanel[VENDING])
txtAudio[VENDING]=CreateTextField(160,320,330,20,TabPanel[VENDING])
Global canPreview:TGadget=CreateCanvas(0,0,600,400,TabPanel[VENDING])

CreateTimer 30

Init()

While WaitEvent()
	Select EventID()
	Case EVENT_TIMERTICK
			RedrawGadget canPreview
UpdateWindowMenu(win)
	Case EVENT_GADGETACTION
		If EventSource()=tabber
			HideGadget currentTabPanel
			currentTabPanel=TabPanel[EventData()]
			ShowGadget currentTabPanel
			'If TabPanel[EventData()]&lt;&gt;TabPanel[GENERAL]
				
				
			'Else
			'	HideGadget(canPreview)
			'EndIf
		EndIf
		RedrawGadget(canPreview)
		Case EVENT_GADGETPAINT
			SetGraphics CanvasGraphics(canPreview)
			SetOrigin 0,0
			SetLineWidth 5
			Cls
			Local t:Int=MilliSecs()
			DrawLine 0,0,120*Cos(t),120*Sin(t)
			DrawLine 0,0,80*Cos(t/60),80*Sin(t/60)

			Flip
	Case EVENT_WINDOWCLOSE
		' Quit
		End
	Case EVENT_WINDOWACCEPT
		'A File was dragged in!  Lets do it!
	Case EVENT_MENUACTION
		' Menu stuff
		Select EventData()
		Case MENU_EXIT
			End
		Case MENU_ABOUT
			Notify "Photobooth skin editor"
		End Select
	Default
		'None of the above....
		'RuntimeError CurrentEvent.toString()
	End Select
Wend


Function Init()
	CurrentTabPanel=TabPanel[GENERAL]
	ShowGadget CurrentTabPanel
	'HideGadget(canPreview)
End Function
Function CreateTabPanel:TGadget(thisTabber:TGadget)
	Local	panel:TGadget
	panel=CreatePanel(0,0,ClientWidth(thisTabber),ClientHeight(thisTabber),thisTabber)
	SetGadgetLayout panel,1,1,1,1
	HideGadget panel
	Return panel
End Function

</pre> <br><br></td></tr></table><br>
<a name="1034880"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> You should parent root window menu to the menu returned by the <b>WindowMenu()</b> function.  Iirc, the docs for <b>CreateMenu()</b> do explain this.  The fact that it works on Windows / Cocoa is more a lucky circumstance.  E.g. the following line...<br><pre class=code>Global filemenu:TGadget=CreateMenu("&amp;File",0,win)</pre>...should be...<br><pre class=code>Global filemenu:TGadget=CreateMenu("&amp;File",0,WindowMenu(win) )</pre>..and similarly for your 'Help' menu. <br><br></td></tr></table><br>
<a name="1034887"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jondecker76</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> thankyou, I must have missed it in the documentation.<br><br>Works great now! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
