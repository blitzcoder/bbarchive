<!DOCTYPE html><html lang="en" ><head ><title >Best Practice for Handling Multiple Windows</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Best Practice for Handling Multiple Windows</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >Best Practice for Handling Multiple Windows</a><br><br>
<a name="1134705"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jayjay</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi all<br><br>Thanks in advance for any help. I have been away a while but have returned to be a bmax faithful!<br><br>I want to create an application that uses several windows (at different times) each will have a canvas for drawing and buttons etc!<br><br>Which is the best way to handle multiple window control from the main program? I understand there are several ways of doing this..the sample in the tutorials which creates an applet leaves the program in the applet event hook, and it seems control is not passed back to the main loop???<br><br>Any ideas greatly appreciated as this is really holding me up! <br><br></td></tr></table><br>
<a name="1134718"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Code your window as its own type and have it register an event hook.  You can then leave it to its own devices.<br><br>When you want the window to command the main application to do something, post a user event for your main app (or any other, interested event handler ) to act upon.<br><br>Here's the source to one of my little dialog window types.  You won't be able to compile it but you can see it's a self contained unit.  When the user hits the ok button it posts a user defined event which the main app picks up.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict 

Import maxgui.drivers
Import "core/3dtypes.bmx"
Import "core/floatUtils.bmx"
Import "core/muParserUtils.bmx"

Import "events.bmx"

'-----------------------------------------------------------------------------------------------------------------
'-----------------------------------------------------------------------------------------------------------------
Type ToolOriginDialog
	
	Global tWindow:TGadget

	Global buttOK:TGadget
	Global buttCancel:TGadget
	
	Global xEdit:TGadget
	Global yEdit:TGadget
	Global zEdit:TGadget
	
	'------------------------------------------------------------------------------------------------------------
	Function Init( window:TGadget )

		Local winwidth:Int = 300
		Local winheight:Int = 120
		
		tWindow = CreateWindow( "TOOL ORIGIN", 0, 0, winwidth, winheight, window, ..
									WINDOW_TITLEBAR | WINDOW_CENTER | WINDOW_TOOL | WINDOW_HIDDEN )
		
		Local cw:Int = ClientWidth( tWindow )
		Local ch:Int = ClientHeight( tWindow )
		
		xEdit = CreateTextField( 8, 20, 60, 21, tWindow )
		SetGadgetFilter( xEdit, muParse_GadgetFilter )
		yEdit = CreateTextField( 72, 20, 60, 21, tWindow )
		SetGadgetFilter( yEdit, muParse_GadgetFilter )
		zEdit = CreateTextField( 136, 20, 60, 21, tWindow )
		SetGadgetFilter( zEdit, muParse_GadgetFilter )		
				
		buttOK = CreateButton( "Apply", 20, ch - 30, 70, 25, tWindow )
		buttCancel = CreateButton( "Cancel", 110, ch - 30, 70, 25, tWindow )

		AddHook EmitEventHook, eventHandler, Null, 1

	End Function 
	'-----------------------------------------------------------------------------------------------------------------
	Function eventHandler:Object( pID%, pData:Object, pContext:Object )
		Local event:TEvent = TEvent(pData)
		If ( event &lt;&gt; Null )
			Select event.id
				Case EVENT_GADGETACTION
					Select event.source
						Case buttCancel
							HideGadget( tWindow )
						Case buttOK
							PostEvent( CreateEvent( UEVENT_TOOLDATUM_SET ) )
							HideGadget( tWindow )
					End Select 
				Case EVENT_WINDOWCLOSE
					Select event.source
					Case tWindow
						HideGadget( tWindow )
				     End Select 
			End Select 
		End If 
		Return pData
	EndFunction
	'------------------------------------------------------------------------------------------------------------
	Function Show( v:Vector3d )
		SetGadgetText( xEdit, truncateFloat( v.x ) )
		SetGadgetText( yEdit, truncateFloat( v.y ) )
		SetGadgetText( zEdit, truncateFloat( v.z ) )
		ShowGadget( tWindow )
	End Function 
	'------------------------------------------------------------------------------------------------------------
	Function Position:Vector3d()
		Local v:Vector3d = New Vector3d
		v.x = muParseGadget( xEdit, "Invalid X Value" )
		v.y = muParseGadget( yEdit, "Invalid Y Value" )
		v.z = muParseGadget( zEdit, "Invalid Z Value" )
		Return v
	End Function 
	'------------------------------------------------------------------------------------------------------------
	

End Type 

</textarea> <br><br></td></tr></table><br>
<a name="1134722"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jayjay</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Matibee<br><br>Thats is just what I am looking for but could you give me an example main app event loop that would pick up the event returned from the okay button?<br><br>Really appreciate your help! <br><br></td></tr></table><br>
<a name="1134724"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> The main app event loop will look like any other - check out the samples.<br><br>After the main window is created, and before the app starts its message processing loop, I need to call ToolDialog.Init() with the main app window (so it knows who its parent is.)<br><br>The user event is 'fired' (rather than 'returned') when the user hits the ok button (see code line 56 above).  As the main app window is still going through its own message loop it'll be picked up there.<br><br>The main app message processing loop might be something like this...<br><br><pre class=code>
While True
	WaitEvent()

	Select EventID()
		Case EVENT_GADGETACTION
			Select EventSource()
				Case toolOriginBtn
					ToolOriginDialog.Show()
			End Select 	
		
		Case UEVENT_TOOLDATUM_SET
			TProject.SetToolDatum( ToolOriginDialog.Position() )
		
		Case EVENT_WINDOWCLOSE
			Select EventSource()
				Case window
					MenuAction_OnExit() ' does same thing
			End Select 
			
		Case EVENT_GADGETPAINT
			OnGadgetPaint()
			
		Case EVENT_WINDOWSIZE
			If EventSource() = window
				SetViewport( 0, 0, window.ClientWidth(), window.ClientHeight() )
			End If 
	EndSelect
	
Wend
</pre><br><br>User events are just const integer values, and should start with the brl defined user event mask, so the id used above was defined like this..<br><br><pre class=code>
Global UEVENT_TOOLDATUM_SET%	 		= EVENT_USEREVENTMASK + $0300
</pre><br><br>I must admit there are methods available to register user event ids rather than defining them adhoc, but I've had no issues doing it this way. <br><br></td></tr></table><br>
<a name="1134771"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jsp</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> To use several windows on one program it is not mandatory to use hooks. While hooks are most of the time useful it is not a must!<br>Just define all windows you need as hidden and have all gadgets in your main WaitEvent() loop.<br>Then show or hide only those windows you need at a certain time. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
