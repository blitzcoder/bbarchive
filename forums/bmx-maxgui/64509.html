<!DOCTYPE html><html lang="en" ><head ><title >Canvas simultaneous keypresses</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Canvas simultaneous keypresses</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >Canvas simultaneous keypresses</a><br><br>
<a name="720106"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >impixi</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I was wondering how others are handling multiple, <b>simultaneous</b> keystrokes on a MaxGUI canvas.<br><br>Here's my attempt:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'A key-handling test for multiple, simultaneous key presses on a MaxGUI canvas.

'Blocks marked with asterisks(*) highlight relevent code.

SuperStrict

Global MainWindow:TGadget = CreateWindow("Simultaneous Key Press Test - MaxGUI Canvas.", 10, 10, 800, 600, Null, WINDOW_TITLEBAR | WINDOW_RESIZABLE)
Global GameCanvas:TGadget = CreateCanvas(0, 0, 0, 0, MainWindow)
Global GameCanvas_Width:Int 
Global GameCanvas_Height:Int
resizeGameCanvas()

Global PlayerX:Int = 0
Global PlayerY:Int = 0

Global FlipTimer:TTimer = CreateTimer(60)


'*** Key-handling initialisation ***

Const MaxKeys:Byte = 255					'Should equal maximum number of key scancodes. 
											'Non-english keyboards may have a higher value.
											
Global KeysDown:Int[MaxKeys]				'An array 'mapped' to the scancodes.
	
Global KeyTimer:TTimer = CreateTimer(100)	'The rate at which to check the keys.

'***********************************


Repeat

	PollEvent
	
	Select EventID()
	
		Case EVENT_TIMERTICK
		
			Select EventSource()
		
				Case FlipTimer
					RedrawGadget GameCanvas
					
				'*** Call the key handling function when 
				'*** the KeyTimer timer event occurs.
				
				Case KeyTimer
					processKeys()
					
				'****************************************
					
			EndSelect
	
		Case EVENT_GADGETPAINT
			SetGraphics CanvasGraphics(GameCanvas)
			Cls
			SetBlend SOLIDBLEND
			SetColor 255, 255, 255
			DrawText "ARROW keys move, ESC quits, T and E and S together brings up a Notify dialog.", 10, 10
			drawPlayer()
			Flip

		'*** When a key is pressed, set the corresponding 
		'*** array address to True.
		
		Case EVENT_KEYDOWN
			KeysDown[EventData()] = True

		'************************************************


		'*** When a key is released, set the corresponding 
		'*** array address to False.

		Case EVENT_KEYUP
			KeysDown[EventData()] = False

		'************************************************


		Case EVENT_WINDOWSIZE
			resizeGameCanvas()
			checkPlayerPosition()

		Case EVENT_APPRESUME
			ActivateGadget GameCanvas

		Case EVENT_WINDOWCLOSE
			endApp()
	
		Case EVENT_APPTERMINATE
			endApp()

	EndSelect	

Forever


'*** Key handling function

Function processKeys()

	If KeysDown[KEY_RIGHT] Then	PlayerX :+ 10
	If KeysDown[KEY_LEFT] Then PlayerX :- 10
	If KeysDown[KEY_UP] Then PlayerY :- 10
	If KeysDown[KEY_DOWN] Then PlayerY :+ 10
	If KeysDown[KEY_ESCAPE] Then endApp()
		
	checkPlayerPosition()
	
	If KeysDown[KEY_T] And KeysDown[KEY_E] And KeysDown[KEY_S] 
		Notify("Test")
		KeysDown[KEY_T] = False
		KeysDown[KEY_E] = False
		KeysDown[KEY_S] = False
	EndIf

End Function

'*************************


Function drawPlayer()

	SetBlend ALPHABLEND
	SetAlpha 0.75
	SetColor 0, 255, 255
	DrawOval PlayerX - 4, PlayerY - 4, 8, 8

EndFunction

Function resizeGameCanvas()

	SetGraphics CanvasGraphics(GameCanvas)
	SetGadgetShape GameCanvas, 5, 5, ClientWidth(MainWindow) - 10, ClientHeight(MainWindow) - 10
	GameCanvas_Width = GadgetWidth(GameCanvas)
	GameCanvas_Height = GadgetHeight(GameCanvas)
	SetViewport 0, 0, GameCanvas_Width, GameCanvas_Height
	ActivateGadget GameCanvas

EndFunction

Function endApp()

	FreeGadget GameCanvas		
	End

EndFunction

Function checkPlayerPosition()

	If PlayerX &gt;= GameCanvas_Width Then PlayerX = GameCanvas_Width
	If PlayerX &lt; 0 Then PlayerX = 0
	If PlayerY &lt; 0 Then PlayerY = 0
	If PlayerY &gt;= GameCanvas_Height Then PlayerY = GameCanvas_Height

End Function
</textarea><br><br>Is there a simplier, more obvious approach? <br><br></td></tr></table><br>
<a name="720827"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> For the moment, I do things the same way: store key states (and also mouse states for that matter!) into variables or arrays. <br><br></td></tr></table><br>
<a name="721043"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> The undocumented (unfortunately) command enablepolledinput should enable you to use standard KeyDown commands with a canvas or active panel.<br><br>Note the use of waitevent instead of pollevent so your app doesn't run with 100% cpu usage:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'A key-handling test for multiple, simultaneous key presses on a MaxGUI canvas.

'Blocks marked with asterisks(*) highlight relevent code.

SuperStrict

Global MainWindow:TGadget = CreateWindow("Simultaneous Key Press Test - MaxGUI Canvas.", 10, 10, 800, 600, Null, WINDOW_TITLEBAR | WINDOW_RESIZABLE)
Global GameCanvas:TGadget = CreateCanvas(0, 0, 0, 0, MainWindow)
Global GameCanvas_Width:Int 
Global GameCanvas_Height:Int
resizeGameCanvas()

Global PlayerX:Int = 0
Global PlayerY:Int = 0

Global FlipTimer:TTimer = CreateTimer(60)

'*** Key-handling initialisation ***

enablepolledinput
	
Global KeyTimer:TTimer = CreateTimer(100)	'The rate at which to check the keys.

'***********************************

Repeat
	Select WaitEvent()
	
		Case EVENT_TIMERTICK
		
			Select EventSource()
		
				Case FlipTimer
					RedrawGadget GameCanvas
					
				'*** Call the key handling function when 
				'*** the KeyTimer timer event occurs.
				
				Case KeyTimer
					processKeys()
					
				'****************************************
					
			EndSelect
	
		Case EVENT_GADGETPAINT
			SetGraphics CanvasGraphics(GameCanvas)
			Cls
			SetBlend SOLIDBLEND
			SetColor 255, 255, 255
			DrawText "ARROW keys move, ESC quits, T and E and S together brings up a Notify dialog.", 10, 10
			drawPlayer()
			Flip

		Case EVENT_WINDOWSIZE
			resizeGameCanvas()
			checkPlayerPosition()

		Case EVENT_APPRESUME
			ActivateGadget GameCanvas

		Case EVENT_WINDOWCLOSE
			endApp()
	
		Case EVENT_APPTERMINATE
			endApp()

	EndSelect	

Forever


'*** Key handling function

Function processKeys()

	If KeyDown(KEY_RIGHT) Then	PlayerX :+ 10
	If KeyDown(KEY_LEFT) Then PlayerX :- 10
	If KeyDown(KEY_UP) Then PlayerY :- 10
	If KeyDown(KEY_DOWN) Then PlayerY :+ 10
	If KeyDown(KEY_ESCAPE) Then endApp()
		
	checkPlayerPosition()
	
	If KeyDown(KEY_T) And KeyDown(KEY_E) And KeyDown(KEY_S) 
		Notify("Test")
		FlushKeys
	EndIf

End Function

'*************************


Function drawPlayer()

	SetBlend ALPHABLEND
	SetAlpha 0.75
	SetColor 0, 255, 255
	DrawOval PlayerX - 4, PlayerY - 4, 8, 8

EndFunction

Function resizeGameCanvas()

	SetGraphics CanvasGraphics(GameCanvas)
	SetGadgetShape GameCanvas, 5, 5, ClientWidth(MainWindow) - 10, ClientHeight(MainWindow) - 10
	GameCanvas_Width = GadgetWidth(GameCanvas)
	GameCanvas_Height = GadgetHeight(GameCanvas)
	SetViewport 0, 0, GameCanvas_Width, GameCanvas_Height
	ActivateGadget GameCanvas

EndFunction

Function endApp()

	FreeGadget GameCanvas		
	End

EndFunction

Function checkPlayerPosition()

	If PlayerX &gt;= GameCanvas_Width Then PlayerX = GameCanvas_Width
	If PlayerX &lt; 0 Then PlayerX = 0
	If PlayerY &lt; 0 Then PlayerY = 0
	If PlayerY &gt;= GameCanvas_Height Then PlayerY = GameCanvas_Height

End Function
</textarea> <br><br></td></tr></table><br>
<a name="721108"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >impixi</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Works a treat. Thanks Simon. <br><br></td></tr></table><br>
<a name="727715"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nigel Brown</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Simon excelent demo dose'nt work under Linux! <br><br></td></tr></table><br>
<a name="773168"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >impixi</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I just tried this under Ubuntu Linux 7.04 and it works okay. <br><br></td></tr></table><br>
<a name="773520"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >LAB[au]</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I had to change this line (winXP) for this to work...<br><br>Global GameCanvas:TGadget = CreateCanvas(0, 0, 0, 0, MainWindow)<br><br>to<br><br>Global GameCanvas:TGadget = CreateCanvas(0, 0, 10, 10, MainWindow) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
