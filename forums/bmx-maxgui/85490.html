<!DOCTYPE html><html lang="en" ><head ><title >Spinner proxy gadget...advanced number editor</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Spinner proxy gadget...advanced number editor</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >Spinner proxy gadget...advanced number editor</a><br><br>
<a name="967323"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is probably one of the nicest pieces of code I have ever written:<br><a href="http://blitzmax.com/codearcs/codearcs.php?code=2514" target="_blank">http://blitzmax.com/codearcs/codearcs.php?code=2514</a><br><br>Maybe this can go in the official module? <br><br></td></tr></table><br>
<a name="967326"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Screenshots please... :D <br><br></td></tr></table><br>
<a name="967383"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I agree, made it officialy in the Proxygadgets<br><br>I've update your code with<br>- SetColor method<br>- SetEnable (for enable/disable the gadget)<br>- fixed SetSpinnerValue (you missed a '_' in the SetValue function)<br><br>I've tested on Win32, FLTK (win32 based) and they works as expected (they use 'standard' MaxGUI code, not OS specific calls)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Import MaxGui.drivers'fltkmaxgui'Drivers

Local window:TGadget

window=CreateWindow("Spinner Example",40,40,320,240)

Local spinner:TSpinner
Local label:TGadget
Local x:Int=2,y:Int=2

label=CreateLabel("Float spinner:",x,y,200,18,window)
SetGadgetLayout label,1,0,1,0
y:+18
spinner=CreateSpinner(x,y,80,20,window)
SetSpinnerRange(spinner,0,1,2)
SetGadgetLayout spinner,1,1,1,0
y:+22

label=CreateLabel("Integer spinner with trackbar:",x,y,200,18,window)
SetGadgetLayout label,1,0,1,0
y:+18
spinner=CreateSpinner(x,y,300,20,window,SPINNER_TRACKBAR)
SetSpinnerRange(spinner,-1,1,0)
SetGadgetLayout spinner,1,1,1,0
y:+22
SetGadgetColor spinner,200,20,20

spinner=CreateSpinner(x,y,300,20,window,SPINNER_TRACKBAR)
SetSpinnerRange(spinner,-10,10,0)
SetSpinnerValue(spinner,-3)
SetGadgetLayout spinner,1,1,1,0

DisableGadget spinner

While True
	WaitEvent 
	Print CurrentEvent.ToString()
	Select EventID()
		Case EVENT_WINDOWCLOSE
			End
	End Select
Wend




'-------------------------------------------------------------------

Const SPINNER_TRACKBAR:Int=1

Type TSpinner Extends TProxygadget
	
	Const SliderWidth:Int=18
	Const Div:Int=80
	
	Field panel:TGadget
	Field textfield:TGadget
	Field slider:TGadget
	Field trackbar:TGadget
	Field value:Double
	Field range:Double[2]
	Field accuracy:Int=1
	Field floatitemmultiplier:Double=10
	
	Method SetRange_(minimum:Double,maximum:Double,accuracy:Int=1)
		Self.accuracy=accuracy
		floatitemmultiplier=10.0^Double(accuracy)
		range[0]=minimum*floatitemmultiplier
		range[1]=maximum*floatitemmultiplier-range[0]+1
		SetSliderRange slider,1,range[1]
		If trackbar
			SetSliderRange trackbar,1,range[1]
		EndIf
		SetValue_(value)
	EndMethod
	
	Method SetColor(r:Int=255,g:Int=255,b:Int=255)
		SetGadgetColor textfield,r,g,b
	End Method
	
	Method SetEnabled(sta:Int=True)
		If sta=False
			DisableGadget panel
			DisableGadget textfield
			DisableGadget slider
			If trackbar DisableGadget trackbar
		Else
			EnableGadget panel
			EnableGadget textfield
			EnableGadget slider
			If trackbar EnableGadget trackbar
		End If
		
	End Method

	
	Method Cleanup()
		RemoveHook(EmitEventHook,EventHook,Self)
		Super.Cleanup()
	EndMethod
	
	Function Create:TSpinner(x:Int,y:Int,width:Int,height:Int,group:TGadget,flags:Int=0)
		Local spinner:TSpinner
		Local w:Int
		
		spinner=New TSpinner
				
		spinner.panel=CreatePanel(x,y,width,height,group)
		spinner.setproxy(spinner.panel)
		
		w=spinner.panel.ClientWidth()
		If (SPINNER_TRACKBAR &amp; flags)
			w=div
			If w&gt;spinner.panel.ClientWidth() w=spinner.panel.ClientWidth()
		EndIf
		
		spinner.textfield=CreateTextField(0,0,w-SliderWidth,spinner.panel.ClientHeight(),spinner.panel)
		If (SPINNER_TRACKBAR &amp; flags)
			SetGadgetLayout spinner.textfield,1,0,1,1
		Else
			SetGadgetLayout spinner.textfield,1,1,1,1
		EndIf
		
		spinner.slider=CreateSlider(w-SliderWidth,0,SliderWidth,spinner.panel.ClientHeight(),spinner.panel,SLIDER_VERTICAL)		
		If (SPINNER_TRACKBAR &amp; flags)
			SetGadgetLayout spinner.slider,1,0,1,1
		Else
			SetGadgetLayout spinner.slider,0,1,1,1
		EndIf
		AddHook(EmitEventHook,EventHook,spinner)
		
		If (SPINNER_TRACKBAR &amp; flags)
			spinner.trackbar=CreateSlider(w,0,spinner.panel.ClientWidth()-w,spinner.panel.ClientHeight(),spinner.panel,SLIDER_TRACKBAR|SLIDER_HORIZONTAL)
			SetGadgetLayout spinner.trackbar,1,1,1,1
		EndIf
		
		spinner.SetRange_(0,1)
		Return spinner
	EndFunction
	
	Method SetValue_(i:Double)
		i=Max(i,range[0])
		i=Min(i,range[1])
		i=Round(i*floatitemmultiplier)/floatitemmultiplier
		i:*floatitemmultiplier
		If i&lt;range[0] i=range[0]
		If i&gt;range[0]+range[1]-1 i=range[0]+range[1]-1
		SetGadgetText textfield,FloatToString(i/floatitemmultiplier,accuracy)
		SetSliderValue slider,range[1]-(i-range[0]+1)
		If trackbar SetSliderValue trackbar,i-range[0]+1
		value=i
	EndMethod
	
	Function EventHook:Object(id:Int,data:Object,context:Object)
		Local event:TEvent
		Local spinner:TSpinner
		Local i:Int
		
		event=TEvent(data)
		If event
			spinner=TSpinner(context)
			If spinner
				Select event.id
				Case EVENT_GADGETLOSTFOCUS
					If event.source=spinner.textfield
						spinner.SetValue_(Double(GadgetText(spinner.textfield)))
						EmitEvent CreateEvent(EVENT_GADGETACTION,spinner)
						Return Null
					EndIf				
				Case EVENT_GADGETACTION
					Select event.source
					Case spinner.trackbar
						If spinner.trackbar
							i=(SliderValue(spinner.trackbar)+spinner.range[0]-1)
							spinner.SetValue_(i/spinner.floatitemmultiplier)
						EndIf
					Case spinner.textfield
						Return Null
					Case spinner.slider
						i=spinner.range[1]-(SliderValue(spinner.slider)-spinner.range[0]+1)
						spinner.SetValue_(Double(i)/spinner.floatitemmultiplier)
						EmitEvent CreateEvent(EVENT_GADGETACTION,spinner)
						Return Null
					EndSelect
				EndSelect
			EndIf
		EndIf
		Return data
	EndFunction
	
	Function Round:Int(val:Double)
		Local dec#
		dec#=val-Floor(val)
		If dec&lt;0.5 Return Floor(val) Else Return Ceil(val)
	EndFunction	
	
	Function FloatToString:String(value:Float,places:Int=3)
		Local sign:Int=Sgn(value)
		value=Abs(value)
		Local i:Int=Round(value*10^places)
		Local ipart:Int=Int(i/10^places)
		Local dpart:Int=i-ipart*10^places
		Local si$=ipart
		Local di$
		If dpart&gt;0
			di=dpart
			While di.length&lt;places
				di="0"+di
			Wend
			di="."+di
		EndIf
		While Right(di,1)="0"
			di=Left(di,di.length-1)
		Wend
		If places
			If di="" di=".0"
		EndIf
		If sign=-1 si="-"+si
		Return si+di
	EndFunction	
	
EndType

Rem
bbdoc:
EndRem
Function CreateSpinner:TSpinner(x:Int,y:Int,width:Int,height:Int,group:TGadget,flags:Int=0)
	Return TSpinner.Create(x,y,width,height,group,flags)
EndFunction

Rem
bbdoc:
EndRem
Function SetSpinnerRange(spinner:TSpinner,minimum:Double,maximum:Double,accuracy:Int=1)
	spinner.SetRange_(minimum,maximum,accuracy)
EndFunction

Rem
bbdoc:
EndRem
Function SetSpinnerValue(spinner:TSpinner,value:Double)
	spinner.SetValue_(value)
EndFunction

Rem
bbdoc:
EndRem
Function SpinnerValue:Double(spinner:TSpinner)
	Return spinner.value
EndFunction
</textarea><br><br>Edit: an a little changes to SetSpinnerRange...not very important, but...<br><br><pre class=code>
Function SetSpinnerRange(spinner:TSpinner,minimum:Double,maximum:Double,accuracy:Int=1)
	spinner.SetRange_(minimum,maximum,accuracy)
	SetGadgetToolTip spinner.textfield,"Min: "+spinner.FloatToString(Minimum,3)+"~nMax: "+spinner.FloatToString(Maximum,3)
EndFunction
</pre><br><br>So the user can SEE what is the range... <br><br></td></tr></table><br>
<a name="967600"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I don't think you should set the tooltip like that.  If someone wants a tooltip, they will specify one.  Otherwise, you should not guess what the user might want. <br><br></td></tr></table><br>
<a name="967626"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mark Tiffany</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Agreed, should at least have a check to make sure it isn't already set as it might be useful if you haven't bothered setting a tooltip... <br><br></td></tr></table><br>
<a name="967810"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, this is a possible fix<br><pre class=code>
Method SetToolTip(tt$)
		Local prev$=gettooltip()
		SetGadgetToolTip textfield,tt+"~n"+prev
End Method
	
Method GetToolTip$()
		Return textfield.Gettooltip()
End Method
...
Function SetSpinnerRange(spinner:TSpinner,minimum:Double,maximum:Double,accuracy:Int=1)
	spinner.SetRange_(minimum,maximum,accuracy)
	
	Local prev$=spinner.GetToolTip()
	If prev&lt;&gt;"" prev=prev+"~n"
	
	SetGadgetToolTip spinner.textfield,prev+"Min: "+spinner.FloatToString(Minimum,3)+"~nMax: "+spinner.FloatToString(Maximum,3)
EndFunction
</pre><br>So the user-tooltip is save.<br>In any case my was only a suggestion: if you have a window full of this controls I think is useful for the user know what he/she's changing.. <br><br></td></tr></table><br>
<a name="967929"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think if the programmer wants a tooltip they should specify one themselves.  You could add a label below the minimum and maximum extents of the track bar. <br><br></td></tr></table><br>
<a name="968041"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Updated the code. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
