<!DOCTYPE html><html lang="en" ><head ><title >I think I trashed my vid mem with two canvases</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >I think I trashed my vid mem with two canvases</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >I think I trashed my vid mem with two canvases</a><br><br>
<a name="996950"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi all,<br><br>I wrote a simple tile editor on my windows box (which has loads of vram) but it fails on my Mac that only has 64mb shared Intel GMA 950.  It uses two canvases and once a tile has been drawn on one, it refuses to appear on the other.<br><br>1) Could a Mac owner with some proper VRAM tell me this works for them? :)<br>2) If clicking in the main window only gives you white squares, comment out the drawimage for the mini-view (line 125)<br>3) Should I be taking specific steps to share images across canvases, along the lines of the glShareContexts?  In which case, why the different behaviour across platforms/drivers?  <br><br>I'm aware of the horror stories with GMA950 with things like DirectX drivers, and actually chose this chipset because if the code's gonna fail, I want it to fail for me :) <br><br>I've kinda 'solved' the issue by having two sets of images; one for each canvas.<br><br>Cheers<br>Matt<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
Import MaxGui.Drivers

'==================================================================================================================
'
'	Init controls
'
Global window:TGadget = CreateWindow:TGadget("Editor",60,60,1010,640,Null,WINDOW_TITLEBAR| WINDOW_CLIENTCOORDS )
	
Global Label9:TGadget = CreateLabel:TGadget("Tiles",11,20,150,14,window:TGadget,Null)
Global Tiles_ListBox:TGadget = CreateListBox:TGadget(10,42,90,110,window:TGadget,Null)
	For Local i:Int = 1 To 50
		AddGadgetItem( Tiles_ListBox:TGadget, i,GADGETITEM_NORMAL )
	Next 
	Tiles_ListBox.SelectItem( 0 )
	
Global tileCanvas:TGadget = CreateCanvas( 110, 42, 70, 50 , window:TGadget )
Global gameCanvas:TGadget = CreateCanvas( 200, 20, 800, 576, window:TGadget )

'==================================================================================================================
'
'	Global variables
'
Global mx:Int 				' mouse position
Global my:Int 
Global tileID:Int 			' current drawing tile
Global tiles:TImage[50]		' tile images
Global data:Int[25,12]

'==================================================================================================================
'
'	Create the images
'
For Local t:Int = 0 To 49
	tiles[t] = CreateImage( 32, 48, 1, DYNAMICIMAGE|MASKEDIMAGE )
	Local pm:TPixmap = LockImage( tiles[t] )
	Local g:Int = t * 5
	Local r:Int = (t * 30 ) Mod 255
	Local col:Int = (255 Shl 24) + (r Shl 16) + (g Shl 8) + 255
	For Local x:Int = 0 To 31
		For Local y:Int = 0 To 47
			Local color:Int = col
			If ( y &lt;= t ) color = (255 Shl 24)
			WritePixel( pm, x, y, color )
		Next 
	Next 
	UnlockImage( tiles[t] )
Next 


PaintWindow( gameCanvas )
PaintWindow( tileCanvas )

'==================================================================================================================
'
'	Application loop
'
Repeat
	WaitEvent()
	Select EventID()
		Case EVENT_WINDOWCLOSE
			Select EventSource()
				Case window 
					End 
			End Select
		Case EVENT_GADGETACTION
			Select EventSource()
				Case Tiles_ListBox
					tileID = EventData()
					PaintWindow( tileCanvas )
			End Select
		Case EVENT_GADGETSELECT
			Select EventSource()
				Case Tiles_ListBox
					tileID = EventData()
					PaintWindow( tileCanvas )
			End Select
		Case EVENT_GADGETPAINT
			Select EventSource()
				Case gameCanvas
					'
					' Draw the main canvas
					'
		              SetGraphics CanvasGraphics( gameCanvas )
					SetClsColor ( 40,40,0 )
					Cls 
		               Local wid% = gameCanvas.ClientWidth()
		               Local hgt% = gameCanvas.ClientHeight()
					SetColor ( 255,255,255 )
					SetBlend( ALPHABLEND )
					For Local x% = 0 To 24
						For Local y% = 0 To 11
							DrawImage( tiles[ data[x,y] ], x * 32, y * 48 )
						Next 
					Next 
		
					If ( tileID &gt; 0 )
						SetColor( 255,0,0 )
						SetLineWidth(2)
						For Local x% = 0 To 24
							For Local y% = 0 To 11
								If ( data[x,y] = tileID )
									Local xFrom:Int = x * 32
									Local yFrom:Int = y * 48
									Local xTo:Int = xFrom + 32
									Local yTo:Int = yFrom + 48
									DrawLine( xFrom, yFrom, xTo, yFrom )
									DrawLine( xTo, yFrom, xTo, yTo )
									DrawLine( xTo, yTo, xFrom, yTo )
									DrawLine( xFrom, yFrom, xFrom, yTo )
								End If 
							Next 
						Next 
						SetLineWidth(1)
						SetColor(255,255,255)
					End If 
					Flip 
				Case tileCanvas
					'
					'-- draw the mini canvas
					'
					SetGraphics CanvasGraphics( tileCanvas )
					SetClsColor ( 40, 40, 0 )
					Cls 
					DrawImage( tiles[ tileID ], 19, 1 )
					Flip 			
				End Select 
		Case EVENT_MOUSEMOVE
			mx = EventX()
			my = EventY()
		Case EVENT_MOUSEDOWN
			Local x% = mx / 32
			Local y% = my / 48
			If ( x &gt;= 0 And x &lt; 25 And y &gt;= 0 And y &lt; 12 )
				data[x,y] = tileID
				PaintWindow( gameCanvas )
			End If 
		Case EVENT_MOUSEENTER
			Select EventSource()
				Case gameCanvas 
					ActivateGadget( gameCanvas )
			End Select 
		End Select
Forever

Function PaintWindow( source:TGadget )
	Local e:TEvent = New TEvent
	e.source = source
	e.id = EVENT_GADGETPAINT
	PostEvent( e )
End Function 
</textarea> <br><br></td></tr></table><br>
<a name="996951"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >klepto2</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Try adding this right after the import part in your code:<br><br><pre class=code>
?Not win32
GLShareContexts()
?
</pre><br><br>The inconsistant behaviour is due the fact that win32 will use DX as a standard gfx driver where linux or mac use OpenGL. <br><br></td></tr></table><br>
<a name="996956"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Perfect Kelpto thanks. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
