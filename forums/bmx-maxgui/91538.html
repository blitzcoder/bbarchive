<!DOCTYPE html><html lang="en" ><head ><title >Event woes in custom dialogue</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Event woes in custom dialogue</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >Event woes in custom dialogue</a><br><br>
<a name="1041460"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ghost Dancer</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have been trying to make a stand-alone dialog function that works much like the Confirm command but also has  a text field to get data from the user.<br><br>This is the code for the function:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Function RequestInput$(title$, labelText$, defaultText$ = "", group:TGadget = Null)
	Local closeWindow:Int, text$
	
	Local window:TGadget = CreateWindow("",406,235,294,99,group,WINDOW_TITLEBAR|WINDOW_CLIENTCOORDS |WINDOW_CENTER)
	Local label:TGadget = CreateLabel("",7,12,280,14,window,Null)
	Local textField:TGadget = CreateTextField(7,32,280,18,window,Null)
	Local okBut:TGadget = CreateButton("OK",7,69,75,23,window,BUTTON_OK)
	Local cancelBut:TGadget = CreateButton("Cancel",212,69,75,23,window,BUTTON_CANCEL)
	
	SetGadgetText window, title$
	SetGadgetText label, labelText$
	SetGadgetText textField, defaultText$
	ActivateGadget textField
	
	If group Then DisableGadget group
	
	Repeat
		WaitEvent()
		
		Select EventID()
		Case EVENT_WINDOWCLOSE
			Select EventSource()
			Case window
				closeWindow = True
			End Select

		Case EVENT_GADGETACTION
			Select EventSource()
			Case okBut
				closeWindow = True
				text$ = GadgetText(textField)
			Case cancelBut
				closeWindow = True
			End Select
	
		End Select
	Until closeWindow
	
	If group Then EnableGadget group
	
	FreeGadget window
	
	Return text$
End Function
</textarea><br><br>This works fine when the main program uses a simple event loop, for example:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import MaxGui.Drivers
Import BRL.EventQueue

Local window:TGadget = CreateWindow("Test", 1, 1, 400, 200, Null, WINDOW_TITLEBAR|WINDOW_CLIENTCOORDS |WINDOW_CENTER)
Local button:TGadget = CreateButton("Click for input dialogue", 10, 10, 140, 20, window)
Local label:TGadget = CreateLabel("Data:", 10, 40, 140, 20, window)

While window
	WaitEvent
	
	Select EventID()
	Case EVENT_WINDOWCLOSE
		Select EventSource()
		Case window
			End
		End Select
	
		Case EVENT_GADGETACTION
			Select EventSource()
			Case button
				SetGadgetText label, "Data: " + RequestInput$("RequestInput Test", "Enter Text:", "Some text", window)
			End Select
	End Select
Wend
</textarea><br><br>However, if the main program uses event hooks to manage the events it hangs the program as in the following example:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import MaxGui.Drivers
Import BRL.EventQueue

AddHook EmitEventHook, mainhook

Global window:TGadget = CreateWindow("Test", 1, 1, 400, 200, Null, WINDOW_TITLEBAR|WINDOW_CLIENTCOORDS |WINDOW_CENTER)
Global button:TGadget = CreateButton("Click for input dialogue", 10, 10, 140, 20, window)
Global label:TGadget = CreateLabel("Data:", 10, 40, 140, 20, window)

While window
	WaitSystem
Wend

End


Function mainhook:Object(id:Int,data:Object,context:Object)
	Local Event:TEvent = TEvent(data)
	
	Select Event.ID
	Case EVENT_WINDOWCLOSE
		Select Event.Source
		Case window
			End
		End Select
	
		Case EVENT_GADGETACTION
			Select Event.Source
			Case button
				SetGadgetText label, "Data: " + RequestInput$("RequestInput Test", "Enter Text:", "Some text", window)
			End Select
	End Select
End Function
</textarea><br><br>I've been working on this for hours but I can't work out how to solve the problem. I have tried making the RequestInput use hooks as well but that didn't work either (I can post the code if it helps).<br><br>Any suggestions how to get this working? <br><br></td></tr></table><br>
<a name="1041485"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br>Have you seen <a href="/posts.php?topic=79966#898662" target="_blank">this</a>?<br><br>On that base I made this 'about gadget' using proxygadgets, and it seems to work very well<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Rem
	new about window
	based on Ked works
End Rem

Import maxgui.drivers

' ----------- test
Local window:TGadget=CreateWindow("Dialog",50,50,640,480,Desktop(),1+2)
Local button:TGadget=CreateButton("Dialog",10,10,80,24,window)
Local image:TPixmap=LoadPixmap("C:\BlitzMax133rc5\docs\html\bmax120.png")

Local ab:taboutGadget=About("Hallo world","Hey!~nThis is to remind you that this "+..
					"magnificent piece of work is fantastic and that you should "+..
					"always use it.~n~nThank you for your time.",window,image)

Repeat
	WaitEvent()
	Select EventID()
	
			Case event_gadgetAction
				If EventSource()=button 
					ab=About("Hello world","Hey!~nThis is to remind you that this "+..
					"magnificent piece of work is fantastic and that you should "+..
					"always use it.~n~nThank you for your time.",window)
				End If
				
			
			Case event_windowclose
			Select EventSource()
				Case window
					End
			EndSelect


	End Select
Until False


Function About:taboutgadget(txt$,comment$,windowp:tgadget=Null,image:TPixmap=Null)

	Local na:taboutgadget=New taboutgadget.Create(txt,100,100,300,400,windowp,,comment,image)
	
	Return NA
End Function

Type taboutgadget Extends tproxygadget
	Field _window:tgadget
	Field _button:tgadget
	Field _image:TPixmap
	Field _parent:tgadget
	Field _panel:tgadget
	Field _label:tgadget
	Field _title:tgadget

	
	Global currentAbout:taboutgadget

	Method New()
		Initialize()
	EndMethod
	
	Method Create:taboutgadget(text$,x:Int,y:Int,w:Int,h:Int,group:TGadget=Null,style:Int=0,customtext$="",image:TPixmap=Null)
		
		Local ipos:Int=5
		
		If image ipos=image.height
		
		_window=CreateWindow(text,x,y,w,h,,WINDOW_CENTER|WINDOW_TITLEBAR)
		_button=CreateButton("Ok",w-100,h-60,80,24,_window,button_ok)
		If image
			_panel =CreatePanel(0,0,w,image.height,_window)
			SetGadgetPixmap _panel,image,PANELPIXMAP_STRETCH
		End If
		
		_parent=group

		_title = CreateLabel(text,10,ipos+2,w-26,34,_window)
		SetGadgetFont(_title,LookupGuiFont(GUIFONT_SERIF,24))
		SetGadgetColor _title,255,0,0,False
		_label = CreateLabel(customtext,13,ipos+45,w-26,h-ipos-120,_window)
		SetGadgetFont(_label,LookupGuiFont(GUIFONT_SANSSERIF,10))
		CreateLabel("",0,h-70,w,0,_window,label_separator)
		DisableGadget _parent
		ActivateWindow _window
		CurrentAbout=Self
		
	'	SetGadgetAlpha _window,.8
		
		Return Self
	EndMethod
	
		
	Function Initialize()
		AddHook EmitEventHook, eventHandler, Null, 1
	EndFunction
	
	Function eventHandler:Object( pID%, pData:Object, pContext:Object )
		Local pEvent:TEvent = TEvent(pData)
		
		
		If pEvent 
		
			Select pevent.id
				Case event_windowclose
					If pevent.source=CurrentAbout._window
						FreeGadget CurrentAbout._window
						
						If currentabout.parent 
							EnableGadget currentabout._parent
							ActivateWindow currentAbout._parent
						End If
						
						Return Null
					End If
				Case event_gadgetaction
					If pevent.source=CurrentAbout._button
			
						FreeGadget CurrentAbout._window
						If currentabout._parent 
							EnableGadget currentabout._parent
							ActivateWindow currentAbout._parent
						End If
						
						Return Null
					End If
			End Select
			
		EndIf

		
		Return pData
	EndFunction

End Type
</textarea><br><br>Maybe you can find inspiration to resolve your implementation.<br><br>Cheers <br><br></td></tr></table><br>
<a name="1041488"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> You can't make a nice neat self-contained function like that.  You have to add an event hook, and detect when the button is closed. <br><br></td></tr></table><br>
<a name="1041499"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ghost Dancer</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks, I did do a search but somehow missed that one. The main thing I need to do that differs from those examples is to halt the main program flow whilst my dialog is running, as it is requesting user input partway through an operation (much like how RequestFile works), and the problem I'm getting is related to this.<br><br>I already tried a hooked version but it resulted in the same problem. Full code below:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import MaxGui.Drivers
Import BRL.EventQueue

AddHook EmitEventHook, mainhook

Global window:TGadget = CreateWindow("Test", 1, 1, 400, 200, Null, WINDOW_TITLEBAR|WINDOW_CLIENTCOORDS |WINDOW_CENTER)
Global button:TGadget = CreateButton("Click for input dialog", 10, 10, 140, 20, window)
Global label:TGadget = CreateLabel("Data:", 10, 40, 140, 20, window)

While window
	WaitSystem
Wend

End


Function mainhook:Object(id:Int,data:Object,context:Object)
	Local Event:TEvent = TEvent(data)
	
	Select Event.ID
	Case EVENT_WINDOWCLOSE
		Select Event.Source
		Case window
			End
		End Select
	
		Case EVENT_GADGETACTION
			Select Event.Source
			Case button
				SetGadgetText label, "Data: " + RequestInput$("RequestInput Test", "Enter Text:", "Some text", window)
			End Select
	End Select
End Function



Function RequestInput$(title$, labelText$, defaultText$ = "", group:TGadget = Null)
	Local dialog:TRequestDialog = TRequestDialog.create(title$, labelText$, defaultText$, group)

	If group Then DisableGadget group
	
	Repeat
		WaitSystem
	Until dialog.closeWindow
	
	If group Then EnableGadget group
	
	Local text$ = dialog.text$
	
	FreeGadget dialog.window
	dialog = Null
	
	Return text$
	
End Function


Type TRequestDialog
	Global current:TRequestDialog
	
	Field closeWindow:Int, text$
	
	Field window:TGadget
	Field label:TGadget
	Field textField:TGadget
	Field okBut:TGadget
	Field cancelBut:TGadget
	
	
	Function eventhook:Object(id:Int, data:Object, context:Object)
		Local Event:TEvent = TEvent(data)
		
		If Event Then
			Select Event.ID
			Case EVENT_WINDOWCLOSE
				Select Event.Source
				Case current.window
					current.closeWindow = True
				End Select
		
			Case EVENT_GADGETACTION
				Select Event.Source
				Case current.okBut
					current.closeWindow = True
					current.text$ = GadgetText(current.textField)
				Case current.cancelBut
					current.closeWindow = True
				End Select
		
			End Select
		End If
		
		Return data	
	End Function
	
	
	Function create:TRequestDialog(title$, labelText$, defaultText$, group:TGadget)
		Local dialog:TRequestDialog = New TRequestDialog
		
		dialog.window = CreateWindow("", 406, 235, 294, 99, group, WINDOW_TITLEBAR|WINDOW_CLIENTCOORDS |WINDOW_CENTER)
		dialog.label = CreateLabel("", 7, 12, 280, 14, dialog.window, Null)
		dialog.textField = CreateTextField(7, 32, 280, 18, dialog.window, Null)
		dialog.okBut = CreateButton("OK", 7, 69, 75, 23, dialog.window, BUTTON_OK)
		dialog.cancelBut = CreateButton("Cancel", 212, 69, 75, 23, dialog.window, BUTTON_CANCEL)
		
		SetGadgetText dialog.window, title$
		SetGadgetText dialog.label, labelText$
		SetGadgetText dialog.textField, defaultText$
		ActivateGadget dialog.textField
		
		Return dialog
	End Function
	
	
	Method New()
		AddHook EmitEventHook, eventhook, Null, 1
		current = Self
	End Method
End Type
</textarea><br><br>The problem seems to be the Repeat/WaitSystem loop, but I need something like this to halt the main program flow until the dialog is closed. <br><br></td></tr></table><br>
<a name="1041857"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ghost Dancer</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Any other suggestions to solve this?  I really need it to behave like a Confirm dialog (or similar) so program flow only continues once the dialog is closed. <br><br></td></tr></table><br>
<a name="1161511"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TAS</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> What about deleting the event hook at start and re-installing upon exit? <br><br></td></tr></table><br>
<a name="1161974"></a>

<a name="1161979"></a>

<a name="1161980"></a>

<a name="1161981"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skn3</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Create a window stack so when you open a window a new entry is added to the stack. All previous windows are disabled. When you close the dialogue, remove it from the stack and enable the top most window on stack.<br><br><br>This is ripped from my dialogue class so no idea how well it is working but hopefully it will help.<br><br>You will need the maxguiex module for some functions for bringing the window to the top n stuff.<br><a href="https://github.com/skn3/maxguiex" target="_blank">https://github.com/skn3/maxguiex</a><br><br>you could probably remove the two lines<pre class=code>
				BringWindowToTop(dialogue)
				FocusWindow(dialogue)</pre><br>and then you wouldn't need maxguiex and find an alternative?<br><br>[bbcode]Import maxgui.drivers<br>Import maxgui.proxygadgets<br>Import skn3.maxguiex<br><br>Type Skn3Dialogue Extends TProxyGadget<br>	Global hooked:Int<br>	Global stack:TList = CreateList()<br>	Global classes:TMap = CreateMap()<br>	<br>	Field Window:TGadget<br>	<br>	Field IsActive:Int = False<br>	Field isOpen:Int = False<br>	Field isModal:Int = True<br>	<br>	Field hasOpened:Int = False<br>	<br>	Field stackLink:TLink<br>	Field stackActiveGadget:TGadget<br>	<br>	'function api<br>	Function OpenDialogue:Skn3Dialogue(dialogue:Skn3Dialogue,parent:Skn3Dialogue)<br>		' --- open a dialogue spawned from this dialogue ---<br>		If dialogue = Null Return Null<br>		<br>		'open this dialogue<br>		dialogue.Open()<br>		<br>		'return it<br>		Return dialogue<br>	End Function<br>	<br>	Function GetTopMostDialogue:Skn3Dialogue()<br>		' --- return teh top most dialogue in the applet ---<br>		Return Skn3Dialogue(stack.Last())<br>	End Function<br>	<br>	'hooks<br>	Function EventHook:Object(id:Int,data:Object,context:Object)<br>		Local event:TEvent = TEvent(data)<br><br>		'check valid event<br>		If event<br>			'process all dialogues from top of dialogue stack to bottom<br>			Local link:TLink = stack.LastLink()<br>			Local dialogue:Skn3Dialogue<br>			<br>			While link<br>				'get dialogue<br>				dialogue = Skn3Dialogue(link.value())<br>				<br>				'see if the dialogue is handling the event<br>				If dialogue.isOpen And dialogue.OnFilterEvent(event)<br>					event = dialogue.OnEvent(event)<br>					If event = Null Return Null<br>				EndIf<br>				<br>				'previous link<br>				link = link.PrevLink()<br>			Wend<br>		EndIf<br>		<br>		'return<br>		Return event<br>	EndFunction<br>	<br>	'constructor/destructor<br>	Method New()<br>		If hooked = False<br>			hooked = True<br>			AddHook(EmitEventHook,Skn3Dialogue.EventHook,Null)<br>		EndIf<br>	End Method<br>	<br>	'events<br>	Method OnCreate:Skn3Dialogue() Abstract<br>	<br>	Method OnFilterEvent:Int(event:TEvent)<br>		Return True<br>	End Method<br>	<br>	Method OnEvent:TEvent(event:TEvent)<br>		Select event.id<br>			Case EVENT_WINDOWCLOSE<br>				' --- window is closed ---<br>				If event.source = Self<br>					close()<br>					<br>					'return event used<br>					Return Null<br>				End If<br>		End Select<br>		<br>		Return event<br>	End Method<br>	<br>	Method OnClose()<br>		If Window FreeGadget(Window)<br>	End Method<br>	<br>	'api<br>	Method OpenChildDialogue:Skn3Dialogue(dialogue:skn3Dialogue)<br>		Return Skn3Dialogue.OpenDialogue(dialogue,Self)<br>	End Method<br>	<br>	Method Open()<br>		' --- open the dialogue ---<br>		If Window And isOpen = False<br>			'flag this dialogue open<br>			isOpen = True<br>			<br>			Local link:TLink<br>			Local dialogue:Skn3Dialogue<br>			<br>			'for the top most dialogue remember the active gadget<br>			dialogue = Skn3Dialogue(stack.Last())<br>			If dialogue<br>				'get active gadget<br>				dialogue.stackActiveGadget = ActiveGadget()<br>				<br>				'make sure the gadget is not the dialogue window<br>				If dialogue.stackActiveGadget = dialogue.Window dialogue.stackActiveGadget = Null<br>			EndIf<br>			<br>			'lock all other dialogues if this new dialogue is modal!<br>			If isModal<br>				link = stack.FirstLink()<br>				While link<br>					'get dialogue<br>					dialogue = Skn3Dialogue(link.value())<br>					<br>					'disable dialogue<br>					If dialogue.isModal DisableGadget(dialogue)<br>					<br>					'next dialogue<br>					link = link.NextLink()<br>				Wend<br>			EndIf<br>			<br>			'add this dialogue to the stack if it is a modal<br>			stackLink = stack.AddLast(Self)<br>			<br>			'setup the dialogue window<br>			If hasOpened = False ShowGadget(Window)<br><br>			'reset first open flag<br>			hasOpened = True<br>		EndIf<br>	End Method<br>		<br>	Method close()<br>		' --- close the dialogue and remove it from the stack ---<br>		'ok indicated whether we are just closing or if we are returning<br>		Local link:TLink<br>		Local dialogue:Skn3Dialogue<br>		<br>		'first activate the what will be new top most dialogue<br>		link = stackLink.PrevLink()<br>		If link<br>			While link<br>				dialogue = Skn3Dialogue(link.value())<br>				if dialogue.isModal Exit<br>				link = link.PrevLink()<br>				dialogue = Null<br>			Wend<br><br>			If dialogue And dialogue.Window<br>				'activate the dialogue window<br>				EnableGadget(dialogue)<br>				BringWindowToTop(dialogue)<br>				FocusWindow(dialogue)<br>				<br>				'activate a gadget if there was one<br>				If dialogue.stackActiveGadget &lt;&gt; Null<br>					ActivateGadget(dialogue.stackActiveGadget)<br>					dialogue.stackActiveGadget = Null<br>				EndIf<br>			EndIf<br>		EndIf<br>		<br>		'now pop dead dialogues starting from this off the stack<br>		link = stackLink<br>		While link<br>			'get dialogue<br>			dialogue = Skn3Dialogue(link.value())<br>			<br>			dialogue.OnClose()<br>			<br>			'next link<br>			link = link.NextLink()<br>			<br>			'remove the dialogue from the stack<br>			If dialogue.isModal<br>				dialogue.stackLink.remove()<br>				dialogue.stackLink = Null<br>			EndIf<br>		Wend<br>		<br>		'return successful close<br>		Return Null<br>	End Method<br>End Type<br><br>Type Skn3DialogueParent Extends Skn3Dialogue	<br>	Field button:TGadget<br>	<br>	Method OnCreate:Skn3Dialogue()<br>		Window = CreateWindow("parent window",50,50,300,300,Null,WINDOW_TITLEBAR)<br>		SetProxy(Window)<br>		<br>		button = CreateButton("press",5,5,Window.ClientWidth()-10,Window.ClientHeight()-10,Window)<br>		<br>		Return Self<br>	End Method<br>	<br>	Method OnFilterEvent:Int(event:TEvent)<br>		Return event.source = button<br>	End Method<br>	<br>	Method OnEvent:TEvent(event:TEvent)<br>		Select event.id<br>			Case EVENT_GADGETACTION<br>				Select event.source<br>					Case button<br>						OpenChildDialogue(New Skn3DialogueChild.OnCreate())<br>						Return Null<br>				End Select<br>		End Select<br>		<br>		Return Super.OnEvent(event)<br>	End Method<br>End Type<br><br>Type Skn3DialogueChild Extends Skn3Dialogue<br>	Method OnCreate:Skn3Dialogue()<br>		Window = CreateWindow("child window",50,50,150,150,Null,WINDOW_TOOL | WINDOW_TITLEBAR)<br>		SetProxy(Window)<br>		<br>		Return Self<br>	End Method<br>End Type<br><br>Local parentDialogue:Skn3Dialogue = New Skn3DialogueParent.OnCreate()<br>parentDialogue.Open()<br><br>Repeat<br>	WaitEvent()<br>Forever[/bbcode]<br><br><font class="tiny">Last edited 2012</font><br><br><font class="tiny">Last edited 2012</font><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
