<!DOCTYPE html><html lang="en" ><head ><title >Menu/canvas problem</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Menu/canvas problem</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >Menu/canvas problem</a><br><br>
<a name="881924"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >iprice</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've created a small app that consists of three simple menus and a canvas in a window.<br><br>I can click on each menu and they appear as they should, however if I select a menu then move to any other menu, the first menu list is still visible, although loses its focus.<br><br>Removing the <b>CreateCanvas</b> command cause the program to work properly, however I need the canvas as I'm drawing images etc. onto it<br><br>Here is a cutdown version<br><pre class=code>
Local style = WINDOW_TITLEBAR | WINDOW_MENU  | WINDOW_CLIENTCOORDS

Local MyWindow:TGadget
Local filemenu:TGadget
Local editmenu:TGadget
Local helpmenu:TGadget

Local canvas:TGadget

MyWindow=CreateWindow("Max GUI App.", 112,84,800,600,Null,style)

filemenu=CreateMenu("&amp;File          ",0,WindowMenu(MyWindow))
CreateMenu "&amp;New",101,filemenu,KEY_N,MODIFIER_COMMAND
CreateMenu "&amp;Open file",102,filemenu,KEY_O,MODIFIER_COMMAND
CreateMenu "&amp;Close file",103,filemenu,KEY_C,MODIFIER_COMMAND
CreateMenu"",0,filemenu
CreateMenu "&amp;Save",104,filemenu,KEY_S,MODIFIER_COMMAND
CreateMenu"",0,filemenu
CreateMenu "&amp;Open bitmap image",105,filemenu,KEY_I,MODIFIER_COMMAND
CreateMenu"",0,filemenu
CreateMenu "&amp;Exit",106,fileMenu,KEY_F4,MODIFIER_COMMAND

editmenu=CreateMenu("&amp;Edit          ",0,WindowMenu(MyWindow))
CreateMenu "Cu&amp;t",107,editmenu,KEY_X,MODIFIER_COMMAND
CreateMenu "&amp;Copy",108,editmenu,KEY_C,MODIFIER_COMMAND
CreateMenu "&amp;Paste",109,editmenu,KEY_V,MODIFIER_COMMAND

helpmenu=CreateMenu("&amp;Help          ",0,WindowMenu(MyWindow))
CreateMenu "&amp;About",110,helpmenu

canvas=CreateCanvas(0,0,800,640,MyWindow)

UpdateWindowMenu MyWindow

While True
	WaitEvent 
	Select EventID()
		Case EVENT_MENUACTION
			Select EventData()
			
				Case 106
					End

				Case 110
					Notify "About stuff here"
			End Select
	End Select
Wend
</pre><br><br>What am I doing wrong? <br><br></td></tr></table><br>
<a name="881949"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I can't reproduce here on Vista, but it sounds like the problem may be that you are using an event queue rather than event hooks.<br><br>Does the following solve your problem?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Local style = WINDOW_TITLEBAR | WINDOW_MENU  | WINDOW_CLIENTCOORDS

Local MyWindow:TGadget
Local filemenu:TGadget
Local editmenu:TGadget
Local helpmenu:TGadget

Local canvas:TGadget

MyWindow=CreateWindow("Max GUI App.", 112,84,800,600,Null,style)

filemenu=CreateMenu("&amp;File          ",0,WindowMenu(MyWindow))
CreateMenu "&amp;New",101,filemenu,KEY_N,MODIFIER_COMMAND
CreateMenu "&amp;Open file",102,filemenu,KEY_O,MODIFIER_COMMAND
CreateMenu "&amp;Close file",103,filemenu,KEY_C,MODIFIER_COMMAND
CreateMenu"",0,filemenu
CreateMenu "&amp;Save",104,filemenu,KEY_S,MODIFIER_COMMAND
CreateMenu"",0,filemenu
CreateMenu "&amp;Open bitmap image",105,filemenu,KEY_I,MODIFIER_COMMAND
CreateMenu"",0,filemenu
CreateMenu "&amp;Exit",106,fileMenu,KEY_F4,MODIFIER_COMMAND

editmenu=CreateMenu("&amp;Edit          ",0,WindowMenu(MyWindow))
CreateMenu "Cu&amp;t",107,editmenu,KEY_X,MODIFIER_COMMAND
CreateMenu "&amp;Copy",108,editmenu,KEY_C,MODIFIER_COMMAND
CreateMenu "&amp;Paste",109,editmenu,KEY_V,MODIFIER_COMMAND

helpmenu=CreateMenu("&amp;Help          ",0,WindowMenu(MyWindow))
CreateMenu "&amp;About",110,helpmenu

canvas=CreateCanvas(0,0,800,640,MyWindow)
SetGraphics CanvasGraphics(canvas)
SetClsColor 255,128,64

UpdateWindowMenu MyWindow

AddHook EmitEventHook, eventHandler

Repeat;WaitSystem();Forever

Function eventHandler:Object( pID%, pData:Object, pContext:Object )
	
	Local pEvent:TEvent = TEvent(pData)
	If Not pEvent Then Return pData
	
	Select pEvent.id
		Case EVENT_WINDOWCLOSE, EVENT_APPTERMINATE
			End
		Case EVENT_GADGETPAINT
			Cls
			Flip
		Case EVENT_MENUACTION
			Select pEvent.data
				Case 106
					End
				Case 110
					Notify "About stuff here"
			End Select
	End Select
	
	Return Null
	
EndFunction
</textarea> <br><br></td></tr></table><br>
<a name="881952"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >iprice</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes. That works exactly as it should. Thankyou :) <br><br></td></tr></table><br>
<a name="882874"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >iprice</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've encountered another problem with this. Try as i might, I can't get the EventX(), EventY() from EVENT_MOUSEMOVE or the value returned from EVENT_MOUSEDOWN or EVENT_MOUSEUP.<br><br>I can create a canvas easily enough, which does report the outcomes, but the above code, with a CASE EVENT_MOUSExxx doesn't return values, no matter where I put it. <br><br>I've rewritten code a dozen times and I can't get menus AND canvas both working properly at the same time. I need to be able to draw on the screen, receive mouse position and press data and access menus (not all at the same time though!).<br><br>Further help and pointers would be greatly appreciated. <br><br></td></tr></table><br>
<a name="882917"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Try as i might, I can't get the EventX(), EventY() from EVENT_MOUSEMOVE or the value returned from EVENT_MOUSEDOWN or EVENT_MOUSEUP. <br></div><br>You can't use <b>EventX()</b>, <b>EventY()</b> or <b>EventData()</b> to get info about an event processed in an event <u>hook</u> - these functions return values for the first event in an event <u>queue</u>, which we aren't processing inside <b>eventHandler()</b> (the event hasn't even been added to the queue yet).<br><br>As such, you have to use the <b>TEvent</b> object fields - look at the select block: it uses <b>pEvent.id</b> instead of <b>EventID()</b>.<br><br>The equivalent fields for <b>TEvent</b> (which should be used instead inside event <u>hooks</u>) are:<br><br>&gt; <b>EventData()</b> - pEvent.data<br>&gt; <b>EventX()</b> - pEvent.x<br>&gt; <b>EventY()</b> - pEvent.y <br><br></td></tr></table><br>
<a name="882928"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >iprice</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Err...<br><br>Whilst I'm used to coding in Max, this GUI stuff is all nonsense to me. The whole "you can't use menus and canvas at the same time without hooks" leaves me completely blank.<br><br>I understand what you're saying about pEvent.X and such, but on putting them in, I can only get them to register mouse position when I click on menus. I need the mouse position MORE than I need the menus, but I still need both.<br><br>I am a GUI noob, and proud of it ;) <br><br></td></tr></table><br>
<a name="882938"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sorry, but I don't have too much time at the moment so I can't explain in detail, but its because clicking a menu puts the system into a modal loop where your program's execution is effectively halted until an item is selected. However, modal loops have special callback functions which allow a program to run a specific function as the loop is happening. Adding an event hook basically adds your function to the list that is called while inside the modal loop. This is therefore the only way we can paint to a canvas while a menu is open, by coding our event handler into one of the callback functions.<br><br>I can highly recommend Assari's EventHooks tutorial as I found it really easy to understand when I first found out about hooks:<br><br><a href="http://www.2dgamecreators.com/maxgui/T15-Eventhooks.html" target="_blank">Assari's EventHooks Tutorial</a><br><br>Anyway, this example seems to work fine for me:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Local style = WINDOW_TITLEBAR | WINDOW_MENU  | WINDOW_CLIENTCOORDS

Global MyWindow:TGadget
Global filemenu:TGadget
Global editmenu:TGadget
Global helpmenu:TGadget

Global canvas:TGadget

MyWindow=CreateWindow("Max GUI App.", 112,84,800,600,Null,style)

filemenu=CreateMenu("&amp;File          ",0,WindowMenu(MyWindow))
CreateMenu "&amp;New",101,filemenu,KEY_N,MODIFIER_COMMAND
CreateMenu "&amp;Open file",102,filemenu,KEY_O,MODIFIER_COMMAND
CreateMenu "&amp;Close file",103,filemenu,KEY_C,MODIFIER_COMMAND
CreateMenu"",0,filemenu
CreateMenu "&amp;Save",104,filemenu,KEY_S,MODIFIER_COMMAND
CreateMenu"",0,filemenu
CreateMenu "&amp;Open bitmap image",105,filemenu,KEY_I,MODIFIER_COMMAND
CreateMenu"",0,filemenu
CreateMenu "&amp;Exit",106,fileMenu,KEY_F4,MODIFIER_COMMAND

editmenu=CreateMenu("&amp;Edit          ",0,WindowMenu(MyWindow))
CreateMenu "Cu&amp;t",107,editmenu,KEY_X,MODIFIER_COMMAND
CreateMenu "&amp;Copy",108,editmenu,KEY_C,MODIFIER_COMMAND
CreateMenu "&amp;Paste",109,editmenu,KEY_V,MODIFIER_COMMAND

helpmenu=CreateMenu("&amp;Help          ",0,WindowMenu(MyWindow))
CreateMenu "&amp;About",110,helpmenu

canvas=CreateCanvas(0,0,800,640,MyWindow)
SetGraphics CanvasGraphics(canvas)
SetClsColor 255,128,64

UpdateWindowMenu MyWindow

AddHook EmitEventHook, eventHandler

Repeat;WaitSystem();Forever

Global mousePosX%, mousePosY%, mouseButton

Function eventHandler:Object( pID%, pData:Object, pContext:Object )
	
	Local pEvent:TEvent = TEvent(pData)
	If Not pEvent Then Return pData
	
	Select pEvent.id
		Case EVENT_WINDOWCLOSE, EVENT_APPTERMINATE
			End
		Case EVENT_MOUSEDOWN
			mouseButton = pEvent.data
			RedrawGadget(canvas)
		Case EVENT_MOUSEUP
			mouseButton = False
			RedrawGadget(canvas)
		Case EVENT_MOUSEMOVE
			mousePosX = pEvent.x;mousePosY = pEvent.y
			mouseButton = pEvent.data
			RedrawGadget(canvas)
		Case EVENT_GADGETPAINT
			Cls
			If mouseButton = MOUSE_LEFT Then SetColor 0,255,0 ElseIf mouseButton = MOUSE_RIGHT Then SetColor 0,0,255
			DrawOval( mousePosX-5, mousePosY-5, 10, 10 )
			SetColor 255,255,255
			DrawText( mousePosX + ", " + mousePosY, mousePosX + 5, mousePosY + 5 )
			Flip
		Case EVENT_MENUACTION
			Select pEvent.data
				Case 106
					End
				Case 110
					Notify "About stuff here"
			End Select
	End Select
	
	Return Null
	
EndFunction</textarea><br><br>Does this example work as expected for you too? <br><br></td></tr></table><br>
<a name="882943"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >iprice</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Doh!<br><br>My code (after your initial pointer) was only missing the redrawgadget(canvas) command during the mouse events, otherwise it was pretty much the same.<br><br>I was redrawing the canvas within the gadgetpaint event only.<br><br>BTW I have been going through the tuts but haven't had time to get to the hook one yet. Perhaps I should have started there!<br><br>Many thanks. :) <br><br></td></tr></table><br>
<a name="882949"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jsp</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yep, i think that's the point. Only the GadgetPaint is missing and to make it not too complicated we can put only that one in a hook function. All your other stuff can be used then as normal...<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
Local Style:Int = WINDOW_TITLEBAR | WINDOW_MENU  | WINDOW_CLIENTCOORDS

Local MyWindow:TGadget
Local filemenu:TGadget
Local editmenu:TGadget
Local helpmenu:TGadget

Global canvas:TGadget

MyWindow=CreateWindow("Max GUI App.", 112,84,800,600,Null,Style)

filemenu=CreateMenu("&amp;File          ",0,WindowMenu(MyWindow))
CreateMenu "&amp;New",101,filemenu,KEY_N,MODIFIER_COMMAND
CreateMenu "&amp;Open file",102,filemenu,KEY_O,MODIFIER_COMMAND
CreateMenu "&amp;Close file",103,filemenu,KEY_C,MODIFIER_COMMAND
CreateMenu"",0,filemenu
CreateMenu "&amp;Save",104,filemenu,KEY_S,MODIFIER_COMMAND
CreateMenu"",0,filemenu
CreateMenu "&amp;Open bitmap image",105,filemenu,KEY_I,MODIFIER_COMMAND
CreateMenu"",0,filemenu
CreateMenu "&amp;Exit",106,fileMenu,KEY_F4,MODIFIER_COMMAND

editmenu=CreateMenu("&amp;Edit          ",0,WindowMenu(MyWindow))
CreateMenu "Cu&amp;t",107,editmenu,KEY_X,MODIFIER_COMMAND
CreateMenu "&amp;Copy",108,editmenu,KEY_C,MODIFIER_COMMAND
CreateMenu "&amp;Paste",109,editmenu,KEY_V,MODIFIER_COMMAND

helpmenu=CreateMenu("&amp;Help          ",0,WindowMenu(MyWindow))
CreateMenu "&amp;About",110,helpmenu

canvas=CreateCanvas(0,0,800,640,MyWindow)

UpdateWindowMenu MyWindow

AddHook EmitEventHook , MyHook

While True
	WaitEvent 
	Select EventID()
		Case EVENT_MENUACTION
			Select EventData()
			
				Case 106
					End

				Case 110
					Notify "About stuff here"
			End Select
	End Select
Wend

Function MyHook:Object(iId:Int,tData:Object,tContext:Object)
	Local Event:TEvent=TEvent(tData)
  	If event = Null Return Null
	
	If (Event.ID = EVENT_GADGETPAINT) Or (Event.ID = EVENT_WINDOWSIZE) Or (Event.ID = EVENT_WINDOWMOVE) Or (Event.ID = Event_AppResume) Then 
		'ReDraw the canvas when requested (GadgetPaint) or when the window was moved, sized, resumed..
		ReDraw()
	End If
	
	Return tData
End Function

Function ReDraw() 
	'Call the ReDraw every time you want to update the canvas
	SetGraphics CanvasGraphics (canvas)
	SetViewport 0,0,GadgetWidth(canvas),GadgetHeight(canvas)
	Cls
	'
	'Put all your Canvas drawing stuff here...
	'
	Flip
End Function

</textarea> <br><br></td></tr></table><br>
<a name="882967"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >iprice</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cheers jsp - I'll have a play with that tomorrow :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
