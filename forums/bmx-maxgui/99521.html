<!DOCTYPE html><html lang="en" ><head ><title >test of virtual canvas for max2d</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >test of virtual canvas for max2d</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >test of virtual canvas for max2d</a><br><br>
<a name="1168545"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bobysait</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I wish some people could help me to test this part of code<br>It user FrameBuffer Object to draw on fake canvas<br>(allows to draw with Max2D commands, Drawimage included)<br><br>I need to know if it works for every body without any troubles<br><br>- It download an image from internet, but you can replace it with any image on your computer<br>(just try to use a png with alpha for the alpha test, else you should have to import the loader &gt; import brl.jpgloader for exemple)<br><br><br>ps : ATM the TCanvas SDK is really for debug purpose only<br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

Framework brl.glmax2d
Import pub.glew
Import brl.retro
Import brl.PNGLoader

Type TCanvas
	Field _tex:Int[] = [-1]
	Field _fbo:Int[] = [-1]
	Field _rbo:Int[] = [-1]
	Field _w:Int, _h:Int
	Field _us:Float = 1.0
	Field _vs:Float = 1.0
	Field _mp:Float[16]
	
	Function Create:TCanvas(w:Int,h:Int)
		Local b:TCanvas = New TCanvas
		If Not(b.Resize(w,h)) Then Return Null
		Return b
	End Function
	
	Function Min_:Float(a:Float,b:Float);If b&lt;a ; Return b; Else; Return a
	End Function
	Function Max_:Float(a:Float,b:Float);If b&gt;a ; Return b; Else; Return a
	End Function

	Method Resize:Byte(w:Int, h:Int)
		Local maxs:Int[1];glGetIntegerv(GL_MAX_RENDERBUFFER_SIZE_EXT, VarPtr maxs[0])
		_w=Min_(w,maxs[0]); _h=Min_(h,maxs[0])
		' create FBO
		If _fbo Then	glDeleteFramebuffersEXT	(1, VarPtr _fbo[0])
						glGenFramebuffersEXT	(1, VarPtr _fbo[0])
						glBindFramebufferEXT	(GL_FRAMEBUFFER_EXT, _fbo[0])
		
		' create and attach a new texture as the FBO's color buffer
		If _tex Then	glDeleteTextures		(1, VarPtr _tex[0])
						glGenTextures			(1, VarPtr _tex[0])
						glBindTexture			(GL_TEXTURE_2D, _tex[0])
						glTexParameteri			(GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER, GL_LINEAR)
						glTexParameteri			(GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_LINEAR)
						glTexImage2D			(GL_TEXTURE_2D, 0, GL_RGBA8, _w, _h,..
												 0, GL_RGBA, GL_UNSIGNED_BYTE, Null)
						glFramebufferTexture2DEXT(GL_FRAMEBUFFER_EXT, GL_COLOR_ATTACHMENT0_EXT, ..
												GL_TEXTURE_2D, _tex[0], 0)
		
		' create and attach a new depth buffer to currently bound FBO
		If _rbo Then	glDeleteRenderbuffersEXT(1, VarPtr _rbo[0])
						glGenRenderbuffersEXT	(1, VarPtr _rbo[0])
						glBindRenderbufferEXT	(GL_RENDERBUFFER_EXT, _rbo[0])
						glRenderbufferStorageEXT(GL_RENDERBUFFER_EXT, GL_DEPTH_COMPONENT, _w, _h)
						glFramebufferRenderbufferEXT(GL_FRAMEBUFFER_EXT, GL_DEPTH_ATTACHMENT_EXT, ..
												GL_RENDERBUFFER_EXT, _rbo[0])
		
		' unbind fbo
		glBindFramebufferEXT(GL_FRAMEBUFFER_EXT, 0)
		_us = 1.0
		_vs = 1.0
		
		' save matrix for faster access
		Local cur:Float[16]
		glMatrixMode		( GL_PROJECTION )
		glGetFloatv			( GL_PROJECTION_MATRIX, cur )
		
			glLoadIdentity	( )
			glOrtho			( 0, _w, _h, 0, -1, 1 )
			glGetFloatv		( GL_PROJECTION_MATRIX, _mp )
			
			glLoadMatrixf	( cur )
			
		glMatrixMode		( GL_MODELVIEW )
		
		Return Check()
	End Method
	
	Global _gViewport:Int[4]
	Global _gOrigin:Float[2]
	Global _gHandle:Float[2]
	Global _gScale:Float[2]
	Global _gRot:Float
	Global _Current:TCanvas = Null
	Global _gblend:Int = 0
	Global _galpha:Float = 1.0
	Global _gcolr:Int = 255
	Global _gcolg:Int = 255
	Global _gcolb:Int = 255
	Global _gclsr:Int = 255
	Global _gclsg:Int = 255
	Global _gclsb:Int = 255
	Global _gmaskr:Int = 255
	Global _gmaskg:Int = 255
	Global _gmaskb:Int = 255
	
	' adjust viewport and projection matrix to texture dimension
	Method Update()
		
		If _Current = Null
			' capture max2d viewport
			GetViewport			( _gViewport[0],_gViewport[1],_gViewport[2],_gViewport[3] )
			GetOrigin			( _gOrigin[0], _gOrigin[1] )
			GetHandle			( _gHandle[0], _gHandle[1] )
			GetScale			( _gScale[0], _gScale[1] )
			_grot			=	GetRotation ( )
			' capture attribs
			_gblend			=	GetBlend	( )
			_galpha			=	GetAlpha	( )
			GetColor			( _gcolr, _gcolb,_gcolb )
			GetClsColor			( _gclsr, _gclsb,_gclsb )
			GetMaskColor		( _gmaskr, _gmaskb,_gmaskb )
		EndIf
		
		glMatrixMode( GL_PROJECTION ) ; glLoadMatrixf( _mp )
		glMatrixMode( GL_MODELVIEW )  ; glLoadIdentity( )
		glViewport	( 0, 0, _w, _h )
		
	End Method
	
	' restore max2d context
	Function Restore()	
		' restore max2d matrix
		GLMax2DDriver().ResetGLContext(TMax2DGraphics.Current())
		' restore max2d params (origin, viewport, etc ...)
		SetViewport			( _gViewport[0],_gViewport[1],_gViewport[2],_gViewport[3] )
		SetOrigin			( _gOrigin[0],_gOrigin[1] )
		SetHandle			( _gHandle[0], _gHandle[1] )
		SetScale			( _gScale[0], _gScale[1] )
		SetRotation			( _grot )
		SetBlend			( _gblend )
		SetAlpha			( _galpha )
		SetColor			( _gcolr, _gcolb,_gcolb )
		SetClsColor			( _gclsr, _gclsb,_gclsb )
		SetMaskColor		( _gmaskr, _gmaskb,_gmaskb )
	End Function
	
	Method Bind()
		glBindFramebufferEXT(GL_FRAMEBUFFER_EXT,_fbo[0])
	End Method
	
	Function UnBind()
		glBindFramebufferEXT(GL_FRAMEBUFFER_EXT,0)
	End Function
	
	Function Free:TCanvas(canvas:TCanvas)
		Reset()
		glDeleteTextures		(1, canvas._tex)
    	glDeleteFramebuffersEXT	(1, canvas._fbo)
    	glDeleteRenderbuffersEXT(1, canvas._rbo)
		Return Null
	End Function
		
	' restore global OpenGL setup
	Function Reset:Byte()
		UnBind()
		Restore()
		_Current = Null
		Return True
	End Function
	
	' set renderbuffer as current graphics mode
	Method Set:Byte()
		Bind()
		Update()
		_Current = Self
		Return True
	End Method
	
	Method Check:Byte()
		Return glCheckFramebufferStatusEXT(GL_FRAMEBUFFER_EXT)=GL_FRAMEBUFFER_COMPLETE_EXT
	End Method
	
	' draw the RenderBuffer as a textured rect
	Method Draw (x:Float, y:Float)
		Local gc:TMax2DGraphics = TMax2DGraphics.Current()
		Local x0#=gc.handle_x, y0#=gc.handle_y
		Local tx#=x+gc.origin_x, ty#=y+gc.origin_y
		Local ix#=gc.tform_ix, iy#=gc.tform_iy
		Local jx#=gc.tform_jx, jy#=gc.tform_jy
		Local x1#=x0+_w, y1#=y0+_h
		glEnable		( GL_TEXTURE_2D )
		glBindTexture	( GL_TEXTURE_2D, _tex[0] )
			glBegin GL_QUADS
				glTexCoord2f( 0.0, _vs )
				glVertex3f	( x0*ix+y0*iy+tx,x0*jx+y0*jy+ty, 0.0 )
				glTexCoord2f(_us, _vs )
				glVertex3f	( x1*ix+y0*iy+tx,x1*jx+y0*jy+ty, 0.0 )
				glTexCoord2f(_us, 0.0 )
				glVertex3f	( x1*ix+y1*iy+tx,x1*jx+y1*jy+ty, 0.0 )
				glTexCoord2f( 0.0, 0.0 )
				glVertex3f	( x0*ix+y1*iy+tx,x0*jx+y1*jy+ty, 0.0 )
			glEnd
		
		glBindTexture	( GL_TEXTURE_2D, 0 )
		glDisable		( GL_TEXTURE_2D )
	End Method
	
	Method Width:Int() Return _w ;End Method
	
	Method Height:Int() Return _h ;End Method
	
End Type


Function CreateCanvas:TCanvas(w:Int,h:Int)
	Return TCanvas.Create(w,h)
End Function

Function SetCanvas:Byte(Canvas:TCanvas)
	If Canvas=Null
		Return TCanvas.Reset()
	End If
	Return canvas.Set()
End Function

Function ResizeCanvas:Byte(Canvas:TCanvas,w:Int,h:Int)
	Return canvas.Resize(w,h)
End Function

Function ReleaseCanvas:TCanvas(Canvas:TCanvas var)
	Canvas = TCanvas.Free(Canvas)
	Return Null
End Function

Function DrawCanvas(Canvas:TCanvas, x:Float,y:Float)
	Canvas.Draw(x,y)
End Function













Global screen_width:Int = 1024;
Global screen_height:Int = 768;

SetGraphicsDriver GLMax2DDriver()
Graphics screen_width,screen_height,0
glewInit()
SetMaskColor(255, 0, 255)

    If(Not(GL_EXT_framebuffer_object))
        Throw "Requires EXT_framebuffer_object"
        End
    EndIf
	
	Local st:Int	=	MilliSecs()-17
	Local mt:Int	=	0
	Local fpst:Int = mt, fps:Int = 0, fpsc:Int = 0
	
	Local img:TImage
	img = LoadImage(LoadBank("http::www.abload.de/img/b-maxckcy4.png"))
	
	Local rx:Float = 0, rw:Int=128, rh:Int=128
	Local ry:Float = 0
	Local hx:Int = 64, hy:Int=64
	Local Canvas:TCanvas = CreateCanvas(rw,rh)
	Local angle:Float = 0.0
	
	Repeat
		
	    If KeyHit(KEY_ESCAPE) Then Exit
		angle :+ .1
		
		' update fps
			mt = MilliSecs()-st
			fpsc:+1 ; If fpst&lt;mt Then fpst:+1000;fps=fpsc;fpsc=0
			
		' control camera
			If MouseHit(1)
				hx=MouseX()-rx
				hy=MouseY()-ry
			ElseIf MouseDown(1)
				rx=MouseX()-hx
				ry=MouseY()-hy
		    EndIf
			
			If MouseDown(2)
				rw = MouseX()-rx
				rh = MouseY()-ry
				If rw&lt;16 Then rw=16
				If rh&lt;16 Then rh=16
				Canvas.Resize(rw,rh)
			End If
			
		SetClsColor		( 010,020,040 )
		Cls				( )
		
		' render using fbo
			SetCanvas	( Canvas )
			SetColor	( 255,255,000 )
			DrawLine	( 0,0,rw-1,0 )
			DrawLine	( 0,0,0,rh-1 )
			DrawLine	( 0,rh-1,rw,rh-1 )
			DrawLine	( rw-1,0,rw-1,rh-1 )
			SetColor	( 255,255,255 )
			SetClsColor	( 0,0,0 )
			
			'Cls()
			glClearColor 1,1,1,1.0 ' enable to clear the background alpha
			
			SetColor	( 255,220,220 )
			DrawRect	( 5,5,rw-10,100 )
			DrawRect	( 5,rh-105,rw-10,100 )
			SetColor	( 000,000,000 )
			DrawOval	( 80,30,ImageWidth(img)+10,ImageHeight(img)-20 )
			SetColor	( 255,255,255 )
			DrawImage	( img, 90,35 )
			
			SetColor	( 001,001,001 )
			DrawRect	( 10,20,rw-20,60 )
			DrawRect	( 10,rh-80,rw-20,60 )
			
			SetColor	( 255,255,255 )
			SetScale	( 2.0,2.0 )
			DrawText	( "CanvasGraphics", 25,35 )
			
		' back to normal viewport and projection matrix
			SetCanvas	( Null )
			
			SetOrigin	( 0,0 )
			SetColor	( 255,200,100 )
			SetScale	( 3.0, 3.0 )
			DrawText	( "backbuffer() draw before canvas", 5,100 )
			SetScale	( 1.0, 1.0 )
			
			SetColor	( 255,255,000 )
			SetClsColor	( 0, 100, 0 )
			
			SetBlend	( ALPHABLEND )
			SetColor	( 001,001,001 )
			For Local d:Float = 1 To 5
				SetAlpha( .6-Cos(-90+d*90/5)*.6 )
				DrawCanvas	( Canvas,rx+d*4,ry+d*4 )
			Next
			SetBlend	( LIGHTBLEND )
			SetColor	( 255,255,255 )
			SetAlpha	( 1.0 )
			DrawCanvas	( Canvas,rx,ry )
			
			SetBlend	( ALPHABLEND )
			SetColor	( 0,0,0 )
			DrawRect	( 0,0,GraphicsWidth(),80 )
			DrawRect	( 0,GraphicsHeight()-80,GraphicsWidth(),80 )
			
			SetColor	( 255,255,255 )
			DrawText	( "texSize  : "+Canvas.Width()+","+Canvas.Height(), 10,10)
			DrawText	( "Fps      : "+fps, 10,40)
			SetScale	( 3.0, 3.0 )
			DrawText	( "- Text and Borders drawn after canvas -", 30,GraphicsHeight()-50 )
			SetScale	( 1.0, 1.0 )
			
	    Flip False
			
	Forever
	
	' --------- End
	ReleaseCanvas(Canvas)
	
	End
</textarea> <br><br></td></tr></table><br>
<a name="1168575"></a>

<a name="1168576"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> - The LoadImage command did not work for your abload-link (had to use a local "download" of that image<br><br>fullsize image:<br><img src="http://www.abload.de/img/virtualcanvash6p84.png"><br><br><br><br>Test system: linux mint 13 64bit, amd llano with integrated gpu.<br><br><br>bye<br>Ron<br><br>edit: image link corrected<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1168594"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bobysait</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I forgot to modify a the alpha flag for glclear<br><br>&gt; glClearColor 1,1,1,1.0 should be glClearColor 1,1,1,0.0 (to have an alpha channel on the renderbuffer)<br><br>Well, it seems to work as well on linux, thanks for testing. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
