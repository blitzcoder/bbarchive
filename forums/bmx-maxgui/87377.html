<!DOCTYPE html><html lang="en" ><head ><title >menu events</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >menu events</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >menu events</a><br><br>
<a name="990192"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CloseToPerfect</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have multiple windows and potentially multiple menus.  I am trying to capture menu events from the specific window but it doesn't catch the EVENT_MENU ACTION.<br><br>Code 1<br><pre class=code>
Repeat
	WaitEvent()
	Select EventSource()
	Case mainWindow
		Select EventID()
		Case EVENT_WINDOWCLOSE
			End
		Case EVENT_MENUACTION
			Print "menu action"
			Select EventData()
			Case MENU_EXIT
				End
			EndSelect
		End Select
	End Select
Forever
</pre><br><br>I can only capture the menu actions from outside the SourceID() like this<br><pre class=code>
Repeat
	WaitEvent()
	Select EventSource()
	Case mainWindow
		Select EventID()
		Case EVENT_WINDOWCLOSE
			End
		End Select
	End Select
	Select EventID()
	Case EVENT_MENUACTION
		Select EventData()
		Case MENU_EXIT
			End
		EndSelect
	End Select
Forever
</pre><br><br>So am I right in thinking that if I have multiple menus in multiple windows that I have to check all the menus in one EventID() outside of specific EventSource() for local windows?<br><br>This isn't a problem if this is the way it works, I was just assuming that a menu tied to a window would generate EventID() under the EventSource of the window it is in.<br><br>John <br><br></td></tr></table><br>
<a name="990199"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> The source I think is the menu gadget handle returned by CreateMenu not the parent window gadget.<br><br>CreateMenu example modified with extra logging:<br><pre class=code>
' createmenu.bmx

Import MaxGui.Drivers

Strict 

Local window:TGadget
Local filemenu:TGadget
Local editmenu:TGadget
Local helpmenu:TGadget

Const MENU_NEW=101
Const MENU_OPEN=102
Const MENU_SAVE=103
Const MENU_CLOSE=104
Const MENU_EXIT=105

Const MENU_CUT=106
Const MENU_COPY=107
Const MENU_PASTE=108

Const MENU_ABOUT=109

window=CreateWindow("My Window",40,40,320,240)

filemenu=CreateMenu("&amp;File",0,WindowMenu(window))
CreateMenu"&amp;New",MENU_NEW,filemenu,KEY_N,MODIFIER_COMMAND
CreateMenu"&amp;Open",MENU_OPEN,filemenu,KEY_O,MODIFIER_COMMAND
CreateMenu"&amp;Close",MENU_CLOSE,filemenu,KEY_W,MODIFIER_COMMAND
CreateMenu"",0,filemenu
CreateMenu"&amp;Save",MENU_SAVE,filemenu,KEY_S,MODIFIER_COMMAND
CreateMenu"",0,filemenu
CreateMenu"E&amp;xit",MENU_EXIT,filemenu,KEY_F4,MODIFIER_COMMAND

editmenu=CreateMenu("&amp;Edit",0,WindowMenu(window))
CreateMenu "Cu&amp;t",MENU_CUT,editmenu,KEY_X,MODIFIER_COMMAND
CreateMenu "&amp;Copy",MENU_COPY,editmenu,KEY_C,MODIFIER_COMMAND
CreateMenu "&amp;Paste",MENU_PASTE,editmenu,KEY_V,MODIFIER_COMMAND

helpmenu=CreateMenu("&amp;Help",0,WindowMenu(window))
CreateMenu "&amp;About",MENU_ABOUT,helpmenu

UpdateWindowMenu window

While True
	WaitEvent 
	If CurrentEvent.source
	 DebugLog CurrentEvent.ToString()+" src="+CurrentEvent.source.tostring()
	EndIf
	Select EventID()
		Case EVENT_WINDOWCLOSE
			End
		Case EVENT_MENUACTION
			Select EventData()
				Case MENU_EXIT
					End
				Case MENU_ABOUT
					Notify "Incrediabler~n(C)2005 Incredible Software"
			End Select
	End Select
Wend
</pre> <br><br></td></tr></table><br>
<a name="990218"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CloseToPerfect</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think I get it now, the menu event doesn't come from a window source.<br><br>Which is why this code doesn't work.<br><pre class=code>
Import MaxGui.Drivers

Strict 
Local window1:TGadget=CreateWindow("Window 1",0,0,200,200)
Local window2:TGadget=CreateWindow("Window 2",250,0,200,200)

Local menu1:TGadget=CreateMenu("Menu 1",0,WindowMenu(window1))
Const item1=100
Local menuItem1:TGadget=CreateMenu("Item 1",item1,menu1)
UpdateWindowMenu window1

Local menu2:TGadget=CreateMenu("Menu 2",0,WindowMenu(window2))
Const item2=200
Local menuItem2:TGadget=CreateMenu("Item 2",item2,menu2)
UpdateWindowMenu window2

Repeat 
WaitEvent
Select EventSource()
Case window1
	Select EventID()
	Case EVENT_WINDOWCLOSE
		End
	Case EVENT_MENUACTION
		Print"Menu 1 action"
	End Select
Case window2
	Select EventID()
	Case EVENT_WINDOWCLOSE
		End
	Case EVENT_MENUACTION
		Print"Menu 2 action"
	End Select
End Select
Forever
</pre><br><br>But then you would think that you could source a menu but this doesn't work either?<br><pre class=code>
Repeat 
WaitEvent
Select EventSource()
Case window1
	Select EventID()
	Case EVENT_WINDOWCLOSE
		End
	End Select
Case window2
	Select EventID()
	Case EVENT_WINDOWCLOSE
		End
	End Select	
Case menu1
	Select EventID()
	Case EVENT_MENUACTION
		Print"Menu 1 action"
	End Select
Case menu2
	Select EventID()
	Case EVENT_MENUACTION
		Print"Menu 2 action"
	End Select	
End Select
Forever
</pre><br><br>so I guess not all TGadets generate SourceID information. <br><br></td></tr></table><br>
<a name="990240"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Menus with children don't generate events. If you, however, replace menu1 with menuItem1, you should find that the sub-menu generates an event. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
