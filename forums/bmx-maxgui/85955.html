<!DOCTYPE html><html lang="en" ><head ><title >Why aren't my trackbar controls responsive?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Why aren't my trackbar controls responsive?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >Why aren't my trackbar controls responsive?</a><br><br>
<a name="973183"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >sswift</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> My trackbar controls aren't as responsive as the regular sliders in a window are.  The movment of a regular slider is super smooth, whereas the trackbar feels almost like it's being updated at a lower framerate.<br><br>Changing the maximum value doesn't seem to have any effect.  MSDN indicates sliders have some kinda tick incremnent which can be set, but Blitzmax appears to provide no interface for this.  I wonder if perhaps internally BlitzMax doesn't set this value high enough. <br><br></td></tr></table><br>
<a name="973189"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Can you post an example?<br><br>Are you using an event queue or an event hook?<br><br>Also, iirc, the tick increment for all the slider controls is set to 1. <br><br></td></tr></table><br>
<a name="973192"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >sswift</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm not using either.  I'm just laying out the gadgets right now.<br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

SuperStrict
Import MaxGUI.Drivers

'Global Font_ArialBlack

Global Application_Title$

Global WINDOW_Main:TGadget

	Global MENU_Root:TGadget
		Global MENU_File:TGadget
			Global MENU_Open:TGadget
			Global MENU_Save:TGadget
			Global MENU_Batch:TGadget
			Global MENU_Quit:TGadget
		Global MENU_Help:TGadget
			Global MENU_Contents:TGadget
			Global MENU_About:TGadget
		Global MENU_Buynow:TGadget
				
	Global CANVAS_Preview:TGadget
	Global PANEL_PreviewPaneIndent:TGadget, PANEL_PreviewPane:TGadget, TABBER_PreviewPane:TGadget, HSLIDER_PreviewPane:TGadget, VSLIDER_PreviewPane:TGadget
	Global PROGBAR_Progressbar:TGadget
	Global PANEL_Indent:TGadget, PANEL_Controls:TGadget, VSLIDER_Controls:TGadget
			
	' Control panel:
	
		Global TEXTFIELD_HBlend:TGadget
		Global HSLIDER_HBlend:TGadget

		Global TEXTFIELD_VBlend:TGadget
		Global HSLIDER_VBlend:TGadget

		Global TEXTFIELD_ShadingStrength:TGadget
		Global HSLIDER_ShadingStrength:TGadget

		Global TEXTFIELD_ShadingBlurRadius:TGadget
		Global HSLIDER_ShadingBlurRadius:TGadget

		Global TEXTFIELD_Sharpness:TGadget
		Global HSLIDER_Sharpness:TGadget

		Global TEXTFIELD_FinalWidth:TGadget
		Global TEXTFIELD_FinalHeight:TGadget

		Global BUTTON_MakeTexture:TGadget	
		Global BUTTON_AutoUpdate:TGadget


' Default image adjustment settings.
	
		Global HBlendRadius_Default# = 1 
		Global VBlendRadius_Default# = 1

		Global ShadingStrength_Default#   = 1
		Global ShadingBlurRadius_Default# = 64
		
		Global Sharpness_Default# = 0

		Global Quad_X#[4]				
		Global Quad_Y#[4]
		
		Global FinalWidth_Default%  = 512
		Global FinalHeight_Default% = 512
		
		
	' Current image adjustment settings.	
		Global FinalWidth%  = FinalWidth_Default
		Global FinalHeight% = FinalHeight_Default
	
		Global Seamless_U% = True
		Global Seamless_V% = True


	' Min and max values for all the image adjustment controls.		
		Global HBlendRadius_Min# = 0 
		Global HBlendRadius_Max# = 1
		Global HBlendRadius_SliderSteps% = 100

		Global VBlendRadius_Min# = 0 
		Global VBlendRadius_Max# = 1 
		Global VBlendRadius_SliderSteps% = 100

		Global ShadingStrength_Min# = 0
		Global ShadingStrength_Max# = 1
		Global ShadingStrength_SliderSteps% = 100
	
		Global ShadingBlurRadius_Min# = 1
		Global ShadingBlurRadius_Max# = 256
		Global ShadingBlurRadius_SliderSteps% = 255

		Global Sharpness_Min# = 0
		Global Sharpness_Max# = 1
		Global Sharpness_SliderSteps% = 100
               	
		Global FinalWidth_Min% = 2
		Global FinalWidth_Max% 
		
		Global FinalHeight_Min% = 2
		Global FinalHeight_Max% 


Init()

Repeat
	Delay(100)
Until KeyHit(KEY_ESCAPE)

' -----------------------------------------------------------------------------------------------------------------------------------
' This function initializes the application.
' -----------------------------------------------------------------------------------------------------------------------------------
Function Init()
	
	Const SIZE_LABEL%     = 18
	Const SIZE_TEXTFIELD% = 22
	Const SIZE_SLIDER%    = 22
	Const SIZE_BUTTON%    = 24
	Const SIZE_PROGBAR%   = 22
	Const SIZE_SEPARATOR% = 8
	Const SIZE_SPACE%     = 8
	
	Local CPWidth%, CPHeight%, CPx%, CPy%, CPFullHeight%
	Local LABEL:TGadget
		
	' Create the window at the smallest size it can be, then maximize it.
		
		WINDOW_Main = CreateWindow(Application_Title$, 0, 0, 640, 480) 
		MaximizeWindow WINDOW_Main

	' Save the size of the area inside the window in which we can draw.
		
		'WINDOW_Width  = ClientWidth(WINDOW_Main)
		'WINDOW_Height = ClientHeight(WINDOW_Main)

	Rem 
	' Load the watermark.
		If VERSION = EVALULATION
		
			' Load the watermark.
				CopyFile "config.dat", "config.png" 
				IMAGE_Watermark = LoadImage("config.png")
				DeleteFile "config.png"
							
			' If the watermark could not be loaded, pop up an error and exit the program.
				If IMAGE_Watermark = 0 
					Notify("Error: CONFIG.DAT not found.  Please reinstall the program.", True)
					End
				EndIf

			' If the watermark has the wrong CRC, exit with an error.
				
				' DebugLog(CRC_File("config.dat"))
				
				If CRC_File("config.dat") &lt;&gt; 669102732
					Notify("Error: CONFIG.DAT has been corrupted.  Please reinstall the program.")
					End
				EndIf  
						

		EndIf
		
	' Load fonts.
	'	FONT_ArialBlack   = LoadFont("arial black", 32)
		
	End Rem
	
	' Create the menus.	
	
		MENU_Root = WindowMenu(WINDOW_Main)
	
			MENU_File = CreateMenu("File", 0, MENU_Root)
		
				MENU_Open = CreateMenu("Open...", 1, MENU_File, KEY_O, MODIFIER_COMMAND)			
				CreateMenu("", 0, MENU_File)			
				MENU_Save = CreateMenu("Save...", 5, MENU_File, KEY_S, MODIFIER_COMMAND)
				CreateMenu("", 0, MENU_File)
				'MENU_Batch = CreateMenu("Batch...", 2, MENU_File, KEY_B, MODIFER_COMMAND)			
				'CreateMenu("", 0, MENU_File)			
				MENU_Quit = CreateMenu("Quit", 99, MENU_File, KEY_Q, MODIFIER_COMMAND)			

				DisableMenu(MENU_Save)
	
			MENU_Help = CreateMenu("Help", 0, MENU_Root)
				
				MENU_Contents  = CreateMenu("Help Contents...", 3, MENU_Help, KEY_F1)	
				CreateMenu("",	0, MENU_Help)	
				MENU_About = CreateMenu("About...", 4, MENU_Help)	
	
'			If VERSION = EVALULATION
'				MENU_Buynow = CreateMenu("Click here to get the full version!", 100, MENU_Root)
'			EndIf
	
		UpdateWindowMenu(WINDOW_Main)		

	' Create the preview window.
		
		' Create an indentation around the main texture Panel.
			
			PANEL_PreviewPaneIndent = CreatePanel(0, 0, ClientWidth(WINDOW_Main)-220, ClientHeight(WINDOW_Main), WINDOW_Main, 1)
			SetGadgetLayout(PANEL_PreviewPaneIndent, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_ALIGNED)

		' Create the Tabber at the top of the preview pane.
	
			TABBER_PreviewPane = CreateTabber(0, 8, ClientWidth(PANEL_PreviewPaneIndent), ClientHeight(PANEL_PreviewPaneIndent)-8, PANEL_PreviewPaneIndent)
			SetGadgetLayout(TABBER_PreviewPane, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_ALIGNED)
			
				AddGadgetItem(TABBER_PreviewPane, "Original") 
				AddGadgetItem(TABBER_PreviewPane, "Transformed")
				AddGadgetItem(TABBER_PreviewPane, "Adjusted")
				AddGadgetItem(TABBER_PreviewPane, "Final")
				AddGadgetItem(TABBER_PreviewPane, "Tiled-Transformed")
				AddGadgetItem(TABBER_PreviewPane, "Tiled-Final")
				
		' Create a Panel to clip the canvas that will contain our image.
			PANEL_PreviewPane = CreatePanel(5, 6, ClientWidth(TABBER_PreviewPane)-12-16, ClientHeight(TABBER_PreviewPane)-12-16, TABBER_PreviewPane, 0)
			SetGadgetLayout PANEL_PreviewPane, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_ALIGNED
			SetPanelColor   PANEL_PreviewPane, 0, 0, 0 

		' Create sliders.
								
			HSLIDER_PreviewPane = CreateSlider(GadgetX(PANEL_PreviewPane), GadgetY(PANEL_PreviewPane) + GadgetHeight(PANEL_PreviewPane), GadgetWidth(PANEL_PreviewPane), 16, TABBER_PreviewPane, 1)
			VSLIDER_PreviewPane = CreateSlider(GadgetX(PANEL_PreviewPane) + GadgetWidth(PANEL_PreviewPane), GadgetY(PANEL_PreviewPane), 16, GadgetHeight(PANEL_PreviewPane), TABBER_PreviewPane, 2)
			
			SetGadgetLayout HSLIDER_PreviewPane, EDGE_ALIGNED,  EDGE_ALIGNED, EDGE_CENTERED, EDGE_ALIGNED
			SetGadgetLayout VSLIDER_PreviewPane, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED,  EDGE_ALIGNED

		' Create dummy canvases to place inside the Panel. 
		' The canvases are deleted and replaced with a new one that fits the image each time a new image is loaded.
		
			' Create one canvas for each tab.
			'	UpdateCanvas(CI_ALL)
		
			' Set the original canvas to be the active canvas.
			'	SetActiveCanvas(CI_ORIGINAL)
						
	' Create the control Panel.

		CPWidth = 208
		CPx = ClientWidth(WINDOW_Main) - (CPWidth + 4)
		CPy = ClientHeight(WINDOW_Main)

		' Antialiasing:

			'CPy = CPy - SIZE_ROW
			'CPy = CPy - SIZE_LABEL
			
			'LABEL_AutoUpdate = CreateLabel("Enable Antialiasing:", CPx+16, CPy, CPWidth-32, 16, WINDOW_Main, 0)
				'SetGadgetLayout LABEL_AutoUpdate, EDGE_CENTERED, EDGE_ALIGNED, EDGE_CENTERED, EDGE_ALIGNED

			'BUTTON_Antialias = CreateButton("", CPx+CPWidth-32, CPy, 16, 16, WINDOW_Main, 2)
				'SetGadgetLayout BUTTON_Antialias, EDGE_CENTERED, EDGE_ALIGNED, EDGE_CENTERED, EDGE_ALIGNED


		' Automatic Update:

			CPy = CPy - SIZE_SPACE
			CPy = CPy - SIZE_LABEL
			
			LABEL = CreateLabel("Enable Auto-Update:", CPx+16, CPy, 128, SIZE_LABEL, WINDOW_Main, 0)
				SetGadgetLayout LABEL, EDGE_CENTERED, EDGE_ALIGNED, EDGE_CENTERED, EDGE_ALIGNED

			BUTTON_AutoUpdate = CreateButton("", CPx+CPWidth-32, CPy, SIZE_LABEL, SIZE_LABEL, WINDOW_Main, 2)
				SetGadgetLayout BUTTON_AutoUpdate, EDGE_CENTERED, EDGE_ALIGNED, EDGE_CENTERED, EDGE_ALIGNED
						
		' Make Texture Button:
			
			CPy = CPy - SIZE_SPACE
			CPy = CPy - SIZE_BUTTON
	
			BUTTON_MakeTexture = CreateButton("Make Texture", CPx+16, CPy, CPWidth-32, SIZE_BUTTON, WINDOW_Main, 1)
				SetGadgetLayout BUTTON_MakeTexture, EDGE_CENTERED, EDGE_ALIGNED, EDGE_CENTERED, EDGE_ALIGNED
				
		' Realtime preview:

			'CPy = CPy - SIZE_SPACE

			'LABEL_Preview = CreateLabel("Preview:", CPx, CPy, 220-32, SIZE_LABEL, WINDOW_Main, 0)
				'SetGadgetLayout LABEL_Preview, EDGE_CENTERED, EDGE_ALIGNED, EDGE_CENTERED, EDGE_ALIGNED

			'CPy = CPy - SIZE_SPACE
		
			'PANEL_Preview = CreatePanel(CPx, CPy, CPWidth, CPWidth, WINDOW_Main, 1)
				'SetGadgetLayout PANEL_Preview, EDGE_CENTERED, EDGE_ALIGNED, EDGE_CENTERED, EDGE_ALIGNED
			
			'CANVAS_Preview = CreateCanvas(4, 4, GadgetWidth(PANEL_Preview)-2-8, GadgetWidth(PANEL_Preview)-2-8, PANEL_Preview)
				'SetGadgetLayout CANVAS_Preview, EDGE_CENTERED, EDGE_ALIGNED, EDGE_CENTERED, EDGE_ALIGNED
				
			'CPy = CPy - GadgetHeight(PANEL_Preview) 
			
				
		' Create an indentation around the controls which resizes vertically with the window.
		' *** This panel is erasing a bit of the left side of the vertical control panel slider! ***		
			
			CPWidth  = CPWidth' - 16
			CPHeight = CPy - SIZE_SPACE*2
			CPy = 0

			PANEL_Indent = CreatePanel(CPx, CPy, CPWidth, CPHeight, WINDOW_Main, 0)
				SetGadgetLayout PANEL_Indent, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_ALIGNED
			

		' Vertical control slider:

			'VSLIDER_Controls = CreateSlider(ClientWidth(WINDOW_Main)-16-8, GadgetY(PANEL_Indent), 16, GadgetHeight(PANEL_Indent), WINDOW_Main, 2)
			
			VSLIDER_Controls = CreateSlider(ClientWidth(PANEL_Indent)-16, 0, 16, GadgetHeight(PANEL_Indent), PANEL_Indent, SLIDER_SCROLLBAR|SLIDER_VERTICAL)
				SetGadgetLayout VSLIDER_Controls, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_ALIGNED
				'SetSliderRange  VSLIDER_Controls, 0, 100
				'Set_Slider_Value(HSLIDER_ShadingStrength, ShadingStrength_Default#)
			
						
		' Create a Panel within the indentation which is clipped by the indentation Panel, to contain the controls.	
		' The height of the panel doesn't matter, (and we won't know it till we create all the controls) but for gadget alignment the width must be set correctly.
			
			PANEL_Controls = CreatePanel(0, 0, ClientWidth(PANEL_Indent)-GadgetWidth(VSLIDER_Controls), 338, PANEL_Indent) 
				SetGadgetLayout PANEL_Controls, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED


		' Calculate base coordinates and size of control Panel.

			CPx = 8 
			CPy = 0
			CPWidth  = ClientWidth(PANEL_Controls)-CPx*2
			CPHeight = ClientHeight(PANEL_Controls)
			
						
		' Shading strength:

			CPy = CPy + SIZE_SPACE
		
			LABEL = CreateLabel("Highlight Removal Strength:", CPx, CPy, 220-32, SIZE_LABEL, PANEL_Controls, 0)
				SetGadgetLayout LABEL, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED

			CPy = CPy + SIZE_LABEL

			TEXTFIELD_ShadingStrength = CreateTextField(CPx, CPy, 40, SIZE_TEXTFIELD, PANEL_Controls, 0)
				SetGadgetLayout TEXTFIELD_ShadingStrength, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
				SetGadgetText   TEXTFIELD_ShadingStrength, ShadingStrength_Default#

			HSLIDER_ShadingStrength = CreateSlider(CPx+40+8, CPy, CPwidth-48, SIZE_SLIDER, PANEL_Controls, SLIDER_HORIZONTAL|SLIDER_TRACKBAR)
				SetGadgetLayout(HSLIDER_ShadingStrength, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED)
				SetSliderRange(HSLIDER_ShadingStrength, 0, 10000)
				SetSliderValue(HSLIDER_ShadingStrength, 0)
				
				'SetSliderRange  HSLIDER_ShadingStrength, 0, ShadingStrength_SliderSteps+1
				
				'Set_Slider_Value(HSLIDER_ShadingStrength, ShadingStrength_Default#)
	
			CPy = CPy + SIZE_TEXTFIELD

	
		' Shading blur radius:
		
			CPy = CPy + SIZE_SPACE

			LABEL = CreateLabel("Highlight Removal Radius:", CPx, CPy, 220-32, 16, PANEL_Controls, 0)
				SetGadgetLayout LABEL, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED

			CPy = CPy + SIZE_LABEL

			TEXTFIELD_ShadingBlurRadius = CreateTextField(CPx, CPy, 40, SIZE_TEXTFIELD, PANEL_Controls, 0)
				SetGadgetLayout TEXTFIELD_ShadingBlurRadius, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
				SetGadgetText   TEXTFIELD_ShadingBlurRadius, Int(ShadingBlurRadius_Default#)

			HSLIDER_ShadingBlurRadius = CreateSlider(CPx+40+8, CPy, CPwidth-48, SIZE_SLIDER, PANEL_Controls, SLIDER_HORIZONTAL|SLIDER_TRACKBAR)
				SetGadgetLayout HSLIDER_ShadingBlurRadius, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
				SetSliderRange  HSLIDER_ShadingBlurRadius, 0, ShadingBlurRadius_SliderSteps+1
	
				'Set_Slider_Value(HSLIDER_ShadingBlurRadius, ShadingBlurRadius_Default#)				

			CPy = CPy + SIZE_TEXTFIELD

		' Seperator:
		
			CPy = CPy + SIZE_SPACE
				
			'LABEL = CreateLabel("", CPx, CPy, ClientWidth(PANEL_Controls), 4, PANEL_Controls, LABEL_SEPARATOR)
			'	SetGadgetLayout LABEL, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED				

			CPy = CPy + SIZE_SEPARATOR

		' Horizontal blend radius:

			CPy = CPy + SIZE_SPACE
		
			LABEL = CreateLabel("Horizontal Blend Radius:", CPx, CPy, 220-32, 16, PANEL_Controls, 0)
				SetGadgetLayout LABEL, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
			
			CPy = CPy + SIZE_LABEL
				
			TEXTFIELD_HBlend = CreateTextField(CPx, CPy, 40, SIZE_TEXTFIELD, PANEL_Controls, 0)
				SetGadgetLayout TEXTFIELD_HBlend, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
				SetGadgetText   TEXTFIELD_HBlend, HBlendRadius_Default#
				
			HSLIDER_HBlend = CreateSlider(CPx+40+8, CPy, CPwidth-48, SIZE_SLIDER, PANEL_Controls, SLIDER_HORIZONTAL|SLIDER_TRACKBAR)
				SetGadgetLayout HSLIDER_HBlend, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
				SetSliderRange  HSLIDER_HBlend, 0, HBlendRadius_SliderSteps+1
			
				'Set_Slider_Value(HSLIDER_HBlend, HBlendRadius_Default#)

			CPy = CPy + SIZE_SLIDER
			
		' Vertical blend radius:

			CPy = CPy + SIZE_SPACE
		
			LABEL = CreateLabel("Vertical Blend Radius:", CPx, CPy, 220-32, SIZE_LABEL, PANEL_Controls, 0)
				SetGadgetLayout LABEL, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED

			CPy = CPy + SIZE_LABEL

			TEXTFIELD_VBlend = CreateTextField(CPx, CPy, 40, SIZE_TEXTFIELD, PANEL_Controls, 0)
				SetGadgetLayout TEXTFIELD_VBlend, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
				SetGadgetText   TEXTFIELD_VBlend, VBlendRadius_Default#

			HSLIDER_VBlend = CreateSlider(CPx+40+8, CPy, CPwidth-48, SIZE_SLIDER, PANEL_Controls, SLIDER_HORIZONTAL|SLIDER_TRACKBAR)
				SetGadgetLayout HSLIDER_VBlend, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
				SetSliderRange  HSLIDER_VBlend, 0, VBlendRadius_SliderSteps+1
	
				'Set_Slider_Value(HSLIDER_VBlend, VBlendRadius_Default#)
			
			CPy = CPy + SIZE_SLIDER
	

		' Seperator:
		
			CPy = CPy + SIZE_SPACE
				
			'LABEL = CreateLabel("", CPx, CPy, ClientWidth(PANEL_Controls), 4, PANEL_Controls, LABEL_SEPARATOR)
			'	SetGadgetLayout LABEL, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED				

			CPy = CPy + SIZE_SEPARATOR
			
			
		' Sharpness:

			CPy = CPy + SIZE_SPACE
		
			LABEL = CreateLabel("Sharpness:", CPx, CPy, 220-32, SIZE_LABEL, PANEL_Controls, 0)
				SetGadgetLayout LABEL, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED

			CPy = CPy + SIZE_LABEL

			TEXTFIELD_Sharpness = CreateTextField(CPx, CPy, 40, SIZE_TEXTFIELD, PANEL_Controls, 0)
				SetGadgetLayout TEXTFIELD_Sharpness, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
				SetGadgetText   TEXTFIELD_Sharpness, Sharpness_Default#

			HSLIDER_Sharpness = CreateSlider(CPx+40+8, CPy, CPwidth-48, SIZE_SLIDER, PANEL_Controls, SLIDER_HORIZONTAL|SLIDER_TRACKBAR)
				SetGadgetLayout HSLIDER_Sharpness, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
				SetSliderRange  HSLIDER_Sharpness, 0, Sharpness_SliderSteps+1
	
				'Set_Slider_Value(HSLIDER_Sharpness, Sharpness_Default#)
			
			CPy = CPy + SIZE_SLIDER
	

		' Seperator:
		
			CPy = CPy + SIZE_SPACE
				
			'LABEL = CreateLabel("", CPx, CPy, ClientWidth(PANEL_Controls), 4, PANEL_Controls, LABEL_SEPARATOR)
			'	SetGadgetLayout LABEL, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED				

			CPy = CPy + SIZE_SEPARATOR


		' Final texture width:
		
			CPy = CPy + SIZE_SPACE
		
			LABEL = CreateLabel("Final Size:", CPx, CPy, 220-32, SIZE_LABEL, PANEL_Controls, 0)
				SetGadgetLayout LABEL, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED

			CPy = CPy + SIZE_LABEL

			TEXTFIELD_FinalWidth = CreateTextField(CPx, CPy, 40, SIZE_TEXTFIELD, PANEL_Controls, 0)
				SetGadgetLayout TEXTFIELD_FinalWidth, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
				SetGadgetText   TEXTFIELD_FinalWidth, FinalWidth_Default

			LABEL = CreateLabel("x", CPx+32+16, CPy, 8, SIZE_LABEL, PANEL_Controls, 0)
				SetGadgetLayout LABEL, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED

			TEXTFIELD_FinalHeight = CreateTextField(CPx+64, CPy, 40, SIZE_TEXTFIELD, PANEL_Controls, 0)
				SetGadgetLayout TEXTFIELD_FinalHeight, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
				SetGadgetText   TEXTFIELD_FinalHeight, FinalHeight_Default
							
			CPy = CPy + SIZE_TEXTFIELD

		
		' Display total control panel size in debugger so we can set it properly at the beginning after me make changes.
		' (We can't set it at the end because the controls will end up with black bacgrounds due to a bug.)
			
			CPy = CPy + SIZE_SPACE					
			
			DebugLog("Control panel height = " + CPy)

					
	' Create the progress bar.
		' PROGBAR_Progressbar = CreateProgBar(WINDOW_Width-212, WINDOW_Height-20, 204, SIZE_PROGRESSBAR, WINDOW_Main)
	
	' Load custom mouse pointers.
	'	LoadPointers()

	'UpdatePreviewPane()			
	'UpdateControlPanel()		
					
End Function</textarea><br><br><br>The slider of interest is the horizontal one at the top on the right side.  The "shading strength" slider.  The other ones don't have min and max values set to something sensible as I just ported the code from BlitzPlus.<br><br>The top slider I tried min and max values of 0 and 100, 0 and 1000 and 0 and 10,000 and saw no difference.  All somewhat choppy movement.  The regular sliders in the left side panel though move super smooth. <br><br></td></tr></table><br>
<a name="973195"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >sswift</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hm, that's odd.  I think I found the problem, though I don't know the cause.<br><br>The problem is the delay(100) I have in the main loop.  For some reason, giving LESS time back to the operating system, ie, getting rid of that or setitng it to 1, makes the slider move more smoothly.<br><br>What I don't get is why that would have the opposite effect you expect and why it only affects the one type of slider. <br><br></td></tr></table><br>
<a name="973197"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >sswift</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yep, that's definitely the problem.  Setting it to 1000 makes the app take a moment to draw and makes the trackbars almost completely unresponsive.  The regular window sliders continue to behave normally however. <br><br></td></tr></table><br>
<a name="973222"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jsp</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> No need for a delay, just use WaitEvent() and it will be super smooth.<br><pre class=code>
Repeat
	WaitEvent()
	'Delay(100)
Until KeyHit(KEY_ESCAPE) '? may better to act on window close? 
</pre> <br><br></td></tr></table><br>
<a name="973266"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> sswift, this is a standard MaxGUI event loop that I recommend you should use instead:<br><br><pre class=code>Repeat
	Select WaitEvent()
		Case EVENT_APPTERMINATE, EVENT_WINDOWCLOSE;End
	EndSelect
Forever</pre>A little technical explanation just in case you are curious: the reason why you were experiencing different behaviour with trackbars and scrollbars is because scrollbars go into a modal system loop when they are being scrolled (effectively halting the execution of your main application until the scroll position has been obtained).  As such, this means that the <b>Delay</b> in your main-loop was not executed during the scroll.  On the otherhand, the trackbar isn't modal and so will be still running the loop and executing the <b>Delay</b> statement while you are scrolling. <br><br></td></tr></table><br>
<a name="973273"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >sswift</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> If application code doesn't execute when in a modal event loop then how did I update my textboxes in realtime as the user moved the sliders inb BlitzPlus?<br><br>And how am I to scroll stuff in a canvas when the sliders are being moved?  I don't want the canvas to only be updated when the user lets go of the slider control. <br><br></td></tr></table><br>
<a name="973276"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> If application code doesn't execute when in a modal event loop then how did I update my textboxes in realtime as the user moved the sliders inb BlitzPlus? <br></div><br>You could have done easily with certain sliders, but not other types.  That's what I'm trying to say - trackbars yep, scrollbars no (unless see below).<br><br><div class="quote"> And how am I to scroll stuff in a canvas when the sliders are being moved? I don't want the canvas to only be updated when the user lets go of the slider control. <br></div><br>If you want your program to update while the system is in a modal loop, then you must use <b>Event Hooks</b> - see <a href="http://www.2dgamecreators.com/maxgui/T15-Eventhooks.html" target="_blank">Assari's amazing tutorial here</a>. <br><br>Also, MaxGUI now has a new <b>CreateScrollPanel()</b> gadget that will handle moving panels around for you. <a href="/posts.php?topic=85843" target="_blank">See here for more info</a>. <br><br></td></tr></table><br>
<a name="973279"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >sswift</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Seb:<br>I used scrollbars when I did it in Blitzplus.  Blitzplus didn't have trackbars.  And I didn't use an event hook either.  I just had your standard event handler.  Same thing for scrolling the canvas.<br><br>I guess Blitzplus worked differently, but I don't know how mark managed that. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
