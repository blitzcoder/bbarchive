<!DOCTYPE html><html lang="en" ><head ><title >FileListBox proxy gadget</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >FileListBox proxy gadget</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >FileListBox proxy gadget</a><br><br>
<a name="908119"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Blueapples</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> A pretty simple file browsing gadget.<br><br>I just use this as an Import, but it should work with no real changes as a module. Is there a way to get these sorts of things added to the official proxy gadget module? It'd be great if we could all contribute the gadgets we've made as proxy gadgets for others to use - built right in.<br><br>I don't really know bbdoc, so that part might not be right, but I'm kinda tired. I will however probably update this as I use it for a project tomorrow.<br><br>As of now I've only really tested this on Linux, but it should work on Windows and Max OS. If not let me know and I will update it.<br><br>Enjoy. <br><br>Screenshot:<br><img src="http://devgroup.metasushi.com/sites/default/files/Screenshot-FileListBoxTest.png"><br><br><b>Update:</b> Oct-4-2008: Added a few functions to let you get the item string (removing the square brackets from directories and getting the parent if .. is selected) or full filename of any item based on index - not just the selected item. Also fixed a bug where the style argument wasn't used to create the actual list box.<br><br><b>fileListBox.bmx</b><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Import MaxGUI.MaxGUI

Rem
bbdoc: Creates a list box that allows browsing for files or folders.
about: The underlying gadget is a listbox, and so the @style parameter can take all the #CreateListBox flags.

When creating the file list box, you must specify the initial directory it should display. this can be changed
with #ChangeDir.

The current dir is always available as the &lt;b&gt;.dir&lt;/b&gt; property, updated to match whatever navigation the user
has done.

Set &lt;b&gt;.showDotFiles&lt;/b&gt; to true to show files and directories that start with a period. By default these
files are hidden.

The following events are emited by this gadget:

&lt;table&gt;
&lt;tr&gt;&lt;td&gt;EVENT_GADGETACTION&lt;/td&gt;&lt;td&gt;When the user activates a file by pressing enter or double clicking.&lt;/td&gt;&lt;/tr&gt;
&lt;tr&gt;&lt;td&gt;EVENT_GADGETOPEN&lt;/td&gt;&lt;td&gt;When the user navigates to a child directory.&lt;/td&gt;&lt;/tr&gt;
&lt;tr&gt;&lt;td&gt;EVENT_GADGETCLOSE&lt;/td&gt;&lt;td&gt;When the user navigates to a parent directory.&lt;/td&gt;&lt;/tr&gt;
&lt;/tr&gt;
&lt;/table&gt;
End Rem
Function CreateFileListBox:TGadget( dir$,x,y,w,h,group:TGadget,style=0)
	
	Return New TFileListBox.Create(dir,x,y,w,h,group,style)
	
EndFunction

Type TFileListBox Extends TProxyGadget
	
	Global instances:TList
	
	Field dir:String
	Field lstBox:TGadget
	Field showDotFiles:Int = False
	
	Method New()
		If Not instances Then Initialize()
	EndMethod
	
	Method Create:TFileListBox(pdir$,x,y,w,h,group:TGadget,style)
		
		lstBox = CreateListBox(x,y,w,h,group,style)
		SetDir(pdir)
		
		'DebugStop
		SetProxy( lstBox )
		
		instances.AddLast Self
		
		Return Self
		
	EndMethod
	
	Method EventHook:TEvent( pEvent:TEvent )
		
		Select pEvent.id
			Case EVENT_GADGETACTION
				Local item:String = GetSelectedItem()
				Local file:String = GetSelectedFilename()
				If FileType(file) = 2 Then
					SetDir(file)
					If item = ".." Then
						Return CreateEvent(EVENT_GADGETCLOSE, Self)
					Else
						Return CreateEvent(EVENT_GADGETOPEN, Self)
					EndIf
				Else
					Return pEvent
				EndIf
			'Default
			'	Print pEvent.id
		EndSelect
		
		Return Null
	
	EndMethod
	
Rem
bbdoc: Set the directory shown by the list box.
bbabout: Automatically calls refreshes after setting the new path.
End Rem
	Method SetDir(pdir:String)
		dir = pdir
		If dir[Len(dir)-1..] &lt;&gt; "/" Then dir = dir + "/"
		Refresh()
	EndMethod
	
Rem
bbdoc: Return the selected file by itself without it's path.
bbabout: To get the full path, call #GetSelectedFilename
End Rem
	Method GetSelectedItem:String()
		Local idx:Int = SelectedGadgetItem(lstBox)
		Return GetItem(idx)
	EndMethod
	
Rem
bbdoc: Return the given file in the list by itself without it's path.
bbabout: To get the full path, call #GetFilename
End Rem
	Method GetItem:String(idx:Int)
		Local result:String = ""
		
		If idx &gt; -1 Then
			result = GadgetItemText(lstBox, idx)
			If result[..1] = "[" Then
				result = result[1..Len(result)-1]
			EndIf
		EndIf
		
		Return result
	EndMethod
	

Rem
bbdoc: Return the full path to the selected file or directory.
bbabout: To get just the item without the path, call #GetSelectedItem.
End Rem
	Method GetSelectedFilename:String()
		Local idx:Int = SelectedGadgetItem(lstBox)
		Return GetFilename(idx)
	EndMethod
	
Rem
bbdoc: Return the full path to a paticular file or directory in the list.
bbabout: To get just the item without the path, call #GetItem.
End Rem
	Method GetFilename:String(idx:Int)
		Local result:String = ""
		Local item:String = GetItem(idx)
		
		If item &lt;&gt; "" Then
			If item = ".." Then
				result = ExtractDir(ExtractDir(dir))
			Else
				result = dir + item
			EndIf
		EndIf
		Return result
	EndMethod
	
Rem
bbdoc: Refresh the contents of the list box.
End Rem
	Method Refresh()
		Local dirs:String[]
		Local files:String[]
		Local selectedItem:String = GetSelectedItem()
		Local hDir:Int
		Local i:Int
		Local item:String
		
		ClearGadgetItems(lstBox)

		'files = LoadDir(dir)
		'files.Sort()
		
		' Get directory listing - sorting into subdirectories and files
		hDir = ReadDir(dir)
		item = NextFile(hDir)
		While item &lt;&gt; ""
			If item &lt;&gt; "." And item &lt;&gt; ".." Then
				'Print dir + item
				If FileType(dir + item) = 2 Then
					dirs = dirs[..Len(dirs)+1]
					dirs[Len(dirs)-1] = item
				Else
					files = files[..Len(files)+1]
					files[Len(files)-1] = item
				EndIf
			EndIf
			item = NextFile(hDir)
		EndWhile
		CloseDir(hDir)
		
		' Sort the two groups independently
		dirs.Sort()
		files.Sort()
		
		' Add the parent item
		AddGadgetItem(lstBox, "[..]")
		' Add directories
		_AddItems(dirs, selectedItem)
		' Add files
		_AddItems(files, selectedItem)
	EndMethod
	
	Method _AddItems(files:String[], selectItem:String)
		Local i:Int
		Local item:String
		
		If files &lt;&gt; Null Then
			For i = 0 To Len(files)-1
				If files[i][..1] &lt;&gt; "." Or showDotFiles Then
					If FileType(dir + "/" + files[i]) = 2 Then
						item = "[" + files[i] + "]"
					Else
						item = files[i]
					EndIf
					AddGadgetItem(lstBox, item)
					If item = selectItem Then SelectGadgetItem(lstBox, i)
				EndIf
			Next
		EndIf
	EndMethod
	
	Method CleanUp()
		instances.Remove(Self)
		Super.CleanUp()
	EndMethod
	
	Function Initialize()
		instances = New TList
		AddHook EmitEventHook, eventHandler, Null, 1
	EndFunction
	
	Function eventHandler:Object( pID%, pData:Object, pContext:Object )
		Local pEvent:TEvent = TEvent(pData)
		
		If pEvent Then
			'If pevent.id = EVENT_GADGETACTION Then DebugStop
			For Local tmpGadget:TFileListBox = EachIn instances
				If tmpGadget = pEvent.source Then
					'Print "found proxy"
					Return tmpGadget.EventHook( pEvent )
				EndIf
			Next
		EndIf
		
		Return pData
	EndFunction
	
EndType
</textarea><br><br>Example:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import MaxGUI.Drivers
Import "fileListBox.bmx"
Global MainWindow:TGadget
Global fileListBox:TFileListBox

MainWindow=CreateWindow("FileListBox test",10,50,185,247,Desktop(),WINDOW_TITLEBAR|WINDOW_RESIZABLE)

fileListBox = TFileListBox(CreateFileListBox("/",0,0,185,247,MainWindow,LISTBOX_MULTISELECT))
SetGadgetLayout fileListBox,1,1,1,1


Repeat
	Select WaitEvent()
		Case EVENT_WINDOWCLOSE
			Exit
		Case EVENT_GADGETACTION
			If EventSource() = fileListBox Then
				Notify "Open a file: " + fileListBox.GetSelectedFilename()
			EndIf
		Case EVENT_GADGETCLOSE
			If EventSource() = fileListBox Then
				Notify "Moved up a directory. Current dir: " + fileListBox.dir
			EndIf
		Case EVENT_GADGETOPEN
			If EventSource() = fileListBox Then
				Notify "Moved down a directory. Current dir: " + fileListBox.dir
			EndIf
	End Select
Forever
</textarea> <br><br></td></tr></table><br>
<a name="908148"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Space_guy</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> That one looks handy :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
