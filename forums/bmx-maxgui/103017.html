<!DOCTYPE html><html lang="en" ><head ><title >Textarea and SetTextArea</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Textarea and SetTextArea</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >Textarea and SetTextArea</a><br><br>
<a name="1237378"></a>

<a name="1237379"></a>

<a name="1237492"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi<br><br>before I start to hammer my computer I decided to post here this piece of code so maybe someone smarter than me can found where is the error!<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

SuperStrict
Import maxgui.drivers


Global window:tgadget=		CreateWindow("Main",100,100,500,300,,WINDOW_TITLEBAR|WINDOW_CENTER)

Global textarea:Tgadget=	CreateTextArea(10,10,480,200,window)

Global btn_do:tgadget=		CreateButton("Change",10,215,100,25,window)

'AddTextAreaText textarea,"Local a,b,c&gt;&gt;int"

Local fnt:Tguifont=LookupGuiFont(GUIFONT_MONOSPACED,10)
SetGadgetFont textarea,fnt

SetGadgetFilter textarea,filter

Function filter:Int(event:TEvent,context:Object)
	Select event.id
		Case EVENT_KEYDOWN
			If event.data=13 Change2(context)
	End Select
	Return 1
End Function

Repeat
	WaitEvent()
	
	Select EventID()
		Case EVENT_APPTERMINATE, EVENT_WINDOWCLOSE
			End
	End Select
Forever

Function UpdateFormat:String(line:String)'ext:String Var,size:Int Var)
If line.contains("&gt;&gt;")=False Return line.Trim()
	Local stype:String,size:Int
	line=line.Trim()
	line=line.Replace(Chr(13),"")
	line=line.Replace(Chr(10),"")
	
	If line.contains("'")
		line=line[..line.find("'")]
	End If
	If line.contains("&gt;&gt;")
		Local p:Int=line.find("&gt;&gt;")+2
		stype=line[p..].Trim()
		line=line[..p-2]
	End If
	
	Local lim:String[]=["Local","Global","Field"]
	Local nline:String,par:String,c:Int,newpar:String
	Local _SEP:String=":"
	Local sd:String[]=["!","#","$","%","@","@@"]
	
	For Local st:String=EachIn sd
		If stype=st	_SEP=""
	Next
	
	For Local ss:String=EachIn lim
		If line.contains(ss+" ")
			nline=ss+" "
			par=line[line.find(ss+" ")+Len(ss)+1..]
			'Print "Par ("+par+")"
			
			For Local p:String=EachIn par.split(",")
					c=0
					For Local ps:String=EachIn sd
						If p.contains(ps)
							p=p[..p.find(ps)]
						End If
					Next
					If p.contains(":") 
							p=p[..p.find(":")]
					End If
					
					If p.contains("[")
						c=1
						p=p[..p.find("[")].Trim()+_SEP+stype+p[p.find("[")..]
					End If
					If c=0
					newpar=newpar+p.Trim()+_SEP+stype+","
					ElseIf c=1
					newpar=newpar+p.Trim()+","
					End If
			Next
			nline=nline+newpar[..Len(newpar)-1]
			size=Len(newpar)-1
			
			Print "New Par ("+newpar+") Size: "+size
			
		End If
	Next
	
	
	Return nline.Trim()

End Function

Function Change2:Int(context:Object)
	Local pos_cursor:Int,pos_line:Int
	pos_cursor=	TextAreaCursor(textarea)
	pos_line=		TextAreaLine(textarea,pos_cursor)
	Local text:String=TextAreaText(textarea,pos_line,1,TEXTAREA_LINES).Trim()
	Local nline:String,size:Int
	nline=UpdateFormat(text).Trim()
	Print "POS LINE         &gt;"+pos_line
	Print "Original Text    &lt;"+text+"&gt; "+Len(text)
	Print "         NewLine &gt;"+nline+"&lt; "+Len(nline)
	LockTextArea(textarea)
	'SetTextAreaText(textarea,nline,0,Len(text),TEXTAREA_CHARS)
	'SetTextAreaText(textarea,"",pos_line,1,TEXTAREA_LINES)
	SetTextAreaText(textarea,nline,pos_line,1,TEXTAREA_LINES)
	UnlockTextArea(textarea)
		
End Function

</textarea><br><br>Basically it is a simple textarea.<br>If you type something like this (just start a line with Global, Local or Field)<br><pre class=code>
Local a,b,c&gt;&gt;Int

Local a1,a2,a3&gt;&gt;#

</pre><br>the textarea should rewrite the line in a 'extended' way like<br><br><pre class=code>
Local a:Int, b:int, c:int
Local a1#,a2#,a3#
</pre><br><br>The function to strip/replace data-types seems to work, I have problems only with the SetTextAreaText function (...)<br><br>Can anyone give me an hand please? <br><br></td></tr></table><br>
<a name="1237445"></a>

<a name="1237446"></a>

<a name="1237448"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> This works but might not be what you're after? -&gt; Press the change button!<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' Ugly code by Grisu :)
SuperStrict
Framework Maxgui.Drivers
Import BRL.EventQueue

Global window:tgadget=		CreateWindow("Main",100,100,500,300,,WINDOW_TITLEBAR|WINDOW_CENTER)

Global textarea:Tgadget=	CreateTextArea(10,10,480,200,window)

Global btn_do:tgadget=		CreateButton("Change",10,215,100,25,window)

AddTextAreaText textarea,"Local a,b,c&gt;&gt;int"+Chr(13)
AddTextAreaText textarea,"Local afloat,bfloat,cfloat&gt;&gt;float"+Chr(13)
Local fnt:Tguifont=LookupGuiFont(GUIFONT_MONOSPACED,10)
SetGadgetFont textarea,fnt

SetGadgetFilter textarea,filter

Function filter:Int(event:TEvent,context:Object)
	Select event.id
		Case EVENT_KEYDOWN
			If event.data=KEY_DOWN Or event.data=13 Change2(context)
	End Select
	Return 1
End Function

Repeat
	WaitEvent()
	
	
    Select EventSource()
	Case btn_do
      Select EventID() 
   	  Case EVENT_GADGETACTION
     	Print "Convert or die!"

		SaveText (TextAreaText(textarea),"helpdecag.txt") 

		Local file:TStream=ReadFile("helpdecag.txt")
		Local line:String 
        If Not file Then 
          Print "ERROR!"
        Else            
		LockTextArea(TextArea) 
		SetTextAreaText(TextArea,"")

		While Not Eof(file)
			line:String=ReadLine(file)
            AddTextAreaText textarea,(UpdateFormat:String(line:String))+Chr(13)
		Wend

        AddTextAreaText textarea,""+Chr(13)
        AddTextAreaText textarea,"*** Grisu's Magic converted your lines! ***"+Chr(13)

		UnlockTextArea(TextArea) 
		CloseFile (file)
        EndIf 


      End Select  

	End Select 
	 
	Select EventID() 
	Case EVENT_APPTERMINATE, EVENT_WINDOWCLOSE
			End
	End Select
Forever

Function UpdateFormat:String(line:String)'ext:String Var,size:Int Var)
If line.contains("&gt;&gt;")=False Return line.Trim()
	
    'Print "Linea ("+line+")"
	Local stype:String,size:Int
	
	If line.contains("'")
		line=line[..line.find("'")]
	End If
	'Print "Linea ("+line+")"
	If line.contains("&gt;&gt;")
		Local p:Int=line.find("&gt;&gt;")+2
		stype=line[p..].Trim()
		'Print "Tipo dato ("+stype+")"
		line=line[..p-2]
	End If
	
	'Print "Linea ("+line+")"
	
	Local lim:String[]=["Local","Global","Field"]
	Local nline:String,par:String,c:Int,newpar:String
	Local _SEP:String=":"
	Local sd:String[]=["!","#","$","%","@","@@"]
	
	For Local st:String=EachIn sd
		If stype=st	_SEP=""
	Next
	
	For Local ss:String=EachIn lim
		If line.contains(ss+" ")
			nline=ss+" "
			par=line[line.find(ss+" ")+Len(ss)+1..]
			'Print "Par ("+par+")"
			
			For Local p:String=EachIn par.split(",")
					c=0
					For Local ps:String=EachIn sd
						If p.contains(ps)
							p=p[..p.find(ps)]
						End If
					Next
					If p.contains(":") 
							p=p[..p.find(":")]
					End If
					
					If p.contains("[")
						c=1
						p=p[..p.find("[")].Trim()+_SEP+stype+p[p.find("[")..]
					End If
					If c=0
					newpar=newpar+p.Trim()+_SEP+stype+","
					ElseIf c=1
					newpar=newpar+p.Trim()+","
					End If
			Next
			nline=nline+newpar[..Len(newpar)-1]
			size=Len(newpar)-1
			
			'Print "New Par ("+newpar+") Size: "+size
			
		End If
	Next
	
	
	Return nline.Trim()

End Function

Function Change2:Int(context:Object)
	Local pos_cursor:Int,pos_line:Int
	pos_cursor=	TextAreaCursor(textarea)
	pos_line=		TextAreaLine(textarea,pos_cursor)
	Local text:String=TextAreaText(textarea,pos_line,1,TEXTAREA_LINES).Trim()
    Print "STEFAn:"+text 
	Local nline:String,size:Int
	nline=UpdateFormat(text).Trim()
	'Print "POS LINE         &gt;"+pos_line
	'Print "Original Text    &lt;"+text+"&gt; "+Len(text)
	'Print "         NewLine &gt;"+nline+"&lt; "+Len(nline)
	
	'SetTextAreaText(textarea,nline,0,Len(text),TEXTAREA_CHARS)
	'SetTextAreaText(textarea,"",pos_line,1,TEXTAREA_LINES)
	SetTextAreaText(textarea,nline,pos_line,1,TEXTAREA_LINES)

End Function
</textarea><br><br>Grisu <br><br></td></tr></table><br>
<a name="1237450"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi<br>So your solution is to rebuild all the content... <br>I don't know if with a big text this could be a speed limit. <br>In any case thanks for your help! <br><br>Ps...  Maybe lock text area could do the job... <br> <br>I really don't understand why in basic example everything works as expected! <br><br></td></tr></table><br>
<a name="1237452"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Locktextarea() helps a lot when dealing with large amounts of text.<br><br>So you want the line to be formatted directly after the user has pressed "Enter"? How much text we are speaking of here? <br><br></td></tr></table><br>
<a name="1237491"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> The idea is to change only the LAST line typed by the user. So there's no much text at all (in one line of course). But consider this should be a change in MaxIDE, so the text could be thousands of lines...<br><br>I noticed that the line is 'truncated' at the previous typed length...<br><pre class=code>
Local a,b,c&gt;&gt;Int (has 16 characters)

RESULTS

Local a:Int,b:In
t,c:Int
</pre><br>If I try to change (without my function) with a standard SetTextAreaText everythink works as expected.<br>It seems MaxGUI/my function 'remembers' where is the last 'newline' character<br><br>EDIT: I changed my function to remove new line characters... but no luck. <br><br></td></tr></table><br>
<a name="1238669"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Still here trying to resolve the problem...<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

SuperStrict
Import maxgui.drivers
Global window:tgadget=		CreateWindow("Main",100,100,500,300,,WINDOW_TITLEBAR|WINDOW_CENTER)
Global textarea:Tgadget=	CreateTextArea(10,10,480,200,window)
Local fnt:Tguifont=LookupGuiFont(GUIFONT_MONOSPACED,10)
SetGadgetFont textarea,fnt
SetGadgetFilter textarea,filter
Function filter:Int(event:TEvent,context:Object)
	Select event.id
		Case EVENT_KEYDOWN
			If event.data=13 Change2(context)
	End Select
	Return 1
End Function

Repeat
	WaitEvent()
	Select EventID()
		Case EVENT_APPTERMINATE, EVENT_WINDOWCLOSE
			End
	End Select
Forever


Function Change2:Int(context:Object)
	Local pos_cursor:Int,pos_line:Int
	pos_cursor=	TextAreaCursor(textarea)
	pos_line=		TextAreaLine(textarea,pos_cursor)	
	Local text:String=TextAreaText(textarea,pos_line,1,TEXTAREA_LINES)'.Trim()
	Local nline:String,size:Int
	nline=text.toupper()
	'nline=text+"__test"

	SetTextAreaText(textarea,nline,pos_line,1,TEXTAREA_LINES)
End Function
</textarea><br><br>Stupid example - working as expected.<br><br>If you type in the textarea it will change in UPPERCASE (set &gt; SET) and so on.<br>No line break, no errors.<br><br>Ok, try to uncomment the second 'nline' (nline=text+"__test")<br>It should add __test to any line typed. But in my case any line is 'broken' like below<br><br><pre class=code>
set

set
__test
</pre><br>I tried to 'trim()' the string, to check if there are any strange ASCII code in the line (ie: 10), but nothing - only normal characters.<br>It's like textarea remembers that after SET (in the example) there is a CR/LF...<br>Any idea how to resolve this?<br>Thanks <br><br></td></tr></table><br>
<a name="1238678"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Henri</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello degac,<br><br>problem is the ENTER-key processed by the control it self and inserting newline character in the wrong place (where cursor is and not at the end of the line).<br><br>One solution is to move the cursor yourself like:<br><pre class=code>Function Change2:Int(context:Object)

	Local pos_cursor:Int, pos_line:Int
	
	pos_cursor	= TextAreaCursor(textarea)
	pos_line	= TextAreaLine(textarea,pos_cursor)	
	
	Local text:String = TextAreaText(textarea,pos_line,1,TEXTAREA_LINES)'.Trim()
	
	Local nline:String, size:Int
	
	nline=text.toupper() + "__test"
	
	SetTextAreaText(textarea, nline, pos_line, 1, TEXTAREA_LINES)
	SelectTextAreaText(textarea, pos_line + 1, 0, TEXTAREA_LINES)
End Function</pre><br><br>-Henri <br><br></td></tr></table><br>
<a name="1238681"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ohhh! THANK YOU!<br><br>I never looked at the Filter function...<br>I still don't understand the nature of the problem, as the returned line 'seems' a full line...<br><br>In any case, Thank you again! <br><br></td></tr></table><br>
<a name="1238687"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Henri</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> It is my understanding that the SetAreaText does not automatically move the cursor, so when you are setting text that is longer, the cursor remains a bit behind.<br>Then the  native callback function (not your filter function) of the textareagadget inserts newline (because user pressed enter) where the cursor is resulting unexpected behaviour.<br><br>PS. AddTextAreaText moves the cursor automatically.<br><br>-Henri <br><br></td></tr></table><br>
<a name="1238705"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah.. Ok thanks for the explanation <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
