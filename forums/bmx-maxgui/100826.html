<!DOCTYPE html><html lang="en" ><head ><title >Dialogues: Create each time or hide?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Dialogues: Create each time or hide?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >Dialogues: Create each time or hide?</a><br><br>
<a name="1193556"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> If I want to create a dialogue box, should I create it once at the start of the program and hide the window, or should I create and destroy it whenever needed?  Which way is best, or does it matter?<br>Example 1 (Create once and Show/Hide)<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

Import MaxGUI.drivers

'------------------------------------------
'
'   Main Window GUI
'
'------------------------------------------
Global MainWindow:TGadget = CreateWindow("Example",0,0,640,480,Null,WINDOW_DEFAULT|WINDOW_CENTER)
Global SaveButton:TGadget = CreateButton("Save",10,10,150,30,MainWindow)

'----------------------------------------
'
'  Save Dialogue
'
'----------------------------------------

Global SaveWindow:TGadget = CreateWindow("Save file",0,0,480,140,MainWindow,WINDOW_TITLEBAR|WINDOW_CENTER|WINDOW_HIDDEN|WINDOW_CLIENTCOORDS)
Global FilenameButton:TGadget = CreateButton("Filename",85,10,150,30,SaveWindow)
Global TextField:TGadget = CreateTextField(10,50,ClientWidth(SaveWindow)-20,30,SaveWindow)
Global Save10:TGadget = CreateButton("Save v1.0",10,90,150,30,SaveWindow)
Global Save11:TGadget = CreateButton("Save v1.1",165,90,150,30,SaveWindow)
Global Cancelbutton:TGadget = CreateButton("Cancel",320,90,150,30,SaveWindow)

AddHook EmitEventHook, MainFunction

Repeat
	WaitEvent()
Forever

Function MainFunction:Object(id:Int, data:Object, Context:Object)
	Local event:TEvent = TEvent(data)

	Select Event.id
		Case EVENT_WINDOWCLOSE
			Select Event.Source
				Case MainWindow
					End
				Case SaveWindow
					HideGadget SaveWindow
				Default
					Return data
			End Select
		Case EVENT_APPTERMINATE
			End
		Case EVENT_GADGETACTION
			Select Event.Source
				Case SaveButton
					ShowGadget SaveWindow
				Case FilenameButton
					Local filename:String = RequestFile("Save as","gpx:gpx;all:*",True)
					If filename
						SetGadgetText(TextField,Filename)
					End If
				Case Cancelbutton
					HideGadget SaveWindow
				Default
					Return data
			End Select 
		Default
			Return data
	End Select
	Return Null
End Function</textarea><br><br>example 2 (Create When needed, then destroy)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

Import MaxGUI.drivers

'------------------------------------------
'
'   Main Window GUI
'
'------------------------------------------
Global MainWindow:TGadget = CreateWindow("Example",0,0,640,480,Null,WINDOW_DEFAULT|WINDOW_CENTER)
Global SaveButton:TGadget = CreateButton("Save",10,10,150,30,MainWindow)

'----------------------------------------
'
'  Save Dialogue
'
'----------------------------------------

Global SaveWindow:TGadget
Global FilenameButton:TGadget
Global TextField:TGadget
Global Save10:TGadget
Global Save11:TGadget
Global Cancelbutton:TGadget

AddHook EmitEventHook, MainFunction

Repeat
	WaitEvent()
Forever

Function MainFunction:Object(id:Int, data:Object, Context:Object)
	Local event:TEvent = TEvent(data)

	Select Event.id
		Case EVENT_WINDOWCLOSE
			Select Event.Source
				Case MainWindow
					End
				Case SaveWindow
					FreeGadget SaveWindow
				Default
					Return data
			End Select
		Case EVENT_APPTERMINATE
			End
		Case EVENT_GADGETACTION
			Select Event.Source
				Case SaveButton
					SaveWindow:TGadget = CreateWindow("Save file",0,0,480,140,MainWindow,WINDOW_TITLEBAR|WINDOW_CENTER|WINDOW_CLIENTCOORDS)
					FilenameButton:TGadget = CreateButton("Filename",85,10,150,30,SaveWindow)
					TextField:TGadget = CreateTextField(10,50,ClientWidth(SaveWindow)-20,30,SaveWindow)
					Save10:TGadget = CreateButton("Save v1.0",10,90,150,30,SaveWindow)
					Save11:TGadget = CreateButton("Save v1.1",165,90,150,30,SaveWindow)
					Cancelbutton:TGadget = CreateButton("Cancel",320,90,150,30,SaveWindow)
				Case FilenameButton
					Local filename:String = RequestFile("Save as","gpx:gpx;all:*",True)
					If filename
						SetGadgetText(TextField,Filename)
					End If
				Case Cancelbutton
					FreeGadget SaveWindow
				Default
					Return data
			End Select 
		Default
			Return data
	End Select
	Return Null
End Function</textarea> <br><br></td></tr></table><br>
<a name="1193569"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Henri</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello,<br><br>I would say what ever is more convenient to you. I don't see anything wrong with either way.<br><br><br>-Henri <br><br></td></tr></table><br>
<a name="1193575"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Create at startup and hide it. <br><br></td></tr></table><br>
<a name="1193577"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Henri</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> On second example I would add HideGadget before FreeGadget as FreeGadget alone might leave ghost trail if parent window isn't redrawed.<br><br>-Henri <br><br></td></tr></table><br>
<a name="1193583"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Creating it once, then hiding it seems to be the easiest way to manage it, just wasn't sure if there was any clear advantage to using one method over the other.  Thanks. :) <br><br></td></tr></table><br>
<a name="1202838"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TAS</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have wonder that too.  The cleanest looking approach I have found is to keep ever thing inside the dialog function<br><br>Function Mydialog()<br>   Global win:tgadget[10]  'preserve values over calls but hidden from Main<br>   If win[0]=null<br>      win[0]=createwindow(......<br>   else<br>       showgadget win[0]<br>   endif<br><br>   repeat<br>      waitevent()<br>      'handle .....<br>      exit<br>    forever<br>    hidegadget win[0]<br>end function <br><br></td></tr></table><br>
<a name="1202863"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jsp</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Your example is somewhat different from TomToad's.<br>He is using hooks and you use a normal WaitEvent loop.<br>That means your dialog is modal and you can't click on any other window/gadget outside your dialog. Sometimes this is actually needed, but not always.<br><br>For what is better CreateGadget... or ShowGadget...<br>I recommend always to create the window hidden (use WINDOW_HIDDEN flag)  and show it when needed, even if you need the window now, because it will less flicker.<br>When I need the dialog several times I create the dialog again and again and show as needed, so the user can fill them out in parallel. <br><br></td></tr></table><br>
<a name="1205122"></a>

<a name="1205123"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Henri</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I had to wrestle with dialogs myself and this is my take on this:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Import maxgui.drivers

Local app:TApp = New TApp
TDialog.Create(app)
Repeat
	WaitEvent()
Forever


'Main application type
'---------------------
Type TApp

	'Reference to dialogs
	Field dial:TDialog
	
	Field window_style:Int = WINDOW_TITLEBAR| WINDOW_RESIZABLE| WINDOW_STATUS|..
							WINDOW_CLIENTCOORDS| WINDOW_CENTER
	
	'Main application gadgets
	Field main_win:tgadget	= CreateWindow("Main",0,0,500,300,Null,window_style)
	Field button1:tgadget	= CreateButton("Open Dial 1",120,200,100,30,main_win)
	Field button2:tgadget	= CreateButton("Open Dial 2",270,200,100,30,main_win)
	
	Method New()
		AddHook EmitEventHook,eventhook,Self
	EndMethod
	
	Function eventhook:Object(id:Int,data:Object,context:Object)
		If TApp(context) Then TApp(context).OnEvent(TEvent(data))
		Return data
	End Function
	
	Method OnEvent(event:TEvent)
		Select event.id
		
		Case EVENT_WINDOWCLOSE, EVENT_APPTERMINATE
			Select event.source
			
			Case main_win	End
			
			EndSelect
		Case EVENT_GADGETACTION
			Select event.source
			
			Case button1	dial.dia1()
			Case button2	dial.dia2()
			
			EndSelect
		EndSelect
		
	EndMethod
End Type


'Dialog type
'------------
Type TDialog
	
	'Reference to main application
	Field app:TApp
	
	'Dialog window style
	Field dialog_style:Int = WINDOW_TITLEBAR|WINDOW_RESIZABLE|WINDOW_CLIENTCOORDS|..
						  			WINDOW_CENTER|WINDOW_TOOL|WINDOW_HIDDEN
	'Dialog 1
	Field dia1_win:tgadget
	Field dia1_button:tgadget
	
	'Dialog 2
	Field dia2_win:tgadget
	Field dia2_button:tgadget
	
	Method New()
		AddHook EmitEventHook,eventhook,Self
	EndMethod
	
	Function eventhook:Object(id:Int,data:Object,context:Object)
		If TDialog(context) Then TDialog(context).OnEvent(TEvent(data))
		Return data
	End Function

	Function Create(MainApp:TApp)
		Local d:TDialog = New TDialog
		d.app		= MainApp
		d.app.dial	= d
		d.OnInit()
	EndFunction
	
	Method OnInit()
		init_dia1()
		init_dia2()
	EndMethod
	
	Method OnEvent(event:TEvent)
		Select event.id
		
		Case EVENT_WINDOWCLOSE
			Select event.source
			
			Case dia1_win		init_dia1(False)
			Case dia2_win		init_dia2(False)
			
			EndSelect
			
		Case EVENT_GADGETACTION
			Select event.source
			
			Case dia1_button	dia1_button_GA()
			Case dia2_button	dia2_button_GA()
			
			EndSelect
		EndSelect
		
	EndMethod
	
	Method init_dia1(initial:Int=True)
		Select initial
		
		Case True
			dia1_win	= CreateWindow("Dialog 1",0,0,300,200,app.main_win,dialog_style)
			dia1_button	= CreateButton("OK",30,30,100,30,dia1_win)
		Case False
			EnableGadget app.main_win
			ClearFields(dia1_win)
			HideGadget dia1_win
		EndSelect
	EndMethod

	Method init_dia2(initial:Int=True)
		Select initial
		
		Case True
			dia2_win	= CreateWindow("Dialog 2",0,0,300,200,app.main_win,dialog_style)
			dia2_button	= CreateButton("OK",30,30,100,30,dia2_win)
		Case False
			EnableGadget app.main_win
			ClearFields(dia2_win)
			HideGadget dia2_win
		EndSelect
	EndMethod
	
	Method dia1()
		DisableGadget app.main_win
		ShowGadget dia1_win
	EndMethod
	
	Method dia2()
		DisableGadget app.main_win
		ShowGadget dia2_win
	EndMethod
	
	Method dia1_button_GA()
		init_dia1(False)
	EndMethod
	
	Method dia2_button_GA()
		init_dia2(False)
	EndMethod
	
End Type

'Clear all child gadgets				
Function ClearFields(parent:tgadget)
	
	For Local g:tgadget =EachIn parent.kids
	
		If g.kids.count() &gt; 0 Then ClearFields(g)
			
		Select GadgetClass(g)
		Case GADGET_TEXTFIELD	SetGadgetText(g,"")
		Case GADGET_TEXTAREA	SetGadgetText(g,"")
		Case GADGET_COMBOBOX	'SelectGadgetItem(g,0)
		Case GADGET_BUTTON
		Case GADGET_LISTBOX		ClearGadgetItems(g);RedrawGadget(g);Delay 1
		Case GADGET_PROGBAR		UpdateProgBar(g,0.0)
		Case GADGET_PANEL
		EndSelect
	Next
EndFunction



</textarea><br><br><br>-Henri <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
