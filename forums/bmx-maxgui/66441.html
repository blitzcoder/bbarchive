<!DOCTYPE html><html lang="en" ><head ><title >TextAreaUndo mod</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >TextAreaUndo mod</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >TextAreaUndo mod</a><br><br>
<a name="742339"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> This was extremely difficult and time-consuming.  It works just like it should, though:<br><a href="http://www.leadwerks.com/index.php?page=downloads.htm&amp;subbar=blank.htm" target="_blank">http://www.leadwerks.com/index.php?page=downloads.htm&amp;subbar=blank.htm</a><br><br>TextAreaUndo:Int(gadget:TGadget)<br>TextAreaRedo:Int(gadget:TGadget)<br>TextAreaCanUndo:Int(gadget:TGadget)<br>TextAreaCanRedo:Int(gadget:TGadget)<br>Function TextAreaFlushUndos:Int(gadget:TGadget)<br>TextAreaCreateUndoState()<br><br>If you manually alter the text area text with SetTextAreaText(), you must call TextAreaCreateUndoState() after the alterations, or your undos will be messed up.  With this, TextAreas will only return a GADGETACTION event when they really have changed.  EventData() will contain the length of the changed text, and may be a positive or negative number.<br><br>This only alters the changed text, so it has low memory usage, and is compatible with syntax highlighting.<br><br>Example program:<br><pre class=code>Import leadwerks.textareaundo

Strict 

Global window:TGadget
Global textarea:TGadget
Local menu:TGadget

window=CreateWindow("TextAreaUndo",200,200,640,480,,15|WINDOW_ACCEPTFILES)
textarea=CreateTextArea(0,0,ClientWidth(window),ClientHeight(window),window)
SetGadgetLayout textarea,1,1,1,1
ActivateGadget textarea
menu=CreateMenu("Edit",0,WindowMenu(window))
Global menuundo:TGadget=CreateMenu("Undo",1,menu,KEY_Z,MODIFIER_COMMAND)
Global menuredo:TGadget=CreateMenu("Redo",2,menu,KEY_Z,MODIFIER_COMMAND|MODIFIER_OPTION)
UpdateWindowMenu window

While WaitEvent()
	UpdateMenu()
	Select EventID()
		Case EVENT_MENUACTION
			Select EventData()
				Case 1
					TextAreaUndo textarea
				Case 2
					TextAreaRedo textarea
			EndSelect
		Case EVENT_WINDOWCLOSE
			End
		Case EVENT_APPTERMINATE
			End
	End Select
Wend

Function UpdateMenu()
	If TextAreaCanUndo(textarea)
		EnableMenu menuundo
	Else
		DisableMenu menuundo
	EndIf
	If TextAreaCanRedo(textarea)
		EnableMenu menuredo
	Else
		DisableMenu menuredo
	EndIf
	UpdateWindowMenu window
EndFunction</pre> <br><br></td></tr></table><br>
<a name="743094"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mousey</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just noticed this... many thanks Josh, a great piece of work. <br><br></td></tr></table><br>
<a name="824069"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Source:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">'Rem
Module leadwerks.textareaundo

Import BRL.FileSystem
Import BRL.System
Import BRL.Win32MaxGUI
Import BRL.Retro


'EndRem

Private

Type TUndoContext
	Global list:TList=New TList
	
	Field link:TLink
	Field gadget:TGadget
	Field undostates:TList=New TList
	Field current:TUndoState
	Field change
	Field disabled

	Method New()
		link=list.addlast(Self)
	EndMethod
	
	Method CreateUndoState:TUndostate(force=0)
		If current current.clearafter
		undostate:TUndoState=New TUndoState
		undostate.context=Self
		undostate.link=undostates.addlast(undostate)
		undostate.selpos=TextAreaCursor(gadget)
		undostate.sellen=TextAreaSelLen(gadget)
		undostate.update
		undostate.force=force
		current=undostate
		Return undostate
	EndMethod
		
	Method CanUndo:TUndoState(undostate:TUndoState=Null)
		If undostate=Null undostate=current
		If undostate
			If undostate.link.prevlink()
				prevundostate:TUndoState=TUndoState(undostate.link.prevlink().value())
				If prevundostate.force=-1
					Return TUndoState(prevundostate.link.prevlink().value())
				Else
					Return prevundostate
				EndIf
			EndIf
		EndIf
		Return Null
	EndMethod
	
	Method CanRedo:TUndoState(undostate:TUndoState=Null)
		If undostate=Null undostate=current
		If undostate
			If undostate.link.nextlink()
				nextundostate:TUndoState=TUndoState(undostate.link.nextlink().value())
				If nextundostate.force=1
					Return TUndoState(nextundostate.link.nextlink().value())
				Else
					Return nextundostate
				EndIf
			EndIf
		EndIf
		Return Null
	EndMethod

	Method Undo:Int()
		If current
			If canundo()
				current.undo
				current=canundo()
				Return True
			EndIf
		EndIf
		Return False
	EndMethod

	Method Redo:Int()
		undostate:TUndoState=CanRedo()
		If undostate
			undostate.redo
			current=undostate
			Return True
		EndIf
		Return False
	EndMethod

	Method Flush()
		For undostate:TUndoState=EachIn undostates
			undostate.kill
		Next
		undostates.clear
		current=Null
		createundostate
	EndMethod
	
	Method Kill()
		flush
		undostates=Null
		gadget=Null
		RemoveLink link
		link=Null
	EndMethod

	Function Callback:Object(id:Int,data:Object,context:Object)
		Local event:TEvent=TEvent(data)
		Select event.id
			
			Case EVENT_GADGETSELECT
				For undocontext:TUndoContext=EachIn TUndoContext.list
					If event.source=undocontext.gadget
						If undocontext.current
							If Not undocontext.disabled
								If undocontext.current.text=TextAreaText(undocontext.gadget)
									undocontext.current.selpos=TextAreaCursor(undocontext.gadget)
									undocontext.current.sellen=TextAreaSelLen(undocontext.gadget)
									undocontext.current.removetext=TextAreaText(undocontext.gadget,undocontext.current.selpos,undocontext.current.sellen)
								EndIf
							EndIf
						EndIf
						Exit
					EndIf
				Next
			
			Case EVENT_GADGETACTION
				For undocontext:TUndoContext=EachIn TUndoContext.list
					If event.source=undocontext.gadget
						If Not undocontext.disabled
							If undocontext.current
								If undocontext.current.text=TextAreaText(undocontext.gadget) Return Null
							EndIf
							undostate:TUndoState=undocontext.createundostate()
							event.data=undostate.change
						EndIf
						Exit
					EndIf
				Next
				
		EndSelect
		Return data
	EndFunction
	
EndType

Function CreateUndoContext:TUndocontext(gadget:TGadget)
	undocontext:TUndoContext=New TUndoContext
	undocontext.gadget=gadget
	undocontext.createundostate
	Return undocontext
EndFunction

Type TUndostate
	Field link:TLink
	Field context:TUndoContext
	Field text$
	Field undotext$
	Field redotext$
	Field selpos
	Field sellen
	Field undolen
	Field undopos
	Field change
	Field force
	
	Field removetext$
	Field removeposition
	
	Field addtext$
	Field addposition

	Method Update()
		text$=TextAreaText(context.gadget)
		selpos=TextAreaCursor(context.gadget)
		sellen=TextAreaSelLen(context.gadget)
		
		removetext=TextAreaText(context.gadget,selpos,sellen)
		
		lastundostate:TUndoState=context.canundo(Self)
		If lastundostate
			change=text.length-lastundostate.text.length
			change=change+lastundostate.sellen
		Else
			change=text.length
		EndIf
				
		If change&lt;0
			undopos=selpos
			undolen=-change
			If lastundostate
				undotext=Mid(lastundostate.text,undopos+1,-change)
			EndIf
		Else
			undopos=selpos-change
			undolen=change
			If lastundostate
				undotext=Mid(text,undopos+1,change)
			EndIf
		EndIf

	EndMethod

	Method Undo()
		start=TextAreaCursor(context.gadget)
		prevundostate:TUndoState=context.canundo(Self)
		nextundostate:TUndoState=context.canredo(Self)
		LockTextArea context.gadget
		If change&gt;0
			SetTextAreaText context.gadget,"",undopos,undolen
		Else
			SetTextAreaText context.gadget,undotext,undopos,0
		EndIf
		UnlockTextArea context.gadget
		prevundostate:TUndoState=context.canundo(Self)
		If prevundostate		
			SetTextAreaText context.gadget,prevundostate.removetext,prevundostate.selpos,0
			SelectTextAreaText context.gadget,prevundostate.selpos,prevundostate.sellen
		Else
			SelectTextAreaText context.gadget,0,0
		EndIf
		
		stop=TextAreaCursor(context.gadget)
		size=Abs(start-stop)+TextAreaSelLen(context.gadget)
		context.disabled=True
		EmitEvent CreateEvent(EVENT_GADGETACTION,context.gadget,size)
		context.disabled=False
	EndMethod
	
	Method Redo()
		start=TextAreaCursor(context.gadget)
		prevundostate:TUndoState=context.canundo(Self)
		nextundostate:TUndoState=context.canredo(Self)
		LockTextArea context.gadget
		If change&lt;0
			SetTextAreaText context.gadget,"",undopos,undolen
		Else
			If prevundostate l=prevundostate.sellen
			SetTextAreaText context.gadget,undotext,undopos,l
		EndIf
		UnlockTextArea context.gadget
		SelectTextAreaText context.gadget,selpos,sellen
		
		stop=TextAreaCursor(context.gadget)
		size=Abs(start-stop)+TextAreaSelLen(context.gadget)
		context.disabled=True
		EmitEvent CreateEvent(EVENT_GADGETACTION,context.gadget,size)
		context.disabled=False
	EndMethod
	
	Method ClearAfter()
		Local sublink:TLink
		Local nextsublink:TLink
		sublink=link.nextlink()
		While sublink
			nextsublink=sublink.nextlink()
			TUndoState(sublink.value()).kill
			sublink=nextsublink			
		Wend
	EndMethod
	
	Method Kill()
		If link.prevlink()
			current=TUndostate(link.prevlink().value())
		Else
			current=Null
		EndIf
		RemoveLink link
		link=Null
	EndMethod

EndType

AddHook EmitEventHook,TUndoContext.Callback

Function FindUndoContext:TUndoContext(gadget:TGadget)
	For undocontext:TUndoContext=EachIn TUndoContext.list
		If undocontext.gadget=gadget Return undocontext
	Next
	undocontext=CreateUndoContext(gadget)
	Return undocontext
EndFunction

Public

'Helper Functions
Function TextAreaUndo:Int(gadget:TGadget)
	Return FindUndoContext(gadget).undo()
EndFunction

Function TextAreaRedo:Int(gadget:TGadget)
	Return FindUndoContext(gadget).redo()
EndFunction

Function TextAreaCanUndo:Int(gadget:TGadget)
	Return (FindUndoContext(gadget).canundo()&lt;&gt;Null)
EndFunction

Function TextAreaCanRedo:Int(gadget:TGadget)
	Return (FindUndoContext(gadget).canredo()&lt;&gt;Null)
EndFunction

Function TextAreaFlushUndos:Int(gadget:TGadget)
	FindUndoContext(gadget).flush()
EndFunction

Function TextAreaCreateUndoState()
	FindUndoContext(gadget).createundostate
EndFunction</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
