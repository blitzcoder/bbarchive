<!DOCTYPE html><html lang="en" ><head ><title >Another spinner gadget</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Another spinner gadget</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >Another spinner gadget</a><br><br>
<a name="857301"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> To avoid floating-point errors this always uses integers.  The SPINNER_DECIMAL style will use increments of 0.1.  However, spinner values will still be stored as integers.  The SPINNER_WRAP flag does not currently work.<br><br>The float handling works perfectly in this, unlike some other implementations I have seen.<br><br><pre class=code>Function CreateSpinner:TSpinner( x:Int, y:Int, w:Int, h:Int, group:TGadget, style:Int=0 )
	Return TSpinner.Create(x,y,w,h,group,style)
EndFunction

Function SetSpinnerRange( spinner:TSpinner, low:Float, high:Float )
	spinner.SetRange low,high
End Function

Function SetSpinnerValue( spinner:TSpinner, value:Int )
	spinner.SetValue_ value
EndFunction

Function SpinnerValue:Int( spinner:TSpinner)
	Return spinner.getvalue()
EndFunction

Const SPINNER_DECIMAL=1
Const SPINNER_WRAP=2

Type TSpinner Extends TProxyGadget
	Field panel	:TGadget
	Field textField:TGadget
	Field stepper:TGadget
	Field trackbar:TGadget
	Field range	=10
	Field precision#=1.0
	Field value
	Field low=0,high=9
	
	Function Create:TSpinner( x:Int, y:Int, w:Int, h:Int, group:TGadget, style:Int=0 )
		Local spinner:TSpinner=New TSpinner
		spinner.panel = CreatePanel(x,y,w,h,group,PANEL_BORDER)
		spinner.stepper = CreateSlider(w-h+4,0,h-8,h-4,spinner.panel,SLIDER_STEPPER)
		spinner.textField = CreateTextField(-2,-2,w-h+8,h,spinner.panel)
		spinner.setrange spinner.low,spinner.high
		SetGadgetFilter spinner.textField,NumFilter
		spinner.SetProxy spinner.textField
		spinner.style=style
		AddHook EmitEventHook,SpinnerHook,spinner
		Return spinner
	EndFunction
	
	Method Free()
		RemoveHook EmitEventHook,SpinnerHook,Self
		'textField is freed in super
		If stepper FreeGadget stepper
		If panel FreeGadget panel
		stepper=Null
		panel=Null
		Super.Free
	End Method

	Method SetRange( lo, hi)
		low=lo
		high=hi
		range=high-low+1
		SetSliderRange stepper,0,range-1
		SetValue low
	EndMethod
	
	Method FixValue:Int(i)
		'If (SPINNER_WRAP &amp; style)
		'If i&lt;low Notify i+", "+(( (i-low) Mod range )+low)
		'	i=( (i-low) Mod range )+low
		'Else
			i=Max(i,low)
			i=Min(i,high)
		'EndIf
		Return i
	EndMethod
	
	Method SetValue_( i,updatetext=1)
		value=FixValue(i)
		If SliderValue(stepper)&lt;&gt;value-low SetSliderValue stepper,value-low
		If updatetext
			If SPINNER_DECIMAL &amp; style
				SetGadgetText textfield,String(PrettyFloat(value/10.0))
			Else
				SetGadgetText textfield,String(value)
			EndIf
		EndIf
	EndMethod

	Method GetValue:Int()
		Return value
	EndMethod
	
	Function SpinnerHook:Object( id:Int, data:Object, context:Object )
		Local spinner:TSpinner
		Local event:TEvent
		event=TEvent(data)
		If event
			spinner=TSpinner(context)	
			If spinner Return spinner.HandleEvents(event)
		EndIf
		Return data
	EndFunction
	
	Method HandleEvents:TEvent( event:TEvent )
		Select event.id
			Case EVENT_GADGETACTION
				If event.source = stepper
					If FixValue(event.data+low)&lt;&gt;value
						SetValue_ event.data+low,1
						EmitEvent CreateEvent(EVENT_GADGETACTION,Self,event.data)
						SendMessageA QueryGadget(textfield,QUERY_HWND),177,0,-1
						ActivateGadget textfield
						Return
					EndIf
				EndIf
				
				If event.source = Self
					Local i
					If SPINNER_DECIMAL &amp; style
						i=Round(Float(TextFieldText(textfield))*10.0)
					Else
						i=Int(TextFieldText(textfield))
					EndIf
					Local newvalue=FixValue(i)
					If newvalue&lt;&gt;value
						SetValue_ newvalue,0
						EmitEvent CreateEvent(EVENT_GADGETACTION,Self)
						Return
					EndIf
				EndIf
				
		EndSelect
		Return event
	EndMethod

	Function NumFilter:Int(event:TEvent, context:Object)
		If event.ID = EVENT_KEYCHAR
			If event.data = KEY_BACKSPACE Return 1
			If event.data = 47 Return
			If event.data &lt; 45 Or event.data &gt; 57 Return
		EndIf
		Return 1
	End Function

	Function PrettyFloat:String(value:Float,places=1)
		Local s$
		value=value*(10.0^places)
		s=Round(value)
		Local i$=Int(Left(s,s.length-1))
		Return i+"."+Right(s,1)
	EndFunction
	
EndType</pre> <br><br></td></tr></table><br>
<a name="857576"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dmaz</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> nevermind. <br><br></td></tr></table><br>
<a name="903184"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Add this<br><br><pre class=code>
	Method SetEnabled(_state:Int=True)
		If _state=False
			DisableGadget stepper
			DisableGadget panel
		Else
			EnableGadget stepper
			EnableGadget panel
		End If
		Super.setenabled(_state)
	End Method
</pre><br><br>to make the Spinner gadget acts like standard MaxGUI gadgets. This means you can use<br><pre class=code>
DisableGadget myspinner:tspinner
EnableGadget myspinner:tspinner
</pre><br>to disable properly the gadget (spinner+textfied).<br><br>And replace the method SetValue_ with this one<br><pre class=code>
Method SetValue_( i,updatetext=1)
value=FixValue(i)
If SliderValue(stepper)&lt;&gt;value-low SetSliderValue stepper,value-low
If updatetext
If SPINNER_DECIMAL &amp; style
	SetGadgetText textfield,String(PrettyFloat(value/10.0))
Else
	SetGadgetText textfield,String(value)
EndIf
			
If value&lt;0 SetGadgetColor textfield,220,20,20 Else SetGadgetColor textfield,255,255,255
			
					
	Local temp$="                                      "+TextFieldText(textfield)
	temp=Right(temp,16+(Len(temp)&gt;3))
	SetGadgetText textfield,temp
EndIf
endMethod

</pre><br>to have a 'visual' response on negative value: this could be useful.<br>And I added an 'hack' for the right alignment, but I cant' guarantee if it will work well. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
