<!DOCTYPE html><html lang="en" ><head ><title >Why do my gadgets redraw slowly?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Why do my gadgets redraw slowly?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >Why do my gadgets redraw slowly?</a><br><br>
<a name="981807"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >sswift</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have around 26 sliders/textboxes down one side of my window, and when I minimize and then restore I can actually see it redrawing the checkboxes first, and then the sliders.  It takes around half a second to do this.<br><br>Why would it even draw the textboxes first anyway?  Shouldn't gadgets be drawn in the order they are created?  I didn't create all the textboxes first.  I created one with each slider.<br><br>Also, on a related note, why are my sliders not being redrawn until I stop moving the mouse when I'm resizing the window?  The regular sliders are redrawn.<br><br>Here's my code:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

SuperStrict
Import MaxGUI.Drivers

' Version:
	
	Const DEMO%  = 0	
	Const BASIC% = 1	
	Const PRO%   = 2

	Global VERSION% = DEMO'PRO
	Global VERSION_NUMBER$ = "1.0"
	
	Select VERSION
			
		Case DEMO  AppTitle$ = "Seamless Texture Generator 2 DEMO" 
		Case BASIC AppTitle$ = "Seamless Texture Generator 2"
		Case PRO   AppTitle$ = "Seamless Texture Generator 2 PRO"

	End Select
		
	Global EXIT_PITCH$       = "Thank you for trying Seamless Texture Generator 2." + Chr$(13) + "Would you like to get the full version now?"
	Global TEXTURE_UPGRADE$  = "This version of Seamless Texture Generator 2 is limited to a max resolution of" + Chr$(13) + "512x512, but if you upgrade to a PROFESSIONAL license, you can render textures of any size!" + Chr$(13) + Chr$(13) + "Would you like to upgrade to Seamless Texture Generator 2 PROFESSIONAL at this time?"
	Global LOADFILE_UPGRADE$ = "This version of Seamless Texture Generator 2 does not support loading PSD or DDS files," + Chr$(13) + "but Seamless Image Generator 2 PRO does!" + Chr$(13) + "Would you like to upgrade to Seamless Image Generator 2 PRO?" 

	Global ORDER_ALL$        = "http://www.shareit.com/product.html?productid=300018902"
	Global ORDER_PRO$        = "http://www.shareit.com/product.html?productid=300018903"
	Global ORDER_PROUPGRADE$ = "http://www.shareit.com/product.html?productid=300018906"


' Default image adjustment settings:
	
	Global HBlendRadius_Default# = 1 
	Global VBlendRadius_Default# = 1

	Global ShadingStrength_Default#   = 1
	Global ShadingBlurRadius_Default# = 64
		
	Global Sharpness_Default# = 0
		
	Global FinalWidth_Default%  = 512
	Global FinalHeight_Default% = 512
		
		
	' Current image adjustment settings.	
		Global FinalWidth%  = FinalWidth_Default
		Global FinalHeight% = FinalHeight_Default
	
		Global Seamless_U% = True
		Global Seamless_V% = True

		Global Quad_X#[4]				
		Global Quad_Y#[4]

	' Min and max values for all the image adjustment controls.		
		Global HBlendRadius_Min# = 0 
		Global HBlendRadius_Max# = 1
		Global HBlendRadius_SliderSteps% = 100

		Global VBlendRadius_Min# = 0 
		Global VBlendRadius_Max# = 1 
		Global VBlendRadius_SliderSteps% = 100

		Global ShadingStrength_Min# = 0
		Global ShadingStrength_Max# = 1
		Global ShadingStrength_SliderSteps% = 100
	
		Global ShadingBlurRadius_Min# = 1
		Global ShadingBlurRadius_Max# = 256
		Global ShadingBlurRadius_SliderSteps% = 255

		Global Sharpness_Min# = 0
		Global Sharpness_Max# = 1
		Global Sharpness_SliderSteps% = 100
               	
		Global FinalWidth_Min% = 2
		Global FinalWidth_Max% 
		
		Global FinalHeight_Min% = 2
		Global FinalHeight_Max% 

		Select VERSION
			
			Case DEMO, BASIC
				FinalWidth_Max  = 512
				FinalHeight_Max = 512
				
			Case PRO        
				FinalWidth_Max  = 65536
				FinalHeight_Max = 65536

		End Select

	' Old image adjustment settings.
		
		' These are set each time the image is recalculated. 
		' They allow the system to tell when a value has been changed, which tells it at which stage in the texture process it should begin recalculating the image.
		
		' These values need to be reset in the load image function so the next time the image is recalculated all stages will be updated.
		
		Global Old_HBlendRadius# 
		Global Old_VBlendRadius# 

		Global Old_ShadingStrength#
		Global Old_ShadingBlurRadius#  

		Global Old_Sharpness#

		Global Old_FinalWidth%
		Global Old_FinalHeight%

		Global Old_Quad_X#[4]
		Global Old_Quad_Y#[4]


	Const TAB_ALL%		   = -1						' Not an actual tab.  This constant is used by some functions to perform operations on all tabs at once.
	Const TAB_ORIGINAL%    =  0						' The original unaltered image, as loaded from disk, in RGB format.  There is one case where this image may not be the original image as loaded from disk, and that would be if the user clicked the FINALIZE button so they can perform a second set of operations on the image.
	Const TAB_TRANSFORMED% =  1						' This is the original image after it has been rotated, scaled, and adjusted for perspective.
	Const TAB_ADJUSTED%    =  2
	Const TAB_FINAL%       =  3						' The final image, after all operations have been performed.  This is what gets saved to disk. 
	Const TAB_TILED_TF%	   =  4 						' The transformed image, tiled.
	Const TAB_TILED_FINAL% =  5						' The final image, tiled.

	Global Active_Tab% 								' This is the index number of the currently active canvas.
	Global Last_Active_Tab%							' Used by UpdatePreviewPane function.


' Fonts:
	
	' Do not use 8pt font on a PC.  It will be 9 pixels tall, but narrower and harder to read.
		
	?Win32	
	
		Global FONT_Label:TGuiFont     = LookupGuiFont(GUIFONT_SYSTEM, 7) ' 7 Pixels 
		Global FONT_Textfield:TGuiFont = LookupGuiFont(GUIFONT_SYSTEM, 9) ' 9 pixels
		Global FONT_Header:TGuiFont    = LookupGuiFont(GUIFONT_SYSTEM, 9) ' 9 pixels 
		
	?MacOS
	
		Global FONT_Label:TGuiFont     = LookupGuiFont(GUIFONT_SYSTEM, 9)  ' 7 Pixels 
		Global FONT_Textfield:TGuiFont = LookupGuiFont(GUIFONT_SYSTEM, 12) ' 9 pixels
		Global FONT_Header:TGuiFont    = LookupGuiFont(GUIFONT_SYSTEM, 12) ' 9 pixels 
		
	?
	

' Window:

	Global WINDOW_Main:TGadget

		' Menu:
	
			Global MENU_Root:TGadget
				Global MENU_File:TGadget
					Global MENU_Open:TGadget
					Global MENU_OpenRecent:TGadget
						Global MENU_RecentFile:TGadget[10]
					Global MENU_Save:TGadget
					Global MENU_Batch:TGadget
					Global MENU_Exit:TGadget
				Global MENU_Options:TGadget
					Global MENU_AutoUpdate:TGadget
					Global MENU_Render:TGadget
						Global MENU_Reflectance:TGadget
						Global MENU_Displacement:TGadget
						Global MENU_Normal:TGadget			
				Global MENU_Help:TGadget
					Global MENU_Contents:TGadget
					Global MENU_About:TGadget
				Global MENU_Buynow:TGadget
		
		' Preview pane:
					
			Global CANVAS_Preview:TGadget
			Global PANEL_PreviewPaneIndent:TGadget, PANEL_PreviewPane:TGadget, TABBER_PreviewPane:TGadget, HSLIDER_PreviewPane:TGadget, VSLIDER_PreviewPane:TGadget
			Global PROGBAR_Progressbar:TGadget
	
		' Control panel:
		
			Global PANEL_Indent:TGadget, PANEL_Controls:TGadget, VSLIDER_Controls:TGadget
				
				Global PANEL_Color:TGadget
		
					Global TEXTFIELD_Brightness:TGadget
					Global HSLIDER_Brightness:TGadget
		
					Global TEXTFIELD_Contrast:TGadget
					Global HSLIDER_Contrast:TGadget
		
					Global TEXTFIELD_Red:TGadget
					Global HSLIDER_Red:TGadget
		
					Global TEXTFIELD_Green:TGadget
					Global HSLIDER_Green:TGadget
		
					Global TEXTFIELD_Blue:TGadget
					Global HSLIDER_Blue:TGadget
				
				Global PANEL_Flash:TGadget
				
					Global TEXTFIELD_ShadingStrength:TGadget
					Global HSLIDER_ShadingStrength:TGadget
			
					Global TEXTFIELD_ShadingBlurRadius:TGadget
					Global HSLIDER_ShadingBlurRadius:TGadget
			
				Global TEXTFIELD_HBlend:TGadget
				Global HSLIDER_HBlend:TGadget
		
				Global TEXTFIELD_VBlend:TGadget
				Global HSLIDER_VBlend:TGadget
		
				Global TEXTFIELD_Sharpness:TGadget
				Global HSLIDER_Sharpness:TGadget
		
				Global TEXTFIELD_FinalWidth:TGadget
				Global TEXTFIELD_FinalHeight:TGadget
		
				Global BUTTON_Render:TGadget	
				Global BUTTON_Preview:TGadget	
		

' ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
' This function creates a separator. 
' It uses the LABEL_SUNKENFRAME style because it looks more professional than LABEL_SEPARATOR.
' ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

	Function CreateSeparator:TGadget(X%, Y%, Width%, Group:TGadget)

		Local Temp:TGadget

		Temp = CreateLabel("", X, Y, Width, 2, Group, LABEL_SUNKENFRAME)
		SetGadgetLayout Temp, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
	
		Return Temp
	
	End Function	


' ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
' This function initializes the application.
' ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

	Function Initialize()
		
		Const HEIGHT_LABEL%     = 16
		Const WIDTH_LABEL%	    = 256
		Const HEIGHT_TEXTFIELD% = 22
		Const WIDTH_TEXTFIELD%  = 40
		Const HEIGHT_TRACKBAR%  = 22
		Const SIZE_SLIDER%      = 16
		Const HEIGHT_BUTTON%    = 24
		Const HEIGHT_COMBOBOX%  = 24
		Const HEIGHT_PROGBAR%   = 22
		Const HEIGHT_SEPARATOR% = 2
		Const SIZE_SPACE%       = 8
		Const WIDTH_CONTROLPANEL% = 200
		
		Local WIDTH_TRACKBAR%
		
		Local Width%, Height%, X%, Y%
		Local Panel:TGadget, Label:TGadget, TextField:TGadget, Slider:TGadget, Button:TGadget
	
			
		' Create window at the smallest size it can be, then maximize it.
			
			WINDOW_Main = CreateWindow(AppTitle$, 0, 0, 640, 480, Null, WINDOW_TITLEBAR|WINDOW_RESIZABLE|WINDOW_MENU|WINDOW_STATUS|WINDOW_ACCEPTFILES|WINDOW_HIDDEN) 
	
		' Save the size of the area inside the window in which we can draw.
			
			'WINDOW_Width  = ClientWidth(WINDOW_Main)
			'WINDOW_Height = ClientHeight(WINDOW_Main)
	
		Rem 
		' Load the watermark.
			If VERSION = EVALULATION
			
				' Load the watermark.
					CopyFile "config.dat", "config.png" 
					IMAGE_Watermark = LoadImage("config.png")
					DeleteFile "config.png"
								
				' If the watermark could not be loaded, pop up an error and exit the program.
					If IMAGE_Watermark = 0 
						Notify("Error: CONFIG.DAT not found.  Please reinstall the program.", True)
						End
					EndIf
	
				' If the watermark has the wrong CRC, exit with an error.
					
					' DebugLog(CRC_File("config.dat"))
					
					If CRC_File("config.dat") &lt;&gt; 669102732
						Notify("Error: CONFIG.DAT has been corrupted.  Please reinstall the program.")
						End
					EndIf  
							
	
			EndIf
			
		' Load fonts.
		'	FONT_ArialBlack   = LoadFont("arial black", 32)
			
		End Rem
		
		' Create the menus.	
		
			MENU_Root = WindowMenu(WINDOW_Main)
		
				MENU_File = CreateMenu("&amp;File", 0, MENU_Root)
			
					MENU_Open = CreateMenu("&amp;Open...", 1, MENU_File, KEY_O, MODIFIER_COMMAND)
					MENU_OpenRecent = CreateMenu("Open &amp;Recent", 2, MENU_File)		
						
						MENU_RecentFile[0] = CreateMenu("c:\blah.jpg", 200, MENU_OpenRecent)			
						MENU_RecentFile[1] = CreateMenu("c:\texture.jpg", 201, MENU_OpenRecent)			
						
					CreateMenu("", 0, MENU_File)			
					MENU_Save = CreateMenu("&amp;Save...", 3, MENU_File, KEY_S, MODIFIER_COMMAND)
					CreateMenu("", 0, MENU_File)
					MENU_Batch = CreateMenu("&amp;Batch...", 4, MENU_File, KEY_B, MODIFIER_COMMAND)			
					CreateMenu("", 0, MENU_File)			
					MENU_Exit = CreateMenu("E&amp;xit", 99, MENU_File)			
	
					DisableMenu(MENU_Save)
	
				MENU_Options = CreateMenu("&amp;Options", 0, MENU_Root)			
					MENU_AutoUpdate = CreateMenu("&amp;Auto Update", 5, MENU_Options) ' Use CheckMenu() when menu selection event occurs to add checkmark.
					CreateMenu("", 0, MENU_Options)
					MENU_Render = CreateMenu("&amp;Render", 6, MENU_Options)
						MENU_Reflectance = CreateMenu("&amp;Reflectance", 7, MENU_Render)
						MENU_Displacement = CreateMenu("&amp;Displacement", 8, MENU_Render)
						MENU_Normal = CreateMenu("&amp;Normal", 9, MENU_Render)
									
				MENU_Help = CreateMenu("&amp;Help", 0, MENU_Root)
					
					MENU_Contents  = CreateMenu("&amp;Help Contents...", 10, MENU_Help, KEY_F1)	
					CreateMenu("",	0, MENU_Help)	
					MENU_About = CreateMenu("&amp;About...", 11, MENU_Help)	
		
	'			If VERSION = EVALULATION
	'				MENU_Buynow = CreateMenu("&amp;Click here to get the full version!", 100, MENU_Root)
	'			EndIf
		
			UpdateWindowMenu(WINDOW_Main)		
			
			CreateSeparator(0, 0, ClientWidth(WINDOW_Main), WINDOW_Main)
	
	
		' Create the preview window.
			
			' Create an indentation around the main texture Panel.
				
			'	PANEL_PreviewPaneIndent = CreatePanel(0, 0, ClientWidth(WINDOW_Main)-220, ClientHeight(WINDOW_Main), WINDOW_Main, PANEL_GROUP)
			'	SetGadgetLayout(PANEL_PreviewPaneIndent, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_ALIGNED)
	
			' Create the Tabber at the top of the preview pane.
		
				TABBER_PreviewPane = CreateTabber(0, HEIGHT_SEPARATOR+SIZE_SPACE, ClientWidth(WINDOW_Main)-WIDTH_CONTROLPANEL, ClientHeight(WINDOW_Main)-HEIGHT_SEPARATOR-SIZE_SPACE, WINDOW_Main)
					SetGadgetLayout(TABBER_PreviewPane, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_ALIGNED)
					'SetGadgetFont(TABBER_PReviewPane, FONT_Tabber)
					
					AddGadgetItem(TABBER_PreviewPane, "Original") 
					AddGadgetItem(TABBER_PreviewPane, "Seamless")
					AddGadgetItem(TABBER_PreviewPane, "Reflectance")
					AddGadgetItem(TABBER_PreviewPane, "Displacement")
					AddGadgetItem(TABBER_PreviewPane, "Normal")
					AddGadgetItem(TABBER_PreviewPane, "Preview")
					
												
			' Create a Panel to clip the canvas that will contain our image.
				PANEL_PreviewPane = CreatePanel(0, 2, ClientWidth(TABBER_PreviewPane)-16, ClientHeight(TABBER_PreviewPane)-16-2, TABBER_PreviewPane)
					SetGadgetLayout PANEL_PreviewPane, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_ALIGNED
					SetPanelColor   PANEL_PreviewPane, 0, 0, 0 
	
			' Create sliders.
									
				HSLIDER_PreviewPane = CreateSlider(GadgetX(PANEL_PreviewPane), GadgetY(PANEL_PreviewPane) + GadgetHeight(PANEL_PreviewPane), GadgetWidth(PANEL_PreviewPane), SIZE_SLIDER, TABBER_PreviewPane, SLIDER_SCROLLBAR|SLIDER_HORIZONTAL)
					SetGadgetLayout HSLIDER_PreviewPane, EDGE_ALIGNED,  EDGE_ALIGNED, EDGE_CENTERED, EDGE_ALIGNED
	
				VSLIDER_PreviewPane = CreateSlider(GadgetX(PANEL_PreviewPane) + GadgetWidth(PANEL_PreviewPane), GadgetY(PANEL_PreviewPane), SIZE_SLIDER, GadgetHeight(PANEL_PreviewPane), TABBER_PreviewPane, SLIDER_SCROLLBAR|SLIDER_VERTICAL)
					SetGadgetLayout VSLIDER_PreviewPane, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED,  EDGE_ALIGNED
	
			' Create canvas.
			
				CANVAS_Preview = CreateCanvas(0, 0, ClientWidth(PANEL_PreviewPane), ClientHeight(PANEL_PreviewPane), PANEL_PreviewPane)
				SetGadgetLayout(CANVAS_Preview, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_ALIGNED)

							
		' Create the control Panel.
	
			' Render button:
				
				X = ClientWidth(WINDOW_Main) - WIDTH_CONTROLPANEL + SIZE_SLIDER + 2
				Y = ClientHeight(WINDOW_Main) - (SIZE_SPACE + HEIGHT_BUTTON)
				Width  = WIDTH_CONTROLPANEL - SIZE_SLIDER*2 - 4
				Height = HEIGHT_BUTTON
				
				BUTTON_Render = CreateButton("Render Texture", X, Y, Width, Height, WINDOW_Main, BUTTON_PUSH)
					SetGadgetLayout BUTTON_Render, EDGE_CENTERED, EDGE_ALIGNED, EDGE_CENTERED, EDGE_ALIGNED
					'SetGadgetFont BUTTON_Render, GUIFONT_Bold	
	
					
			' Preview button:
				
			'	Y = Y - SIZE_SPACE - HEIGHT_BUTTON
		
			'	BUTTON_Preview = CreateButton("Render Preview", X, Y, Width, Height, WINDOW_Main, BUTTON_PUSH)
			'		SetGadgetLayout BUTTON_Preview, EDGE_CENTERED, EDGE_ALIGNED, EDGE_CENTERED, EDGE_ALIGNED
	
					
			' Control panel indent: 
				
				Width  = WIDTH_CONTROLPANEL
				Height = Y - HEIGHT_SEPARATOR - SIZE_SPACE*2 
				X = ClientWidth(WINDOW_Main) - WIDTH_CONTROLPANEL
				Y = HEIGHT_SEPARATOR 
	
				Panel = CreatePanel(X, Y, Width, Height, WINDOW_Main, PANEL_GROUP)
					SetGadgetLayout Panel, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_ALIGNED
					
					PANEL_Indent = Panel
				
				
			' Control panel slider:
				
				VSLIDER_Controls = CreateSlider(ClientWidth(PANEL_Indent)-16, 0, SIZE_SLIDER, ClientHeight(PANEL_Indent), PANEL_Indent, SLIDER_SCROLLBAR|SLIDER_VERTICAL)
					SetGadgetLayout VSLIDER_Controls, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_ALIGNED
					'SetSliderRange  VSLIDER_Controls, 0, 100
					'Set_Slider_Value(HSLIDER_ShadingStrength, ShadingStrength_Default#)
				
									
			' Control panel:
	
				Panel = CreatePanel(0, 0, ClientWidth(PANEL_Indent)-SIZE_SLIDER, 2048, PANEL_Indent) 
					SetGadgetLayout Panel, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
					
					PANEL_Controls = Panel
				
				WIDTH_TRACKBAR = ClientWidth(Panel) - (SIZE_SPACE*5 + WIDTH_TEXTFIELD)
				
				Width = ClientWidth(PANEL_Controls) - SIZE_SPACE*2 ' The width of the interior of the control panel, with some space around the edges.
				
				Y = 0
				
				
			' Shadow Removal:
				
				X = SIZE_SPACE
							
				Label = CreateLabel("Shadow removal :", X, Y, Width, HEIGHT_LABEL, Panel)
					SetGadgetLayout Label, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
					SetGadgetFont Label, FONT_Header
				
				Y = Y + HEIGHT_LABEL + SIZE_SPACE*2
	
				X = X + SIZE_SPACE
	
	
					' Strength:
										
						Label = CreateLabel("Strength", X, Y, Width, HEIGHT_LABEL, Panel)
							SetGadgetLayout Label, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
							SetGadgetFont Label, FONT_Label
							
						Y = Y + HEIGHT_LABEL
	
						Slider = CreateSlider(X, Y, WIDTH_TRACKBAR, HEIGHT_TRACKBAR, Panel, SLIDER_HORIZONTAL|SLIDER_TRACKBAR)
							SetGadgetLayout(Slider, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED)
							SetSliderRange(Slider, 0, 1000)
							SetSliderValue(Slider, 0)
	
						TextField = CreateTextField(X + WIDTH_TRACKBAR + SIZE_SPACE, Y, WIDTH_TEXTFIELD, HEIGHT_TEXTFIELD, Panel)
							SetGadgetLayout Textfield, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
							SetGadgetFont   Textfield, FONT_Textfield
							SetGadgetText   Textfield, ShadingStrength_Default#
						
						'TEXTFIELD_Brightness = TextField
						'HSLIDER_Brightness = Slider
						
						Y = Y + HEIGHT_TEXTFIELD + SIZE_SPACE
	
	
					' Radius:
										
						Label = CreateLabel("Radius", X, Y, Width, HEIGHT_LABEL, Panel)
							SetGadgetLayout Label, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
							SetGadgetFont Label, FONT_Label
							
						Y = Y + HEIGHT_LABEL
	
						Slider = CreateSlider(X, Y, WIDTH_TRACKBAR, HEIGHT_TRACKBAR, Panel, SLIDER_HORIZONTAL|SLIDER_TRACKBAR)
							SetGadgetLayout(Slider, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED)
							SetSliderRange(Slider, 0, 1000)
							SetSliderValue(Slider, 0)
	
						TextField = CreateTextField(X + WIDTH_TRACKBAR + SIZE_SPACE, Y, WIDTH_TEXTFIELD, HEIGHT_TEXTFIELD, Panel)
							SetGadgetLayout Textfield, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
							SetGadgetFont   Textfield, FONT_Textfield
							SetGadgetText   Textfield, ShadingStrength_Default#
						
						'TEXTFIELD_Brightness = TextField
						'HSLIDER_Brightness = Slider
						
						Y = Y + HEIGHT_TEXTFIELD + SIZE_SPACE
				
	
			' Separator:	
				Y = Y + SIZE_SPACE
				CreateSeparator(SIZE_SPACE, Y, Width, Panel)
				Y = Y + HEIGHT_SEPARATOR + SIZE_SPACE			
				
			
			' Blending:
	
				X = SIZE_SPACE
							
				Label = CreateLabel("Seam removal :", X, Y, Width, HEIGHT_LABEL, Panel)
					SetGadgetLayout Label, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
					SetGadgetFont Label, FONT_Header
				
				Y = Y + HEIGHT_LABEL + SIZE_SPACE*2
	
				X = X + SIZE_SPACE
	
	
					' Horizontal:
										
						Label = CreateLabel("Horizontal", X, Y, Width, HEIGHT_LABEL, Panel)
							SetGadgetLayout Label, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
							SetGadgetFont Label, FONT_Label
							
						Y = Y + HEIGHT_LABEL
	
						Slider = CreateSlider(X, Y, WIDTH_TRACKBAR, HEIGHT_TRACKBAR, Panel, SLIDER_HORIZONTAL|SLIDER_TRACKBAR)
							SetGadgetLayout(Slider, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED)
							SetSliderRange(Slider, 0, 1000)
							SetSliderValue(Slider, 1000)
	
						TextField = CreateTextField(X + WIDTH_TRACKBAR + SIZE_SPACE, Y, WIDTH_TEXTFIELD, HEIGHT_TEXTFIELD, Panel)
							SetGadgetLayout Textfield, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
							SetGadgetFont   Textfield, FONT_Textfield
							SetGadgetText   Textfield, "1.0"
	
						'Set_Slider_Value(HSLIDER_HBlend, HBlendRadius_Default#)
						
						'TEXTFIELD_Brightness = TextField
						'HSLIDER_Brightness = Slider
						
						Y = Y + HEIGHT_TEXTFIELD + SIZE_SPACE
	
	
					' Vertical:
										
						Label = CreateLabel("Vertical", X, Y, Width, HEIGHT_LABEL, Panel)
							SetGadgetLayout Label, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
							SetGadgetFont Label, FONT_Label
							
						Y = Y + HEIGHT_LABEL
	
						Slider = CreateSlider(X, Y, WIDTH_TRACKBAR, HEIGHT_TRACKBAR, Panel, SLIDER_HORIZONTAL|SLIDER_TRACKBAR)
							SetGadgetLayout(Slider, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED)
							SetSliderRange(Slider, 0, 1000)
							SetSliderValue(Slider, 1000)
	
						TextField = CreateTextField(X + WIDTH_TRACKBAR + SIZE_SPACE, Y, WIDTH_TEXTFIELD, HEIGHT_TEXTFIELD, Panel)
							SetGadgetLayout Textfield, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
							SetGadgetFont   Textfield, FONT_Textfield
							SetGadgetText   Textfield, "1.0"
						
						'TEXTFIELD_Brightness = TextField
						'HSLIDER_Brightness = Slider
						
						Y = Y + HEIGHT_TEXTFIELD + SIZE_SPACE
	
	
					' Overlap:
										
						Label = CreateLabel("Overlap", X, Y, Width, HEIGHT_LABEL, Panel)
							SetGadgetLayout Label, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
							SetGadgetFont Label, FONT_Label
							
						Y = Y + HEIGHT_LABEL
	
						Slider = CreateSlider(X, Y, WIDTH_TRACKBAR, HEIGHT_TRACKBAR, Panel, SLIDER_HORIZONTAL|SLIDER_TRACKBAR)
							SetGadgetLayout(Slider, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED)
							SetSliderRange(Slider, 0, 1000)
							SetSliderValue(Slider, 1000)
	
						TextField = CreateTextField(X + WIDTH_TRACKBAR + SIZE_SPACE, Y, WIDTH_TEXTFIELD, HEIGHT_TEXTFIELD, Panel)
							SetGadgetLayout Textfield, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
							SetGadgetFont   Textfield, FONT_Textfield
							SetGadgetText   Textfield, "10%"
						
						'TEXTFIELD_Brightness = TextField
						'HSLIDER_Brightness = Slider
						
						Y = Y + HEIGHT_TEXTFIELD + SIZE_SPACE
	
	
	
			' Separator:	
				Y = Y + SIZE_SPACE
				CreateSeparator(SIZE_SPACE, Y, Width, Panel)
				Y = Y + HEIGHT_SEPARATOR + SIZE_SPACE			
			
										
			' Final Adjustment:
				
				X = SIZE_SPACE
							
				Label = CreateLabel("Color adjustment :", X, Y, Width, HEIGHT_LABEL, Panel)
					SetGadgetLayout Label, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
					SetGadgetFont Label, FONT_Header
				
				Y = Y + HEIGHT_LABEL + SIZE_SPACE*2
	
				X = X + SIZE_SPACE ' Indent
							
					' Brightness:
										
						Label = CreateLabel("Brightness", X, Y, Width, HEIGHT_LABEL, Panel)
							SetGadgetLayout Label, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
							SetGadgetFont Label, FONT_Label
							
						Y = Y + HEIGHT_LABEL
	
						Slider = CreateSlider(X, Y, WIDTH_TRACKBAR, HEIGHT_TRACKBAR, Panel, SLIDER_HORIZONTAL|SLIDER_TRACKBAR)
							SetGadgetLayout(Slider, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED)
							SetSliderRange(Slider, 0, 1000)
							SetSliderValue(Slider, 500)
	
						TextField = CreateTextField(X + WIDTH_TRACKBAR + SIZE_SPACE, Y, WIDTH_TEXTFIELD, HEIGHT_TEXTFIELD, Panel)
							SetGadgetLayout Textfield, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
							SetGadgetFont   Textfield, FONT_Textfield
							SetGadgetText   Textfield, "1"
						
						TEXTFIELD_Brightness = TextField
						HSLIDER_Brightness = Slider
						
						Y = Y + HEIGHT_TEXTFIELD + SIZE_SPACE
	
							
					' Contrast:
									
						Label = CreateLabel("Contrast", X, Y, Width, HEIGHT_LABEL, Panel)
							SetGadgetLayout Label, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
							SetGadgetFont Label, FONT_Label
							
						Y = Y + HEIGHT_LABEL
	
						Slider = CreateSlider(X, Y, WIDTH_TRACKBAR, HEIGHT_TRACKBAR, Panel, SLIDER_HORIZONTAL|SLIDER_TRACKBAR)
							SetGadgetLayout(Slider, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED)
							SetSliderRange(Slider, 0, 1000)
							SetSliderValue(Slider, 500)
					
						TextField = CreateTextField(X + WIDTH_TRACKBAR + SIZE_SPACE, Y, WIDTH_TEXTFIELD, HEIGHT_TEXTFIELD, Panel)
							SetGadgetLayout Textfield, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
							SetGadgetFont   Textfield, FONT_Textfield
							SetGadgetText   Textfield, "1"
					
						Y = Y + HEIGHT_TEXTFIELD + SIZE_SPACE
						
					' Hue:
					
						Label = CreateLabel("Hue", X, Y, Width, HEIGHT_LABEL, Panel)
							SetGadgetLayout Label, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
							SetGadgetFont Label, FONT_Label
							
						Y = Y + HEIGHT_LABEL
	
						Slider = CreateSlider(X, Y, WIDTH_TRACKBAR, HEIGHT_TRACKBAR, Panel, SLIDER_HORIZONTAL|SLIDER_TRACKBAR)
							SetGadgetLayout(Slider, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED)
							SetSliderRange(Slider, 0, 1000)
							SetSliderValue(Slider, 500)
					
						TextField = CreateTextField(X + WIDTH_TRACKBAR + SIZE_SPACE, Y, WIDTH_TEXTFIELD, HEIGHT_TEXTFIELD, Panel)
							SetGadgetLayout Textfield, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
							SetGadgetFont   Textfield, FONT_Textfield
							SetGadgetText   Textfield, "0"
					
						Y = Y + HEIGHT_TEXTFIELD + SIZE_SPACE
						
					' Saturation:
					
						Label = CreateLabel("Saturation", X, Y, Width, HEIGHT_LABEL, Panel)
							SetGadgetLayout Label, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
							SetGadgetFont Label, FONT_Label
							
						Y = Y + HEIGHT_LABEL
	
						Slider = CreateSlider(X, Y, WIDTH_TRACKBAR, HEIGHT_TRACKBAR, Panel, SLIDER_HORIZONTAL|SLIDER_TRACKBAR)
							SetGadgetLayout(Slider, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED)
							SetSliderRange(Slider, 0, 1000)
							SetSliderValue(Slider, 500)
								
						TextField = CreateTextField(X + WIDTH_TRACKBAR + SIZE_SPACE, Y, WIDTH_TEXTFIELD, HEIGHT_TEXTFIELD, Panel)
							SetGadgetLayout Textfield, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
							SetGadgetFont   Textfield, FONT_Textfield
							SetGadgetText   Textfield, "0"
							
					
						Y = Y + HEIGHT_TEXTFIELD + SIZE_SPACE
	
			' Separator:	
				Y = Y + SIZE_SPACE
				CreateSeparator(SIZE_SPACE, Y, Width, Panel)
				Y = Y + HEIGHT_SEPARATOR + SIZE_SPACE			
	
	
			' Sharpening:
				
				X = SIZE_SPACE
							
				Label = CreateLabel("Sharpening :", X, Y, Width, HEIGHT_LABEL, Panel)
					SetGadgetLayout Label, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
					SetGadgetFont Label, FONT_Header
				
				Y = Y + HEIGHT_LABEL + SIZE_SPACE*2
	
				X = X + SIZE_SPACE
	
					' Intensity:
					
						Label = CreateLabel("Intensity", X, Y, Width, HEIGHT_LABEL, Panel)
							SetGadgetLayout Label, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
							SetGadgetFont Label, FONT_Label
							
						Y = Y + HEIGHT_LABEL
	
						Slider = CreateSlider(X, Y, WIDTH_TRACKBAR, HEIGHT_TRACKBAR, Panel, SLIDER_HORIZONTAL|SLIDER_TRACKBAR)
							SetGadgetLayout(Slider, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED)
							SetSliderRange(Slider, 0, 1000)
							SetSliderValue(Slider, 0)
								
						TextField = CreateTextField(X + WIDTH_TRACKBAR + SIZE_SPACE, Y, WIDTH_TEXTFIELD, HEIGHT_TEXTFIELD, Panel)
							SetGadgetLayout Textfield, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
							SetGadgetFont   Textfield, FONT_Textfield
							SetGadgetText   Textfield, Sharpness_Default#
					
						Y = Y + HEIGHT_TEXTFIELD + SIZE_SPACE
	
	
			' Separator:	
				Y = Y + SIZE_SPACE
				CreateSeparator(SIZE_SPACE, Y, Width, Panel)
				Y = Y + HEIGHT_SEPARATOR + SIZE_SPACE			
	
	
			' Sharpening:
				
				X = SIZE_SPACE
							
				Label = CreateLabel("Final size :", X, Y, Width, HEIGHT_LABEL, Panel)
					SetGadgetLayout Label, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
					SetGadgetFont Label, FONT_Header
				
				Y = Y + HEIGHT_LABEL + SIZE_SPACE*2
	
				X = X + SIZE_SPACE
	
	
					' Width:
					
						Label = CreateLabel("Width", X, Y, Width, HEIGHT_LABEL, Panel)
							SetGadgetLayout Label, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
							SetGadgetFont Label, FONT_Label
							
						Y = Y + HEIGHT_LABEL
	
						Slider = CreateSlider(X, Y, WIDTH_TRACKBAR, HEIGHT_TRACKBAR, Panel, SLIDER_HORIZONTAL|SLIDER_TRACKBAR)
							SetGadgetLayout(Slider, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED)
							SetSliderRange(Slider, 1, 6)
							SetSliderValue(Slider, 1)
								
						TextField = CreateTextField(X + WIDTH_TRACKBAR + SIZE_SPACE, Y, WIDTH_TEXTFIELD, HEIGHT_TEXTFIELD, Panel)
							SetGadgetLayout Textfield, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
							SetGadgetFont   Textfield, FONT_Textfield
							SetGadgetText   Textfield, "256"
					
						Y = Y + HEIGHT_TEXTFIELD + SIZE_SPACE
	
	
					' Height:
					
						Label = CreateLabel("Height", X, Y, Width, HEIGHT_LABEL, Panel)
							SetGadgetLayout Label, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
							SetGadgetFont Label, FONT_Label
							
						Y = Y + HEIGHT_LABEL
	
						Slider = CreateSlider(X, Y, WIDTH_TRACKBAR, HEIGHT_TRACKBAR, Panel, SLIDER_HORIZONTAL|SLIDER_TRACKBAR)
							SetGadgetLayout(Slider, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED)
							SetSliderRange(Slider, 1, 6)
							SetSliderValue(Slider, 1)
								
						TextField = CreateTextField(X + WIDTH_TRACKBAR + SIZE_SPACE, Y, WIDTH_TEXTFIELD, HEIGHT_TEXTFIELD, Panel)
							SetGadgetLayout Textfield, EDGE_CENTERED, EDGE_ALIGNED, EDGE_ALIGNED, EDGE_CENTERED
							SetGadgetFont   Textfield, FONT_Textfield
							SetGadgetText   Textfield, "256"
					
						Y = Y + HEIGHT_TEXTFIELD + SIZE_SPACE
	
	
					
		' Create the progress bar.
			' PROGBAR_Progressbar = CreateProgBar(WINDOW_Width-212, WINDOW_Height-20, 204, HEIGHT_PROGRESSBAR, WINDOW_Main)
		
		' Load custom mouse pointers.
		'	LoadPointers()
	
		'UpdatePreviewPane()			
		'UpdateControlPanel()		
		
		MaximizeWindow WINDOW_Main
						
	End Function	
	


' Main loop:

	Initialize()

	SetGadgetText(WINDOW_Main, AppTitle$ + "- C:/textures/blah.jpg (1024 x 768)") ' TEST CODE.  Do this after loading a file.
	SetStatusText(WINDOW_Main, "Rendering...") ' TEST CODE.

	AddHook EmitEventHook, MyHook 

	Repeat
	
		' Process events which don't require immediate attention.

			Select WaitEvent()
				Case EVENT_APPTERMINATE, EVENT_WINDOWCLOSE
					End
			EndSelect

		' BUG? Is 0 the first index of tabber gadget, or 1?
		'If SelectedGadgetItem(TABBER_PreviewPane) = 0 Then DrawQuad() ' If the user is viewing the ORIGINAL tab, then draw the quad deformer gadget.
		
	Forever


' ---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
' This function triggers the instant an event occurs even if we are in a modal event loop.
' Performing operations in here allows us to do things like redraw the canvas while the user is moving a slider that would be impossible otherwise.
' ---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	
	Function MyHook:Object(id:Int, Data:Object, Context:Object)
	
		Local Event:TEvent = TEvent(Data)
		
		Select Event.id
		
			Case EVENT_GADGETPAINT 
			
				SetGraphics CanvasGraphics(CANVAS_Preview)
				SetViewport 0, 0, GadgetWidth(CANVAS_Preview), GadgetHeight(CANVAS_Preview)
				Cls
				Flip
				
				'RedrawGadget CANVAS_Preview
			
				Return Null
					
		End Select
		
		Return Data
		
	End Function
</textarea> <br><br></td></tr></table><br>
<a name="981809"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >sswift</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Okay I've noticed that if I remove the event hook that redraws the canvas when the gadgetpaint event is triggered, that things speed up quite a bit.  It's still not instant though, and I still get the sliders not redrawing when I am resizing the window. <br><br></td></tr></table><br>
<a name="981832"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> You could place all your sliders etc on a panel and hide it when you're are resizing / maximising the window. Doesn't look great, but it should be faster.<br><br>Also, you could try to disable "double buffering" (not sure what the exact command was) for MaxGUI. <br><br></td></tr></table><br>
<a name="981842"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Also, you could try to disable "double buffering" (not sure what the exact command was) for MaxGUI.  <br></div><br>Double buffering should be disabled automatically as soon as a canvas is created, or if you are Windows XP or lower.<br><br>I'll have a look into this for you.  This is definitely redrawing quite slowly, and I'm guessing this is due to all the transparency hacks needed to get sliders to show their background panel's color/images properly.<br><br>The problem is that group panels, sliders and buttons all recreate new GDI brushes from their parents every time they are drawn, and the particular combination of them you are using is highlighting this overhead.  <br><br>As I say, I'll look into this for you and post back. ;-) <br><br></td></tr></table><br>
<a name="981959"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cool! Right, I think I've managed to optimise this a little.  If you could try it out, and let me know, that would be awesome.<br><br><a href="/seb/sswift.zip" target="_blank">SSwift Sample</a><br><br>Also, I saw the following line was commmented out in your EVENT_GADGETPAINT handler:<br><pre class=code>'RedrawGadget CANVAS_Preview</pre>I know it's commented out, but I just wanted to point out that this is likely to cause an infinite loop of redraws.<br><br>Finally, in answer to your inline comment:<br><br><div class="quote"> BUG? Is 0 the first index of tabber gadget, or 1? <br></div><br>All item based gadgets are base '0' - i.e. the first item has index '0'. <br><br></td></tr></table><br>
<a name="981980"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grisu</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> The sample works fine here on Win 7. <br><br></td></tr></table><br>
<a name="981995"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >sswift</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Seb:<br>The new version is much faster.  What did you change?<br><br><br><div class="quote"> I know it's commented out, but I just wanted to point out that this is likely to cause an infinite loop of redraws. <br></div><br><br>Yeah, I figured as much.  I was working with some example code I didn't really understand.  That's gone now.  I only redraw the canvas once after it's been created, (because otherwise it's left filled with garbage) and the rest of the updates for it occur only on the gadgetpaint event like so:<br><br><pre class=code>
			Case EVENT_GADGETPAINT 
			
				SetGraphics CanvasGraphics(CANVAS_Preview)
				SetViewport 0, 0, GadgetWidth(CANVAS_Preview), GadgetHeight(CANVAS_Preview)
				Cls
				Flip 0
				
				Return Null
</pre><br><br><br>Not sure why gadgetpaint doesn't occur when the canvas is first created.  Maybe it has something to do with my code to hide the window until all the gadgets are created... code which I had added to try to make the app seem faster.<br><br><br><div class="quote"> <br>All item based gadgets are base '0' - i.e. the first item has index '0'. <br> <br></div><br><br>Thanks. :-) <br><br></td></tr></table><br>
<a name="982004"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> What did you change? <br></div><br>Hehe! I didn't think you'd want to know, but it basically involved caching system brushes so they don't have to be recreated every time the gadgets are redrawn.  Rest assured that this will be implemented in the next MaxGUI release.<br><br><div class="quote"> Not sure why gadgetpaint doesn't occur when the canvas is first created.  <br></div><br>I'm guessing this is because you only set-up your event hook after you initialize all the gadgets, and so there is nothing to handle the EVENT_GADGETPAINT message when it is first created and drawn by the system.<br><br>This is hard to handle properly, as at the moment the gadget is created the reference hasn't even been returned and stored in your global variable.  The method you're using (i.e. calling <b>RedrawGadget()</b>) after the event hook is set-up is what I use, works perfectly, and avoids all the headache. ;-) <br><br></td></tr></table><br>
<a name="983925"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> sswift:<br><br>Does MaxGUI v1.34 improve redrawing speed for you? <br><br></td></tr></table><br>
<a name="994896"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >sswift</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> I just installed MaxGUI 1.34 and it is redrawing a lot faster now.  Thanks! <br><br></td></tr></table><br>
<a name="994940"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> No problem.  Glad it's fixed. :-) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
