<!DOCTYPE html><html lang="en" ><head ><title >MaxGUI custom gadget font (external font for GUI)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >MaxGUI custom gadget font (external font for GUI)</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >MaxGUI custom gadget font (external font for GUI)</a><br><br>
<a name="1294096"></a>

<a name="1294097"></a>

<a name="1294098"></a>

<a name="1294099"></a>

<a name="1294100"></a>

<a name="1294101"></a>

<a name="1294102"></a>

<a name="1294103"></a>

<a name="1294418"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello.<br><br><img src="http://www.writerscafe.org/uploads/rte/d3404cc793fcdb16c6f353c9b6a71ea4.png"><br><br>I am deciding that the 70's menu interface I currently have for my Carryall 750k Method is not going to be well received, especially since you hit numbers from 1-5 followed by the ENTER key in a menu to do something. Not very modern.<br><br>So ... I am delving into the mysterious world of MaxGui, a leaner meaner GUI for more serious applications - which I hope is what this forum area is about.<br><br>I would like to know please how to use a custom Truetype <b>*.ttf</b> font, not one installed in Windows, to be used for all my gadgets, including pull-down menus, fileboxes, alerts, the works.<br><br>I know how to do this for standard BlitzMAX apps:<pre class=code>Strict
Graphics 640,480
Global font:timagefont=LoadImageFont("fonts/audimrg.ttf",32)
SetImageFont font
DrawText "Amazing Grace.",50,50
Flip
WaitKey</pre>But no, it's different apparently in MaxGUI.<pre class=code>Strict
Import maxgui.drivers

Global scrn:TGadget=CreateWindow("Carryall The 750k Method", 0,0,512,512,,WINDOW_TITLEBAR|WINDOW_CENTER)
Global gadget:tgadget[99]
Global font:tguifont=LoadGuiFont("fonts/audimrg.ttf",16)
SetGadgetFont scrn,font
addgadget 0,"text",0,0,200,50
WaitKey

Function addgadget(id,typ$,h,v,x,y)
  If typ$="text"
    gadget[id]=CreateTextArea(h,v,x,y,scrn)
  EndIf
EndFunction</pre>And no, this code does not work. It does not change the font.<br><br>So if you guys have any ideas on how to do this, it might just help some other coders as well who aren't content with default Arial or New Courier for their MaxGUI projects as well.<br><br>Thanks in advance ! <br><br></td></tr></table><br>
<a name="1294106"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi<br><br>to change a gadget font you need to use <b>LookupGuiFont()</b> or <b>LoadGuiFont()</b><br>The first one is needed to find a system-font with some features (ie: GUIFONT_MONOSPACED), the latter just to load what font you want.<br><br>The you can use SetGadgetFont gadget,TGUIFont <br><br></td></tr></table><br>
<a name="1294107"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Degac.<br><br>No, I need to load specifically that one and very font from a directory that is not inside <b>\windows\</b>, no, it is inside <b>c:\david\blitz code\fonts\</b>.<br><br>Do you have code or an example that can load and load and make use of a font from such a directory for use specifically with <b>MaxGUI</b> ? <br><br></td></tr></table><br>
<a name="1294110"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, I understand now.<br>But I doubt you can do it, as the 'fonts' (and the gadgets etc) are 'handled' by the underneath OS. So, if the OS cant' see the font, it can't use it.<br><br>Possible solutions:<br>- use panel &amp; pre-loaded images (overkill in terms of work and speed)<br>- use your own font, forcing to install it in the font-system folder<br><br>Here should be a possible solution for this (see latest post, but I never found if anyone developed it) <a href="http://www.blitzbasic.com/Community/posts.php?topic=88949" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=88949</a> <br><br></td></tr></table><br>
<a name="1294111"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> So what I'm looking for is a working example of AddFontResource() for MaxGUI BlitzMAX then - which hopefully will let me use a font that is not currently installed in Windows.<br><br>I wish it was easier than this. LoadImageFont() to load or INCBIN any TrueType font at all is one of the very definite boons of BlitzMAX. :/ <br><br></td></tr></table><br>
<a name="1294160"></a>

<a name="1294161"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> That would be unproductive. The technique used in binding that WinAPI function in BlitzMax is the same for all other WinAPI functions.<br>You use an Extern "win32" block, and you consult the MSDN documentation on the function to know the amount and types of parameters etc.<br><br>Instead of looking for an example, you should be looking for how to make these Extern bindings yourself, and how to consult the documentation on the function.<br>Then when you find a new OS function or C library that you want to bind you'll do it yourself instead of reaching a dead end.<br><br>EDIT: if you don't know how to start, I will give you an example for that particular function. <br><br></td></tr></table><br>
<a name="1294163"></a>

<a name="1294164"></a>

<a name="1294165"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> About your top example. You cant set the font of a window, as the only thing that has text is the titlebar and that font is governed by the system. IE its a system wide setting.<br><br>Try setting the font of individual gadgets instead.<br><br>Now, i believe there is a way for windows to inherit the font of their parent window, but that bit is probably not enabled for maxgui.<br><br>EDIT: I didnt notice that you also was trying to load one from disk...<br>Youd have to intern a font for that to work properly. <br><br></td></tr></table><br>
<a name="1294167"></a>

<a name="1294168"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I do have some code to register fonts though, as i had the same problem a few years back ;)<br><br>RegisterSystemFont.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'
' make fonts visible in windows
'
Import BRL.Stream
Import BRL.System

SuperStrict

Import "Registry.bmx"

Function RegisterSystemFont:Int( url:String, title:String)
?Win32
	Extern "Win32"
		Function AddFontResource:Int( file$z) = "AddFontResourceA@4"
	EndExtern
	
	Extern "C"
		Function GetEnv$z( name$z) = "getenv"
		Function SetEnv:Int( name$z, value$z, overwrite:Int = True) = "setenv"
	EndExtern
	If Not url Then Return False
	
	Local fontdir:String = GetEnv( "WINDIR") + "\Fonts\"
	' allow protocoll urls (to incbin fonts)
	Local filename:String, a:String[] = url.Split("::")
	If a.Length = 1 Then
		filename = a[0]
	Else
		filename = a[1]
	EndIf
	
	' first, copy the font to the system font dir
	If Not FileType( fontdir + filename) Then
		Local stream:TStream = ReadStream( url)
		Local file:TStream = WriteStream( fontdir + filename)
		Assert stream And file
		Try
			CopyStream stream, file
			file.Close()
			Stream.Close()
		Catch e:Object
			DebugLog "RegisterSystemFont:: error writing to ~q" + fontdir + filename + "~q"
			file.Close()
			Stream.Close()
			Return False
		EndTry
	EndIf
		
	' then register it with the os
	AddFontResource( fontdir + filename)
	SetRegistryValueStr( HKEY_LOCAL_MACHINE, "SOFTWARE\Microsoft\Windows NT\CurrentVersion\Fonts", title, filename)
	
	Return True
?
EndFunction
</textarea><br>Registry.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

SuperStrict

Import "-ladvapi32"

Extern "Win32"
	Function RegOpenKey:Int( hKeyParent:Int,SubKey$w,phkResult:Int Var) = "RegOpenKeyW@12"
	Function RegCloseKey:Int( hKey:Int) = "RegCloseKey@4"
	Function RegCreateKey:Int( hKeyParent:Int,SubKey$w,phkResult:Int Var) = "RegCreateKeyW@12"
	Function RegDeleteKey:Int( hKeyParent:Int,SubKey$w) = "RegDeleteKeyW@8"
	Function RegDeleteValue:Int( hKey:Int,ValueName$w) = "RegDeleteValueW@8"
	Function RegEnumKey:Int( hKey:Int,idx:Int,Key:Byte Ptr,size:Int) = "RegEnumKeyW@16"
	Function RegEnumValue:Int( hKey:Int,idx:Int,ValueName:Byte Ptr,NameSize:Byte Ptr,Reserved:Int,nType:Byte Ptr,ValueBytes:Byte Ptr,ValueSize:Byte Ptr) = "RegEnumValueW@32"

	Function RegOpenKeyEx:Int( hkey:Int, subkey$w, options:Int, samdesired:Int, result:Int Var) = "RegOpenKeyExW@20"
	Function RegEnumKeyEx:Int( hkey:Int, index:Int, name:Byte Ptr, cname:Int Var, reserved:Int, class:Byte Ptr, cclass:Int Var, time:Byte Ptr) = "RegEnumKeyEx@32"
	Function RegSetValueEx:Int( hKey:Int,ValueName$w,Reserved:Int,nType:Int,Bytes:Byte Ptr,size:Int) = "RegSetValueExW@24"
	Function RegQueryValueEx:Int( hKey:Int,ValueName$w,Reserved:Int,nType:Byte Ptr,ValueBytes:Byte Ptr,ValueSize:Byte Ptr) = "RegQueryValueExW@24"
EndExtern

Const HKEY_CLASSES_ROOT:Int = $80000000
Const HKEY_CURRENT_USER:Int = $80000001
Const HKEY_LOCAL_MACHINE:Int = $80000002
Const HKEY_USERS:Int = $80000003
Const HKEY_PERFORMANCE_DATA:Int = $80000004
Const HKEY_CURRENT_CONFIG:Int = $80000005
Const HKEY_DYN_DATA:Int = $80000006

Const KEY_QUERY_VALUE:Int = $1
Const KEY_SET_VALUE:Int = $2
Const KEY_CREATE_SUB_KEY:Int = $4
Const KEY_ENUMERATE_SUB_KEYS:Int = $8
Const KEY_NOTIFY:Int = $10
Const KEY_CREATE_LINK:Int = $20
Const KEY_SYNCHRONIZE:Int = $100000

Const REG_NONE:Int                            = 0
Const REG_SZ:Int                              = 1
Const REG_EXPAND_SZ:Int                       = 2
Const REG_BINARY:Int                          = 3
Const REG_DWORD:Int                           = 4
Const REG_DWORD_BIG_ENDIAN:Int                = 5
Const REG_LINK:Int                            = 6
Const REG_MULTI_SZ:Int                        = 7
Const REG_RESOURCE_LIST:Int                   = 8
Const REG_FULL_RESOURCE_DESCRIPTOR:Int        = 9 
Const REG_RESOURCE_REQUIREMENTS_LIST:Int      = 10
Const REG_QWORD:Int                           = 11

Const KEY_ACCESS_ALL:Int = KEY_QUERY_VALUE | KEY_SET_VALUE | KEY_ENUMERATE_SUB_KEYS | KEY_NOTIFY

Const ERROR_SUCCESS:Int = 0
Const ERROR_NO_MORE_ITEMS:Int = 259
Const ERROR_MORE_DATA:Int = 234


'
' can set a value in a key from a String value
'
'	@key			HKEY_xxx
'	@subkey		"registry\path"
'	@name		name of value
'	@value		String value
'	@typetag		REG_SZ | REG_MULTI_SZ | REG_EXPAND_SZ | REG_BINARY
'
Function SetRegistryValueStr:Int( key:Int, subkey:String, name:String, value:String, typetag:Int = REG_SZ)
	Local handle:Int
	If Not RegOpenKey( key, subkey, handle) Then
		Local res:Int = RegSetValueEx( handle, name, 0, typetag, value, value.Length + 1) = ERROR_SUCCESS
		RegCloseKey( handle)
		Return res
	EndIf
	Return False
EndFunction

'
' can set a value from binary data or a pointer to binary data
'
'	@key			HKEY_xxx
'	@subkey		"registry\path"
'	@name		name of value
'	@value		value or pointer
'	@typetag		REG_DWORD | REG_DWORD_BIG_ENDIAN | REG_QWORD
'
Function SetRegistryValueInt:Int( key:Int, subkey:String, name:String, value:Long, typetag:Int = REG_DWORD)
	Local handle:Int
	If Not RegOpenKey( key, subkey, handle) Then
		Local res:Int = False
		If typetag = REG_DWORD Or typetag = REG_DWORD_BIG_ENDIAN Then
			res = RegSetValueEx( handle, name, 0, typetag, Byte Ptr Varptr value, 4) = ERROR_SUCCESS
		ElseIf typetag = REG_QWORD Then
			res = RegSetValueEx( handle, name, 0, typetag, Byte Ptr Varptr value, 8) = ERROR_SUCCESS
		EndIf
		RegCloseKey( handle)
		Return res
	EndIf
	Return False
EndFunction

'
' can set a value from binary data or a pointer to binary data
'
'	@key			HKEY_xxx
'	@subkey		"registry\path"
'	@name		name of value
'	@value		pointer to data
'	@size		size of data
'
' typetag is always REG_BINARY :
'
Function SetRegistryValueData:Int( key:Int, subkey:String, name:String, data:Byte Ptr, size:Int)
	Local handle:Int
	If Not RegOpenKey( key, subkey, handle) Then
		Local res:Int = False
		res = RegSetValueEx( handle, name, 0, REG_BINARY, data, size) = ERROR_SUCCESS
		RegCloseKey( handle)
		Return res
	EndIf
	Return False
EndFunction
</textarea> <br><br></td></tr></table><br>
<a name="1294191"></a>

<a name="1294193"></a>

<a name="1294194"></a>

<a name="1294195"></a>

<a name="1294196"></a>

<a name="1294197"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> All THIS, just to load a font ? Wow ... I appreciate your immensely hard work, Grable.<br><br>I did come across this by DEGAC.<pre class=code>Function SetGlobalGadgetFont:Int(par:tgadget=Null,fnt:tguifont=Null)
	If fnt=Null Return -1
	If par=Null Return -1
	If par.kids=Null Return -1
	For Local s:tgadget=EachIn par.kids 
			SetGadgetFont s,fnt
	Next
End Function</pre>This changes the SIZE of the font and lets you select from a limited choice of system fonts which is nice.<br><br>But isn't there any code about this size or smaller just to load a font not installed in Windows ?<br><br>And no, I don't want to modify a user's system font directory by directly installing a new one just to display in a program because it's not part of Windows.<br>. . .<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict
Import maxgui.drivers

Global zvideo:TGadget=CreateWindow("Carryall The 750k Method", 0,0,512,512,,WINDOW_TITLEBAR|WINDOW_CENTER)
Global zframe:tgadget=CreateCanvas(0,0,512,512,zvideo,0) ' is frameless
Global gadget:tgadget[99]
Global font:Tguifont=LookupGuiFont(guifont_system,12)
addgadget 0,"text",0,20,200,50
setGlobalGadgetFont(zvideo,font)
WaitKey

' &gt;&gt; SET GLOBAL FONT
' THANK YOU DEGAC
Function SetGlobalGadgetFont:Int(par:tgadget=Null,fnt:tguifont=Null)
	If fnt=Null Return -1
	If par=Null Return -1
	If par.kids=Null Return -1
	For Local s:tgadget=EachIn par.kids 
			SetGadgetFont s,fnt
	Next
End Function

Function addgadget(id,typ$,h,v,x,y)
  If typ$="txt"
    gadget[id]=CreateTextArea(h,v,x,y,zvideo)
  EndIf
EndFunction
</textarea><br><br>Let me ask this, is there a way to place a gadget directly on top of another and have it appear. In this case from the code I posted above, I want the <b>Text</b> gadget to appear above the <b>Canvas</b> gadget.<br><br>If I can do this, then I can use BlitzMAX's own <b>LoadImageFont</b> to display text details of any controls I add to the ZVideo. <br><br></td></tr></table><br>
<a name="1294200"></a>

<a name="1294202"></a>

<a name="1294203"></a>

<a name="1294205"></a>

<a name="1294206"></a>

<a name="1294208"></a>

<a name="1294210"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> All THIS, just to load a font ? Wow ... I appreciate your immensely hard work, Grable. <br></div>Well not really, the meat of the code is in RegisterSystemFont(). The other file is just an api for accessing the registry, and its fairly complete set of functions for doing that.<br>Even though you only really need one of them.<br><br><div class="quote"> Let me ask this, is there a way to place a gadget directly on top of another and have it appear. In this case from the code I posted above, I want the Text gadget to appear above the Canvas gadget. <br></div>If you create the text gadget AFTER the canvas gadget it should appear on top.<br>But it wont help you with what you want to do, as it will draw its own thing no matter what is beneath it.<br><br><div class="quote"> And no, I don't want to modify a user's system font directory by directly installing a new one just to display in a program because it's not part of Windows. <br></div>As said by others in this threads, you just cant. If you want to use Windows GUI controls, you MUST register a font with the system for it to be able to be rendered by the system.<br><br>I personally dont see the problem with doing it like that, as EVERY other application that has its own fonts and uses windows gui allso must do this.<br>But it can be done transparently to the user, by registering it upon application startup and unregistering it when exiting. <br><br></td></tr></table><br>
<a name="1294209"></a>

<a name="1294211"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Unfortunately it is not, Grable. It does not appear at all. Here is current code:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict
Import maxgui.drivers

Global gadget:tgadget[99]
Global zvideo:TGadget=CreateWindow("Carryall The 750k Method", 0,0,512,512,,WINDOW_TITLEBAR|WINDOW_CENTER)
Global zframe:tgadget=CreateCanvas(0,0,512,512,zvideo,0) ' is frameless

Local ev

SetGraphics CanvasGraphics(zframe)
SetBlend alphablend
Global font:Tguifont=LookupGuiFont(guifont_system,12)
addgadget 0,"txt",0,20,200,50
setGlobalGadgetFont(zvideo,font)
ActivateGadget zframe

enablepolledinput

Repeat

ev=WaitEvent()

Until KeyDown(27)

' &gt;&gt; SET GLOBAL FONT
' THANK YOU DEGAC
Function SetGlobalGadgetFont:Int(par:tgadget=Null,fnt:tguifont=Null)
	If fnt=Null Return -1
	If par=Null Return -1
	If par.kids=Null Return -1
	For Local s:tgadget=EachIn par.kids 
			SetGadgetFont s,fnt
	Next
End Function

Function addgadget(id,typ$,h,v,x,y)
  If typ$="txt"
    gadget[id]=CreateTextArea(h,v,x,y,zvideo)
  EndIf
  ActivateGadget gadget[id]
EndFunction
</textarea> <br><br></td></tr></table><br>
<a name="1294213"></a>

<a name="1294215"></a>

<a name="1294218"></a>

<a name="1294219"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well canvases are a bit special, in that the system takes over its rendering, essentially sidestepping the normal gui drawing order.<br>So with any other control it will work.<br><br>To simulate a canvas, you can use a Panel with a Pixmap attached to it. Though it would probably not be a good idea to do realtime rendering to it that way.<br><br>EDIT: Hmm seems it actually will work if you attach it to the canvas instead (like you should really).<br>But dont be surprised if they start fighting about who should be on top ;) <br><br></td></tr></table><br>
<a name="1294221"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> No, I was talking with someone else, and they said this font technique of yours is NOT permanent. That it can be removed. Okay, let's do this the right way. Could you please show in =ONE= source code the method to load and use/install a font from said location and file, <b>/fonts/cpmono.ttf</b>.<br><br>In this demo code it maybe show two fields of proof it is working, one a text field, where you can type in, the other static text.<br><br>If exit is chosen from here, then the font is unloaded/UNINSTALLED and the program ends.<br><br>With that code I should be able to glean what is going on. <br><br></td></tr></table><br>
<a name="1294223"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Actually it is permanent. That is why i made it in the first place.<br>That is, permanent until you or the user explicitly remove it.<br><br>To make it NOT permanent, use only the AddFontResource() function.<br>It will persist until you reboot the computer or log off.<br><br>To remove it yourself before that, use RemoveFontResource() function. <br><br></td></tr></table><br>
<a name="1294224"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Okay, I am looking at your two functions, Grable. <b>RegisterSystemFont</b> and <b>Registry</b> Could you please post an example using these functions correctly.<br><br>And - doesn't there need to be a routine for <b>UnregisterSystemFont</b> ? <br><br></td></tr></table><br>
<a name="1294225"></a>

<a name="1294226"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> I would think its use is self evident by its prototype.<br>But i also just said you dont need to use that version IF you dont want it to persist across reboots.<br><br>Against my better judgement i modified your sample to use the two functions i mentioned in my post above (and others have mentioned before that)<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict
Import maxgui.drivers

Extern "Win32"
	Function AddFontResourceW:Int( fn$w)
	Function RemoveFontResourceW:Int( fn$w)
EndExtern

AddFontResourceW("c:\dump\destroy x.ttf")

Global gadget:tgadget[99]
Global zvideo:TGadget=CreateWindow("Carryall The 750k Method", 0,0,512,512,,WINDOW_TITLEBAR|WINDOW_CENTER)
Global font:Tguifont=LoadGuiFont("Destroy X",20)
addgadget 0,"txt",0,20,400,40
setGlobalGadgetFont(zvideo,font)

Repeat
	WaitEvent()
	If CurrentEvent.ID = EVENT_WINDOWCLOSE Then Exit
Forever
RemoveFontResourceW("c:\dump\destroy x.ttf")
End

' &gt;&gt; SET GLOBAL FONT
' THANK YOU DEGAC
Function SetGlobalGadgetFont:Int(par:tgadget=Null,fnt:tguifont=Null)
	If fnt=Null Return -1
	If par=Null Return -1
	If par.kids=Null Return -1
	For Local s:tgadget=EachIn par.kids 
			SetGadgetFont s,fnt
	Next
End Function

Function addgadget(id,typ$,h,v,x,y)
  If typ$="txt"
    gadget[id]=CreateTextArea(h,v,x,y,zvideo)
  EndIf
  ActivateGadget gadget[id]
EndFunction
</textarea><br>It literally cant be simpler to use those functions! Do you REALLY need code to tell you how to call a function with a single argument?<br>Because it honestly feels like you refuse to read the posts detailing HOW to do something and demand source code for every little damn thing. <br><br></td></tr></table><br>
<a name="1294227"></a>

<a name="1294228"></a>

<a name="1294229"></a>

<a name="1294230"></a>

<a name="1294231"></a>

<a name="1294232"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> Understand Grable. I'm a BASIC programmer. My mindset is still very much in the 70s. Any help you give here I very much and deeply appreciate. A lot of this stuff is very new to me.<br><br>I in turn am more than happy and generous to give out my works and source code in the efforts of helping and educating others. While my coding methods are dated, I am usually pretty good about solving problems involving logic, math, and method.<br><br>I know you have seen the code submits I've done recently and since I arrived.<br><br>Despite your blustering your program does not work here. I went ahead and made the directory "c:\dump\" and put a select font there and renamed it to "destroy x.ttf" - and running your program, it still does not work.<br><br>Now just think of me trying to write on my own what you did above - and it still didn't work. Where would I be then ? :)<br><br>BTW, here is the current model. I have no problem about sharing this w others. AddGadget and FirstCap are my own routines.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict
Import maxgui.drivers

Global gadget:tgadget[99],menukey$
Global zvideo:TGadget=CreateWindow("Carryall The 750k Method", 0,0,512,512,,WINDOW_TITLEBAR|WINDOW_CENTER)

Local ev

Global font:Tguifont=LookupGuiFont(guifont_system,12)
addgadget 0,"text","",0,0,200,50

addgadget 50,"menu","file"
addgadget 51,"mopt","new",50
addgadget 52,"mopt","open",50
addgadget 53,"mopt","close",50
addgadget 54,"mopt","",50
addgadget 55,"mopt","save",50
addgadget 56,"mopt","exit",50

addgadget 60,"menu","edit"
addgadget 61,"mopt","cut",60
addgadget 62,"mopt","copy",60
addgadget 63,"mopt","paste",60

setGlobalGadgetFont(zvideo,font)

enablepolledinput
UpdateWindowMenu zvideo

Repeat

ev=WaitEvent()

Until KeyDown(27)

' &gt;&gt; SET GLOBAL FONT
' THANK YOU DEGAC
Function SetGlobalGadgetFont:Int(par:tgadget=Null,fnt:tguifont=Null)
	If fnt=Null Return -1
	If par=Null Return -1
	If par.kids=Null Return -1
	For Local s:tgadget=EachIn par.kids 
			SetGadgetFont s,fnt
	Next
End Function

Function addgadget(id,typ$,nam$,h=0,v=0,x=0,y=0)
  If typ$="text"
    gadget[id]=CreateTextArea(h,v,x,y,zvideo)
  ElseIf typ$="menu"
    gadget[id]=CreateMenu("&amp;"+firstcap$(nam$),0,WindowMenu(zvideo))
  ElseIf typ$="mopt"
    If nam$=""
      CreateMenu "",id,gadget[h]
    Else
      CreateMenu "&amp;"+firstcap$(nam$),id,gadget[h],Asc(Upper$(nam$[0..1])),modifier_command
    EndIf
  EndIf
'  ActivateGadget gadget[id]
EndFunction

Function firstcap$(a$)
Local i,r$,u=1,c$,o$
  For i=0 Until Len(a$)
    c$=a$[i..i+1]
    o$=Lower$(c$)
    If o$&gt;="a" And o$&lt;="z" And u=1 Then c$=Upper$(c$) ; u=0
    If o$&lt;"a" Or o$&gt;"z" Then u=1
    r$:+c$
  Next
  Return r$
EndFunction    
</textarea><br><br>The next thing I was going to do was have a short-cut keeper, where a short-cut letter is always used but it checks to see if it's already in use, and if so, it reads the next character and instead makes that the shortcut key. I.E.:<br><br>[C]opy<br>C[r]eate <br><br></td></tr></table><br>
<a name="1294233"></a>

<a name="1294235"></a>

<a name="1294237"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> Its not about about the lack of will to share code. I too have no problem with that.<br>But when detailing how to perform some action or other, the willingness to learn of the receiving party is paramount.<br>Do some research yourself, put some function names into google and see what pops up etc.<br>Usually all it takes is a nudge in the right direction and people manage to figure out the rest on their own.<br>You have a direct line to whatever kind of information you could ever want in the form of "the internet", which is also my source of knowledge half the time ;)<br>What i dont know, i figure out. Maybe im special i dont know, but i actually LIKE figuring things out and solving problems. Even if they arent even MY problems to begin with.<br><br>And thus ends my rant :P<br>On to coding...<br><br>The reason my example does not work for you is because fonts have both a filename (its actual file) and a name (the name of the font itself).<br>Renaming a font to something else only gives you ONE part of that piece.<br><br>Try using your own font instead, your own file-name in the calls to AddFontResource() and RemoveFontResource(). And also use your own font-name in the call to LoadGuiFont().<br><br>EDIT: To get the name of the font, use the builtin Font Preview program that comes with windows (the default for fonts) and look for the text "Font Name:" in the upper left part of the window. <br><br></td></tr></table><br>
<a name="1294238"></a>

<a name="1294239"></a>

<a name="1294240"></a>

<a name="1294241"></a>

<a name="1294242"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think there's a cleaner way by making use of the TWindowsFont class from MaxGUI.<br>The API needed is used here:<br><a href="https://autohotkey.com/boards/viewtopic.php?f=6&amp;t=813" target="_blank">https://autohotkey.com/boards/viewtopic.php?f=6&amp;t=813</a><br><br>- Load a .TTF file from the hard disk with AddFontResourceEx with the private flag, or load an IncBin-ed .TTF file with that AddFontMemResourceEx also with the private flag.<br>This gives you an HFONT value.<br><br>- Use that HFONT value with MaxGUI's TWindowsFont.LoadFromHandle( HFONT ) to get a MaxGUI font.<br><br>- Use the MaxGUI font with that "global gadget font" convenience function.<br>Gadgets should have the custom font.<br><br>- If the HFONT value was created from memory with AddFontMem(...), then when the application ends it's necessary to remove it, for safety (even though the OS will automatically do that itself).<br>It's easy to do this by having that HFONT be a global value that is deleted in a function registered with OnEnd.<br><br>Someone's gonna have to write all that. <br><br></td></tr></table><br>
<a name="1294243"></a>

<a name="1294244"></a>

<a name="1294245"></a>

<a name="1294246"></a>

<a name="1294247"></a>

<a name="1294249"></a>

<a name="1294250"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have always learned by example, Grable. That's what makes me the good (or bad) programmer I am today. <br><br>In the past I wrote a database that kept track of all the churches in the world. I wrote it in QBasic.<br><br>The original programmer before had written his in C. It took 2-days to compile the data with his model. Me ? My program to replace his took 2-hours to calculate everything. It also had bells, whistles, lights, and a cool screensaver. :)<br><br>So, yes, I do know how to program. But I learn because of Example. That is my anvil and that is my crucible. There are many MANY QBasic example codes out there and were there before the Internet. It is from those I learned the bulk of my programming.<br><br>It is from those codes I learned what I did and started to build Scenario 1, an ambitious project, a full and complete RPG Maker. I was only interrupted from someone who said my coding was good and wanted me to advance, to start to program in GFA, a considerably more powerful language.<br><br>And with a little kicking and screaming and promises (that weren't broken), I did.<br><br>And I did go on to write Scenario 2 - completely in GFABasic, a Windows 3.1 Programming Language. And it still runs perfectly in Windows XP computers and before.<br><br><a href="http://creatools.gameclassification.com/EN/creatools/481-Scenario-RPGMaker/index.html" target="_blank">http://creatools.gameclassification.com/EN/creatools/481-Scenario-RPGMaker/index.html</a><br><br>I'll even send you the source code if you like - if you think you can learn something from it. It's a little over 20,000 lines long.<br><br>Thus ends =MY= rant. :)<br><br>Now, understand the code you write is just 'barely' on the edge of my understanding. You are saying that it needs to go by the actual Font Name ? This then is viewed when you press <b>ENTER</b> on the file ?<br><br>Let me see ... Font Filename, "destroy x.ttf" actual Font name, "Audimat Mono"<br><br>So change the code you submitted above thus ?<pre class=code>Global font:Tguifont=LoadGuiFont("Audimat Mono",20)</pre>No, that didn't fix it either. There is still no font change.<br><br>No other lines have been changed from your code.<br><br>. . .<br><br>Kryzon, what programming language is that ? <br><br></td></tr></table><br>
<a name="1294256"></a>

<a name="1294257"></a>

<a name="1294278"></a>

<a name="1294363"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> No, that didn't fix it either. There is still no font change. <br></div>Then your doing something wrong perhaps...<br><br>I got the font from here <a href="http://www.dafont.com/destroy-x.font" target="_blank">http://www.dafont.com/destroy-x.font</a><br>Drop the font in the same directory as the sample below.<br><br>And with some of the suggested code by Kryzon, to not have to unregister a font and handle incbin.<br>(i see no reason to use LoadFromHandle() as to get a handle one has to call CreateFont anyway, so it becomes less generic. Obviously if one is including a font one already knows its name)<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict
Import maxgui.drivers

Incbin "destroy x.ttf"
If Not RegisterGuiFont("incbin::destroy x.ttf") Then RuntimeError "error registering font"

Global gadget:tgadget[99]
Global zvideo:TGadget=CreateWindow("Carryall The 750k Method", 0,0,512,512,,WINDOW_TITLEBAR|WINDOW_CENTER)
Global font:TGuiFont = LoadGuiFont( "Destroy X", 20)
addgadget 0,"txt",0,20,400,40
setGlobalGadgetFont(zvideo,font)

Repeat
	WaitEvent()
	If CurrentEvent.ID = EVENT_WINDOWCLOSE Then Exit
Forever
End

' &gt;&gt; SET GLOBAL FONT
' THANK YOU DEGAC
Function SetGlobalGadgetFont:Int(par:tgadget=Null,fnt:tguifont=Null)
	If fnt=Null Return -1
	If par=Null Return -1
	If par.kids=Null Return -1
	For Local s:tgadget=EachIn par.kids 
			SetGadgetFont s,fnt
	Next
End Function

Function addgadget(id,typ$,h,v,x,y)
  If typ$="txt"
    gadget[id]=CreateTextArea(h,v,x,y,zvideo)
  EndIf
  ActivateGadget gadget[id]
EndFunction

Private
Extern "Win32"
	Const FR_PRIVATE:Int = $10
	Const FR_NOT_ENUM:Int = $20
	
	Function AddFontMemResourceEx:Int( p:Byte Ptr, sz:Int, r:Byte Ptr, count:Int Var)
	Function AddFontResourceExW:Int( fn$w, flags:Int, r:Byte Ptr)
	Function RemoveFontResourceExW:Int( url$w, flags:Int, r:Byte Ptr)
EndExtern
Public

Function RegisterGuiFont:Int( url:String)
	Local font:Int
	If url.StartsWith("incbin::") Then
		url = url["incbin::".Length ..]
		Local count:Int
		AddFontMemResourceEx( IncbinPtr(url), IncbinLen(url), Null, count)
		Return count &gt; 0
	Else
		Return AddFontResourceExW( url, FR_PRIVATE, Null)
	EndIf
EndFunction

Function UnregisterGuiFont( url:String)
	If Not url.StartsWith("incbin::") Then
		RemoveFontResourceExW( url, FR_PRIVATE, Null)
	EndIf
EndFunction
</textarea> <br><br></td></tr></table><br>
<a name="1294262"></a>

<a name="1294263"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> I didn't know about IncbinPtr, that makes it simple.<br><br>In case you don't know the family name of the font, you'd do something like this I imagine (untested):<br><pre class=code>Global font:TWindowsFont = Null

Local HFONT:Int = RegisterGuiFont( "incbin::destroy x.ttf" )
If HFONT Then 
   font = New TWindowsFont
   font.LoadFromHandle( HFONT )
   setGlobalGadgetFont( zvideo, font )
EndIf</pre> <br><br></td></tr></table><br>
<a name="1294264"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#23">[#23]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Kryzon. Hmm ... I can but try. Still not working. Here is current model. Perhaps I'm doing something wrong in my code ? It's running. I have the RegisterGuiFont() function in place alongside your code - it just doesn't want to work.<br><br>Feel free to make a change in it if you think I did something in error.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict
Import maxgui.drivers

Global gadget:tgadget[99]
Global zvideo:TGadget=CreateWindow("Underway", 0,0,512,512,,WINDOW_TITLEBAR|WINDOW_CENTER)
Global menucode$,k$

Global font:TWindowsFont = New TWindowsFont
Local HFONT:Int = RegisterGuiFont( "incbin::c:\dump\destroy x.ttf" )
If HFONT Then 
   font.LoadFromHandle( HFONT )
   setGlobalGadgetFont( zvideo, font )
EndIf

Local ev,cid,evd,p,i

'Global font:Tguifont=LookupGuiFont(guifont_system,12)
addgadget 0,"text","",0,0,200,50

addgadget 50,"menu","file"
addgadget 51,"mopt","n*new state",50
addgadget 52,"mopt","o*open state",50
addgadget 53,"mopt","z*close state",50
addgadget 54,"mopt","",50
addgadget 55,"mopt","s*save as ...",50
addgadget 56,"mopt","exit",50

addgadget 60,"menu","edit"
addgadget 61,"mopt","cut",60
addgadget 62,"mopt","copy",60
addgadget 63,"mopt","paste",60

addgadget 70,"menu","help"
addgadget 71,"mopt","about",70
addgadget 72,"mopt","website",70

'setGlobalGadgetFont(zvideo,font)

'enablepolledinput
UpdateWindowMenu zvideo

Repeat

WaitEvent
cid=EventID()
evd=EventData()
If cid=event_windowclose Then zz
If cid=event_menuaction
  If evd&gt;=50 And evd&lt;=99
    p=Instr(menucode$,str$(evd))
    If p Then k$="@"+Mid$(menucode$,p+2,3)
  EndIf
  Print k$
EndIf

Until KeyDown(27)

Function zz()
  End
EndFunction

' &gt;&gt; SET GLOBAL FONT
' THANK YOU DEGAC
Function SetGlobalGadgetFont:Int(par:tgadget=Null,fnt:tguifont=Null)
	If fnt=Null Return -1
	If par=Null Return -1
	If par.kids=Null Return -1
	For Local s:tgadget=EachIn par.kids 
			SetGadgetFont s,fnt
	Next
End Function

Function addgadget(id,typ$,nam$,h=0,v=0,x=0,y=0)
Local i,c$
  If typ$="text"
    gadget[id]=CreateTextArea(h,v,x,y,zvideo)
  ElseIf typ$="menu"
    gadget[id]=CreateMenu(firstcap$(nam$),0,WindowMenu(zvideo))
  ElseIf typ$="mopt"
    If nam$=""
      CreateMenu "",id,gadget[h]
    Else
      If nam$[1..2]="*"
        c$=nam$[0..1]
        nam$=Mid$(nam$,3)
      EndIf
      CreateMenu firstcap$(nam$),id,gadget[h],Asc(Upper$(c$)),modifier_command
      menucode$:+str$(id)+Left$(nam$,3)
    EndIf
  EndIf
'  ActivateGadget gadget[id]
EndFunction

Function firstcap$(a$)
Local i,r$,u=1,c$,o$
  For i=0 Until Len(a$)
    c$=a$[i..i+1]
    o$=Lower$(c$)
    If o$&gt;="a" And o$&lt;="z" And u=1 Then c$=Upper$(c$) ; u=0
    If o$&lt;"a" Or o$&gt;"z" Then u=1
    r$:+c$
  Next
  Return r$
EndFunction    

' &gt;&gt; RETURN STRING FROM VALUE
Function str$(n)
  Return Int(n)
EndFunction

Function RegisterGuiFont:Int( url:String)
	Extern "Win32"
		Const FR_PRIVATE:Int = $10
		Const FR_NOT_ENUM:Int = $20
		
		Function AddFontMemResourceEx:Int( p:Byte Ptr, sz:Int, r:Byte Ptr, count:Int Var)
		Function AddFontResourceExW:Int( fn$w, flags:Int, r:Byte Ptr)
	EndExtern

	Local font:Int
	If url.StartsWith("incbin::") Then
		url = url["incbin::".Length ..]
		Local count:Int
		Return AddFontMemResourceEx( IncbinPtr(url), IncbinLen(url), Null, count)
	Else
		Return AddFontResourceExW( url, FR_PRIVATE, Null)
	EndIf
EndFunction
</textarea> <br><br></td></tr></table><br>
<a name="1294266"></a>

<a name="1294267"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#24">[#24]</a></td></tr></table></td></tr><tr ><td class="posttext"> That wont work. RegisterGuiFont() returns only a boolean for success or failure.<br>You still have to load it via LoadGuiFont().<br>The reason for this is that the underlying functions does not return HFONT either. Id have to call CreateFont() manually, and that would lock style and size to it. So it wouldnt be usable as a generic font.<br><br>Did my example not work? If it didnt even after downloading that font, i have no idea whats wrong.<br>Thats when i start asking: What version of Windows your running, if its 32-bit or 64-bit and the like.<br>But since those APIs are supported since Windows 2000, and it doesnt complain about missing functions i assume you have newer. <br><br></td></tr></table><br>
<a name="1294268"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#25">[#25]</a></td></tr></table></td></tr><tr ><td class="posttext"> Taking a break. Briefly, my OS, Windows 8.1, 64-bit quad. Speed ? REALLY FAST. :)<br>Resolution 1024x768.<br><br>System is stored: "c:\windows" <br><br></td></tr></table><br>
<a name="1294271"></a>

<a name="1294272"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#26">[#26]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah that's right, the AddFontMem(...) seems to return some handle that you use to delete it later. Then using the HFONT path seems more complicated.<br><br>It's simpler to just go to the font file, right-click and select Preview to open Windows Font Viewer that will show you what's the family name.<br><br>Then use LoadGuiFont function from MaxGUI like grable originally showed in post #21. Post #21 is the answer. <br><br></td></tr></table><br>
<a name="1294274"></a>

<a name="1294275"></a>

<a name="1294276"></a>

<a name="1294277"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#27">[#27]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hmm ... And now the font in "\dump\" won't delete at all. I'm getting the message:<br><br><b>The action cannot be completed because the file is open in System</b>.<br><br>This is after I have shut down BlitzMAX and checked for any stray EXEs with Process Explorer - and there aren't any.<br><br>Likely this can be fixed by shutting everything completely down and rebooting the computer. Ok, maybe I have my answer there. If it's possible to accidentally lock a file by registering it so no-one can delete it at all unless it is properly released and only by rebooting or code - I really should be messing in that kind of area. That is not my type of coding.<br><br>Further, I can't think of a single Windows interface I have ever seen that uses a custom font, you know where you have a text input field, radio buttons and the like. Perhaps they too thought it was too much work or too invasive to the OS as well. It's just a curiosity I thought I would explore.<br><br>Now as for putting images in, that shouldn't be a problem. As long as I don't have the image override any controls, it should work. I can put a nice unobtrusive logo in the top-right-hand corner and continue my control interface work.<br><br>Thanks for the help guyz. Now maybe if someone posted just an EXE, no source, to show what a custom font looks like on all the standard window controls, it might be further appreciated. Failing that, I'm good where I'm at. :) <br><br></td></tr></table><br>
<a name="1294279"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#28">[#28]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> If it's possible to accidentally lock a file by registering it so no-one can delete it at all unless it is properly released and only by rebooting or code <br></div>That only happens if you terminate the application without releasing the resource, which is to be expected with that specific API.<br>And your own example did just that, by not having a way to cleanly exit the application.<br><br>There is no such limitation with the one using AddFontResourceEx() and FR_PRIVATE (as in post #21), as it will be released when the process terminates no matter what.<br>And if its incbined theres not even a file to lock ;) <br><br></td></tr></table><br>
<a name="1294280"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#29">[#29]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm good, Grable. Still, maybe a screenshot or something of a custom font in a standard GUI interface might be interesting to look at.<br><br>If there is no easy way to have a custom font in a GUI interface without pulling out the kitchen sink from all the way in the bathroom, likely it was not meant to be.<br><br>Failing that, I can proceed without, and am already working on my libs, exchanging reference names and lookup for object IDs. <br><br></td></tr></table><br>
<a name="1294284"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#30">[#30]</a></td></tr></table></td></tr><tr ><td class="posttext"> Go take a rest and try again tomorrow dude. I copy-pasted post #21 and it works, but I deleted the Incbin call and specified a path to a local TTF file in my harddisk.<br><br>It didn't work at first, so I renamed the TTF file to a name without spaces and it worked -- I'm not sure if this made a difference, I'm too tired to tell.<br><br><img src="http://s23.postimg.org/em698yhob/dw817font.jpg"> <br><br></td></tr></table><br>
<a name="1294356"></a>

<a name="1294357"></a>

<a name="1294359"></a>

<a name="1294364"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#31">[#31]</a></td></tr></table></td></tr><tr ><td class="posttext"> Good Morning ! Yes, it looks like it works. :D<br><br>However, every font that gets 'registered' also gets locked in. I think I asked earlier, is there an UnregisterGuiFont() routine ?<br><br>Here is the current model:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict
Import maxgui.drivers

Global ib$="system\"
Local f$="cpmono",n$="cpmono_v07 plain"
?Not debug
  ib$="incbin::system\"
  Incbin "system\logo-1.png"
  Incbin "system\logo-2.png"
?

If Not RegisterGuiFont("system\"+f$+".ttf") Then RuntimeError "error registering font"

Global gadget:tgadget[200]

Global zvideo:TGadget=CreateWindow("Underway", 0,0,546,336,,WINDOW_TITLEBAR|WINDOW_CENTER)
Global font:TGuiFont = LoadGuiFont(n$, 8)

Global k$,menuid=50,menucode$,gadgetid=1,gadgetcode$

Local ev,cid,evd,p,i
'addgadget "slid","pixels across",0,20,400,60
'addgadget ib$+"logo-2.png","brand",550-256,0,256,144
addgadget "inp","file address",10,170,450,50
addgadget "butn","access",470,185,60,30
addgadget "inp","entry code",10,225,520,50
'addgadget "butn","open",10,10,50,30

addmenu "file,n@new state,o@open state,z@close state,-,s@save as ...,q@exit"
addmenu "options,e@encryption key,d@data parameters,g@get access code,-,compress file or folder,decompress file or folder"
addmenu "help,instructions,-,about,author,credits,-,website"

setGlobalGadgetFont(zvideo,font)
UpdateWindowMenu zvideo

enablepolledinput
UpdateWindowMenu zvideo

Repeat

WaitEvent
cid=EventID()
evd=EventData()
If cid=event_windowclose Then zz
If cid=event_menuaction
  If evd&gt;=50 And evd&lt;=99
    p=Instr(menucode$,str$(evd))
    If p Then k$="@"+Mid$(menucode$,p+2,3)
  EndIf
  Print k$
EndIf

Until KeyDown(27)

Function zz()
  End
EndFunction

' &gt;&gt; SET GLOBAL SYSTEM FONT &amp; SIZE
' THANK YOU DEGAC
Function SetGlobalGadgetFont:Int(par:tgadget=Null,fnt:tguifont=Null)
	If fnt=Null Return -1
	If par=Null Return -1
	If par.kids=Null Return -1
	For Local s:tgadget=EachIn par.kids 
			SetGadgetFont s,fnt
	Next
End Function

' &gt;&gt; SLICK ADD A MENU
Function addmenu(a$)
Local n,b$,c$
  b$=yank$(a$)
  gadget[menuid]=CreateMenu(firstcap$(b$),0,WindowMenu(zvideo))
  n=1
  While a$&gt;""
    c$=""
    b$=yank$(a$)
    If b$="-" ' add only a horizontal line
      CreateMenu "",0,gadget[menuid]
    Else
      If b$[1..2]="@"
        c$=b$[0..1]
        b$=Mid$(b$,3)
      EndIf
      CreateMenu firstcap$(b$),menuid+n,gadget[menuid],Asc(Upper$(c$)),modifier_command
    EndIf
    menucode$:+str$(menuid+n)+Left$(b$,3)
    n:+1
  Wend
  menuid:+10
EndFunction

' &gt;&gt; SLICK ADD A GADGET
Function addgadget(typ$,id$,h=0,v=0,x=0,y=0)
Local i,c$,img:TImage
  If typ$="slid" ' slider
    gadget[gadgetid]=CreateSlider(h+10,v+20,x-20,20,zvideo,slider_horizontal|slider_trackbar)
    SetSliderRange gadget[gadgetid],0,20
    CreatePanel h,v,x,y,zvideo,panel_group,firstcap$(id$)
  ElseIf typ$="butn" ' button
    gadget[gadgetid]=CreateButton(firstcap$(id$),h,v,x,y,zvideo,button_push)
  ElseIf typ$="inp" ' input field
    gadget[gadgetid]=CreateTextField(h+10,v+20,x-20,20,zvideo)
    CreatePanel h,v,x,y,zvideo,panel_group,firstcap$(id$)
  ElseIf Right$(typ$,4)=".png" ' image
    gadget[gadgetid]=CreateCanvas(h,v,x,y,zvideo)
    SetGraphics CanvasGraphics(gadget[gadgetid])
    DrawImageRect LoadImage(typ$),0,0,x,y
    Flip 0
  EndIf
  ActivateGadget gadget[gadgetid]
  If gadgetid&lt;10 Then gadgetcode$:+"0"
  gadgetcode$:+str$(gadgetid)+id$+","
  gadgetid:+1
EndFunction

' &gt;&gt; RETURN TEXT WHERE FIRST LETTER AFTER SPACE IS UPPERCASE
Function firstcap$(a$)
Local i,r$,u=1,c$,o$
  For i=0 Until Len(a$)
    c$=a$[i..i+1]
    o$=Lower$(c$)
    If o$&gt;="a" And o$&lt;="z" And u=1 Then c$=Upper$(c$) ; u=0
    If o$&lt;"a" Or o$&gt;"z" Then u=1
    r$:+c$
  Next
  Return r$
EndFunction    

' &gt;&gt; RETURN STRING FROM VALUE
Function str$(n)
  Return Int(n)
EndFunction

' &gt;&gt; RETURN COMMA DELIMITED TEXT FROM A$
Function yank$(a$ Var)
Local b=Instr(a$+",",","),c$=Left$(a$,b-1)
  a$=Mid$(a$,b+1) ' also remove it from input string
  Return c$
EndFunction

' &gt;&gt; REGISTER A TRUETYPE FONT FOR USE W GADGETS
' THANK YOU GRABLE &amp; KRYZON
Function RegisterGuiFont:Int( url:String)
	Extern "Win32"
		Const FR_PRIVATE:Int = $10
		Const FR_NOT_ENUM:Int = $20
		
		Function AddFontMemResourceEx:Int( p:Byte Ptr, sz:Int, r:Byte Ptr, count:Int Var)
		Function AddFontResourceExW:Int( fn$w, flags:Int, r:Byte Ptr)
	EndExtern

	Local font:Int
	If url.StartsWith("incbin::") Then
		url = url["incbin::".Length ..]
		Local count:Int
		AddFontMemResourceEx( IncbinPtr(url), IncbinLen(url), Null, count)
		Return count &gt; 0
	Else
		Return AddFontResourceExW( url, FR_PRIVATE, Null)
	EndIf
EndFunction
</textarea><br><br>Oh, and before I reinvent something - has someone already made a gadget maker ? That is, a program that lets you use the mouse and keyboard to set down and arrange gadgets on a canvas of your choosing and then give you the option to convert that lot to raw BlitzMAX code afterwards ?<br><br>And - there is no way to change the menu font ? <br><br></td></tr></table><br>
<a name="1294358"></a>

<a name="1294360"></a>

<a name="1294361"></a>

<a name="1294362"></a>

<a name="1294365"></a>

<a name="1294395"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#32">[#32]</a></td></tr></table></td></tr><tr ><td class="posttext"> I dont know why yours is locking, i cant seem to reproduce it. Not even force terminating the application locks the file...<br>In any case, incbining the font would fix that.<br><br>To unregister a font call the function below, though there should not be a need for it. As the OS will unregister the font when the process terminates.<br><br>EDIT: Updated both functions.<br><pre class=code>
Private
Extern "Win32"
	Const FR_PRIVATE:Int = $10
	Const FR_NOT_ENUM:Int = $20
	
	Function AddFontMemResourceEx:Int( p:Byte Ptr, sz:Int, r:Byte Ptr, count:Int Var)
	Function AddFontResourceExW:Int( fn$w, flags:Int, r:Byte Ptr)
	Function RemoveFontResourceExW:Int( url$w, flags:Int, r:Byte Ptr)
EndExtern
Public

Function RegisterGuiFont:Int( url:String)
	Local font:Int
	If url.StartsWith("incbin::") Then
		url = url["incbin::".Length ..]
		Local count:Int
		AddFontMemResourceEx( IncbinPtr(url), IncbinLen(url), Null, count)
		Return count &gt; 0
	Else
		Return AddFontResourceExW( url, FR_PRIVATE, Null)
	EndIf
EndFunction

Function UnregisterGuiFont( url:String)
	If Not url.StartsWith("incbin::") Then
		RemoveFontResourceExW( url, FR_PRIVATE, Null)
	EndIf
EndFunction
</pre><br><br>From <a href="https://msdn.microsoft.com/en-us/library/windows/desktop/dd162923%28v=vs.85%29.aspx" target="_blank">https://msdn.microsoft.com/en-us/library/windows/desktop/dd162923%28v=vs.85%29.aspx</a> :<br>When you try to replace an existing font file that contains a font with outstanding references to it, you might get an error that indicates that the original font can't be deleted because its in use even after you call RemoveFontResourceEx. If your app requires that the font file be replaced, to reduce the resource count of the original font to zero, call RemoveFontResourceEx in a loop as shown in this example code. If you continue to get errors, this is an indication that the font file remains loaded in other sessions. Make sure the font isn't listed in the font registry and restart the system to ensure the font is unloaded from all sessions.<br><br>So it might be a previous session that didnt clean up properly.<br>Try copying the font to a new file and see if it gets locked even after normally exiting the application. <br><br></td></tr></table><br>
<a name="1294367"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#33">[#33]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Grable. Yep, that fixed it. And yes, the fonts were definitely getting stuck. I already did a reboot before I tried this new code. And - unfortunately these fonts really don't look very good compared to what the system chooses.<br><br>After all that. :) Well, I =CAN= see where this routine would come in handy for RPG text where you want to have a large and fancy font.<br><br>There's nothing to say I could actually (for the first time) make a true RPG Maker that makes use of standard Windows GUI controls to modify and edit the world file.<br><br>Here's another question. Is it possible to change the background color of a field you insert text to ? <br><br></td></tr></table><br>
<a name="1294376"></a>

<a name="1294377"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#34">[#34]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Is it possible to change the background color of a field you insert text to ?  <br></div>SetGadgetColor and SetGadgetTextColor.<br>Press F1 twice on a MaxGI function and youl get a list of all of them. <br><br></td></tr></table><br>
<a name="1294378"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#35">[#35]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Grable &amp; other: many thanks for the solution posted!<br><br>I think I put on the online manual for future request... too handy in some occasion! <br><br></td></tr></table><br>
<a name="1294393"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#36">[#36]</a></td></tr></table></td></tr><tr ><td class="posttext"> dw817, I suggest renaming the thread to something like "MaxGUI custom gadget font", so people know what it's about just by reading the title.<br>Objectivity comes to mind. <br><br></td></tr></table><br>
<a name="1294417"></a>

<a name="1294429"></a>

<a name="1294430"></a>

<a name="1294431"></a>

<a name="1294432"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dw817</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#37">[#37]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah sure, Kryzon. That's a better title. I think this question has been well answered. Thanks to all contributors !<br><br>Got the gadget page done, working on the internals now.<br><br><img src="http://www.writerscafe.org/uploads/rte/157abd07decf9512f10ea21a384c382b.png"><br><br>Not sure why it appears blurry here. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
