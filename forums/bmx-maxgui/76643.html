<!DOCTYPE html><html lang="en" ><head ><title >Attaching objects to tabbers and updating.</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Attaching objects to tabbers and updating.</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >Attaching objects to tabbers and updating.</a><br><br>
<a name="857046"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ryan Burnside</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Recently I have needed to create a small pop up with tabbers. My problem is attaching individual gadgets to each tabber page and then updating them.<br><br>I've read the maxGUI tutorials on Tabbers and the author does not offer a way to create each tabber with their own attached Tgadgets.<br><br>Here is my current code, I need a special canvas for each tabber page or perhaps one canvas object that works between the three pages. How do I check which tab is down and update accordingly?<br><br><pre class=code>
' GUI application to adjust RGB levels of a photograph
Strict

Global start_x:Float = 320
Global start_y:Float = 240

' a simple spline type

Function spline_f:Float(p:Float, p1:Float, p2:Float, t:Float) 
	Local a:Float = (1 - t) 
	Return (a ^ 2.0 * p) + (2.0 * t * a * p1) + (t ^ 2 * p2) 
End Function

Function draw_spline(x:Float, y:Float, x2:Float, y2:Float, x3:Float, y3:Float, divisions:Float) 
	
End Function

' create the window and tabbers
Local MyWindow:TGadget = CreateWindow("Color Curves", 320, 320, 312, 324, Null, 1) 
Local Tabber:TGadget = CreateTabber(0, 0, 312, 300, MyWindow) 
' make some canvases (canvi?) to house the Red,Green, and Blue curve drawings respectivly
Local red_canvas:TGadget = CreateCanvas(12, 12, 255, 255, tabber) 
red_canvas.SetColor(0, 0, 0) 

Local green_canvas:TGadget = CreateCanvas(12, 12, 255, 255, tabber) 
green_canvas.SetColor(0, 0, 0) 

Local blue_canvas:TGadget = CreateCanvas(12, 12, 255, 255, tabber) 
blue_canvas.SetColor(255, 255, 255) 

' create tabs
AddGadgetItem(Tabber, "Red Curve", 0, 0, "Adjust red levels.") 
AddGadgetItem (Tabber, "Green Curve", 0, 0, "Adjust green levels.") 
AddGadgetItem (Tabber, "Blue Curve", 0, 0, "Adjust blue levels.") 


Repeat
	WaitEvent() 
	Select EventID() 
		Case EVENT_WINDOWCLOSE
     	End
	
		Case EVENT_GADGETPAINT
		
    			SetGraphics CanvasGraphics (red_canvas) 
			SetColor(255, 0, 0) 
    			DrawText("Red Canvas", 0, 0) 
			Flip
			
			SetGraphics CanvasGraphics (green_canvas) 
			SetColor(0, 255, 0) 
    			DrawText("Green Canvas", 0, 0) 
			Flip
			
			SetGraphics CanvasGraphics (blue_canvas) 
			SetColor(0, 0, 255) 
    			DrawText("Blue Canvas", 0, 0) 
			Flip
	
   End Select
Forever
</pre> <br><br></td></tr></table><br>
<a name="857058"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> you only really need one canvas. I hope this helps you understand it better:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' GUI application to adjust RGB levels of a photograph
Strict

Global start_x:Float = 320
Global start_y:Float = 240

' a simple spline type
Function spline_f:Float(p:Float, p1:Float, p2:Float, t:Float) 
	Local a:Float = (1 - t) 
	Return (a ^ 2.0 * p) + (2.0 * t * a * p1) + (t ^ 2 * p2) 
End Function

Function draw_spline(x:Float, y:Float, x2:Float, y2:Float, x3:Float, y3:Float, divisions:Float) 
	
End Function

' create the window and tabbers
Local MyWindow:TGadget = CreateWindow("Color Curves", 320, 320, 312, 324, Null, 1) 
Local Tabber:TGadget = CreateTabber(0, 0, 312, 300, MyWindow)
' make some canvases (canvi?) to house the Red,Green, and Blue curve drawings respectivly
Local canvas:TGadget = CreateCanvas(12, 12, 255, 255, tabber) 
' create tabs
AddGadgetItem(Tabber, "Red Curve", 0, 0, "Adjust red levels.") 
AddGadgetItem (Tabber, "Green Curve", 0, 0, "Adjust green levels.") 
AddGadgetItem (Tabber, "Blue Curve", 0, 0, "Adjust blue levels.")
SelectGadgetItem tabber,0 
Global grfx:tgraphics = CanvasGraphics(canvas)
SetGraphics grfx 
Global text$ = "Red Canvas"
Global r% = 255,g%=0,b%=0
CreateTimer 60
Global index% = 0

Repeat
	WaitEvent() 

	Select EventID() 
		Case EVENT_WINDOWCLOSE
     	End
	
		Case EVENT_TIMERTICK
			RedrawGadget canvas
		Case EVENT_GADGETPAINT
			Cls
			SetColor r,g,b
			DrawText text,0,0
			Flip()
		Case EVENT_GADGETACTION
			Select EventData()
    			Case 0
					r = 255
					g = 0
					b = 0 
    				text = "Red Canvas" 
				Case 1
					r = 0
					g = 255
					b = 0 
    				text = "Green Canvas"
				Case 2
					r = 0
					g = 0
					b = 255 
    				text = "Blue Canvas"
			End Select
   End Select
Forever
</textarea><br>having more than one canvas is a waste of resources. Or maybe it's cause I don't know how to do it that way :-). <br><br></td></tr></table><br>
<a name="857087"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> You can have different canvas for each tabs...<br>I used the basic example that comes with MaxGUI<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' createtabber.bmx
Import MaxGui.Drivers
Strict 
Local window:TGadget
Local tabber:TGadget
Local document:TGadget[3],pa:tgadget[3]
Local currentdocument:TGadget

' CreateDocument creates a hidden panel that fills entire tabber client area 

Function CreateDocument:TGadget(tabber:TGadget)
	Local	panel:TGadget
	panel=CreatePanel(0,0,ClientWidth(tabber),ClientHeight(tabber),tabber)
	SetGadgetLayout panel,1,1,1,1
	HideGadget panel
	Return panel
End Function

' create a default window with a tabber gadget that fills entire client area

window=CreateWindow("My Window",30,20,400,300)

tabber=CreateTabber(0,0,ClientWidth(window),ClientHeight(window),window)
SetGadgetLayout tabber,1,1,1,1 

' add three items and corresponding document panels to the tabber

AddGadgetItem tabber,"Document 0",False,-1,""
AddGadgetItem tabber,"Document 1",False,-1,"Tabber Tip 1"
AddGadgetItem tabber,"Document 2",False,-1,"tips 4 2"

document[0]=CreateDocument(tabber)
document[1]=CreateDocument(tabber)
document[2]=CreateDocument(tabber)

SetPanelColor document[0],255,200,200
SetPanelColor document[1],200,255,200
SetPanelColor document[2] , 200 , 200 , 255

'to keep track of the canvases associated each to one tab (document[])


pa[0]=CreateCanvas(10 , 10 , 100 , 100 , document[0])
pa[1]=CreateCanvas(10 , 10 , 200 , 100 , document[1])
pa[2]=CreateCanvas(10 , 10 , 100 , 200 , document[2])


' our documents start off hidden so make first one current and show

currentdocument=document[0]
ShowGadget currentdocument


Global grfx:tgraphics = CanvasGraphics(pa[0])
SetGraphics grfx 

' standard message loop with special tabber GADGET_ACTION handling
Global text$ = "Red Canvas"
Global what_canvas:Int=0
While WaitEvent()
	Select EventID() 
	
		Case EVENT_TIMERTICK
			'redraw ONLY the current document-canvas
			RedrawGadget pa[what_canvas]
			
		Case EVENT_GADGETPAINT
			Cls
			DrawText text,0,0
			Flip()

	
		Case EVENT_GADGETACTION
			If EventSource()=tabber
				HideGadget currentdocument
				currentdocument = document[EventData()]
				'choose what canvas uses
				what_canvas = EventData()
				'setup graphics for that one
				grfx:tgraphics = CanvasGraphics(pa[what_canvas])
				SetGraphics grfx
				ShowGadget currentdocument
			EndIf
		Case EVENT_WINDOWCLOSE
			End
	End Select
Wend
</textarea> <br><br></td></tr></table><br>
<a name="857183"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> using degac sample and an eventhook, I think this is more or less what you want.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict' createtabber.bmx
'Import MaxGui.Drivers
 
Local window:TGadget
Global tabber:TGadget
Global document:TGadget[3],pa:tgadget[3]
Global currentdocument:TGadget

' CreateDocument creates a hidden panel that fills entire tabber client area 

Function CreateDocument:TGadget(tabber:TGadget)
	Local	panel:TGadget
	panel=CreatePanel(0,0,ClientWidth(tabber),ClientHeight(tabber),tabber)
	SetGadgetLayout panel,1,1,1,1
	HideGadget panel
	Return panel
End Function

' create a default window with a tabber gadget that fills entire client area

window=CreateWindow("My Window",30,20,400,300)

tabber=CreateTabber(0,0,ClientWidth(window),ClientHeight(window),window)
SetGadgetLayout tabber,1,1,1,1 

' add three items and corresponding document panels to the tabber

AddGadgetItem tabber,"Red Curve",False,-1,""
AddGadgetItem tabber,"Green Curve",False,-1,"Tabber Tip 1"
AddGadgetItem tabber,"Blue Curve",False,-1,"tips 4 2"

document[0]=CreateDocument(tabber)
document[1]=CreateDocument(tabber)
document[2]=CreateDocument(tabber)

SetPanelColor document[0],255,200,200
SetPanelColor document[1],200,255,200
SetPanelColor document[2],200,200,255

'to keep track of the canvases associated each to one tab (document[])


pa[0]=CreateCanvas(10 , 10 , 100 , 100 , document[0])
pa[1]=CreateCanvas(10 , 10 , 200 , 100 , document[1])
pa[2]=CreateCanvas(10 , 10 , 100 , 200 , document[2])

AddHook EmitEventHook, MyHook
' our documents start off hidden so make first one current and show

currentdocument=document[0]
ShowGadget currentdocument


Global grfx:tgraphics = CanvasGraphics(pa[0])
SetGraphics grfx 

' standard message loop with special tabber GADGET_ACTION handling
SetColor 255,0,0
Global text$ = "Red Canvas"
Global what_canvas:Int=0
'CreateTimer 60
Global index% = 0
While WaitEvent()
	Select EventID() 
	
			
		Case EVENT_GADGETPAINT
			updateCanvas()
			
			
		Case EVENT_WINDOWCLOSE
			End
	End Select
Wend

Function MyHook:Object(iId:Int,tData:Object,tContext:Object)
	Local Event:TEvent=TEvent(tData)
	
	If Event.source=tabber 
		index = event.data
		UpdateCanvas()
		Return Null
  	EndIf

	Return tData
End Function

Function UpdateCanvas:Int()
	Select  index
		Case 0
			SetColor 255,0,0
			text = "Red Canvas"
		Case 1
			SetColor 0,255,0
			text = "Green Canvas"
		Case 2
			text = "Blue Canvas"
			SetColor 0,0,255
	End Select
	grfx:tgraphics = CanvasGraphics(pa[index])
	SetGraphics grfx
	If currentdocument &lt;&gt; document[index]
		HideGadget currentdocument
		currentdocument = document[index]
	EndIf
	ShowGadget currentdocument 
    Cls
	DrawText text,0,0 
    Flip
End Function
</textarea> <br><br></td></tr></table><br>
<a name="857190"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ryan Burnside</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks gentlemen. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
