<!DOCTYPE html><html lang="en" ><head ><title >canvas &amp; menu &amp; animation not cooperating.</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >canvas &amp; menu &amp; animation not cooperating.</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >canvas &amp; menu &amp; animation not cooperating.</a><br><br>
<a name="930812"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Is ther a way to have canvas, working correctly while animating.<br><br>I am trying to adopt the code from this thread:<br><a href="http://www.blitzmax.com/Community/posts.php?topic=78597#881924" target="_blank">http://www.blitzmax.com/Community/posts.php?topic=78597#881924</a><br>it doesn't seem to work with animation.<br>If I change the hook to a timer it does not update the menu properly as in the mentioned thread. Is there a way to solve this problem? I did a forum search and I couldn't find anything to help me. <br>Any help would be appreciated.<br><br><br>this is the original code with animation included:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Import MaxGui.Drivers
SuperStrict
Local Style:Int = WINDOW_TITLEBAR | WINDOW_MENU  | WINDOW_CLIENTCOORDS

Local MyWindow:TGadget
Local filemenu:TGadget
Local editmenu:TGadget
Local helpmenu:TGadget

Global canvas:TGadget

MyWindow=CreateWindow("Max GUI App.", 112,84,800,600,Null,Style)

filemenu=CreateMenu("&amp;File          ",0,WindowMenu(MyWindow))
CreateMenu "&amp;New",101,filemenu,KEY_N,MODIFIER_COMMAND
CreateMenu "&amp;Open file",102,filemenu,KEY_O,MODIFIER_COMMAND
CreateMenu "&amp;Close file",103,filemenu,KEY_C,MODIFIER_COMMAND
CreateMenu"",0,filemenu
CreateMenu "&amp;Save",104,filemenu,KEY_S,MODIFIER_COMMAND
CreateMenu"",0,filemenu
CreateMenu "&amp;Open bitmap image",105,filemenu,KEY_I,MODIFIER_COMMAND
CreateMenu"",0,filemenu
CreateMenu "&amp;Exit",106,fileMenu,KEY_F4,MODIFIER_COMMAND

editmenu=CreateMenu("&amp;Edit          ",0,WindowMenu(MyWindow))
CreateMenu "Cu&amp;t",107,editmenu,KEY_X,MODIFIER_COMMAND
CreateMenu "&amp;Copy",108,editmenu,KEY_C,MODIFIER_COMMAND
CreateMenu "&amp;Paste",109,editmenu,KEY_V,MODIFIER_COMMAND

helpmenu=CreateMenu("&amp;Help          ",0,WindowMenu(MyWindow))
CreateMenu "&amp;About",110,helpmenu

canvas=CreateCanvas(0,0,800,640,MyWindow)

UpdateWindowMenu MyWindow

AddHook EmitEventHook , MyHook

While True
	WaitEvent 
	Select EventID()
		Case EVENT_MENUACTION
			Select EventData()
			
				Case 106
					End

				Case 110
					Notify "About stuff here"
			End Select
	End Select
Wend

Function MyHook:Object(iId:Int,tData:Object,tContext:Object)
	Local Event:TEvent=TEvent(tData)
  	If event = Null Return Null
	
	If (Event.ID = EVENT_GADGETPAINT) Or (Event.ID = EVENT_WINDOWSIZE) Or (Event.ID = EVENT_WINDOWMOVE) Or (Event.ID = Event_AppResume) Then 
		'ReDraw the canvas when requested (GadgetPaint) or when the window was moved, sized, resumed..
		ReDraw()
	End If
	
	Return tData
End Function

Function ReDraw() 
	'Call the ReDraw every time you want to update the canvas
	SetGraphics CanvasGraphics (canvas)
	SetViewport 0,0,GadgetWidth(canvas),GadgetHeight(canvas)
	Cls
	Local t:Long=MilliSecs()/100
	DrawLine 400,300,400+120*Cos(t),300+120*Sin(t)
	DrawLine 400,300,400+80*Cos(t/60),300+80*Sin(t/60)
	Flip
End Function</textarea><br><br>but does not work <br><br></td></tr></table><br>
<a name="930864"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> If I add the timer, and include <b>EVENT_TIMERTICK</b> in the event-hook processing, then everything seems to work fine over here...<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict
Import MaxGui.Drivers

AppTitle = "MaxGUI Application"

Local MyWindow:TGadget = CreateWindow(AppTitle,112,84,800,600,Null,WINDOW_TITLEBAR|WINDOW_MENU|WINDOW_CLIENTCOORDS)

Local filemenu:TGadget = CreateMenu("&amp;File",0,WindowMenu(MyWindow))
CreateMenu "&amp;New",101,filemenu,KEY_N,MODIFIER_COMMAND
CreateMenu "&amp;Open file",102,filemenu,KEY_O,MODIFIER_COMMAND
CreateMenu "&amp;Close file",103,filemenu,KEY_C,MODIFIER_COMMAND
CreateMenu "",0,filemenu
CreateMenu "&amp;Save",104,filemenu,KEY_S,MODIFIER_COMMAND
CreateMenu "",0,filemenu
CreateMenu "&amp;Open bitmap image",105,filemenu,KEY_I,MODIFIER_COMMAND
CreateMenu "",0,filemenu
CreateMenu "&amp;Exit",106,fileMenu,KEY_F4,MODIFIER_COMMAND

Local editmenu:TGadget = CreateMenu("&amp;Edit",0,WindowMenu(MyWindow))
CreateMenu "Cu&amp;t",107,editmenu,KEY_X,MODIFIER_COMMAND
CreateMenu "&amp;Copy",108,editmenu,KEY_C,MODIFIER_COMMAND
CreateMenu "&amp;Paste",109,editmenu,KEY_V,MODIFIER_COMMAND

Local helpmenu:TGadget = CreateMenu("&amp;Help",0,WindowMenu(MyWindow))
CreateMenu "&amp;About",110,helpmenu

UpdateWindowMenu MyWindow

Global canvas:TGadget = CreateCanvas(0,0,800,640,MyWindow)
SetGraphics CanvasGraphics (canvas)

AddHook EmitEventHook, MyHook

CreateTimer(100)

Repeat
	Select WaitEvent()
		Case EVENT_MENUACTION
			Select EventData()
				Case 106
					End
				Case 110
					Notify "About stuff here"
			End Select
	End Select
Forever

Function MyHook:Object(iId:Int,tData:Object,tContext:Object)
	Local Event:TEvent=TEvent(tData)
  	If event = Null Return Null
	
	Select event.id
		Case EVENT_TIMERTICK, EVENT_GADGETPAINT, EVENT_WINDOWMOVE, EVENT_APPRESUME
		'ReDraw the canvas when requested (GadgetPaint) or when the window was moved, sized, resumed..
			ReDraw()
		Case EVENT_WINDOWSIZE
			SetViewport 0,0,ClientWidth(canvas),ClientHeight(canvas)
			ReDraw()
	EndSelect
	
	Return tData
End Function

Function ReDraw() 
	'Call ReDraw() every time you want to update the canvas
	Cls
	Local t:Long=MilliSecs()/100
	DrawLine 400,300,400+120*Cos(t),300+120*Sin(t)
	DrawLine 400,300,400+80*Cos(t/60),300+80*Sin(t/60)
	Flip
End Function</textarea><br><br>What OS are you on? <br><br></td></tr></table><br>
<a name="930899"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks!, that did it!,<br>I don't know why but I can't figure this stuff out. I tried like crazy to figure it out on my own but I ended up posting about something simple yet unintuitive to me. The whole gui concept is new to me and has become a real challenge to learn. The real challeng to me is making the whole thing work together. I can use all of the functions sintax properly but I can't seem to figure out the hooks and timer thing. I went through Assari tutorials and I thought I understood them well But as he said on the canvas lesson:<div class="quote"> <br>This gadget actually deserves a whole series of tutorial by itself as it actually brings the whole power of Max2D with it.<br> <br></div><br>Thanks Again, much appreciated. <br><br>By the way, I am using WIN XP SP2 <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
