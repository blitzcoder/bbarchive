<!DOCTYPE html><html lang="en" ><head ><title >GetGraphics:TGraphics() function</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >GetGraphics:TGraphics() function</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >GetGraphics:TGraphics() function</a><br><br>
<a name="943853"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kistjes</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello,<br><br>In my current MaxGUI project I have to deal with multiply canvas.<br>I have a list of sprites (let's say simple white rectangles) that need to be drawn in the correct canvas.<br>Now I can add a TList of all related sprites to the canvas but I can also add a reference to the correct CanvasGraphics to every sprite. I prefer the second approach. <br><br>Each sprite has a container and it's location is relative to it's container. The container can be another sprite or can be null. If the sprite's container is null the location is relative to the graphical object: in non MaxGUI apps it is the Graphics() object but in a MaxGUI app it's CanvasGraphics(canvas).<br>When the Draw() method of the sprite is called, the sprite needs to know if it's container is currently active (with the setGraphics(CanvasGraphics(canvas)) function).<br>There is no GetGraphics() function. How can I get the currenty active TGraphics object?<br><br>I hope the example code below helps to clarify my question...<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

'create two windows
TMainWindow.Create(100, 100, 800, 600)
TSecondWindow.Create(460, 300, 640, 480)

CreateTimer(100)

While True
	WaitEvent()

	Select EventID()
		Case EVENT_TIMERTICK
'			DebugLog("EVENT_TIMERTICK")
			'draw the canvas content
			TGraphicalWindow.DrawAllWindows()
		Case EVENT_GADGETPAINT
'			DebugLog("EVENT_GADGETPAINT")

		Case EVENT_MOUSEMOVE
'			DebugLog("EVENT_MOUSEMOVE")

		Case EVENT_WINDOWCLOSE
			DebugLog("EVENT_WINDOWCLOSE")
			End

		Case EVENT_APPTERMINATE
			DebugLog("EVENT_APPTERMINATE")
			End
	End Select
Wend

End

'I need to have a function like this to retreive the current active graphics window
Function GetGraphics:TGraphics()
	'ok, how to do this?
	Return Null
End Function

'this type contains a full size canvas
Type TGraphicalWindow Abstract
	Global golWindows:TList
	
	Field poWindow:TGadget
	Field poCanvas:TGadget
	
	
	Method New()
		If golWindows = Null Then golWindows = CreateList()
		golWindows.AddLast(Self)
	End Method


	Function DrawAllWindows()
		For Local o:TGraphicalWindow = EachIn golWindows
			o.DrawWindow()
		Next
	End Function
	
	Method DrawWindow() Abstract
	
	Method Remove()
		FreeGadget(poCanvas)
		FreeGadget(poWindow)
		golWindows.Remove(Self)
	End Method

End Type

'This window should display two sprites:
'-one reletive to upper-left corner of canvas
'-and another one reletive to the first sprite

Type TMainWindow Extends TGraphicalWindow
	Field poSprite1:TSprite
	Field poSprite2:TSprite
	
	Function Create:TMainWindow(ix:Int, iy:Int, iWidth:Int, iHeight:Int)
		Local o:TMainWindow = New TMainWindow
		o.poWindow = CreateWindow("Main", ix, iy, iWidth, iHeight)
		o.poCanvas = CreateCanvas(0, 0, iWidth, iHeight, o.poWindow)
		Return o
	End Function

	Method DrawWindow()
		SetGraphics(CanvasGraphics(poCanvas))
		If Not poSprite1 Then
			poSprite1 = TSprite.Create(Null, 200, 100, 50, 50)
			poSprite2 = TSprite.Create(poSprite1, 100, 100, 50, 50)
		End If
		Cls

		TSprite.DrawAll()
		Flip
	End Method

End Type

'This window should show only one sprite
Type TSecondWindow Extends TGraphicalWindow
	Field poSprite:TSprite
	
	Function Create:TSecondWindow(ix:Int, iy:Int, iWidth:Int, iHeight:Int)
		Local o:TSecondWindow = New TSecondWindow
		o.poWindow = CreateWindow("Second", ix, iy, iWidth, iHeight)
		o.poCanvas = CreateCanvas(0, 0, iWidth, iHeight, o.poWindow)
		Return o
	End Function

	Method DrawWindow()
		SetGraphics(CanvasGraphics(poCanvas))
		If Not poSprite Then
			poSprite = TSprite.Create(Null, 20, 10, 150, 50)
		End If
		Cls

		TSprite.DrawAll()
		Flip
	End Method

End Type


'Simplyfied sprite type to illustrate the purpose of the TGraphicalWindow type
Type TSprite

	Global golSprites:TList
	
	Field poGraphics:TGraphics
	Field poContainer:TSprite
	Field pfRelX:Float
	Field pfRelY:Float
	Field pfWidth:Float
	Field pfHeight:Float
	
	Function Create:TSprite(oContainer:TSprite = Null, fx:Float, fy:Float, fWidth:Float, fHeight:Float)
		Local o:TSprite = New TSprite
		If golSprites = Null Then golSprites = CreateList()
		golSprites.AddLast(o)
		
		If oContainer Then
			o.poContainer = oContainer
		End If
'		Here we create the connection to the active TGraphics object
'		perhaps I can use the poContainer variable for this as well.
 		o.poGraphics = GetGraphics()

		o.pfRelX = fx
		o.pfRelY = fy
		o.pfWidth = fWidth
		o.pfHeight = fHeight
		return o
	End Function
	

	Function DrawAll()
		For Local o:TSprite = EachIn golSprites
			'only draw the sprites that are connected to the active TGraphics object
			If o.poGraphics = GetGraphics() Then o.Draw()
		Next
	End Function
	

	Method GetAbsX:Float()
		If poContainer Then Return poContainer.GetAbsX() + pfRelX
		Return pfRelX
	End Method
	
	
	Method GetAbsY:Float()
		If poContainer Then Return poContainer.GetAbsY() + pfRelY
		Return pfRelY
	End Method
	
	
	Method Draw()
		'for the sake of this demo, sprites are only drawn as white rectangles
		DrawRect(Self.GetAbsX(), Self.GetAbsY(), pfWidth, pfHeight)
	End Method
End Type
</textarea> <br><br></td></tr></table><br>
<a name="944349"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kistjes</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Same question but without all the explanation...<br><br>To <b>set</b> a TGraphics object I use SetGraphics()<br>How do I <b>get</b> the active TGraphics object? There is no such command like GetGraphics:TGraphics() <br><br></td></tr></table><br>
<a name="944468"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >kfprimm</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> No there is not. Add the following to BRL.Graphics,<br><pre class=code>
Function GetGraphics:TGraphics()
	Return _graphics
End Function
</pre><br>Alternatively, you could just store the Graphics object in a global variable and modify it each time you call SetGraphics.<br><br>That would most easily be accomplished with,<br><pre class=code>
Function SetGraphics(g:TGraphics)
	Global _graphics:TGraphics
	_graphics=g
	Return brl.graphics.SetGraphics(_graphics)
End Function
</pre><br>Just stick this into your source.<br><br>All of this code is untested but it should function properly. <br><br></td></tr></table><br>
<a name="944552"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kistjes</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks! I'll try that. <br><br></td></tr></table><br>
<a name="945493"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >kfprimm</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ha. Sorry, I was tired when I wrote that code.<br>The following should be used instead of just the SetGraphics function.<br><br><pre class=code>
Private
Global _graphics:TGraphics

Public
Function SetGraphics(g:TGraphics)
	_graphics=g
	Return brl.graphics.SetGraphics(_graphics)
End Function

Function GetGraphics:TGraphics()
	Return _graphics
End Function
</pre> <br><br></td></tr></table><br>
<a name="945561"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kistjes</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> The getGraphics() functions should work in all circumstances. In a non-maxGUI project I only use the Graphics(x,y,d,hz) function. This functions does not call my own SetGraphics() function <i>and</i> will therefor not set the global _graphics.<br><br>That's why I decided to add a GetGraphics() function to brl.Graphics (your first suggestion, Khomy). <br><br></td></tr></table><br>
<a name="945629"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >kfprimm</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Alright cool. What I was saying with the SetGraphics function is that originally, the _graphics was set as global in the function and therefore inaccessible outside the function. That snippet corrects the original. When calling brl.graphics.SetGraphics, the _graphics variable in BRL.Graphics is set. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
