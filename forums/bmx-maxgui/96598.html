<!DOCTYPE html><html lang="en" ><head ><title >Gui newbie :)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Gui newbie :)</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >Gui newbie :)</a><br><br>
<a name="1118429"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hiya all,<br><br>I have a 'main' editor window, that when a button is pressed, it opens a smaller worker window. What's the correct procedure to close the 'worker' window after I'm finished altering data. Most examples I looked at simply use End on EVENT_WINDOWCLOSE, which is not what I want to do. I just want to close the window using the correct procedure.<br><br>Cheers :P <br><br></td></tr></table><br>
<a name="1118430"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GfK</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hidegadget() or freegadget() <br><br></td></tr></table><br>
<a name="1118443"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks.<br>Anothet quesion... Does freegadget automatically call any child gadgets to free them correctly too? Or I do it myself? <br><br></td></tr></table><br>
<a name="1118453"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GfK</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Erm, pass! <br><br></td></tr></table><br>
<a name="1118455"></a>

<a name="1118456"></a>

<a name="1118458"></a>

<a name="1118521"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>Remove a gadget and free its resources.<br> <br></div><br><br> I am guessing that is what that means. if not than I have been doing it wrong.<br>-from a newbie to a newbie<br>I have been converting( more or less starting from scratch) my tile map editor.<br>I don't know if it will help you or misguide you but here it is anyway:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import MaxGui.Drivers

SuperStrict

Include "newMapGui.bmx"

Global mouse_x:Int
Global mouse_y:Int

Function CreateDocument:TGadget(tabber:TGadget)
	Local	panel:TGadget
	panel=CreatePanel(0,0,ClientWidth(tabber),ClientHeight(tabber),tabber)
	SetGadgetLayout panel,1,1,1,1
	HideGadget panel
	Return panel
End Function

Type TSprites
	Field images:TImage
	Field offsety			:Int
	Field maxOffsetY		:Int
	
	Method update() Abstract
	Method render() Abstract
	Method setActive() Abstract
	Method getActive:Int()Abstract
End Type

Type TSingular Extends TSprites
	Field tilesAcross		:Int
	Field tilesDown		:Int
	
	Field viewWidth		:Int
	Field viewHeight		:Int
	
	Field totalTiles		:Int

	Field tileWidth		:Int
	Field tileHeight		:Int

	Field inTilex			:Int
	Field inTiley			:Int

	Field tileIndex		:Int
	Field activeIndex		:Int

	Field canvas			:TGadget
	
	Function Create:Tsingular(image:TImage,canvas:TGadget Var)
		Local s:TSingular = New TSingular
		Local w:Int = GadgetWidth(canvas)
		Local h:Int = GadgetHeight(canvas)
		s.totalTiles = image.frames.length
		s.images = image
		s.tileWidth = image.width
		s.tileHeight = image.height
		s.canvas = canvas
		w = w/image.width
		h = h/image.height
		Local n:Int
		If w &gt; s.totalTiles Then 
			s.tilesAcross = s.totalTiles
			s.tilesDown = 1
		Else'If w*h &gt; s.totalTiles
			n = Ceil(Float(s.totalTiles)/w)
			If n &lt; h Then 
				s.tilesdown  = n
			Else
				s.tilesDown = h
			EndIf
			s.tilesAcross = w
		EndIf
		If n &gt; h s.maxOffsetY = n-h Else s.maxOffsetY = 0
		s.viewWidth = s.tilesAcross*s.tileWidth
		s.viewHeight= s.tilesDown*s.tileHeight
		Return s
	End Function
	
	Method update()
		Local x:Int = mouse_X
		Local y:Int = mouse_Y
		If x &lt; viewWidth And y &lt; viewHeight
			x = x/tileWidth
			y = y/tileHeight
			inTileX = x
			inTileY = y
			tileIndex = (offsety+inTileY)*tilesAcross+inTileX
			If tileIndex &gt;= totalTiles tileIndex = -1
		Else	
			tileIndex = -1
		EndIf
	End Method
	
	Method setActive()
		Local x:Int = mouse_X
		Local y:Int = mouse_Y
		If x &lt; viewWidth And y &lt; viewHeight
			x = x/tileWidth
			y = y/tileHeight
			inTileX = x
			inTileY = y
			Local i:Int = (offsety+inTileY)*tilesAcross+inTileX
			If i &lt; totalTiles activeIndex = i
		EndIf
	End Method
	
	Method getActive:Int()
		Return activeIndex
	End Method
	
	Method render()
		SetGraphics CanvasGraphics(canvas)
		SetBlend alphablend
		Cls()
		For Local y:Int = 0 Until tilesDown
			For Local x:Int = 0 Until tilesAcross
				Local i:Int = (y+offsety)*tilesAcross+x
				If i &gt;= totalTiles Exit
				DrawImage images,x*tileWidth,y*tileHeight,i
			Next
		Next
		If tileIndex &lt;&gt; -1
			Local x:Int = inTileX * tileWidth
			Local y:Int = inTiley * tileHeight
			SetColor 128,128,0
			DrawLine x,y,x+tileWidth,y
			SetColor 255,0,255
			DrawLine x,y+1,x+tileWIdth,y+1
			DrawLine x+tileWidth,y,x+tileWidth,y+tileHeight
			SetColor 128,128,0
			DrawLine x+tileWidth-1,y,x+tileWidth-1,y+tileHeight
			DrawLine x,y+tileHeight,x+tileWidth,y+tileHeight
			SetColor 255,0,255
			DrawLine x,y+tileHeight-1,x+tileWidth,y+tileHeight-1
			DrawLine x,y,x,y+tileHeight
			SetColor 128,128,0
			DrawLine x+1,y,x+1,y+tileHeight
			SetColor 255,255,255
		EndIf
		Flip()
	End Method

End Type

Type TGroup Extends TSprites
	Field canvas			:TGadget

	Function Create:Tgroup(canvas:TGadget Var)
		Local g:TGroup = New TGroup
		g.canvas = canvas
		Return g
	End Function
	
	Method update()
	
	End Method
	
	Method setActive()
		
	End Method
	
	Method getActive:Int()
		
	End Method
	
	Method render()
		SetGraphics CanvasGraphics(canvas)
		Cls()
		Flip()
	End Method

End Type


Type TSection
	Field width:Int
	Field height:Int
	Field canvas:TGadget
	Field canvasWidth:Int
	Field canvasHeight:Int
	
	Method Update:Int() Abstract
	Method Render:Int() Abstract
	Method setActive() Abstract
	Method getActive:Int() Abstract
End Type

Type TTiles Extends TSection
	Field sprites:TSingular
	Field objects:TGroup 
	Field currentTab:TSprites
	Field Panel:TGadget

	Field vSlider:TGadget
	Field voffset:Int
	
	Field inTilex:Int
	Field intiley:Int
	
	Const SLIDER_GIRTH:Int = 16
	
	
	Function Create:TTiles(tilesTabber:TGadget,tiles:TImage,obj:TImage)
		Local t:TTiles = New TTiles
		t.init(tilesTabber,tiles,obj)
		Return t
	End Function
	
	Method init(tabber:TGadget,tiles:TImage,obj:TImage)
		If canvas FreeGadget(canvas)
		If VSlider FreeGadget(VSlider)
		Local cw:Int = ClientWidth(Tabber)
		Local ch:Int = ClientHeight(Tabber)
		Local tsw:Int = (cw - SLIDER_GIRTH)/tiles.width*tiles.Width
		Local tsh:Int = ch/tiles.height*tiles.height
		Local sx:Int = (cw - (tsw+SLIDER_GIRTH)) / 2 
		Local sy:Int = 0
		canvas = CreateCanvas(sx,sy,tsw,tsh,Tabber)
		canvasWidth = GadgetWidth(canvas)
		canvasHeight = GadgetHeight(canvas)
		VSlider=CreateSlider(sx+canvasWidth,sy,SLIDER_GIRTH,canvasHeight,Tabber,SLIDER_VERTICAL)
		SetGadgetSensitivity(VSlider,EVENT_MOUSEMOVE|EVENT_MOUSEDOWN)
		sprites = TSingular.Create(tiles,canvas)
		SetSliderRange(VSlider,0,sprites.MaxOffsetY)
		objects = Tgroup.Create(canvas)
		currentTab = sprites
	End Method
	
	Method update:Int()
		currentTab.offsetY = vOffset
		currentTab.update()
	End Method
	
	Method SetActive()
		currentTab.setActive()
	End Method
	
	Method getActive:Int()
	
	End Method
	
	Method selectTab()
		Select EventData()
			Case 0
				currentTab = sprites
			Case 1
				currentTab = objects
		End Select
	End Method
	
	Method Render:Int()
		currentTab.render()
	End Method
End Type

Type TControl Extends TSection
	Field controlPanel			:TGadget
	Field vissibleLayerPanel	:TGadget
		
	Function Create:Tcontrol(mainWindow:TGadget,mainWidth:Int,mainHeight:Int)
		Local c:Tcontrol = New Tcontrol
		c.buildcontrolSection(mainWidth,mainHeight,mainWindow)
		Return c
	End Function
	
	Method buildControlSection(mainWidth:Int,mainHeight:Int,mainWindow:TGadget)
		controlPanel = CreatePanel(8,mainHeight/2,mainWidth/3-8,mainHeight/2,mainWindow,PANEL_GROUP)
		Local layerPanel:TGadget = CreatePanel(5,5,ClientWidth(controlPanel)-10,80,controlPanel,PANEL_GROUP)
		CreateLabel("Current Layer",20,5,200,20,layerPanel)
		CreateSlider(20,25,250,30,layerPanel,SLIDER_TRACKBAR)
		CreateLabel("0",300,30,33,22,layerPanel)
		vissibleLayerPanel = CreatePanel(5,85,ClientWidth(controlPanel)-10,50,controlPanel,PANEL_GROUP,"Fully Vissible Layers")
		buildLayersButtons(8,VissibleLayerPanel)
	End Method
	
	Method buildLayersButtons(numberOfButtons:Int,panel:TGadget)
		Local x:Int = 0,y:Int = 0
		For Local n:Int = 0 Until numberOfButtons
			CreateButton(n,5+x*48,5+y*25,60,20,Panel,BUTTON_CHECKBOX)
			x :+ 1
		Next
	End Method
	
	Method update()
	End Method
	
	Method Render:Int()
	End Method

	Method setActive()
	End Method

	Method getActive:Int()
	End Method

End Type

Type TMaze Extends TSection
	Field hSlider			:TGadget
	Field hOffset			:Int
	
	Field vSlider			:TGadget
	Field vOffset			:Int
	
	Field images			:TImage
	
	Field array			:Byte[][][]
	
	Field ViewAcross		:Int
	Field ViewDown		:Int
	
	Field tilesViewAcross	:Int
	Field tilesViewDown	:Int
	
	Field tileWidth		:Int
	Field tileHeight		:Int
	Field tileCount		:Int
	
	Field layerCount		:Int
	Field activeLayer		:Int
	
	Field tileX			:Int
	Field tileY			:Int
	
	Field posX			:Int
	Field posY			:Int
	
	Field mapWidth		:Int
	Field mapHeight		:Int
	
	
	Function Create:TMaze(mapPanel:TGadget,tiles:TImage,mapWidth:Int,mapHeight:Int,tileWidth:Int,tileHeight:Int,tileCount:Int,layerCount:Int)
		Local m:TMaze = New TMaze
		Local cw:Int = ClientWidth(mapPanel)
		Local ch:Int = ClientHeight(mapPanel)
		
		'figures out how much drawing area left after subtracting 16(slieder width).
		Local mw:Int = (cw-16)/tiles.width*tiles.width 
		Local mh:Int = (ch-16)/tiles.height*tiles.height
		
		'Maximum displayable tiles
		Local tw:Int = mw/tileWidth
		Local th:Int = mh/tileHeight
		If mapWidth &lt; tw Then  mw = mapWidth*tileWidth 
		If mapHeight &lt; th Then  mh = mapHeight*tileHeight
		Local sx:Int = (cw-(mw+16))/2
		Local sy:Int = (ch-(mh+16))/2

		m.canvas = CreateCanvas(sx,sy,mw,mh,mapPanel)
		
		m.canvasWidth = GadgetWidth(m.canvas)
		m.canvasHeight = GadgetHeight(m.canvas)
		
		m.mapWidth = mapWidth
		m.mapHeight = mapHeight
		
		m.tilesViewAcross = m.canvasWidth/tileWidth
		m.tilesViewDown = m.canvasHeight/tileHeight
		
		m.tileWidth = tileWidth
		m.tileHeight = tileHeight
		
		m.tileCount = tileCount
		
		m.layerCount = layerCount
		m.activeLayer = 0
		
		m.createMaze()
		
		m.vSlider=CreateSlider(sx+m.canvasWidth,sy,16,m.canvasHeight,mapPanel,SLIDER_VERTICAL)
		m.vslider=CreateSlider(sx,sy+m.canvasHeight,m.canvasWidth,16,mapPanel,SLIDER_HORIZONTAL)
		m.images = tiles
		m.tileWidth = tiles.width
		m.tileHeight = tiles.height
		m.ViewAcross = m.canvasWidth/m.tileWidth * m.tileWidth
		m.ViewDown = m.canvasHeight/m.tileHeight * m.tileHeight
		Return m
	End Function
	
	Method createMaze()
		array = array[..mapWidth]
		For Local i:Int = 0 Until array.length
			array[i] = array[i][..mapHeight]
		Next
		For Local y:Int = 0 Until mapHeight
			For Local x:Int = 0 Until mapWidth
				array[x][y] = array[x][y][..layerCount]
			Next
		Next	 
	End Method
	
	Method update:Int()
		Local x:Int = mouse_X
		Local y:Int = mouse_Y
		If x &lt; viewAcross And y &lt; viewDown
			posX = x/tileWidth
			posY = y/tileHeight
		EndIf
	End Method
	
	Method putTile(index:Int)
		array[posx][posy][activeLayer] = index
	End Method
	Method setActive()
	
	End Method
	
	Method getActive:Int()
	
	End Method
	
	Method Render()
		SetGraphics CanvasGraphics(canvas)
'******************* clock *******************************
		SetOrigin canvasWidth/2,canvasHeight/2
		SetLineWidth 5
		SetClsColor 0,0,0
		Cls()
		Local t:Int=MilliSecs()
		DrawLine 0,0,120*Cos(t),120*Sin(t)
		DrawLine 0,0,80*Cos(t/60),80*Sin(t/60)
		Local x:Int = posx * tileWidth
		Local y:Int = posy * tileHeight
'***************** done clock ****************************
		SetOrigin 0,0
		For Local z:Int = 0 Until layerCount
			For Local y:Int = 0 Until tilesViewDown
				For Local x:Int = 0 Until tilesViewAcross
					DrawImage images,x*tileWidth,y*tileHeight,array[x][y][z]
				Next
			Next
		Next
		SetLineWidth 1
		DrawLine x,y,x+tileWidth,y
		DrawLine x+tileWidth,y,x+tileWidth,y+tileHeight
		DrawLine x,y+tileHeight,x+tileWidth,y+tileHeight
		DrawLine x,y,x,y+tileHeight
		Flip()	
	End Method
End Type

Type Tmap
	Field tiles				:TTiles
	Field maze				:Tmaze
	Field control				:Tcontrol
	Field currentSection		:Tsection
	
	Field tilesTabber			:TGadget
	Field mapPanel			:TGadget

	Field tilesIndex			:Int
	
	Function Create:Tmap(mainWindow:TGadget,mainWIdth:Int,mainHeight:Int,..
					   mapWidth:Int,mapHeight:Int,layerCount:Int,collisionState:Int,sprites:String,tileWidth:Int,tileHeight:Int,tileCount:Int)
		Local m:Tmap = New Tmap
		m.buildTilesSection(mainWidth,mainHeight,mainWindow)
		m.buildMazeSection(mainWidth,mainHeight,MainWindow)
		m.tiles = TTiles.Create(m.tilesTabber,LoadAnimImage(sprites,tileWidth,tileHeight,0,tileCount),LoadAnimImage(sprites,tileWidth,tileHeight,0,tileCount))
		m.control = Tcontrol.Create(mainWindow,mainWidth,MainHeight)
		m.maze = Tmaze.Create(m.mapPanel,LoadAnimImage(sprites,tileWidth,tileHeight,0,tileCount),mapWidth,mapHeight,tileWidth,tileHeight,tileCount,layerCount)
		m.currentSection = m.maze
		AddHook EmitEventHook, eventSliderHook
		Return m
	End Function
	
	Method buildTilesSection(mainWidth:Int,mainHeight:Int,mainWindow:TGadget)
		tilesTabber = CreateTabber(4,0,mainWidth/3,mainHeight/2,mainWindow)
		SetGadgetLayout tilesTabber,1,1,1,1
		AddGadgetItem(tilesTabber,"Tiles",False,-1)
		AddGadgetItem(tilesTabber,"Tile Groups",False,-1)
		AddGadgetItem(tilesTabber,"Animated Objs",False,-1)
		
	End Method
	
	Method buildMazeSection(mainWidth:Int,mainHeight:Int,mainWindow:TGadget)
		mapPanel = CreatePanel(mainWidth/3,0,mainWidth/3*2,mainHeight,mainWindow,PANEL_GROUP,"Map")
		SetGadgetLayout mapPanel,1,1,1,1
	End Method
	
	Function eventSliderHook:Object(id:Int, data:Object, context:Object)
		
		Local event:TEvent = TEvent(data)
		If Not event Return data
		
		Select event.source
			Case app.map.maze.HSlider
				If event.ID = EVENT_MOUSEENTER
					app.map.activateMapSection()
					Return Null
				Else					
					app.map.maze.hOffset = SliderValue(app.map.maze.VSlider)
					app.map.update()
					app.map.render()
					Return Null
				EndIf
			Case app.map.maze.VSlider
				If event.ID = EVENT_MOUSEENTER
					app.map.activateMapSection()
					Return Null
				Else
					app.map.maze.VOffset = SliderValue(app.map.maze.VSlider)
					app.map.update()
					app.map.render()
					Return Null
				EndIf
			Case app.map.tiles.VSlider
				If event.ID = EVENT_MOUSEENTER
					app.map.activatetileSection
				Else
					app.map.tiles.VOffset = SliderValue(app.map.tiles.VSlider)
					app.map.update()
					app.map.render()
					Return Null			
				EndIf
		End Select
			
		Return data
	End Function

	Method process()
		Select EventID()
			Case EVENT_GADGETPAINT
				'RedrawGadget(activeSection.canvas)				
			Case EVENT_GADGETACTION
				Select EventSource() 
					Case mapPanel
						Print "MAP"
					Case tilesTabber
						selectTileTab()
					Default
						Print CurrentEvent.ToString() '"invalid button pressed"
				End Select
			Case EVENT_MOUSEENTER
				Select EventSource()
					Case Maze.canvas
						activateMapSection()
					Case tiles.canvas
						activateTileSection()
					Case tiles.VSlider
						activateTileSection()
					Case maze.HSlider
						activateMapSection()
					Case Maze.VSlider
						activateMapSection()
				End Select
			Case EVENT_MOUSEMOVE
				mouse_X = CurrentEvent.x
				mouse_Y = CurrentEvent.y
			Case EVENT_MOUSEDOWN
				setActive()
				If TTiles(currentSection)
					Local t:TTiles = TTiles(currentSection)
					If t
						Local s:Tsingular = TSingular(t.currentTab)
						If s 
							tilesIndex = s.getActive()
						EndIf
					EndIf
				ElseIf TMaze(CurrentSection)
					Local m:TMaze = Tmaze(CurrentSection)
					If m
						m.putTile(tilesIndex)
					EndIf 
				EndIf
			Default	
		End Select
		update()
		render()
		
	End Method

	Method activateMapSection()
		currentSection = maze	
	End Method
	
	Method activateTileSection()
		currentSection = tiles
	End Method
	
	Method selectTileTab()
		tiles.selectTab()
	End Method

	Method update()
	
		currentSection.update()
	End Method
	
	Method setActive()
		currentSection.setActive()
	End Method
	
	Method getActive:Int()
		Return currentSection.getActive()
	End Method
	
	Method cut()
	
	End Method
	
	Method copy()
	
	End Method
	
	
	Method paste()
	
	End Method
	
	
	Method undo()
	
	End Method
	
	Method redo()
	
	End Method
	
	
	
	Method Render()
		currentSection.render()
	End Method
	
End Type


Type TApplication
	
	Field mainWindow			:TGadget
	Field toolbar				:TGadget
	
	Field fileMenu			:TGadget
	Field editMenu			:TGadget
	Field helpMenu			:TGadget
		
	Field mainWidth			:Int
	Field mainHeight			:Int
	
	Field map					:Tmap
	
	Field newWindow			:TNewWindow


	Const MENU_FILE			:Int = 100
	Const MENU_NEW			:Int = 120 
	Const MENU_OPEN			:Int = 130
	Const MENU_SAVE			:Int = 140
	Const MENU_CLOSE			:Int = 150
	Const MENU_EXIT			:Int = 160
	
	Const MENU_EDIT			:Int = 200
	Const MENU_CUT			:Int = 210
	Const MENU_COPY			:Int = 220
	Const MENU_PASTE			:Int = 230
	
	Const MENU_UNDO			:Int = 240
	Const MENU_REDO			:Int = 250
	
	Const MENU_HELP			:Int = 300
	Const MENU_ABOUT			:Int = 310
	
	
	Function Create:TApplication()
		Local a:TApplication = New TApplication
		a.buildMainWindow()
		a.buildFileMenu()
		a.buildEditMenu()
		a.buildHelpMenu()
		
		a.createNewWindow()
		
		Return a
	End Function

	Method buildMainWindow()
		MainWindow = CreateWindow("Tile Map Editor",0,0,DesktopWidth(),DesktopHeight(),Null,WINDOW_TITLEBAR | WINDOW_RESIZABLE | WINDOW_MENU | WINDOW_STATUS)
		toolbar = CreateToolbar("gfx\icons.png",0,0,24,24,mainWindow)
		SetToolbarTips(toolbar,["New","Open","Close","Save","","Cut","Copy","Paste","","Undo","Redo"])
		mainWidth = ClientWidth(mainWindow)
		mainHeight = ClientHeight(mainWindow)
		newWindow = TNewWindow.Create(200,200,mainWindow)
	End Method
	
	Method createNewWindow()
		Local w:Int = 500
		Local h:Int = 400
		Local x:Int = (DesktopWidth()-w)/2
		Local y:Int = (DesktopHeight()-h)/2
		
	End Method

	Method buildFileMenu()
		fileMenu = CreateMenu("&amp;File",MENU_FILE,mainWindow,KEY_F,MODIFIER_COMMAND)
		CreateMenu("New",MENU_NEW,fileMenu,KEY_N,MODIFIER_COMMAND)
		CreateMenu("Open",MENU_OPEN,fileMenu,KEY_O,MODIFIER_COMMAND)
		CreateMenu("Save",MENU_SAVE,fileMenu,KEY_S,MODIFIER_COMMAND)
		CreateMenu("Close",MENU_CLOSE,fileMenu,KEY_W,MODIFIER_COMMAND)
		CreateMenu("",0,fileMenu)
		CreateMenu("Quit",MENU_EXIT,fileMenu,KEY_Q,MODIFIER_COMMAND)
	End Method
	
	Method buildEditMenu()
		editMenu = CreateMenu("&amp;Edit",MENU_EDIT,mainWindow,KEY_E,MODIFIER_COMMAND)
		CreateMenu("Cut",MENU_CUT,editMenu,KEY_X,MODIFIER_COMMAND)
		CreateMenu("Copy",MENU_COPY,editMenu,KEY_C,MODIFIER_COMMAND)
		CreateMenu("Paste",MENU_PASTE,editMenu,KEY_V,MODIFIER_COMMAND)
		CreateMenu("",0,editMenu)
		CreateMenu("Undo",MENU_UNDO,editMenu,KEY_Z,MODIFIER_COMMAND)
		CreateMenu("Redo",MENU_REDO,editMenu,KEY_Z,MODIFIER_COMMAND|MODIFIER_SHIFT)
	End Method
	
	Method buildHelpMenu()
		helpMenu = CreateMenu("&amp;Help",MENU_HELP,mainWindow,KEY_H,MODIFIER_COMMAND)
		CreateMenu("About",MENU_ABOUT,helpMenu,KEY_A,MODIFIER_COMMAND)
	End Method
	
	Method process()

		WaitEvent()
		Select EventID()
			Case EVENT_WINDOWCLOSE,EVENT_APPTERMINATE
				End
			Case EVENT_MENUACTION
				Select EventData()
					Case MENU_NEW
						processNewMap()
					Case MENU_OPEN
					Case MENU_SAVE
					Case MENU_CLOSE
					Case MENU_EXIT
						quit()
					Case MENU_CUT
						map.cut()
					Case MENU_COPY
						map.copy()
					Case MENU_PASTE
						map.paste()
					Case MENU_UNDO
						map.undo()
					Case MENU_REDO
						map.redo()
					Case MENU_HELP
					Case MENU_ABOUT
					Default
				End Select
			Case EVENT_GADGETACTION
				Select EventSource()
					Case toolbar
						Select EventData()
							Case 0
								processNewMap()
							Case 1
							
							Default
						End Select
					Default
				End Select
			Default
		End Select
		If map map.process()		
	
	End Method
	
	Method processNewMap()
		If map = Null
			If newWindow.process()
				Local mapWidth:Int = newWindow.getMapWidth()
				Local mapHeight:Int = newWindow.getMapHeight()
				Local layerCount:Int = newWindow.getNumberOfLayers()
				Local collisionState:Int = newWindow.getCollisionLayerState()
				Local sprites:String = newWindow.getSpritesName()
				Local Dir:String = newWindow.getDirectory()
				Local tileWidth:Int = newWindow.getTilesWidth()
				Local tileHeight:Int = newWindow.getTilesHeight()
				Local tileCount:Int = newWindow.getTilesCount()
				?Not Win32
					sprites = dir+ "/"+sprites
				?Win32
					sprites = dir+ "\"+sprites
				?
				buildMap(mapWidth,mapHeight,layerCount,collisionState,sprites,tileWidth,tileHeight,tileCount)
			EndIf
		Else
			'request To Delete Or save old map
		EndIf	
	EndMethod
	
	Method buildMap(mapWidth:Int,mapHeight:Int,layerCount:Int,collisionState:Int,sprites:String,tileWidth:Int,tileHeight:Int,tileCount:Int)
		map = Tmap.Create(mainWindow,mainWidth,mainHeight,mapWidth,mapHeight,LayerCount,collisionState,sprites,tileWidth,tileHeight,tileCount)
		map.tiles.render()
		map.maze.render()	
	End Method
	
	Method openMap()
	
	End Method
	
	Method saveMap()
	
	End Method
	
	Method close()
	
	End Method
	
	Method quit()
		If map = Null
			End
		Else
			Notify "exit action with an exiting map created 'not' supported yet"
		End If	
	End Method
	
End Type



Global app:Tapplication = Tapplication.Create()
Repeat
	app.process()
Forever

</textarea><br><br>"newMapGui.bmx"<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Type TNewWindow
	Field window				:TGadget
	Field posx				:Int
	Field posy				:Int
	
	Field width				:Int
	Field Height				:Int
	
	Field mapWidth			:Int
	Field mapHeight			:Int
	Field layers				:Int
	Field collisionLayer		:Int
	
	Field tiles				:TImage
	Field tileWidth			:Int
	Field tileHeight			:Int
	Field numberOfTiles		:Int
	Field spritesName			:String
	Field spritesDir			:String
	Field spritesFilter		:String
	
	Field mapPanel			:TGadget
	Field MapWidthTextArea		:TGadget
	Field HeightMapTextArea	:TGadget
	Field layerCountTextArea	:TGadget
	
	Field FileNameTextArea		:TGadget
	Field fileNameFindButton	:TGadget
	Field widthTileTextArea	:TGadget
	Field heightTileTextArea	:TGadget
	Field countTileTextArea	:TGadget
	Field collisionLayerButton	:TGadget
	
	Field responcePanel		:TGadget
	Field newButton			:TGadget
	Field cancelButton			:TGadget
	
	
	Const MIN_TILE_WIDTH		:Int = 4
	Const MIN_TILE_HEIGHT		:Int = 4
	
	Const MAX_TILE_WIDTH		:Int = 256
	Const MAX_TILE_HEIGHT		:Int = 256
	
	Const MIN_NUMBER_LAYERS	:Int = 1
	Const MAX_NUMBER_LAYERS	:Int = 6
	
	Const MIN_MAP_WIDTH		:Int = 6
	Const MAX_MAP_WIDTH		:Int = 999	
	
	Const MIN_MAP_HEIGHT		:Int = 6
	Const MAX_MAP_HEIGHT		:Int = 999
	
	Const MIN_TILES_COUNT 		:Int = 2
	Const MAX_TILES_COUNT		:Int = 256
	
	Function Create:TNewWindow(x:Int,y:Int,parentWindow:TGadget)
		Local win:TnewWindow = New TnewWindow
		win.posx = x
		win.posy = y
		win.width = 500
		win.height = 400
		win.window = CreateWindow("New Map", x, y,win.width,win.height, parentWindow,WINDOW_TITLEBAR)
		win.makeTextFields()
		win.makeButtons()
		win.spritesdir = CurrentDir()
		win.spritesFilter = "Image Files:png,jpg,bmp"
		HideGadget(win.window)
		Return win
	End Function
	
	Method show()
		ShowGadget(window)
	End Method
	
	Method hide()
		HideGadget(window)
	End Method
	
	Method getMapWidth:Int()
		Return Int(TextFieldText(mapWidthTextArea))
	End Method
	
	Method getMapHeight:Int()
		Return Int(TextFieldText(heightMapTextArea))
	End Method
	
	Method getNumberOfLayers:Int()
		Return Int(TextFieldText(layerCountTextArea))
	End Method
	
	Method getCollisionLayerState:Int()
		Return ButtonState(collisionLayerButton)
	End Method
	
	Method getDirectory:String()
		Return spritesDir
	End Method
	Method getSpritesName:String()
		Return spritesName
	End Method
	
	Method getTilesWidth:Int()
		Return Int(TextFieldText(widthTileTextArea))	
	End Method
	
	Method getTilesHeight:Int()
		Return Int(TextFieldText(heightTileTextArea))	
	End Method
	
	Method getTilesCount:Int()
		Return Int(TextFieldText(countTileTextArea))
	End Method
	
	Method Process:Int()
		ShowGadget(window)
		Local state:Int = False
		Repeat
			WaitEvent()
			Select EventID()
				Case EVENT_APPTERMINATE,EVENT_WINDOWCLOSE
					Return False
				Case EVENT_GADGETACTION
					Select EventSource()
						Case MapWidthTextArea
							filterNumerical(mapWidthTextArea,4)
						Case heightMapTextArea
							filterNumerical(heightMapTextArea,4)
						Case layerCountTextArea
							filterNumerical(layerCountTextArea,4)
						Case widthTileTextArea
							filterNumerical(widthTileTextArea,3)
						Case heightTileTextArea
							filterNumerical(heightTileTextArea,3)
						Case countTileTextArea
							filterNumerical(countTileTextArea,3)
						Case cancelButton
							Exit
						Case newButton
							If mapConditionsAcceptable()
								state = True
								Exit
							Else
								
							EndIf
						Case fileNameFindButton
							spritesName = RequestFile("Load Sprite Sheet",spritesFilter,Null,spritesDir)
							If spritesName
								spritesDir = ExtractDir(spritesName)
								spritesName = StripDir(spritesName)
								SetGadgetText(FileNameTextArea,spritesName)
							EndIf
'							Print spritesDir
'						Case collisionLayerButton
'							Print ButtonState(collisionlayerButton)
'						Default
'							Print CurrentEvent.ToString()
					End Select
				Default
					Print CurrentEvent.ToString()
			EndSelect
		Forever
		HideGadget(window)
		Return state
	End Method
	
	Method filterNumerical(textField:TGadget,length:Int)
		Local s:String = TextFieldText(textfield)
		If s.length &gt; 0 And s.length &lt;= length
			If s[s.length-1] &lt; Asc("0") Or s[s.length-1] &gt; Asc("9")
				s = s[..s.length-1]
				SetGadgetText(textField,s)
			EndIf
		ElseIf s.length &gt; length
			s = s[..length]
			SetGadgetText(textField,s)
		EndIf
	End Method
	
	Method mapConditionsAcceptable:Int()
		
		Local width:Int= getMapWidth()
		If width &lt; MIN_MAP_WIDTH
			Notify"Invalid Map width! ~nMinimum Map width is "+ MIN_MAP_WIDTH
			Return False
		ElseIf width &gt; MAX_MAP_WIDTH
			Notify "Invalid Map height! ~nMaximum Map Width is "+MAX_MAP_WIDTH
			Return False
		EndIf
		
		Local height:Int = getMapHeight()
		If height &lt; MIN_MAP_HEIGHT
			Notify"Invalid Map Height! ~nMinimum Map height is "+MIN_MAP_HEIGHT
			Return False
		ElseIf height &gt; MAX_MAP_HEIGHT
			Notify"Invalid Map Height! ~nMaximum Map Height is "+MAX_MAP_HEIGHT
			Return False
		EndIf
		
		Local count:Int = getNumberOfLayers()
		If count &lt; MIN_NUMBER_LAYERS
			Notify"Invalid number of layers ~nMinimum Numbers is "+MIN_NUMBER_LAYERS
			Return False
		ElseIf count &gt; MAX_NUMBER_LAYERS
			Notify"Invalid number of layers ~nMaximum Number is "+MAX_NUMBER_LAYERS
			Return False
		EndIf
		?Win32
			Local t:String = "\"
		?Not Win32
			Local t:String = "/"
		?
		Local name:String = spritesDir+t+getSpritesName()
		If FileSize(name) = -1
			Notify "Invalid Sprites Tiles! ~nFile not found"
		EndIf
		
		width = getTilesWidth()
		If width &lt; MIN_TILE_WIDTH
			Notify"Invalid Tile width! ~nMinimum tile width is "+MIN_TILE_WIDTH
			Return False
		ElseIf width &gt; MAX_TILE_WIDTH
			Notify"Invalid tile weight! ~nMaximum tile Width is "+MAX_TILE_WIDTH
			Return False
		EndIf	
		
		height = getTilesHeight()
		If height &lt; MIN_TILE_HEIGHT
			Notify"Invalid tile height! ~nMinimum tile height is "+MIN_TILE_HEIGHT
			Return False
		ElseIf Height &gt; MAX_TILE_HEIGHT
			Notify"Invalid tile height! ~nMaximum tile height is "+MAX_TILE_HEIGHT
			Return False
		EndIf
		
		count = getTilesCount()
		If count &lt; MIN_TILES_COUNT
			Notify"Invalid number of tiles! ~nThe minimum number of tiles is "+MIN_TILES_COUNT 
			Return False
		ElseIf count &gt; MAX_TILES_COUNT
			Notify"Invalid number of tiles! ~nThe maximum number of tiles is "+MAX_TILES_COUNT
			Return False
		EndIf
		Return True
	End Method		
	
	Method makeTextFields()
		mapPanel = CreatePanel(0,0,ClientWidth(window),340,window,PANEL_GROUP)
		
		CreateLabel("Map Width",50,20,100,20,mapPanel)
		MapWidthTextArea = CreateTextField(285,20,38,22,mapPanel)
		
		CreateLabel("Map Height",50,50,100,20,mappanel)
		heightMapTextArea = CreateTextField(285,50,38,22,mapPanel)
		
		CreateLabel("Number Of Layers",50,80,150,20,mapPanel)
		layerCountTextArea = CreateTextField(285,80,38,22,mapPanel)
		
		collisionLayerButton = CreateButton("Add Collision Layer",100,120,200,30,mapPanel, BUTTON_CHECKBOX)
		
		CreateLabel("Sprite Tile Set",50,180,100,20,mapPanel)
		fileNameTextArea = CreateTextField(170,180,155,22,mapPanel)
		fileNameFindButton = CreateButton("Find",338,180,60,22,mapPanel)
		
		CreateLabel("Tile Width",50,210,100,20,mapPanel)
		widthTileTextArea = CreateTextField(293,210,30,22,mapPanel)
		
		CreateLabel("Tile Height",50,240,100,20,mapPanel)
		heightTileTextArea = CreateTextField(293,240,30,22,mapPanel)
		
		CreateLabel("Number Of Tiles",50,270,100,20,mapPanel)
		countTileTextArea = CreateTextField(293,270,30,22,mapPanel)
	
	End Method
	
	Method makeButtons()
		responcePanel = CreatePanel(-5,325,ClientWidth(window)+10,55,window,PANEL_GROUP)
		cancelButton = CreateButton("Cancel",320,5,80,30,responcePanel)
		NewButton = CreateButton("New",400,5,80,30,responcePanel)
	End Method
	
End Type

</textarea><br><br>can use the graphics in this post: <a href="http://www.blitzmax.com/Community/editpost.php?post=1117659" target="_blank">http://www.blitzmax.com/Community/editpost.php?post=1117659</a><br><br>You or others might find something that might help me also( don't have too).<br><br><font class="tiny">Last edited 2011</font><br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1118463"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jsp</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Does freegadget automatically call any child gadgets to free them correctly too? <br></div><br>Yes <br><br></td></tr></table><br>
<a name="1118507"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks jsp. All clear now.<br><br>Thanks for posting that code Jesse. As a gui newbie, all variations of how to get it done are a great help for the learning curve.<br><br>Cheers all. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
