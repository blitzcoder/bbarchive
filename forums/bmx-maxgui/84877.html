<!DOCTYPE html><html lang="en" ><head ><title >Active panel with a label</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Active panel with a label</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >Active panel with a label</a><br><br>
<a name="959304"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am having some success with a property grid control using MaxGUI.  I am using an active panel for the category headers.  When the user clicks on the header, it shows or hides the category properties.<br><img src="http://www.leadwerks.com/post/grid.jpg"><br><br>However, the label on the header covers up a section of the panel, and if the user clicks on this nothing happens.  In theory I could use an image for the text, but that is a pretty horrific work-around.  Any ideas?  You can just run this code without the two bitmaps it uses, and it will work:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import MaxGui.Drivers

Strict 

Local window:TGadget

window=CreateWindow("My Window",40,40,640,480)

Local grid:TGrid=TGrid.Create(0,0,,,window)
SetGadgetLayout grid,1,1,1,1
Local gs:TGridSection=grid.addsection("Matrix")
gs.additem("Position:")
gs.additem("Rotation:")
gs.additem("Scale:")

gs=grid.addsection("Settings")
gs.additem("Color:")
gs.additem("Material:")
gs.additem("Intensity:")
gs.additem("Health:")


While True
	WaitEvent 
	Select EventID()
		Case EVENT_WINDOWCLOSE
			End
	EndSelect
Wend



Type TGrid Extends TProxyGadget
	
	Const headerheight:Int=20
	Const headerspacer:Int=1
	
	Field panel:TGadget
	Field sections:TList=New TList
	
	Global list:TList=New TList
	
	Function Create:TGrid(x=0,y=0,width=-1,height=-1,group:TGadget)
		Local grid:TGrid=New tgrid
		If width=-1 width=group.ClientWidth()-x
		If height=-1 height=group.ClientHeight()-y
		grid.panel=CreatePanel(x,y,width,height,group)
		grid.SetProxy(grid.panel)
		AddHook EmitEventHook, eventHandler, Null, 1
		list.addlast(grid)
		Return grid
	EndFunction

	Method AddSection:TGridSection(name:String)
		Local gs:TGridSection=New TGridSection
		gs.header=CreatePanel(0,0,panel.ClientWidth(),20,panel,PANEL_ACTIVE)
		SetGadgetLayout gs.header,1,1,1,0
		SetPanelColor gs.header,128,128,128
		Local label:TGadget
		label=CreateLabel(name,headerheight,2,80,20,gs.header)
		SetGadgetColor label,255,255,255,0
		SetGadgetLayout label,1,0,1,0
		gs.panel=CreatePanel(0,20,panel.ClientWidth(),2,panel)
		SetGadgetLayout gs.panel,1,1,1,0
		gs.grid=Self
		sections.addlast(gs)

		gs.imagepanel=CreatePanel(0,0,headerheight,headerheight,gs.header,PANEL_ACTIVE)		
		SetGadgetPixmap gs.imagepanel,LoadPixmap("header.bmp")
		SetGadgetLayout gs.imagepanel,1,0,1,0
		SetPanelColor gs.imagepanel,128,128,128
		
		AdjustLayout()
		Return gs
	EndMethod
	
	Method AdjustLayout()
		Local x:Int
		Local y:Int
		For Local gs:TGridSection=EachIn sections
			SetGadgetShape gs.header,0,y,panel.ClientWidth(),headerheight
			If gs.hidden
				SetGadgetPixmap gs.imagepanel,LoadPixmap("header.bmp")
				HideGadget gs.panel
				y:+headerheight+headerspacer
			Else
				SetGadgetPixmap gs.imagepanel,LoadPixmap("header2.bmp")
				SetGadgetShape gs.panel,0,y+headerheight,panel.ClientWidth(),gs.y
				ShowGadget gs.panel
				y:+headerheight+gs.y
			EndIf
		Next
	EndMethod
	
	Function eventHandler:Object( pID%, pData:Object, pContext:Object )
		Local event:TEvent = TEvent(pData)
		
		If event
			Local grid:TGrid
			Local gs:TGridSection
			For grid=EachIn TGrid.list
			For gs=EachIn grid.sections
				If gs.header=event.source Or gs.imagepanel=event.source
					Return gs.EventHook(event)
				EndIf
			Next
			Next
		EndIf
		
		Return pData
	EndFunction
	
EndType

Type TGridSection

	Const indent:Int=20

	Field grid:TGrid
	Field imagepanel:TGadget
	Field header:TGadget
	Field panel:TGadget
	Field x:Int=8
	Field y:Int=2
	Field h:Int=2
	Field hidden:Int

	Method EventHook:TEvent( event:TEvent )
		Select event.id
			Case EVENT_MOUSEUP
				toggle()
				Return Null
		EndSelect
		Return event
	EndMethod

	Method Toggle()
		If hidden show() Else hide()
	EndMethod
	
	Method Hide()
		hidden=True
		grid.AdjustLayout()
	EndMethod
	
	Method Show()
		hidden=False
		grid.AdjustLayout()
	EndMethod
	
	Method AddItem(name:String,style:Int=0)
		Local g:TGadget
		g=CreateLabel(name,indent+x,y,60,20,panel)
		SetGadgetLayout g,1,0,1,0
		g=CreateTextField(indent+x+60,y,panel.ClientWidth()-x-60-2,20,panel)
		SetGadgetLayout g,1,1,1,0
		y:+22
		'SetGadgetShape panel,0,20,GadgetGroup(panel).ClientWidth(),y
		grid.AdjustLayout()
	EndMethod	
EndType</textarea> <br><br></td></tr></table><br>
<a name="959307"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey,<br><br>This is looking very nice!<br><br>Have you tried calling <b>SetGadgetSensitivity(</b> label, SENSITIZE_MOUSE <b>)</b> on the label, so that it emits mouse events?<br><br>The following appears to work as might be expected:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Import MaxGui.Drivers

Strict 

Local window:TGadget

window=CreateWindow("My Window",40,40,640,480)

Local grid:TGrid=TGrid.Create(0,0,,,window)
SetGadgetLayout grid,EDGE_ALIGNED,EDGE_ALIGNED,EDGE_ALIGNED,EDGE_ALIGNED
Local gs:TGridSection=grid.addsection("Matrix")
gs.additem("Position:")
gs.additem("Rotation:")
gs.additem("Scale:")

gs=grid.addsection("Settings")
gs.additem("Color:")
gs.additem("Material:")
gs.additem("Intensity:")
gs.additem("Health:")


While True
	WaitEvent 
	Select EventID()
		Case EVENT_WINDOWCLOSE
			End
	EndSelect
Wend



Type TGrid Extends TProxyGadget
	
	Const headerheight:Int=20
	Const headerspacer:Int=1
	
	Field panel:TGadget
	Field sections:TList=New TList
	
	Global list:TList=New TList
	
	Function Create:TGrid(x=0,y=0,width=-1,height=-1,group:TGadget)
		Local grid:TGrid=New tgrid
		If width=-1 width=group.ClientWidth()-x
		If height=-1 height=group.ClientHeight()-y
		grid.panel=CreatePanel(x,y,width,height,group)
		grid.SetProxy(grid.panel)
		AddHook EmitEventHook, eventHandler, Null, 1
		list.addlast(grid)
		Return grid
	EndFunction

	Method AddSection:TGridSection(name:String)
		Local gs:TGridSection=New TGridSection
		gs.header=CreatePanel(0,0,panel.ClientWidth(),20,panel,PANEL_ACTIVE)
		SetGadgetLayout gs.header,EDGE_ALIGNED,EDGE_ALIGNED,EDGE_ALIGNED,0
		SetPanelColor gs.header,128,128,128
		
		gs.label=CreateLabel(name,headerheight,2,80,20,gs.header)
		SetGadgetColor gs.label,255,255,255,0
		SetGadgetLayout gs.label,EDGE_ALIGNED,0,EDGE_ALIGNED,0
		SetGadgetSensitivity gs.label, SENSITIZE_MOUSE
		
		gs.panel=CreatePanel(0,20,panel.ClientWidth(),2,panel)
		SetGadgetLayout gs.panel,EDGE_ALIGNED,EDGE_ALIGNED,EDGE_ALIGNED,0
		gs.grid=Self
		sections.addlast(gs)

		gs.imagepanel=CreatePanel(0,0,headerheight,headerheight,gs.header,PANEL_ACTIVE)		
		SetGadgetPixmap gs.imagepanel,LoadPixmap("header.bmp")
		SetGadgetLayout gs.imagepanel,EDGE_ALIGNED,0,EDGE_ALIGNED,0
		SetPanelColor gs.imagepanel,128,128,128
		
		AdjustLayout()
		Return gs
	EndMethod
	
	Method AdjustLayout()
		Local x:Int
		Local y:Int
		For Local gs:TGridSection=EachIn sections
			SetGadgetShape gs.header,0,y,panel.ClientWidth(),headerheight
			If gs.hidden
				SetGadgetPixmap gs.imagepanel,LoadPixmap("header.bmp")
				HideGadget gs.panel
				y:+headerheight+headerspacer
			Else
				SetGadgetPixmap gs.imagepanel,LoadPixmap("header2.bmp")
				SetGadgetShape gs.panel,0,y+headerheight,panel.ClientWidth(),gs.y
				ShowGadget gs.panel
				y:+headerheight+gs.y
			EndIf
		Next
	EndMethod
	
	Function eventHandler:Object( pID%, pData:Object, pContext:Object )
		Local event:TEvent = TEvent(pData)
		
		If event
			Local grid:TGrid
			Local gs:TGridSection
			For grid=EachIn TGrid.list
			For gs=EachIn grid.sections
				Select event.source
					Case gs.header, gs.imagepanel, gs.label
						Return gs.EventHook(event)
				EndSelect
			Next
			Next
		EndIf
		
		Return pData
	EndFunction
	
EndType

Type TGridSection

	Const indent:Int=20

	Field grid:TGrid
	Field imagepanel:TGadget
	Field header:TGadget
	Field panel:TGadget
	Field label:TGadget
	
	Field x:Int=8
	Field y:Int=2
	Field h:Int=2
	Field hidden:Int

	Method EventHook:TEvent( event:TEvent )
		Select event.id
			Case EVENT_MOUSEUP
				toggle()
				Return Null
		EndSelect
		Return event
	EndMethod

	Method Toggle()
		If hidden show() Else hide()
	EndMethod
	
	Method Hide()
		hidden=True
		grid.AdjustLayout()
	EndMethod
	
	Method Show()
		hidden=False
		grid.AdjustLayout()
	EndMethod
	
	Method AddItem(name:String,style:Int=0)
		Local g:TGadget
		g=CreateLabel(name,indent+x,y+2,60,20,panel)
		SetGadgetLayout g,EDGE_ALIGNED,0,EDGE_ALIGNED,0
		g=CreateTextField(indent+x+60,y,panel.ClientWidth()-x-60-2-indent,20,panel)
		SetGadgetLayout g,EDGE_ALIGNED,EDGE_ALIGNED,EDGE_ALIGNED,0
		y:+22
		'SetGadgetShape panel,0,20,GadgetGroup(panel).ClientWidth(),y
		grid.AdjustLayout()
	EndMethod	
EndType</textarea><br><br>I cleaned up a minor thing whereby the text-fields didn't take into account the indentation, and swapped a few literals for their equivalent constants, but it is effectively the same code as before.<br><br>Hope this helps...<br><br>Seb <br><br></td></tr></table><br>
<a name="959309"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Use SetGadgetSensitivity( gadget:TGadget, flags )<br>...<br>edit: too late... <br><br></td></tr></table><br>
<a name="959310"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks!<br><br>It may take a few days to finish this, but it will be really good when I post it. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
