<!DOCTYPE html><html lang="en" ><head ><title >Draggable window - Ghosting issue!</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Draggable window - Ghosting issue!</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >Draggable window - Ghosting issue!</a><br><br>
<a name="1341008"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Scaremonger</td><td align="right"><font class=tiny>(Posted May)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br>I'm trying to create a draggable window that doesn't have a titlebar. The code below sort of works, but I get a ghosting effect where the mouse seems to jump when drag it!<br><br>Run the following code, click and hold the mouse button and drag the window around the screen. The more you move it, the more the ghosting occurs!<br><br>RIGHT-CLICK to quit! <br><br>I've only tested this on Windows so far.<br><br>Any suggestions?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
Import Maxgui.drivers

Global window:TGadget = CreateWindow( "Window", 64,64, 128,256, Null, 0 )
Global canvas:TGadget=CreateCanvas(0,0,GadgetWidth(window),GadgetHeight(window),window)
SetGadgetLayout(canvas,1,1,1,1)

'# CREATE A REDRAW TIMER
CreateTimer 60

'# CREATE A POPUP MENU
Global menu:Tgadget = CreateMenu( "popup", 0, Null )
CreateMenu( "Quit", 199, menu )

'# EVENT HOOK
AddHook EmitEventHook, window_hook

'# DRAG AND DROP
Global Drag:Int = False
Global drag_MX:Int = 0
Global drag_MY:Int = 0

Repeat
	Select WaitEvent()
		Case EVENT_WINDOWCLOSE, EVENT_APPTERMINATE
			FreeGadget canvas
			End
	EndSelect
Forever

Function Redraw()
	SetGraphics CanvasGraphics(canvas)
	'# Fill window with Canvas
	SetViewport 0,0, GadgetWidth(window),GadgetHeight(window)
	SetOrigin GadgetWidth(canvas)/2,GadgetHeight(canvas)/2
	'#
	SetLineWidth 5
	Cls
	Local t:Float=MilliSecs()
	DrawLine 0,0,120*Cos(t),120*Sin(t)
	DrawLine 0,0,80*Cos(t/60),80*Sin(t/60)
	Flip
End Function

Function window_hook:Object( id:Int, data:Object, context:Object)
	Local ev:TEvent = TEvent( data)
	
	If ev Then
		Select ev.ID 
		Case EVENT_TIMERTICK
			RedrawGadget canvas
		Case EVENT_GADGETPAINT
			Redraw()
		Case EVENT_MENUACTION
			If ev.data=199 Then '# QUIT
				FreeGadget( canvas )
				End
			End If
		Case EVENT_MOUSEDOWN
			Select ev.data
			Case 1	'# LEFT MOUSE BUTTON
				drag = True
				'# Set offset of mouse from window
				drag_MX = ev.x - GadgetX(window)
				drag_MY = ev.y - GadgetY(window)
Print "DRAG START"
				Return Null
			Case 2	'# RIGHT MOUSE BUTTON
				PopupWindowMenu window, menu
				Return Null
			End Select
		Case EVENT_MOUSEUP
			Select ev.data
			Case 1	'# LEFT MOUSE BUTTON
				If drag Then drag = False
Print "DRAG STOP"
				Return Null
			End Select
		Case EVENT_MOUSEMOVE
			Local wx%, wy%

			If drag And ev.source=canvas Then
				'# Calculate where mouse is
				wx = ev.x - drag_MX
				wy = ev.y - drag_MY
Print "DRAG TO: "+wx+","+wy
				SetGadgetShape( window, wx, wy, GadgetWidth(window), GadgetHeight(window) )
			End If

		End Select
	EndIf
	Return data
EndFunction
</textarea> <br><br></td></tr></table><br>
<a name="1341013"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >xlsior</td><td align="right"><font class=tiny>(Posted May)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> FWIW, when I drag the window, the lage severely behind the mouse (e.g. the mouse travels the entire width of the screen, your window maybe 1/4th of the way)<br><br>The biggest problem here is that normally, windows suspends the drawing of the windows contents while you move the window. I think that whenever you're forcing it to redraw, windows isn't able to move the window at the same time, so it starts lagging behind.<br><br>Not sure what to do about that... <br><br></td></tr></table><br>
<a name="1341026"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>(Posted May)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> This kind of works in FLTK:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
Import Maxgui.drivers

Global window:TGadget = CreateWindow( "Window", 64,64, 128,256, Null, 0 )
Global canvas:TGadget=CreateCanvas(0,0,GadgetWidth(window),GadgetHeight(window),window)
SetGadgetLayout(canvas,1,1,1,1)

'# CREATE A REDRAW TIMER
CreateTimer 60

'# CREATE A POPUP MENU
Global menu:Tgadget = CreateMenu( "popup", 0, Null )
CreateMenu( "Quit", 199, menu )

'# EVENT HOOK
AddHook EmitEventHook, window_hook

'# DRAG AND DROP
Global Drag:Int = False
Global drag_MX:Int = 0
Global drag_MY:Int = 0

Repeat
	Select WaitEvent()
		Case EVENT_WINDOWCLOSE, EVENT_APPTERMINATE
			FreeGadget canvas
			End
	EndSelect
Forever

Function Redraw()
	SetGraphics CanvasGraphics(canvas)
	'# Fill window with Canvas
	SetViewport 0,0, GadgetWidth(window),GadgetHeight(window)
	SetOrigin GadgetWidth(canvas)/2,GadgetHeight(canvas)/2
	'#
	SetLineWidth 5
	Cls
	Local t:Float=MilliSecs()
	DrawLine 0,0,120*Cos(t),120*Sin(t)
	DrawLine 0,0,80*Cos(t/60),80*Sin(t/60)
	Flip
End Function

Function window_hook:Object( id:Int, data:Object, context:Object)
	Local ev:TEvent = TEvent( data)
	
	If ev Then
		Select ev.ID 
		Case EVENT_TIMERTICK
			RedrawGadget canvas
		Case EVENT_GADGETPAINT
			Redraw()
		Case EVENT_MENUACTION
			If ev.data=199 Then '# QUIT
				FreeGadget( canvas )
				End
			End If
		Case EVENT_MOUSEDOWN
			Select ev.data
			Case 1	'# LEFT MOUSE BUTTON
				drag = True
				'# Set offset of mouse from window
				drag_MX = ev.x 
				drag_MY = ev.y
Print "DRAG START"
				Return Null
			Case 2	'# RIGHT MOUSE BUTTON
				PopupWindowMenu window, menu
				Return Null
			End Select
		Case EVENT_MOUSEUP
			Select ev.data
			Case 1	'# LEFT MOUSE BUTTON
				If drag Then drag = False
Print "DRAG STOP"
				Return Null
			End Select
		Case EVENT_MOUSEMOVE
			Local wx%, wy%

			If drag And ev.source=canvas Then
				'# Calculate where mouse is
				wx = GadgetX(window) + ev.x - drag_MX
				wy = GadgetY(window) + ev.y - drag_MY
Print "DRAG TO: "+wx+","+wy
				SetGadgetShape( window, wx, wy, GadgetWidth(window), GadgetHeight(window) )
			End If

		End Select
	EndIf
	Return data
EndFunction

</textarea> <br><br></td></tr></table><br>
<a name="1341090"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Scaremonger</td><td align="right"><font class=tiny>(Posted May)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> @skidracer: Thats it! - You are a star :)<br><br>Thanks. <br><br></td></tr></table><br>
<a name="1341146"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>(Posted May)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
