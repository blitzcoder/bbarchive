<!DOCTYPE html><html lang="en" ><head ><title >Self-contained find dialog</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Self-contained find dialog</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=117" >MaxGUI Module</a>/<a href="#bottom" >Self-contained find dialog</a><br><br>
<a name="828835"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> This code will enable a Windows Notepad-like Find dialog on any textarea gadget.<br><br>Usage:<br><br>Import "Find.bmx"<br>GADGET_MENU_FIND=CreateMenu("Find",0,menu)<br>CreateFindWindow(MainWindow:TGadget,TextArea:TGadget)<br><br>That's it!<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict

Import brl.maxgui

Private

Global FindWindow:TGadget
Global GADGET_FIND_TEXT:TGadget
Global GADGET_FIND_OK:TGadget
Global GADGET_FIND_CANCEL:TGadget
Global GADGET_FIND_UP:TGadget
Global GADGET_FIND_DOWN:TGadget
Global GADGET_FIND_MATCHCASE:TGadget

Public

Global GADGET_MENU_FIND:TGadget
Global GADGET_FIND_TEXTAREA:TGadget

Function CreateFindWindow(group:TGadget,textarea:TGadget)
	Local panel:TGadget
	Local flags
	flags=WINDOW_TITLEBAR|WINDOW_HIDDEN'|WINDOW_CENTER|WINDOW_DIALOG
	FindWindow=CreateWindow("Find",200,200,360,133,group,flags)
	CreateLabel("Find what:",10,11,60,16,FindWindow)
	GADGET_FIND_TEXT=CreateTextField(71,11,192,20,FindWindow)
	GADGET_FIND_OK=CreateButton("Find Next",274-1,10,73+2,21+2,FindWindow,BUTTON_OK)
	GADGET_FIND_CANCEL=CreateButton("Cancel",274-1,33+2,73+2,21+2,FindWindow,BUTTON_CANCEL)
	GADGET_FIND_MATCHCASE=CreateButton("Match case",7,71,80,16,FindWindow,BUTTON_CHECKBOX)
	DisableGadget GADGET_FIND_OK
	panel=CreatePanel(160,48,102,44,FindWindow,PANEL_GROUP,"Direction")
	GADGET_FIND_UP=CreateButton("Up",5,7,35,16,panel,BUTTON_RADIO)
	GADGET_FIND_DOWN=CreateButton("Down",46,7,46,16,panel,BUTTON_RADIO)
	SetButtonState GADGET_FIND_DOWN,1
	GADGET_FIND_TEXTAREA=textarea
	AddHook EmitEventHook,FindWindowHook
EndFunction

Function ReverseString$(s$)
	Local n
	Local rs$
	For n=0 To s.length-1
		rs:+Chr(s[s.length-1-n])
	Next
	Return rs
EndFunction

Function FindWindowHook:Object(id:Int,data:Object,context:Object)
	Global LastActiveGadget:TGadget
	Local text$,p,searchstring$
	
	If data=Null Return Null
	Local event:TEvent=TEvent(data)
	Select event.id
		
		Case EVENT_GADGETACTION
			Select event.source
			
				Case GADGET_FIND_OK
					text=TextAreaText(GADGET_FIND_TEXTAREA)
					If Not ButtonState(GADGET_FIND_MATCHCASE) text=Lower(text)
					p=TextAreaCursor(GADGET_FIND_TEXTAREA)
					searchstring=TextFieldText(GADGET_FIND_TEXT)
					If Not ButtonState(GADGET_FIND_MATCHCASE) searchstring=Lower(searchstring)
					If ButtonState(GADGET_FIND_UP)
						text=ReverseString(text)
						searchstring=reversestring(searchstring)
						p=text.length-p
					EndIf
					p=text.find(searchstring,p+1)
					If p&gt;-1
						If ButtonState(GADGET_FIND_UP)
							p=text.length-p
							p=p-searchstring.length
						EndIf
						SelectTextAreaText GADGET_FIND_TEXTAREA,p,searchstring.length
						ActivateGadget GADGET_FIND_OK
					Else
						Notify "Cannot find ~q"+TextFieldText(GADGET_FIND_TEXT)+"~q"
					EndIf
					
				Case GADGET_FIND_TEXT
					If TextFieldText(GADGET_FIND_TEXT)=""
						DisableGadget GADGET_FIND_OK
					Else
						EnableGadget GADGET_FIND_OK
					EndIf
					
				Case GADGET_FIND_CANCEL
					EmitEvent CreateEvent(EVENT_WINDOWCLOSE,FindWindow)
			
			EndSelect
		
		Case EVENT_MENUACTION
			Select event.source
				Case GADGET_MENU_FIND
					LastActiveGadget=ActiveGadget()
					ShowGadget FindWindow
					ActivateGadget GADGET_FIND_OK
					ActivateGadget GADGET_FIND_TEXT
					
			EndSelect

		Case EVENT_WINDOWCLOSE
			Select event.source
				
				Case FindWindow
					If LastActiveGadget
						ActivateGadget LastActiveGadget
					Else
						ActivateGadget GadgetGroup(FindWindow)
					EndIf
					HideGadget FindWindow
					
			EndSelect
	EndSelect
	Return data
EndFunction</textarea> <br><br></td></tr></table><br>
<a name="828908"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JoshK</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is an updated version that will search up &amp; down.  I also added a GADGET_MENU_FINDNEXT menu item.  Just create it and the application will handle everything, including enabling and disabling the menu.  You should disable the GADGET_MENU_FINDNEXT menu item when it is created.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict

Import brl.maxgui

Private

Global FindWindow:TGadget
Global GADGET_FIND_TEXT:TGadget
Global GADGET_FIND_OK:TGadget
Global GADGET_FIND_CANCEL:TGadget
Global GADGET_FIND_UP:TGadget
Global GADGET_FIND_DOWN:TGadget
Global GADGET_FIND_MATCHCASE:TGadget

Public

Global GADGET_MENU_FIND:TGadget
Global GADGET_MENU_FINDNEXT:TGadget
Global GADGET_FIND_TEXTAREA:TGadget

Function CreateFindWindow(group:TGadget,textarea:TGadget)
	Local panel:TGadget
	Local flags
	flags=WINDOW_TITLEBAR|WINDOW_HIDDEN'|WINDOW_CENTER|WINDOW_DIALOG
	FindWindow=CreateWindow("Find",200,200,360,133,group,flags)
	CreateLabel("Find what:",10,11,60,16,FindWindow)
	GADGET_FIND_TEXT=CreateTextField(71,11,192,20,FindWindow)
	GADGET_FIND_OK=CreateButton("Find Next",274-1,10,73+2,21+2,FindWindow,BUTTON_OK)
	GADGET_FIND_CANCEL=CreateButton("Cancel",274-1,33+2,73+2,21+2,FindWindow,BUTTON_CANCEL)
	GADGET_FIND_MATCHCASE=CreateButton("Match case",7,71,80,16,FindWindow,BUTTON_CHECKBOX)
	DisableGadget GADGET_FIND_OK
	panel=CreatePanel(160,48,102,44,FindWindow,PANEL_GROUP,"Direction")
	GADGET_FIND_UP=CreateButton("Up",5,7,35,16,panel,BUTTON_RADIO)
	GADGET_FIND_DOWN=CreateButton("Down",46,7,46,16,panel,BUTTON_RADIO)
	SetButtonState GADGET_FIND_DOWN,1
	GADGET_FIND_TEXTAREA=textarea
	AddHook EmitEventHook,FindWindowHook
EndFunction

Private

Function Find()
	Local text$,p,searchstring$
	text=TextAreaText(GADGET_FIND_TEXTAREA)
	If text="" Return
	If Not ButtonState(GADGET_FIND_MATCHCASE) text=Lower(text)
	p=TextAreaCursor(GADGET_FIND_TEXTAREA)
	searchstring=TextFieldText(GADGET_FIND_TEXT)
	If Not ButtonState(GADGET_FIND_MATCHCASE) searchstring=Lower(searchstring)
	If ButtonState(GADGET_FIND_UP)
		text=ReverseString(text)
		searchstring=reversestring(searchstring)
		p=text.length-p
	EndIf
	p=text.find(searchstring,p+1)
	If p&gt;-1
		If ButtonState(GADGET_FIND_UP)
			p=text.length-p
			p=p-searchstring.length
		EndIf
		SelectTextAreaText GADGET_FIND_TEXTAREA,p,searchstring.length
	Else
		Notify "Cannot find ~q"+TextFieldText(GADGET_FIND_TEXT)+"~q"
	EndIf
	
	Function ReverseString$(s$)
		Local n
		Local rs$
		For n=0 To s.length-1
			rs:+Chr(s[s.length-1-n])
		Next
		Return rs
	EndFunction
	
EndFunction

Function FindWindowHook:Object(id:Int,data:Object,context:Object)
	Global LastActiveGadget:TGadget
	If data=Null Return Null
	Local event:TEvent=TEvent(data)
	Select event.id
		
		Case EVENT_GADGETACTION
			Select event.source

				Case GADGET_FIND_OK
					Find()
					ActivateGadget GADGET_FIND_OK
					
				Case GADGET_FIND_TEXT
					If TextFieldText(GADGET_FIND_TEXT)=""
						DisableGadget GADGET_FIND_OK
						If GADGET_MENU_FINDNEXT
							DisableGadget GADGET_MENU_FINDNEXT
							UpdateWindowMenu GadgetGroup(FindWindow)
						EndIf
					Else
						EnableGadget GADGET_FIND_OK
						If GADGET_MENU_FINDNEXT
							EnableGadget GADGET_MENU_FINDNEXT
							UpdateWindowMenu GadgetGroup(FindWindow)
						EndIf
					EndIf
					
				Case GADGET_FIND_CANCEL
					EmitEvent CreateEvent(EVENT_WINDOWCLOSE,FindWindow)
			
			EndSelect
		
		Case EVENT_MENUACTION
			Select event.source
				Case GADGET_MENU_FIND
					LastActiveGadget=ActiveGadget()
					ShowGadget FindWindow
					ActivateGadget GADGET_FIND_OK
					ActivateGadget GADGET_FIND_TEXT
				Case GADGET_MENU_FINDNEXT
					Find()
				
			EndSelect

		Case EVENT_WINDOWCLOSE
			Select event.source
				
				Case FindWindow
					If LastActiveGadget
						ActivateGadget LastActiveGadget
					Else
						ActivateGadget GadgetGroup(FindWindow)
					EndIf
					HideGadget FindWindow
					
			EndSelect
	EndSelect
	Return data	
EndFunction

Public</textarea> <br><br></td></tr></table><br>
<a name="898273"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >danielos</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> looks good!<br>could you (or someone else) code that also for blitzplus ? <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
