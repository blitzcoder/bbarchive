<!DOCTYPE html><html lang="en" ><head ><title >Algorithm for drawing filled Pie</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Algorithm for drawing filled Pie</h1><a href="forums.php" >Community Forums</a>/<a href="topics.php?forum=125" >General Help</a>/<a href="#bottom" >Algorithm for drawing filled Pie</a><br><br>
<a name="1145898"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Virtech</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi guys!<br><br>I need a function that draws a filled pie (slice of a circle).<br><br>It should do this using the following parameters:<br>Radius, StartAngle, EndAngle<br><br>Also the algo must draw the shape plotting pixels (no lines!)<br><br>I've already googled around and found only examples that draws unfilled pie shapes. Including the blitz codearchives.<br><br>Please help! :) <br><br></td></tr></table><br>
<a name="1145906"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >*</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Use sin and cos to get the points you need for example:<br><pre class=code>
for Angle = StartAngle to EndAngle
   xPos = Sin( Angle ) *Radius
   yPos = Cos( Angle ) *Radius
   DrawLine( X, Y, XPos, YPos )
next
</pre><br><br>Where X and Y is the center of the circle<br>Radius is the size of the circle<br>XPos and YPos are floats<br>and StartAngle and EndAngle are whatever you want<br>Angle can be a float as it gives smoother transitions if you wanted. <br><br></td></tr></table><br>
<a name="1145907"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GfK</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Mmmmm pie.<br><br>I realise this doesn't help. <br><br></td></tr></table><br>
<a name="1145908"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Virtech</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> @EdzUp[GD]<br>Thanks for your suggestion! But as stated in the first post the algorithm must draw the pie shape one pixel/interation, both shape and its fill. I cannot use lines for this. <br><br></td></tr></table><br>
<a name="1145929"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's a starter:<br><br>It draws a circle, 1 pixel at a time using horizontal rows of pixels.  All you have to add is, for each pixel, compute what region of pie it's in and set the associated color.  Right now it's just a random color.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics 100, 100

Cls 

	drawpie ( 50, 50, 48 )

Flip 

WaitKey 

Function DrawPie( x:Int, y:Int, r:Int )
	Local a:Int 
	For Local ypos:Int = y - r To y + r 
		Local width:Int = Sqr((r * r) - ((r - a) * (r - a)))
		For Local pixel:Int = x - width To x + width
			SetColor ( Rand(0,255), Rand(0,255), Rand(0,255))
			DrawRect ( pixel, ypos, 1, 1 )
		Next 
		a :+ 1
	Next
End Function 
</textarea> <br><br></td></tr></table><br>
<a name="1145941"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Got bored / couldn't sleep E&amp;OE ;)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics 300, 300


' pie data is just a bunch of regions, ie 	0 to 30degres,
'										30 to 170
'										170 to 240
'										240 to 360  &lt;-- the last value should always be 360
'
Local pieData:Float[] = [30.0,170.0,240.0,360.0]

' simple test set of data - four 90 degree regions...
'Local pieData:Float[] = [90.0,180.0,270.0,360.0]

Cls 

	drawpie ( 150, 150, 120, piedata )

Flip 

WaitKey 

Function DrawPie( x:Int, y:Int, r:Int, data:Float[] )
	
	' you might want to add more colors...
	Global colors:Int[][] = [ [255,0,0], [0,255,0], [0,0,255], [255,255,0], [0,255,255] ]
	
	Local a:Int 
	For Local ypos:Int = y - r To y + r 
		Local width:Int = Sqr((r * r) - ((r - a) * (r - a)))
		For Local pixel:Int = x - width To x + width
			Local region:Int = 0
			Local ang:Float = ATan2( y - ypos, x - pixel ) + 179.99
			For Local f:Float = EachIn data
				If ang &lt;= f 
					Exit 
				End If 
				region :+ 1
			Next 				
			SetColor ( colors[region Mod 5][0], colors[region Mod 5][1], colors[region Mod 5][2] )
			DrawRect ( pixel, ypos, 1, 1 )
		Next 
		a :+ 1
	Next
End Function 
</textarea> <br><br></td></tr></table><br>
<a name="1145944"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ooh, seems I forgot the contents of your original post:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics 300, 300

Cls 

	drawPieSlice ( 150, 150, 120, 22.5, 60.0 )

Flip 

WaitKey 

Function drawPieSlice ( x:Int, y:Int, r:Int, startAngle:Float, endAngle:Float )
	Local a:Int 
	For Local ypos:Int = y - r To y + r 
		Local width:Int = Sqr((r * r) - ((r - a) * (r - a)))
		For Local pixel:Int = x - width To x + width
			Local ang:Float = ATan2( y - ypos, x - pixel ) + 179.99
			If ang &gt;= startAngle And ang &lt;= endAngle 
				DrawRect ( pixel, ypos, 1, 1 )
			End If 
		Next 
		a :+ 1
	Next
End Function 
</textarea> <br><br></td></tr></table><br>
<a name="1146065"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> here is an example although it would be wayyyyyy faster with opengl points<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics 300,300,0,60

For Local i:Int = 1 To 10
	pie.Create(Rnd(255),Rnd(255),Rnd(255),Rnd(360),Rnd(360),Rnd(-5.0,5.0),Rnd(1,GraphicsWidth()-1),Rnd(1,GraphicsHeight()-1),Rnd(-4.0,4.0),Rnd(-4.0,4.0),Rnd(10,40))
Next

SetBlend lightblend
SetAlpha 1
While Not KeyHit(key_escape)
Cls 

	Pie.RenderAll()

Flip
Wend

WaitKey 

Function drawPieSlice ( x:Int, y:Int, r:Int, startAngle:Float, endAngle:Float )
	Local a:Int 
	For Local ypos:Int = y - r To y + r 
		Local width:Int = Sqr((r * r) - ((r - a) * (r - a)))
		For Local pixel:Int = x - width To x + width
			Local ang:Float = ATan2( y - ypos, x - pixel ) + 179.99
			If ang &gt;= startAngle And ang &lt;= endAngle 
				DrawRect ( pixel, ypos, 1, 1 )
			End If 
		Next 
		a :+ 1
	Next
End Function 


Type Pie
	Global list:TList = New TList
	
	Field r:Int,g:Int,b:Int
	Field a1:Float,a2:Float,aspd:Float
	Field x:Float,y:Float,dx:Float,dy:Float
	Field radius:Int
	
	Function Create:pie(r:Int,g:Int,b:Int,a1#,a2#,aspd#,x#,y#,dx#,dy#,radius:Int)
		
		Local p:pie = New pie
		p.r = r
		p.g = g
		p.b = b
		p.a1 = a1
		p.a2 = a2
		p.aspd = aspd
		p.x = x
		p.y = y
		p.dx = dx
		p.dy = dy
		p.radius = radius
		
		list.addlast(p:pie)
		
		Return p:pie
	End Function
	
	Function RenderAll()
		For Local p:pie = EachIn list
			p.x :+ p.dx
			p.y :+ p.dy
			
			If p.x &lt; 0 Then
				p.dx = -p.dx
				p.x = 0
			EndIf
			If p.y &lt; 0 Then
				p.dy = -p.dy
				p.y = 0
			EndIf
			If p.x &gt; GraphicsWidth() Then
				p.dx = -p.dx
				p.x = GraphicsWidth()
			EndIf
			If p.y &gt; GraphicsHeight() Then
				p.dy = -p.dy
				p.y = GraphicsHeight()
			EndIf
			
			
			SetColor p.r,p.g,p.b
			
			If p.a1 &lt; p.a2 Then
				drawpieslice(p.x,p.y,p.radius,p.a1,p.a2)
			Else
				drawpieslice(p.x,p.y,p.radius,p.a2,p.a1)
			EndIf
			
		Next
	End Function
End Type</textarea> <br><br></td></tr></table><br>
<a name="1146092"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Virtech</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> @matibee<br>Thanks for providing exactly what I was looking for! :)<br><br>@Nate<br>Your app reminds me of the dream I had last night after thinking about this problem for to long :D Thanks, its a good one:)<br><br>I intend to use this routine for drawing lots of smaller pie shapes, with very short radius. So I put together this testapp to magnify the drawing result at smaller sizes.  Im not sure if it is an float/int rouding error on my end, but the testapp shows some "artifacts" at some angles and  different radius. Also I need all tiles (or pixels) overlapped by the slice to get drawed. Finally I need the routine to cope with angle wrapping . Feel free to leave any suggestions if you like :)<br><br>Here is the source for the test app (blitz3d). Or you could get an exe <a href="http://www.marbas3D.com/blitz/pieslice.rar" target="_blank">here</a><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics 1280,720,32,2
SetBuffer BackBuffer()

Const tileW = 32
Const tileH = 32

Local px# = GraphicsWidth()/2
Local py# = GraphicsHeight()/2
Local pa# = 200.0
Local pr# = 200.0
Local pf# = 90.0

Global plotN
Global plotTime
While Not KeyHit(1)

	Cls

	If KeyDown(17) Then px=px+Sin(pa) : py=py+Cos(pa)
	If KeyDown(31) Then px=px-Sin(pa) : py=py-Cos(pa)
	If KeyDown(30) Then px=px+Sin(pa+90) : py=py+Cos(pa+90)
	If KeyDown(32) Then px=px+Sin(pa-90) : py=py+Cos(pa-90)
	If KeyDown(203) Then pa=pa-1
	If KeyDown(205) Then pa=pa+1
	If KeyDown(200) Then pr=pr+1
	If KeyDown(208) Then pr=pr-1
	If KeyDown(78) Then pf=pf+1
	If KeyDown(74) Then pf=pf-1
	
	DrawGrid()
	DrawCross(px,py)
	DrawPieSlice(px,py,pa,pr,pf)
	DrawSliceHelper(px,py,pa,pr,pf)	
	
	; Stats
	Local tx=10,ty=0
	Color 255,255,255
	ty=ty+15:Text tx,ty,"[WASD] -&gt; Move Pie Center"
	ty=ty+15:Text tx,ty,"[Arroe Left/Right] -&gt; Rotate Pie Slice"
	ty=ty+15:Text tx,ty,"[Keypad +/-] -&gt; Change Pie Radius "
	ty=ty+30:Text tx,ty,"x,y:"+Int(px)+","+Int(py)
	ty=ty+15:Text tx,ty,"angle:"+pa
	ty=ty+15:Text tx,ty,"radius:"+pr;/32
	ty=ty+15:Text tx,ty,"pieSize:"+pf
	ty=ty+15:Text tx,ty,"plotN:"+plotN
	ty=ty+15:Text tx,ty,"plotTime:"+plotTime

	Flip
	
Wend

End

Function DrawGrid()

	Local x,y
	Color 55,55,55
	For y = 0 To GraphicsHeight()-1 Step tileH
		For x = 0 To GraphicsWidth()-1 Step tileW
			Rect x,y,tileW,tileH,0
		Next
	Next
	
End Function

Function DrawCross(x,y,size=10)

	Local hsize=size/2
	Color 255,255,255
	Line x-hsize,y,x+hsize,y
	Line x,y-hsize,x,y+hsize
	
End Function

Function DrawSliceHelper(x,y,angle#,radius#=100,pieSize#=90)

	Local hpieSize#=pieSize/2.0
	Local vx# = x+Sin(angle)*radius
	Local vy# = y+Cos(angle)*radius
	Local vx1# = x+Sin(angle-hpieSize)*radius
	Local vy1# = y+Cos(angle-hpieSize)*radius
	Local vx2# = x+Sin(angle+hpieSize)*radius
	Local vy2# = y+Cos(angle+hpieSize)*radius
	Color 0,255,0
	Line x,y,vx,vy
	Color 255,0,0
	Line x,y,vx1,vy1
	Line x,y,vx2,vy2
	Color 100,100,100
	Oval x-radius,y-radius,radius*2,radius*2,0

End Function

Function DrawPieSlice(x,y,angle#,radius#=100,pieSize#=90)

	Local hpieSize#=pieSize/2.0
	Local r#=radius/32.0
	Local startAngle=angle-hpieSize
	Local endAngle=angle+hpieSize

	x=x/tileH
	y=y/tileH
	Local a
	plotN=0
	plotTime=MilliSecs()	
	For ypos = y - r To y + r 
		Local width = Sqr((r * r) - ((r - a) * (r - a)))
		For pixel = x - width To x + width
			Local ang# = ATan2( x - pixel, y - ypos ) + 179.99
			If ang &gt;= startAngle And ang &lt;= endAngle 
				Rect pixel*tileW, ypos*tileH, tileW, tileH, 1
				plotN = plotN+1
			End If 
		Next 
		a =a+ 1
	Next
	plotTime=MilliSecs()-plotTime
	
End Function
</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
