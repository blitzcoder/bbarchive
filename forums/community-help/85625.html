<!DOCTYPE html><html lang="en" ><head ><title >Hooray!  I'm getting events! BUT...</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Hooray!  I'm getting events! BUT...</h1><a href="forums.php" >Community Forums</a>/<a href="topics.php?forum=125" >General Help</a>/<a href="#bottom" >Hooray!  I'm getting events! BUT...</a><br><br>
<a name="968850"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >sswift</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> <textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Import MaxGUI.Drivers

Extern "Win32"

	Function RegisterRawInputDevices:Int(pRawInputDevices:Byte Ptr, uiNumDevices:Int, cbSize:Int)
	Function GetRawInputData:Int(hRawInput:Byte Ptr, uiCommand:Int, pData:Byte Ptr, pcbSize:Int Ptr, cbSizeHeader:Int)
	'Function SetWindowLongPtrA:Byte Ptr(hWnd:Int, nIndex:Int, dwNewLong:Byte Ptr)

End Extern


Global OldWinProc:Int ' This is the old blitzmax WinProc() function pointer.  This is set when you call HookWinProc().  


Const HID_USAGE_PAGE_GENERIC%  = $1
Const HID_USAGE_GENERIC_MOUSE% = $2
Const RIDEV_INPUTSINK% = $100
Const RIM_TYPEMOUSE% = 0
Const RID_INPUT% = $10000003

Type RAWINPUTDEVICE 
   Field usUsagePage:Short
   Field usUsage:Short
   Field dwFlags:Int
   Field hwndTarget:Int
End Type

Global Rid:RAWINPUTDEVICE[1] ' We use an array here just in case you wish to register more than one raw input device and process the data from it.
Rid:RAWINPUTDEVICE[0] = New RAWINPUTDEVICE 


Type RAWINPUT ' Combination of Windows structs RAWINPUTHEADER, RAWINPUT, and RAWMOUSE to avoid need for unions and issues with variables of a certain type being a pointer to data rather than having the data at that location.
	
	' Header - 16 bytes:
		Field dwType:Int
		Field dwSize:Int
		Field hDevice:Byte Ptr
		Field WPARAM:Int Ptr 
	
	' Data:
		Field usFlags:Short 
		Field ulButtons:Int ' Depending on flags, can either be single long value ulButtons or short values usButtonFlags in low word, and usButtonData in high word.  Again, a work around for unions.
	  	Field ulRawButtons:Int 
		Field lLastX:Int 
	  	Field lLastY:Int 
  		Field ulExtraInformation:Int 

End Type 



' ---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
' hWnd is the pointer to your window.  hWnd is not the gadget pointer! 
' You can get hWnd with hWnd = GetActiveWindow(), or with hWnd = QueryGadget(Window, QUERY_HWND), where Window is your window's gadget pointer.
' ---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

	Function HookWinProc(hWnd:Int)
		Local GWL_WNDPROC:Int = -4
		OldWinProc = SetWindowLongA(hWnd, GWL_WNDPROC, Int(Byte Ptr(WinProc)))
	End Function  


' ---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
' This function is called automatically.  It grabs raw windows events before Blitzmax processes them, so that you can handle events that Blitzmax doesn't provide an interface for.
'
' Blitzmax has its own function like this internally, and this one calls that when it is done processing.
' Presumably, events handled by this function will still be passsed onto BlitzMax, so avoid processing the same data in both event handlers!
' ---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

	Function WinProc:Int(hWnd:Int, Msg:Int, wParam:Int, lParam:Int) "win32"
	
		Const WM_INPUT% = $00FF
	
		Function LWord:Short(I:Int) ' Returns the lower 16 bits of a 32 bit integer.
			Return I &amp; $FFFF
		End Function
	
		Function HWord:Short(I:Int) ' Returns the upper 16 bits of a 32 bit integer.
			Return I Shr 16
		End Function
		
		Select Msg
	
		    Case WM_INPUT

	    			Local dwSize:Int = 40
				Local Raw:RAWINPUT = New RAWINPUT
    
				GetRawInputData(Byte Ptr(lParam), RID_INPUT, Raw, Varptr dwSize, 16) ' Get data in RAWINPUT structure.
    
				If Raw.dwType = RIM_TYPEMOUSE
					'xPosRelative = Raw.lLastX
					'yPosRelative = Raw.lLastY
					
					DebugLog(Raw.lLastX)
					
				EndIf
				
			Default
				DebugLog("Unknown Msg: " + Msg)
					
		End Select
	
		If OldWinProc &lt;&gt; 0 Then Return CallWindowProcA(Byte Ptr(OldWinProc), hWnd, Msg, wParam, lParam)   
		
	End Function


' ---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
' This function registers the mouse as a raw input device, so that WM_INPUT events will be generated for it.
' hWnd is the pointer to your window.  See HookWinProc() above for more details.
' ---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

	Function RegisterRID_MOUSE(hWnd:Int)

	    Rid[0].usUsagePage = HID_USAGE_PAGE_GENERIC
	    Rid[0].usUsage = HID_USAGE_GENERIC_MOUSE 
	    Rid[0].dwFlags = RIDEV_INPUTSINK   
	    Rid[0].hwndTarget = hWnd			

	    RegisterRawInputDevices(Rid, 1, SizeOf(Rid[0]))

	End Function





Global Window:TGadget = CreateWindow("Blah", 0, 0, 800, 600, Null)
			
Global _hWnd = QueryGadget(Window, QUERY_HWND)
 
HookWinProc(_hWnd) 
RegisterRID_MOUSE(_hWnd)

Repeat

	WaitEvent 
	Select EventID()
		Case EVENT_WINDOWCLOSE
			End
	End Select
	
	Delay(10)

Forever
</textarea><br><br><br>I'm not getting the WM_INPUT event at all!<br><br>Why?!<br><br><br>Btw, that's working example code that will compile. <br><br></td></tr></table><br>
<a name="968869"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >N</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Really ought to keep these in the forums for their respective programming languages and topics... <br><br></td></tr></table><br>
<a name="968874"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> <a href="/posts.php?topic=85590#968607" target="_blank">tut tut</a> :-p <br><br></td></tr></table><br>
<a name="968910"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >sswift</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Brucey:<br>I didn't notice that post before. <br><br></td></tr></table><br>
<a name="968912"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Adam Novagen</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah, unless I'm mistaken, this belongs in the BMax forum.<br><br>Oh, and DUDE!!! Use CODEBOX, not CODE!!! You've exploded my screen! @_@ <br><br></td></tr></table><br>
<a name="968938"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >sswift</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I stil can't figure this out.  I can't believe I spent three days on this and now I can't figure out why I'm not getting any WM_INPUT messages.  I'm getting lots of other messages, so I know the event handler code is working.  But I'm not getting the WM_INPUT messages.<br><br>I've double checked the constants.  They're correct.<br><br>I double checked the size reported for the RAWINPUTDEVICE array.  12 bytes, as expected.<br><br>I tried VarPtr Rid[0] when registering the device in case the pointer was pointing to the wrong place.  I tried Byte Ptr(Rid) too.  No dice. <br><br></td></tr></table><br>
<a name="968978"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >sswift</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think I know what the problem is,  I don't know how to fix it yet though.<br><br>The RegisterRawinputdevice function requires a pointer to the data for the device.<br><br>In C, this is an array of structs.  But types in Blitzmax work differently.  When I make an array of RAWINPUTDEVICE, all I'm really doing is creating an array of pointers to my data, not an array of the data itself.<br><br>So I need to somehow point the function at my data.  This should be possible, but it occurs to me that if I cannot create an array of types which contains the actual data in the type format, then I cannot pass more than one device to the function because the data will not be contiguous.<br><br>In my case that should be okay because I just want to read the mouse, but if one needed to read more than one device, I think you'd be stuck using banks or arrays or something instead of types. <br><br></td></tr></table><br>
<a name="968983"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >sswift</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> <textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Import MaxGUI.Drivers

Extern "Win32"

	Function RegisterRawInputDevices:Int(pRawInputDevices:Byte Ptr, uiNumDevices:Int, cbSize:Int)
	Function GetRawInputData:Int(hRawInput:Byte Ptr, uiCommand:Int, pData:Byte Ptr, pcbSize:Int Ptr, cbSizeHeader:Int)
	'Function SetWindowLongPtrA:Byte Ptr(hWnd:Int, nIndex:Int, dwNewLong:Byte Ptr)
	'Function GetRegisteredRawInputDevices:Int(PRAWINPUTDEVICE pRawInputDevices, puiNumDevices:Int Ptr, cbSize:Int)
	Function GetLastError:Int()
	
End Extern


Global OldWinProc:Int ' This is the old blitzmax WinProc() function pointer.  This is set when you call HookWinProc().  


Const HID_USAGE_PAGE_GENERIC%  = $1
Const HID_USAGE_GENERIC_MOUSE% = $2
Const RIDEV_NOLEGACY% = $30
Const RIDEV_INPUTSINK% = $100
Const RIM_TYPEMOUSE% = 0
Const RID_INPUT% = $10000003
				
Type RAWINPUTDEVICE 
   Field usUsagePage:Short
   Field usUsage:Short
   Field dwFlags:Int
   Field hwndTarget:Int
End Type

Global Rid:RAWINPUTDEVICE[1] ' We use an array here just in case you wish to register more than one raw input device and process the data from it.
Rid:RAWINPUTDEVICE[0] = New RAWINPUTDEVICE 


Type RAWINPUT ' Combination of Windows structs RAWINPUTHEADER, RAWINPUT, and RAWMOUSE to avoid need for unions and issues with variables of a certain type being a pointer to data rather than having the data at that location.
	
	' Header - 16 bytes:
		Field dwType:Int
		Field dwSize:Int
		Field hDevice:Byte Ptr
		Field WPARAM:Int Ptr 
	
	' Data:
		Field usFlags:Short 
		Field ulButtons:Int ' Depending on flags, can either be single long value ulButtons or short values usButtonFlags in low word, and usButtonData in high word.  Again, a work around for unions.
	  	Field ulRawButtons:Int 
		Field lLastX:Int 
	  	Field lLastY:Int 
  		Field ulExtraInformation:Int 

End Type 


' ---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
' This function registers the mouse as a raw input device, so that WM_INPUT events will be generated for it.
' hWnd is the pointer to your window.  See HookWinProc() above for more details.
' ---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

	Function RegisterRID_MOUSE(hWnd:Int)
		
		Local Success%
		Local Error%
		
		Rid[0].usUsagePage = HID_USAGE_PAGE_GENERIC
		Rid[0].usUsage = HID_USAGE_GENERIC_MOUSE 
		Rid[0].dwFlags = RIDEV_INPUTSINK
		Rid[0].hwndTarget = hWnd
		
		Success = RegisterRawInputDevices(Rid[0], 1, SizeOf(Rid[0]))
		Error = GetLastError()
		
		Print "Success = " + Success
		Print Error
		Print "RIDSIZE = " + SizeOf(Rid[0])
		
	End Function


' ---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
' hWnd is the pointer to your window.  hWnd is not the gadget pointer! 
' You can get hWnd with hWnd = GetActiveWindow(), or with hWnd = QueryGadget(Window, QUERY_HWND), where Window is your window's gadget pointer.
' ---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

	Function HookWinProc(hWnd:Int)
		Local GWL_WNDPROC:Int = -4
		OldWinProc = SetWindowLongA(hWnd, GWL_WNDPROC, Int(Byte Ptr(WinProc)))
	End Function  


' ---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
' This function is called automatically.  It grabs raw windows events before Blitzmax processes them, so that you can handle events that Blitzmax doesn't provide an interface for.
'
' Blitzmax has its own function like this internally, and this one calls that when it is done processing.
' Presumably, events handled by this function will still be passsed onto BlitzMax, so avoid processing the same data in both event handlers!
' ---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

	Function WinProc:Int(hWnd:Int, Msg:Int, wParam:Int, lParam:Int) "win32"
	
		Const WM_INPUT% = $00FF
	
		Function LWord:Short(I:Int) ' Returns the lower 16 bits of a 32 bit integer.
			Return I &amp; $FFFF
		End Function
	
		Function HWord:Short(I:Int) ' Returns the upper 16 bits of a 32 bit integer.
			Return I Shr 16
		End Function
		
		Select Msg
	
		    Case WM_INPUT

	    			Local dwSize:Int = 40
				Local Raw:RAWINPUT = New RAWINPUT
    
				GetRawInputData(Byte Ptr(lParam), RID_INPUT, Raw, Varptr dwSize, 16) ' Get data in RAWINPUT structure.
    
				If Raw.dwType = RIM_TYPEMOUSE
					'xPosRelative = Raw.lLastX
					'yPosRelative = Raw.lLastY
					
					DebugLog(Raw.lLastX)
					
				EndIf
				
				DebugLog("WM_INPUT")

				
			Default
				DebugLog("Unknown Msg: " + Msg)
					
		End Select
	
		If OldWinProc &lt;&gt; 0 Then Return CallWindowProcA(Byte Ptr(OldWinProc), hWnd, Msg, wParam, lParam)   
		
	End Function





Global Window:TGadget = CreateWindow("Blah", 0, 0, 800, 600, Null)
			
Global _hWnd = QueryGadget(Window, QUERY_HWND)
 
HookWinProc(_hWnd) 
RegisterRID_MOUSE(_hWnd)

Repeat

	WaitEvent 
	Select EventID()
		Case EVENT_WINDOWCLOSE
			End
	End Select
	
	Delay(10)

Forever
</textarea><br><br>Hooray!  Finally!  I'm getting WM_INPUT events!<br><br>And it looks like I'm getting the mouse data too!<br><br>And it ONLY TOOK THREE DAYS.<br><br>Or was that four?  I've lost track of time. :-( <br><br></td></tr></table><br>
<a name="968988"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >sswift</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> If someone on a laptop could test that code and tell me what kind of values it returns for lastmousex when using a trackpad, that would be most helpful.<br><br>If the numbers are large, like 1000-30000, then that means it's returning absolute coordinates, which would need to be converted to relative. (tossing out changes which are too big I suppose)<br><br>With my mouse I'm getting pretty small numbers.  Like no more than +-10. <br><br></td></tr></table><br>
<a name="968994"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> In C a struct is really just a block of memory. Then you have names for little pieces of that memory, which you can use like ordinary variables such as int,  short, pointers etc.<br><br>BlitzMax is managed ( garbage collected ) and has an extra level of indirection. Once you have an instance of a BlitzMax Type object memory occupied by the various Fields corresponds the the C struct. The address of the first field corresponds to the address of the struct.<br><br>Your next adventure will be padding and memory alignment. The address of a four-byte value will typically be a multiple of four, the address of an eight-byte value a multiple of eight etc. Suppose you have consective fields, an integer and a double. If the address of the integer happens to be a multiple of eight then it will be followed by four bytes of unused space so that the address of the double will also be a multiple of eight. <br><br></td></tr></table><br>
<a name="968995"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >EOF</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> I tested the code on an EeePC 901 and am seeing values within -/+34 range <br><br></td></tr></table><br>
<a name="969015"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >sswift</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Your next adventure will be padding and memory alignment. The address of a four-byte value will typically be a multiple of four, the address of an eight-byte value a multiple of eight etc. Suppose you have consective fields, an integer and a double. If the address of the integer happens to be a multiple of eight then it will be followed by four bytes of unused space so that the address of the double will also be a multiple of eight. <br></div><br><br>But even so... if I set up my type the same way as the struct is set up, won't they both end up having the data in the same places?<br><br>(The app seems to be reading he data just fine too, so this must be the case.) <br><br></td></tr></table><br>
<a name="969017"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >sswift</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> "I tested the code on an EeePC 901 and am seeing values within -/+34 range"<br><br>Hm.  Well, that's good that it's not absolute.  I tried it with my wacon tablet and it was absolute, but I didn't know if a trackpad would be the same.<br><br>And the +-34 makes sense I guess.  Your pad may be set up to be fairly sensitive so you can scross the screen in one swipe.<br><br>It is a bit problematic though, having numbers so much higher.  I really don't want to have to implement mouse sensitivity code in the game. <br><br></td></tr></table><br>
<a name="969406"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> if I set up my type the same way as the struct is set up, won't they both end up having the data in the same places? <br></div><br>Probably yes. I once checked 4-byte and 8-byte values and found that BlitzMax handled alignment the same as MingW. But I never got around to checking byte or short. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
