<!DOCTYPE html><html lang="en" ><head ><title >please help  Glow question</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >please help  Glow question</h1><a href="forums.php" >Community Forums</a>/<a href="topics.php?forum=125" >General Help</a>/<a href="#bottom" >please help  Glow question</a><br><br>
<a name="1312390"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NotAGamer</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> i've been very serious to make a game and i want to use blitz3d for fast prototyping my game.. <br><br>i read this code and its amazing .. but if you changed the alpha of the sprite to 0.8 <br><br>you can see that the sprite is not in the middle.. has any body know how to make this work and make the sprite centerd?<br><br><a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=1351" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=1351</a> <br><br></td></tr></table><br>
<a name="1312393"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Use this code archive entry:<br><a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=2862" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=2862</a><br><br>You can modify it like this:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">AppTitle "Motion Blur"
Graphics3D 640,480,0,2
SetBuffer BackBuffer()

Global buffer_size% = 256

blurtex = CreateTexture(buffer_size,buffer_size)

Global objecttex = CreateTexture(128,128)
SetBuffer TextureBuffer(objecttex)
Color 255,255,0
Rect 0,0,128,128
Color 0,127,255
Rect 0,0,64,64,True
Rect 64,64,64,64,True
SetBuffer BackBuffer()

Global camera% = CreateCamera()
CameraRange camera,0.1,1000

Global cube% = CreateCube()
PositionEntity cube,0,0,3
EntityTexture cube,objecttex

blurquad = CreateMesh()
surf = CreateSurface(blurquad)

Local texOffset# = 0.5 / buffer_size ;Prevents texture sliding.

AddVertex(surf,-1,1,0,0+texOffset,0+texOffset)
AddVertex(surf,1,1,0,1+texOffset,0+texOffset)
AddVertex(surf,1,-1,0,1+texOffset,1+texOffset)
AddVertex(surf,-1,-1,0,0+texOffset,1+texOffset)
AddTriangle(surf,0,1,2)
AddTriangle(surf,0,2,3)
EntityTexture blurquad,blurtex
PositionEntity blurquad,0,0,1
EntityFX blurquad,1
EntityAlpha blurquad,0.8 ;Never 1.0 so there's some decay, else it saturates.
EntityOrder blurquad,-1
Const BLEND_ADD = 3
EntityBlend blurquad, BLEND_ADD

Local timer = CreateTimer( 30 )

While Not KeyHit(1)
	WaitTimer( timer )
	Cls
	
	TurnEntity cube,1,2,3
	UpdateWorld()

	CameraViewport camera,0,0,buffer_size,buffer_size
	RenderWorld()
	CopyRect 0,0,buffer_size,buffer_size,0,0,BackBuffer(),TextureBuffer(blurtex)

	CameraViewport camera,0,0,GraphicsWidth(),GraphicsHeight()

	RenderWorld()
	
	Flip True
Wend

End</textarea> <br><br></td></tr></table><br>
<a name="1312396"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NotAGamer</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> @kyrzon <br><br>thank you very much!!!!! <br><br></td></tr></table><br>
<a name="1315989"></a>

<a name="1315990"></a>

<a name="1315992"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yue</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> <textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 640,480,32,2
SetBuffer BackBuffer()

; Create camera
Global camera=CreateCamera()
MoveEntity camera,3,3,0
RotateEntity camera,30,30,0

light=CreateLight()

Global cube=CreateCube()
PositionEntity cube,0,0,5
tex0=CreateTexture(300,300)
SetBuffer TextureBuffer(tex0)
ClsColor 255,0,0
Cls
SeedRnd(MilliSecs())
For k=1 To 50
	Color Rand(256),Rand(256),Rand(256)
	Rect Rand(600),Rand(600),Rand(600),Rand(600)
Next
EntityTexture cube,tex0
SetBuffer BackBuffer()

;glow setup
s=1
Global sizex=640/s
Global sizey=480/s
Global glowtexture=CreateTexture (384,384,256)
Global sp=CreateSprite(camera)
MoveEntity sp,-.25,-0.06,1.18
EntityAlpha sp,.32
ScaleTexture glowtexture,GraphicsWidth()/sizex,GraphicsHeight()/sizey
EntityTexture sp,glowtexture
TextureBlend glowtexture, 5
While Not KeyDown( 1 )
	TurnEntity cube,0.5,0.5,0.5
	CameraViewport camera,0,0,sizex,sizey
	RenderWorld
	CopyRect 0,0,sizex,sizey,0,0,BackBuffer(),TextureBuffer(glowtexture)
	CameraViewport camera,0,0,GraphicsWidth(),GraphicsHeight()
	
	RenderWorld
	Flip
Wend

End
</textarea><br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 640,480,32,2
SetBuffer BackBuffer()

; Create camera
Global camera=CreateCamera()
MoveEntity camera,3,3,0
RotateEntity camera,30,30,0

light=CreateLight()

Global cube=CreateCube()
PositionEntity cube,0,0,5

;Global cube2=CreateCube() 
;PositionEntity cube2,-5,0,5

tex0=CreateTexture(300,300)
SetBuffer TextureBuffer(tex0)
ClsColor 255,255,255
Cls
SeedRnd(MilliSecs())
For k=1 To 50
	Color Rand(256),Rand(256),Rand(256)
	Rect Rand(600),Rand(600),Rand(600),Rand(600)
Next
EntityTexture cube,tex0
SetBuffer BackBuffer()

;glow setup
s=1
Global sizex=640/s
Global sizey=480/s
Global glowtexture=CreateTexture (384,384,256)
Global sp=CreateSprite(camera)
MoveEntity sp,-.25,-0.06,1.21
ScaleTexture glowtexture,GraphicsWidth()/sizex,GraphicsHeight()/sizey
EntityTexture sp,glowtexture

TextureBlend glowtexture, 5    ;Also try mode 2 ...and increase the Alpha from 0.44 to about 0.80
EntityAlpha sp, 0.44

While Not KeyDown( 1 )
	TurnEntity cube,0.5,0.5,0.5
	CameraViewport camera,0,0,sizex,sizey
;	RenderWorld
	CopyRect 0,0,sizex,sizey,0,0,BackBuffer(),TextureBuffer(glowtexture)
	CameraViewport camera,0,0,GraphicsWidth(),GraphicsHeight()
	
	RenderWorld
	Flip
Wend

End
</textarea><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Type entlist
	Field ent
End Type


Graphics3D 1024,768,32,2
AmbientLight(10,10,20)
light=CreateLight()
PositionEntity light,-500,5000,2000
LightColor light,200,200,200
RotateEntity light,45,45,0
	
camera = CreateCamera()

ob1 = CreateCylinder()
PositionEntity ob1,0,1,7
EntityColor ob1,0,0,255
EntityFX ob1,1 
AddEnt(ob1)
		
ob2 = CreateCone()
PositionEntity ob2,-4,1,7
EntityColor ob2,0,255,0
EntityFX ob2,1
AddEnt(ob2)
	
ob3 = CreateSphere()
PositionEntity ob3,4,1,7
EntityColor ob3,255,255,255
AddEnt(ob3)
	
column = CreateCylinder()
ScaleEntity column,2,100,2
PositionEntity column,50,0,100
EntityColor column,255,0,0
EntityFX column,1
AddEnt(column)
	
	
While Not KeyHit(1)
	TurnEntity ob1,1,1,1
	TurnEntity ob2,1,1,1
	TurnEntity ob3,1,1,1
	TurnEntity column,1,1,1
	UpdateWorld
	RenderWorld 

	EntityGlow(camera,ob1,4,.2,50,50,255)
	EntityGlow(camera,ob2,3,.05,50,255,50)
	EntityGlow(camera,ob3,2,.05,255,255,0)

	
	Flip
Wend
End	
	
	
Function EntityGlow(eg_cam,eg_ent,eg_glowsteps=1,eg_glowsize#=.1,eg_r=-1,eg_g=-1,eg_b=-1)
	HideWorld()
	eg_pivot=CreatePivot()
	For eg_i=eg_glowsteps To 1 Step -1
		eg_glowent=CopyMesh(eg_ent)
		ShowEntity eg_glowent
		PositionEntity eg_glowent,EntityX(eg_ent),EntityY(eg_ent),EntityZ(eg_ent)
		RotateEntity eg_glowent,EntityPitch(eg_ent),EntityYaw(eg_ent),EntityRoll(eg_ent)
		EntityFX eg_glowent,1
		EntityBlend eg_glowent,3 ;optional
		ScaleMesh eg_glowent,1+eg_glowsize*eg_i,1+eg_glowsize*eg_i,1+eg_glowsize*eg_i
		EntityAlpha eg_glowent,0.1
		EntityParent eg_glowent,eg_pivot
		If (eg_r+eg_g+eg_b)&gt;0 EntityColor eg_glowent,eg_r,eg_g,eg_b
	Next
	CameraClsMode eg_cam,0,0:RenderWorld:CameraClsMode eg_cam,1,1
	For i=1 To CountChildren(eg_pivot):FreeEntity GetChild(eg_pivot,i):Next
	FreeEntity eg_pivot
	ShowWorld()
End Function
	

;HOUSEKEEPING FUNCTIONS

Function AddEnt(ent)
	e.entlist=New entlist
	e\ent=ent
End Function
Function HideWorld()
	For e.entlist=Each entlist
		HideEntity e\ent
	Next
End Function
Function ShowWorld()
	For e.entlist=Each entlist
		ShowEntity e\ent
	Next
End Function
Function DeleteEnt(ent)
	For e.entlist=Each entlist
		If e\ent=ent
			Delete e
			Exit
		EndIf
	Next
End Function
Function ClearEntList()
		Delete Each entlist
End Function

</textarea> <br><br></td></tr></table><br>
<a name="1316009"></a>

<a name="1316010"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Worth a mention is the method demonstrated by sswift (small render of the glowing surfaces in fullbright, then blur of the texture by using several textured quads with an offset at up, down, left, right, upleft, upright, downleft, downright, then display the resulting texture on a quadmesh over the whole screen) not precise but good enough for some cases...<br>Here : <a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=754" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=754</a> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
