<!DOCTYPE html><html lang="en" ><head ><title >Mojo Vs Mojo 2</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Mojo Vs Mojo 2</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=507" >Monkey Programming</a>/<a href="#bottom" >Mojo Vs Mojo 2</a><br><br>
<a name="2106747"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hotshot</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I had good look at Bouncing Aliens code of Mojo and Mojo 2<br><br>Mojo<br><br><pre class=code>
Import mojo

Public

Const WIDTH#=320
Const HEIGHT#=240

Class Sprite

	Field x#,vx#
	Field y#,vy#
	Field f#,vf#
	
	Method New()
		x=Rnd( WIDTH )
		y=Rnd( HEIGHT )
		vx=Rnd(1,2);If Rnd(1)&gt;=.5 vx=-vx
		vy=Rnd(1,2);If Rnd(1)&gt;=.5 vy=-vy
		vf=Rnd( .5,1.5 )
	End

	Method Update()
		x+=vx;If x&lt;0 Or x&gt;=WIDTH vx=-vx
		y+=vy;If y&lt;0 Or y&gt;=HEIGHT vy=-vy
		f+=vf;If f&gt;=8 f-=8
	End
	
End

Class MyApp Extends App

	Field utime,uframes,ufps
	Field rtime,rframes,rfps
	
	Field image:Image
	Field image1:Image
	Field image2:Image
	Field sprites:=New Stack&lt;Sprite&gt;
	
	Field rot#
	
	Field ums

	Field fullscreen:=False
	
	Method ToggleFullscreen:Void()
		fullscreen=Not fullscreen
		If fullscreen
			SetDeviceWindow 1024,768,1
			SetSwapInterval 1			'I reckon there's a 98% chance this will give us 60fps on YOUR PC!
			SetUpdateRate 0
		Else
			SetDeviceWindow 640,480,0
			SetSwapInterval 0			'As for windowed mode...
			SetUpdateRate 60
		Endif
	End
	
	Method OnCreate()
	
		Print "Display modes:"
		For Local mode:=Eachin DisplayModes()
			Print mode.Width+","+mode.Height
		Next

		image1=LoadImage( "alien1.png",8,Image.MidHandle )
		image2=LoadImage( "alien2.png",8,Image.MidHandle )
		
		For Local i=0 Until 100
			sprites.Push New Sprite
		Next

		SetSwapInterval 0
		SetUpdateRate 60
		
		utime=Millisecs()
		rtime=utime
	End
	
	Method OnUpdate()
	
		If KeyHit( KEY_SPACE ) ToggleFullscreen
	
		ums=Millisecs

		uframes+=1
		Local e=Millisecs-utime
		If e&gt;=1000
			ufps=uframes
			uframes=0
			utime+=e
		Endif
	
		If MouseHit(0)
		
			If MouseX()&lt;DeviceWidth/3
				For Local i=0 Until 25
					If Not sprites.IsEmpty() sprites.Pop
				Next
			Else If MouseX()&gt;DeviceWidth*2/3
				For Local i=0 Until 25
					sprites.Push New Sprite
				Next
			Else
				If fullscreen ToggleFullscreen			
				OpenUrl "http://www.blitzbasic.com"
			Endif
		Endif
	
		For Local sprite:=Eachin sprites
			sprite.Update
		Next
		
		rot+=1
		
		ums=Millisecs-ums;

 	End
 	
 	Field paused:Bool
	
	Method OnRender()
	
		rframes+=1
		Local e=Millisecs-rtime
		If e&gt;=1000
			rfps=rframes
			rframes=0
			rtime+=e
		Endif
	
		Cls 128,0,255
		
		PushMatrix
				
		Scale DeviceWidth/WIDTH,DeviceHeight/HEIGHT
		
		Local r:=rot
		Local image:=image1
		Local i,n=sprites.Length()/10	'ie: simulate 10 render state changes
		random.Seed=1234
		For Local sprite:=Eachin sprites
			i+=1
			If i Mod n=0
				If image=image1 image=image2 Else image=image1
			End
			r+=Rnd(360)
			DrawImage image,sprite.x,sprite.y,r,Rnd(1,2),Rnd(1,2),sprite.f
		Next
		
		PopMatrix
				
		DrawText "[&lt;&lt;]",0,8,0,.5
		DrawText "imgs="+sprites.Length()+", ufps="+ufps+", rfps="+rfps+", last update="+ums,DeviceWidth/2,8,.5,.5
		DrawText "[&gt;&gt;]",DeviceWidth,8,1,.5
		
		If paused

			SetAlpha .5
			SetColor 0,0,0
			DrawRect 0,0,DeviceWidth,DeviceHeight
			SetAlpha 1
			SetColor 255,255,255
			DrawText "Suspended",DeviceWidth/2,DeviceHeight/2,.5,.5
		Endif

	End
	
	Method OnSuspend()
		Print "BouncyAliens: OnSuspend"
		paused=True
	End

	Method OnResume()
		Print "BouncyAliens: OnResume"
		paused=False
	End

End

Function Main()

	New MyApp
	
End
</pre><br><br>Mojo2<br><br><pre class=code>

'a port of the mojo1 banana...

Import mojo2

Public

Const WIDTH#=320
Const HEIGHT#=240

Class Sprite

	Field x#,vx#
	Field y#,vy#
	Field f#,vf#
	
	Method New()
		x=Rnd( WIDTH )
		y=Rnd( HEIGHT )
		vx=Rnd(1,2);If Rnd(1)&gt;=.5 vx=-vx
		vy=Rnd(1,2);If Rnd(1)&gt;=.5 vy=-vy
		vf=Rnd( .5,1.5 )
	End

	Method Update()
		x+=vx;If x&lt;0 Or x&gt;=WIDTH vx=-vx
		y+=vy;If y&lt;0 Or y&gt;=HEIGHT vy=-vy
		f+=vf;If f&gt;=8 f-=8
	End
	
End

Class MyApp Extends App

	Field canvas:Canvas

	Field utime,uframes,ufps
	Field rtime,rframes,rfps
	
	Field frames1:Image[]
	Field frames2:Image[]
	Field sprites:=New Stack&lt;Sprite&gt;
	
	Field rot#
	
	Field ums

	Field fullscreen:=False
	
	Method ToggleFullscreen:Void()
		fullscreen=Not fullscreen
		If fullscreen
			SetDeviceWindow DesktopMode.Width,DesktopMode.Height,1
			SetSwapInterval 1
			SetUpdateRate 0
		Else
			SetDeviceWindow 640,480,4
			SetSwapInterval 1
			SetUpdateRate 0
		Endif
	End
	
	Method OnCreate()
	
		canvas=New Canvas
		
		Print "Display modes:"
		For Local mode:=Eachin DisplayModes()
			Print mode.Width+","+mode.Height
		Next

		frames1=Image.LoadFrames( "alien1.png",8 )
		frames2=Image.LoadFrames( "alien2.png",8 )
		
		For Local i=0 Until 100
			sprites.Push New Sprite
		Next

		SetSwapInterval 1
		SetUpdateRate 0
		
		utime=Millisecs()
		rtime=utime
	End
	
	Method OnUpdate()
	
		If KeyHit( KEY_SPACE ) ToggleFullscreen
	
		ums=Millisecs

		uframes+=1
		Local e=Millisecs-utime
		If e&gt;=1000
			ufps=uframes
			uframes=0
			utime+=e
		Endif
	
		If MouseHit(0)
		
			If MouseX()&lt;DeviceWidth/3
				For Local i=0 Until 25
					If Not sprites.IsEmpty() sprites.Pop
				Next
			Else If MouseX()&gt;DeviceWidth*2/3
				For Local i=0 Until 25
					sprites.Push New Sprite
				Next
			Else
				If fullscreen ToggleFullscreen			
				OpenUrl "http://www.blitzbasic.com"
			Endif
		Endif
	
		For Local sprite:=Eachin sprites
			sprite.Update
		Next
		
		rot+=1
		
		ums=Millisecs-ums;

 	End
 	
 	Field paused:Bool
	
	Method OnRender()
	
		'in case device mode changed
		canvas.SetViewport 0,0,DeviceWidth,DeviceHeight
		canvas.SetScissor 0,0,DeviceWidth,DeviceHeight
	
		canvas.SetProjection2d 0,WIDTH,0,HEIGHT
	
		rframes+=1
		Local e=Millisecs-rtime
		If e&gt;=1000
			rfps=rframes
			rframes=0
			rtime+=e
		Endif
		
		canvas.Clear .5,0,1

		Local r:=rot
		Local frames:=frames1
		Local i,n=sprites.Length()/10	'ie: simulate 10 render state changes
		random.Seed=1234
		For Local sprite:=Eachin sprites
			i+=1
			If i Mod n=0
				If (i/n)&amp;1 frames=frames2 Else frames=frames1
			End
			r+=Rnd(360)
			canvas.DrawImage frames[sprite.f],sprite.x,sprite.y,r,Rnd(1,2),Rnd(1,2)
		Next
		
		canvas.SetProjection2d 0,DeviceWidth,0,DeviceHeight
		
		canvas.DrawText "[&lt;&lt;]",0,8,0,.5
		canvas.DrawText "imgs="+sprites.Length()+", ufps="+ufps+", rfps="+rfps+", last update="+ums,DeviceWidth/2,8,.5,.5
		canvas.DrawText "[&gt;&gt;]",DeviceWidth,8,1,.5
		
		If paused

			canvas.SetColor 0,0,0,.5
			canvas.DrawRect 0,0,DeviceWidth,DeviceHeight
			canvas.SetColor 1,1,1,1
			canvas.DrawText "Suspended",DeviceWidth/2,DeviceHeight/2,.5,.5
			canvas.SetColor 1,1,1,1
			
		Endif
		
		canvas.Flush

	End
	
	Method OnSuspend()
		Print "BouncyAliens: OnSuspend"
		paused=True
	End

	Method OnResume()
		Print "BouncyAliens: OnResume"
		paused=False
	End

End

Function Main()

	New MyApp
	
End
</pre><br><br>There is some different between them. So the questions I like to ask is will Mojo2 have more things under the hood? <br><br></td></tr></table><br>
<a name="2106746"></a>

<a name="2106777"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nobuyuki</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> you have to set up more things, but you gain more control over the drawing surface. <br><br></td></tr></table><br>
<a name="2106786"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skape</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> ^^That... Mojo2 has built-in shader support, and some support for lights and shadows with little extra work. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
