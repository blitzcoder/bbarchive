<!DOCTYPE html><html lang="en" ><head ><title >bug challenge, I think?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >bug challenge, I think?</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=507" >Monkey Programming</a>/<a href="#bottom" >bug challenge, I think?</a><br><br>
<a name="2120787"></a>

<a name="2120788"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have a problem, I want some object to move the same as in space invaders, from left to right and from right to left. It works fine moving right but not moving left. It goes beyond the edge of the screen which shouldn't.<br>the error seems to be in the aliens Update() method. I am braking my head and can't figure it out:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import mojo2

Global canvas:Canvas

Function Main()
	New Game
	
End Function
Class Game Extends App
	Field alienManager:AlienManager
	
	Method OnCreate()
			alienManager = New AlienManager()
			canvas = New Canvas
			SetUpdateRate(30)
	End Method
	
	Method OnUpdate()
		alienManager.Update()
	End Method
	
	Method OnRender()
		alienManager.Render()
	End Method
	
End Class

Class PVector2D
	Field x:Float
	Field y:Float
	
	Method New(x:Float,y:Float)
		Set(x,y)
	End Method
	
	Method New(x1:Float,y1:Float,x2:Float,y2:Float)
		Set(x2-x1,y2-y1)
	End Method
	
	Method New(v:PVector2D)
		Set(v)
	End Method
	
	Method Set:PVector2D(v:PVector2D)
		x = v.x
		y = v.y
		Return Self
	End Method

	Method Set:PVector2D(x:Float,y:Float)
		Self.x = x
		Self.y = y
		Return Self
	End Method
		
	Method Add:PVector2D(x:Float,y:Float)
		Self.x += x
		Self.y += y
		Return Self
	End Method
	
	Method Add:PVector2D(v:PVector2D)
		Self.x += v.x
		Self.y += v.y
		Return Self
	End Method
	
	Method Add:PVector2D(value:Float)
		Self.x += value
		Self.y += value
		Return Self
	End Method
End Class

Class Alien
	Field pos:PVector2D
	Field image:mojo2.Image[]
	Field time:Int
	Field delay:Int
	Field waitAmount:Int
	Field moveLeft:PVector2D
	Field moveRight:PVector2D
	Field moveDown:PVector2D
	
	Global direction:PVector2D

	
	Method New(x:Float,y:Float)
		pos = New PVector2D(x,y)
		time = Millisecs()
		delay  = 500
		moveLeft = New PVector2D(-10,0)
		moveRight = New PVector2D(10,0)
		moveDown = New PVector2D(0,10)
		direction = moveRight
		
	End Method
	
	
	Method Update:int()
		If time + delay &lt; Millisecs()
			pos.Add(direction)
			time = Millisecs()
			If pos.x &gt; 500 And direction = moveRight
				direction = moveLeft
				Return True
			Elseif pos.x &lt; 140 And direction = moveLeft			
				direction = moveRight
				Return True
			Endif
		Endif
		Return False
	End Method
	
	Method DropDown()
		'pos.Add(moveDown)
	End Method
	
	Method Render()
		canvas.SetColor(1,1,1,1)
		canvas.SetColor(.8,.4,.2,1)
		canvas.DrawOval(pos.x,pos.y,28,28)
	End Method
	
End Class
	
Class AlienManager

	
	Field list:List&lt;Alien&gt;
	
	Method New()
		list = New List&lt;Alien&gt;
		For Local i = 0 Until 8
			Local alien := New Alien(100+i*40, 100)
			list.AddLast(alien)
		Next
	End Method
	
	Method Update()
		Local alienLink:list.Node&lt;Alien&gt; = list.FirstNode()
		Local drop:Bool = False
		While alienLink
			If Alien(alienLink.Value()).Update() = True 
				
			Endif
			alienLink = alienLink.NextNode()
		Wend 
	End Method
	
	Method Render()
		canvas.Clear()
		Local alienLink:list.Node&lt;Alien&gt; = list.FirstNode()
		While alienLink
			Alien(alienLink.Value()).Render()
			alienLink = alienLink.NextNode()
		Wend
		canvas.Flush()
	End Method
	
End Class
</textarea><br><br>any help appreciated. <br><br></td></tr></table><br>
<a name="2120790"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey Jesse... looks like it is checking only the far right "alien", I've made the code so you can see what is happening:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Import mojo2

Global canvas:Canvas

Function Main()
	New Game
	
End Function
Class Game Extends App
	Field alienManager:AlienManager
	
	Method OnCreate()
			alienManager = New AlienManager()
			canvas = New Canvas
			SetUpdateRate(120)
	End Method
	
	Method OnUpdate()
		alienManager.Update()
	End Method
	
	Method OnRender()
		alienManager.Render()
		canvas.SetColor(1,1,1)
		canvas.DrawLine(140,0,140,480)
		canvas.DrawLine(500,0,500,480)
		canvas.Flush
	End Method
	
End Class

Class PVector2D
	Field x:Float
	Field y:Float
	
	Method ToString:String()
		Return "x = " + x + " y = " + y
	End
	
	Method New(x:Float,y:Float)
		Set(x,y)
	End Method
	
	Method New(x1:Float,y1:Float,x2:Float,y2:Float)
		Set(x2-x1,y2-y1)
	End Method
	
	Method New(v:PVector2D)
		Set(v)
	End Method
	
	Method Set:PVector2D(v:PVector2D)
		x = v.x
		y = v.y
		Return Self
	End Method

	Method Set:PVector2D(x:Float,y:Float)
		Self.x = x
		Self.y = y
		Return Self
	End Method
		
	Method Add:PVector2D(x:Float,y:Float)
		Self.x += x
		Self.y += y
		Return Self
	End Method
	
	Method Add:PVector2D(v:PVector2D)
		Self.x += v.x
		Self.y += v.y
		Return Self
	End Method
	
	Method Add:PVector2D(value:Float)
		Self.x += value
		Self.y += value
		Return Self
	End Method
End Class

Class Alien
	Field pos:PVector2D
	Field image:mojo2.Image[]
	Field time:Int
	Field delay:Int
	Field waitAmount:Int
	Field moveLeft:PVector2D
	Field moveRight:PVector2D
	Field moveDown:PVector2D
	Field r#,g#,b#
	Global direction:PVector2D

	
	Method New(x:Float,y:Float)
		pos = New PVector2D(x,y)
		time = Millisecs()
		delay  = 40
		moveLeft = New PVector2D(-10,0)
		moveRight = New PVector2D(10,0)
		moveDown = New PVector2D(0,10)
		direction = moveLeft
		r = 0
		g = 0
		b = 0
	End Method
	
	
	Method Update:int()
		If time + delay &lt; Millisecs()
			pos.Add(direction)
			time = Millisecs()
			If direction = moveRight
				r = 1
				g = 0
				b = 0
			Else If direction = moveLeft	
				r = 0
				g = 1
				b = 0
			End
			
			If pos.x &gt; 500 And direction = moveRight
				direction = moveLeft
				Return True
			Elseif pos.x &lt; 140 And direction = moveLeft			
				direction = moveRight
				Return True
			Endif
		Endif
		Return False
	End Method
	
	Method DropDown()
		'pos.Add(moveDown)
	End Method
	
	Method Render()
		canvas.SetColor(1,1,1,1)
		canvas.SetColor(r,g,b,1)
		canvas.DrawOval(pos.x,pos.y,28,28)
		canvas.SetColor(1,1,1,1)
		canvas.DrawText(pos.x, pos.x, pos.y)
	End Method
	
End Class
	
Class AlienManager

	
	Field list:List&lt;Alien&gt;
	
	Method New()
		list = New List&lt;Alien&gt;
		For Local i = 0 Until 8
			Local alien := New Alien(100+i*40, 100)
			list.AddLast(alien)
		Next
	End Method
	
	Method Update()
		Local alienLink:list.Node&lt;Alien&gt; = list.FirstNode()
		Local drop:Bool = False
		While alienLink
			If Alien(alienLink.Value()).Update() = True 
				
			Endif
			alienLink = alienLink.NextNode()
		Wend 
	End Method
	
	Method Render()
		canvas.Clear()
		Local alienLink:list.Node&lt;Alien&gt; = list.FirstNode()
		While alienLink
			Alien(alienLink.Value()).Render()
			alienLink = alienLink.NextNode()
		Wend
		canvas.Flush()
	End Method
	
End Class</textarea><br><br>Still checking... <br><br></td></tr></table><br>
<a name="2120791"></a>

<a name="2120792"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ahhhhh!!!<br><br><pre class=code>	Method New(x:Float,y:Float)
		pos = New PVector2D(x,y)
		time = Millisecs()
		delay  = 40
		moveLeft = New PVector2D(-10,0)
		moveRight = New PVector2D(10,0)
		moveDown = New PVector2D(0,10)
		direction = moveLeft
		r = 0
		g = 0
		b = 0
	End Method</pre><br><br>The direction object points to the last one in the list as you create the aliens! So it is the only last alien which direction will compare against successfully!<br><br>It'll have to be something like this:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Import mojo2

Global canvas:Canvas

Function Main()
	New Game
	
End Function
Class Game Extends App
	Field alienManager:AlienManager
	
	Method OnCreate()
			alienManager = New AlienManager()
			canvas = New Canvas
			SetUpdateRate(120)
	End Method
	
	Method OnUpdate()
		alienManager.Update()
	End Method
	
	Method OnRender()
		alienManager.Render()
		canvas.SetColor(1,1,1)
		canvas.DrawLine(140,0,140,480)
		canvas.DrawLine(500,0,500,480)
		canvas.Flush
	End Method
	
End Class

Class PVector2D
	Field x:Float
	Field y:Float
	
	Method ToString:String()
		Return "x = " + x + " y = " + y
	End
	
	Method Compare:Bool(v:PVector2D)
		Return x = v.x And y = v.y
	End
	
	Method New(x:Float,y:Float)
		Set(x,y)
	End Method
	
	Method New(x1:Float,y1:Float,x2:Float,y2:Float)
		Set(x2-x1,y2-y1)
	End Method
	
	Method New(v:PVector2D)
		Set(v)
	End Method
	
	Method Set:PVector2D(v:PVector2D)
		x = v.x
		y = v.y
		Return Self
	End Method

	Method Set:PVector2D(x:Float,y:Float)
		Self.x = x
		Self.y = y
		Return Self
	End Method
		
	Method Add:PVector2D(x:Float,y:Float)
		Self.x += x
		Self.y += y
		Return Self
	End Method
	
	Method Add:PVector2D(v:PVector2D)
		Self.x += v.x
		Self.y += v.y
		Return Self
	End Method
	
	Method Add:PVector2D(value:Float)
		Self.x += value
		Self.y += value
		Return Self
	End Method
End Class

Class Alien
	Field pos:PVector2D
	Field image:mojo2.Image[]
	Field time:Int
	Field delay:Int
	Field waitAmount:Int
	Field moveLeft:PVector2D
	Field moveRight:PVector2D
	Field moveDown:PVector2D
	Field r#,g#,b#
	Global direction:PVector2D
	Field left:PVector2D
	Field right:PVector2D
	
	Method New(x:Float,y:Float)
		pos = New PVector2D(x,y)
		time = Millisecs()
		delay  = 40
		moveLeft = New PVector2D(-10,0)
		moveRight = New PVector2D(10,0)
		moveDown = New PVector2D(0,10)
		left = New PVector2D(-10,0)
		right = New PVector2D(10,0)
		direction = right
		r = 0
		g = 0
		b = 0
	End Method
	
	
	Method Update:int()
		If time + delay &lt; Millisecs()
			pos.Add(direction)
			time = Millisecs()
			If direction.Compare(moveRight)
				r = 1
				g = 0
				b = 0
			Else If direction.Compare(moveLeft)
				r = 0
				g = 1
				b = 0
			End
			
			If pos.x &gt; 500 And direction.Compare(moveRight)
				direction = left
				Return True
			Elseif pos.x &lt; 140 And direction.Compare(moveLeft)
				direction = right
				Return True
			Endif
		Endif
		Return False
	End Method
	
	Method DropDown()
		'pos.Add(moveDown)
	End Method
	
	Method Render()
		canvas.SetColor(1,1,1,1)
		canvas.SetColor(r,g,b,1)
		canvas.DrawOval(pos.x,pos.y,28,28)
		canvas.SetColor(1,1,1,1)
		canvas.DrawText(pos.x, pos.x, pos.y)
	End Method
	
End Class
	
Class AlienManager

	
	Field list:List&lt;Alien&gt;
	
	Method New()
		list = New List&lt;Alien&gt;
		For Local i = 0 Until 8
			Local alien := New Alien(100+i*40, 100)
			list.AddLast(alien)
		Next
	End Method
	
	Method Update()
		'Local alienLink:list.Node&lt;Alien&gt; = list.FirstNode()
		'Local drop:Bool = False
		'While alienLink
	'		If Alien(alienLink.Value()).Update() = True 
'				'
'			Endif
'			alienLink = alienLink.NextNode()
'		Wend 
		
		For Local a:Alien = Eachin list
			a.Update()
		Next
	End Method
	
	Method Render()
		canvas.Clear()
		Local alienLink:list.Node&lt;Alien&gt; = list.FirstNode()
		While alienLink
			Alien(alienLink.Value()).Render()
			alienLink = alienLink.NextNode()
		Wend
		canvas.Flush()
	End Method
	
End Class</textarea> <br><br></td></tr></table><br>
<a name="2120793"></a>

<a name="2120794"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Therevills:<br>&gt;Hey Jesse... looks like it is checking only the far right "alien"<br>what I find odd is that direction is global so if one alien goes right all of them need to go right. It also checks every alien to see if it went beyond the left edge then it switches direction as it does for the right but for some reason it only works while  moving right. <br><br></td></tr></table><br>
<a name="2120797"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> It does work moving left, but you have to wait for the far right (last) alien to move past 180.<br><br>When you assign direction, you are assigning it every time when you create a new alien and so when the last alien is created it has the value of the last alien's right, therefore when you compare (If pos.x &gt; 500 And direction = moveRight) direction is only the same as the last alien's right and then you assign it to be the same as the last alien's left. So again when you compare (Elseif pos.x &lt; 140 And direction = moveLeft) it is only the same as the last alien's left. <br><br></td></tr></table><br>
<a name="2120799"></a>

<a name="2120800"></a>

<a name="2120801"></a>

<a name="2120802"></a>

<a name="2120803"></a>

<a name="2120804"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> check it out,  I just figured out that if I replace the pointer comparison in the alien.Update with the direction variable, the movement works. There is another bug but that I will deal with differently. There seems to be something wrong with the pointer comparison:<br><pre class=code>
	Method Update:int()
		If time + delay &lt; Millisecs()
			pos.Add(direction)
			time = Millisecs()
			If pos.x &gt; 500 And direction.x = moveRight.x '***********************************
				direction = moveLeft
				Return True
			Elseif pos.x &lt; 140 And direction.x = moveLeft.x	'*****************************		
				direction = moveRight
				Return True
			Endif
		Endif
		Return False
	End Method
</pre> <br><br></td></tr></table><br>
<a name="2120805"></a>

<a name="2120806"></a>

<a name="2120807"></a>

<a name="2120808"></a>

<a name="2120809"></a>

<a name="2120810"></a>

<a name="2120811"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> here it is working:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import mojo2

Global canvas:Canvas

Function Main()
	New Game	
End Function

Class Game Extends App
	Field alienManager:AlienManager
	
	Method OnCreate()
			alienManager = New AlienManager()
			canvas = New Canvas
			SetUpdateRate(30)
	End Method
	
	Method OnUpdate()
		alienManager.Update()
	End Method
	
	Method OnRender()
		alienManager.Render()
	End Method
	
End Class

Class PVector2D
	Field x:Float
	Field y:Float
	
	Method New(x:Float,y:Float)
		Set(x,y)
	End Method
	
	Method New(x1:Float,y1:Float,x2:Float,y2:Float)
		Set(x2-x1,y2-y1)
	End Method
	
	Method New(v:PVector2D)
		Set(v)
	End Method
	
	Method Set:PVector2D(v:PVector2D)
		x = v.x
		y = v.y
		Return Self
	End Method

	Method Set:PVector2D(x:Float,y:Float)
		Self.x = x
		Self.y = y
		Return Self
	End Method
		
	Method Add:PVector2D(x:Float,y:Float)
		Self.x += x
		Self.y += y
		Return Self
	End Method
	
	Method Add:PVector2D(v:PVector2D)
		Self.x += v.x
		Self.y += v.y
		Return Self
	End Method
	
	Method Add:PVector2D(value:Float)
		Self.x += value
		Self.y += value
		Return Self
	End Method
End Class

Class Alien
	Field pos:PVector2D
	Field moveLeft:PVector2D
	Field moveRight:PVector2D
	Field moveDown:PVector2D
	
	Global direction:PVector2D

	
	Method New(x:Float,y:Float)
		pos = New PVector2D(x,y)
		moveLeft = New PVector2D(-10,0)
		moveRight = New PVector2D(10,0)
		moveDown = New PVector2D(0,10)
		direction = moveRight
		
	End Method
	
	Method move()
			pos.Add(direction)
	End Method
	
	Method Update:int()
			If pos.x &gt; 500 And direction.x = moveRight.x
				direction = moveLeft
				Return True
			Elseif pos.x &lt; 140 And direction.x = moveLeft.x			
				direction = moveRight
				Return True
			Endif
		Return False
	End Method
	
	Method Render()
		canvas.SetColor(1,1,1,1)
		canvas.SetColor(.8,.4,.2,1)
		canvas.DrawOval(pos.x,pos.y,28,28)
	End Method
	
End Class

	
Class AlienManager

	Field list:List&lt;Alien&gt;
	Field time:Int
	Field delay:Int
	Field waitAmount:Int
	
	Method New()
		time = Millisecs()
		delay  = 500
		list = New List&lt;Alien&gt;
		For Local i = 0 Until 8
			Local alien := New Alien(100+i*40, 100)
			list.AddLast(alien)
		Next
	End Method
	
	Method Update()
		If time + delay &lt; Millisecs()
			time = Millisecs()
			For Local alien:Alien = Eachin list
				alien.move() 
				
			Next
		Endif 
		For Local alien:Alien = Eachin list
			alien.Update()
		Next
	End Method
	
	Method Render()
		canvas.Clear()
		For Local alien:Alien = Eachin list
			alien.Render()
		Next
		canvas.Flush()
	End Method
	
End Class</textarea><br>I still think there is something wrong with the pointer comparison. What do you think? I have used it in several games and works fine but for some reason it's not working there.<br>FYI, I just added.x to the pointer comparison and move the timer into the alien manager class. and I separated movement from comparison so it would not break the patter.<br>if you remove the x rom the direction comparison it should still fail which is an enigma to me. <br><br></td></tr></table><br>
<a name="2120812"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >muddy_shoes</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> You have direction as a global within the Alien class and moveRight and moveLeft as fields so comparing them by "pointer" would only be valid for whichever Alien instance happened to set the direction last time around as the moveLeft and moveRight references are unique for each Alien. <br><br></td></tr></table><br>
<a name="2120814"></a>

<a name="2120815"></a>

<a name="2120816"></a>

<a name="2120817"></a>

<a name="2120818"></a>

<a name="2120819"></a>

<a name="2120820"></a>

<a name="2120821"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> yes, and all it needs is to find one thats beyond the edge for it to change direction for all and thats why I used the global movement. <br>I don't know if you notice but I just added x's  to the comparison and it worked. so technically it would be the same comparison except that now instead of pointers is values.<br><br>Sorry Muddy but maybe I am just being stubborn  but my brain logic don't want to let it go that easy.<br><br>this is technically what is going on:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import mojo2

Function Main()

	Local temp:Test
	Local a:Test = New Test(3)
	Local b:Test = New Test(4)
	temp = a

	'pointer comparison
	If a = temp
		Print b.x
	Else 
		Print "Invalid"
	Endif
	'value comparison
	If a.x = temp.x
		Print b.x
	Else
		Print "Invalid"
	Endif

	'pointer comparison
	If a = temp
		Print b.x
	Else 
		Print "Invalid"
	Endif
	'value comparison
	If a.x = temp.x
		Print b.x
	Else
		Print "Invalid"
	Endif
End Function
Class Test
	Field x
	Method New(a:Int)
		x = a
	End Method
End Class
</textarea><br>if you notice they produce the same output. and it's working fine here but not in the code above. <br><br></td></tr></table><br>
<a name="2120822"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >muddy_shoes</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> It's not technically the same comparison as you explicitly set all the values to be the same but the "pointers" are all different as I said (and therevills seems to have tried to explain). With the value comparison any of the Alien instance tests will work and with the "pointer" comparison only one will, specifically the last created instance with the code as you have it now. <br><br></td></tr></table><br>
<a name="2120823"></a>

<a name="2120824"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> sorry, Muddy but explain to me this one now then<br>wait for the second pass:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import mojo2

Global canvas:Canvas

Function Main()
	New Game	
End Function

Class Game Extends App
	Field alienManager:AlienManager
	
	Method OnCreate()
			alienManager = New AlienManager()
			canvas = New Canvas
			SetUpdateRate(30)
	End Method
	
	Method OnUpdate()
		alienManager.Update()
	End Method
	
	Method OnRender()
		alienManager.Render()
	End Method
	
End Class

Class PVector2D
	Field x:Float
	Field y:Float
	
	Method New(x:Float,y:Float)
		Set(x,y)
	End Method
	
	Method New(x1:Float,y1:Float,x2:Float,y2:Float)
		Set(x2-x1,y2-y1)
	End Method
	
	Method New(v:PVector2D)
		Set(v)
	End Method
	
	Method Set:PVector2D(v:PVector2D)
		x = v.x
		y = v.y
		Return Self
	End Method

	Method Set:PVector2D(x:Float,y:Float)
		Self.x = x
		Self.y = y
		Return Self
	End Method
		
	Method Add:PVector2D(x:Float,y:Float)
		Self.x += x
		Self.y += y
		Return Self
	End Method
	
	Method Add:PVector2D(v:PVector2D)
		Self.x += v.x
		Self.y += v.y
		Return Self
	End Method
	
	Method Add:PVector2D(value:Float)
		Self.x += value
		Self.y += value
		Return Self
	End Method
End Class

Class Alien
	Field pos:PVector2D
	Field moveLeft:PVector2D
	Field moveRight:PVector2D
	Field moveDown:PVector2D
	
	Global direction:PVector2D

	
	Method New(x:Float,y:Float)
		pos = New PVector2D(x,y)
		moveLeft = New PVector2D(-10,0)
		moveRight = New PVector2D(10,0)
		moveDown = New PVector2D(0,10)
		direction = moveRight
		
	End Method
	
	Method move()
			pos.Add(direction)
	End Method
	
	Method Update:int()
			If pos.x &gt; 500 And direction = moveRight
				direction = moveLeft
				Return True
			Elseif pos.x &lt; 140 And direction.x = moveLeft.x			
				direction = moveRight
				Return True
			Endif
		Return False
	End Method
	
	Method Render()
		canvas.SetColor(1,1,1,1)
		canvas.SetColor(.8,.4,.2,1)
		canvas.DrawOval(pos.x,pos.y,28,28)
	End Method
	
End Class

	
Class AlienManager

	Field list:List&lt;Alien&gt;
	Field time:Int
	Field delay:Int
	Field waitAmount:Int
	
	Method New()
		time = Millisecs()
		delay  = 100
		list = New List&lt;Alien&gt;
		For Local i = 0 Until 8
			Local alien := New Alien(100+i*40, 100)
			list.AddLast(alien)
		Next
	End Method
	
	Method Update()
		If time + delay &lt; Millisecs()
			time = Millisecs()
			For Local alien:Alien = Eachin list
				alien.move() 
				
			Next
		Endif 
		For Local alien:Alien = Eachin list
			alien.Update()
		Next
	End Method
	
	Method Render()
		canvas.Clear()
		For Local alien:Alien = Eachin list
			alien.Render()
		Next
		canvas.Flush()
	End Method
	
End Class
</textarea> <br><br></td></tr></table><br>
<a name="2120825"></a>

<a name="2120826"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >muddy_shoes</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> You don't say what is occurring that you don't understand so it's a little difficult to know what you want me to explain. I'd guess you've failed to notice the inconsistency in your update:<br><br><pre class=code>
Method Update:int()
			If pos.x &gt; 500 And direction = moveRight
				direction = moveLeft
				Return True
			Elseif pos.x &lt; 140 And direction.x = moveLeft.x			
				direction = moveRight
				Return True
			Endif
		Return False
	End Method
</pre><br><br>Going right you test by reference and going left you test by value. The direction reference in use therefore swaps from the last alien to the first on the second pass. Just putting some prints in shows what is happening:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import mojo2

Global canvas:Canvas

Function Main()
	New Game
End Function

Class Game Extends App
	Field alienManager:AlienManager
	
	Method OnCreate()
		alienManager = New AlienManager()
		canvas = New Canvas
		SetUpdateRate(30)
	End Method
	
	Method OnUpdate()
		alienManager.Update()
	End Method
	
	Method OnRender()
		alienManager.Render()
	End Method
	
End Class

Class PVector2D
	Field x:Float
	Field y:Float
	
	Method New(x:Float,y:Float)
		Set(x,y)
	End Method
	
	Method New(x1:Float,y1:Float,x2:Float,y2:Float)
		Set(x2-x1,y2-y1)
	End Method
	
	Method New(v:PVector2D)
		Set(v)
	End Method
	
	Method Set:PVector2D(v:PVector2D)
		x = v.x
		y = v.y
		Return Self
	End Method

	Method Set:PVector2D(x:Float,y:Float)
		Self.x = x
		Self.y = y
		Return Self
	End Method
		
	Method Add:PVector2D(x:Float,y:Float)
		Self.x += x
		Self.y += y
		Return Self
	End Method
	
	Method Add:PVector2D(v:PVector2D)
		Self.x += v.x
		Self.y += v.y
		Return Self
	End Method
	
	Method Add:PVector2D(value:Float)
		Self.x += value
		Self.y += value
		Return Self
	End Method
End Class

Class Alien
	Global alienCount:Int
	Field id:Int
	Field pos:PVector2D
	Field moveLeft:PVector2D
	Field moveRight:PVector2D
	Field moveDown:PVector2D
	

	Global direction:PVector2D
	
	Method New(x:Float,y:Float)
		id = alienCount
		alienCount += 1
		pos = New PVector2D(x,y)
		moveLeft = New PVector2D(-10,0)
		moveRight = New PVector2D(10,0)
		moveDown = New PVector2D(0,10)
		direction = moveRight
		Print("Direction set by Alien id - " + String(id))	
	End Method
	
	Method move()
		pos.Add(direction)
	End Method
	
	Method Update:int()
		If pos.x &gt; 500 And direction = moveRight
			direction = moveLeft
			Print("Direction set by Alien id - " + String(id))
			Return True
		Elseif pos.x &lt; 140 And direction.x = moveLeft.x
			direction = moveRight
			Print("Direction set by Alien id - " + String(id))
			Return True
		Endif
		Return False
	End Method
	
	Method Render()
		canvas.SetColor(1,1,1,1)
		canvas.SetColor(.8,.4,.2,1)
		canvas.DrawOval(pos.x,pos.y,28,28)
	End Method
	
End Class

	
Class AlienManager

	Field list:List&lt;Alien&gt;
	Field time:Int
	Field delay:Int
	Field waitAmount:Int
	
	Method New()
		time = Millisecs()
		delay  = 100
		list = New List&lt;Alien&gt;
		For Local i = 0 Until 8
			Local alien := New Alien(100+i*40, 100)
			list.AddLast(alien)
		Next
	End Method
	
	Method Update()
		If time + delay &lt; Millisecs()
			time = Millisecs()
			For Local alien:Alien = Eachin list
				alien.move()
				
			Next
		Endif
		For Local alien:Alien = Eachin list
			alien.Update()
		Next
	End Method
	
	Method Render()
		canvas.Clear()
		For Local alien:Alien = Eachin list
			alien.Render()
		Next
		canvas.DrawLine(140,0,140,canvas.Height)
		canvas.DrawLine(500,0,500,canvas.Height)
		canvas.Flush()
	End Method
	
End Class
</textarea> <br><br></td></tr></table><br>
<a name="2120827"></a>

<a name="2120828"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Muddy and Threvills thank for you time and effort in helping me.<br>Muddy, well it's being working since several post above.  so I don't really need a solution anymore  and to continue this redundant conversation is pointless because I am being stubborn and you guys are not seeing my point. I appreciate your help. Maybe sometime in the future I will see your guy's point. and I will have one of those ahhhh! moments. <br><br></td></tr></table><br>
<a name="2120835"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> In the code I posted above you can see I created a Compare method in the PVector2D class:<br><pre class=code>Method Compare:Bool(v:PVector2D)
	Return x = v.x And y = v.y
End</pre><br><br>Which allows you to compare the direction object and moveRight object by value:<br><pre class=code>If pos.x &gt; 500 And direction.Compare(moveRight)</pre> <br><br></td></tr></table><br>
<a name="2120837"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Therevills. I guess I am guilty for not really looking at the code thoroughly and since it wasn't what I wanted to figure out I partially ignored your code. But you are right.<br>and thanks for your help. <br><br></td></tr></table><br>
<a name="2120864"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just for fun a MX2 version using a struct instead of a class for the PVector2D:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Namespace aliens

#Import "&lt;std&gt;"
#Import "&lt;mojo&gt;"

Using std..
Using mojo..

Function Main()
	New AppInstance
	New MyWindow("Alien Game!", App.DesktopSize.x / 2, App.DesktopSize.y / 2)
	App.Run()
End

Class MyWindow Extends Window
	Field alienManager:AlienManager
	
	Method New(title:String, width:Int, height:Int)
		Super.New(title, width, height, WindowFlags.Resizable)
		alienManager = New AlienManager()
		ClearColor = Color.Black
		SwapInterval = 1
	End

	Method OnUpdate()
		alienManager.Update()
	End
	
	Method OnRender(canvas:Canvas) Override
		App.RequestRender()
		OnUpdate()
		alienManager.Render(canvas)
	End
End

Struct PVector2D
	Field x:Float
	Field y:Float
	
	Method New(x:Float,y:Float)
		Set(x,y)
	End
	
	Method New(x1:Float,y1:Float,x2:Float,y2:Float)
		Set(x2-x1,y2-y1)
	End
	
	Method Set:PVector2D(v:PVector2D)
		x = v.x
		y = v.y
		Return Self
	End

	Method Set:PVector2D(x:Float,y:Float)
		Self.x = x
		Self.y = y
		Return Self
	End
		
	Method Add:PVector2D(x:Float,y:Float)
		Self.x += x
		Self.y += y
		Return Self
	End
	
	Method Add:PVector2D(v:PVector2D)
		Self.x += v.x
		Self.y += v.y
		Return Self
	End
	
	Method Add:PVector2D(value:Float)
		Self.x += value
		Self.y += value
		Return Self
	End
End

Class Alien
	Field pos:PVector2D
	Field moveLeft:PVector2D
	Field moveRight:PVector2D
	Field moveDown:PVector2D
	
	Global direction:PVector2D

	Method New(x:Float,y:Float)
		pos = New PVector2D(x,y)
		moveLeft = New PVector2D(-10,0)
		moveRight = New PVector2D(10,0)
		moveDown = New PVector2D(0,10)
		direction = moveRight
		
	End
	
	Method Move()
		pos.Add(direction)
	End
	
	Method Update:Int()
		If pos.x &gt; 500 And direction = moveRight
			direction = moveLeft
			Return True
		Elseif pos.x &lt; 140 And direction = moveLeft			
			direction = moveRight
			Return True
		End
		Return False
	End
	
	Method Render(canvas:Canvas)
		canvas.Color = New Color(1,1,1,1)
		canvas.Color = New Color(.8,.4,.2,1)
		canvas.DrawOval(pos.x,pos.y,28,28)
	End
	
End

	
Class AlienManager

	Field list:List&lt;Alien&gt;
	Field time:Int
	Field delay:Int
	Field waitAmount:Int
	
	Method New()
		time = Millisecs()
		delay = 100
		list = New List&lt;Alien&gt;
		For Local i:Int = 0 Until 8
			Local alien := New Alien(100+i*40, 100)
			list.AddLast(alien)
		Next
	End
	
	Method Update()
		If time + delay &lt; Millisecs()
			time = Millisecs()
			For Local alien:Alien = Eachin list
				alien.Move() 
			Next
		End
		For Local alien:Alien = Eachin list
			alien.Update()
		Next
	End
	
	Method Render(canvas:Canvas)
		For Local alien:Alien = Eachin list
			alien.Render(canvas)
		Next
		canvas.Flush()
	End
End</textarea><br><br>Notice that we can now do the compare using the struct directly (as structs are by value).<br><br>And if you want to live dangerously you could do operator overloading on the vector:<br><br><pre class=code>Operator+:PVector2D(v:PVector2D)
	Return New PVector2D(x + v.x, y + v.y)
End</pre><br><pre class=code>Method Move()
	pos += direction
End</pre> <br><br></td></tr></table><br>
<a name="2120863"></a>

<a name="2120862"></a>

<a name="2120861"></a>

<a name="2120860"></a>

<a name="2120859"></a>

<a name="2120858"></a>

<a name="2120890"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> all that sounds fun but I don't want to get into it until the official release. I am struggling as it is with monkey.<br>Thanks. <br><br></td></tr></table><br>
<a name="2120889"></a>

<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
