<!DOCTYPE html><html lang="en" ><head ><title >Can't get my swipe method to work correctly</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Can't get my swipe method to work correctly</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=507" >Monkey Programming</a>/<a href="#bottom" >Can't get my swipe method to work correctly</a><br><br>
<a name="2041028"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tri|Ga|De</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm trying to make a swipe method but I'm having a little trouble with it.<br><br><br><pre class=code>
	'**********************************
	'** Check Swipe
	'**********************************
	Method CheckSwipe:Int()
		Local result:Int = 0
		
		If touching = 0 And TouchDown(0) = 1 Then
			touching = 1
			touchingXstart = TouchX()
			touchingYstart = TouchY()
		End
		If TouchDown(0) = 0  And touching = 1 Then
			touching = 0
			touchingXend = TouchX()
			touchingYend = TouchY()
			
			If touchingXend &gt; touchingXstart Then result = 1
			If touchingXend &lt; touchingXstart Then result = 3
			If touchingYend &gt; touchingYstart Then result = 2
			If touchingYend &lt; touchingYstart Then result = 4
	
			touchingXstart = 0
			touchingXend = 0
			touchingYstart = 0
			touchingYend = 0
		End

		Return result
	End	
</pre><br><br>The touching variables are defined as Integers.<br><br>I then call it like this in OnUpdate.<br><br><pre class=code>
swipeResult = CheckSwipe()
</pre><br><br>I want the swipeResult to give this results.<br><br>1 = swipe right<br>2 = swipe down<br>3 = swipe left<br>4 = swipe up.<br><br>BUT I can't get it working, any one got any idea on how to solve this? <br><br></td></tr></table><br>
<a name="2041030"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Goodlookinguy</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> The results are overwriting each other because of the way you setup the if statements.<br><br>If start x is less than end x, but then if start y is less than end y as well, the result is 2, even if you were aiming 1. <br><br></td></tr></table><br>
<a name="2041029"></a>

<a name="2041031"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tri|Ga|De</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Okay tried it differently but still not working.<br><br><pre class=code>

	'**********************************
	'** Check Swipe
	'**********************************
	Method CheckSwipe:Int()
		Local result:Int = 0
		
		If touching = 0 And TouchDown(0) = 1 Then
			touching = 1
			touchingXstart = TouchX()
			touchingYstart = TouchY()
		End
		If TouchDown(0) = 0  And touching = 1 Then
			touching = 0
			touchingXend = TouchX()
			touchingYend = TouchY()
			
			If touchingXend &gt; touchingXstart Then 
				touchingXstart = 0
				touchingXend = 0
				touchingYstart = 0
				touchingYend = 0
				Return 1
			End	
			If touchingXend &lt; touchingXstart Then 
				touchingXstart = 0
				touchingXend = 0
				touchingYstart = 0
				touchingYend = 0
				result = 3
			End	
			If touchingYend &gt; touchingYstart Then 
				touchingXstart = 0
				touchingXend = 0
				touchingYstart = 0
				touchingYend = 0
				result = 2
			End	
			If touchingYend &lt; touchingYstart Then 
				touchingXstart = 0
				touchingXend = 0
				touchingYstart = 0
				touchingYend = 0
				result = 4
			End
		End

		Return result
	End	
</pre> <br><br></td></tr></table><br>
<a name="2041032"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Goodlookinguy</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Try this<br><br>[monkeycode]	Method CheckSwipe:Int()<br>		If Not touching And TouchDown(0) Then<br>			touching = True<br>			touchingXstart = TouchX()<br>			touchingYstart = TouchY()<br>		ElseIf Not TouchDown(0) And touching Then<br>			touching = False<br>			touchingXend = TouchX()<br>			touchingYend = TouchY()<br>			<br>			Local xDiff:Int = Max(touchingXend, touchingXstart) - Min(touchingXstart, touchingXend)<br>			Local yDiff:Int = Max(touchingYend, touchingYstart) - Min(touchingYstart, touchingYend)<br>			<br>			If xDiff &gt; yDiff<br>				If touchingXend &gt; touchingXstart Then Return 1<br>				If touchingXend &lt; touchingXstart Then Return 3<br>			Else<br>				If touchingYend &gt; touchingYstart Then Return 2<br>				If touchingYend &lt; touchingYstart Then Return 4<br>			End<br>		End<br><br>		Return 0<br>	End	[/monkeycode] <br><br></td></tr></table><br>
<a name="2041034"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tri|Ga|De</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sorry but your code doesn't seem to work for me.<br><br>I checking like this in OnUpdate<br><br><pre class=code>
If CheckSwipe() = 1 Then score = 1
If CheckSwipe() = 2 Then score = 2
If CheckSwipe() = 3 Then score = 3
If CheckSwipe() = 4 Then score = 4
</pre><br><br>I'm drawing the value to screen but I dod not get the correct result.<br>I only get a 1 return. <br><br></td></tr></table><br>
<a name="2041036"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Raz</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> The subsequent calls to CheckSwipe won't work, what do you get if you do the following...<br><br><pre class=code>
Local checkResult:int = CheckSwipe()
If checkResult &gt; 0
    Print checkResult
End
</pre> <br><br></td></tr></table><br>
<a name="2041037"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tri|Ga|De</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thats it!<br><br>Thanks both of you now the swipe function works. <br><br></td></tr></table><br>
<a name="2041043"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I just quickly knocked this up, it allows for the screen to rotate but still detect up/left/right/down (relative to the rotation). It also has an optional tolerance value, allowing you to fine tune what should be recognised.<br><br><pre class=code>
Import mojo

Const SWIPE_NONE : Int = 0
Const SWIPE_UP : Int = 1
Const SWIPE_DOWN : Int = 2
Const SWIPE_LEFT : Int = 3
Const SWIPE_RIGHT : Int = 4


'/ Screen Angle (allows swiping with rotation)
Global AxisAngle : Float = 0.0




Class MyApp Extends App


	Field touchStart : Int[ 2 ]
	Field touchEnd : Int[ 2 ]
	Field touching : Bool
	
	
	Method OnCreate()
		SetUpdateRate 60
	End Method
	


	Method OnUpdate()


		If KeyDown( KEY_LEFT ) Then AxisAngle = AxisAngle - 1.0
		If KeyDown( KEY_RIGHT ) Then AxisAngle = AxisAngle + 1.0
		

		If TouchDown() = 1
			If touching = False
				touching = True
				touchStart[ 0 ] = TouchX()
				touchStart[ 1 ] = TouchY()
			Else
				touchEnd[ 0 ] = TouchX()
				touchEnd[ 1 ] = TouchY()
			Endif
		Else
			If touching = True

				'/ check if touch vector is a valid swip
				Local result : Int = GestureSwipe( touchStart, touchEnd )
				
				If result = SWIPE_NONE Then Print "Not a valid swipe"
				If result = SWIPE_UP Then Print "Swipe Up Detected!"
				If result = SWIPE_DOWN Then Print "Swipe Down Detected!"
				If result = SWIPE_LEFT Then Print "Swipe Left Detected!"
				If result = SWIPE_RIGHT Then Print "Swipe Right Detected!"
				
				touching = False
			Endif
		Endif

	End Method
	



	Method OnRender()
		Cls 0, 0, 0
		
		DrawText "Press Left / Right to rotate Axis", 0, 0

		Local centreX : Int = DeviceWidth() / 2
		Local centreY : Int = DeviceHeight() / 2
		DrawLine centreX, centreY, centreX + Cos( AxisAngle ) * 100, centreY + Sin( AxisAngle ) * 100
		DrawLine centreX, centreY, centreX + Sin( AxisAngle ) * 100, centreY - Cos( AxisAngle ) * 100
		
	End Method
	


End Class



Function Main()
	New MyApp()
End Function






Function GestureSwipe : Int( touchStart : Int[], touchEnd : Int[], tolerance : Float = 0.15 )
	Local touchVectorX : Float = touchEnd[ 0 ] - touchStart[ 0 ]
	Local touchVectorY : Float = touchEnd[ 1 ] - touchStart[ 1 ]
	Local touchLength : Float = Sqrt( touchVectorX * touchVectorX + touchVectorY * touchVectorY )
	touchVectorX = touchVectorX / touchLength
	touchVectorY = touchVectorY / touchLength
	Local dot : Float = touchVectorX * Cos( AxisAngle ) + touchVectorY * Sin( AxisAngle )
	If Abs( dot ) &lt; tolerance
		If touchVectorY &lt; 0 Then Return SWIPE_UP
		Return SWIPE_DOWN
	Endif
	If Abs( dot ) &gt; 1.0 - tolerance
		If touchVectorX &lt; 0 Then Return SWIPE_LEFT
		Return SWIPE_RIGHT
	Endif
	Return SWIPE_NONE
End Function
</pre> <br><br></td></tr></table><br>
<a name="2041042"></a>

<a name="2041040"></a>

<a name="2041038"></a>

<a name="2041044"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tri|Ga|De</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thats some nice piece of code NoOdle.<br><br>Could come in handy! <br><br></td></tr></table><br>
<a name="2063578"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Raph</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is definitely hacky, but I extended NoOdle's code with detection of a few more gestures, some basic inertia, etc.<br><br>New stuff:<br><br>* I moved GestureSwipe() and the update stuff into a class; all you should need to do is create an instance of NoodleSwipe and check <br><pre class=code>Local result:Int = swipeMon.Update()</pre><br>* it can now return while drags are in progress, including if the drag changes direction midway<br><br>* it also returns inertia values (a decaying float; change NoodleSwipe.inertiaDamp to affect the damping rate)<br><br>* it maintains a history of the drag, might be useful for drawing paths or lines or detecting arcs. You can get segment lengths and durations out, etc. <br><br>* there's a bunch of new helper methods in there.<br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' nOodle's simple swipe code
' source: <a href="http://www.monkeycoder.co.nz/Community/posts.php?topic=3844" target="_blank">http://www.monkeycoder.co.nz/Community/posts.php?topic=3844</a>
' extended with path tracking

Import mojo

Const SWIPE_NONE:Int = 0				' returned as the default when nothing has been detected
Const SWIPE_UP:Int = 1					' returned when a gesture longer than the tolerance threshold is found
Const SWIPE_DOWN:Int = 2
Const SWIPE_LEFT:Int = 3
Const SWIPE_RIGHT:Int = 4
Const SWIPE_TAP:Int = 5					' returned when a gesture that is under the distance tolerance is found
Const SWIPE_INERTIA_UP:Int = 6			' returned when a gesture had sufficient speed to trigger inertia.
Const SWIPE_INERTIA_DOWN:Int = 7		'  inertia damps over time
Const SWIPE_INERTIA_LEFT:Int = 8
Const SWIPE_INERTIA_RIGHT:Int = 9
Const SWIPE_DRAG_UP = 10				' returned when the touch has moved beyond dragRange from the last drag start
Const SWIPE_DRAG_DOWN:Int = 11
Const SWIPE_DRAG_LEFT:Int = 12
Const SWIPE_DRAG_RIGHT:Int = 13

' Screen Angle (allows swiping with rotation)
Global _AxisAngle : Float = 0.0

#Rem
	summary: a class for detecting taps and swipes
	Use 
#END
Class NoodleSwipe
	Field touchStart:Int[2]
	Field touchEnd:Int[2]
	Field dragTouchStart:Int[2]
	Field prevTouchStart:Int[2]
	Field prevTouchEnd:Int[2]
	Field touching:Bool
	Field touchPath:Int[][]
	Field touchStartTime:Int
	Field touchDuration:Int
	Field segmentDuration:Int
	Field lastSegmentStart:Int
	Field inertia:Float
	Field lastInertia:Float
	Field inertiaDamp:Float = 0.9 ' the lower this number, the faster the damping. 1 will mean it never damps.
	Field dragRange:Int = 50  ' the minimum distance to traverse for a drag to be recognized
	Field inertiaCounter:Float = 0
	
	#Rem
		summary: returns swipe direction.
		 SWIPE_NONE:Int = 0				' returned as the default when nothing has been detected
		 SWIPE_UP:Int = 1					' returned when a gesture longer than the tolerance threshold is found
		 SWIPE_DOWN:Int = 2
		 SWIPE_LEFT:Int = 3
		 SWIPE_RIGHT:Int = 4
		 SWIPE_TAP:Int = 5					' returned when a gesture that is under the distance tolerance is found
		 SWIPE_INERTIA_UP:Int = 6			' returned when a gesture had sufficient speed to trigger inertia.
		 SWIPE_INERTIA_DOWN:Int = 7		'  inertia damps over time
		 SWIPE_INERTIA_LEFT:Int = 8
		 SWIPE_INERTIA_RIGHT:Int = 9
		 SWIPE_DRAG_UP = 10				' returned when the touch has moved beyond dragRange from the last drag start
		 SWIPE_DRAG_DOWN:Int = 11
		 SWIPE_DRAG_LEFT:Int = 12
		 SWIPE_DRAG_RIGHT:Int = 13		
	#END
	
	Method Update:Int()
	
		' if the touch is happening
		If TouchDown() = 1
			' and we didn't know about it
			If touching = False
				' initialize all the tracking
				touching = True
				inertia = 0
				prevTouchStart[0] = touchStart[0]
				prevTouchStart[1] = touchStart[1]
				prevTouchEnd[0] = touchEnd[0]
				prevTouchEnd[1] = touchEnd[1]
				touchStart[ 0 ] = TouchX()
				touchStart[1] = TouchY()
				dragTouchStart[0] = touchStart[0]
				dragTouchStart[1] = touchStart[1]
				touchPath = touchPath.Resize(1)
				touchPath[0] = touchStart
				touchStartTime = Millisecs()
				lastSegmentStart = touchStartTime
				inertiaCounter = 0
			Else
				' we did know about it; update the end of the path data
				touchEnd[0] = TouchX()
				touchEnd[1] = TouchY()
				
				' and the durations
				touchDuration = Millisecs() -touchStartTime
				segmentDuration = Millisecs() -lastSegmentStart

				' add to the path history
				If TouchX &lt;&gt; touchPath[touchPath.Length - 1][0] OR TouchY &lt;&gt; touchPath[touchPath.Length - 1][1]
					touchPath = touchPath.Resize(touchPath.Length + 1)
					touchPath[touchPath.Length - 1] =[Int(TouchX), Int(TouchY)]
				EndIf
				
				' zero out inertia
				inertia = 0				
				
				' handle drags if there's enough data points
				If touchPath.Length() &gt; 2
				
					' check for a gesture from the last drag start point to where we are now
					Local result:Int = GestureSwipe(dragTouchStart, touchEnd)
					
					' if it looks like a gesture and it's longer than our drag threshold
					If result &gt; SWIPE_NONE And result &lt; SWIPE_TAP and DragLength &gt; dragRange
						' reset the drag start point
						dragTouchStart[0] = touchEnd[0]
						dragTouchStart[1] = touchEnd[1]
						' the plus 9 moves it from the swipe to the drag range
						Return result + 9
					EndIf

				EndIf
				
				' update timer
				lastSegmentStart = Millisecs()
			EndIf

		Else
			' there is no touch happening
			If touching = True
				' but we thought there was
				
				' check if touch vector is a valid swipe
				Local result:Int = GestureSwipe(touchStart, touchEnd)
				
				' clear touching
				touching = False
				
				' if we moved fast, set inertia
				If touchDuration &lt; Length() Then inertia = Float(touchDuration) / Length() Else inertia = 0
				
				' cap off the path history
				prevTouchEnd[0] = touchEnd[0]
				prevTouchEnd[1] = touchEnd[1]
				
				Return result
			Else
				' no touch happening, and we didn't think there was... so update inertia
				
				' dampen inertia
				lastInertia = inertia
				inertia = inertia * inertiaDamp
				If inertia &lt; 0.01 Then inertia = 0

				' if inertia is still present
				If inertia &gt; 0
					' add it to our counter. This makes inertia tick slower and slower
					inertiaCounter = inertiaCounter + inertia
					
					' if it ticks over
					If inertiaCounter &gt;= 1
						' detect the entire gesture again
						Local result:Int = GestureSwipe(touchStart, touchEnd)
						
						' and if it was a swipe, send it on as an inertial swipe
						If result &gt; SWIPE_NONE and result &lt; SWIPE_TAP Then Return result + 5
						
						' and reset our counter
						inertiaCounter = 0
					EndIf
				EndIf
			EndIf
		EndIf
		
		' nothing is happening
		Return SWIPE_NONE
	End
	
	'summary: returns duration of a touch event in milliseconds
	Method Duration:Int()
		Return touchDuration
	End
	
	'summary: returns true if touch is down (should match TouchDown())
	Method IsSwiping:Bool()
		Return touching
	End

	'summary: returns array of [x, y] of touch start
	Method StartCoords:Int[] ()
		Return touchStart
	End
	
	'summary: returns X coordinate of swipe start
	Method StartX:Int()
		Return touchStart[0]
	End

	'summary: returns Y coordinate of swipe start
	Method StartY:Int()
		Return touchStart[1]
	End	
	
	'summary: returns end of swipe as [x, y]. If touching, this is current position.
	Method EndCoords:Int[] ()
		Return touchEnd
	End
	
	'summary: returns X of swipe end. If touching, this is current position.
	Method EndX:Int()
		Return touchEnd[0]
	End

	'summary: returns Y of swipe end. If touching, this is current position.	
	Method EndY:Int()
		Return touchEnd[1]
	End

	'summary: returns array of [x, y] of touch start preceding the current one
	Method PrevStartCoords:Int[] ()
		Return prevTouchStart
	End
	
	'summary: returns X coordinate of swipe start preceding the current one
	Method PrevStartX:Int()
		Return prevTouchStart[0]
	End

	'summary: returns Y coordinate of swipe start preceding the current one
	Method PrevStartY:Int()
		Return prevTouchStart[1]
	End	
	
	'summary: returns end of swipe  preceding the current one as [x, y].
	Method PrevEndCoords:Int[] ()
		Return prevTouchEnd
	End
	
	'summary: returns X of swipe end preceding the current one.
	Method PrevEndX:Int()
		Return prevTouchEnd[0]
	End

	'summary: returns Y of swipe end preceding the current one.	
	Method PrevEndY:Int()
		Return prevTouchEnd[1]
	End	
	
	'summary: returns a list of [x,y] coordinates for the swipe
	Method Path:Int[][] ()
		Return touchPath
	End
	
	'summary: returns length of swipe as a vector
	Method Length:Int()
		Local touchVectorX:Float = touchEnd[0] - touchStart[0]
		Local touchVectorY : Float = touchEnd[ 1 ] - touchStart[ 1 ]
		Local touchLength:Float = Sqrt(touchVectorX * touchVectorX + touchVectorY * touchVectorY)
		Return Int(touchLength)
	End
	
	'summary: returns length of last path segment
	Method SegmentLength:Int()
		If touchPath.Length &lt; 2 Then Return 0
		Local touchVectorX:Float = touchPath[touchPath.Length - 2][0] - touchPath[touchPath.Length - 1][0]
		Local touchVectorY:Float = touchPath[touchPath.Length - 2][1] - touchPath[touchPath.Length - 1][1]
		Local touchLength:Float = Sqrt(touchVectorX * touchVectorX + touchVectorY * touchVectorY)
		Return Int(touchLength)		
	End
	
	'summary: returns length of current drag segment
	Method DragLength:Int()
		If touchPath.Length &lt; 2 Then Return 0
		Local touchVectorX:Float = dragTouchStart[0] - touchPath[touchPath.Length - 1][0]
		Local touchVectorY:Float = dragTouchStart[1] - touchPath[touchPath.Length - 1][1]
		Local touchLength:Float = Sqrt(touchVectorX * touchVectorX + touchVectorY * touchVectorY)
		Return Int(touchLength)			
	End

	'summary: returns a value based on the speed of the swipe
	Method Inertia:Float()
		Return inertia
	End
	
	'summary: returns the current damping value. 1 means no damp, 2 means damp half every tick, etc
	Method InertiaDamp:Float()
		Return inertiaDamp()
	End
	
	'summary: returns swipe directions as SWIPE_UP, etc, or SWIPE_TAP if brief and static, or SWIPE_NONE
	Method GestureSwipe:Int(touchStart:Int[], touchEnd:Int[], tolerance:Float = 0.15)
		Local touchVectorX : Float = touchEnd[ 0 ] - touchStart[ 0 ]
		Local touchVectorY : Float = touchEnd[ 1 ] - touchStart[ 1 ]
		Local touchLength : Float = Sqrt( touchVectorX * touchVectorX + touchVectorY * touchVectorY )
		touchVectorX = touchVectorX / touchLength
		touchVectorY = touchVectorY / touchLength
		Local dot : Float = touchVectorX * Cos( _AxisAngle ) + touchVectorY * Sin( _AxisAngle )
		If touchDuration &lt; 500 And touchLength &lt; 100
			'DebugLog "TouchLength:" + touchLength
			Return SWIPE_TAP
		EndIf
		If Abs( dot ) &lt; tolerance
			If touchVectorY &lt; 0 Then Return SWIPE_UP
			Return SWIPE_DOWN
		Endif
		If Abs( dot ) &gt; 1.0 - tolerance
			If touchVectorX &lt; 0 Then Return SWIPE_LEFT
			Return SWIPE_RIGHT
		EndIf

		Return SWIPE_NONE
	End
End

' Test class.
Class MyApp Extends App
	Field swipeMon:NoodleSwipe = New NoodleSwipe

	Method OnCreate()
		SetUpdateRate(60)
	End
	
	Method OnUpdate()
		Local result:Int = swipeMon.Update()

		If result = SWIPE_UP Then Print "Swipe Up Detected!"
		If result = SWIPE_DOWN Then Print "Swipe Down Detected!"
		If result = SWIPE_LEFT Then Print "Swipe Left Detected!"
		If result = SWIPE_RIGHT Then Print "Swipe Right Detected!"
		
		If result = SWIPE_TAP Then Print "Tap!"

		If result = SWIPE_INERTIA_UP Then Print "Inertia Up Detected! " + swipeMon.Inertia()
		If result = SWIPE_INERTIA_DOWN Then Print "Inertia Down Detected! " + swipeMon.Inertia()
		If result = SWIPE_INERTIA_LEFT Then Print "Inertia Left Detected! " + swipeMon.Inertia()
		If result = SWIPE_INERTIA_RIGHT Then Print "Inertia Right Detected! " + swipeMon.Inertia()

		If result = SWIPE_DRAG_UP Then Print "Drag Up Detected!"
		If result = SWIPE_DRAG_DOWN Then Print "Drag Down Detected!"
		If result = SWIPE_DRAG_LEFT Then Print "Drag Left Detected!"
		If result = SWIPE_DRAG_RIGHT Then Print "Drag Right Detected!"
	End
	
	Method OnRender()
		Cls 0, 0, 0
		SetColor 255, 255, 255
		DrawText("TOUCH: " + Int(swipeMon.IsSwiping), 0, 0)
		DrawText("START: " + swipeMon.StartX + "," + swipeMon.StartY, 0, 20)
		DrawText("END: " + swipeMon.EndX + "," + swipeMon.EndY, 0, 40)
		
		If swipeMon.Path.Length &gt; 0
			Local s:String = "P:"
			For Local i:Int = 0 To swipeMon.Path.Length - 1
				DrawOval(swipeMon.Path[i][0] - 1, swipeMon.Path[i][1] - 1, 3, 3)
				If i &lt;&gt; 0
					DrawLine(swipeMon.Path[i][0], swipeMon.Path[i][1], swipeMon.Path[i - 1][0], swipeMon.Path[i - 1][1])
				EndIf
				s = s + "[" + swipeMon.Path[i][0] + "," + swipeMon.Path[i][1] + "]"
			Next
		EndIf
		
		If swipeMon.PrevEndCoords.Length &gt; 0
			SetColor 0, 255, 0
			DrawRect(swipeMon.PrevEndX - 8, swipeMon.PrevEndY - 8, 16, 16)
			DrawLine(swipeMon.touchStart[0], swipeMon.touchStart[1], swipeMon.PrevEndX, swipeMon.PrevEndY)
			SetColor 255, 255, 255
		EndIf
		
		SetColor 255, 0, 0
		DrawOval(swipeMon.dragTouchStart[0], swipeMon.dragTouchStart[1], 4, 4)
		DrawOval(swipeMon.touchEnd[0], swipeMon.touchEnd[1], 4, 4)
		DrawLine(swipeMon.dragTouchStart[0], swipeMon.dragTouchStart[1], swipeMon.touchEnd[0], swipeMon.touchEnd[1])
		SetColor 255, 255, 255
				
	End
	
End

Function Main()
	New MyApp
End
</textarea> <br><br></td></tr></table><br>
<a name="2063540"></a>

<a name="2063580"></a>

<a name="2063581"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Raph</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Updated the above with a fix for drags, much nicer inertia, plus added more visualization to the test app. <br><br></td></tr></table><br>
<a name="2063702"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Zurrr</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Look great! could you show the use of 'Inertia' in your next update test. <br><br></td></tr></table><br>
<a name="2063709"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Raph</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> It's pretty simple, and the test class does show how it works. If you run this in HTML5 you can see the inertia events firing in the text box, and see how it decays away.<br><br>How I am using it in my game right now (pseudocode):<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Field touchMon:NoodleSwipe = New NoodleSwipe

Method OnCreate()
    touchMon.dragRange = 100
    touchMon.inertiaDamp = 0.75
End

Method OnUpdate()
    Local swipe:Int = touchMon.Update()

    If swipe = SWIPE_DRAG_LEFT or swipe = SWIPE_INERTIA_LEFT
        ...move stuff
    End
End
</textarea><br><br>Be sure to adjust the inertiaDamp value to suit your use case. I defaulted it at 0.9, but that was too many inertia ticks for the particular usage I had. I then tried 0.5, and it died off too fast. Currently at 0.75, may move it to 0.8!<br><br>Same with dragRange. The default is 50. Probably want to adjust it based on screen and device size. 44px at non-retina levels is what Apple recommends for a touch, but of course, you go retina and it goes to 100, Android has a pile of resolutions and screen sizes, etc.<br><br>Also, cf this bug report here: <a href="http://www.monkeycoder.co.nz/Community/posts.php?topic=5645" target="_blank">http://www.monkeycoder.co.nz/Community/posts.php?topic=5645</a><br><br>If you add this one-line hack:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
		Else
			' there is no touch happening
			If touching = True
				' but we thought there was
				
				' check if touch vector is a valid swipe
				Local result:Int = GestureSwipe(touchStart, touchEnd)
				
				If TouchHit() And result = SWIPE_NONE Then result = SWIPE_TAP  ' Hack for touchDown() bug
</textarea><br><br>you'll get consistent behavior across platforms. <br><br></td></tr></table><br>
<a name="2063755"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Supertino</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> I use ATAN2 with my swipes, then from the angle I can see if it's up,down,left or right.  And then Sqrt to find the distance.<br><br><pre class=code>
Class TouchSwip
	Field originX:Int
	Field originY:Int
	Field distanceFromOrigin:Int
	Field angle:Int
	Field isTouched:Bool
	Field state:Int
	Field nullZoneDistance:Int
	Field direction:Int
	
	Method New(tMinDistance:Int)
		nullZoneDistance = tMinDistance	
	End Method
	
	Method Render()
		If state &lt;&gt; 2 Then Return
		DrawLine originX - nullZoneDistance, originY - nullZoneDistance, originX + nullZoneDistance, originY + nullZoneDistance
		DrawLine originX + nullZoneDistance, originY - nullZoneDistance, originX - nullZoneDistance, originY + nullZoneDistance
	End Method
	
	Method Update()
		Local x:Int = TouchX
		Local y:Int = TouchY

		Select state
			Case 0
				If isTouched = False And TouchDown(0) = True Then state = 1
			Case 1
				isTouched = True
				state = 2
				originX = TouchX
				originY = TouchY
			Case 2
				Local x:Int = TouchX
				Local y:Int = TouchY
				If TouchDown(0) = True
					distanceFromOrigin = Sqrt( (x - originX) * (x - originX) + (y - originY) * (y - originY))
					angle = (ATan2(originX - x, originY - y) + (180)) / 45
				Else
					If distanceFromOrigin &gt; nullZoneDistance
						If angle = 4 or angle = 3 Then direction = 0
						If angle = 2 or angle = 1 Then direction = 1
						If angle = 7 or angle = 0 Then direction = 2
						If angle = 5 or angle = 6 Then direction = 3
					Else
						Self.Clear
					End If
				End If
		End Select
	End Method
	
	Method Clear()
		isTouched = False
		angle = -1
		direction = -1
		state = 0
	End Method
	
	Method Up:Bool()
		If direction = 0 Then Self.Clear; Return True
		Return False
	End Method
	
	Method Right:Bool()
		If direction = 1 Then Self.Clear; Return True
		Return False
	End Method
	
	Method Down:Bool()
		If direction = 2 Then Self.Clear; Return True
		Return False
	End Method
	
	Method Left:Bool()
		If direction = 3 Then Self.Clear; Return True
		Return False
	End Method
	
End Class
</pre> <br><br></td></tr></table><br>
<a name="2063754"></a>

<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
