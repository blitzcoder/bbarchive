<!DOCTYPE html><html lang="en" ><head ><title >Scripted Movement</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Scripted Movement</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=507" >Monkey Programming</a>/<a href="#bottom" >Scripted Movement</a><br><br>
<a name="2030890"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have been trying to create a type of scripted movement for objects such as ships characters etc.. this is my second time attempting this although I don't know if this is feasible or is there a better way as I am not too happy with it.  has anybody here worked on something like this? I am hoping somebody has a better algorithm that might want to share. <br><br>this is what I have:<br>[monkeycode]<br>Strict<br>Import mojo<br><br>Class PVect2D<br>	Field x:Float<br>	Field y:Float<br>End Class<br><br><br>Function Main:Int()<br>	New Game	<br>	Return True<br>End Function<br><br>Class Game Extends App<br>	Field script:Script<br><br>	Field txt:String = "S=2.5;P=200,400;A=0;C=50,90,L;N=100;C=100,45,R;N=50;C=20,135,R;N=200"<br><br>	Method OnCreate:Int()<br>		script = New Script<br>		script.Init(txt)<br>		SetUpdateRate 30<br>		Return True<br>	End Method<br>	<br>	Method OnUpdate:Int()<br>		Local n:Int = script.Update()<br>		Return True<br>	End Method<br>	<br>	Method OnRender:Int()<br>		Cls()<br>		script.Render()<br>		Return True<br>	End Method<br>	<br>End Class<br><br><br>Class Movement<br>	Field pos:PVect2D<br>	Field v:PVect2D<br>	Field d:PVect2D<br>	Field len:Float<br>	Field distance:Float<br>	Field angle:Float<br>	Field direction:Int<br>	Field radius:Float<br>	Field speed:Float<br>	Field stp:Float<br>	Field state:Int<br>	Field remaind:Float<br>	<br>	Field index:Int<br>	<br>	Const READSCRIPT:Int = 1<br>	Const ARC:Int = 2<br>	Const LINE:Int = 3<br>	<br>	Const LEFT:Int = 1<br>	Const RIGHT:Int = 2 <br><br>	Method Update:Int() Abstract<br>	<br>End Class<br><br><br>Class Script Extends Movement<br>	Field pivot:PVect2D<br>	Field pattern:String[][][]<br>	<br>	Method New()<br>		pos = New PVect2D<br>		v = New PVect2D<br>		d = New PVect2D<br>		pivot = New PVect2D<br>	End Method<br><br>'**************************************************************************************<br>'	the scripted movement uses a set of instruction passed to the <br>'	the module in a string and are as follow:<br>'<br>'	postion:<br>'	P=x:float,y:float<br>'	example: "P=300,300"<br>'	sets the position<br>'<br>'	angle:<br>'   A=angle:float<br>'	exampe: "A=45"<br>'	sets the direction of travel<br>'<br>'	speed:<br>'	S=Speed:float<br>'	example: "S=2.5"<br>'   sets the speed of travel<br>'<br>'	Arc:<br>'	C=radius:float,NumberOfDigrees:float,DIRECTION:int(left/right)<br>'	example: "C=20,45,L"<br>'	moves in an arc pattern from its current diretion using specified radius and number of<br>'	Degrees To travel in the direction specified using L(For left) Or R(For right)<br>'	<br>'	Line:<br>'	N=distanceToTravel:Float<br>'	exampe: "N=100"<br>'	moves in a straight line in the current direction a specifield number of pixels.<br>'<br>'	the instructions can be conbined in a single string and separated by ";"(semicolon).<br>'	example: "P=300,300;A=0;S=3"<br>'<br>'**************************************************************************************<br><br>	Method Init:Void(scr:String)<br>		Local pat:String[] = scr.Replace(" ","").Split(";")<br>		pattern = New String[pat.Length()][][]<br>		For Local i:Int = 0 Until pat.Length()<br>			Local parts:String[]= pat[i].Split("=")<br>			pattern[i] = New String[parts.Length()][]<br>			Local comp:String[] = parts[1].Split(",")<br>			pattern[i][1] = New String[comp.Length()+1]<br>			pattern[i][0] = New String[1]<br>			pattern[i][0][0] = parts[0]<br>			For Local k:Int = 0 Until comp.Length()<br>				pattern[i][1][k] = comp[k]<br>			Next<br>		Next<br>		state = READSCRIPT<br>		index = 0<br>	End Method<br>	<br>	Method InitArc:Void(dir:Float,rad:Float,distance:Float)<br>		direction = dir<br>		d.x = Cos(angle)<br>		d.y = Sin(angle)<br>		If dir = LEFT<br>			pivot.x = pos.x + (d.y * rad)<br>			pivot.y = pos.y - (d.x * rad)<br>		Elseif dir = RIGHT<br>			pivot.x = pos.x - (d.y * rad)<br>			pivot.y = pos.y + (d.x * rad)<br>		Else<br>			Error "Invalid Arc direction parameter"<br>		Endif<br>		stp = 1.0/(PI/180.0 * rad)<br>		radius = rad<br>		Self.distance = distance<br>		len = 0<br>		state = ARC<br>	End Method<br>	<br>	Method InitLine:Void(distance:Float)<br>		Self.distance = distance<br>		len = 0<br>		d.x = Cos(angle)<br>		d.y = Sin(angle)<br>		state = LINE<br>	End Method<br>	<br>	Method SetPosition:Void(x:Float,y:Float)<br>		pos.x = x<br>		pos.y = y<br>	End Method<br>	<br>	Method SetAngle:Void(ang:Float)<br>		angle = ang<br>	End Method<br>	<br>	Method Update:Int()<br>	<br>		If state = READSCRIPT<br>			If index &lt; pattern.Length()<br>				Repeat<br>					Select pattern[index][0][0]<br>						Case "P" 'position<br>								pos.x = Float(pattern[index][1][0])<br>								pos.y = Float(pattern[index][1][1])<br>						Case "A" 'angle<br>							angle = Float(pattern[index][1][0])<br>						Case "S" 'speed<br>							speed = Float(pattern[index][1][0])<br>						Case "C" 'arc<br>							If pattern[index][1][2] = "L" <br>								InitArc(LEFT, Float(pattern[index][1][0]),Float(pattern[index][1][1])) ' dir, radius, distance<br>							Elseif pattern[index][1][2] = "R"<br>								InitArc(RIGHT,Float(pattern[index][1][0]),Float(pattern[index][1][1])) ' dir, radius, distance<br>							Else<br>								Error "Invalid Arc parameters pattern #"+index<br>							Endif<br>							index += 1<br>							Exit<br>						Case "N" 'line<br>							InitLine(Float(pattern[index][1][0]))<br>							index += 1<br>							Exit<br>						Default<br>							Error "Invalid movement type instruction #"+index<br>							<br>					End Select<br>					index += 1<br>				Until index = pattern.Length()<br>			Else<br>				Return False<br>			Endif<br>		Endif<br>		If state = ARC<br>			Local ang:Float<br>			If len &lt; distance<br>				ang = stp*speed<br>				len += ang<br>				If len &gt; distance<br>					remaind = len - distance<br>					ang -= remaind<br>					state = READSCRIPT<br>					distance = len<br>				Endif<br>			Else<br>				remaind = 0<br>				state = READSCRIPT<br>			Endif<br>			<br>			Local tx:Float = pos.x - pivot.x<br>			Local ty:Float = pos.y - pivot.y<br>			If direction = LEFT<br>				pos.x = pivot.x + tx*Cos(-ang) - ty*Sin(-ang) <br>				pos.y = pivot.y + ty*Cos(-ang) + tx*Sin(-ang) <br>				angle -= ang<br>			Else<br>				pos.x = pivot.x + tx*Cos(ang) - ty*Sin(ang) <br>				pos.y = pivot.y + ty*Cos(ang) + tx*Sin(ang)<br>				angle += ang<br>			Endif<br>		Elseif state = LINE<br>			len += speed<br>			If len &gt; distance<br>				remaind = len - distance<br>				len = distance<br>				pos.x += d.x * (speed-remaind)<br>				pos.y += d.y * (speed-remaind)<br>				state = READSCRIPT<br>			Else<br>				pos.x += d.x *speed<br>				pos.y += d.y *speed<br>			Endif<br>		Endif<br>		Return True<br>	End Method<br>	<br>	Method Render:Int()<br>		DrawOval pos.x-1,pos.y-1,2,2<br>		Return True<br>	End Method<br><br>End Class<br>[/monkeycode] <br><br></td></tr></table><br>
<a name="2030889"></a>

<a name="2030888"></a>

<a name="2030887"></a>

<a name="2030912"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gerry Quinn</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I don't see anything wrong with it in general, except for the temptation to make it more elaborate than your game needs.<br><br>I presume Render() is just for debugging, and normally a script would be attached to an object which would consult it when it wants to move. <br><br></td></tr></table><br>
<a name="2030913"></a>

<a name="2030914"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Gerry,  I was just hoping somebody had a better more optimized algorithm, Because the way I have it, I use strings and they are really slow to process.  when I first did the tests, I was splitting the string on the fly and flash was lagging pretty badly  with a 30 FPS updater rate. I had to do all of the pre splitting of string in the initialization to improve it slightly. While this is fine for a single object, for multiple objects it would probably be really slow specially while introducing new objects during game play. Also, based on what I read from muddy shoes, multiple dimension array are slow.  so, I will end up figuring an alternative to the data.<br><br>And yes, the Render is just for demonstration purpose.  I was going to include an image that included the rotation for demonstration but  I changed my mind and just included the DrawOval. <br><br></td></tr></table><br>
<a name="2030919"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >muddy_shoes</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Also, based on what I read from muddy shoes, multiple dimension array are slow <br></div><br><br>Can be, but the real point was that moving to single dimensional arrays is not something that will provide a noticeable performance boost outside of fairly specific circumstances. I'm not sure this is one as it seems highly unlikely that the array accesses are even a blip compared to the string operations.<br><br>I ran your code with a few thousand instances of your test script objects and speed didn't seem to be an issue on HTML5 or Flash. Enough objects or large enough scripts will make it an issue, of course, but I don't know how ambitious you intend to be.<br><br>As you said, you've front-loaded the string parsing. If that initial cost becomes a problem then you can spread the cost by only splitting it into the commands and parsing those as required, e.g.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Strict
Import mojo

Class PVect2D
        Field x:Float
        Field y:Float
End Class


Function Main:Int()
        New Game        
        Return True
End Function
Const NUM_TEST_OBJECTS:Int = 5000
Class Game Extends App
        Field script:Script[]

        Field txt:String = "S=2.5;P=200,400;A=0;C=50,90,L;N=100;C=100,45,R;N=50;C=20,135,R;N=200;S=2.5;P=200,400;A=0;C=50,90,L;N=100;C=100,45,R;N=50;C=20,135,R;N=200;S=2.5;P=200,400;A=0;C=50,90,L;N=100;C=100,45,R;N=50;C=20,135,R;N=200;S=2.5;P=200,400;A=0;C=50,90,L;N=100;C=100,45,R;N=50;C=20,135,R;N=200;S=2.5;P=200,400;A=0;C=50,90,L;N=100;C=100,45,R;N=50;C=20,135,R;N=200;S=2.5;P=200,400;A=0;C=50,90,L;N=100;C=100,45,R;N=50;C=20,135,R;N=200;S=2.5;P=200,400;A=0;C=50,90,L;N=100;C=100,45,R;N=50;C=20,135,R;N=200;S=2.5;P=200,400;A=0;C=50,90,L;N=100;C=100,45,R;N=50;C=20,135,R;N=200;S=2.5;P=200,400;A=0;C=50,90,L;N=100;C=100,45,R;N=50;C=20,135,R;N=200;S=2.5;P=200,400;A=0;C=50,90,L;N=100;C=100,45,R;N=50;C=20,135,R;N=200;S=2.5;P=200,400;A=0;C=50,90,L;N=100;C=100,45,R;N=50;C=20,135,R;N=200;S=2.5;P=200,400;A=0;C=50,90,L;N=100;C=100,45,R;N=50;C=20,135,R;N=200;S=2.5;P=200,400;A=0;C=50,90,L;N=100;C=100,45,R;N=50;C=20,135,R;N=200"

        Method OnCreate:Int()
                script = New Script[NUM_TEST_OBJECTS]
                Init()
                SetUpdateRate 30
                Return True
        End Method
        
        Method Init:Void()
            For Local i:Int = 0 Until NUM_TEST_OBJECTS
                    script[i] = New Script
                    script[i].Init(txt)
            End
        End
        
        Method OnUpdate:Int()
                If MouseHit()
                    Init()
                    Return 1
                End
                Local done:Bool = True
                For Local i:Int = 0 Until NUM_TEST_OBJECTS
                    If script[i].Update()
                        done = False
                    End
                End
                If done
                    Init()
                End
                Return True
        End Method
        
        Method OnRender:Int()
                Cls()
                script[0].Render()
                Return True
        End Method
        
End Class


Class Movement
        Field pos:PVect2D
        Field v:PVect2D
        Field d:PVect2D
        Field len:Float
        Field distance:Float
        Field angle:Float
        Field direction:Int
        Field radius:Float
        Field speed:Float
        Field stp:Float
        Field state:Int
        Field remaind:Float
        
        Field index:Int
        
        Const READSCRIPT:Int = 1
        Const ARC:Int = 2
        Const LINE:Int = 3
        
        Const LEFT:Int = 1
        Const RIGHT:Int = 2 

        Method Update:Int() Abstract
        
End Class


Class Script Extends Movement
        Field pivot:PVect2D
        Field pattern:String[]
        
        Method New()
                pos = New PVect2D
                v = New PVect2D
                d = New PVect2D
                pivot = New PVect2D
        End Method

'**************************************************************************************
'       the scripted movement uses a set of instruction passed to the 
'       the module in a string and are as follow:
'
'       postion:
'       P=x:float,y:float
'       example: "P=300,300"
'       sets the position
'
'       angle:
'   A=angle:float
'       exampe: "A=45"
'       sets the direction of travel
'
'       speed:
'       S=Speed:float
'       example: "S=2.5"
'   sets the speed of travel
'
'       Arc:
'       C=radius:float,NumberOfDigrees:float,DIRECTION:int(left/right)
'       example: "C=20,45,L"
'       moves in an arc pattern from its current diretion using specified radius and number of
'       Degrees To travel in the direction specified using L(For left) Or R(For right)
'       
'       Line:
'       N=distanceToTravel:Float
'       exampe: "N=100"
'       moves in a straight line in the current direction a specifield number of pixels.
'
'       the instructions can be conbined in a single string and separated by ";"(semicolon).
'       example: "P=300,300;A=0;S=3"
'
'**************************************************************************************

        Method Init:Void(scr:String)
            pattern = scr.Split(";")
            state = READSCRIPT
            index = 0
        End Method
        
        Method InitArc:Void(dir:Float,rad:Float,distance:Float)
                direction = dir
                d.x = Cos(angle)
                d.y = Sin(angle)
                If dir = LEFT
                        pivot.x = pos.x + (d.y * rad)
                        pivot.y = pos.y - (d.x * rad)
                Elseif dir = RIGHT
                        pivot.x = pos.x - (d.y * rad)
                        pivot.y = pos.y + (d.x * rad)
                Else
                        Error "Invalid Arc direction parameter"
                Endif
                stp = 1.0/(PI/180.0 * rad)
                radius = rad
                Self.distance = distance
                len = 0
                state = ARC
        End Method
        
        Method InitLine:Void(distance:Float)
                Self.distance = distance
                len = 0
                d.x = Cos(angle)
                d.y = Sin(angle)
                state = LINE
        End Method
        
        Method SetPosition:Void(x:Float,y:Float)
                pos.x = x
                pos.y = y
        End Method
        
        Method SetAngle:Void(ang:Float)
                angle = ang
        End Method
        
        Method ReadScript:Int()
            If index &lt; pattern.Length()
                    Repeat
                        Local parts:String[]= pattern[index].Split("=")
                        Local comp:String[] = parts[1].Replace(" ","").Split(",")
                        Local command:String = parts[0].Trim()
                        
                            Select command
                                    Case "P" 'position
                                                    pos.x = Float(comp[0])
                                                    pos.y = Float(comp[1])
                                    Case "A" 'angle
                                            angle = Float(comp[0])
                                    Case "S" 'speed
                                            speed = Float(comp[0])
                                    Case "C" 'arc
                                            If comp[2] = "L" 
                                                    InitArc(LEFT, Float(comp[0]),Float(comp[1])) ' dir, radius, distance
                                            Elseif comp[2] = "R"
                                                    InitArc(RIGHT,Float(comp[0]),Float(comp[1])) ' dir, radius, distance
                                            Else
                                                    Error "Invalid Arc parameters pattern #"+index
                                            Endif
                                            index += 1
                                            Exit
                                    Case "N" 'line
                                            InitLine(Float(comp[0]))
                                            index += 1
                                            Exit
                                    Default
                                            Error "Invalid movement type instruction #"+index
                                            
                            End Select
                            index += 1
                    Until index = pattern.Length()
                    
                    Return True
            Else
                    Return False
            Endif
        End
        
        Method Update:Int()
        
                If state = READSCRIPT
                    If Not ReadScript()
                        Return False
                    End
                Endif
                If state = ARC
                        Local ang:Float
                        If len &lt; distance
                                ang = stp*speed
                                len += ang
                                If len &gt; distance
                                        remaind = len - distance
                                        ang -= remaind
                                        state = READSCRIPT
                                        distance = len
                                Endif
                        Else
                                remaind = 0
                                state = READSCRIPT
                        Endif
                        
                        Local tx:Float = pos.x - pivot.x
                        Local ty:Float = pos.y - pivot.y
                        If direction = LEFT
                                pos.x = pivot.x + tx*Cos(-ang) - ty*Sin(-ang) 
                                pos.y = pivot.y + ty*Cos(-ang) + tx*Sin(-ang) 
                                angle -= ang
                        Else
                                pos.x = pivot.x + tx*Cos(ang) - ty*Sin(ang) 
                                pos.y = pivot.y + ty*Cos(ang) + tx*Sin(ang)
                                angle += ang
                        Endif
                Elseif state = LINE
                        len += speed
                        If len &gt; distance
                                remaind = len - distance
                                len = distance
                                pos.x += d.x * (speed-remaind)
                                pos.y += d.y * (speed-remaind)
                                state = READSCRIPT
                        Else
                                pos.x += d.x *speed
                                pos.y += d.y *speed
                        Endif
                Endif
                Return True
        End Method
        
        Method Render:Int()
                DrawOval pos.x-1,pos.y-1,2,2
                Return True
        End Method

End Class
</textarea><br><br>You still need to test to see if it's actually better for your purposes on your intended platform though. <br><br></td></tr></table><br>
<a name="2030922"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamRedwoods</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> interesting ideas.<br><br>another thought would be to parse the animation string into an object array. this can be done OnInit() for all objects that have an animation string. you would have to keep an object list which is added to during a New(). <br><br></td></tr></table><br>
<a name="2030926"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gerry Quinn</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I find it hard to believe that the problem is really in your algorithm, unless you have an extraordinarily large number of objects.  Have you tried carrying out just typical algorithmic operations a few (or many) thousand times in a loop and checking the time taken using Millisecs()?<br><br>You could certainly make a few optimisations here and there, for example calculating Cos( ang ) just once.  Some compilers will probably do that for you, but I suspect that won't apply to all the targets.<br><br>Your arrays are just look-up tables, so I doubt they are a speed issue.  Where you might get a speed issue for multidimensional arrays is pathfinding, dungeon generation, board-games and such, where you are very much focused on cells and adjacent cells.  And even there it's not all that terrible.  I found for example that the Dijkstra algorithm was a little more than half as fast on 2D as 1D. <br><br></td></tr></table><br>
<a name="2030940"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> thanks muddy, I have yet to do some real tests as I haven't had much time to do work on it and won't today either going out fishing with a couple of friends but will soonish. <br><br>Garry,  my initials test proved to me that the update method was slow. obviously, I don't have the original code to prove it because of the modifications I made with this new code. Maybe I jumped the gun as I haven't done any proper testing.  My only reason for concluding that is because when I removed the parsing from the main loop the frame rate got normal before that it was slow and it showed clearly because it was skipping OnRender processing. So my conclusion is that every time I do parsing it will consume quite a bit of milliseconds. so if I have multiple objects in the screen each with its own script it will cause some serious lagging.  All of these tests were done with flash so don't know about the other targets. If you wan't to believe that, its fine with me, and if you don't then that's also fine. I am just glad you took the time to comment and thank you for that. <br><br>Adam, I like your idea. I might end up doing that. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
