<!DOCTYPE html><html lang="en" ><head ><title >Rotation Demo Issues...</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Rotation Demo Issues...</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=507" >Monkey Programming</a>/<a href="#bottom" >Rotation Demo Issues...</a><br><br>
<a name="2008850"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Neuro</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Is anyone else having issues running the rotation sample demos (Rotation sickness, and Jump rotate)? It appears to run extremely slow, and have color issues when running on HTML 5 browsers. I've tested this on Firefox 4.0, IE 9.0, Chrome 12.0, and Safari browsers on my MacBook Pro and multiple Window 7 laptops all with the same results. Also tested an Open GL, and IOS version with the same results too. The demos seem to stop working right at around Monkey version 3.9. Any ideas? <br><br></td></tr></table><br>
<a name="2008851"></a>

<a name="2008852"></a>

<a name="2008853"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> they use setcolor which was enabled for html5 recently , it is slow on html5, disable it if you want to speed things up (by removing it from the code)<br><br>html5 may be the slowest target but it is fast to compile and crashes in a way that is easier to debug. <br><br></td></tr></table><br>
<a name="2008854"></a>

<a name="2008856"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DruggedBunny</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah, SetColor screwed them up. I'd swear I updated them for future use (on the BRL testers forum), but since the Blitz site is down I can't prove it!<br><br>I must have done something wrong originally, as the demos fail to set things back to SetColor 255, 255, 255 before drawing the rocket, etc. If you put that <i>before</i> the For/Next loop in each case, it should sort it (from memory). <br><br></td></tr></table><br>
<a name="2008876"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Neuro</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks guys, but is it only suppose to slow down in HTML 5? Cause the same code also crawls when running on OGL or IOS too. <br><br></td></tr></table><br>
<a name="2008903"></a>

<a name="2008904"></a>

<a name="2008905"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DruggedBunny</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Strange, I wouldn't have thought it affected OpenGL, but I can't test this right now. (I also don't have an IOS device.)<br><br>The code was setting the colour to green, drawing the 'ground', then drawing the blocks in a For/Next loop. Because SetColor had no effect on images before, it was fine.<br><br>After the change, it's manually colouring each image then drawing it. Putting a SetColor 255, 255, 255 before the For/Next loop fixes this, as images' colours are not manually changed with 255, 255, 255, so it should run full-speed again.<br><br>Here's rotationsickness.monkey fixed:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

' -----------------------------------------------------------------------------
' Rotation around player. Confusing, disorientating and, well, sickening...
' -----------------------------------------------------------------------------

' WARNING: MAY INDUCE MOTION SICKNESS! Not actually kidding...

' This looks and feels 'wrong' while moving since the rotation point is also
' constantly moving, but it's doing what it's meant to do.

' Basic imports for most purposes...

Import mojo

' -----------------------------------------------------------------------------
' DISPLAY ROTATION FUNCTIONS...
' -----------------------------------------------------------------------------



' -----------------------------------------------------------------------------
' RotateDisplay: rotates view around x, y by angle...
' -----------------------------------------------------------------------------

	Function RotateDisplay (x:Float, y:Float, angle:Float)
		PushMatrix				' Store current rotation, scale, etc
		Translate x, y			' Shift origin across to here
		Rotate angle			' Rotate around origin
		Translate -x, -y		' Shift origin back where it was
	End

' -----------------------------------------------------------------------------
' ResetDisplay: resets view back to normal...
' -----------------------------------------------------------------------------

	Function ResetDisplay ()
		PopMatrix				' Restore rotation, scale, etc
	End

Class Block
	Field x:Float
	Field y:Float
	Field image:Image
End

' App class...

Class Game Extends App

	Field player:Image
	Field block:Image
	
	Field px:Float
	Field py:Float

	Field pxs:Float
	Field pys:Float

	Field ang:Float
	Field angspeed:Float
	
	Field rotatemode:Int
	
	Field blocklist:List &lt;Block&gt;
	
	Method OnCreate ()		' Load media and SET UPDATE RATE here...

		px = DeviceWidth * 0.5	
		py = DeviceHeight * 0.5	

		player	= LoadCenteredImage ("player.png")
		block		= LoadCenteredImage ("block.png")
		
		blocklist = New List &lt;Block&gt;
		
		For Local loop:Int = 1 To 100
			Local temp:Block = New Block
			temp.image = block
			temp.x = Rnd (-DeviceWidth, DeviceWidth)
			temp.y = Rnd (-DeviceHeight, DeviceHeight)
			blocklist.AddLast temp
		Next
		
		SetUpdateRate 60
		
		rotatemode = 1
		
	End
	
	Method OnUpdate ()		' Take input and update game here...

		If KeyDown (KEY_LEFT) Then pxs = pxs - 0.1
		If KeyDown (KEY_RIGHT) Then pxs = pxs + 0.1

		If KeyDown (KEY_UP) Then pys = pys - 0.1
		If KeyDown (KEY_DOWN) Then pys = pys + 0.1

		If KeyDown (KEY_Z) Then angspeed = angspeed - 0.01
		If KeyDown (KEY_X) Then angspeed = angspeed + 0.01

		If KeyDown (KEY_SPACE)
			pxs = 0
			pys = 0
		Endif

		If KeyDown (KEY_ENTER) Then angspeed = 0
		
		If KeyHit (KEY_R)
		
			rotatemode = rotatemode + 1
			If rotatemode &gt; 4 Then rotatemode = 1
		
			px = DeviceWidth * 0.5	
			py = DeviceHeight * 0.5	

			pxs = 0
			pys = 0
			
			ang = 0
			angspeed = 0
		
		Endif
		
		px = px + pxs
		py = py + pys
		
		If px &lt; 0
			pxs = 0
			px = 0
		Else
			If px &gt; DeviceWidth
				pxs = 0
				px = DeviceWidth - 1
			Endif
		Endif

		If py &lt; 0
			pys = 0
			py = 0
		Else
			If py &gt; DeviceHeight
				pys = 0
				py = DeviceHeight - 1
			Endif
		Endif
		
		ang = ang + angspeed
		' ang = ang Mod 360.0 ' Mod returning Int!

	End
	
	Method OnRender ()		' Draw frame here...
	
		Cls 64, 96, 128

		Select rotatemode
		
			Case 1

				RotateDisplay px, py, ang
				
				SetColor 0, 64, 0
				DrawRect -DeviceWidth, DeviceHeight * 0.5, DeviceWidth * 4.0, DeviceHeight * 3.0 ' Just making sure...

				SetColor 255, 255, 255

				For Local b:Block = Eachin blocklist
					DrawImage b.image, b.x, b.y, 0, 0.25, 0.25
				Next
		
				ResetDisplay

				' Display set to normal so player drawn normally, with control movements
				' relative to screen as normal...
				
				DrawImage player, px, py, 0, 0.25, 0.25
				
			Case 2

				RotateDisplay px, py, ang
				
				SetColor 0, 64, 0
				DrawRect -DeviceWidth, DeviceHeight * 0.5, DeviceWidth * 4.0, DeviceHeight * 3.0 ' Just making sure...
					
				SetColor 255, 255, 255

				For Local b:Block = Eachin blocklist
					DrawImage b.image, b.x, b.y, 0, 0.25, 0.25
				Next
		
				' Display still set to rotated state, so player movements are affected by rotation,
				' ie. relative to background...
				
				DrawImage player, px, py, 0, 0.25, 0.25

				ResetDisplay
				
			Case 3

				RotateDisplay DeviceWidth * 0.5, DeviceHeight * 0.5, ang
				
				SetColor 0, 64, 0
				DrawRect -DeviceWidth, DeviceHeight * 0.5, DeviceWidth * 4.0, DeviceHeight * 3.0 ' Just making sure...
					
				SetColor 255, 255, 255

				For Local b:Block = Eachin blocklist
					DrawImage b.image, b.x, b.y, 0, 0.25, 0.25
				Next
		
				ResetDisplay
				
				DrawImage player, px, py, 0, 0.25, 0.25
				
			Case 4

				RotateDisplay DeviceWidth * 0.5, DeviceHeight * 0.5, ang
				
				SetColor 0, 64, 0
				DrawRect -DeviceWidth, DeviceHeight * 0.5, DeviceWidth * 4.0, DeviceHeight * 3.0 ' Just making sure...
					
				SetColor 255, 255, 255

				For Local b:Block = Eachin blocklist
					DrawImage b.image, b.x, b.y, 0, 0.25, 0.25
				Next
		
				DrawImage player, px, py, 0, 0.25, 0.25
				
				ResetDisplay
				
		End
		
		DrawText "[CURSORS - move player] [SPACE - stop player]", 20, 20
		DrawText "[Z/X - rotate around player] [ENTER - stop rotation]", 20, 40

		Local mode$ = ""
		
		Select rotatemode
			Case 1
				mode = "Rotate around player (controls relative to screen)"
			Case 2
				mode = "Rotate with player (controls relative to background)"
			Case 3
				mode = "Rotate around centre (controls relative to screen)"
			Case 4
				mode = "Rotate around centre (controls relative to background)"
		End
		
		DrawText "[R - change rotation mode: " + mode, 20, 60
		
	End
	
End

' Main function...

Function Main ()
	New Game
End

' Set image's handle to centre...

Function MidHandle (image:Image)
		 image.SetHandle image.Width () * 0.5, image.Height () * 0.5
End

' Load an image with handle already centered (uses MidHandle function above)...

Function LoadCenteredImage:Image (image:String)
		 Local img:Image = LoadImage (image)
		 MidHandle img
		 Return img
End
</textarea><br><br>... and here's jump_rotate.monkey:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

' -----------------------------------------------------------------------------
' Rotate background around player...
' -----------------------------------------------------------------------------

' Demo 2. Quick hack showing rotation while jumping. Worst jumping code ever,
' hard-coded rotation speed for jump height, and very buggy... but that's not
' the point! It rotates correctly around the player, that's all.

' Move using LEFT/RIGHT and hit SPACE to jump really badly...



' -----------------------------------------------------------------------------
' Basic imports for most purposes...
' -----------------------------------------------------------------------------

Import mojo

' -----------------------------------------------------------------------------
' DISPLAY ROTATION FUNCTIONS...
' -----------------------------------------------------------------------------



' -----------------------------------------------------------------------------
' RotateDisplay: rotates view around x, y by angle...
' -----------------------------------------------------------------------------

	Function RotateDisplay (X:Float, Y:Float, angle:Float)
		PushMatrix				' Store current rotation, scale, etc
		Translate X, Y			' Shift origin across to here
		Rotate angle			' Rotate around origin
		Translate -X, -Y		' Shift origin back where it was
	End

' -----------------------------------------------------------------------------
' ResetDisplay: resets view back to normal...
' -----------------------------------------------------------------------------

	Function ResetDisplay ()
		PopMatrix				' Restore rotation, scale, etc
	End



' -----------------------------------------------------------------------------
' USAGE...
' -----------------------------------------------------------------------------

' Step 1) In OnRender, call RotateDisplay with x/y position to rotate around
' and the angle at which to draw...

' Step 2) DRAW BACKGROUND STUFF HERE!

' Step 3) Call ResetDisplay and continue drawing as normal...

' See OnRender code for example...



' -----------------------------------------------------------------------------
' TOP TIP! You can use this call to just rotate around screen centre...
' -----------------------------------------------------------------------------

' 	RotateDisplay DeviceWidth * 0.5, DeviceHeight * 0.5, ang




Class Block
	Field X:Float
	Field Y:Float
	Field Image:Image
End

' App class...

Class Game Extends App

	Field gravity:Float = 0.05
	
	Field player:Image
	Field block:Image
	
	Field px:Float
	Field py:Float

	Field pxs:Float
	Field pys:Float

	Field groundy:Float
	
	Field ang:Float
	Field angspeed:Float

	Field rotating:Bool
	
	Field blocklist:List &lt;Block&gt;
	
	Method OnCreate ()		' Load media and SET UPDATE RATE here...

'		px = DeviceWidth * 0.5
		py = DeviceHeight * 0.5	

		player	= LoadCenteredImage ("player.png")
		block	= LoadCenteredImage ("block.png")
		
		blocklist = New List &lt;Block&gt;
		
		For Local loop:Int = 1 To 100
			Local temp:Block = New Block
			temp.Image = block
			temp.X = Rnd (-DeviceWidth, DeviceWidth)
			temp.Y = Rnd (-DeviceHeight, DeviceHeight)
			blocklist.AddLast temp
		Next
		
		groundy = 300 + (player.Height * 0.5) * 0.25
		
		SetUpdateRate 60
		
	End
	
	Method OnUpdate ()		' Take input and update game here...

		Local movinglr:Bool
		
		If Not rotating

			If KeyDown (KEY_LEFT)
				pxs = pxs - 0.1
				movinglr = True
			Endif
			
			If KeyDown (KEY_RIGHT)
				pxs = pxs + 0.1
				movinglr = True
			Endif
		
			If py &gt;= 295
				If KeyDown (KEY_SPACE)
					pys = -3
					rotating = True
				Endif
			Endif

		Endif
		
		If pxs &gt; 2 Then pxs = 2
		If pxs &lt; -2 Then pxs = -2
		px = px + pxs

		If Not movinglr And py &gt;= 295 Then pxs = pxs * 0.99
		
		pys = pys + gravity
		py = py + pys
		If py &gt; 300
			py = 300
			pys = -pys * 0.5
		Endif
		
		If px &lt; 0 Or px &gt; DeviceWidth
			pxs = -pxs
			px = px + pxs
		Endif

		If py &lt; 0 Or py &gt; DeviceHeight
			pys = -pys
			py = py + pys
		Endif
		
		If rotating
			ang = ang + 3 * Sgn (pxs)
			If ang &lt; -360 Or ang &gt; 360
				ang = 0
				rotating = False
			Endif
		Endif

	End
	
	Method OnRender ()		' Draw frame here...
	
		Cls 64, 96, 128

		' ---------------------------------------------------------------------
		' Rotate display around player position...
		' ---------------------------------------------------------------------
			RotateDisplay px, py, ang
		' ---------------------------------------------------------------------

		' DRAW BACKGROUND...
				
		SetColor 0, 64, 0
		DrawRect -DeviceWidth, groundy, DeviceWidth * 4.0, DeviceHeight * 3.0 ' Just making sure...
	
		SetColor 255, 255, 255
		
		For Local B:Block = Eachin blocklist
			DrawImage B.Image, B.X, B.Y, 0, 0.25, 0.25
		Next
	
		DrawText "-11 -10 -09 -08 -07 -06 -05 -04 -03 -02 -01 000 +01 +02 +03 +04 +05 +06 +07 +08 +09 +10 + 11", 0, groundy
		
		' ---------------------------------------------------------------------
		' Set display back to previous (ie. normal) state...
		' ---------------------------------------------------------------------
			ResetDisplay
		' ---------------------------------------------------------------------
		
		' DRAW PLAYER AT NORMAL ORIENTATION and DRAW DISPLAY TEXT...
		
		DrawImage player, px, py, 0, 0.25, 0.25
		DrawText "[CURSORS - move player] [SPACE - jump]", 20, 20

	End
	
End

' Main function...

Function Main ()
	New Game
End

' Set image's handle to centre...

Function MidHandle (Image:Image)
		 Image.SetHandle Image.Width () * 0.5, Image.Height () * 0.5
End

' Load an image with handle already centered (uses MidHandle function above)...

Function LoadCenteredImage:Image (Image:String)
		 Local img:Image = LoadImage (Image)
		 MidHandle img
		 Return img
End
</textarea> <br><br></td></tr></table><br>
<a name="2008994"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Neuro</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks DruggedBunny. But new codes, doesn't compiles on GLFX and on HTML 5 it throws this error :<br><b>TypeError : Unable to get value of the property 'bbm_SetHandle': object is null or undefined</b> <br><br></td></tr></table><br>
<a name="2009029"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DruggedBunny</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Did you save it in the same folder as the original, and delete the .build folder? I don't see why it should fail, since that's a Monkey command...<br><br>Just tried copying and pasting both over the originals and they're working fine here on both HTML5 and GLFW. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
