<!DOCTYPE html><html lang="en" ><head ><title >Movement in Monkey !!</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Movement in Monkey !!</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=507" >Monkey Programming</a>/<a href="#bottom" >Movement in Monkey !!</a><br><br>
<a name="2055574"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >trainer</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello guys ..<br>Can u plz tell me how can I make some self movement in the background of my game .. for example, I want to let the bird flying at the background while I'm playing .. ??<br><br>Is there any such a way to do this ?? :\ <br><br></td></tr></table><br>
<a name="2055576"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gerry Quinn</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sure.  <br><br>You have moving objects in your game anyway, right?  The bird is just another one, except it doesn't interact with anything (its movement is simply based on time passing).<br><br>Method OnRender:Int()<br>   Cls( 0, 0, 0 )<br>   DrawBird()<br>   DrawTetrisBlocks()<br>   Return 0<br>End<br><br><br>There is no direct way to import an animated image like a GIF to use as a background, if that's what you're thinking of.  But you can import a series of frames and just draw the correct one based on the time. <br><br></td></tr></table><br>
<a name="2055648"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >trainer</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Can u plz explain this more<br>What shall I write inside those two methods :<br>DrawBird()<br>DrawTetrisBlocks()<br>?? <br><br></td></tr></table><br>
<a name="2055650"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CodeGit</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Try this link -<br><br><a href="http://www.monkeycoder.co.nz/Community/posts.php?topic=3318" target="_blank">http://www.monkeycoder.co.nz/Community/posts.php?topic=3318</a> <br><br></td></tr></table><br>
<a name="2055659"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gerry Quinn</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's an app that draws a moving dot.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict


Import mojo


Function Main:Int()
	New DotApp()									
	Return 0
End


Class DotApp Extends App

	Field time:Float
		
	Method OnCreate:Int()
		SetUpdateRate( 30 )
		Return 0
	End
	
	Method OnUpdate:Int()
		time = Millisecs() / 1000.0
		Return 0
	End
		
	Method OnRender:Int()		
		Cls( 255, 224, 224 )
		Local dotAngle:Float = time * 120.0
		Local xPos:Float = DeviceWidth() * ( 0.5 + 0.3 * Cos( dotAngle ) )
		Local yPos:Float = DeviceHeight() * ( 0.5 + 0.3 * Sin( dotAngle ) )
		SetColor( 255, 0, 0 )
		DrawCircle( xPos, yPos, 10 )
		Return 0
	End

End	
	
</textarea> <br><br></td></tr></table><br>
<a name="2056348"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dawlane</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well I would have posted here a lot sooner, but I've been a little busy as of late.<br><br>@NoraHarthi:<br>I take it that you are new to programming. So first study the tutorials in the link that codeGit has supplied and any others that you can find as I will be mentioning Class, Lists and using Bitwise operations for checking certain states.<br><br>There are a number of ways to make a game object (in this case sprites) automatically move. Some are simple while others can get complicated. It all depends on what you are trying to do.<br>The simplest would be to add an offset value to the current sprites position and then changing the offset value when a condition is met.<br><br>e.g<br>If x &lt;= target_position_left OR x &gt;= target_position_right then offset = -offset<br>x = x + offset<br><br>So how to use it? Well lets make a sort of simple class that's going to do all the work of moving, animating and collision checking.<br>Note in general I would write a simple base sprite class to handle the display, animation and collision data side of things and then extend it for doing things like movement, including collision checking. I would also write a class and make it global to control timing and to do number of other tasks. Look up delta timing around the forum and internet for one usage example.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Import mojo

Class CSprite
	'Adding objects to a list is an easy way to
	'to preform operations on many object of the same type
	Global sprite_list:= New List&lt;CSprite&gt;	'hold all sprite objects
	Global collision_list:= New IntList 	'hold any sprite object id's that have collided

	Field x:Int, y:int	'Screen co-oridantes
	Field image:Image	'The image to use
	Field flags:Int		'A demonstration using bitwise operations
	Field x1:Int, x2:Int, y1:Int, y2:Int	'Screen coordinates when to change direction
	Field offset:Int	'Pixels to move
	Field current_frame:Int	'current frame of animation
	Field radius:Float		'used for collision detection
	Field id:Int			'identification of the sprite object
	
	'A few constant variables that will let use control how the sprite is displayed
	'and moved using bitwise operations
	Const VISIBLE:Int = 1
	Const ANIMATE:Int = 2
	Const MOVE_HORI:Int = 4
	Const MOVE_VERT:Int = 8

	'Primary function to create a sprite and return the sprite object back
	Function CreateSprite:CSprite(id:Int, x:Int, y:Int, image:Image, flags:Int, radius:Float)
		Local sp:CSprite = New CSprite()
		sp.id = id
		sp.x = x
		sp.y = y
		sp.image = image
		sp.flags = flags
		sp.current_frame = 0
		sp.radius = radius
		return sp
	End Function
	
	'Function to add a sprite to the sprite_list. 
	Function AddSprite:Void(id:int, x:Int, y:Int, image:Image, flags:Int, offset:Int, x1:Int, x2:Int, y1:Int, y2:Int, radius:float)
		Local sp:= CreateSprite(id, x, y, image, flags, radius)
		sp.x1 = x1	'Left limit
		sp.x2 = x2	'Right limit
		sp.y1 = y1	'Top limit
		sp.y2 = y2	'Bottom limit
		sp.offset = offset
                sprite_list.AddLast(sp)	'Place the new object to the movement list
	End Function

	'Function to select movement logic.
	'This would also be a good place to do any collision detection by passing say
	'the player sprite to this function
	Function Update:Void(player:CSprite)
		If sprite_list.IsEmpty() Then Return	'Safty check. If the list is empty then exit
		If Not collision_list.IsEmpty() Then collision_list.Clear()	'Clear any previous collision occurrences
		
		'go through each item in the list and do logic operations
		For Local i:CSprite = EachIn sprite_list
			If i.flags &amp; CSprite.MOVE_HORI Then i.UpdateX()	'bitwise 'AND' to check only the horizontal movement is on
			If i.flags &amp; CSprite.MOVE_VERT Then i.UpdateY()	'bitwise 'AND' to check only the vertical movement is on 
			If i.flags &amp; CSprite.ANIMATE Then i.Animate()	'bitwise 'AND' to check if animation is on
			i.CheckCollision(player, i)
		Next
	End Function

	'Move logic for horizontal
	Method UpdateX:Void()
		If Self.x &lt; Self.x1 OR Self.x &gt; Self.x2 Then Self.offset = -Self.offset
		Self.x += Self.offset
	End Method

	'Move logic for vertical
	Method UpdateY:Void()
		If Self.y &lt; Self.y1 OR Self.y &gt; Self.y2 Then Self.offset = -Self.offset
		Self.y += Self.offset
	End Method
	
	'Animte a sprite
	Method Animate:Void()
		Self.current_frame += 1
		If Self.current_frame &gt;= Self.image.Frames() Then Self.current_frame = 0
	End Method
	
	'Function to draw every thing in the sprite list
	Function DrawAll:Void()
		If sprite_list.IsEmpty() Then Return	'If the list is empty then exit
		For Local i:CSprite = EachIn sprite_list
			i.DrawSprite()
		Next
	End Function
	
	'Method just to draw a single sprite
	Method DrawSprite:Void()		
		If Self.flags &amp; VISIBLE Then DrawImage(Self.image, Self.x, Self.y, Self.current_frame) 'Only draw the image if the visible flag is set
		'Code below is not needed but shows you the collision sphere
		SetAlpha(0.4)
		SetColor(255, 0, 0)
		DrawCircle(Self.x, Self.y, Self.radius)
		SetColor(255, 255, 255)
		SetAlpha(1.0)
	End Method
	
	'Collision detection
	'To check to see if there has been more than one collision
	'add it to a list that we can check
	Method CheckCollision:Void(sp1:CSprite, sp2:CSprite)
		If Circle2Circle(sp1.x, sp1.y, sp2.x, sp2.y, sp1.radius, sp2.radius)
			collision_list.AddLast(sp2.id)
		EndIf
	End Method
End Class

'Globles for holding the screen height and width
Global scr_width:Int
Global scr_height:Int

'Circle to circle collision detection
Function Circle2Circle:Bool(xPos:Float, yPos:Float, xPos2:Float, yPos2:Float, iRadius:Float, iRadius2:Float)

	Local dx:Float
	Local dy:Float
	Local radii:Float
			
	'get the distance between the two centres
	dx = xPos - xPos2
	dy = yPos - yPos2
			
	'add the two radii
	radii = iRadius + iRadius2
			
	'returns true if a collision has occurred else false
	If ( (dx * dx) + (dy * dy) &lt;= radii * radii) Then Return True
	Return False
		
End Function

'Main Program class
Class CGame Extends App
	
	'Two self explained variables
	Field bubble:Image
	Field player:CSprite
	
	'The OnCreate method for basic initialisation
	Method OnCreate:Int()
		SetUpdateRate(30)	'Refresh rate
		scr_width = DeviceWidth()
		scr_height = DeviceHeight()
                
                'Use your own animated image here.
		bubble = LoadImage("cyan_bubble.png", 64, 60, 10, Image.MidHandle)
		
		Local radius:Float = 24	'set the collision radius
		'create the player object
		'CreateSprite( id, x, y, image, flags, collision_radius 0
		player = CSprite.CreateSprite(1, 50, 100, bubble, CSprite.ANIMATE | CSprite.VISIBLE, radius)
		Local animate_y:Int = CSprite.VISIBLE | CSprite.MOVE_VERT | CSprite.ANIMATE
		Local animate_x:Int = CSprite.VISIBLE | CSprite.MOVE_HORI | CSprite.ANIMATE
		Local y_axis:Int = CSprite.VISIBLE | CSprite.MOVE_VERT
		Local x_axis:Int = CSprite.VISIBLE | CSprite.MOVE_HORI | CSprite.ANIMATE
		
		'Add movable objects to a list
		'AddSprite( id, x, y, image, flags, offset, target_left, target_right, target_top, target_bottom, collison_radius )
		CSprite.AddSprite(2, 96, 96, bubble, animate_x, 10, 0, scr_width, 0, 0, radius)
		CSprite.AddSprite(3, 500, 100, bubble, animate_y, 6, 0, 0, 100, scr_height - 200, radius)
		CSprite.AddSprite(4, 200, 260, bubble, x_axis, -4, 100, scr_width - 200, 0, 0, radius)
		CSprite.AddSprite(5, 200, 260, bubble, y_axis, 6, 0, 0, 100, scr_height - 100, radius)
		CSprite.AddSprite(6, 280, 320, bubble, CSprite.VISIBLE, -4, 100, scr_width - 200, 0, 0, radius)
		CSprite.AddSprite(7, 330, 350, bubble, CSprite.VISIBLE, 6, 0, 0, 100, scr_height - 100, radius)
		Return 0
	End Method
	
	'The OnUpdate do logic stuff here
	Method OnUpdate:Int()
		'Simple playe controls
		If KeyDown(KEY_LEFT) And Self.player.x &gt; 0 Then Self.player.x -= 4
		If KeyDown(KEY_RIGHT) And Self.player.x &lt; scr_width Then Self.player.x += 4
		If KeyDown(KEY_UP) And Self.player.y &gt; 0 Then Self.player.y -= 4
		If KeyDown(KEY_DOWN) And Self.player.y &lt; scr_height Then Self.player.y += 4
		
		player.Animate()	'Animate the player
		CSprite.Update(player)	'Update any movement/animation and check for collision by passing the player sprite object
		Return 0
	End Method
	
	'Drawing functions
	Method OnRender:Int()
		Cls
		
		'Draw all the movable objects
		CSprite.DrawAll()
		'Draw only the player in a diferent colour
		SetColor(128, 128, 128)
		Self.player.DrawSprite()
		SetColor(255, 255, 255)
		
		'Print out collistion information.
		Local y:Int = 100
		If Not CSprite.collision_list.IsEmpty()
			For Local i:Int = EachIn CSprite.collision_list
				DrawText("PLAYER HAS COLLIDED WITH SPRITE:" + i, scr_width / 2 - 100, y)
				y += 12
			Next
		EndIf
		
		Return 0
	End Method
End Class

'Program entry point
Function Main:Int()
	New CGame
	Return 0
End Function</textarea><br><br>The the example above would be good for some thing like a platform game like Manic Miner (using a different collision method and adding delta time), but wouldn't work well if you wanted to do diagonal or direction movement. Then you would use vectors which open up lots of possibilities.<br>Lookup vector sprite movement on the internet; the language isn't important but the maths and logic are and can be adapted to work with any programming language with a little work.<br><br>Other things to search for are.<br>Sprite Z-Order<br>Sprite path way points<br>Sprite path following<br>Path finding algorithms <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
