<!DOCTYPE html><html lang="en" ><head ><title >help with gl lights</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >help with gl lights</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=507" >Monkey Programming</a>/<a href="#bottom" >help with gl lights</a><br><br>
<a name="2020774"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am playing around with Mark's opengl test. I can't seem to get light 0 working properly... I assume its to do with the lack of normals for the cube.<br><br>The light is completely black until the cube rotates and then the whole thing lights up and then fades to black.<br><br>How would I go about adding normals? I have not used vbo's before and getting my head around them is tricky.<br><br>Could anyone help please?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

#rem

Very simple 'does it work' opengl test app.

Note: for glfw and ios, you'll need to modify config.h in .build/target folder and change ENABLE_DEPTH_BUFFER to 1.

#end

Import mojo

Import opengl.gles11

Class GLApp Extends App

	Field vbo,ibo	'vertex buffer,index buffer
	Field rot#

	Method OnCreate()
	
		SetUpdateRate 60

	End
	
	Field _inited
	
	Method Init()

		If _inited Return
		_inited=True

		'create buffer objects
		Local bufs[2]
		glGenBuffers 2,bufs
		vbo=bufs[0]
		ibo=bufs[1]

		'create VBO
		Local vtxs:=[ -.5,+.5,-.5, +.5,+.5,-.5, +.5,-.5,-.5, -.5,-.5,-.5,  -.5,+.5,+.5, +.5,+.5,+.5, +.5,-.5,+.5, -.5,-.5,+.5 ]
		
		
		
		'
		Local vbuf:=DataBuffer.Create( vtxs.Length*4 )
		For Local i=0 Until vtxs.Length
			vbuf.PokeFloat i*4,vtxs[i]
		Next
		glBindBuffer GL_ARRAY_BUFFER,vbo
		glBufferData GL_ARRAY_BUFFER,vbuf.Size(),vbuf,GL_STATIC_DRAW

		'create IBO		
		Local idxs:=[ 0,1,2, 0,2,3, 4,0,3, 4,3,7, 1,5,6, 1,6,2, 4,5,6, 4,6,7, 4,5,1, 4,1,0, 7,3,2, 7,2,6 ]
		'
		Local ibuf:=DataBuffer.Create( idxs.Length*2 )
		For Local i=0 Until idxs.Length
			ibuf.PokeShort i*2,idxs[i]
		Next
		glBindBuffer GL_ELEMENT_ARRAY_BUFFER,ibo
		glBufferData GL_ELEMENT_ARRAY_BUFFER,ibuf.Size(),ibuf,GL_STATIC_DRAW
		
	End
	
	Method OnSuspend()
		Error ""
	End
	
	Method OnRender()

		Init
		
		glViewport 0,0,DeviceWidth,DeviceHeight
	
		glDisable( GL_BLEND );
		
		glMatrixMode( GL_PROJECTION );
		glLoadIdentity
		glFrustumf -1,1,-1,1,1,100
		
		glMatrixMode( GL_MODELVIEW );
		glLoadIdentity
		

		'lighting
		glMatrixMode(GL_MODELVIEW)
		glEnable(GL_LIGHTING)
		glEnable(GL_LIGHT0)
		glLoadIdentity()
		glLightfv( GL_LIGHT0, GL_POSITION, [-1.0, 2.0, -1.0] )
		
		
		
		rot+=1
		glRotatef rot,0,0,1

		glClearColor 0,0,.5,1
		glClear GL_COLOR_BUFFER_BIT|GL_DEPTH_BUFFER_BIT
		
		glEnableClientState GL_VERTEX_ARRAY
		glDisableClientState GL_COLOR_ARRAY
		glDisableClientState GL_TEXTURE_COORD_ARRAY

		glBindBuffer GL_ARRAY_BUFFER,vbo
		glVertexPointer 3,GL_FLOAT,0,0
		glBindBuffer GL_ELEMENT_ARRAY_BUFFER,ibo

		glEnable GL_DEPTH_TEST
		glDepthFunc GL_LESS		
		glDepthMask True
		
		
		glLoadIdentity
		glTranslatef 0,-1,-2
		glRotatef 90,1,0,0
		glScalef 4,1,1
		glColor4f 0,1,0,1
		glDrawElements GL_TRIANGLES,36,GL_UNSIGNED_SHORT,0
		
		glLoadIdentity
		glTranslatef 0,0,-2
		glRotatef rot,1,1,0
		glScalef 1,1,1
		glColor4f 1,1,0,1
		glDrawElements GL_TRIANGLES,36,GL_UNSIGNED_SHORT,0
		

		
	End
	
End

Function Main()
	New GLApp
End


</textarea> <br><br></td></tr></table><br>
<a name="2020801"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >bruZard</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> you need vertex normals and the corresponding pointer to that data. scale your vertex buffer to "vertex count * 2" and poke vertexX, vertexY, vertexZ, normalX, normalY, normalZ. <br><br></td></tr></table><br>
<a name="2020822"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> ok well i tried to write a fresh example for showing a cube with normals. Doesn't work :P<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Import mojo
Import opengl.gles11


'class to store vertex data
Class Vertex

	Field x : Float
	Field y : Float
	Field z : Float
	
	Field nx : Float
	Field ny : Float
	Field nz : Float

	Method New( vx : Float, vy : Float, vz : Float, vnx : Float, vny : Float, vnz : Float )
		Self.x = vx
		Self.y = vy
		Self.z = vz
		Self.nx = vnx
		Self.ny = vny
		Self.nz = vnz
	End Method

	
	Function Create : Vertex( vx : Float, vy : Float, vz : Float, vnx : Float, vny : Float, vnz : Float )
		Local this : Vertex = New Vertex( vx, vy, vz, vnx, vny, vnz )
		Return this
	End Function
	

End Class





'stores stuff
Global index : Int[ 36 ]
Global verts : Vertex[ 24 ]
Global vbo, ibo
Global rot : Float


Class GLApp Extends App



	Method OnCreate()
	
		SetUpdateRate 60
		
		BuildCube()
		InitVBO()
		
	End Method
	
	
		
	Method InitVBO()
	
		'grab vertex and index
		Local vcount : Int = verts.Length
		Local icount : Int = index.Length
			
	
		'create vertex data buffer
		Local vdata := DataBuffer.Create( vcount * 24 )
		For Local i := 0 Until vcount
			Local p = i * 24
			Local v : Vertex = verts[ i ]
			vdata.PokeFloat p + 0, v.x
			vdata.PokeFloat p + 4, v.y
			vdata.PokeFloat p + 8, v.z
			vdata.PokeFloat p + 12, v.nx
			vdata.PokeFloat p + 16, v.ny
			vdata.PokeFloat p + 20, v.nz
		Next
		
		'create index data buffer
		Local idata := DataBuffer.Create( icount * 2 )
		For Local i := 0 Until icount
			idata.PokeShort i * 2, index[ i ]
		Next
			
		'generate buffers
		Local bufs[ 2 ]
		glGenBuffers 2, bufs
		
		vbo = bufs[ 0 ]
		glBindBuffer GL_ARRAY_BUFFER, vbo
		glBufferData GL_ARRAY_BUFFER, vdata.Size(), vdata, GL_STATIC_DRAW
	
		ibo = bufs[ 1 ]
		glBindBuffer GL_ELEMENT_ARRAY_BUFFER, ibo
		glBufferData GL_ELEMENT_ARRAY_BUFFER, idata.Size(), idata, GL_STATIC_DRAW	

		
		'is this needed here?
		glEnableClientState GL_VERTEX_ARRAY
		glEnableClientState GL_NORMAL_ARRAY	
		glDisableClientState GL_TEXTURE_COORD_ARRAY
		glDisableClientState GL_COLOR_ARRAY
		
		'glVertexPointer 3, GL_FLOAT, 32, 0
		'glNormalPointer GL_FLOAT, 32, 12
		
		
	End Method

	



	Method BuildCube()
		
		' Top
		verts[ 0 ] = Vertex.Create( 1, 1, -1, 0, 1, 0 )
		verts[ 1 ] = Vertex.Create( -1, 1, -1, 0, 1, 0 )
		verts[ 2 ] = Vertex.Create( -1, 1, 1, 0, 1, 0 )
		verts[ 3 ] = Vertex.Create(	1, 1, 1, 0, 1, 0 )
	
		' Bottom
		verts[ 4 ] = Vertex.Create( 1, -1, 1, 0, -1, 0 )
		verts[ 5 ] = Vertex.Create( -1, -1, 1, 0, -1, 0 )
		verts[ 6 ] = Vertex.Create( -1, -1, -1, 0, -1, 0 )
		verts[ 7 ] = Vertex.Create( 1, -1, -1, 0, -1, 0 )
		
		' Front
		verts[ 8 ] = Vertex.Create( 1, 1, 1, 0, 0, 1 )
		verts[ 9 ] = Vertex.Create( -1, 1, 1, 0, 0, 1 )
		verts[ 10 ] = Vertex.Create( -1, -1, 1, 0, 0, 1 )
		verts[ 11 ] = Vertex.Create( 1, -1, 1, 0, 0, 1 )
	
		' Back
		verts[ 12 ] = Vertex.Create( 1, -1, -1, 0, 0, -1 )
		verts[ 13 ] = Vertex.Create( -1, -1, -1, 0, 0, -1 )
		verts[ 14 ] = Vertex.Create( -1, 1, -1, 0, 0, -1 )
		verts[ 15 ] = Vertex.Create( 1, 1, -1, 0, 0, -1 )
		
		' Left
		verts[ 16 ] = Vertex.Create( -1, 1, 1, -1, 0, 0 )
		verts[ 17 ] = Vertex.Create( -1, 1, -1, -1, 0, 0 )
		verts[ 18 ] = Vertex.Create( -1, -1, -1, -1, 0, 0 )
		verts[ 19 ] = Vertex.Create( -1, -1, 1, -1, 0, 0 )
		
		' Right
		verts[ 20 ] = Vertex.Create( 1, 1, -1, 1, 0, 0 )
		verts[ 21 ] = Vertex.Create( 1, 1, 1, 1, 0, 0 )
		verts[ 22 ] = Vertex.Create( 1, -1, 1, 1, 0, 0 )
		verts[ 23 ] = Vertex.Create( 1, -1, -1, 1, 0, 0 )
		
		
		' Index Array (define our triangles)
		index[ 0 ] = 0;		index[ 1 ] = 1;		index[ 2 ] = 2;
		index[ 3 ] = 2;		index[ 4 ] = 3;		index[ 5 ] = 0;
		
		index[ 6 ] = 4;		index[ 7 ] = 5;		index[ 8 ] = 6;
		index[ 9 ] = 6;		index[ 10 ] = 7; 	index[ 11 ] = 4;
		
		index[ 12 ] = 8; 	index[ 13 ] = 9; 	index[ 14 ] = 10;
		index[ 15 ] = 10; 	index[ 16 ] = 11; 	index[ 17 ] = 8;
		
		index[ 18 ] = 12; 	index[ 19 ] = 13;	index[ 20 ] = 14;
		index[ 21 ] = 14; 	index[ 22 ] = 15;	index[ 23 ] = 12;
		
		index[ 24 ] = 16;	index[ 25 ] = 17;	index[ 26 ] = 18;
		index[ 27 ] = 18;	index[ 28 ] = 19;	index[ 29 ] = 16;
		
		index[ 30 ] = 20;	index[ 31 ] = 21;	index[ 32 ] = 22;
		index[ 33 ] = 22;	index[ 34 ] = 23;	index[ 35 ] = 20;
	
	End Method

	
	
	

	Method OnRender()

		glViewport 0,0,DeviceWidth,DeviceHeight
	
		glDisable( GL_BLEND );
		
		glMatrixMode( GL_PROJECTION );
		glLoadIdentity
		glFrustumf -1,1,-1,1,1,100
		
		glMatrixMode( GL_MODELVIEW );
		glLoadIdentity

		rot+=1
		glRotatef rot,0,0,1	

		glClearColor 0,0,.5,1
		glClear GL_COLOR_BUFFER_BIT|GL_DEPTH_BUFFER_BIT
		
		glEnableClientState GL_VERTEX_ARRAY
		glEnableClientState GL_NORMAL_ARRAY
		glDisableClientState GL_COLOR_ARRAY
		glDisableClientState GL_TEXTURE_COORD_ARRAY

		glBindBuffer GL_ARRAY_BUFFER,vbo
		glVertexPointer 3,GL_FLOAT,32,0
		'glNormalPointer GL_FLOAT,32,12 
		glBindBuffer GL_ELEMENT_ARRAY_BUFFER,ibo

		glEnable GL_DEPTH_TEST
		glDepthFunc GL_LESS		
		glDepthMask True
		
		glLoadIdentity
		glTranslatef 0,-1,-4
		glRotatef rot,1,1,0
		glScalef 1,1,1
		glColor4f 0,1,0,1
		glDrawElements GL_TRIANGLES,36,GL_UNSIGNED_SHORT,0
	
	
	End Method



End Class



Function Main()
	New GLApp
End Function

</textarea> <br><br></td></tr></table><br>
<a name="2020832"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >marksibly</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>&gt; glVertexPointer 3,GL_FLOAT,32,0<br><br>32 should be 24 - this is the 'stride' or 'bytes per vertex'.<br><br>Also, glNormalPointer is commented out! <br><br></td></tr></table><br>
<a name="2020854"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> ah yes, made a mistake with the stride. Correcting that and the cube is visible. Still an issue with the normal though.<br><br><div class="quote"> Also, glNormalPointer is commented out!  <br></div><br><br>It doesn't compile with the glNormalPointer commented out. It fails with the following error:<br><br><pre class=code>
/Users/saferguson09/Desktop/gltest/main.build/glfw/xcode/../main.cpp:3595: error: no matching function for call to '_glNormalPointer(int, int, int)'
/Users/saferguson09/Desktop/gltest/main.build/glfw/xcode/../main.cpp:3086: note: candidates are: void _glNormalPointer(int, int, int, RamBuffer*)
/Users/saferguson09/Desktop/gltest/main.build/glfw/xcode/../main.cpp:3090: note:                 void _glNormalPointer(int, int, int, int)


TRANS Failed to execute 'xcodebuild -configuration Debug', return code=16640
** BUILD FAILED **
The following build commands failed:
	CompileC build/MonkeyGame.build/Debug/MonkeyGame.build/Objects-normal/x86_64/main.o ../main.cpp normal x86_64 c++ com.apple.compilers.llvmgcc42
(1 failure)
</pre><br><br>I am attempting to use: glNormalPointer GL_FLOAT,24,12<br><br>Also attempting to enable a light results in the app to exit immediately. Doesn't matter where I place it. This worked in another test I did but might be doing it wrong:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
		glMatrixMode( GL_MODELVIEW )
		glLoadIdentity()
		glEnable( GL_LIGHTING )
		glEnable( GL_LIGHT0 )
		glLightfv( GL_LIGHT0, GL_POSITION, [0.0, 0.0, -1.0] )
</textarea><br><br>Cheers! <br><br></td></tr></table><br>
<a name="2020855"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> ah yes, made a mistake with the stride. Correcting that and the cube is visible. Still an issue with the normal though.<br><br><div class="quote"> Also, glNormalPointer is commented out!  <br></div><br><br>It doesn't compile with the glNormalPointer commented out. It fails with the following error:<br><br><pre class=code>
/Users/saferguson09/Desktop/gltest/main.build/glfw/xcode/../main.cpp:3595: error: no matching function for call to '_glNormalPointer(int, int, int)'
/Users/saferguson09/Desktop/gltest/main.build/glfw/xcode/../main.cpp:3086: note: candidates are: void _glNormalPointer(int, int, int, RamBuffer*)
/Users/saferguson09/Desktop/gltest/main.build/glfw/xcode/../main.cpp:3090: note:                 void _glNormalPointer(int, int, int, int)


TRANS Failed to execute 'xcodebuild -configuration Debug', return code=16640
** BUILD FAILED **
The following build commands failed:
	CompileC build/MonkeyGame.build/Debug/MonkeyGame.build/Objects-normal/x86_64/main.o ../main.cpp normal x86_64 c++ com.apple.compilers.llvmgcc42
(1 failure)
</pre><br><br>I am attempting to use: glNormalPointer GL_FLOAT,24,12<br><br>Also attempting to enable a light results in the app to exit immediately. Doesn't matter where I place it. This worked in another test I did but might be doing it wrong:<br><br><pre class=code>
		glMatrixMode( GL_MODELVIEW )
		glLoadIdentity()
		glEnable( GL_LIGHTING )
		glEnable( GL_LIGHT0 )
		glLightfv( GL_LIGHT0, GL_POSITION, [0.0, 0.0, -1.0] )
</pre><br><br>Cheers! <br><br></td></tr></table><br>
<a name="2020851"></a>

<a name="2020852"></a>

<a name="2020853"></a>

<a name="2020856"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I noticed bruZard's normal fix in the forums and have added that. It now compiles and the light doesn't crash the app immediately.<br><br>The cube is however, inside out in places and still looks flat shaded. Here is the updated code:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Import mojo
Import opengl.gles11


'class to store vertex data
Class Vertex

	Field x : Float
	Field y : Float
	Field z : Float
	
	Field nx : Float
	Field ny : Float
	Field nz : Float

	Method New( vx : Float, vy : Float, vz : Float, vnx : Float, vny : Float, vnz : Float )
		Self.x = vx
		Self.y = vy
		Self.z = vz
		Self.nx = vnx
		Self.ny = vny
		Self.nz = vnz
	End Method

	
	Function Create : Vertex( vx : Float, vy : Float, vz : Float, vnx : Float, vny : Float, vnz : Float )
		Local this : Vertex = New Vertex( vx, vy, vz, vnx, vny, vnz )
		Return this
	End Function
	

End Class





'stores stuff
Global index : Int[ 36 ]
Global verts : Vertex[ 24 ]
Global vbo, ibo
Global rot : Float


Class GLApp Extends App



	Method OnCreate()
	
		SetUpdateRate 60
		
		BuildCube()
		InitVBO()
		
	End Method
	
	
		
	Method InitVBO()
	
		'grab vertex and index
		Local vcount : Int = verts.Length
		Local icount : Int = index.Length
			
	
		'create vertex data buffer
		Local vdata := DataBuffer.Create( vcount * 24 )
		For Local i := 0 Until vcount
			Local p = i * 24
			Local v : Vertex = verts[ i ]
			vdata.PokeFloat p + 0, v.x
			vdata.PokeFloat p + 4, v.y
			vdata.PokeFloat p + 8, v.z
			vdata.PokeFloat p + 12, v.nx
			vdata.PokeFloat p + 16, v.ny
			vdata.PokeFloat p + 20, v.nz
		Next
		
		'create index data buffer
		Local idata := DataBuffer.Create( icount * 2 )
		For Local i := 0 Until icount
			idata.PokeShort i * 2, index[ i ]
		Next
			
		'generate buffers
		Local bufs[ 2 ]
		glGenBuffers 2, bufs
		
		vbo = bufs[ 0 ]
		glBindBuffer GL_ARRAY_BUFFER, vbo
		glBufferData GL_ARRAY_BUFFER, vdata.Size(), vdata, GL_STATIC_DRAW
	
		ibo = bufs[ 1 ]
		glBindBuffer GL_ELEMENT_ARRAY_BUFFER, ibo
		glBufferData GL_ELEMENT_ARRAY_BUFFER, idata.Size(), idata, GL_STATIC_DRAW		
		
	End Method

	



	Method BuildCube()
		
		' Top
		verts[ 0 ] = Vertex.Create( 1, 1, -1, 0, 1, 0 )
		verts[ 1 ] = Vertex.Create( -1, 1, -1, 0, 1, 0 )
		verts[ 2 ] = Vertex.Create( -1, 1, 1, 0, 1, 0 )
		verts[ 3 ] = Vertex.Create(	1, 1, 1, 0, 1, 0 )
	
		' Bottom
		verts[ 4 ] = Vertex.Create( 1, -1, 1, 0, -1, 0 )
		verts[ 5 ] = Vertex.Create( -1, -1, 1, 0, -1, 0 )
		verts[ 6 ] = Vertex.Create( -1, -1, -1, 0, -1, 0 )
		verts[ 7 ] = Vertex.Create( 1, -1, -1, 0, -1, 0 )
		
		' Front
		verts[ 8 ] = Vertex.Create( 1, 1, 1, 0, 0, 1 )
		verts[ 9 ] = Vertex.Create( -1, 1, 1, 0, 0, 1 )
		verts[ 10 ] = Vertex.Create( -1, -1, 1, 0, 0, 1 )
		verts[ 11 ] = Vertex.Create( 1, -1, 1, 0, 0, 1 )
	
		' Back
		verts[ 12 ] = Vertex.Create( 1, -1, -1, 0, 0, -1 )
		verts[ 13 ] = Vertex.Create( -1, -1, -1, 0, 0, -1 )
		verts[ 14 ] = Vertex.Create( -1, 1, -1, 0, 0, -1 )
		verts[ 15 ] = Vertex.Create( 1, 1, -1, 0, 0, -1 )
		
		' Left
		verts[ 16 ] = Vertex.Create( -1, 1, 1, -1, 0, 0 )
		verts[ 17 ] = Vertex.Create( -1, 1, -1, -1, 0, 0 )
		verts[ 18 ] = Vertex.Create( -1, -1, -1, -1, 0, 0 )
		verts[ 19 ] = Vertex.Create( -1, -1, 1, -1, 0, 0 )
		
		' Right
		verts[ 20 ] = Vertex.Create( 1, 1, -1, 1, 0, 0 )
		verts[ 21 ] = Vertex.Create( 1, 1, 1, 1, 0, 0 )
		verts[ 22 ] = Vertex.Create( 1, -1, 1, 1, 0, 0 )
		verts[ 23 ] = Vertex.Create( 1, -1, -1, 1, 0, 0 )
		
		
		' Index Array (define our triangles)
		index[ 0 ] = 0;		index[ 1 ] = 1;		index[ 2 ] = 2;
		index[ 3 ] = 2;		index[ 4 ] = 3;		index[ 5 ] = 0;
		
		index[ 6 ] = 4;		index[ 7 ] = 5;		index[ 8 ] = 6;
		index[ 9 ] = 6;		index[ 10 ] = 7; 	index[ 11 ] = 4;
		
		index[ 12 ] = 8; 	index[ 13 ] = 9; 	index[ 14 ] = 10;
		index[ 15 ] = 10; 	index[ 16 ] = 11; 	index[ 17 ] = 8;
		
		index[ 18 ] = 12; 	index[ 19 ] = 13;	index[ 20 ] = 14;
		index[ 21 ] = 14; 	index[ 22 ] = 15;	index[ 23 ] = 12;
		
		index[ 24 ] = 16;	index[ 25 ] = 17;	index[ 26 ] = 18;
		index[ 27 ] = 18;	index[ 28 ] = 19;	index[ 29 ] = 16;
		
		index[ 30 ] = 20;	index[ 31 ] = 21;	index[ 32 ] = 22;
		index[ 33 ] = 22;	index[ 34 ] = 23;	index[ 35 ] = 20;
	
	End Method

	
	
	

	Method OnRender()


		glMatrixMode( GL_MODELVIEW )
		glLoadIdentity()
		glEnable( GL_LIGHTING )
		glEnable( GL_LIGHT0 )
		glLightfv( GL_LIGHT0, GL_POSITION, [0.0, 0.0, 1.0] )


		glViewport 0,0,DeviceWidth,DeviceHeight
	
		glDisable( GL_BLEND );
		
		glMatrixMode( GL_PROJECTION );
		glLoadIdentity
		glFrustumf -1,1,-1,1,1,100

		
		glMatrixMode( GL_MODELVIEW );
		glLoadIdentity
		
		rot+=1
		glRotatef rot,0,0,1	

		glClearColor 0,0,.5,1
		glClear GL_COLOR_BUFFER_BIT|GL_DEPTH_BUFFER_BIT
		
		glEnableClientState GL_VERTEX_ARRAY
		glEnableClientState GL_NORMAL_ARRAY
		glDisableClientState GL_COLOR_ARRAY
		glDisableClientState GL_TEXTURE_COORD_ARRAY

		glBindBuffer GL_ARRAY_BUFFER,vbo
		glBindBuffer GL_ELEMENT_ARRAY_BUFFER,ibo

		glVertexPointer 3,GL_FLOAT,24,0 
		glNormalPointer GL_FLOAT,24,12

		glEnable GL_DEPTH_TEST
		glDepthFunc GL_LESS		
		glDepthMask True
		
		glLoadIdentity
		glTranslatef 0,-1,-4
		glRotatef rot,1,1,0
		glScalef 1,1,1
		glColor4f 0,1,0,1
		glDrawElements GL_TRIANGLES,36,GL_UNSIGNED_SHORT,0
	
	
	End Method

End Class


Function Main()
	New GLApp
End Function

</textarea> <br><br></td></tr></table><br>
<a name="2020862"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >marksibly</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>glLightfv with GL_POSITION requires 4 params - the last param is 1.0 for a point light, 0.0 for a directional light. <br><br></td></tr></table><br>
<a name="2020893"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for your help Mark!  I had also forgtotten to set depth buffer to 32 in the config.h folder for the new test app. <br><br></td></tr></table><br>
<a name="2020897"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> In case anyone was following this and might find the code useful. Here is a working version with a textured cube and a light. You can also move a vertex with the up key. Don't forget like I did to enable depth buffer in the build folder.<br><br>The code is a mess and poorly coded as I was trying to get it working and didn't really know what I was doing.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import mojo
Import opengl.gles11



'class to store vertex data
Class Vertex

	Field x : Float
	Field y : Float
	Field z : Float
	
	Field nx : Float
	Field ny : Float
	Field nz : Float

	Field u : Float
	Field v : Float

	Method New( vx : Float, vy : Float, vz : Float, vnx : Float, vny : Float, vnz : Float, vu : Float, vv : Float )
		Self.x = vx
		Self.y = vy
		Self.z = vz
		Self.nx = vnx
		Self.ny = vny
		Self.nz = vnz
		Self.u = vu
		Self.v = vv
	End Method

	
	Function Create : Vertex( vx : Float, vy : Float, vz : Float, vnx : Float, vny : Float, vnz : Float, vu : Float, vv : Float )
		Local this : Vertex = New Vertex( vx, vy, vz, vnx, vny, vnz, vu, vv )
		Return this
	End Function
	

End Class






Class Texture

	'texture buffer object
	Field tbo : Int
	
	'stores texture data
	Field tData : DataBuffer
	
	'stores width and height
	Field tInfo : Int[ 2 ]
	
	
	
	'loads texture
	Method New( fileName : String )
	
		Local buf:Int[1]
		glGenTextures(1, buf)
		Self.tbo = buf[0]
		
		glBindTexture(GL_TEXTURE_2D, Self.tbo)
		glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_LINEAR)
		glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER, GL_LINEAR)
		
		Self.tData = LoadImageData(fileName, Self.tInfo)
		glTexImage2D( GL_TEXTURE_2D, 0, GL_RGBA, Self.tInfo[0], Self.tInfo[1], 0, GL_RGBA, GL_UNSIGNED_BYTE, Self.tData)

	End Method
	
	
End Class





'stores stuff
Global index : Int[ 36 ]
Global verts : Vertex[ 24 ]
Global vbo, ibo
Global rot : Float
Global vdata : DataBuffer
Global tex : Texture


Class GLApp Extends App



	Method OnCreate()
	
		SetUpdateRate 60
		
		GenCube()
		InitVBO()
		
		tex = New Texture( "crate.jpg" )
		
	End Method
	
	
		
	Method InitVBO()
	
		'grab vertex and index
		Local vcount : Int = verts.Length
		Local icount : Int = index.Length
			
	
		'create vertex data buffer
		vdata = DataBuffer.Create( vcount * 32 )
		For Local i := 0 Until vcount
			Local p = i * 32
			Local v : Vertex = verts[ i ]
			vdata.PokeFloat p + 0, v.x
			vdata.PokeFloat p + 4, v.y
			vdata.PokeFloat p + 8, v.z
			vdata.PokeFloat p + 12, v.nx
			vdata.PokeFloat p + 16, v.ny
			vdata.PokeFloat p + 20, v.nz
			vdata.PokeFloat p + 24, v.u
			vdata.PokeFloat p + 28, v.v
		Next
		
		'create index data buffer
		Local idata := DataBuffer.Create( icount * 2 )
		For Local i := 0 Until icount
			idata.PokeShort i * 2, index[ i ]
		Next
			
		'generate buffers
		Local bufs[ 2 ]
		glGenBuffers 2, bufs
		
		vbo = bufs[ 0 ]
		glBindBuffer GL_ARRAY_BUFFER, vbo
		glBufferData GL_ARRAY_BUFFER, vdata.Size(), vdata, GL_STATIC_DRAW
	
		ibo = bufs[ 1 ]
		glBindBuffer GL_ELEMENT_ARRAY_BUFFER, ibo
		glBufferData GL_ELEMENT_ARRAY_BUFFER, idata.Size(), idata, GL_STATIC_DRAW		
		
	End Method



	Method GenCube()
	

		'/ Generate Vertices
		verts[ 0 ] = Vertex.Create( -0.5, -0.5, -0.5, 0.0, -1.0, 0.0, 0.0, 0.0 )
		verts[ 1 ] = Vertex.Create( -0.5, -0.5,  0.5, 0.0, -1.0, 0.0, 0.0, 1.0 )
		verts[ 2 ] = Vertex.Create(  0.5, -0.5,  0.5, 0.0, -1.0, 0.0, 1.0, 1.0 )
		verts[ 3 ] = Vertex.Create(  0.5, -0.5, -0.5, 0.0, -1.0, 0.0, 1.0, 0.0 )
	
		verts[ 4 ] = Vertex.Create( -0.5,  0.5, -0.5, 0.0, 1.0, 0.0, 1.0, 0.0 )
		verts[ 5 ] = Vertex.Create( -0.5,  0.5,  0.5, 0.0, 1.0, 0.0, 1.0, 1.0 )
		verts[ 6 ] = Vertex.Create(  0.5,  0.5,  0.5, 0.0, 1.0, 0.0, 0.0, 1.0 )
		verts[ 7 ] = Vertex.Create(  0.5,  0.5, -0.5, 0.0, 1.0, 0.0, 0.0, 0.0 )
		
		verts[ 8 ] = Vertex.Create( -0.5, -0.5, -0.5, 0.0, 0.0, -1.0, 0.0, 0.0 )
		verts[ 9 ] = Vertex.Create( -0.5,  0.5, -0.5, 0.0, 0.0, -1.0, 0.0, 1.0 )
		verts[ 10 ] = Vertex.Create( 0.5,  0.5, -0.5, 0.0, 0.0, -1.0, 1.0, 1.0 )
		verts[ 11 ] = Vertex.Create( 0.5, -0.5, -0.5, 0.0, 0.0, -1.0, 1.0, 0.0 )
		
		verts[ 12 ] = Vertex.Create( -0.5, -0.5, 0.5, 0.0, 0.0, 1.0, 0.0, 0.0 )
		verts[ 13 ] = Vertex.Create( -0.5,  0.5, 0.5, 0.0, 0.0, 1.0, 0.0, 1.0 )
		verts[ 14 ] = Vertex.Create(  0.5,  0.5, 0.5, 0.0, 0.0, 1.0, 1.0, 1.0 )
		verts[ 15 ] = Vertex.Create(  0.5, -0.5, 0.5, 0.0, 0.0, 1.0, 1.0, 0.0 )
	
		verts[ 16 ] = Vertex.Create( -0.5, -0.5, -0.5, -1.0, 0.0, 0.0, 0.0, 0.0 )
		verts[ 17 ] = Vertex.Create( -0.5, -0.5,  0.5, -1.0, 0.0, 0.0, 0.0, 1.0 )
		verts[ 18 ] = Vertex.Create( -0.5,  0.5,  0.5, -1.0, 0.0, 0.0, 1.0, 1.0 )
		verts[ 19 ] = Vertex.Create( -0.5,  0.5, -0.5, -1.0, 0.0, 0.0, 1.0, 0.0 )
	
		verts[ 20 ] = Vertex.Create( 0.5, -0.5, -0.5, 1.0, 0.0, 0.0, 0.0, 0.0 )
		verts[ 21 ] = Vertex.Create( 0.5, -0.5,  0.5, 1.0, 0.0, 0.0, 0.0, 1.0 )
		verts[ 22 ] = Vertex.Create( 0.5,  0.5,  0.5, 1.0, 0.0, 0.0, 1.0, 1.0 )
		verts[ 23 ] = Vertex.Create( 0.5,  0.5, -0.5, 1.0, 0.0, 0.0, 1.0, 0.0 )

		'/ Generate Indices
        index[ 0 ] = 0; 	index[ 1 ] = 2; 	index[ 2 ] = 1;
        index[ 3 ] = 0; 	index[ 4 ] = 3;		index[ 5 ] = 2;
        index[ 6 ] = 4;		index[ 7 ] = 5;		index[ 8 ] = 6;
        index[ 9 ] = 4; 	index[ 10 ] = 6; 	index[ 11 ] = 7;
        index[ 12 ] = 8; 	index[ 13 ] = 9; 	index[ 14 ] = 10;
        index[ 15 ] = 8; 	index[ 16 ] = 10; 	index[ 17 ] = 11;
        index[ 18 ] = 12; 	index[ 19 ] = 15; 	index[ 20 ] = 14;
        index[ 21 ] = 12; 	index[ 22 ] = 14; 	index[ 23 ] = 13; 
		index[ 24 ] = 16; 	index[ 25 ] = 17; 	index[ 26 ] = 18;
        index[ 27 ] = 16; 	index[ 28 ] = 18; 	index[ 29 ] = 19;
		index[ 30 ] = 20;	index[ 31 ] = 23; 	index[ 32 ] = 22;
        index[ 33 ] = 20; 	index[ 34 ] = 22; 	index[ 35 ] = 21;
	
	End Method	


	
	Method OnUpdate()
	
		If KeyHit( KEY_UP )
			Local p : Int = 6 * 32
			Local y : Float = vdata.PeekFloat( p + 4 )
			y = y + 0.1
			vdata.PokeFloat( p + 4, y )
			glBindBuffer GL_ARRAY_BUFFER, vbo
			glBufferData GL_ARRAY_BUFFER, vdata.Size(), vdata, GL_STATIC_DRAW
		Endif
	
	End Method
	
	

	Method OnRender()

		'enable a light
		glMatrixMode( GL_MODELVIEW )
		glLoadIdentity()
		glEnable( GL_LIGHTING )
		glEnable( GL_LIGHT0 )
		glLightfv( GL_LIGHT0, GL_POSITION, [0.0, 0.0, 0.0, 1.0] )


		glEnable(GL_TEXTURE_2D)
    	glEnable(GL_BLEND)

		glViewport 0,0,DeviceWidth,DeviceHeight
		
		glMatrixMode( GL_PROJECTION );
		glLoadIdentity
		glFrustumf -1,1,-1,1,1,100

		
		glMatrixMode( GL_MODELVIEW );
		glLoadIdentity
		
		rot+=1
		glRotatef rot,0,0,1	

		glClearColor 0,0,.5,1
		glClear GL_COLOR_BUFFER_BIT|GL_DEPTH_BUFFER_BIT
		
		glEnableClientState GL_VERTEX_ARRAY
		glEnableClientState GL_NORMAL_ARRAY
		glDisableClientState GL_COLOR_ARRAY
		glEnableClientState GL_TEXTURE_COORD_ARRAY

		glBindBuffer GL_ARRAY_BUFFER,vbo
		glBindBuffer GL_ELEMENT_ARRAY_BUFFER,ibo
		glBindTexture GL_TEXTURE_2D, tex.tbo

		glVertexPointer 3, GL_FLOAT, 32, 0 
		glNormalPointer GL_FLOAT, 32, 12
		glTexCoordPointer 2, GL_FLOAT, 32, 24
		
		glEnable GL_DEPTH_TEST
		glDepthFunc GL_LESS		
		glDepthMask True

		glLoadIdentity
		glTranslatef 2, 0,-4
		glRotatef rot,1,1,0
		glScalef 1,1,1
		glDrawElements GL_TRIANGLES,36,GL_UNSIGNED_SHORT,0

		glLoadIdentity
		glTranslatef -2, 0,-4
		glRotatef rot,1,1,0
		glScalef 1,1,1
		glDrawElements GL_TRIANGLES,36,GL_UNSIGNED_SHORT,0
		
		glLoadIdentity
		glTranslatef 0, 0,-2
		glRotatef rot,1,1,0
		glScalef 1,1,1
		glDrawElements GL_TRIANGLES,36,GL_UNSIGNED_SHORT,0


	
	End Method

End Class


Function Main()
	New GLApp
End Function

</textarea> <br><br></td></tr></table><br>
<a name="2020896"></a>

<a name="2020894"></a>

<a name="2020895"></a>

<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
