<!DOCTYPE html><html lang="en" ><head ><title >When to use delta timing...</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >When to use delta timing...</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=507" >Monkey Programming</a>/<a href="#bottom" >When to use delta timing...</a><br><br>
<a name="2011559"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've got a bit of code and I think I am doing too much delta timing on it.<br><br>Here is the code without any delta timing:<br><pre class=code>
if dx &gt; 0
	if height &lt;&gt; 0
		dy += 0.3
		angle += amount
	End
	height += dy
	if height &gt; 0
		height = 0
		amount = 0
		dx *= .5
		dy = 0 - (dy * 0.4)
		if dy &gt;-1
			dy = 0
			height = 0
		End
	End
	if dy = 0
		dx -= 0.010
	End
	
	x += (Sin(angle) * dx)
	y += (Cos(angle) * dx)
end
</pre><br><br>I had delta timing everytime I added/multipled/subtracted... and now I am confussed even more :(<br><br>So where should I use delta timing in this code? <br><br></td></tr></table><br>
<a name="2011556"></a>

<a name="2011557"></a>

<a name="2011558"></a>

<a name="2011560"></a>

<a name="2011561"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GfK</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> You should only need it on the two sin and cos lines at the end otherwise you'll be applying delta multiple times and end up with weird results. also you have dx on the cos line and it should be dy. <br><br></td></tr></table><br>
<a name="2011562"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Dave... thats what I thought but it still wrong:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Import mojo

Global dt:DeltaTimer

Function Main:Int()
	New Game()
	Return 1
End

Class DeltaTimer

	Field targetfps:Float = 60
	Field currentticks:Float
	Field lastticks:Float
	Field frametime:Float
	Field delta:Float
	
	Method New (fps:Float)
		targetfps = fps
		lastticks = Millisecs()
	End
	
	Method UpdateDelta:Void ()
		currentticks = Millisecs()
		frametime = currentticks - lastticks
		delta = frametime / (1000.0 / targetfps)
		lastticks = currentticks
	End
	
End

Class Game Extends App
	Global FPS:Int = 60
	
	Field uncorrectedBall:Ball
	Field correctedBall:Ball
	
	Method OnCreate:Int()
		dt = New DeltaTimer ( FPS )
		SetUpdateRate FPS
		uncorrectedBall = New Ball()
		uncorrectedBall.ResetBall(10, 200)
		correctedBall = New Ball()
		correctedBall.ResetBall(10, 300)
		Return 0
	End
	
	Method OnUpdate:Int()

		dt.UpdateDelta

		uncorrectedBall.Update(False)
		correctedBall.Update(True)

		If KeyHit (KEY_LEFT)
			FPS = FPS - 10
			If FPS &lt; 10 Then FPS = 10
			SetUpdateRate FPS
		Endif
		
		If KeyHit (KEY_RIGHT)
			FPS = FPS + 10
			SetUpdateRate FPS
		Endif
		
		If KeyDown (KEY_ENTER)
			FPS = 60
			SetUpdateRate FPS
		Endif
		Return 0
	End
	
	Method OnRender:Int ()
	
		Cls 32, 64, 128
		
		DrawText "Time scale factor (Delta): " + dt.delta, 0, 10 
		DrawText "(Frame time: " + dt.frametime + " ms)", 0, 20
		
		uncorrectedBall.Draw(255, 0, 0)
		correctedBall.Draw(0, 255, 0)
		
		DrawText "Use &lt;- and -&gt; plus ENTER to change game update rate (currently " + FPS + " fps)", 0, 60
		Return 0
	End
	
End

Class Ball
	Field x#, y#
	Field dx#, dy#
	Field angle#
	field height#
	Field amount#
	Field ox#, oy#
	
	Method Update:Void(useDelta?)
		Local delta# = dt.delta
		if not useDelta
			delta = 1
		End
	
		if dx &gt; 0
			if height &lt;&gt; 0
				dy = dy + 0.3
				angle = angle + amount
			End
			height = height + dy
			if height &gt; 0
				height = 0
				amount = 0
				dx = dx * .5
				dy = 0 - (dy * 0.4)
				if dy &gt;-1
					dy = 0
					height = 0
				End
			End
			if dy = 0
				dx -= 0.030
			End

			x = x + (Sin(angle) * dx) * delta
			y = y + (Cos(angle) * dx) * delta
		Else
			ResetBall(ox, oy)
		End
	End
	
	Method ResetBall:Void(x#, y#)
		
		angle = 90
		Local power# = 40
		dx = ((25 / 100.0) * power) * 1
		dy = 0 - ((12 / 100.0) * power)
		self.x = x
		Self.y = y
		ox = x
		oy = y
	End
	
	Method Draw:Void(r%, g%, b%)
		SetColor 0,0,0
		DrawOval x-3, y-3, 6, 6
		SetColor r,g,b
		DrawOval x-3, y-3 + height, 6, 6
	End
End
</textarea><br><br>Using Jame's delta timing example, press left and right cursor keys to alter the framerate.<br><br>The green ball has delta timing. <br><br></td></tr></table><br>
<a name="2011564"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> This looks better:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Import mojo

Global dt:DeltaTimer

Function Main:Int()
	New Game()
	Return 1
End

Class DeltaTimer

	Field targetfps:Float = 60
	Field currentticks:Float
	Field lastticks:Float
	Field frametime:Float
	Field delta:Float
	
	Method New (fps:Float)
		targetfps = fps
		lastticks = Millisecs()
	End
	
	Method UpdateDelta:Void ()
		currentticks = Millisecs()
		frametime = currentticks - lastticks
		delta = frametime / (1000.0 / targetfps)
		lastticks = currentticks
	End
	
End

Class Game Extends App
	Global FPS:Int = 60
	
	Field uncorrectedBall:Ball
	Field correctedBall:Ball
	
	Method OnCreate:Int()
		dt = New DeltaTimer ( FPS )
		SetUpdateRate FPS
		uncorrectedBall = New Ball()
		uncorrectedBall.ResetBall(10, 200)
		correctedBall = New Ball()
		correctedBall.ResetBall(10, 300)
		Return 0
	End
	
	Method OnUpdate:Int()

		dt.UpdateDelta

		uncorrectedBall.Update(False)
		correctedBall.Update(True)

		If KeyHit (KEY_LEFT)
			FPS = FPS - 10
			If FPS &lt; 10 Then FPS = 10
			SetUpdateRate FPS
		Endif
		
		If KeyHit (KEY_RIGHT)
			FPS = FPS + 10
			SetUpdateRate FPS
		Endif
		
		If KeyDown (KEY_ENTER)
			FPS = 60
			SetUpdateRate FPS
		Endif
		Return 0
	End
	
	Method OnRender:Int ()
	
		Cls 32, 64, 128
		
		DrawText "Time scale factor (Delta): " + dt.delta, 0, 10 
		DrawText "(Frame time: " + dt.frametime + " ms)", 0, 20
		
		uncorrectedBall.Draw(255, 0, 0)
		correctedBall.Draw(0, 255, 0)
		
		DrawText "Use &lt;- and -&gt; plus ENTER to change game update rate (currently " + FPS + " fps)", 0, 60
		Return 0
	End
	
End

Class Ball
	Field x#, y#
	Field dx#, dy#
	Field angle#
	field height#
	Field amount#
	Field ox#, oy#
	
	Method Update:Void(useDelta?)
		Local delta# = dt.delta
		if not useDelta
			delta = 1
		End
	
		if dx &gt; 0
			if height &lt;&gt; 0
				dy = dy + 0.3 * delta
				angle = angle + amount * delta
			End
			height = height + dy * delta
			if height &gt; 0
				height = 0
				amount = 0
				dx = dx / 2
				dy = 0 - (dy * 0.4)
				if dy &gt;-1
					dy = 0
					height = 0
				End
			End
			if dy = 0
				dx -= 0.030 * delta
			End
			x = x + Sin(angle) * (dx * delta)
			y = y + Cos(angle) * (dx * delta)

		Else
			ResetBall(ox, oy)
		End
	End
	
	Method ResetBall:Void(x#, y#)
		
		angle = 90
		Local power# = 30
		dx = ((25 / 100.0) * power) * 1
		dy = 0 - ((12 / 70.0) * power)
		self.x = x
		Self.y = y
		ox = x
		oy = y
		amount = 1
	End
	
	Method Draw:Void(r%, g%, b%)
		SetColor 0,0,0
		DrawOval x-3, y-3, 6, 6
		SetColor r,g,b
		DrawOval x-3, y-3 + height, 6, 6
	End
End
</textarea><br><br><pre class=code>
if dx &gt; 0
	if height &lt;&gt; 0
		dy = dy + 0.3 * delta
		angle = angle + amount * delta
	End
	height = height + dy * delta
	if height &gt; 0
		height = 0
		amount = 0
		dx = dx / 2
		dy = 0 - (dy * 0.4)
		if dy &gt;-1
			dy = 0
			height = 0
		End
	End
	if dy = 0
		dx -= 0.030 * delta
	End
	x = x + Sin(angle) * (dx * delta)
	y = y + Cos(angle) * (dx * delta)
end
</pre> <br><br></td></tr></table><br>
<a name="2011584"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >luggage</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think you only need to do it on the last couple of lines as someone mentioned.<br><br>Try removing all the delta's and use something like.<br><br><pre class=code>
Local velocityX:Float = Sin(angle) * dx
Local velocityY:Float = Cos(angle) * dx ' you use dx here in your code are you sure this isn't supposed to be dy?

x = x + (velocityX * delta)
y = y + (velocityY * delta)
</pre><br><br>You might need to play with your numbers though.  The idea is that only when you move your player do you need to use your delta.  So for example, say you wanted him to move 10 pixels per second then velocityX should be 10.  You mulitply this value when you add it on to X so if 1 second had passed between the last frame and this one you'd jump 10 pixels.  If 0.5 of a second had passed you'd move 5 pixels. <br><br></td></tr></table><br>
<a name="2011594"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks luggage, yeah normally it would be fine to add the delta to just the movement variables - but this is slightly different. I am trying to get a ball moving around the screen that looks a bit like Sensible Soccer eg 2d but when the ball is in the "air" you can see the shadow.<br><br>Have a play with the code in post #4. <br><br></td></tr></table><br>
<a name="2011604"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matt</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I can dig out the code to my Simple Soccer game if you like?<br><br><a href="http://www.gingerbeardman.com/archive/soccer/" target="_blank">http://www.gingerbeardman.com/archive/soccer/</a> try the early bird download (PC only) <br><br></td></tr></table><br>
<a name="2011605"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>I can dig out the code to my Simple Soccer game if you like?</pre><br><br>That would be cool - Cheer Matt! <br><br></td></tr></table><br>
<a name="2011620"></a>

<a name="2011622"></a>

<a name="2011623"></a>

<a name="2011624"></a>

<a name="2011625"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matt</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'd be interested to know what you have planned! ;)<br><br><pre class=code>Type BallType							; The Ball Object Type.
	Field x#,y#							; Ball X and Y co-ords. 
	Field z								; Ball Z-coord.
	Field dx#,dy#						; Ball X and Y direction increments
	Field dz							; Ball Z increments
	Field frame							; The current ball frame.
End Type
</pre>properties should be self explanatory<br><br><pre class=code>Function draw_ball(posx, posy)
	;sensi style shadow
	DrawImage bmp_ball, posx+ball\z, posy, 4

	;sensi style ball
	DrawImage bmp_ball, posx, posy-ball\z, Abs(Int(ball\frame/5) Mod 4)
End Function</pre>shadow is drawn with z property affecting x co-ordinate, ball is drawn with z affecting y co-ordinate. This gives the effect of "3D" ball movement in 2D.<br><br>I guess this means lighting source is exactly West in direction? I know Croteam's football games on the Amiga used multiple lighting sources and therefore had multiple ball shadows (which was a bit OTT and confusing for me). <a href="http://hol.abime.net/hol_search.php?N_ref_developer=123" target="_blank">http://hol.abime.net/hol_search.php?N_ref_developer=123</a><br><br>Hope that helps! <br><br></td></tr></table><br>
<a name="2011629"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cool - can you show me how you move the actual ball? <br><br></td></tr></table><br>
<a name="2011636"></a>

<a name="2011637"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matt</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just checked and I don't use delta timing at all.<br><br><pre class=code>;move ball in x and y directions
ball\x=ball\x+ball\dx
ball\y=ball\y+ball\dy</pre>plus some friction/dampening applied each frame, and a check to clamp the speed to zero if it's close enough. <br><br></td></tr></table><br>
<a name="2011806"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Foppy</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is a topic on blitzbasic.com from 2006 where Grey Alien asks whether to apply delta time to the value by which the acceleration itself is changed:<br><br><a href="http://www.blitzbasic.com/Community/posts.php?topic=61530" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=61530</a> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
