<!DOCTYPE html><html lang="en" ><head ><title >error in HTML5 only.</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >error in HTML5 only.</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=507" >Monkey Programming</a>/<a href="#bottom" >error in HTML5 only.</a><br><br>
<a name="2003771"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> some how an object becomes null after a certain amount of time of being executed.<br>this same code works fine executed for different platforms. It works as expected in GLFW and Flash.<br><br>tested on my Mac with google chrome and safari, fails with both on MonkeyPro V33.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict
Import mojo

'Create game Object
Class Tpoint
	Field x:Float
	Field y:Float
End Class


Class Tvector
	Field p0:Tpoint
	Field p1:Tpoint
	Field dx:Float
	Field dy:Float
	Field vx:Float
	Field vy:Float
	Field length:Float
	Field rx:Float
	Field ry:Float
	Field lx:Float
	Field ly:Float
	Field b:Float
	Field f:Float
	Field airf:Float
	Field lastTime:Float
	Field timeFrame:Float
	
	Method New()
		p0 = New Tpoint
		p1 = New Tpoint
	End Method
	
	Function Create:Tvector(x1:Float,y1:Float,x2:Float,y2:Float,b:Float=1.0,f:Float=1.0,airf:Float=1.0)
		Local v:Tvector = New Tvector
		v.p0.x = x1
		v.p0.y = y1
		v.p1.x = x2
		v.p1.y = y2
		v.b = b
		v.f = f
		v.airf = airf
		Return v
	End Function

	'Find all parameters For the vector
	Method update:Void()
		'x And y components
		'End point coordinate - start point coordinate
		vx = p1.x-p0.x
		vy = p1.y-p0.y
		'length of vector
		length = Sqrt(vx*vx+vy*vy)
		'normalized unti-sized components
		If (length&gt;0)
			dx = vx/length
			dy = vy/length
		Else
			dx = 0
			dy = 0
		Endif
		'Right hand normal
		rx = -vy
		ry = vx
		'Left hand normal
		lx = vy
		ly = -vx
	End Method

	'find intersection point of 2 vectors
	Method findIntersection:Float(v2:Tvector)
		Local t1:Float,t2:Float
		'vector between starting points
		Local v3a:Tvector = New Tvector
		v3a.vx=v2.p0.x-p0.x
		v3a.vy=v2.p0.y-p0.y
		Local v3b:Tvector = New Tvector
		v3b.vx=p0.x-v2.p0.x
		v3b.vy=p0.y-v2.p0.y
		'If they are parallel vectors, Return big number
		If ((dx = v2.dx And dy = v2.dy) Or (dx = -v2.dx And dy = -v2.dy))
			Return 1000000
		Else
			t1 = v3a.perP(v2)/perP( v2)
			t2 = v3b.perP(Self)/v2.perP(Self)
		Endif
		If (t1&gt;0 And t1&lt;=1 And t2&gt;0 And t2&lt;=1)
			Return t2
		Endif
		Return 1000000
	End Method

	'calculate perp product of 2 vectors
	Method perP:Float(vb:Tvector)
		Return vx*vb.vy - vy*vb.vx
	End Method

	'find New vector bouncing from v2
	Method bounce:Tvector(v2:Tvector)
	
		'projection of v1 on v2
		Local proj1:Tvector = project(v2.dx, v2.dy)
		'projection of v1 on v2 normal
		Local proj2:Tvector = project(v2.lx/v2.length, v2.ly/v2.length)
		Local proj:Tvector = New Tvector
		'reverse projection on v2 normal
		proj2.vx *= -1
		proj2.vy *= -1
		'add the projections
		proj.vx = f*v2.f*proj1.vx+b*v2.b*proj2.vx
		proj.vy = f*v2.f*proj1.vy+b*v2.b*proj2.vy
		Return proj
	End Method
	
	'project vector v1 on unit-sized vector dx/dy
	Method project:Tvector(dx:Float, dy:Float)
	
		'find dot product
		Local dp:Float = vx*dx+vy*dy
		Local proj:Tvector = New Tvector
		'projection components
		proj.vx = dp*dx
		proj.vy = dp*dy
		Return proj
	End Method
	
End Class

Class Tgame
	Field stageW:Float
	Field stageH:Float
	Field maxV:Float
	Field gravity:Float
	Field myOb:Tvector
	Field bounce:Tvector
	Field time:Int
	
	Field wall:Tvector[4]
	
	Function Create:Tgame(w:Float,h:Float,maxV:Float,gravity:Float)
		Local g:Tgame = New Tgame
		g.stageW = w
		g.stageH = h
		g.maxV = maxV
		g.myOb = Tvector.Create(150,100,151,100,0.8,0.8,0.99)
		g.myOb.update()
		g.myOb.lastTime=Millisecs()
		g.gravity = gravity
		g.wall[0] = Tvector.Create( 50, 50,250, 50)
		g.wall[1] = Tvector.Create(250, 50,250,130)
		g.wall[2] = Tvector.Create(250,130, 50,130)
		g.wall[3] = Tvector.Create( 50,130, 50, 50)
		Return g
	End Function

	'Method to draw the ball and lines
	Method display:Void()
		For Local n:Tvector = Eachin wall
			DrawLine(n.p0.x, n.p0.y, n.p0.x+n.vx, n.p0.y+n.vy)
		Next
		DrawOval(myOb.p0.x-3,myOb.p0.y-3,6,6)
		DrawText myOb.p0.x+"   "+myOb.p0.y,10,250
		DrawText (myOb.vx +"   "+myOb.vy),10,270
	End Method

	Method update:Void()
	
		'start To calculate movement
		'add air resistance
		myOb.vx *= myOb.airf
		myOb.vy *= myOb.airf
		'dont let it go over Max speed
		If (myOb.vx&gt;maxV)
			myOb.vx = maxV
		Else If (myOb.vx&lt;-maxV)
			myOb.vx = -maxV
		Endif
		If (myOb.vy&gt;maxV)
			myOb.vy = maxV
		Else If (myOb.vy&lt;-maxV)
			myOb.vy = -maxV
		Endif
		'update the vector parameters
		updateObject(myOb)
		'time To collide something big
		time = 1000000
		'prepare collision
		bounce = Null
		'find collisions with walls
		For Local i:Tvector = Eachin wall
			Local t:Float = myOb.findIntersection(i)
			'If this has collision, save it
			If (t&lt;time)
				'which wall To collide with
				bounce = i
				'save time
				time = t
			Endif
		Next
		'we have collision
		If bounce
			'set End point To intersection point
			myOb.p1.x = myOb.p0.x+myOb.vx*time
			myOb.p1.y = myOb.p0.y+myOb.vy*time
			'bounce
			Local newv:Tvector = myOb.bounce(bounce)
			'change movement vector
			myOb.vx = newv.vx
			myOb.vy = newv.vy
			'add New direction To End point
			myOb.p1.x += myOb.vx*(1.0-time)
			myOb.p1.y += myOb.vy*(1.0-time)
			'save the time
			time = 1.0-time
		Endif
		'reset Object To other side If gone out of stage
		If (myOb.p1.x&gt;stageW)
			myOb.p1.x -= stageW
		Elseif (myOb.p1.x&lt;0)
			myOb.p1.x -= stageW
		Endif
		If (myOb.p1.y&gt;stageH)
			myOb.p1.y -= stageH
		Elseif (myOb.p1.y&lt;0)
			myOb.p1.y += stageH
		Endif
		'make End point equal To starting point For Next cycle
		myOb.p0 = myOb.p1
		'save the movement without time
		myOb.vx /= myOb.timeFrame
		myOb.vy /= myOb.timeFrame
	End Method

	Method updateObject:Void(v:Tvector)
		'find time passed from last update
		Local thisTime:Float = Millisecs()
		Local time:Float = (thisTime-v.lastTime)/100.0
		'using time to move so multiply movement vector with time passed
		v.vx *= time
		v.vy *= time
		'add gravity, also based on time
		v.vy = v.vy+time*gravity
		'find End point coordinates
		v.p1.x = v.p0.x+v.vx
		v.p1.y = v.p0.y+v.vy
		'length of vector
		v.length = Sqrt(v.vx*v.vx+v.vy*v.vy)
		'normalized unti-sized components
		v.dx = v.vx/v.length
		v.dy = v.vy/v.length
		'Right hand normal
		v.rx = -v.vy
		v.ry = v.vx
		'Left hand normal
		v.lx = v.vy
		v.ly = -v.vx
		'save the Current time
		v.lastTime = thisTime
		'save time passed
		v.timeFrame = time
	End Method

End Class


Global game:Tgame

Class ball Extends App

	Method OnCreate:Int()
		game = Tgame.Create(300, 180, 10, 0.5)
		'calculate all parameters For the wall vectors
		game.wall[0].update()
		game.wall[1].update()
		game.wall[2].update()
		game.wall[3].update()
	  	SetUpdateRate 60
		Return 1
	End Method
	
	Method OnUpdate:Int()
		game.update()
		Return 1
	End Method
	
	Method OnRender:Int()
		'draw it
		Cls()
		game.display()
		Return 1	
	End Method
End Class




Function Main:Int()
	New ball
	Return 1
End Function
</textarea> <br><br></td></tr></table><br>
<a name="2003759"></a>

<a name="2003757"></a>

<a name="2003758"></a>

<a name="2003765"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skid</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think your program needs to be a little more sensitive to when Millisecs returns 0, which it can do on the first Update. If I add this code to the top and call Tick from OnUpdate I can't get it to break, if the Tick call is moved to after game.Update then the code breaks.<br><br><pre class=code>

Global ftime%

Function Millisecs%()
Return ftime
End

Function Tick:Void()
ftime+=20
End
</pre> <br><br></td></tr></table><br>
<a name="2003764"></a>

<a name="2003774"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I don't know if we are talking about the same thing and excuse me if I miss interpret your post.<br> I know that the code is not logically correct but the problem is that it is returning a "NAN" for a an object variable value that is predefined once at the beginning of the application. It's the object "myOB" in the global "game" object and only values are moved in to it. <br><br>in which case it should return at least Zeros but Both of the values for the DrawText  are displayed as "NAN" which should not happen at all as if myOb became null after a certain time. sometimes it takes a while sometimes it happens right away. <br><br></td></tr></table><br>
<a name="2003773"></a>

<a name="2003772"></a>

<a name="2003770"></a>

<a name="2003775"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >marksibly</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>Nans generally indicate a math error such as sqrt(-1). They propagate through any calculations too, so if an expression has just one nan in it, the result is nan.<br><br>And it's not unusual to get nans on one target but not another - it'll depend on what FPU modes are in effect.<br><br>I don't get what you mean by 'predefined once at the beginning of the application' though - the myOb.p0 and p1 fields are being constaly updated as far as I can tell, as are myOb.vx and vy.<br><br>But it's a fairly long piece of code - can you try trimming it down a bit? <br><br></td></tr></table><br>
<a name="2003778"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >marksibly</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>Well, sort of related to what skid was saying...<br><br>If I add this to updateObject:Void(v:TVector) after the "Local time:Float=..." calculation:<br><br>If time=0 Return<br><br>...it's fixes the nans here.<br><br>This may be to do with the new update timing code, which will now execute multiple 'OnUpdates' if necessary to catch up with any lost time. This also appears to happen in flash, and it's something you'll need to allow for. It's probably not happening in glfw/xna because there is much less 'performance noise' compared to browser based stuff.<br><br>I'm still a bit confused about why 'delta timing' is so popular - update rate should be reliable (over time anyway, to allow for glitches/spikes - and now that html5 has been fixed!) so 'time delta' should be constant. <br><br></td></tr></table><br>
<a name="2003777"></a>

<a name="2003776"></a>

<a name="2003782"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>I don't get what you mean by 'predefined once at the beginning of the application' though - the myOb.p0 and p1<br> <br></div><br>I meant instantiate it as in "new" sorry.<br><br>Thanks Mark. I knew about -1 producing the imaginary number but I completely felt out of my mind. <br>on the other hand I did some test and it seems that Sqrt(0) is giving me that error of which I don't know if it's correct or not. <br><br></td></tr></table><br>
<a name="2003780"></a>

<a name="2003781"></a>

<a name="2003779"></a>

<a name="2003783"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Its the division by zero following the sqrt.<br><br><br><br>solved thanks! <br><br></td></tr></table><br>
<a name="2003784"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >marksibly</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>You're right, sqrt(0) appears to be giving a nan in javascript and actionscript!<br><br>I think this may be because it's actually doing sqrt(-0). In FP, 0 can be positive or negative (I think, it's been a while...). Another source of nans can be 0/0 if I remember correctly. <br><br></td></tr></table><br>
<a name="2003785"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> yea same error.<br>is there a way to to test if a Sqrt is returning a value or a nan? <br><br></td></tr></table><br>
<a name="2003786"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >marksibly</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>Actually, I don't think it is the sqrt.<br><br>When time is 0, v.vx, v.vy, v.length all become 0.<br><br>It's the v.dx=v.vx/v.length stuff that comes a bit later that's causing the nans - ie: 0/0. <br><br></td></tr></table><br>
<a name="2003789"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >marksibly</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>To fix it, I think you'll have to:<br><br>* Check for elapsed time=0 in OnUpdate and do nothing, AND...<br><br>* Check for v.length&gt;0 before updating v.dx,v.rx,v.lx etc. eg:<br><br><pre class=code>
		If v.length&gt;0
			'normalized unti-sized components
			v.dx = v.vx/v.length
			v.dy = v.vy/v.length
			'Right hand normal
			v.rx = -v.vy
			v.ry = v.vx
			'Left hand normal
			v.lx = v.vy
			v.ly = -v.vx
		Else
			v.dx=0
			v.dy=0
			v.rx=0
			v.ry=0
			v.lx=0
			v.ly=0
		Endif
</pre><br><br>This is because, even if time elapsed is &gt;0, it's still possible for v.length (and therefore v.vx and v.vy) to be 0 - ie: if the ball is at the 'peak' of it's bounce and therefore momentarily stationery.<br><br>All in all, I'd say you're getting bitten by the 0/0 nan, not the sqrt nan.<br><br>And I still don't get the delta timing... <br><br></td></tr></table><br>
<a name="2003788"></a>

<a name="2003787"></a>

<a name="2003791"></a>

<a name="2003792"></a>

<a name="2003793"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> yea Mark.  All of the problems are in the division by zero. I been checking all of the divisions and sooner or later they end up being a nan. it's all my bad logic. <br><br>Thanks, Thanks. Thanks......<br><br>I would had never created this problem if it had not being that I never expected to deal with "NAN"'s.<br><br>there is also an error in the t1 and t2 with the perps division. <br><br></td></tr></table><br>
<a name="2003790"></a>

<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
