<!DOCTYPE html><html lang="en" ><head ><title >Is there more generic approach for this?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Is there more generic approach for this?</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=507" >Monkey Programming</a>/<a href="#bottom" >Is there more generic approach for this?</a><br><br>
<a name="2043779"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MikeHart</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi folks,<br><br>when I have designed fantomEngine, I didn't thought about that there could be the need to extend the object class by the user. One now tried to do this and how I have build the engine, I think it isn't possible atm. Maybe it isn't possible at all, but my OOP knowledge is pretty slim so maybe someone can give me a hint.<br><br>Here is basically a very slim version of how I have the classes connect to each other. I wonder how I have to set it up so it works when someone wants to extend the object class without going into the engine code<br><br>Here is the myEngine.monkey file:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">'***************************************
Class cObject
	Field xPos:Float
	Field yPos:Float
	Field radius:Float
	'------------------------------------------
	Method Update:Void()
	End
	'------------------------------------------
	Method Render:Void()
		DrawCircle xPos, yPos, radius
	End
End
'***************************************
Class cEngine
	Field objList := New List&lt;cObject&gt;
	'------------------------------------------
	Method CreateCircle:cObject(r:Float, xp:Float, yp:Float)
		Local obj:cObject = New cObject
		obj.xPos = xp
		obj.yPos = yp
		obj.radius = r
		Self.objList.AddLast(obj)
		Return obj
	End
	'------------------------------------------
	Method Update:Void()
		For Local obj := Eachin objList
			obj.Update()
		Next
	End
	'------------------------------------------
	Method Render:Void()
		For Local obj := Eachin objList
			obj.Render()
		Next
	End
End</textarea><br><br><br>And here is the engineTest.monkey file:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict

Import mojo
Import myEngine
Global g:game

'***************************************
Class myObject2 Extends cObject
	Method Update:Void()
		Self.xPos = Self.xPos + 0.5
	End
End
'***************************************
Class myObject3 Extends cObject
	Method Update:Void()
		Self.xPos = Self.xPos + 1.5
	End
End
'***************************************
Class game Extends App
	Field eng:cEngine
	Field obj1:cObject
	Field obj2:myObject2
	Field obj3:myObject3
	'------------------------------------------
	Method OnCreate:Int()
		SetUpdateRate(60)
		eng = New cEngine
		obj1 = eng.CreateCircle(30,320,240)
		obj2 = eng&lt;myObject&gt;.CreateCircle(20,320,240)     '&lt;&lt;&lt;&lt; That doesn't work.
		obj3 = eng&lt;myObject&gt;.CreateCircle(20,320,240)     '&lt;&lt;&lt;&lt; That doesn't work.
		Return 0
	End
	'------------------------------------------
	Method OnUpdate:Int()
		eng.Update()
		Return 0
	End
	'------------------------------------------
	Method OnRender:Int()
		Cls 
		eng.Render()
		Return 0
	End
End	


'***************************************
Function Main:Int()
	g = New game
	Return 0
End
</textarea><br><br>So my questions is, how do I have to change the engine, that I can let the user extend the object class and use this, still be able to use the normal update and render calls of the engine? <br><br></td></tr></table><br>
<a name="2043787"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rone</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>I guess the simplest way is to provide a constructor for ftObject, in order simplyfy extending ftObject and provide also a way for adding and removing ftObjects from ftEngine...<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Class ftObject 

	'...

	Method New(x#,y#,w#,h#,radius#, type%, colType%)
		' ...
	End 
End 

Class ftEngine

	'...
	
	Method AddObject(obj:ftObject)
		obj.engine = Self
		obj.SetLayer(Self.defaultLayer)
		obj.SetActive(Self.defaultActive)
		obj.SetVisible(Self.defaultVisible)
		obj.collType = ctBound
		obj.internal_RotateSpriteCol(obj)
	End
	
	Method CreateBox:ftObject(w:Float, h:Float, xp:Float, yp:Float)
		Local box:= New ftObject(xp,yp,w,h,(Max(w,h)*1.42)/2.0,otBox,ctBound)
		AddObject(box)
		Return box
	End 
End

</textarea><br><br>If you do not want to provide constructors and force the user to use the ftEngine.CreateXX functions, I suggest to provide a factory for each ftObject type... -&gt; ftEngine.CreateObject( ftEngine.otBox, x,y,...)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

'' enumerates available ftObjects
Class ftObjectEnumerator
	Function Factories:IntMap&lt;ftObjectFactory&gt;()
		If FactoryMap = Null Then 
			FactoryMap = New IntMap&lt;ftObjectFactory&gt;
			Local factoryInfo:= GetClass("ftObjectFactory")
			Local classInfos:= GetClasses()
			For Local i = 0 Until classInfos.Length
				if classInfos[i].ExtendsClass(factoryInfo) Then 
					Local f:= classInfos[i].GetConstructor([])
					If f Then 
						Local factory:= UnitFactory(f.Invoke([]))
						FactoryMap.Add(factory.Type, factory )
					End
				End
			Next
		End 
		Return factoryMap
	End 
	Global FactoryMap:IntMap&lt;ftObjectFactory&gt;
End 

'' ftObjetct factory interface
Interface ftObjectFactory 
	Method CreateInstance:ftObject(x#,y#,w#,h#,radius#) 
	Method Type:Int() 
End 

'' 
Class BoxObjectFactory Interface ftObjectFactory
	Method CreateInstance:ftObject(x#,y#,w#,h#,radius#) 
		Local box:= New ftObject(x,y,w,h,radius, Self.Type,ftEngine.ctBound)
		
		'' special box initialization...
		
		Return box
	End 
	Method Type:Int()
		Return ftEngine.otBox
	End 
End 

''...all ftObject types needs to implement ftObjectFactory

Class ftObject 

	'...
	
	Method New(x#,y#,w#,h#,radius#, type%, colType%)
		' ...
	End 
	
End 

Class ftEngine

	'...
	
	Method AddObject(obj:ftObject)
		obj.engine = Self
		obj.SetLayer(Self.defaultLayer)
		obj.SetActive(Self.defaultActive)
		obj.SetVisible(Self.defaultVisible)
		obj.collType = ctBound
		obj.internal_RotateSpriteCol(obj)
	End

	
	Method CreateObject:ftObject(type, x#,y#,w#,h#,radius#)
		Local obj:ftObject = NULL
		Local f:= ftObjectFactoryEnumerator.FactoryMap.Get(type)
		If f Then 
			obj = f.CreateInstance(x,y,w,h,radius)
			AddObject(obj)
		End 
		Return obj
	End 
	
End 
</textarea><br><br>all pseudocode <br><br></td></tr></table><br>
<a name="2043786"></a>

<a name="2043813"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MikeHart</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> The problem is the casting between the engine and the objec t (or an extended) class. How do i do it, so it works? Maybe it isnt possible. <br><br></td></tr></table><br>
<a name="2043820"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ferdi</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Mike, hopefully this is what you are looking for in a solution.  I am not quite sure about your question.  But your problem is this line:<br><br><pre class=code>
Local obj:cObject = New cObject  '&lt;&lt;&lt; you are basically creating the root / base object all the time.
</pre><br><br>This is how I modified it (sorry I put it all in 1 file, lazy to create 2 files =P):<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Import mojo
'Import myEngine


'***************************************
Class cObject
	Field xPos:Float
	Field yPos:Float
	Field radius:Float
	'------------------------------------------
	Method Update:Void()
		Print "Root"
	End
	'------------------------------------------
	Method Render:Void()
		DrawCircle xPos, yPos, radius
	End
End
'***************************************
Class cEngine
	Field objList := New List&lt;cObject&gt;
	'------------------------------------------
	Method CreateCircle:cObject(obj:cObject, r:Float, xp:Float, yp:Float)
		'Local obj:cObject = New cObject  '&lt;&lt;&lt; you are basically creating 1 type of object.
		obj.xPos = xp
		obj.yPos = yp
		obj.radius = r
		Self.objList.AddLast(obj)
		Return obj
	End
	'------------------------------------------
	Method Update:Void()
		For Local obj := Eachin objList
			obj.Update()
		Next
	End
	'------------------------------------------
	Method Render:Void()
		For Local obj := Eachin objList
			obj.Render()
		Next
	End
End




Global g:game

'***************************************
Class myObject2 Extends cObject
	Method Update:Void()
		Print "2"
		Self.xPos = Self.xPos + 0.5
	End
End
'***************************************
Class myObject3 Extends cObject
	Method Update:Void()
		Print "3"
		Self.xPos = Self.xPos + 1.5
	End
End
'***************************************
Class game Extends App
	Field eng:cEngine
	Field obj1:cObject
	Field obj2:myObject2
	Field obj3:myObject3
	'------------------------------------------
	Method OnCreate:Int()
		SetUpdateRate(60)
		eng = New cEngine
		obj1 = eng.CreateCircle(New cObject, 30,320,240)
		obj2 = myObject2(eng.CreateCircle(New myObject2, 20,320,240))     '&lt;&lt;&lt;&lt; That doesn't work - should work now.
		obj3 = myObject3(eng.CreateCircle(New myObject3, 20,320,240))     '&lt;&lt;&lt;&lt; That doesn't work - should work now.
		Return 0
	End
	'------------------------------------------
	Method OnUpdate:Int()
		eng.Update()
		Return 0
	End
	'------------------------------------------
	Method OnRender:Int()
		Cls 
		eng.Render()
		Return 0
	End
End	


'***************************************
Function Main:Int()
	g = New game
	Return 0
End
</textarea><br><br>Just on a side note and not to derail this thread, you may run into trouble in the future, if you structure it like this.  Read this article he explains it best:<br><br><a href="http://www.rtsoft.com/wiki/doku.php?id=proton_entity" target="_blank">http://www.rtsoft.com/wiki/doku.php?id=proton_entity</a><br><br>I believe you are bullet number 2:<br><br><div class="quote"> Create a giant entity class hierarchy where each kind of thing gets its own class. A bird might be derived like: Object -&gt; Visual -&gt; MovingObject -&gt; NPC -&gt; Bird or something ridiculous like that. End up using virtuals everywhere.<br>VERDICT: Slow to add new things, too difficult to refactor, maintenance is a hassle. <br></div><br><br>If you are interested, I am using t-machine entity system, which I have ported to monkey.  I can share that code, it seems to work, but it is probably still buggy =) <br><br></td></tr></table><br>
<a name="2043840"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rone</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> definitely, the best example of God object / blob pattern<br><br><a href="http://cowboyprogramming.com/2007/01/05/evolve-your-heirachy/" target="_blank">http://cowboyprogramming.com/2007/01/05/evolve-your-heirachy/</a><br><a href="http://stackoverflow.com/questions/1901251/component-based-game-engine-design" target="_blank">http://stackoverflow.com/questions/1901251/component-based-game-engine-design</a> <br><br></td></tr></table><br>
<a name="2043844"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MikeHart</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi folks,<br><br>thanks for all the input. Based of Ferdis last suggestion I found a method that fits into the existing workflow of my engine. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
