<!DOCTYPE html><html lang="en" ><head ><title >Finding sample card game code in Monkey?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Finding sample card game code in Monkey?</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=507" >Monkey Programming</a>/<a href="#bottom" >Finding sample card game code in Monkey?</a><br><br>
<a name="2065089"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >zardon</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am very interested in writing a simple card game with Monkey but I am unsure of how to start a card game using Monkey.<br><br>Is there any sample code or projects which I can look at.<br><br>I am interested in understanding:<br><br>1. How to animate cards, flip them over, should a visual representation of a deck of cards actually have X cards displayed one on top of each other.<br><br>2. The logical flow of how a card game actually works, ie: Draw card, player on turn, etc<br><br>3. Understanding game states and how they effect the logical flow of the card game<br><br>Many thanks <br><br></td></tr></table><br>
<a name="2065091"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Xaron</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I might be able to help. I did a Mau Mau game (crazy 8):<br><br><a href="https://play.google.com/store/apps/details?id=com.codeknitters.mau" target="_blank">https://play.google.com/store/apps/details?id=com.codeknitters.mau</a> <br><br></td></tr></table><br>
<a name="2065094"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Zardon - Have you tried yourself yet? If so which parts are you stuck at and can you post your code here so we can help you. <br><br></td></tr></table><br>
<a name="2065092"></a>

<a name="2065093"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gerry Quinn</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think the principles are the same as with any other strategy game, i.e. each player at every turn has a 'position' based on his hand and any cards on the table or previous scores, then must decide what 'move' to make.<br><br>Since the game is turn-based, you have the luxury of not caring too much about graphic efficiency.  So write code that's super-clear, rather than code that's super-efficient.  And if you want to draw every card in a pile, slightly offset to indicate depth, go for it.  You can always change to a more efficient or realistic representation of a pile of cards later.<br><br>Animation of events such as playing a card is probably something you can add later once you have the basic mechanisms sorted. <br><br></td></tr></table><br>
<a name="2065096"></a>

<a name="2065097"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >rIKmAN</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just done a quick Google and there are lots of tutorials / articles about this very thing.<br><br>ie. <a href="http://snipplr.com/view/55221/" target="_blank">http://snipplr.com/view/55221/</a> should get you started and at the very least give you an idea of how to go about it. <br><br></td></tr></table><br>
<a name="2065323"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >zardon</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> What I'm stuck with is the following.<br><br>1. Lets say a game as 50 cards to draw from. You want to display the deck of 50 cards somewhere on the screen, and animate a card from this pile onto the board.<br><br>This seems like wasted memory to me, but I don't know if I should actually put 50 cards one on top of each other like layers and draw from it or not<br><br>2. How do I animate the cards from the deck into the board, flipping them over, etc<br><br>3. Handling the logic of the game.<br><br>a. Shuffle cards<br>b. Draw X cards from deck to the board, flip them over to show them<br>c. Player on turn takes a card, or does their actions.<br>d. Move a card from the board to the player's hand.<br>e. Move a card from the player's hand to the board.<br><br>I have gotten all the models done, I just couldn't wrap my head around the logic .<br><br>Thanks for now <br><br></td></tr></table><br>
<a name="2065327"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Xaron</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I will post some code. I'm on vacation atm so please have some patience. <br><br></td></tr></table><br>
<a name="2065356"></a>

<a name="2065357"></a>

<a name="2065358"></a>

<a name="2065359"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gerry Quinn</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Drawing 52 cards will probably be fast enough, you can add some efficient code for representing big stacks later if you want.<br><br>For game purposes (other than animation) cards are always in one place, e.g. they are in the deck, or on the board, or in the hand of player X.  These could all be lists or stacks.  A card should know also whether it is face up or face down.  A game action simply changes these states for one or more cards, and the game rules say which states can permissibly be changed (or are forced to be).<br><br>I think I would do it so that the cards also hold information related to drawing/animation.  E.g. a card will store its position (coordinates) and also an animation parameter that goes from 0.0 to 1.0 over a series of renders as it engages in an animation such as turning over or changing position.  I might give it two position values so it is easy to tell a card to move from A to B, and a separate animation parameter for that.<br><br>Then animation is simply a case of updating the parameters of all animating cards and redrawing the game, until all animations are done.  Then it is time for a move. Say it is player 2's turn and he gets to play a card to the board.  You set up the animations for that players cards (the card he plays will flip over and move to the board, and his other cards will move slightly to give him an even spread).  For your first iteration of the game, don't bother with a real AI, just make him play a random legal card.  Your game will have code for saying whether a move is legal, so he can select cards at random and call that until one is valid or all have been checked.<br><br>The animations happen.  When they're done, check whether anybody won the game.  If Player 2's turn is over, it's on to Player 3, and so on.<br><br>That's the gist of how I would go about it, and there are some small wrinkles that will have to be added (e.g. do we offset the positions of cards slightly according to the number of cards stacked below them), but maybe it gives you an idea of one way to approach things. <br><br></td></tr></table><br>
<a name="2065351"></a>

<a name="2065352"></a>

<a name="2065353"></a>

<a name="2065354"></a>

<a name="2065355"></a>

<a name="2065508"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >zardon</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sorry I was very busy, but I wish to thank you for time. I'm going to have a go of it and try my hardest to have a go of it.<br><br>I'm well of aware the rules and how to play the game; I just find it hard to articulate it onto the visual screen.<br><br>Anyway, I have now mocked up the graphics for the cards and am going to have a go by using Arrays and simple objects. <br><br></td></tr></table><br>
<a name="2065510"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Snader</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> I made my first card game, and although there isn't much animation, it all worked out nicely.<br><br><a href="http://www.monkeycoder.co.nz/Apps/app.php?id=263&amp;pic_id=1" target="_blank">http://www.monkeycoder.co.nz/Apps/app.php?id=263&amp;pic_id=1</a><br><br>Each card in my game is an object with a current X,Y and rotation, and a X and Y coordinate and rotation to 'travel to'. Each card also has an OWNER var. This can be PLAYER_A, _B, _C or _D. Then I simply can render the cards on the right spot, or animate it to the right spot. <br>Based upon the rules of the game, the AI and the current hands and turn of the players I basically move cards by switching ownership. <br><br></td></tr></table><br>
<a name="2065527"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Capn Lee</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Although I could never recommend anyone look at my code for a positive example, I quickly knocked together a single player version of Set when feeling inspired on a long train journey. If you're not familiar with the game, the rules are <a href="http://www.setgame.com/sites/default/files/instructions/SET%20INSTRUCTIONS%20-%20ENGLISH%208.23.11.pdf" target="_blank">here</a> or below is the video instructions I learned about the game from<br><br><iframe width="560" height="345" src="http://www.youtube.com/embed/24qEZYKw86Y" frameborder="0" allowfullscreen></iframe><br><br>anyway, this is the complete code, it's very simple and not at all pretty<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Import mojo

Function Main()
	New Game
End

Global gameMode:Int = 0
Const gmMenu:Int = 0
Const gmPlay:Int = 1
Const gmShuffle:Int = 2


Global deckList:= New List&lt;Card&gt;
Global playArray:Card[16]
Global playList:= New List&lt;Card&gt;
Global shuffleList:= New List&lt;Card&gt;

Global selectedCount:Int = 0
Global score:Int = 0

Class Game Extends App
	
	Function RenderCards()
		For Local c:= EachIn playList
			c.Render()
		End
	End

	Field s01:Card
	Field s02:Card
	Field s03:Card
	
	Method OnCreate()
		SetUpdateRate(60)
	End
	
	Method PickCards()
		For Local i:Int = 1 To 12
			Local c:= deckList.First()
			deckList.RemoveFirst()
			playList.AddLast(c)
		Next
		SetCardPos()
	End
	
	Method FeedToDeck()
		For Local c:= EachIn playList
			c.selected = False
			deckList.AddFirst(c)
		End
		playList.Clear()
	End
	
	Method SetCardPos()
		Local i:Int = 0
		For Local c:= EachIn playList
			c.SetPos(i)
			i += 1
		End
	End
	
	Method StartGame()
		score = 0
		Seed = Millisecs()
		GenerateCards()
		ShuffleCards(12)
		PickCards()
	End
	
	Method DeselectCards()
		For Local c:= EachIn playList
			c.selected = False
		Next
	End
	
	Method OnUpdate()
		If gameMode = gmPlay
			If KeyHit(KEY_SPACE)
				If deckList.Count() &gt; 0
					DeselectCards()
					If HandCheck()
						score -= 1
						gameMode = gmShuffle
					Else
						FeedToDeck()
						ShuffleCards(6)
						PickCards()
					EndIf
				EndIf
			EndIf
			If KeyHit(KEY_P) Then StartGame()
			If MouseHit()
				SelectCard()
				If CheckNull(s01) = False And CheckNull(s02) = False And CheckNull(s03) = False
					If SetCheck(s01, s02, s03)
						score += 1
						s01.NewCard()
						s02.NewCard()
						s03.NewCard()
						s01 = Null
						s02 = Null
						s03 = Null
					EndIf
				EndIf
			EndIf
		ElseIf gameMode = gmShuffle
			If KeyHit(KEY_SPACE) or MouseHit(MOUSE_LEFT)
				FeedToDeck()
				ShuffleCards(6)
				PickCards()
				gameMode = gmPlay
			EndIf
		Else
			If KeyHit(KEY_P) Then gameMode = gmPlay; StartGame()
		EndIf
	End
	
	Method SelectCard()
		MakeNull(s01)
		MakeNull(s02)
		MakeNull(s03)
		For Local c:= EachIn playList
			Local x:Int = 96 + (c.posX * 128)
			Local y:Int = 32 + (c.posY * 160)
			If MouseX() &gt; x And MouseX() &lt; (x + 64)
				If MouseY() &gt; y And MouseY() &lt; (y + 96)
					If c.selected = True
						c.selected = False
						If s01 = c
							s01 = Null
						ElseIf s02 = c
							s02 = Null
						ElseIf s03 = c
							s03 = Null
						EndIf
					Else
						If s01 = Null or s01 = c
							c.selected = True
							s01 = c
						ElseIf s02 = Null or s02 = c
							c.selected = True
							s02 = c
						ElseIf s03 = Null or s03 = c
							c.selected = True
							s03 = c
						Else
						EndIf
					EndIf
				EndIf
			EndIf
		Next
	End
	
	Method OnRender()
		If gameMode = gmPlay or gameMode = gmShuffle
			Cls(0, 42, 0)
			RenderCards()
			SetColor(200, 200, 200)
			DrawText(deckList.Count(), 30, 30, 0.5, 0.5)
			DrawText(score, 610, 30, 0.5, 0.5)
		Else
			Cls(40, 30, 20)
			SetColor(200, 200, 200)
			DrawText("P to begin, bitch", 320, 240, 0.5, 0.5)
		EndIf
	End
	
	Method MakeNull:Bool(c:Card)
		If c &lt;&gt; Null
			If c.selected = False
				c = Null
			EndIf
			Return True
		EndIf
		Return False
	End
	
	Method CheckNull:Bool(c:Card)
		If c &lt;&gt; Null
			Return False
		EndIf
		Return True
	End
	
	Method GenerateCards()
		deckList.Clear()
		playList.Clear()
		selectedCount = 0
		For Local clr:Int = 1 To 3
			For Local shd:Int = 1 To 3
				For Local shp:Int = 1 To 3
					For Local cnt:Int = 1 To 3
						Local crd:Card = New Card(clr, shd, shp, cnt)
						deckList.AddLast(crd)
					Next
				Next
			Next
		Next
	End
	
	Method ShuffleCards(times:Int)
		s01 = Null
		s02 = Null
		s03 = Null
		For Local t:Int = 0 Until times
			ListToArray()
			For Local c:= EachIn shuffleList
				Local r:Float = Rnd()
				c.selected = False
				If r &lt; 0.5
					deckList.AddFirst(c)
				Else
					deckList.AddLast(c)
				EndIf
			Next
			shuffleList.Clear()
		Next
	End
	
	Method HandCheck:Bool()
		For Local a:= EachIn playList
			For Local b:= EachIn playList
				For Local c:= EachIn playList
					If SetCheck(a, b, c)
						a.selected = True
						b.selected = True
						c.selected = True
					Return True
					EndIf
				Next
			Next
		Next
		Return False
	End
	
	Method SetCheck:Bool(a:Card, b:Card, c:Card)
		If a = Null or b = Null or c = Null
			Return False
		EndIf
		If a = b or a = c or b = c
			Return False
		EndIf
		If (a.color = b.color And a.color = c.color) Or (a.color + b.color + c.color = 6)
		Else
			Return False
		EndIf
		If (a.shade = b.shade And a.shade = c.shade) Or (a.shade + b.shade + c.shade = 6)
		Else
			Return False
		EndIf
		If (a.shape = b.shape And a.shape = c.shape) Or (a.shape + b.shape + c.shape = 6)
		Else
			Return False
		EndIf
		If (a.count = b.count And a.count = c.count) Or (a.count + b.count + c.count = 6)
		Else
			Return False
		EndIf
		Return True
	End
	
	Method ListToArray()
		shuffleList.Clear()
		For Local i:Int = 1 To deckList.Count()
			Local c:Card = deckList.First()
			deckList.RemoveFirst()
			shuffleList.AddLast(c)
		Next
	End
	
	Method PlayToArray()
		For Local i:Int = 0 To 11
			playArray[i] = playList.First()
		Next
	End

End

Class Card
	
	Field color:Int = 0
	Field shade:Int = 0
	Field shape:Int = 0
	Field count:Int = 0
	
	Field selected:Bool = False
	
	Field posX:Int = 0
	Field posY:Int = 0
	
	Method New(color:Int, shade:Int, shape:Int, count:Int)
		Self.color = color
		Self.shade = shade
		Self.shape = shape
		Self.count = count
	End
	
	Method SetPos(pos:Int)
		posX = 0
		posY = 0
		Repeat
			If pos &gt; 3 Then posY += 1; pos -= 4
		Until pos &lt; 4
		posX = pos
	End
		
	Method NewCard()
		If deckList.Count = 0
			playList.Remove(Self)
		Else
			Local c:Card = deckList.First()
			deckList.RemoveFirst()
			Self.color = c.color
			Self.shade = c.shade
			Self.shape = c.shape
			Self.count = c.count
			selected = False
		EndIf
	End
		
		
	Method Render()
		PushMatrix()
			Translate(96 + (Self.posX * 128), 32 + (Self.posY * 160))
			Local countRemaining:Int = count
			SetColor(23, 24, 25)
			DrawRect(6, 3, 58, 96)
			DrawRect(3, 6, 64, 90)
			DrawCircle(6, 6, 3)
			DrawCircle(64, 6, 3)
			DrawCircle(6, 96, 3)
			DrawCircle(64, 96, 3)
			If selected Then DrawOutline()
			SetColor(212, 211, 210)
			DrawRect(3, 0, 58, 96)
			DrawRect(0, 3, 64, 90)
			DrawCircle(3, 3, 3)
			DrawCircle(61, 3, 3)
			DrawCircle(3, 93, 3)
			DrawCircle(61, 93, 3)
			SelectColor()
			For Local i:Int = 1 To count
				Select shape
					Case 1
						DrawRect(16, 24 * countRemaining, 32, 8)
						If shade = 1 or shade = 3
							SetColor(212, 211, 210)
							DrawRect(18, (24 * countRemaining) + 2, 28, 4)
						EndIf
						If shade = 1
							SelectColor()
							SetAlpha(0.4)
							DrawRect(16, 24 * countRemaining, 32, 8)
							SetAlpha(1)
						EndIf
						SelectColor()
					Case 2
						DrawEllipse(32, 24 * countRemaining, 16, 8)
						If shade = 1 or shade = 3
							SetColor(212, 211, 210)
							DrawEllipse(32, 24 * countRemaining, 14, 6)
						EndIf
						If shade = 1
							SelectColor()
							SetAlpha(0.4)
							DrawEllipse(32, 24 * countRemaining, 16, 8)
							SetAlpha(1)
						EndIf
						SelectColor()
					Case 3
						DrawCircle(32, 24 * countRemaining, 9)
						If shade = 1 or shade = 3
							SetColor(212, 211, 210)
							DrawCircle(32, 24 * countRemaining, 7)
						EndIf
						If shade = 1
							SelectColor()
							SetAlpha(0.4)
							DrawCircle(32, 24 * countRemaining, 9)
							SetAlpha(1)
						EndIf
						SelectColor()
				End
				countRemaining -= 1
			Next
		PopMatrix()
	End
	
	Method DrawOutline()
		If gameMode = gmPlay
			SetColor(130, 180, 130)
		Else
			SetColor(180, 130, 150)
		EndIf
		DrawRect(-1, -4, 66, 104)
		DrawRect(-4, -1, 72, 98)
		DrawCircle(0, 0, 4)
		DrawCircle(64, 0, 4)
		DrawCircle(0, 96, 4)
		DrawCircle(64, 96, 4)
	End
	
	Method SelectColor()
		Select color
			Case 1
				SetColor(223, 115, 98)
			Case 2
				SetColor(64, 122, 139)
			Case 3
				SetColor(128, 175, 60)
		End
	End
End</textarea><br><br>Hope that helps <br><br></td></tr></table><br>
<a name="2065898"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >zardon</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is exactly the info I was after!  Many thanks.   The card game I am writing is straight forward; but I could never understand how to transmute my understanding of the game to the screen.<br><br>I have got basic graphics (it'll do for now) done, now I just need to do the code which I feel much more confident about now.<br><br>Thanks again <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
