<!DOCTYPE html><html lang="en" ><head ><title >Rotation Backwards?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Rotation Backwards?</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=507" >Monkey Programming</a>/<a href="#bottom" >Rotation Backwards?</a><br><br>
<a name="2002538"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> When I rotate using the drawImage rotation parameter its seems backwards...<br><br>When I rotate my ship by adding to rotation, in Monkey it rotates left, in BlitzMax it rotates right... I am doing something wrong?<br><br>BlitzMax code:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Global SCREEN_WIDTH% = 800
Global SCREEN_HEIGHT% = 600

Graphics SCREEN_WIDTH, SCREEN_HEIGHT
AutoMidHandle 1

Local player:TPlayer = New TPlayer
player.x = SCREEN_WIDTH / 2
player.y = SCREEN_HEIGHT / 2
player.image = LoadImage("ship.png")

Repeat

	player.update()

	Cls
	player.draw()
	DrawText "x = " + player.x, 10, 10
	DrawText "y = " + player.y, 10, 20
	DrawText "r = " + player.rotation, 10, 30	
	DrawText "s = " + player.speed, 10, 40		
	Flip
	
Until KeyHit(KEY_ESCAPE)

Type TPlayer
	Field x#, y#
	Field dx#, dy#, speed#
	Field rotation#
	Field scaleX# = 1, scaleY# = 1
	Field frame% = 0
	Field image:TImage
		
	Method update()
		If KeyDown(KEY_UP)
			Self.speed:+0.4
		End If
		If KeyDown(KEY_DOWN)
			Self.speed:-0.4
		End If
		If speed &lt; -5 speed = -5
		If speed &gt; 8 speed = 8
		
		
		If KeyDown(KEY_LEFT)
			Self.rotation:-2
		End If
		If KeyDown(KEY_RIGHT)
			Self.rotation:+2
		End If
		
		dx = Sin(rotation) * speed
        	dy = -Cos(rotation) * speed
		x:+dx
		y:+dy
		
		If x &lt; 0 x = SCREEN_WIDTH
		If x &gt; SCREEN_WIDTH x = 0
		If y &lt; 0 y = SCREEN_HEIGHT
		If y &gt; SCREEN_HEIGHT y = 0
	End Method
	
	Method draw()
		SetRotation rotation
		DrawImage(image, x, y)
		SetRotation 0
	End Method
	
End Type
</textarea><br><br>Monkey code:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

import mojo

Global SCREEN_WIDTH%
Global SCREEN_HEIGHT%

Function Main:int()
	new MyGame
	return 0
End Function

Class MyGame extends App
	Field player:Player
	
	Method OnCreate:Int()
		SCREEN_WIDTH = DeviceWidth()
		SCREEN_HEIGHT = DeviceHeight()
		
		player = new Player(LoadImage("ship.png", 1, Image.MidHandle), SCREEN_WIDTH / 2, SCREEN_HEIGHT / 2)
		
		SetUpdateRate 60
		return 0
	End Method
	
	Method OnLoading:Int()
		return 0
	End Method
	
	Method OnUpdate:Int()
		player.update()
		return 0
	End Method
	
	Method OnRender:Int()
		Cls
		player.draw()
		
		DrawText "x = " + player.x, 10, 10
		DrawText "y = " + player.y, 10, 20
		DrawText "r = " + player.rotation, 10, 30	
		DrawText "s = " + player.speed, 10, 40	
			
		return 0
	End Method
end class

Class Player
	Field x#, y#
	Field dx#, dy#, speed#
	Field rotation#
	Field scaleX# = 1, scaleY# = 1
	field frame% = 0
	Field image:Image
	
	Method New(img:Image, x#, y#)
		self.image = img
		self.x = x
		self.y = y
		self.rotation = 0
	End Method
	
	Method update:Void()
		If KeyDown(KEY_UP)
			self.speed+=0.4
		End If
		If KeyDown(KEY_DOWN)
			Self.speed-=0.4
		End If
		If speed &lt; -5 speed = -5
		If speed &gt; 8 speed = 8
		
		If KeyDown(KEY_LEFT)
			self.rotation-=2
		End If
		If KeyDown(KEY_RIGHT)
			self.rotation+=2
		End If
		
		dx = Sin(rotation) * speed
		dy = -Cos(rotation) * speed
		x+=dx
		y+=dy
		
		If x &lt; 0 x = SCREEN_WIDTH
		If x &gt; SCREEN_WIDTH x = 0
		If y &lt; 0 y = SCREEN_HEIGHT
		If y &gt; SCREEN_HEIGHT y = 0
	End Method
	
	Method draw:Void()
		DrawImage(image, x, y, rotation, scaleX, scaleY, frame)
	End Method
	
End Class
</textarea> <br><br></td></tr></table><br>
<a name="2002539"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yahfree</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I use these functions for rotation:<br><br><pre class=code>
Function SetRotation (X:Float, Y:Float, angle:Float)
	PushMatrix
	Translate X, Y
	Rotate angle
	Translate -X, -Y
End

Function ResetRotation ()
	PopMatrix
End
</pre> <br><br></td></tr></table><br>
<a name="2002603"></a>

<a name="2002621"></a>

<a name="2002622"></a>

<a name="2002623"></a>

<a name="2002628"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> **** duplicate post **** <br><br></td></tr></table><br>
<a name="2002629"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah Ive altered the Monkey code to "fix" this, but is it a bug or is it BlitzMax that is backwards? <br><br></td></tr></table><br>
<a name="2002610"></a>

<a name="2002611"></a>

<a name="2002612"></a>

<a name="2002613"></a>

<a name="2002614"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DGuy</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I believe it's because the code is using global transformations in BlitzMax, but local transformations in Monkey.<br><br>Monkey has global (affects all) and local (affects one) transformations, while BlitzMax only has global transformations:<br><br><pre class=code>BlitzMax Global Transformations (Max2D):
----------------------------------------
SetRotation
SetScale
SetTransform


Monkey Global Transformations (mojo):
-------------------------------------
Rotate
Scale
SetMatrix
Transform
Translate


Monkey Local Transformations (mojo):
------------------------------------
DrawImage     =&gt; the rotation, scaleX, scaleY parameters
DrawImageRect =&gt; the rotation, scaleX, scaleY parameters</pre><br>Try using mojo.Rotate instead of the DrawImage rotation parameter; that should give the effect your expecting. <br><br></td></tr></table><br>
<a name="2002631"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ive tried both global and local:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

import mojo

Global SCREEN_WIDTH%
Global SCREEN_HEIGHT%

Function Main:int()
	new MyGame
	return 0
End Function

Class MyGame extends App
	Field player:Player
	
	Method OnCreate:Int()
		SCREEN_WIDTH = DeviceWidth()
		SCREEN_HEIGHT = DeviceHeight()
		
		player = new Player(LoadImage("ship.png", 1, Image.MidHandle), 200, 100)
		
		SetUpdateRate 60
		return 0
	End Method
	
	Method OnLoading:Int()
		return 0
	End Method
	
	Method OnUpdate:Int()
		player.update()
		return 0
	End Method
	
	Method OnRender:Int()
		Cls
		player.draw()

		DrawImage(player.image, 100, 100, player.rotation, player.scaleX, player.scaleY, player.frame)
		
		DrawText "x = " + player.x, 10, 10
		DrawText "y = " + player.y, 10, 20
		DrawText "r = " + player.rotation, 10, 30	
		DrawText "s = " + player.speed, 10, 40	
			
		return 0
	End Method
end class

Class Player
	Field x#, y#
	Field dx#, dy#, speed#
	Field rotation#
	Field scaleX# = 1, scaleY# = 1
	field frame% = 0
	Field image:Image
	
	Method New(img:Image, x#, y#)
		self.image = img
		self.x = x
		self.y = y
		self.rotation = 0
	End Method
	
	Method update:Void()
		If KeyDown(KEY_UP)
			self.speed+=0.4
		End If
		If KeyDown(KEY_DOWN)
			Self.speed-=0.4
		End If
		If speed &lt; -5 speed = -5
		If speed &gt; 8 speed = 8
		
		If KeyDown(KEY_LEFT)
			self.rotation-=2
		End If
		If KeyDown(KEY_RIGHT)
			self.rotation+=2
		End If
		
		dx = Sin(rotation) * speed
		dy = -Cos(rotation) * speed
		x+=dx
		y+=dy
		
		If x &lt; 0 x = SCREEN_WIDTH
		If x &gt; SCREEN_WIDTH x = 0
		If y &lt; 0 y = SCREEN_HEIGHT
		If y &gt; SCREEN_HEIGHT y = 0
	End Method
	
	Method draw:Void()
		PushMatrix
		Translate x, y
		Rotate rotation
		DrawImage(image, 0, 0)
		PopMatrix
	End Method
	
End Class
</textarea><br><br>Both looks backwards to me??? <br><br></td></tr></table><br>
<a name="2002647"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DGuy</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hmmm, ... guess we need some clarification on what direction of rotation to expect from a positive angle.<br><br>Currently, a positive angle is producing a counter-clockwise rotation. <br><br></td></tr></table><br>
<a name="2002652"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> <a href="http://en.wikipedia.org/wiki/Rotation_(mathematics)" target="_blank">http://en.wikipedia.org/wiki/Rotation_(mathematics)</a><br><br>Mathematically, using a positive angle it's counter clockwise from right, in Blitzmax it's clockwise from top.<br><br>I guess we are doing it the proper way now... <br><br></td></tr></table><br>
<a name="2002654"></a>

<a name="2002655"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> No Blitzmax is clockwise from right. 0 degrees is at 3 o'clock on Blitzmax. <br><br></td></tr></table><br>
<a name="2002657"></a>

<a name="2002658"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> 0 degrees is at 3 o'clock on Blitzmax. <br></div><br><br>Yep, my mistake - ta! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
