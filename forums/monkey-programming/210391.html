<!DOCTYPE html><html lang="en" ><head ><title >DrawColorText</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >DrawColorText</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=507" >Monkey Programming</a>/<a href="#bottom" >DrawColorText</a><br><br>
<a name="2116601"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >hub</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> i'm searching a way to display some coloured words inside a drawtext command so...<br><br>example  DrawColorText "this is [[ff0000]] an example [[ffffff]] for you"<br><br>i"ve this. No work as expected. some others words are also ff0000 red.<br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

	Method HexToInteger:Int(Hex:String)

		Local digit:Int, value:Int;
		For Local i:Int = 0 Until Hex.Length '-1;
			value*=16
			Select Hex[i]
				Case "0"[0]; digit = 0;
				Case "1"[0]; digit = 1;
				Case "2"[0]; digit = 2;
				Case "3"[0]; digit = 3;
				Case "4"[0]; digit = 4;
				Case "5"[0]; digit = 5;
				Case "6"[0]; digit = 6;
				Case "7"[0]; digit = 7;
				Case "8"[0]; digit = 8;
				Case "9"[0]; digit = 9;
				Case "A"[0], "a"[0]; digit = 10;
				Case "B"[0], "b"[0]; digit = 11;
				Case "C"[0], "c"[0]; digit = 12;
				Case "D"[0], "d"[0]; digit = 13;
				Case "E"[0], "e"[0]; digit = 14;
				Case "F"[0], "f"[0]; digit = 15;
				Default
					'Error("unexpected character on hexadecimal literal.");
					digit = 0;
			End
			value = (value + digit) 
		Next
		Return value;
		
	End Method

	Method DrawColorText:Void( text:String,x:Float,y:Float,xhandle:Float=0,yhandle:Float=0 )
		
		Local OffsetX : Int []
		Local r : Int []
		Local v : Int []
		Local b : Int []
		
		Local Count = 0
		
		Local Pos_start : Int
		Local Pos_end : Int
		Local Hexa_color : String
		
		Local bQuit_loop
		
		bQuit_loop = False
		
		While bQuit_loop = False 
		
			Pos_start = text.Find( "[[", 0 )
			If Pos_start &lt;&gt; - 1 Then
				Pos_end = text.Find ("]]",  Pos_start)
				If Pos_end &lt;&gt; - 1 Then
				
					If Pos_end - Pos_start = 8 Then
					
						Hexa_color = text[Pos_start+2..Pos_end]

						OffsetX = New Int[Count+1]
						
						If Count = 0 Then
						
							r = New Int[Count+1]
							v = New Int[Count+1]
							b = New Int[Count+1]
						
						Else
						
							r = r.Resize (Count+1)
							v = v.Resize (Count+1)
							b = b.Resize (Count+1)
						
						Endif
						
						OffsetX[Count] = Pos_start
						
						r[Count] = HexToInteger ( Hexa_color [0..2] )
						v[Count] = HexToInteger ( Hexa_color [2..4] )
						b[Count] = HexToInteger ( Hexa_color [4..6] )
						
						Local To_replace : String
						To_replace = "[[" + Hexa_color + "]]"
						
						text = text.Replace (To_replace,"")

						Count = Count + 1

					Else
					
						' erreur de valeur
						bQuit_loop = True
					
					End If
				Else
				
					bQuit_loop = True
					' erreur manque balise de fin.
				
				End If
			Else
				bQuit_loop = True
			End If
		Wend
	
		x-=TextWidth( text )*xhandle
		y-=TextHeight( text )*yhandle
		
		Local Pos : Int
		
		For Local char:=Eachin text
		
			For Local i=0 To OffsetX.Length()-1
				If OffsetX[i] = Pos Then
					SetColor r[i], v[i], b[i]
				End If
			Next
		
			Local glyph:=GetGlyph( char )
			If Not glyph Continue
			DrawImageRect (glyph.image, x, y, glyph.x,glyph.y, glyph.width,glyph.height)
			
			x+=glyph.advance
			Pos = Pos + 1
		Next
	End
</textarea> <br><br></td></tr></table><br>
<a name="2116600"></a>

<a name="2116693"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dawlane</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's a working example for you. Study this code and compare it to your code. Dig around the Monkey Code section. You will see a few other method of converting an hexadecimal string into an integer. Note that there may be a bug or two as I haven't fully tested it.<br>Edit: I've updated it to include alpha.<br>Edit: And fixed a small bug.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict
Import mojo

Class CGame Extends App
	Field str:String
	Method OnCreate:Int()
		SetUpdateRate(60)
		' Hex format Alpha Red Green Blue
		str="This is a test for half alpha [[0x7FFF0000]]RED[[0xFFFFFFFF]]. This is a test for [[0xFF00FF00]]GREEN[[0xFFFFFFFF]]. This is as test for [[0xFF0000FF]]BLUE[[0xFFFFFFFF]].~n"
		str+="This is a test for [[0xFF00FFFF]]CYAN[[0xFFFFFFFF]]. This is a test for [[0xFFFF00FF]]MAGENTA[[0xFFFFFFFF]]. This is a test for [[0xFFFFFF00]]YELLOW[[0xFFFFFFFF]].~n ~n" ' Bug ~n~n will not work, so you need to have a space between them.
		str+="This is a test for wrapping. This is a test for wrapping. This is a test for wrapping. This is a test for wrapping. This is a test for wrapping. This is a test for wrapping. This is a test for wrapping. This is a test for wrapping."
		Return 0
	End
	Method OnUpdate:Int()
		Return 0
	End
	Method OnRender:Int()
		Cls
		DrawColourText( 0,0, str )
		Return 0
	End
	
	Method DrawColourText:Void( x:Int=0, y:Int=0, str:String="", width:Int=9, height:Int=14, colour:Int=$FFFFFFFF)
		Local xx:Int=x, yy:Int=y, index:Int, p1:Int, tmp:String
		While index &lt; str.Length()
			' Check for colour switch in text
			If str[ index .. index+4 ]&lt;&gt;"[[0x"
				' Check that test string will fit on display area and if not, wrap round and move one line down
				' Use a tmp string to process what we have
				p1=str.Find( " ", index )
				If p1=-1 p1=str.Length()
				tmp=ProcessString(str[ index .. p1 ])
				' Check to see if word will fit
				If xx+(tmp.Length()*width)&gt;DeviceWidth() Or str[index]="~n"[0]
					xx=x
					yy+=height
					If str[index]="~n"[0] index+=1
				Endif
				' Draw text and move right
				DrawText(String.FromChar(str[index]),xx,yy)
				xx+=width
				index+=1
			Else
				' Switch colour
				' Here we can use the index witout worry
				p1=str.Find("]]", index+4) ' Find the hex enclosing brackets
				If p1=-1 Error("Error: Hex without enclosing brackets. Starting from "+index)
				' Subtract the start from the end to get total number of character to process
				If (p1-(index+4))&lt;&gt;8 Then Error("Error: Value must be 32 bit hexadecimal (max 8 hex digits) starting with 0x or enclosing brackets missing. Starting at from "+index)
				colour=Hex2Int( str[ (index+4) .. p1 ] )
				' Use the binary Shift-Right to move the required bits into the first byte and using the binary AND to capture the first byte only
				SetColor((colour Shr 16)&amp;$FF ,(colour Shr 8)&amp;$FF,(colour&amp;$FF))
				SetAlpha(Normalize((colour Shr 24)&amp;$FF))
				index+=(p1-(index+4))+6 ' Skip [[0x(s2-s1 length)]] to point the index to the next character
			Endif
		Wend
	End
	
	Method ProcessString:String(str:String)
		Local checks:String[]=[ "[[0x", "~n" ], p1:Int, i:Int
		While i&lt;checks.Length()
			p1=str.Find(checks[i])
			If p1=-1 p1=str.Length()
			str=str[ .. p1 ]
			i+=1
		Wend
		Return str
	End
End

' This converts an integer in the range [0 - 255] to a float in the range [0.0 - 1.0]
' Allowances must be made for float inaccuracies
Function Normalize:Float( val:Int )
	Return Float(val)/255.0
End

' One of a few methods to convert an hex number into a decimal
Function Hex2Int:Int( s:String )
	Local hexdigit:Int, i:Int, n:Int
	s=s.ToUpper()	' Turn all alpha character into capital letters
	' Loop each character to process
	While i&lt;s.Length()
		' As reading a string as an array returns the character code. We can subtract the start character to get a value
		' Using "0"[0] would be the equvalent of writing 48. You can hard code these instead of using "char"[0] for a bit of optimising.
		If s[i]&gt;="0"[0] And s[i]&lt;="9"[0]
			hexdigit=s[i]-"0"[0]
		Elseif s[i]&gt;="A"[0] And s[i]&lt;="F"[0]
			hexdigit=s[i]-"A"[0]+10
		Else
			Error("Error: Invalid hex digit")
		End
		' Multiply by 16 (as each hex digit is 16 times creater than the last) and add the next converted number.
		' Using n=(n Shl 4)+hexdigit would be the equivalent of n=16*n+hexdigit.
		n=(n Shl 4)+hexdigit
		i+=1
	Wend
	Return n
End

Function Main:Int()
	New CGame()
	Return 0
End</textarea><br>Edit: If a string doesn't change, then I would pre-process the string and store the parts of a string with the associated colour as an object onto a stack. It saves re-parsing a string over and over again.<br><br>If you need a brush up on strings then see my post <a href="http://www.monkey-x.com/Community/posts.php?topic=7619" target="_blank">here (post #36)</a>. <br><br></td></tr></table><br>
<a name="2116690"></a>

<a name="2116689"></a>

<a name="2116688"></a>

<a name="2116687"></a>

<a name="2116686"></a>

<a name="2116683"></a>

<a name="2116682"></a>

<a name="2116681"></a>

<a name="2116679"></a>

<a name="2116671"></a>

<a name="2116670"></a>

<a name="2116669"></a>

<a name="2116668"></a>

<a name="2116667"></a>

<a name="2116665"></a>

<a name="2116664"></a>

<a name="2116657"></a>

<a name="2116656"></a>

<a name="2116655"></a>

<a name="2116654"></a>

<a name="2116653"></a>

<a name="2116652"></a>

<a name="2116651"></a>

<a name="2116649"></a>

<a name="2116648"></a>

<a name="2116647"></a>

<a name="2116646"></a>

<a name="2116645"></a>

<a name="2116644"></a>

<a name="2116643"></a>

<a name="2116641"></a>

<a name="2116640"></a>

<a name="2116639"></a>

<a name="2116637"></a>

<a name="2116638"></a>

<a name="2116673"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >hub</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Many thanks for your help dawlane ! Nice idea to add alpha. <br><br></td></tr></table><br>
<a name="2116691"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dawlane</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Your welcome. I've cleaned up the code above, add some comments and a few teaks.<br>I should point out that doing anything more, such as flashing text would require a display engine.<br><br>Edit: I think there is one the one bug in it to do with new line characters. Haven't the time to see what's up with it. <br><br></td></tr></table><br>
<a name="2116680"></a>

<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
