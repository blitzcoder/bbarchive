<!DOCTYPE html><html lang="en" ><head ><title >Worms Style Destructable Terrain Help</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Worms Style Destructable Terrain Help</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=507" >Monkey Programming</a>/<a href="#bottom" >Worms Style Destructable Terrain Help</a><br><br>
<a name="2036808"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tibit</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Was going to test Worms-Style Destructable Terrain, adding it as a Monkey example.<br><br>Found this:<br>http://web.archive.org/web/20090101215451/http://blog.xna3.com/2007/12/2d-deformable-level.html<br><br>I'm not just getting it to work properly, can you solve the mystery?<br>[monkeycode]<br><br>'create a new image...single frame only?<br>'Function CreateImage:Image( width,height,flags )<br><br>'read from 'backbuffer'...<br>'Function ReadPixels:Void( pixels:Int[],x:Int,y:Int,width:Int,height:Int,arrayOffset:Int=0,arrayPitch:Int=0 )<br><br>'write to image...must be a CreateImage image.<br>'Method Image.WritePixels( pixels:Int[],x:Int,y:Int,width:Int,height:Int,arrayOffset:Int=0,arrayPitch:Int=0 )<br><br>Import mojo<br><br>Function Main:Int()<br>	New MyApp()<br>	Return 0<br>End<br><br>Global ToolWidth:Int = 64<br>Global ToolHeight:Int = 64<br><br>Global MapWidth:Int  = 800<br>Global MapHeight:Int = 600<br><br>Class MyApp Extends App<br>	Field sky:Image<br>	Field ground:Image<br>	Field tool:Image<br>	<br>	Field destructableGround:Image<br>	'Above: This image is created by CreateImage and will be a editable copy of ground<br>	<br>	Field groundPixels:Int[] = [MapWidth*MapHeight] <br>	'Above: Every single pixel of the ground.png image will be put in this array<br><br>	Field tool1Pixels:Int[] = [ToolWidth*ToolHeight] <br>	Field tool1Mask:Int[] = [ToolWidth*ToolHeight] 	<br>	'Above: Every single pixel of the tool.png image will be put in this array<br>		<br>	Field FirstFrame:Bool = True<br>	'Above: A simple hack to run the InitializeTerrain Method only once<br>				<br>	Method OnCreate()			<br>		SetUpdateRate(60)<br>		sky = LoadImage("sky.png")<br>		ground = LoadImage("ground.png")<br>		tool = LoadImage("tool.png")<br>		destructableGround = CreateImage(MapWidth,MapHeight)<br>	End<br>	<br>	Method OnUpdate()<br>		<br>	End<br>	<br>	'ReadPixels Buffer,0,0,40,40<br>	'TargetImage.WritePixels Buffer,0,0,80,40<br>	Method InitializeTerrain()<br>		DrawImage(ground,0,0)<br>		<br>		' From Docs:<br>		' ReadPixels( pixels:Int[], x:Int, y:Int, width:Int, height:Int, arrayOffset:Int=0, arrayPitch:Int=0 )<br>		ReadPixels( groundPixels,0,0,MapWidth,MapHeight )<br>		<br>		DrawImage(tool,0,0)<br>		ReadPixels( tool1Pixels,0,0,ToolWidth,ToolHeight )<br>		ReadPixels( tool1Mask,0,0,ToolWidth,ToolHeight )<br>	'	For Local i = 0 To ToolWidth*ToolHeight-1<br>	'		If tool1Pixels[i] &gt; 0 <br>	'			tool1Mask[i] = 0<br>	'		Else<br>	'			tool1Mask[i] = 1<br>	''		End <br>	'	Next<br>		'Write can only be done on an Image created by CreateImage()<br>		destructableGround.WritePixels( groundPixels,0,0,MapWidth,MapHeight )<br>		destructableGround.WritePixels( tool1Pixels,0,0,ToolWidth,ToolHeight)<br>	End<br>	<br>	' This Adds terrain<br>	Method AddTerrain()<br>		ReadPixels( tool1Pixels,MouseX-ToolWidth/2,MouseY-ToolHeight/2,ToolWidth,ToolHeight )<br>		destructableGround.WritePixels( tool1Pixels,MouseX-ToolWidth/2,MouseY-ToolHeight/2,ToolWidth,ToolHeight )<br>	End <br>	<br>	Method RemoveTerrain()<br>		Local counter:Int = 0<br>		ReadPixels( tool1Pixels,MouseX-ToolWidth/2,MouseY-ToolHeight/2,ToolWidth,ToolHeight )<br>		For Local x = 0 until ToolWidth<br>			For Local y = 0 until ToolHeight<br>				'Print "Pixel [x,y] = ["+x+","+y+"] = "+tool1Pixels[x*y]<br>				If tool1Mask[x * y] = -16777216<br>					tool1Pixels[x*y] = groundPixels[(Int(MouseX) + x) + (Int(MouseY) + y)];''<br>					counter += 1<br>				Else<br>					tool1Pixels[x*y] = 0<br>				End<br>			Next<br>			Print tool1Mask[x*32]<br>		Next<br>		Print "Found "+counter+" pixels with Alpha"<br>		<br>		destructableGround.WritePixels( tool1Mask,MouseX-ToolWidth/2,MouseY-ToolHeight/2,ToolWidth,ToolHeight )<br>	End<br>	<br>	Method OnRender()<br>		<br>		If FirstFrame Then InitializeTerrain()<br>		FirstFrame = False<br>		Cls(0, 0, 0)<br>		DrawImage(sky,0,0)<br>		DrawImage(destructableGround,0,0)<br>		<br>		SetColor( 0,153,0)<br>		DrawCircle(MouseX,MouseY,ToolHeight/2) 'Read &amp; Write<br>		SetColor( 255,255,255)<br>		'DrawImage(tool,MouseX,MouseY)<br>				<br>		If MouseDown(MOUSE_LEFT) And KeyDown(KEY_CONTROL) = False <br>			If EditMode1 = False Then AddTerrain<br>			EditMode1 = True <br>		Else<br>			EditMode1 = False<br>		End <br>		If ( MouseDown(MOUSE_LEFT) And KeyDown(KEY_CONTROL) )<br>			If EditMode2 = False Then RemoveTerrain<br>			EditMode2 = True <br>		Else<br>			EditMode2 = False<br>		End 			<br>	End<br>	<br>	Field EditMode1:Bool = False<br>	Field EditMode2:Bool = False<br>End<br>[/monkeycode]<br><br>Here is a downloadable with graphics included. www.truplo.com/UploadedFiles/destructableTerrain.zip <br><br></td></tr></table><br>
<a name="2036807"></a>

<a name="2036806"></a>

<a name="2036803"></a>

<a name="2036809"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think you are nearly there (although the code is slightly messy).<br><br>With your tool.png just create an empty image (ie one that is only alpha), does that solve your issue? (You havent actually said what the issue is).<br><br>To make this playable in-game, I think you would have to make your Terrain into a grid and only alter that grids pixels. <br><br></td></tr></table><br>
<a name="2036908"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Tibit, I wrote a quite example to show how this can be done. You will need the deform sprite from the link you posted, I also used their ground image as well so I would grab that:<br>http://web.archive.org/web/20110609003109/http://sites.google.com/site/edwarddennekamp/deform.png<br><br>It would definitely benefit from therevills suggestion of using a grid. Anyway heres the code example:<br><br>[monkeycode]<br>Strict<br>Import mojo<br><br>Global tool : Image<br>Global toolPixels : Int[]<br>	<br>Global ground : Image<br>Global groundPixels : Int[]<br>	<br>Global loaded : Bool = False<br><br><br>Class MyApp Extends App<br><br>	<br>	Method OnCreate : int()<br>		SetUpdateRate( 60 )<br><br>		ground = LoadImage( "level.png" )<br>		tool = LoadImage( "deform.png" )<br>		<br>		Return 0<br>	End Method<br>	<br><br><br>	Method OnUpdate : Int()<br>		<br>		If MouseHit()<br><br>			Local w : Int = ground.Width()<br>			Local h : Int = ground.Height()<br>			<br>			Local tw : Int = tool.Width()<br>			Local th : Int = tool.Height()<br>			<br>			'// offset mouse by tool size ( center tool )<br>			Local mx : Int = MouseX() - ( tw / 2.0 )<br>			Local my : Int = MouseY() - ( th / 2.0 )	<br>			<br>			'// calculate starting pixel index<br>			Local ti : Int = w * my + mx<br>			<br>			<br>			'// loop through tool pixels<br>			For Local y : Int = 0 Until th<br>				For Local x : Int = 0 Until tw<br>					<br>					'// if current location is on the screen<br>					If mx + x &lt; DeviceWidth() And my + y &lt; DeviceHeight() And mx + x &gt; -1 And my + y &gt; -1<br>						Local argb : Int<br>						<br>						'// get ground alpha<br>						argb = groundPixels[ ti + x ]<br>						Local ga : Int = ( argb Shr 24 ) &amp; $ff<br>						<br>						'// get tool alpha and RGB to check for white delete part<br>						argb = toolPixels[( y * tw ) + x ]<br>						Local ta : Int = ( argb Shr 24 ) &amp; $ff					<br>						Local tr : Int = ( argb Shr 16 ) &amp; $ff<br>						Local tg : Int = ( argb Shr 8 ) &amp; $ff<br>						Local tb : Int = argb &amp; $ff<br>		<br>						'// if neither are transparent write the tool to the ground pixels (draws white tool section on the ground)<br>						If ga &lt;&gt; 0 And ta &lt;&gt; 0<br>						<br>							'if current pixel of tool is white we want to cut this part out so use alpha color instead<br>							If tr = 255 And tg = 255 And tb = 255<br>								groundPixels[ ti + x ] = 0<br>							<br>							'if current pixel of tool is not white, e.g. black border, leave this as it is<br>							Else<br>								groundPixels[ ti + x ] = toolPixels[( y * tw ) + x ]<br>							Endif<br>						Endif<br>					endif<br>				Next<br>				<br>				'// increment pixel index by the ground width to get the next rows starting index<br>				ti = ti + w<br>			Next<br><br>			'// update ground image<br>			ground.WritePixels( groundPixels, 0, 0, w, h )<br>		<br>		Endif<br><br>		Return 0<br>	End Method<br>	<br><br><br>	Method OnRender : Int()	<br>	<br>		'// Creates Manipulatable Images<br>		If loaded = False<br>			CreateCustomImages()<br>			loaded = True<br>		Endif<br>		<br>		Cls 64, 96, 160		<br><br>		'// Draws Images<br>		If ground &lt;&gt; Null Then DrawImage( ground, 0, 0 )<br>		If tool &lt;&gt; Null Then DrawImage( tool, MouseX(), MouseY() )<br>		<br><br>		Return 0<br>	End Method<br>	<br><br>End Class<br><br><br><br><br>Function Main : Int()<br>	New MyApp()<br>	Return 0<br>End Function<br><br><br><br><br>Function CreateCustomImages : Void()<br><br>	'// Create space for tool<br>	Local w : Int = tool.Width()<br>	Local h : Int = tool.Height()<br>	Local pixels : Int[ w * h ]<br>	Local img : Image<br>	<br>	Cls 128, 0, 255<br>	<br>	'// Grab tool sprite and mask background colour<br>	DrawImage tool, 0, 0<br>	ReadPixels( pixels, 0, 0, w, h )<br>	PixelArrayMask( pixels, 128, 0, 255 )<br>	img = CreateImage( w, h, 1, Image.MidHandle )<br>	img.WritePixels( pixels, 0, 0, w, h )<br>	tool = img<br>	toolPixels = pixels<br>	<br>	'// Create space for ground<br>	w = Min( ground.Width(), DeviceWidth() )<br>	h = Min( ground.Height(), DeviceHeight() )<br>	pixels = New Int[ w * h ]<br>	<br>	Cls 128, 0, 255<br>	<br>	'// Grab ground sprite and mask background colour<br>	DrawImage ground, 0, 0<br>	ReadPixels( pixels, 0, 0, w, h )<br>	PixelArrayMask( pixels, 128, 0, 255 )<br>	img = CreateImage( w, h )<br>	img.WritePixels( pixels, 0, 0, w, h )<br>	ground = img<br>	groundPixels = pixels	<br>	<br>End Function<br><br><br><br><br>'// Converts Mask Pixel Color to Transparent Pixel<br>Function PixelArrayMask : void( pixels : Int[], mask_r : Int = 0, mask_g : Int = 0, mask_b : Int = 0 )<br>	For Local i : Int = 0 Until pixels.Length<br>		Local argb : Int = pixels[ i ]<br>		Local a : Int = ( argb Shr 24 ) &amp; $ff<br>		Local r : Int = ( argb Shr 16 ) &amp; $ff<br>		Local g : Int = ( argb Shr 8 ) &amp; $ff<br>		Local b : Int = argb &amp; $ff<br>		If a = 255 And r = mask_r And g = mask_g And b = mask_b<br>			a = 0<br>			argb = ( a Shl 24 ) | ( r Shl 16 ) | ( g Shl 8 ) | b<br>			pixels[ i ] = argb<br>		Endif<br>	Next<br>End Function<br>[/monkeycode] <br><br></td></tr></table><br>
<a name="2036907"></a>

<a name="2036828"></a>

<a name="2036827"></a>

<a name="2036826"></a>

<a name="2036825"></a>

<a name="2036824"></a>

<a name="2036822"></a>

<a name="2037194"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tibit</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have been looking at this, but I just can't get it to work.<br><br>Noodle, your example first says Write cannot be performed in OnUpdate, I fixed that, but then nothing happens at all at MouseHit. I used the correct images for your sample. Can you or someone else get it to run properly?<br><br>It feels like there is some minor thing missing? I tested on html5 and glhw targets.<br><br>The aim:<br>* I want to be able to add a shape to the terrain [This works]<br>* I want to be able to remove a shape from the terrain [This works ONLY for a rectangular shape]<br><br>So the intention with my code above and Noodle's Mask code is to use an image as the tool and use that image's white (or non alpha) pixels (as an example) to add alpha (that is remove pixels) from the background image.<br><br>And yes I agree that the right tactic might be using a grid and rendering tiles of images. However this technique was done back in Worms in 1995, it just blows my mind it cannot be done with hardware from 2012 Hah ;) <br><br></td></tr></table><br>
<a name="2037193"></a>

<a name="2037192"></a>

<a name="2037191"></a>

<a name="2037196"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CopperCircle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> This works:<br><br><pre class=code>
Strict
Import mojo

Global tool : Image
Global toolPixels : Int[]
        
Global ground : Image
Global groundPixels : Int[]
        
Global loaded : Bool = False

Global pointIndexsToKeep:IntList = New IntList


Class MyApp Extends App

        
        Method OnCreate : int()
                SetUpdateRate( 60 )

                ground = LoadImage( "level.png" )
                tool = LoadImage( "deform.png" )
                
                Return 0
        End Method
        


        Method OnUpdate : Int()
                
                If MouseHit()

                        Local w : Int = ground.Width()
                        Local h : Int = ground.Height()
                        
                        Local tw : Int = tool.Width()
                        Local th : Int = tool.Height()
                        
                        '// offset mouse by tool size ( center tool )
                        Local mx : Int = MouseX() - ( tw / 2.0 )
                        Local my : Int = MouseY() - ( th / 2.0 )        
                        
                        '// calculate starting pixel index
                        Local ti : Int = w * my + mx
                        
                        
                        '// loop through tool pixels
                        For Local y : Int = 0 Until th
                                For Local x : Int = 0 Until tw
                                        
                                        '// if current location is on the screen
                                        If mx + x &lt; DeviceWidth() And my + y &lt; DeviceHeight() And mx + x &gt; -1 And my + y &gt; -1
                                                Local argb : Int
                                                
                                                '// get ground alpha
                                                argb = groundPixels[ ti + x ]
                                                Local ga : Int = ( argb Shr 24 ) &amp; $ff
                                                
                                                '// get tool alpha and RGB to check for white delete part
                                                argb = toolPixels[( y * tw ) + x ]
                                                Local ta : Int = ( argb Shr 24 ) &amp; $ff                                  
                                                Local tr : Int = ( argb Shr 16 ) &amp; $ff
                                                Local tg : Int = ( argb Shr 8 ) &amp; $ff
                                                Local tb : Int = argb &amp; $ff
                
                                                '// if neither are transparent write the tool to the ground pixels (draws white tool section on the ground)
                                                If ga &lt;&gt; 0 And ta &lt;&gt; 0
                                                
                                                        'if current pixel of tool is white we want to cut this part out so use alpha color instead
                                                        If tr = 255 And tg = 255 And tb = 255
                                                                groundPixels[ ti + x ] = 0
                                                        
                                                        'if current pixel of tool is not white, e.g. black border, leave this as it is
                                                        Else
                                                                groundPixels[ ti + x ] = toolPixels[( y * tw ) + x ]
                                                        Endif
                                                Endif
                                        endif
                                Next
                                
                                '// increment pixel index by the ground width to get the next rows starting index
                                ti = ti + w
                        Next
                Endif

                Return 0
        End Method
        


        Method OnRender : Int() 
        
                '// Creates Manipulatable Images
                If loaded = False
                        CreateCustomImages()
                        loaded = True
                Endif
                
                Cls 64, 96, 160         
				
				Local w : Int = ground.Width()
                Local h : Int = ground.Height()
				'// update ground image
                ground.WritePixels( groundPixels, 0, 0, w, h )

                '// Draws Images
                If ground &lt;&gt; Null Then DrawImage( ground, 0, 0 )
                If tool &lt;&gt; Null Then DrawImage( tool, MouseX(), MouseY() )
                
                Return 0
        End Method

End Class


Function Main : Int()
        New MyApp()
        Return 0
End Function


Function CreateCustomImages : Void()

        '// Create space for tool
        Local w : Int = tool.Width()
        Local h : Int = tool.Height()
        Local pixels : Int[ w * h ]
        Local img : Image
        
        Cls 128, 0, 255
        
        '// Grab tool sprite and mask background colour
        DrawImage tool, 0, 0
        ReadPixels( pixels, 0, 0, w, h )
        PixelArrayMask( pixels, 128, 0, 255 )
        img = CreateImage( w, h, 1, Image.MidHandle )
        img.WritePixels( pixels, 0, 0, w, h )
        tool = img
        toolPixels = pixels
        
        '// Create space for ground
        w = Min( ground.Width(), DeviceWidth() )
        h = Min( ground.Height(), DeviceHeight() )
        pixels = New Int[ w * h ]
        
        Cls 128, 0, 255
        
        '// Grab ground sprite and mask background colour
        DrawImage ground, 0, 0
        ReadPixels( pixels, 0, 0, w, h )
        PixelArrayMask( pixels, 128, 0, 255 )
        img = CreateImage( w, h )
        img.WritePixels( pixels, 0, 0, w, h )
        ground = img
        groundPixels = pixels   
        
End Function

'// Converts Mask Pixel Color to Transparent Pixel
Function PixelArrayMask : void( pixels : Int[], mask_r : Int = 0, mask_g : Int = 0, mask_b : Int = 0 )
        For Local i : Int = 0 Until pixels.Length
                Local argb : Int = pixels[ i ]
                Local a : Int = ( argb Shr 24 ) &amp; $ff
                Local r : Int = ( argb Shr 16 ) &amp; $ff
                Local g : Int = ( argb Shr 8 ) &amp; $ff
                Local b : Int = argb &amp; $ff
                If a = 255 And r = mask_r And g = mask_g And b = mask_b
                        a = 0
                        argb = ( a Shl 24 ) | ( r Shl 16 ) | ( g Shl 8 ) | b
                        pixels[ i ] = argb
                Endif
        Next
End Function
</pre> <br><br></td></tr></table><br>
<a name="2037197"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Noodle, your example first says Write cannot be performed in OnUpdate, <br></div><br><br>This is a bug in Monkey, you need to run it in Release mode:<br><br><a href="http://www.monkeycoder.co.nz/Community/posts.php?topic=3492" target="_blank">http://www.monkeycoder.co.nz/Community/posts.php?topic=3492</a> <br><br></td></tr></table><br>
<a name="2037204"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> This is a bug in Monkey, you need to run it in Release mode <br></div><br>Yea the code I posted runs fine. I would recommend using that instead of CopperCircle's edit as that writes to the ground image every render which will be slow. This only needs to be done when the ground image is modified. Hopefully this incorrect error message will be fixed in the next release. <br><br></td></tr></table><br>
<a name="2037203"></a>

<a name="2037202"></a>

<a name="2037205"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tibit</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Awesome, it works now!<br><br>Thanks :) <br><br></td></tr></table><br>
<a name="2037230"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >zoqfotpik</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> If I were doing this, and I may well be doing something similar, I would store the terrain data in a space partitioning tree and render it as sub-rects out of a tilemap.  If the tile is undamaged, draw the full tile.  If it is totally destroyed, don't draw anything. If it is partially damaged, go into each quadrant and perform the same operations.  <br><br>This should be much faster to draw as well as being far more memory efficient.<br><br>What I would really like would be a way to tell if a piece of terrain was disconnected from the rest of the terrain-- a stalactite shot off the ceiling or an overhang blown off.  Then that piece would become a physics object of mass equal to its number of pixels, and fall.<br><br>It seems like you could do this with some sort of floodfill algorithm and it might be possible to speed it up immensely with the bsp tree, somehow.  These checks would also only need to happen when terrain was actually destroyed and it might be possible to amortize it out across a number of frames.<br><br>Or you could see if an explosion connected empty space to other empty space. <br><br></td></tr></table><br>
<a name="2037228"></a>

<a name="2037229"></a>

<a name="2037281"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yea Therevills suggested something similar. All you would really need is to divide the level up into a grid of individual modifiable images with pixel arrays. Check for intersection with AABB (pixel AABB not including alpha pixels) and modify the overlapping grid rects. That should be fast enough for realtime use, I was planning to test this out at some point but busy working on my own projects. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
