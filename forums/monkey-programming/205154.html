<!DOCTYPE html><html lang="en" ><head ><title >admob native - help with lib</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >admob native - help with lib</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=507" >Monkey Programming</a>/<a href="#bottom" >admob native - help with lib</a><br><br>
<a name="2057011"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Lugato</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi for all, <br><br>I'm using the monkey v70e with native admob, I create a lib to abstract lib to make some compatibility with other plataforms (html, flash, etc).. But the banner don't appear.. Anyoune can give a little help to find the problem ???<br><br>Thanks for the Mark .. the code are fixed now ;) <br><br>here's the codes:<br><br>LIB libBanner.monkey<br><pre class=code>
Import mojo

#If TARGET="android" Or TARGET="ios"
	Import brl.admob
#End

Class Banner
	#If TARGET="android" Or TARGET="ios"
		Field admob:Admob
	#End
	Field bannerCalled:Bool = False
	Field bannerSize:Int
	Field bannerLayout:Int

	Method New (bannerSize:Int=1 , bannerLayout:Int=2)
		Self.bannerSize		= bannerSize
		Self.bannerLayout	= bannerLayout

		#If TARGET="android" Or TARGET="ios"
			Self.admob = Admob.GetAdmob()
			Self.admob.ShowAdView Self.bannerSize, Self.bannerLayout
		#End
		
		Self.bannerCalled = True
	End    

	Method SetType(bannerSize:Int=1 , bannerLayout:Int=2)
		Self.bannerSize		= bannerSize
		Self.bannerLayout	= bannerLayout
'		Print (Self.bannerSize +"|"+ Self.bannerLayout)

	End

	Method Show()
		If Not Self.bannerCalled Then 
			#If TARGET="android" Or TARGET="ios"
				Self.admob.ShowAdView Self.bannerSize, Self.bannerLayout
			#End
'			Print (Self.bannerSize +"|"+ Self.bannerLayout)
			Self.bannerCalled = True
		End
	End

	Method Disable()
		#If TARGET="android" Or TARGET="ios"
			Self.admob.HideAdView()
		#End
		Self.bannerCalled = False
	End
End
</pre>   <br><br><br>Example how to use:<br>banner.monkey<br><pre class=code>
#ADMOB_PUBLISHER_ID="abcdabcdabcdabc"                           'from your admod account
#ADMOB_ANDROID_TEST_DEVICE1="TEST_EMULATOR"
#ADMOB_ANDROID_TEST_DEVICE2="ABCDABCDABCDABCDABCDABCDABCDABCD"  'your device's admob ID for test mode
Import mojo
Import libBanner

Class MyApp Extends App

	Const BANNER_TOP_CENTER		= 2
	Const BANNER_TOP_LEFT		= 1
	Const BANNER_TOP_RIGHT		= 3
	
	Const BANNER_BOTTON_CENTER	= 5 
	Const BANNER_BOTTON_LEFT	= 4
	Const BANNER_BOTTON_RIGHT	= 6
	
	Const BANNER_SIZE_320X50	= 1

    Global banner:Banner = New Banner 

	Field enabled:Bool = True
    
    Method OnCreate()
		banner = New Banner()
        SetUpdateRate 60
    End
    
    Method OnUpdate()
        If MouseHit( 0 )
            If Self.enabled
                banner.Disable()
                Self.enabled=False
            Else
				banner.Show()
        		Self.enabled=True
            Endif
        End
    End
    
    Method OnRender()
        Cls
        DrawText "Click to toggle ads!",DeviceWidth/2,DeviceHeight/2,.5,.5
    End
    
End

Function Main()
    New MyApp
End
</pre> <br><br></td></tr></table><br>
<a name="2056861"></a>

<a name="2056862"></a>

<a name="2056859"></a>

<a name="2056860"></a>

<a name="2056858"></a>

<a name="2056939"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >marksibly</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> &gt; #If TARGET="android" And TARGET="ios"<br><br>Try 'Or' instead of 'And'... <br><br></td></tr></table><br>
<a name="2057010"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Lugato</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Mark, <br><br>Some times you need verify what are you coping and pasting hahahah <br><br>After the fixes the banner worked perfectly in the app ;)<br><br>Thanks for the help ;) <br><br></td></tr></table><br>
<a name="2060580"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nobuyuki</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've made some changes to your lib thingy to suit my needs.  The consts now live in the class, some typos have been corrected, and Disable() was changed to Hide() to provide more consistency with the underlying classes.  In addition, there is a DebugRender() method to show what the ad banner might look like, which can be called on any platform, and respects the enabled state of the ad.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import mojo

#If TARGET="android" Or TARGET="ios"
	Import brl.admob
#End

Class AdBanner
	Const SIZE_320X50 = 1

	Const LAYOUT_TOP_LEFT = 1
	Const LAYOUT_TOP_CENTER = 2
	Const LAYOUT_TOP_RIGHT = 3
	Const LAYOUT_BOTTOM_LEFT = 4
	Const LAYOUT_BOTTOM_CENTER = 5
	Const LAYOUT_BOTTOM_RIGHT = 6
	

	#If TARGET="android" Or TARGET="ios"
		Field admob:Admob
	#End
	Field bannerCalled:Bool = False
	Field bannerSize:Int
	Field bannerLayout:Int

	Method New(bannerSize:Int = SIZE_320X50, bannerLayout:Int = LAYOUT_TOP_CENTER)
		Self.bannerSize		= bannerSize
		Self.bannerLayout	= bannerLayout

		#If TARGET="android" Or TARGET="ios"
			Self.admob = Admob.GetAdmob()
			'Self.admob.ShowAdView Self.bannerSize, Self.bannerLayout
		#Else
		
		#End
		
		'Self.bannerCalled = True
	End    

	Method SetType(bannerSize:Int=1 , bannerLayout:Int=2)
		Self.bannerSize		= bannerSize
		Self.bannerLayout	= bannerLayout
'		Print (Self.bannerSize +"|"+ Self.bannerLayout)

	End

	Method Show()
		If Not Self.bannerCalled Then 
			#If TARGET="android" Or TARGET="ios"
				Self.admob.ShowAdView Self.bannerSize, Self.bannerLayout
			#Else
		
			#End
'			Print (Self.bannerSize +"|"+ Self.bannerLayout)
			Self.bannerCalled = True
		End
	End

	Method Hide()
		#If TARGET="android" Or TARGET="ios"
			Self.admob.HideAdView()
		#Else
		
		#End
		Self.bannerCalled = False
	End
	
	Method DebugRender()
		Local x:Float, y:Float, w:Float, h:Float
		
		Select bannerSize
		 Case SIZE_320X50
			w = 320; h = 50			
		 Default
			w = 320; h = 50
		End Select
		
		Select bannerLayout
		 Case LAYOUT_TOP_LEFT
			x = 0; y = 0
		 Case LAYOUT_TOP_CENTER
			x = DeviceWidth() / 2 - w / 2; y = 0
		 Case LAYOUT_TOP_RIGHT
			x = DeviceWidth() -w; y = 0
		 Case LAYOUT_BOTTOM_LEFT
			x = 0; y = DeviceHeight() -h
		 Case LAYOUT_BOTTOM_CENTER
			x = DeviceWidth() / 2 - w / 2; y = DeviceHeight() -h
		 Case LAYOUT_BOTTOM_RIGHT
			x = DeviceWidth() -w; y = DeviceHeight() -h
		Default
		End Select
		
		If Self.bannerCalled
			Local matrix:Float[] = GetMatrix()
			SetMatrix(1, 0, 0, 1, 0, 0)  'Identity Matrix
			
			SetAlpha(0.5)
			DrawRect(x, y, w, h)
			DrawText("Ads Here", x + w / 2, y + h / 2, 0.5, 0.5)
			SetAlpha(1)
			
			SetMatrix(matrix)  'Return old matrix
		End If
	End Method
End
</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
