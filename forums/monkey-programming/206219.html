<!DOCTYPE html><html lang="en" ><head ><title >Using brl.json to parse Texture Packer json files</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Using brl.json to parse Texture Packer json files</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=507" >Monkey Programming</a>/<a href="#bottom" >Using brl.json to parse Texture Packer json files</a><br><br>
<a name="2072704"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Texture Packer outputs the following json:<br><pre class=code>
{"frames": {
    
"frame-abc.png":
{
	"frame": {"x":41,"y":2,"w":256,"h":256},
	"rotated": false,
	"trimmed": false,
	"spriteSourceSize": {"x":0,"y":0,"w":256,"h":256},
	"sourceSize": {"w":256,"h":256}
},
"someframe":
{
	"frame": {"x":2,"y":2,"w":37,"h":382},
	"rotated": false,
	"trimmed": false,
	"spriteSourceSize": {"x":0,"y":0,"w":37,"h":382},
	"sourceSize": {"w":37,"h":382}
}
}
}
</pre><br><br>Yet I am having a hard time parsing it using brl.json... in fact I don't know where to start!! :(<br><br>This is what I've got so far and its not much:<br><pre class=code>		Local str:String = LoadString("sprites.json")
		Local jso:JsonObject = New JsonObject(str)
		Local sprites:JsonValue = jso.Get("frames")
		Print sprites.ToJson()
</pre><br><br>This outputs all the frames to the console, but how do I parse those objects to create "sprites"? <br><br></td></tr></table><br>
<a name="2071372"></a>

<a name="2071373"></a>

<a name="2071374"></a>

<a name="2071375"></a>

<a name="2072702"></a>

<a name="2072703"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Must have been tired last night:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict

Import brl.json
Import mojo

Function Main:Int()
	New Game
	Return True
End

Class Game Extends App
	Field spriteList:List&lt;SpriteAtlas&gt; = New List&lt;SpriteAtlas&gt;

	Method OnCreate:Int()
		SetUpdateRate(60)
		Local startTime:Int = Millisecs()
		Local endTime:Int
		Local diff:Int
		Print "!----" + startTime + "-------!"
		Local str:String = LoadString("test.json")
		Local jso:JsonObject = New JsonObject(str)
		Local sprites:JsonValue = jso.Get("frames")
		Local spriteJso:JsonObject = New JsonObject(sprites.ToJson())
		Local spriteData:StringMap&lt;JsonValue&gt; = spriteJso.GetData()
		
		For Local spriteKey:String = Eachin spriteData.Keys()
			Local sa:SpriteAtlas = New SpriteAtlas()
			sa.name = spriteKey
			Local frameValue:JsonValue = spriteData.Get(spriteKey)
			Local frameObject:JsonObject = New JsonObject(frameValue.ToJson())
			Local frameData:StringMap&lt;JsonValue&gt; = frameObject.GetData()
			For Local frameKey:String = Eachin frameData.Keys()
				Local frameKeyValue:JsonValue = frameData.Get(frameKey)
				Select frameKey
					Case "frame"
						Local frameKeyObject:JsonObject = New JsonObject(frameKeyValue.ToJson())
						Local frameKeyData:StringMap&lt;JsonValue&gt; = frameKeyObject.GetData()
						For Local frameDataKey:String = Eachin frameKeyData.Keys()
							If frameDataKey
								Local frameDataKeyValue:JsonValue = frameKeyData.Get(frameDataKey)
								Select frameDataKey
									Case "x"
										sa.frameX = frameDataKeyValue.IntValue()
									Case "y"
										sa.frameY = frameDataKeyValue.IntValue()
									Case "h"
										sa.frameH = frameDataKeyValue.IntValue()
									Case "w"		
										sa.frameW = frameDataKeyValue.IntValue()
								End							
							End
						Next
					Case "rotated"
						sa.rotated = frameKeyValue.BoolValue()
					Case "sourceSize"
						Local frameKeyObject:JsonObject = New JsonObject(frameKeyValue.ToJson())
						Local frameKeyData:StringMap&lt;JsonValue&gt; = frameKeyObject.GetData()
						For Local frameDataKey:String = Eachin frameKeyData.Keys()
							If frameDataKey
								Local frameDataKeyValue:JsonValue = frameKeyData.Get(frameDataKey)
								Select frameDataKey
									Case "x"
										sa.spriteSourceSizeX  = frameDataKeyValue.IntValue()
									Case "y"
										sa.spriteSourceSizeY  = frameDataKeyValue.IntValue()
									Case "h"
										sa.spriteSourceSizeH  = frameDataKeyValue.IntValue()
									Case "w"		
										sa.spriteSourceSizeW = frameDataKeyValue.IntValue()
								End							
							End
						Next
					Case "spriteSourceSize"
						Local frameKeyObject:JsonObject = New JsonObject(frameKeyValue.ToJson())
						Local frameKeyData:StringMap&lt;JsonValue&gt; = frameKeyObject.GetData()
						For Local frameDataKey:String = Eachin frameKeyData.Keys()
							If frameDataKey
								Local frameDataKeyValue:JsonValue = frameKeyData.Get(frameDataKey)
								Select frameDataKey
									Case "h"
										sa.sourceSizeH = frameDataKeyValue.IntValue()
									Case "w"		
										sa.sourceSizeW = frameDataKeyValue.IntValue()
								End							
							End
						Next
					Case "trimmed"
						sa.trimmed = frameKeyValue.BoolValue()
				End
			Next
			spriteList.AddLast(sa)
		Next
		
		endTime = Millisecs()
		diff = endTime - startTime
		Print "--- " + diff + " ms ---"		
		Print "--- Finished Parsing ---"
		
		For Local sa:SpriteAtlas = Eachin spriteList
			Print "** name = " + sa.name + " **"
			Print "frameX = " + sa.frameX
			Print "frameY = " + sa.frameY
			Print "frameW = " + sa.frameW
			Print "frameH = " + sa.frameH
			If sa.rotated Then
				Print "rotated = true"
			Else
				Print "rotated = false"
			End
			If sa.trimmed Then
				Print "trimmed = true"
			Else
				Print "trimmed = false"
			End
			Print "spriteSourceSizeX = " + sa.spriteSourceSizeX
			Print "spriteSourceSizeY = " + sa.spriteSourceSizeY
			Print "spriteSourceSizeW = " + sa.spriteSourceSizeW
			Print "spriteSourceSizeH = " + sa.spriteSourceSizeH
			Print "sourceSizeW = " + sa.sourceSizeW
			Print "sourceSizeH = " + sa.sourceSizeH
		Next
		
		Return True
	End

	Method OnLoading:Int()
		Return True
	End Method

	Method OnUpdate:Int()
		Return True
	End Method
	
	Method OnRender:Int()
		Return True
	End Method

	Method OnSuspend:Int()
		Return True
	End Method

	Method OnResume:Int()
		Return True
	End Method
	
End

Class SpriteAtlas
	Field name:String
	Field frameX:Int
	Field frameY:Int
	Field frameW:Int
	Field frameH:Int
	Field rotated:Bool
	Field trimmed:Bool
	Field spriteSourceSizeX:Int
	Field spriteSourceSizeY:Int
	Field spriteSourceSizeW:Int
	Field spriteSourceSizeH:Int
	Field sourceSizeW:Int
	Field sourceSizeH:Int
End</textarea><br><br>Still not happy about changing the "objects" to json all the time... and its fugly... but it works... <br><br></td></tr></table><br>
<a name="2072716"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >marksibly</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> You could use GetData() to retrieve the underlying StringMap&lt;JsonValue&gt;, then iterate through the map, eg:<br><br><pre class=code>
Local str:=LoadString("sprites.json")
Local jso:=New JsonObject(str)
Local sprs:=JsonObject( jso.Get("frames") )
For Local it:=EachIn sprs.GetData()
   Local key:=it.Key                 'eg: frame-abc.png
   Local spr:=JsonObject( it.Value )
   Local frame:=JsonObject( spr.Get( "frame" ) )
   Print "x,y,w,h="+frame.GetInt( "x" )...etc...
Next
</pre><br><br>(IMO, frames should really be an array...)<br><br>There's an issue if the order of the frames is important, in which case brl.json would need to be modified, eg: a GetOrderedKeys:String[]() method could be added to JsonObject... <br><br></td></tr></table><br>
<a name="2072706"></a>

<a name="2072707"></a>

<a name="2072712"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah thats a lot nicer! Thanks Mark! And I do agree that Frames should be an array, but its not my format ;)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict

Import brl.json
Import mojo

Function Main:Int()
	New Game
	Return True
End

Class Game Extends App
	Field spriteList:List&lt;SpriteAtlas&gt; = New List&lt;SpriteAtlas&gt;

	Method OnCreate:Int()
		SetUpdateRate(60)
		
		Local startTime:Int = Millisecs()
		Local endTime:Int
		Local diff:Int
		
		Local str:String = LoadString("decor.json")
		Local jso:JsonObject = New JsonObject(str)
		Local sprs:JsonObject = JsonObject( jso.Get("frames") )
		For Local it:map.Node&lt;String, JsonValue&gt; = Eachin sprs.GetData()
			Local sa:SpriteAtlas = New SpriteAtlas()
			sa.name = it.Key

			Local spr:JsonObject = JsonObject( it.Value )
			Local frame:JsonObject = JsonObject( spr.Get( "frame" ) )
			sa.frameX = frame.GetInt("x")
			sa.frameY = frame.GetInt("y")
			sa.frameW = frame.GetInt("w")
			sa.frameH = frame.GetInt("h")
			
			Local rotated:JsonValue = spr.Get( "rotated" )
			sa.rotated = rotated.BoolValue()

			Local trimmed:JsonValue = spr.Get( "trimmed" )
			sa.trimmed = trimmed.BoolValue()

			Local spriteSourceSize:JsonObject = JsonObject( spr.Get( "spriteSourceSize" ) )
			sa.spriteSourceSizeX = spriteSourceSize.GetInt("x")
			sa.spriteSourceSizeY = spriteSourceSize.GetInt("y")
			sa.spriteSourceSizeW = spriteSourceSize.GetInt("w")
			sa.spriteSourceSizeH = spriteSourceSize.GetInt("h")

			Local sourceSize:JsonObject = JsonObject( spr.Get( "sourceSize" ) )
			sa.sourceSizeW = sourceSize.GetInt("w")
			sa.sourceSizeH = sourceSize.GetInt("h")

			spriteList.AddLast(sa)
		Next
		endTime = Millisecs()
		diff = endTime - startTime
		Print "--- " + diff + " ms ---"		
		Print "--- Finished Parsing ---"

		For Local sa:SpriteAtlas = Eachin spriteList
			Print "** name = " + sa.name + " **"
			Print "frameX = " + sa.frameX
			Print "frameY = " + sa.frameY
			Print "frameW = " + sa.frameW
			Print "frameH = " + sa.frameH
			If sa.rotated Then
				Print "rotated = true"
			Else
				Print "rotated = false"
			End
			If sa.trimmed Then
				Print "trimmed = true"
			Else
				Print "trimmed = false"
			End
			Print "spriteSourceSizeX = " + sa.spriteSourceSizeX
			Print "spriteSourceSizeY = " + sa.spriteSourceSizeY
			Print "spriteSourceSizeW = " + sa.spriteSourceSizeW
			Print "spriteSourceSizeH = " + sa.spriteSourceSizeH
			Print "sourceSizeW = " + sa.sourceSizeW
			Print "sourceSizeH = " + sa.sourceSizeH
		Next

		Return True
	End

	Method OnLoading:Int()
		Return True
	End Method

	Method OnUpdate:Int()
		Return True
	End Method
	
	Method OnRender:Int()
		Return True
	End Method

	Method OnSuspend:Int()
		Return True
	End Method

	Method OnResume:Int()
		Return True
	End Method
	
End

Class SpriteAtlas
	Field name:String
	Field frameX:Int
	Field frameY:Int
	Field frameW:Int
	Field frameH:Int
	Field rotated:Bool
	Field trimmed:Bool
	Field spriteSourceSizeX:Int
	Field spriteSourceSizeY:Int
	Field spriteSourceSizeW:Int
	Field spriteSourceSizeH:Int
	Field sourceSizeW:Int
	Field sourceSizeH:Int
End
</textarea><br><br>Oh, just found out the TexturePacker can output two types of JSON, one in Hash format (above) and Array based (below):<br><pre class=code>{"frames": [

{
	"filename": "axe.png",
	"frame": {"x":2,"y":364,"w":128,"h":126},
	"rotated": true,
	"trimmed": true,
	"spriteSourceSize": {"x":0,"y":1,"w":128,"h":126},
	"sourceSize": {"w":128,"h":128}
},
{
	"filename": "sword.png",
	"frame": {"x":117,"y":199,"w":126,"h":126},
	"rotated": false,
	"trimmed": true,
	"spriteSourceSize": {"x":1,"y":0,"w":126,"h":126},
	"sourceSize": {"w":128,"h":128}
}],
"meta": {
	"app": "http://www.codeandweb.com/texturepacker ",
	"version": "1.0",
	"image": "array.png",
	"format": "RGBA8888",
	"size": {"w":256,"h":542},
	"scale": "1",
	"smartupdate": "$TexturePacker:SmartUpdate:&lt;SNIP&gt;"
}
}</pre> <br><br></td></tr></table><br>
<a name="2072711"></a>

<a name="2072710"></a>

<a name="2072709"></a>

<a name="2072719"></a>

<a name="2072720"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Erik</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> You don't have to use JSON as an exporter from texturepacker, I prefer simple textfiles.<br><br>Put the following in a textfile and save under TexturePacker\bin\exporters\[yourname] to get a simple one line per image file.<br><br><pre class=code>{% for sprite in allSprites %}{{sprite.trimmedName}} {{sprite.frameRect.x}} {{sprite.frameRect.y}} {{sprite.frameRect.width}} {{sprite.frameRect.height}} {{sprite.cornerOffset.x}} {{sprite.cornerOffset.y}} {{sprite.untrimmedSize.width}} {{sprite.untrimmedSize.height}} {% if sprite.rotated %}1{% else %}0{% endif %}{% if not forloop.last %}
{% endif %}{% endfor %}</pre><br><br>Then copy exporter.xml from another exporter in \exporters\ to your dir and change the name etc in the xml file.<br>It should have the following values:<br>&lt;fileExtension&gt;txt&lt;/fileExtension&gt;<br>&lt;supportsTrimming&gt;yes&lt;/supportsTrimming&gt; <br>&lt;supportsRotation&gt;yes&lt;/supportsRotation&gt;<br>&lt;rotationDirection&gt;cw&lt;/rotationDirection&gt;<br>&lt;supportsNPOT&gt;no&lt;/supportsNPOT&gt;<br>&lt;supportsTrimSpriteNames&gt;yes&lt;/supportsTrimSpriteNames&gt;<br>&lt;supportsTextureSubPath&gt;yes&lt;/supportsTextureSubPath&gt;<br><br>Here's an image class that can load from the above exporter:<br><br><pre class=code>Class bbImage

  Field Name:String
  Field Width
  Field Height
  Field OffX
  Field OffY
  Field Rotated? 'If rotated in textureatlas to save space

  Field Img:Image

  Method Draw(atx#,aty#)
    If Rotated Then
      DrawImage(Img, atx+OffX, aty + OffY + Width, 90,1,1)
    Else
      DrawImage(Img, atx+OffX, aty+OffY)
    End
  End

  Function LoadFromTexturePacker:bbImage[](filename:String)
    Local Atlas := LoadImage(filename+".png")

    If Atlas = Null Then
      Error "Can't find ~q"+filename+"~q"
      Return
    End

    Local lines := app.LoadString(filename+".txt").Split("~n")[1..]
    Local result := New List&lt;bbImage&gt;()

    For Local i := Eachin lines
       Local c := i.Split(" ")

       Local bb := New bbImage()
       bb.Name = c[0]
       Local x := Int(c[1])
       Local y := Int(c[2])
       Local w := Int(c[3])
       Local h := Int(c[4])

       bb.Img = Atlas.GrabImage(x,y,w,h)

       bb.OffX   = Int(c[5])
       bb.OffY   = Int(c[6])
       bb.Width = Int(c[7])
       bb.Height = Int(c[8])
       bb.Rotated = Int(c[9]) = 1

       result.AddLast(bb)
    End

    Return result.ToArray()
  End

End
</pre> <br><br></td></tr></table><br>
<a name="2072718"></a>

<a name="2072717"></a>

<a name="2072721"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Erik, I was also using it to learn how to use the brl.json module :)<br><br>One thing with the JSON output from TexturePacker is that it doesn't tell you what rotation to do if the image has been rotated, which is weird... <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
