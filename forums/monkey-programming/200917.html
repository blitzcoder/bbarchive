<!DOCTYPE html><html lang="en" ><head ><title >DrawPoly</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >DrawPoly</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=507" >Monkey Programming</a>/<a href="#bottom" >DrawPoly</a><br><br>
<a name="2007669"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi<br>there are plans to add DrawPoly (filled) command to Monkey?<br>I think it will be handy. <br><br></td></tr></table><br>
<a name="2007792"></a>

<a name="2007793"></a>

<a name="2007794"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >invaderJim</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is the best I could do porting over some old code from the blitzmax forums.<br><br>You can either draw a poly by passing a list of vertices, or by specifying a number of sides and a radius. If you try the latter method, you will see that I'm having issues with spacing between the triangles. Fun fun...<br><br>example.monkey:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import mojo
Import drawpoly

Class DrawPolyTest Extends App
	Field polyAng:Float = 0.0
	
	Field poly1:List&lt;PolyVertex&gt; = New List&lt;PolyVertex&gt;()
	
	Method New()

	End
	
	Method OnCreate()
		SetUpdateRate(30)
		
		poly1.AddLast(New PolyVertex(50.0, 400.0))
		poly1.AddLast(New PolyVertex(100.0, 400.0))
		poly1.AddLast(New PolyVertex(100.0, 450.0))
		poly1.AddLast(New PolyVertex(75.0, 425.0))
		poly1.AddLast(New PolyVertex(50.0, 450.0))
	End
	
	Method OnUpdate()
		
	End
	
	Method OnRender()
		Cls 0, 0, 0
		
		DrawTriangle(100, 100, 100, 200, 200, 200, True, 255, 255, 255)
		
		' DrawPoly(centerX, centerY, numSides, radius, rotation, filled, r, g, b)
		DrawPoly(200, 200, 6, 70, polyAng, True, 0, 255, 0)
		
		DrawPoly(poly1, True)
		
		If Not KeyDown(KEY_SPACE) Then polyAng += 1		
	End
End

Function Main()
	New DrawPolyTest
End
</textarea><br><br>drawpoly.monkey:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import mojo.graphics

Class PolyVertex
	Field x:Float
	Field y:Float
	
	Method New(_x:Float, _y:Float)
		x = _x
		y = _y
	End
End

Class PolyTri
	Field p1:PolyVertex
	Field p2:PolyVertex
	Field p3:PolyVertex
	
	Method New(_p1:PolyVertex, _p2:PolyVertex, _p3:PolyVertex)
		p1 = _p1
		p2 = _p2
		p3 = _p3
	End
End

' ******* Re-write. Based on Axel Wheeler's post on: <a href="http://www.blitzbasic.com/Community/posts.php?topic=93985" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=93985</a>
Function DrawTriangle(x0:Float, y0:Float, x1:Float, y1:Float, x2:Float, y2:Float, filled:Bool = False, r:Int = 255, g:Int = 255, b:Int = 255)
	SetColor(r, g, b)
	
	If filled
	
		Local Ya:Float=Ceil(y0)
		Local Xa:Float=x0
		Local Yb:Float=Ceil(y1)
		Local Xb:Float=x1
		Local Yc:Float=Ceil(y2)
		Local Xc:Float=x2
		
		Local Xlong:Float
		Local Xshort:Float
		Local Xtemp:Float
		Local Ytemp:Float
		
		If Yb&lt;Ya
			Xtemp=Xa
			Ytemp=Ya
			Xa=Xb
			Ya=Yb
			Xb=Xtemp
			Yb=Ytemp
		EndIf
		
		If Yc&lt;Ya 
			Xtemp=Xa
			Ytemp=Ya
			Xa=Xc
			Ya=Yc
			Xc=Xtemp
			Yc=Ytemp
		EndIf
		
		If Yc&lt;Yb
			Xtemp=Xb
			Ytemp=Yb
			Xb=Xc
			Yb=Yc
			Xc=Xtemp
			Yc=Ytemp
		EndIf
		
		Local slopeAC:Float = 0.0, slopeAB:Float = 0.0, slopeBC:Float = 0.0
		
		If Yc &lt;&gt; Ya Then slopeAC = (Xc-Xa)/(Yc-Ya)
		If Yb &lt;&gt; Ya Then slopeAB = (Xb-Xa)/(Yb-Ya)
		If Yb &lt;&gt; Yc Then slopeBC = (Xc-Xb)/(Yc-Yb)
		
		For Local y:= Ya To Yc
			
			Xlong = Xa+((y-Ya)*slopeAC)
			
			If y &lt; Yb
				Xshort = Xa+((y-Ya)*slopeAB)
			Else
				Xshort = Xb+((y-Yb)*slopeBC)
			End
			
			DrawLine(Xlong,y,Xshort,y)
			
		Next
		
	Else
		DrawLine(x0,y0,x1,y1)
		DrawLine(x1,y1,x2,y2)
		DrawLine(x2,y2,x0,y0)
	End
End

Function DrawPoly(centerX:Int, centerY:Int, numSides:Int, radius:Float, rotation:Float = 0.0, filled:Bool = False, r:Int = 255, g:Int = 255, b:Int = 255)
	Local polyList:List&lt;PolyVertex&gt; = New List&lt;PolyVertex&gt;()
	Local angInc:Float = 360.0/numSides
	
	For Local i:= 1 To numSides
		Local x:Float = centerX + Sin(rotation)*radius
		Local y:Float = centerY - Cos(rotation)*radius
		polyList.AddLast(New PolyVertex(x, y))
		rotation += angInc
	Next
	
	DrawPoly(polyList, filled, r, g, b)
	
End

Function DrawPoly(polys:List&lt;PolyVertex&gt;, filled:Bool = False, r:Int = 255, g:Int = 255, b:Int = 255)
	If filled
		Local tris:List&lt;PolyTri&gt; = TriangulatePoly(polys)
		For Local tTri:= Eachin tris
			DrawTriangle(tTri.p1.x, tTri.p1.y, tTri.p2.x, tTri.p2.y, tTri.p3.x, tTri.p3.y, True, r, g, b)
		Next
	Else
		SetColor(r, g, b)
		Local ac:Int = polys.Count()
		Local count:= 0
		Local polyArray:PolyVertex[ac]
		
		For Local tVert:= Eachin polys
			polyArray[count] = tVert
			count += 1
		Next
		
	    For Local i:= 0 To ac-1
	        Local x1:= polyArray[i].x
	        Local y1:= polyArray[i].y
	        Local x2:= polyArray[(i+1) Mod ac].x
	        Local y2:= polyArray[(i+1) Mod ac].y
	        DrawLine(x1, y1, x2, y2)
	    Next i
	EndIf
End

' ******* Conversion from Warpy's post on: <a href="http://www.blitzbasic.com/Community/posts.php?topic=78165" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=78165</a>
Function TriangulatePoly:List&lt;PolyTri&gt;(polys:List&lt;PolyVertex&gt;)
	Local polyCopy:List&lt;PolyVertex&gt; = New List&lt;PolyVertex&gt;()
	
	Local c:= polys.Count()
	
	For Local tPoly:= Eachin polys
		polyCopy.AddLast(New PolyVertex(tPoly.x, tPoly.y))
	Next
	
	If c &lt; 3 Then Return Null
	
	Local l:List&lt;PolyTri&gt; = New List&lt;PolyTri&gt;() 'this list will store all our triangles
	While c&gt;3
		Local polyArray:PolyVertex[c]
		Local count:= 0
		For Local tp:= Eachin polyCopy
			polyArray[count] = tp
			count += 1
		Next
		
		Local i:= 0
		Local go:= 0
		
		Local p1:PolyVertex, p2:PolyVertex, p3:PolyVertex
		
		While Not go
			p1 = polyArray[i]
			p2 = polyArray[(i+1) Mod c]
			p3 = polyArray[(i+2) Mod c]
			
			'p1,p2,p3 are consecutive points on the boundary of the polygon.
			'consider the triangle p1-&gt;p2-&gt;p3
			
			Local midx:Float = (p1.x + p2.x + p3.x) / 3.0	'(midx,midy) is a point inside the candidate triangle
			Local midy:Float = (p1.y + p2.y + p3.y) / 3.0
			
			'here we check if (midx,midy) is inside the polygon. An 'S'-bend in the polygon can cause the candidate triangle
			'to actually be on the outside of the polygon, making it useless in a triangulation.
			'This check works by counting the number of times a horizontal ray originating from (midx,midy) crosses the boundary of the polygon
			'if hits is odd, then (midx,midy) is inside the polygon.
			Local hits:=0
			
			
			For Local ii:= 0 To c - 1
				Local x1:Float=polyArray[ii].x
				Local y1:Float=polyArray[ii].y
				Local x2:Float=polyArray[(ii+1) Mod c].x
				Local y2:Float=polyArray[(ii+1) Mod c].y
				If (y1-midy)*(y2-midy)&lt;0
					Local ix:Float = x1+(x2-x1)*(midy-y1)/(y2-y1)
					If ix&lt;midx hits += 1
				EndIf
			Next

			If (hits Mod 2) 'tri is inside polygon
			
				'We now know the triangle is inside the polygon, so the last thing we need to check is that the line p3-&gt;p1
				'doesn't cross the boundary at any point.
				
				Local x1:Float=p1.x
				Local y1:Float=p1.y
				Local x2:Float=p3.x
				Local y2:Float=p3.y
				Local dx1:Float=x2-x1
				Local dy1:Float=y2-y1
				
				go=1
				Local n:= (i+3) Mod c
				While n&lt;&gt;i
					Local x3:Float=polyArray[n].x
					Local y3:Float=polyArray[n].y
					Local dx2:Float=x3-x2
					Local dy2:Float=y3-y2
					
					If dx1&lt;&gt;dx2 Or x1&lt;&gt;x2 Or dy1&lt;&gt;dy2 Or y1&lt;&gt;y2
						Local lambda:Float=(y2-y1+dy2*(x1-x2)/dx2)/(dy1-dx1*dy2/dx2)
						Local mu:Float=(x1-x2+lambda*dx1)/dx2
						If lambda&gt;0 And lambda&lt;1
							If mu&gt;=0 And mu&lt;=1
								go=0
							EndIf
						EndIf
					EndIf
					x2=x3
					y2=y3
					n=(n+1) Mod c
				Wend
			EndIf
			
			If Not go 'if go=0, then our line crossed the boundary at some point, so this triangle isn't an ear.
				i=(i+1) Mod c
				If i=0 Return Null
			EndIf
		Wend


		
		'remove p2 from the list of points - this is the same as removing the whole ear from the polygon - now there is no way 
		'p1-&gt;p2-&gt;p3 will be considered again.
		polyCopy.Remove(p2)
		
		l.AddLast(New PolyTri(p1, p2, p3))
		
		c -= 1	'we've removed a point

	Wend
	
	'we're left with a single triangle, but it's not in our list of ears yet, so we need to add it
	Local polyArray:PolyVertex[c]
	Local count:= 0
	For Local tp:= Eachin polyCopy
		polyArray[count] = tp
		count += 1
	Next
	l.AddLast(New PolyTri(polyArray[0], polyArray[1], polyArray[2]))
	
	'done! return the list of triangles
	Return l
End
</textarea> <br><br></td></tr></table><br>
<a name="2007800"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi<br><br>Thanks for your contribution, but I think a 'native' solution should be better: I've already changed mojo-html5 to handle DrawPoly, and I'm sure it could be done in all the other platforms. <br><br></td></tr></table><br>
<a name="2007826"></a>

<a name="2007827"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >invaderJim</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah, I noticed you had done that shortly after posting :P<br><br>I wasn't aware that it was so easy to 'hack' mojo like that. Following your example, I've fixed my triangle drawing problem by natively drawing them in each platform.<br><br>I went with just triangles to reduce the amount of code required to triangulate the polygons properly in each platform, plus I really like warpy's triangulation method ;)<br><br>If you or anyone else is interested, I can post all of my DrawTriangle() functions here, but, for the most part, all I did was copy and modify the DrawRect() functions to draw one tri instead of two. <br><br></td></tr></table><br>
<a name="2007833"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well, the 'Monkey Code Section' is here to be filled!<br>Each new hack/function is welcomed!<br>(and maybe Mark could be impressed/interested/stimulated enough to port the changes as 'official' in mojo... <br><br></td></tr></table><br>
<a name="2017571"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >hardcoal</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Can you draw poly as lines only without fill? <br><br></td></tr></table><br>
<a name="2017580"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> No. It shouldn't be too hard for you too implement. <br><br></td></tr></table><br>
<a name="2017589"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >hardcoal</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> ok good to know.<br>tnx <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
