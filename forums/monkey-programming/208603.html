<!DOCTYPE html><html lang="en" ><head ><title >Platform Collision Fun</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Platform Collision Fun</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=507" >Monkey Programming</a>/<a href="#bottom" >Platform Collision Fun</a><br><br>
<a name="2088953"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >erebel55</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am working with therevill's platform code and am changing it for my needs.<br><br>I want to allow the player to jump through the bottom of tiles, which was easily accomplished by getting rid of some of the collision checks.  However, the next thing I want is turning out to be more difficult.<br><br>I only want to check for a collision on the outside of the end tiles of a platform.  Since the user can jump through the bottom of a platform I don't want to check for collision while the player is "inside" of the platform.<br><br>Here is a image of what I am trying to accomplish.  This image represents a platform composed of 3 tiles.<br>The red sections represent where I want to check for collision and stop the player from moving through.<br><img src="https://dl.dropboxusercontent.com/u/106998981/collision_image.png"><br><br>So the rules are..<br>1) Player walks on top of platform without falling through.<br>2) Player can jump through bottom of platform and reach the top.<br>3) If the player is jumping to the right he can't jump through the left side of the first tile in the platform.<br>4) If the player is jumping to the left he can't jump through the right side of the last tile in the platform.<br><br>Here is the modified code I am working with:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

import mojo

' Store the device width and height
Global SCREEN_WIDTH:Int
Global SCREEN_HEIGHT%

' Game Width and Height
Const WIDTH:Float = 320
Const HEIGHT#=240

Global game:MyGame

Function Main:int()
	game = new MyGame()
	return 0
End Function

Class MyGame extends App
	Field player:Player
	Field level:Level

	Field offsetX#, offsetY#
	Field scrollX#, scrollY#

	Field frameRate# = 200
	Field ms# = 0
	Field tmpMs#
	Field numTicks#
	Field lastNumTicks#
	Field maxMs% = 50
	Field lastTime#

	Method OnCreate:Int()

		SetUpdateRate 60
		
		' Store the device width and height
		SCREEN_WIDTH = DeviceWidth()
		SCREEN_HEIGHT = DeviceHeight()
		
		level = new Level()
		level.load()
		
		' timing
		player = new Player(level.playerX, level.playerY)
		ms = 1000 / frameRate
		numTicks = 0
		lastNumTicks = 1
		lastTime = Millisecs()
		
		return 0
	End Method
	
	Method OnUpdate:Int()
		local now% = Millisecs()
		
		If now &lt; lastTime
			numTicks = lastNumTicks
		else
			tmpMs = now - lastTime
			if tmpMs &gt; maxMs tmpMs = maxMs
			numTicks = tmpMs / ms
		Endif
	
		lastTime = now
		lastNumTicks = numTicks
	
		For local i% = 1 to Floor(numTicks)
			player.update()
		Next
		
		return 0
	End Method
	
	Method OnRender:Int()
		FPSCounter.update()
		Cls 
		PushMatrix 
		Scale SCREEN_WIDTH/WIDTH, SCREEN_HEIGHT/HEIGHT
		level.draw()
		player.draw(game.scrollX, game.scrollY)
		DrawText "Ticks:" + numTicks, 10, 10
		FPSCounter.draw(10, 20)
		PopMatrix
		return 0
	End Method	
end class

Class GameImage
	Field w%, h%
	Field w2%, h2%
End Class

Class Player
	Field x#, y#
	Field dx#, dy#
    Field speedX:Float, speedY:Float
	Field image:GameImage
	Field jumping:Bool
	
	Method New(x#, y#)
        self.x = x
		self.y = y
		image = new GameImage
		image.w = 16
		image.h = 16
		image.w2 = image.w / 2
		image.h2 = image.h / 2
        speedX = .2
        speedY = .5
	End Method
	
	Method draw:Void(offsetx# = 0, offsety# = 0)
        SetColor 0, 255, 0
        DrawOval x - image.w2 - offsetx, y - image.h2 - offsety, image.w, image.h
     
        ' DRAW DOTS TO DEBUG COLLISION
        ' debugging left collision
        SetColor 255, 0, 0
        DrawPoint x-speedX - image.w2- offsetx , y + image.h2 - 1
        DrawPoint x-speedX - image.w2- offsetx , y - image.h2
        DrawPoint x-speedX - image.w2- offsetx , y
        'SetColor 255, 255, 255
           
        ' debugging right collision
        DrawPoint x-speedX + image.w2- offsetx , y + image.h2 - 1
        DrawPoint x-speedX + image.w2- offsetx , y - image.h2
        DrawPoint x-speedX + image.w2- offsetx , y
        
        SetColor 0, 0, 255
        DrawPoint x - image.w2 + 10 - offsetx, y+dy + image.h2 - 1
        DrawPoint x + image.w2 - 10 - offsetx, y+dy + image.h2 - 1
        DrawPoint x - offsetx, y+dy + image.h2 - 1
                    
        SetColor 255, 255, 255
    End Method
	
	Method update:Void()
    
		if KeyDown(KEY_LEFT)
            Local tempx:Float = x - speedX
			if game.level.getCollisionTile(tempx - image.w2 , y + image.h2 - 1 ) = 0 and
				game.level.getCollisionTile(tempx - image.w2 , y - image.h2) = 0 and
				game.level.getCollisionTile(tempx - image.w2 , y) = 0 then
						
				x = tempx
			else
                x = round((x-image.w2)/Level.TILE_SIZE)*Level.TILE_SIZE + image.w2
			endif
		else if KeyDown(KEY_RIGHT)
			Local tempx# = x + speedX
			if game.level.getCollisionTile(tempx + image.w2 , y + image.h2 - 1 ) = 0 and
				game.level.getCollisionTile(tempx + image.w2 , y - image.h2) = 0 and
				game.level.getCollisionTile(tempx + image.w2 , y) = 0 then
				
				x = tempx
			else
				x = round((x+image.w2)/Level.TILE_SIZE)*Level.TILE_SIZE - image.w2
			endif
		End if
		
		If KeyDown(KEY_SPACE) And not jumping Then
			dy=-speedY
            jumping = true
		EndIf
		
        Local amount:Int = 10
        Local amountY:int = 1
        
		if not jumping
			if game.level.getCollisionTile(x - image.w2 + amount, y + image.h2) = 0 and 
				game.level.getCollisionTile(x + image.w2 - amount, y + image.h2) = 0 and 
				game.level.getCollisionTile(x, y + image.h2) = 0 then
				
				jumping = true
			endif
		endif
		
		if jumping
            dy += Level.GRAVITY
			
			local tempY# = y + dy
			if dy &lt;&gt; 0
				if dy &gt; 0
                    if game.level.getCollisionTile(x - image.w2 + amount, tempY + image.h2 - amountY) &gt; 0 or
                        game.level.getCollisionTile(x + image.w2 - amount, tempY + image.h2 -amountY) &gt; 0 or
                        game.level.getCollisionTile(x, tempY + image.h2 - amountY) &gt; 0 then
						
						dy = 0
						jumping = false
						y = round( (tempY + image.h2) / Level.TILE_SIZE ) * Level.TILE_SIZE - image.h2
					else
						y = tempY
					endif
                Else ' way up
                    #Rem
                    if game.level.getCollisionTile(x - image.w2 + amount, tempY - image.h2 ) &gt; 0 or
						game.level.getCollisionTile(x + image.w2 - amount, tempY - image.h2) &gt; 0 or
						game.level.getCollisionTile(x, tempY - image.h2 ) &gt; 0 then
						
						dy = 0
						y = round((tempY - image.h2) / Level.TILE_SIZE) * Level.TILE_SIZE + image.h2
						
                    Else
					#End
						y = tempY
					'endif
				endif
			endif
		endif
		
		Local border% = 200
		If x-game.scrollX &lt; border Then game.level.scrollHorizontal((x-game.scrollX)-border)
		If x-game.scrollX &gt; WIDTH-border Then game.level.scrollHorizontal((x-game.scrollX)-(WIDTH-border))
	End Method
	
End Class

Function round%(f#)
	Return Floor(f+0.5)
End Function

Class Level
	Const TILE_SIZE% = 16
	'Const GRAVITY# = 0.06
    Const GRAVITY:Float = 0.001
    
    Field map:Tile[][]
    Field width:Int, height:Int
	Field pixelWidth% = 0
	Field pixelHeight% = 0
	
	Field playerX%, playerY%
	
	' 32 across
	Global level1:=[
		0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,
		0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,124,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,
		0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,
		0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,124,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,
		0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,
		0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,124,0,0,0,0,0,0,0,0,0,0,0,0,0,0,
		0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,
		0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,
		0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,
		0,0,0,0,0,0,0,0,0,0,0,124,0,0,0,16,124,16,124,16,0,0,0,0,0,0,0,0,0,0,0,0,
		0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,
		0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,
		0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,
		116,116,116,116,116,116,116,116,116,116,116,116,116,116,116,116,116,116,116,116,116,116,116,116,116,116,116,116,116,116,116,116,
		115,115,115,115,115,115,115,115,115,115,115,115,115,115,115,115,115,115,115,115,115,115,115,115,115,115,115,115,115,115,115,115,
		0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0]

	Method load:Void()
		width = 32
		height = 16
		
		pixelWidth = width * TILE_SIZE
		pixelHeight = height * TILE_SIZE
		
		map = tileArray(width, height)
		
		For Local x:Int = 0 To width - 1
			For Local y:Int = 0 To height - 1
				map[x][y] = New Tile((level1 [ x + y * width ] ))
			Next
		Next
		
		playerX = 10
		playerY = 200
		
	End Method
	
	Method draw:Void()
		For Local x% = 0 To width - 1
			For Local y% = 0 To height - 1
				If map[x][y].frame &gt; 0
					SetColor map[x][y].red, map[x][y].green, map[x][y].blue
					DrawRect Int(x * TILE_SIZE - game.scrollX), Int(y * TILE_SIZE - game.scrollY), TILE_SIZE, TILE_SIZE
				endif
			Next
		Next
	End Method
	
	Method scrollHorizontal:Void(change#)
		game.scrollX+=change

		If game.scrollX &lt;0 Then
			game.scrollX = 0
		Else
			Local maxX% = pixelWidth - WIDTH
			If game.scrollX &gt; maxX Then game.scrollX = maxX
		EndIf
	End Method
	
	Method getCollisionTile:Int(x:Float, y:Float)
		If x &lt; 0 Or x &gt;= pixelWidth Or y &lt; 0 Or y &gt;= pixelHeight Then Return 0
		
		local xx% = (Floor(x / TILE_SIZE)) 
		local yy% = (Floor(y / TILE_SIZE))
		
		Return map[xx][yy].frame
	End Method
End Class

Function tileArray:Tile[][](x_size:Int, y_size:Int)
	Local map:Tile[x_size][]
	For Local x:Int=0 Until x_size
	   map[x]=New Tile[y_size]
	Next
	Return map
End Function

Class Tile
	Field frame:Int
	Field red%, green%, blue%	
	
	Method New(frame:Int=0)
		self.frame = frame
		red = 255 - frame
		green = 255 - frame
		blue = 255 - frame
	End
End

Class FPSCounter Abstract
	Global fpsCount:Int
	Global startTime:Int
	Global totalFPS:Int

	Function update:Void()
		If Millisecs() - startTime &gt;= 1000
			totalFPS = fpsCount
			fpsCount = 0
			startTime = Millisecs()
		Else
			fpsCount+=1
		EndIf
	End Function

	Function draw:Void(x% = 0, y% = 0, ax# = 0, ay# = 0)
		DrawText("FPS: " + totalFPS, x, y, ax, ay)
	End Function
End Class
</textarea><br><br>I was thinking about setting a flag for end tiles and then somehow only check to see if the player is hitting the outside x pixels of those.  However, I was hoping someone else would have a more clever solution.<br><br>Thanks! <br><br></td></tr></table><br>
<a name="2088952"></a>

<a name="2088942"></a>

<a name="2088941"></a>

<a name="2088946"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gerry Quinn</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> It might help to first check the player's vertical position relative to the platform.  If his feet are above the top of the platform, standard collisions apply.  If not, check is he to the right of the platform, and if so check for a collision with the right edge.  Same for the left. <br><br></td></tr></table><br>
<a name="2088947"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ElectricBoogaloo</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> A tip for doing "Jumping through the platform" stuff..<br>Instead of changing your entire collision method based upon whereabouts the player is, instead think about the direction the player's moving.  Essentially, test for collisions with the entire brick at all times, but only allow collision code to do anything when the player isn't actually moving upwards.<br><br>(Jay's silly hand test..  Hold out left hand, with right hand under it, both palm down.. bring up right hand, and note that the fingers curl down to allow the right hand to pass by the left hand..  next, bring the right hand back down, and note that the fingers don't bend, so the right hand slaps firmly against the left..   In both cases, the left hand collides with the right, but when moving up, the curling of the fingers allows passage, whilst on the way back down, there's a block.  You didn't magically change the left hand's parameters, or even the right hand's parameters, in order to collide or not.) <br><br></td></tr></table><br>
<a name="2088950"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >zoqfotpik</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> The easiest way to do platformer code is to treat it as special cases.  There isn't really an easy way to generalize it that I have found so just write it, make it perfect, encapsulate it and forget about it.  For this there would be special cases based on whether the player is jumping or not.  It might even be as simple as not checking for collisions when the player is moving upward.  Then when he ceases to move upward, check for a tile collision and if he's inside a tile bump him toward the edge that is closest to his center.<br><br>Also for general interest there is a good article on platformer physics here:<br><a href="http://higherorderfun.com/blog/2012/05/20/the-guide-to-implementing-2d-platformers/" target="_blank">http://higherorderfun.com/blog/2012/05/20/the-guide-to-implementing-2d-platformers/</a> <br><br></td></tr></table><br>
<a name="2088949"></a>

<a name="2088948"></a>

<a name="2088951"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >erebel55</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Gerry, right that is kind of how it is working right now. I check 3 positions on the bottom of the player to see if he is above a platform tile.<br><br>I also check 3 positions on the left side of the player when he is moving left and 3 positions on the right side of the player when he is moving right.<br>I am rendering dots at these positions to make it more obvious what is going on. <br><br></td></tr></table><br>
<a name="2088954"></a>

<a name="2088955"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >zoqfotpik</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> You could check the appropriate two corner pixels of a tile and see if either of them are inside the player boundingbox.  Then count the horizontal neighbors of that tile and if there are two, disregard the collision. <br><br></td></tr></table><br>
<a name="2088993"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >erebel55</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> It looks like I can't get my head around writing that for some reason today :(<br>It sounds simple how you explained it zoqfotpik. I'm just having problems integrating that into the current collision code.<br>Will try to figure it out tomorrow. <br><br></td></tr></table><br>
<a name="2088992"></a>

<a name="2089032"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >erebel55</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here is my first attempt. I haven't added any safeties to make sure I'm not checking for neighbors that are outside of the level array yet.<br>Also, my code is pretty messy/repetitive/inefficient within the player update function. Specifically within the if clause at lines 150 and 229.  If anyone has any ideas please share or feel free to touch up the code :)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

import mojo

' Store the device width and height
Global SCREEN_WIDTH:Int
Global SCREEN_HEIGHT:Int
Const WIDTH:Float = 320
Const HEIGHT:Float = 240

Global game:MyGame

Function Main:int()
    game = new MyGame()
    return 0
End Function

Class MyGame extends App
    Field player:Player
    Field level:Level

    Field frameRate:Float = 200
    Field ms:Float = 0
    Field tmpMs:Float
    Field numTicks:Float
    Field lastNumTicks:Float
    Field maxMs:Int = 50
    Field lastTime:Float

    Method OnCreate:Int()

        SetUpdateRate 60
        
		' Store the device width and height
        SCREEN_WIDTH = DeviceWidth()
        SCREEN_HEIGHT = DeviceHeight()
        
        level = new Level()
        level.load()
        
		' timing
        player = new Player(level.playerX, level.playerY)
        ms = 1000 / frameRate
        numTicks = 0
        lastNumTicks = 1
        lastTime = Millisecs()
        
        return 0
    End Method
    
    Method OnUpdate:Int()
        local now:Int = Millisecs()
        
        If now &lt; lastTime
            numTicks = lastNumTicks
        else
            tmpMs = now - lastTime
            if tmpMs &gt; maxMs tmpMs = maxMs
            numTicks = tmpMs / ms
        Endif
    
        lastTime = now
        lastNumTicks = numTicks
    
        For local i:Int = 1 to Floor(numTicks)
            player.update()
        Next
        
        return 0
    End Method
    
    Method OnRender:Int()
        FPSCounter.update()
        Cls
        PushMatrix
        Scale SCREEN_WIDTH/WIDTH, SCREEN_HEIGHT/HEIGHT
        level.draw()
        player.draw()
        DrawText "Ticks:" + numTicks, 10, 10
        FPSCounter.draw(10, 20)
        PopMatrix
        return 0
    End Method
end class

Class GameImage
    Field w:Int, h:Int
    Field w2:Int, h2:Int
End Class

Class Player
    Field x:Float, y:Float
    Field dx:Float, dy:Float
    Field speedX:Float, speedY:Float
    Field image:GameImage
    Field jumping:Bool
    
    
    Method New(x:Float, y:Float)
        self.x = x
        self.y = y
        image = new GameImage
        image.w = 16
        image.h = 16
        image.w2 = image.w / 2
        image.h2 = image.h / 2
        speedX = .5 '.2
        speedY = 2 '.5
    End Method
    
    Method draw:Void()
        SetColor 0, 255, 0
        DrawOval x - image.w2, y - image.h2, image.w, image.h
     
        ' DRAW DOTS TO DEBUG COLLISION
        ' debugging left collision
        SetColor 255, 0, 0
        DrawPoint x-speedX - image.w2 , y + image.h2 - 1
        DrawPoint x-speedX - image.w2 , y - image.h2
        DrawPoint x-speedX - image.w2 , y
        'SetColor 255, 255, 255
           
        ' debugging right collision
        DrawPoint x-speedX + image.w2 , y + image.h2 - 1
        DrawPoint x-speedX + image.w2 , y - image.h2
        DrawPoint x-speedX + image.w2 , y
        
        SetColor 0, 0, 255
        DrawPoint x - image.w2 + 10, y+dy + image.h2 - 1
        DrawPoint x + image.w2 - 10, y+dy + image.h2 - 1
        DrawPoint x, y+dy + image.h2 - 1
                    
        SetColor 255, 255, 255
    End Method
    
    Method update:Void()
    
        if KeyDown(KEY_LEFT)
            Local tempx:Float = x - speedX
            Local collisionPoints:Int[3] ' holds tile indexes that we are colliding with at points a, b, and c
            collisionPoints[0] = game.level.getCollisionTile(tempx - image.w2 , y + image.h2 - 1 ) ' point a
            collisionPoints[1] = game.level.getCollisionTile(tempx - image.w2 , y - image.h2) ' point b
            collisionPoints[2] = game.level.getCollisionTile(tempx - image.w2 , y) ' point c
            If collisionPoints[0] = 0 And
                collisionPoints[1] = 0 And
                collisionPoints[2] = 0 Then
                        
                x = tempx
            Else
                If jumping = True
                    ' i've already done these calculations in getCollisionTile, so this can be optimized!!!!!!
                    ' point a
                    Local aX:Int = Floor((tempx - image.w2) / Level.TILE_SIZE)
                    Local aY:Int = Floor((y + image.h2 - 1) / Level.TILE_SIZE)
                    ' point b
                    'Local bX:Int = Floor((tempx + image.w2) / TILE_SIZE) ' same as aX
                    Local bY:Int = Floor((y - image.h2) / Level.TILE_SIZE)
                    ' point c
                    'Local cX:Int = Floor((tempx + image.w2) / TILE_SIZE) ' same as aX
                    Local cY:Int = Floor(y / Level.TILE_SIZE)
                    
                    ' we already know at least one of the points a/b/c is colliding with a tile other than 0
                    If collisionPoints[0] = 1 ' point a is colliding with a platform tile
                        If (game.level.map[aX - 1][aY].frame = 1 And game.level.map[aX + 1][aY].frame = 0) Or ' this is a last end tile
                            (game.level.map[aX - 1][aY].frame = 0 And game.level.map[aX + 1][aY].frame = 0) ' single tile plat
                            ' only collide if player is left of the tile
                            If tempx &gt;= (aX*Level.TILE_SIZE)+Level.TILE_SIZE ' top right corner of tile
                                ' collision
                                x = round((x-image.w2)/Level.TILE_SIZE)*Level.TILE_SIZE + image.w2
                            Else
                                ' move freely
                                x = tempx
                            Endif
                        Else
                            ' move freely
                            x = tempx
                        Endif
                    Elseif collisionPoints[1] = 1 ' point b is colliding with a platform tile
                        If (game.level.map[aX - 1][bY].frame = 1 And game.level.map[aX + 1][bY].frame = 0) Or ' this is a last end tile
                            (game.level.map[aX - 1][bY].frame = 0 And game.level.map[aX + 1][bY].frame = 0) ' single tile plat
                            ' only collide if player is left of the tile
                            If tempx &gt;= (aX*Level.TILE_SIZE)+Level.TILE_SIZE ' top right corner of tile
                                ' collision
                                x = round((x-image.w2)/Level.TILE_SIZE)*Level.TILE_SIZE + image.w2
                            Else
                                ' move freely
                                x = tempx
                            Endif
                        Else
                            ' move freely
                            x = tempx
                        Endif
                    Elseif collisionPoints[2] = 1 ' point c is colliding with a platform tile
                        If (game.level.map[aX - 1][cY].frame = 1 And game.level.map[aX + 1][cY].frame = 0) Or ' this is a last end tile
                            (game.level.map[aX - 1][cY].frame = 0 And game.level.map[aX + 1][cY].frame = 0) ' single tile plat
                            ' only collide if player is left of the tile
                            If tempx &gt;= (aX*Level.TILE_SIZE)+Level.TILE_SIZE ' top right corner of tile
                                ' collision
                                x = round((x-image.w2)/Level.TILE_SIZE)*Level.TILE_SIZE + image.w2
                            Else
                                ' move freely
                                x = tempx
                            Endif
                        Else
                            ' move freely
                            x = tempx
                        Endif
                    Else ' we are colliding with a tile other than 0 and 1
                        ' collision
                        x = round((x-image.w2)/Level.TILE_SIZE)*Level.TILE_SIZE + image.w2
                    Endif
                Else ' not jumping
                    ' collision
                    x = round((x-image.w2)/Level.TILE_SIZE)*Level.TILE_SIZE + image.w2
                Endif
            endif
        else if KeyDown(KEY_RIGHT)
            Local tempx:Float = x + speedX
            Local collisionPoints:Int[3] ' holds tile indexes that we are colliding with at points a, b, and c
            collisionPoints[0] = game.level.getCollisionTile(tempx + image.w2 , y + image.h2 - 1 )
            collisionPoints[1] = game.level.getCollisionTile(tempx + image.w2 , y - image.h2 )
            collisionPoints[2] = game.level.getCollisionTile(tempx + image.w2 , y)
            if collisionPoints[0] = 0 and
                collisionPoints[1] = 0 and
                collisionPoints[2] = 0 then
                
                x = tempx
            Else
                If jumping = True
                    ' i've already done these calculations in getCollisionTile, so this can be optimized!!!!!!
                    ' point a
                    Local aX:Int = Floor((tempx + image.w2) / Level.TILE_SIZE)
                    Local aY:Int = Floor((y + image.h2 - 1) / Level.TILE_SIZE)
                    ' point b
                    'Local bX:Int = Floor((tempx + image.w2) / TILE_SIZE) ' same as aX
                    Local bY:Int = Floor((y - image.h2) / Level.TILE_SIZE)
                    ' point c
                    'Local cX:Int = Floor((tempx + image.w2) / TILE_SIZE) ' same as aX
                    Local cY:Int = Floor(y / Level.TILE_SIZE)
                    
                    ' we already know at least one of the points a/b/c is colliding with a tile other than 0
                    If collisionPoints[0] = 1 ' point a is colliding with a platform tile
                        If (game.level.map[aX - 1][aY].frame = 0 And game.level.map[aX + 1][aY].frame = 1) Or ' this is the first end tile
                            (game.level.map[aX - 1][aY].frame = 0 And game.level.map[aX + 1][aY].frame = 0) ' single tile plat
                            ' only collide if player is left of the tile
                            If tempx &lt;= (aX*Level.TILE_SIZE) ' top left corner of tile
                                ' collision
                                x = round((x+image.w2)/Level.TILE_SIZE)*Level.TILE_SIZE - image.w2
                            Else
                                ' move freely
                                x = tempx
                            Endif
                        Else
                            ' move freely
                            x = tempx
                        Endif
                    Elseif collisionPoints[1] = 1 ' point b is colliding with a platform tile
                        If (game.level.map[aX - 1][bY].frame = 0 And game.level.map[aX + 1][bY].frame = 1) Or ' this is the first end tile
                            (game.level.map[aX - 1][bY].frame = 0 And game.level.map[aX + 1][bY].frame = 0) ' single tile plat
                            ' only collide if player is left of the tile
                            If tempx &lt;= (aX*Level.TILE_SIZE) ' top left corner of tile
                                ' collision
                                x = round((x+image.w2)/Level.TILE_SIZE)*Level.TILE_SIZE - image.w2
                            Else
                                ' move freely
                                x = tempx
                            Endif
                        Else
                            ' move freely
                            x = tempx
                        Endif
                    Elseif collisionPoints[2] = 1 ' point c is colliding with a platform tile
                        If (game.level.map[aX - 1][cY].frame = 0 And game.level.map[aX + 1][cY].frame = 1) Or ' this is the first end tile
                            (game.level.map[aX - 1][cY].frame = 0 And game.level.map[aX + 1][cY].frame = 0) ' single tile plat
                            ' only collide if player is left of the tile
                            If tempx &lt;= (aX*Level.TILE_SIZE) ' top left corner of tile
                                ' collision
                                x = round((x+image.w2)/Level.TILE_SIZE)*Level.TILE_SIZE - image.w2
                            Else
                                ' move freely
                                x = tempx
                            Endif
                        Else
                            ' move freely
                            x = tempx
                        Endif
                    Else ' we are colliding with a tile other than 0 and 1
                        ' collision
                        x = round((x+image.w2)/Level.TILE_SIZE)*Level.TILE_SIZE - image.w2
                    Endif
                Else ' not jumping
                    ' collision
                    x = round((x+image.w2)/Level.TILE_SIZE)*Level.TILE_SIZE - image.w2
                Endif
            endif
        End if
        
        If KeyDown(KEY_SPACE) And not jumping Then
            dy = -speedY
            jumping = true
        EndIf
        
        Local amount:Int = 10
        Local amountY:int = 1
        
        if not jumping
            if game.level.getCollisionTile(x - image.w2 + amount, y + image.h2) = 0 and
                game.level.getCollisionTile(x + image.w2 - amount, y + image.h2) = 0 and
                game.level.getCollisionTile(x, y + image.h2) = 0 then
                
                jumping = true
            endif
        endif
        
        if jumping
            dy += Level.GRAVITY
            
            local tempY:Float = y + dy
            if dy &lt;&gt; 0
                if dy &gt; 0
                    if game.level.getCollisionTile(x - image.w2 + amount, tempY + image.h2 - amountY) &gt; 0 or
                        game.level.getCollisionTile(x + image.w2 - amount, tempY + image.h2 -amountY) &gt; 0 or
                        game.level.getCollisionTile(x, tempY + image.h2 - amountY) &gt; 0 then
                        
                        dy = 0
                        jumping = false
                        y = round( (tempY + image.h2) / Level.TILE_SIZE ) * Level.TILE_SIZE - image.h2
                    else
                        y = tempY
                    endif
                Else ' way up
                    #Rem
                    if game.level.getCollisionTile(x - image.w2 + amount, tempY - image.h2 ) &gt; 0 or
                        game.level.getCollisionTile(x + image.w2 - amount, tempY - image.h2) &gt; 0 or
                        game.level.getCollisionTile(x, tempY - image.h2 ) &gt; 0 then
                        
                        dy = 0
                        y = round((tempY - image.h2) / Level.TILE_SIZE) * Level.TILE_SIZE + image.h2
                        
                    Else
					#End
                        y = tempY
					'endif
                endif
            endif
        endif
    End Method
    
End Class

Function round:Int(f:Float)
    Return Floor(f+0.5)
End Function

Class Level
    Const TILE_SIZE:Int = 16
    'Const GRAVITY:Float = 0.06
    Const GRAVITY:Float = 0.02
    
    Field map:Tile[][]
    Field width:Int, height:Int
    Field pixelWidth:Int = 0
    Field pixelHeight:Int = 0
    
    Field playerX:Int, playerY:Int
    
	' 20 across
    Global level1 := [
        0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,
        0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,
        0, 0, 0, 0, 0, 0, 1, 1, 1, 1, 0, 0, 0, 1, 1, 1, 0, 1, 1, 0,
        0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,
        0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,
        0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 1, 1, 0, 0, 0, 0,
        0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,
        0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0,
        0, 0, 0, 1, 1, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,
        0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,
        0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 1, 1, 1, 0, 0, 0,
        0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,
        0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,
        1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1,
        1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1,
        0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]

    Method load:Void()
        width = 20
        height = 16
        
        pixelWidth = width * TILE_SIZE
        pixelHeight = height * TILE_SIZE
        
        map = tileArray(width, height)
        
        For Local x:Int = 0 To width - 1
            For Local y:Int = 0 To height - 1
                map[x][y] = New Tile((level1 [ x + y * width ] ))
            Next
        Next
        
        playerX = 10
        playerY = 200
        
    End Method
    
    Method draw:Void()
        For Local x:Int = 0 To width - 1
            For Local y:Int = 0 To height - 1
                If map[x][y].frame &gt; 0
                    SetColor map[x][y].red, map[x][y].green, map[x][y].blue
                    DrawRect Int(x * TILE_SIZE), Int(y * TILE_SIZE), TILE_SIZE, TILE_SIZE
                endif
            Next
        Next
    End Method
    
    Method getCollisionTile:Int(x:Float, y:Float)
        If x &lt; 0 Or x &gt;= pixelWidth Or y &lt; 0 Or y &gt;= pixelHeight Then Return 0
        
        local xx:Int = (Floor(x / TILE_SIZE))
        Local yy:Int = (Floor(y / TILE_SIZE))
        
        Return map[xx][yy].frame
    End Method
End Class

Function tileArray:Tile[][](x_size:Int, y_size:Int)
    Local map:Tile[x_size][]
    For Local x:Int = 0 Until x_size
       map[x] = New Tile[y_size]
    Next
    Return map
End Function

Class Tile
    Field frame:Int
    Field red:Int, green:Int, blue:Int
    
    Method New(frame:Int = 0)
        self.frame = frame
        red = 255 - frame*100
        green = 255 - frame*100
        blue = 255 - frame*100
    End
End

Class FPSCounter Abstract
    Global fpsCount:Int
    Global startTime:Int
    Global totalFPS:Int

    Function update:Void()
        If Millisecs() - startTime &gt;= 1000
            totalFPS = fpsCount
            fpsCount = 0
            startTime = Millisecs()
        Else
            fpsCount += 1
        EndIf
    End Function

    Function draw:Void(x:Int = 0, y:Int = 0, ax:Float = 0, ay:Float = 0)
        DrawText("FPS: " + totalFPS, x, y, ax, ay)
    End Function
End Class
</textarea> <br><br></td></tr></table><br>
<a name="2089025"></a>

<a name="2089024"></a>

<a name="2089023"></a>

<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
