<!DOCTYPE html><html lang="en" ><head ><title >JungleGUI need some help peeps...</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >JungleGUI need some help peeps...</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=507" >Monkey Programming</a>/<a href="#bottom" >JungleGUI need some help peeps...</a><br><br>
<a name="2125976"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Paul - Taiphoz</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Right I'm pulling this out of the module forum cos I dont seem to be getting any kind of response. my hope now is that some one other than the mod author will have or know the solution..<br><br>Code Bellow should I hope preset the issue. but the cruks of it is that when I create a TextField and asing it a starting text value it works, but later when I try and set it a new text value via code not via user input it simply refuses to render the new text.<br><br>Another odd thing is that I even tried deleting the object and creating a new instance with the text i want which funnily didn't work either it was clearly still rendering the old one under the new one.<br><br>So background on this is that its settings for an app, when I make the form I make the fields , sliders and textfields with the values from the config file, but I need to be able to update these values via code as well as user input.<br><br>So pressing F2 has no visible effect.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import junglegui
Import junglegui.renderers.concretejungle
Import junglegui.renderers.roundforms

'It's important to add this file to the reflection filter if we want it to be able to process event handlers
#REFLECTION_FILTER+="${MODPATH}"

#GLFW_WINDOW_RESIZABLE=True
#GLFW_WINDOW_TITTLE="Jungle Gui Sample"
Global jgui:Gui

'Start the application
Function Main()
	New Sample  
End


Global OptionsForm:cOptionsGUI

Class Sample Extends App
	Field jgui:Gui

	Method OnCreate()
		SetUpdateRate(60)
		jgui = New Gui	'We create the Gui manager.
		OptionsForm = New cOptionsGUI
		try
			OptionsForm.InitForm(jgui)
		Catch jge:JungleGuiException
			Print "Form could not be initialized becouse of an exception:"
			Print jge.ToString()
		End
	End
	
	Method OnUpdate()
		Try
			jgui.Update()
		Catch jge:JungleGuiException
			Print "Error updating the Gui component:"
			Print jge.ToString()
			Error(jge.ToString())
		End
		
		If KeyHit(KEY_F1)
			Select OptionsForm.Visible
				Case True
					OptionsForm.Visible(False)
				Case False
					OptionsForm.Visible(True)
			End Select
		EndIf
		
		If KeyHit(KEY_F2)
			OptionsForm.homeSystemsBox.Text="This"
                        Print "Options Form Text = "+OptionsForm.homeSystemsBox.Text
		Endif
	End
	
	Method OnLoading()
	End
	
	Method OnRender()
		Cls
		
		Try
			jgui.Render()
		Catch jge:JungleGuiException
			Print "Error rendering the Gui component:"
			Print jge.ToString()
			Error(jge.ToString())
		end	
	End
End







Class cOptionsGUI Extends Form

	'Field panel:junglegui.Panel
	
	Field Volume_Status_Current:junglegui.Slider
	Field Volume_Status_Other:junglegui.Slider
	Field Volume_Home:junglegui.Slider
	Field Volume_Watch:junglegui.Slider
	Field Volume_Ping:junglegui.Slider
	Field Volume_Current:junglegui.Slider
	
	Field volumePanel:junglegui.Panel
	Field toonPanel:junglegui.Panel
	Field apiPanel:junglegui.Panel

	'character input
	Field characterBox:TextField
	Field intelChanBox:TextField
	Field homeSystemsBox:TextField
	Field watchSystemsBox:TextField
	Field regionBox:TextField
	Field themeBox:TextField
	
	Field FirstRun:Bool
	Field Changed:Bool
	
	Method OnInit()
		'''
		''' MyForm
		'''
		Self.FirstRun = True
		Self.Changed = False
		
		Self.Name = "Options"
		Self.Text = "Options"
		Self.Size.SetValues(520, 380)
		Self.Position.SetValues(20, 20)
		Self.BackgroundColor.SetColor(.5, 204, 204, 204)
		'Self.Visible = False
		
		
		
		'
		'Volume 
		'
		
		Local vol_label:= New junglegui.Label
		vol_label.Parent = Self
		vol_label.Text = "Sound Options"
		vol_label.Position.SetValues(5, 10)
				
		Self.volumePanel = New junglegui.Panel
		volumePanel.Parent = Self
		volumePanel.Position.SetValues(5, 30)
		volumePanel.Size.SetValues(240, 245)
		volumePanel.BorderColor = SystemColors.FormBorder.Clone()
		volumePanel.BackgroundColor.SetColor(1, 214, 214, 214)
		
		Volume_Status_Current = New junglegui.Slider
		SetSliderValues(Volume_Status_Current, 0, "Status Current", 1 * 100)
		Volume_Status_Other = New junglegui.Slider
		SetSliderValues(Volume_Status_Other, 1, "Status Other", 1 * 100)
		Volume_Current = New junglegui.Slider
		SetSliderValues(Volume_Current, 2, "Current", 1 * 100)
		Volume_Home = New junglegui.Slider
		SetSliderValues(Volume_Home, 3, "Home", 1 * 100)
		Volume_Watch = New junglegui.Slider
		SetSliderValues(Volume_Watch, 4, "Watch", 1 * 100)
		Volume_Ping = New junglegui.Slider
		SetSliderValues(Volume_Ping, 5, "Ping", 1 * 100)
		
		
		'
		' Character and stuff. 
		'	
	

		Local tempBoxLabel:= New junglegui.Label
		tempBoxLabel.Parent = Self
		tempBoxLabel.Position.SetValues(260, 10)
		tempBoxLabel.Text = "Intel options"
		tempBoxLabel.Name = "inteloptions"
		
		Local IntelPanel:= New junglegui.Panel
		IntelPanel.Parent = Self
		IntelPanel.Position.SetValues(260, 30)
		IntelPanel.Size.SetValues(240, 245)
		IntelPanel.BorderColor = SystemColors.FormBorder.Clone()
		IntelPanel.BackgroundColor.SetColor(1, 214, 214, 214)
		
		
		characterBox = New junglegui.TextField
		SetBoxValues(characterBox, 0, "Character", "Player", 265)
		
		intelChanBox = New junglegui.TextField
		SetBoxValues(intelChanBox, 1, "Intel", "chan1,chan2", 265)
		
		homeSystemsBox = New junglegui.TextField
		SetBoxValues(homeSystemsBox, 2, "Home", "", 265)
		
		watchSystemsBox = New junglegui.TextField
		SetBoxValues(watchSystemsBox, 3, "Watch", "", 265)
		
		regionBox = New junglegui.TextField
		SetBoxValues(regionBox, 4, "Region", "TestRegion", 265)
				
		themeBox = New junglegui.TextField
		SetBoxValues(themeBox, 5, "Theme", "TestTheme", 265)
		
		FirstRun = False
		
	End

	Method SetTextFieldText:Void(box:junglegui.TextField,txt:String)
		box.Text=txt
	End Method
	
	Method GetNextHeight(control:Control)
		Const Margin:Int = 5
		Return control.Position.Y + control.Size.Y + Margin
	End
	
	Method GetVisible:Bool()
		'If Self.Position.GetValue
		Return Self.Visible
	End Method
	
	Method Hide:Void()
		Self.Visible(False)
		If Self.Changed = True
			'Note: Options GUI changed, save to Options Object and Update the readers.
			'Account.UpdateOptionsFromGUI()
			Self.Changed = False
		EndIf
	End Method
	
	Method Show:Void()
		Self.Changed = False
		Self.Visible(True)
		'Account.FillOptionsGUI()
		
	End Method
	
	Method SetBoxValues(box:junglegui.TextField, Index:Int, name:String, value:String[], _x:Int = 10, _y:Int = 10)
		
		box = New junglegui.TextField
		box.Parent = Self
		box.Position.SetValues(_x, _y + 28 + Index * 30)
		box.Size.SetValues(220, 15)
		box.AutoAdjustSize = False
		box.Name = name
		box.Event_TextModified.Add(Self, "Box_Value_Changed")
		
		Local st:String
		For Local i:Int = 0 To value.Length - 1
			st += value[i] + ","
		Next
		box.Text = st
		
		Local label:= New junglegui.Label
		label.Parent = self
		label.Text = name
		label.Position.SetValues(_x, _y + Index * 25)
		label.Position.Y = box.Position.Y - (label.Size.Y - box.Size.Y) / 2 - 13
		
	End Method
	
	Method SetBoxText:Void(box:junglegui.TextField, value:String[])
		Local st:String
		For Local i:Int = 0 To value.Length - 1
			st += value[i] + ","
		Next
		
		Select box
			Case Self.homeSystemsBox
				Print "Changing " + Self.homeSystemsBox.Name + " Value From " + Self.homeSystemsBox.Text + " to " + st
				Self.homeSystemsBox.Text = st
				Self.homeSystemsBox.Event_TextModified.Add(Self, "Box_Value_Changed")
			Case Self.watchSystemsBox
				Print "watch"
				Self.watchSystemsBox.Text = st
				Self.watchSystemsBox.Event_TextModified.Add(Self, "Box_Value_Changed")
			Case Self.characterBox
				Print "char"
				Self.watchSystemsBox.Text = st
				Self.watchSystemsBox.Event_TextModified.Add(Self, "Box_Value_Changed")
			Case Self.intelChanBox
				Print "intel"
				Self.watchSystemsBox.Text = st
				Self.watchSystemsBox.Event_TextModified.Add(Self, "Box_Value_Changed")
		End Select

		

	End Method

	Method SetBoxText:Void(box:junglegui.TextField, value:String)
		Select box
			Case Self.themeBox
				Print "theme"
				Self.themeBox.Text = value
				Self.themeBox.Event_TextModified.Add(Self, "Box_Value_Changed")
			Case Self.regionBox
				Print "Region"
				Self.regionBox.Text = value
				Self.regionBox.Event_TextModified.Add(Self, "Box_Value_Changed")
		End Select		
	End Method
		
		
	Method SetBoxValues(box:junglegui.TextField, Index:Int, name:String, value:String, _x:Int = 10, _y:Int = 10)
		
		box = New junglegui.TextField
		box.Parent = Self
		box.Position.SetValues(_x, _y + 40 + Index * 40)
		box.Size.SetValues(220, 19)
		box.AutoAdjustSize = False
		box.Name = name
		box.Text = value
		'box.Event_TextModified.Add(Self, "Box_Value_Changed")
		
		Local label:= New junglegui.Label
		label.Parent = self
		label.Text = name
		label.Position.SetValues(_x, _y + Index * 20)
		label.Position.Y = box.Position.Y-5 - (label.Size.Y - box.Size.Y) / 2 - 13
		
	End Method	
	
	Method SetSliderValues(slider:junglegui.Slider, Index:Int, name:String, initValue:Int = 100, _x:Int = 10, _y:Int = 10)

		slider.Position.SetValues(_x, _y + 40 + Index * 40)
		slider.Parent = Self
		slider.Name = name
		slider.Maximum = 100.0
		slider.Size.SetValues(230, 19)
		slider.Maximum = 100.0
		slider.Value = initValue
		slider.Event_ValueChanged.Add(Self, "Slider_Value_Changed")
		slider.BackgroundColor.SetColor(1, 214, 214, 214)
				
		Local label:= New junglegui.Label
		label.Parent = self
		label.Text = name
		label.Position.SetValues(_x, _y + Index * 20)
		label.Position.Y = slider.Position.Y-5 - (label.Size.Y - slider.Size.Y) / 2 - 13
		
	End

	Method Box_Value_Changed(sender:Object, e:EventArgs)
		Print "Box Value Changed"
		Local box:= junglegui.TextField(sender)
		If box = Null Then Return
		If FirstRun Then Return
		'Print "Performing Edit"
		Select box.Name
			Case "Character"
				'Print "On Character Box"
				Self.Changed = True
			Case "Intel"
				Self.Changed = True
			Case "Home"
				Print "Home Got Changed"
				Self.Changed = True
			Case "Watch"
				Self.Changed = True
			Case "Region"
				Self.Changed = True
			Case "Theme"
				Self.Changed = True
		End Select
	End Method
	
	Method Slider_Value_Changed(sender:Object, e:EventArgs)
		Local slider:= junglegui.Slider(sender)
		if slider = null Then Return
		If Self.FirstRun Then Return
		
		Select slider
			Case Volume_Status_Current
				'Print "volume " + Self.Volume_Status_Current.Value
				'Print "ass float" + Float(Self.Volume_Status_Current.Value)
				'Print "by 100 " + Float(Self.Volume_Status_Current.Value / 100)
				'This works. 'Print "by 100 as float " + Float(Float(Self.Volume_Status_Current.Value) / 100)
				'Options.Volume_Status_Current = Float(Float(Self.Volume_Status_Current.Value) / 100)
			Case Volume_Status_Other
				'Options.Volume_Status_Other = Float(Float(Self.Volume_Status_Other.Value) / 100)
			Case Volume_Home
				'Options.Volume_Home = Float(Float(Self.Volume_Home.Value) / 100)
			Case Volume_Watch
				'Options.Volume_Watch = Float(Float(Self.Volume_Watch.Value) / 100)
			Case Volume_Ping
				'Options.Volume_Ping = Float(Float(Self.Volume_Ping.Value) / 100)
			Case Volume_Current
				'Options.Volume_Current = Float(Float(Self.Volume_Current.Value) / 100)
		End
	end
	
End
</textarea> <br><br></td></tr></table><br>
<a name="2125988"></a>

<a name="2125989"></a>

<a name="2125990"></a>

<a name="2125991"></a>

<a name="2125992"></a>

<a name="2125993"></a>

<a name="2125994"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Goodlookinguy</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> You're going to feel stupid about this one so I apologize in advance.<br><br>Line 293, comment this out<br><pre class=code>box = New junglegui.TextField</pre><br><br>I should note, however, despite this fixing the issue, Jungle GUI has its own issues. If you have a lots of text in the box (enough to scroll it) and have your caret on the last letter then change the text to "This". The scrolling will not reset itself. <br><br></td></tr></table><br>
<a name="2125995"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Paul - Taiphoz</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> oh my goodness.<br><br>Three lessons here, <br>1.... NEVER copy paste code from one function/method to another without actually checking it first<br>2.... When passing an object into a method, its not nice to then ignore its sad little face and create a new one instead..<br>3.... Don't take a break from coding your brain will goto mush.<br>Again mate thanks for pointing this out, that's what I get for taking a long break from writing code. <br><br></td></tr></table><br>
<a name="2126004"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ziggy</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Great to see you find a solution for your issue. Remember JungleGUI is open source so if you fix anything, don't hesitate to pull request at GitHub! <br><br></td></tr></table><br>
<a name="2126005"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Paul - Taiphoz</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> had to add a bit of code to handle clicking the close button on a form and the close button image was missing if it helps at all. im not to good when it comes to github so will leave that to some one else. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
