<!DOCTYPE html><html lang="en" ><head ><title >Texture Atlas with TexturePacker</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Texture Atlas with TexturePacker</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=507" >Monkey Programming</a>/<a href="#bottom" >Texture Atlas with TexturePacker</a><br><br>
<a name="2085874"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >erebel55</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I searched around the forums and found this thread<br><a href="http://www.monkey-x.com/Community/posts.php?topic=1216" target="_blank">http://www.monkey-x.com/Community/posts.php?topic=1216</a><br><br>So, I took the penguin image from there, added it to TexturePacker and set the following settings.<br><img src="https://dl.dropboxusercontent.com/u/106998981/texture_settings.png"><br><br>Which generated the following the following penwalker.png file<br><img src="https://dl.dropboxusercontent.com/u/106998981/penwalker.png"><br><br>And the following penwalker.txt file<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
penwalker.png
format: RGBA8888
filter: Linear,Linear
repeat: none
walker
  rotate: false
  xy: 0, 0
  size: 256, 64
  orig: 256, 64
  offset: 0, 0
  index: -1
</textarea><br><br>From here I created the following from Adam's Another Texture Atlas Loader<br>textureloader.monkey<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Import mojo.app
Import mojo.graphics
Import monkey

'' libgdx sprite sheet loader
'' 
''this is best used in loading all textures, don't create during the game time, as it would create lag

Function LoadAtlas:CTextureAtlas(fn:String)
	Local atlas:CTextureAtlas = New CTextureAtlas
	If Not atlas.LoadTex(fn) Then Return Null
	Return atlas
End

Function LoadAtlas:CTextureAtlas(fn:String[])
	Local atlas:CTextureAtlas = New CTextureAtlas
	Local fconcat$
	
	For Local f$ = Eachin fn
		fconcat += LoadString( f )+"~n"
		
	Next
	If Not atlas.ProcessAtlas(fconcat) Then Return Null
	
	Return atlas
End

Class CTextureAtlas
	Field tmap:StringMap&lt;CSubTextureAtlas&gt; = New StringMap&lt;CSubTextureAtlas&gt;
	Field texture:Image[]
	Field total:Int =0
	Field subimage:CSubTextureAtlas[]

	Method LoadTex:Int(fn:String)
		
		If Not fn Return 0
		Local txtfile:String = fn[..(fn.FindLast("."))]+".txt"
		Local str:String=LoadString(txtfile)
		
		Return ProcessAtlas(str)
		
	End

	Method ProcessAtlas:Int(str:String)
		
		'Print "******BEGIN"
		
		If Not str Then Return 0
		
		texture = New Image[1]
		
		Local all:String[] = str.Split("~n")
		Local textureCount:Int = 0
		
		texture[textureCount] = LoadImage(all[0].Trim(), 1)
		DebugLog("Loading new texture " + all[0].Trim() + " here, index " + textureCount)
		If Not texture Then Return
		
		''count total
		For Local i:Int = 0 To all.Length()-1
			If all[i].Contains("xy:") Then total += 1
		Next
				
		''create sub images
		subimage = New CSubTextureAtlas[total + 1]
		Local si:Int = 0
		Local nextimage:Int = 0
		Local nextfile:Int = 0
		Local xy:String[] =["",""] '= New String[10]
		Local row:String = ""

		For Local i:Int = 4 To all.Length()-1
			Local EOF:Bool = (i=(all.Length()-1))
			
			row = all[i].Trim()
			If row.Contains(":")
			
				If row.Contains("xy")
					xy = row[ (row.FindLast(":")+1)..].Split(",")				
					subimage[si].x = Int(xy[0].Trim())
					subimage[si].y = Int(xy[1].Trim())
				Elseif row.Contains("size")
					xy = row[ (row.FindLast(":") + 1) ..].Split(",")
					subimage[si].w = Int(xy[0].Trim())
					subimage[si].h = Int(xy[1].Trim())
				Elseif row.Contains("offset")
					xy = row[ (row.FindLast(":") + 1) ..].Split(",")
					subimage[si].offsetx = Int(xy[0].Trim())
					subimage[si].offsety = Int(xy[1].Trim())
				Elseif row.Contains("index")
					Local ind:String = row[ (row.FindLast(":") + 1) ..]
					subimage[si].index = Int(ind.Trim() )
				EndIf
				
			Else
				
				If row="" Then
					' blank lines lead to a new texture file
					nextfile = 1
					Continue
				EndIf
				
				If nextfile = 1
					If row.Contains(".")
						' new texture file
						textureCount = textureCount + 1
						DebugLog("Loading new texture " + row + " here, index " + textureCount)
						texture = texture.Resize(texture.Length + 1)
						texture[textureCount] = LoadImage(row, 1)
						nextfile = 0
					Endif
					Continue					
				EndIf
				
				'' nextimage=0 is used for first time through
				If nextimage
					If subimage[si].index &gt;-1 Then subimage[si].file = subimage[si].file+"_"+String(subimage[si].index)
					tmap.Set( (subimage[si].file), subimage[si])
					DebugLog("Added " + subimage[si].file + "~n")
					si += 1
				EndIf

				''start new image
				subimage[si] = New CSubTextureAtlas
				subimage[si].file = row
				subimage[si].page = textureCount ' added to handle pages
				
				nextimage = 1
				
			Endif
		Next
	
		If si&gt;0 Or total &lt;=1
			''add last image
			If subimage[si].index &gt;-1 Then subimage[si].file = subimage[si].file+"_"+String(subimage[si].index)
			DebugLog subimage[si].file
			tmap.Set((subimage[si].file), subimage[si])
		Endif
		
		total = si
		
		Return 1
	End
	
	Method GetSubTexture:CSubTextureAtlas(fn:String, index:Int=0)
		Local noindexfn:String
		
		''drop any .png ext
		If fn.Contains(".") Then fn = fn[..(fn.FindLast(".")) ]
		noindexfn = fn.Trim()
		If index&gt;0 Then fn=fn+"_"+String(index)

		Local subimage:CSubTextureAtlas = tmap.Get(fn)
		If Not subimage
			''try without index
			subimage = tmap.Get(noindexfn)
			If Not subimage
				Print "**atlas not found:"+fn
				Return Null
			Endif
		Endif

		Return subimage
	End
	
	Method Grab:Image(fn:String, index:Int=0, flags:Int=Image.DefaultFlags)
	
		Local subimage:CSubTextureAtlas = GetSubTexture(fn,index)
		If Not subimage Then Return Null
		
		Local p:Int = subimage.page ' added to handle pages
		Return texture[p].GrabImage(subimage.x, subimage.y, subimage.w, subimage.h, 1, flags)
	End
	
	Method GrabSet:Image[](fn:String, w:Int, h:Int, frames:Int=0, flags:Int=Image.DefaultFlags, index:Int=0)
		Local imgs:Image[frames+1]
		imgs[0] = Null
		
		Local subimage:CSubTextureAtlas = GetSubTexture(fn,index)
		Local p:Int = subimage.page ' added to handle pages
		Local newx:Int =0 , newy:Int=0
		For Local i:=0 To frames-1
			imgs[i] = texture[p].GrabImage(subimage.x + newx, subimage.y + newy, w, h, 1, flags)
			newx += w+1
			If newx &gt;= subimage.w-2
				newx=0
				newy += h+1
				If newy&gt;subimage.h
					Exit
				Endif
			Endif
			
		Next
		Return imgs
	End

End

Class CSubTextureAtlas
	Field file:String = ""
	Field page:Int = 0 ' added to handle pages
	Field x:Int, y:Int, offsetx:Int, offsety:Int
	Field w:Int, h:Int
	Field index:Int
	Field totalframes:Int
End
</textarea><br><br>I then took therevills example and created the following<br>textureloadertest.monkey<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Import mojo
Import textureloader

Global myGame:MyGame

Function Main:Int()
	myGame = new MyGame
	Return 0
End

Class MyGame Extends App
	Field unitAtlas:CTextureAtlas
	Field imgunit:Image[16]
	Field frame:Int
	Field frameTimer:Int
	Field frameEnd:Int = 15
	Field frameSpeed:Int = 100

	Method OnCreate:Int()
		SetUpdateRate 60
		unitAtlas = LoadAtlas("penwalker.txt")
		For Local i:Int = 0 to 15
			if i &lt; 10
				imgunit[i] = unitAtlas.Grab("0"+i,0,Image.MidHandle)
			Else
				imgunit[i] = unitAtlas.Grab(i,0,Image.MidHandle)
			End
		Next

		Return 0
	End
	
	Method OnUpdate:Int()
		if frameSpeed &gt; 0
			If Millisecs() &gt; frameTimer + frameSpeed
				frame+=1
				If frame &gt; frameEnd
					frame = 0
                End
				frameTimer = Millisecs()
			End	
		end
		Return 0		
	End
	
	Method OnRender:Int()
		Cls
        DrawImage imgunit[frame], 100, 100
		Return 0
	End
End
</textarea><br><br>However, I am getting the following when I try to run.  <br><pre class=code>
Loading new texture penwalker.png here, index 0
walker
**atlas not found:00
**atlas not found:01
**atlas not found:02
**atlas not found:03
**atlas not found:04
**atlas not found:05
**atlas not found:06
**atlas not found:07
**atlas not found:08
**atlas not found:09
**atlas not found:10
**atlas not found:11
**atlas not found:12
**atlas not found:13
**atlas not found:14
**atlas not found:15
Monkey Runtime Error : Memory access violation
C:/MonkeyXPro78c/modules/mojo/graphics.monkey&lt;457&gt;
C:/Users/Ethan/Dropbox/textureloadertest.monkey&lt;50&gt;
C:/MonkeyXPro78c/modules/mojo/app.monkey&lt;113&gt;
</pre><br><br>So I am assuming I did something wrong in TexturePacker? <br><br></td></tr></table><br>
<a name="2085873"></a>

<a name="2085872"></a>

<a name="2085871"></a>

<a name="2085870"></a>

<a name="2085888"></a>

<a name="2085889"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamRedwoods</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> according to the "penwalker.txt" file, you only added one file, so the texture atlas only sees the one.<br>in this case, you do not need the texture atlas loader. instead use this, which is in Monkey:<br><pre class=code>
myImage = LoadImage ( path:String, frameWidth:Int, frameHeight:Int, frameCount:Int, flags:Int=image.defaultflags )
</pre><br>where frameWidth=32 and frameHeight=32 and frameCount=16<br>then use <br><pre class=code>
DrawImage(myImage, x, y, your frame number)
</pre> <br><br></td></tr></table><br>
<a name="2085886"></a>

<a name="2085887"></a>

<a name="2085891"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >erebel55</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Right, I understand how to do LoadImage/DrawImage; that is what I'm currently using in my game.<br><br>I am trying to understand how to use atlases.  So for animated images I have to add each frame as a separate image to TexturePacker?  I just added the whole frame strip. <br><br></td></tr></table><br>
<a name="2085895"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AdamRedwoods</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> So for animated images I have to add each frame as a separate image to TexturePacker? <br></div><br>yes. then the "atlas.Get()" filename would be the names of the files you added. <br><br></td></tr></table><br>
<a name="2085903"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >erebel55</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks I got it working now :D <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
