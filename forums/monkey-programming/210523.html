<!DOCTYPE html><html lang="en" ><head ><title >How to avoid canvas.Clear - MOJO2</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >How to avoid canvas.Clear - MOJO2</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=507" >Monkey Programming</a>/<a href="#bottom" >How to avoid canvas.Clear - MOJO2</a><br><br>
<a name="2119191"></a>

<a name="2119192"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >APC</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> The code below suppose to draw a line on the screen as I move my mouse, because I commented out the canvas.Clear, but for some reason MOJO 2 still clearing the canvas.<br>Is there a command to tell MOJO 2 to not clear the canvas?<br>On the code below you can set the #MOJO to 1 or 2 and see the problem happening.<br><br><br><pre class=code>
Strict
#MOJO = 1
#IF MOJO=2
	Import mojo2
#ELSE
	Import mojo
#END
Class MyApp Extends App
	Field canvas:Canvas
    Field isMojo2:Bool = False
	Method OnCreate:Int()
		SetUpdateRate(0)
		#IF MOJO=2
			canvas = New Canvas
		#END

		Return 0
	End
 	Method OnRender:Int()
		#If MOJO=2
		canvas.SetColor 1, 1, 0
			canvas.DrawText("MOJO 2 Test", 0, 0)
 			canvas.SetColor 0, 1, 0
			canvas.DrawCircle(MouseX(), MouseY(), 3)
			canvas.Flush
		#Else
		SetColor(255, 0, 255)
			DrawText("MOJO 1 Test", 0, 0)
			SetColor(255, 0, 0)
			DrawCircle(MouseX(), MouseY(), 3)
			
		#EndIf
		Return 0
	End
End
Function Main:Int()
	New MyApp
	Return 0
End
</pre> <br><br></td></tr></table><br>
<a name="2119189"></a>

<a name="2119188"></a>

<a name="2119196"></a>

<a name="2119198"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >APC</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok  I got it working! I had to create a Render and use layers.<br><br>Here is my new code<br><br><b>Mark Sibly</b> , why when I omit the canvas.Clear() the CANVAS still gets cleared? Is that a bug? Is there a way to SetClearMode(0) to the CANVAS , like it is for the Render?<br><br><pre class=code>
Strict
#MOJO = 2
#IF MOJO=2
	Import mojo2
#ELSE
	Import mojo
#END

Const NUM_LIGHTS:= 5

'create an orthographics projection matrix
Function Mat4Ortho:Float[]( left:Float,right:Float,bottom:Float,top:Float,znear:Float,zfar:Float )

	Local w:=right-left,h:=top-bottom,d:=zfar-znear
	
	Return [ 2.0/w,0,0,0, 0,2.0/h,0,0, 0,0,2.0/d,0, -(right+left)/w,-(top+bottom)/h,-(zfar+znear)/d,1 ]
End

Class MyLight Implements ILight

	'note: x,y,z,w go in last 4 components of matrix...
	Field matrix:=[1.0,0.0,0.0,0.0, 0.0,1.0,0.0,0.0, 0.0,0.0,1.0,0.0, 0.0,0.0,-100.0,1.0]
	Field color:=[0.2,0.2,0.2,1.0]
	Field range:=400.0

	'implement ILight interface...
	'
	Method LightMatrix:Float[]()
		Return matrix
	End
	
	Method LightType:Int()
		Return 1
	End
	
	Method LightColor:Float[]()
		Return color
	End
	
	Method LightRange:Float()
		Return range
	End
	
	Method LightImage:Image()
		Return Null
	End

End

Class MyLayer Extends DrawList Implements ILayer

	Field lights:=New Stack&lt;MyLight&gt;
	Field layerMatrix:=[1.0,0.0,0.0,0.0, 0.0,1.0,0.0,0.0, 0.0,0.0,1.0,0.0, 0.0,0.0,0.0,1.0]
	Field layerFogColor:=[0.0,0.0,0.0,0.0]

	'implement ILayer interface...
	'
	Method LayerMatrix:Float[]()
		Return layerMatrix
	End
	
	Method LayerFogColor:Float[]()
		Return layerFogColor
	End
	
	Method LayerLightMaskImage:Image()
		Return Null
	End
	
	Method EnumLayerLights:Void( lights:Stack&lt;ILight&gt; )
		For Local light:=Eachin Self.lights
			lights.Push light
		Next
	End

	Method OnRenderLayer:Void( drawLists:Stack&lt;DrawList&gt; )
		drawLists.Push Self
	End

End
Class MyApp Extends App
	Field canvas:Canvas
    Field isMojo2:Bool = False
	Field renderer:Renderer
	Field layer0:MyLayer
	
	Method OnCreate:Int()
		SetUpdateRate(0)
		#IF MOJO=2
			canvas = New Canvas
			
			renderer = New Renderer
			
			renderer.SetViewport(0, 0, DeviceWidth(), DeviceHeight())
			renderer.SetProjectionMatrix(Mat4Ortho(0, 640, 0, 480, -1, 1))
			renderer.SetAmbientLight( [0.1,0.1,0.1,1.0] )
			renderer.SetClearMode(0)
			
			'create layer 0
			layer0=New MyLayer
		
		#END
		renderer.Layers.Push layer0
		Return 0
	End
    
	Method OnRender:Int()
		If MouseDown()
			#If MOJO=2
				layer0.SetColor 1, 1, 0
				layer0.DrawText("MOJO 2 Test", 0, 0)
				layer0.SetColor 0, 1, 0
				layer0.DrawCircle(MouseX(), MouseY(), 3)
'				canvas.SetColor 1, 1, 0
'				canvas.DrawText("MOJO 2 Test", 0, 0)
'	 			canvas.SetColor 0, 1, 0
'				canvas.DrawCircle(MouseX(), MouseY(), 3)
'				canvas.Flush
				renderer.Render
			#Else
			SetColor(255, 0, 255)
				DrawText("MOJO 1 Test", 0, 0)
				SetColor(255, 0, 0)
				
				DrawCircle(MouseX(), MouseY(), 3)
				
			#EndIf
		EndIf
		Return 0
	End
End

Function Main:Int()
	New MyApp
	Return 0
End
</pre> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
