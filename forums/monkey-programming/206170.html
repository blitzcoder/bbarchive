<!DOCTYPE html><html lang="en" ><head ><title >(x,y) Array? (solved)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >(x,y) Array? (solved)</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=507" >Monkey Programming</a>/<a href="#bottom" >(x,y) Array? (solved)</a><br><br>
<a name="2070786"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Markus</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> hello,<br>i convert a old source code (path finding algorithms).<br>i want have a object in a x,y grid because i need to find<br>the nearest neighbours with +1 -1<br><br>Class Pos<br> Field w:Int<br>End <br><br>Class Area<br>	Field grid:Pos[50,50] &lt;???<br><br>i need <br>grid[x+1,y-1].w=123 <br><br></td></tr></table><br>
<a name="2070767"></a>

<a name="2070768"></a>

<a name="2070769"></a>

<a name="2070771"></a>

<a name="2070772"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gerry Quinn</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Array syntax is like this:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Class Area

	Field grid:Pos[][]
	
	Method New()
		For Local x:Int = 0 Until 50
			grid[ x ] = New Pos[ 50 ]
			For Local y:Int = 0 Until 50
				grid[ x ][ y ] = New Pos()
			Next
		Next
	End
End

' Access:
grid[ x + 1 ][ y + 1 ].w = 123
</textarea><br><br>(A two-dimensional array in Monkey is an array of one-dimensional arrays.) <br><br></td></tr></table><br>
<a name="2070773"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Markus</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> ok,thank you :-)<br><br>i wonder why the compiler have a problem with this<br>Field a:Pos[][]     ok <br>Field ab:Pos[50]    ok<br>Field abc:Pos[50][50]  error?<br>Field abcd:Pos[50][]  ok <br><br></td></tr></table><br>
<a name="2070830"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ziggy</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> As an alternative method: (I think this one is faster at allocating the array, as it is done in one single pass)<br><pre class=code>
Function Main()
	Local td:= New TwoDimensionalMatrix&lt;String&gt;
	td.Dim(100, 100)
	td.SetItem(50, 50, "Hello!")
	Print td.GetItem(50, 50)
End

Class TwoDimensionalMatrix&lt;T&gt;
	
	Method Dim(width, height)
		Self.width = width
		Self.height = height
		data = New T[width * height]
	End

	Method ReDim(width, height)
		Self.width = width
		Self.height = height
		data = data[ .. width * height]
	End
	
	Method GetWidth()
		Return width
	End
	
	Method GetHeight()
		Return height
	End
	
	Method GetLength()
		Return data.Length
	End
	
	Method GetItemByOffset:T(item:Int)
		Return data[item]
	End
	
	Method GetItem:T(x:Int, y:Int)
		Local val:Int = y * width + x
		Return data[val]
	End
	
	Method SetItem(x:Int, y:Int, value:T)
		Local val:Int = y * width + x
		data[val] = value
	End
	Private
	Field data:T[]
	Field width:Int = 0, height:Int = 0
End</pre> <br><br></td></tr></table><br>
<a name="2070778"></a>

<a name="2070777"></a>

<a name="2070775"></a>

<a name="2070776"></a>

<a name="2070774"></a>

<a name="2070780"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Markus</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> @ziggy<br>cool!, thanks :) <br><br></td></tr></table><br>
<a name="2070781"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Supertino</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> @ziggy - that's a nice implantation that I am going to steal... yonk! <br><br></td></tr></table><br>
<a name="2070784"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Why0Why</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I like that approach, Ziggy.  I will be stealing it too! <br><br></td></tr></table><br>
<a name="2070788"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gerry Quinn</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Markus:<br><br>Field abc:Pos[50][50] error?<br><br>This is because you are trying to define too much at once.<br><br>Field abc:Pos[50][] is fine, it says "make an array with 50 spaces for 1D arrays of Pos"<br><br>But (although it is rarely used) all those 1D arrays could be different lengths.  You need to specify that each one is a Pos[50].  I guess since they are nearly always the same length, it could be added as syntactic sugar sometime.  On the other hand, that would mean a lot of object allocations were going on behind the scenes, which might be confusing.<br><br>Personally I use a generic allocator:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

class Generic&lt; T &gt;

	' Allocate a 1D array of objects (note that primitives get auto-filled)
	Function AllocateArray:T[]( i:Int )
		Local arr:T[] = New T[ i ]
		Return arr
	End
	
	
	' Allocate a 2D array of objects
	Function AllocateArray:T[][]( i:Int, j:Int )
		Local arr:T[][] = New T[ i ][]
		For Local index:Int = 0 Until i
			arr[ index ] = AllocateArray( j )
		Next
		Return arr
	End
 	
	
	' Allocate a 3D array of objects 
	Function AllocateArray:T[][][]( i:Int, j:Int, k:int )
	    Local arr:T[][][] = New T[ i ][][]
	    For Local index:Int = 0 Until i
			arr[ index ] = AllocateArray( j, k )
		Next
		Return arr
	End
	
	
	' Fill an array of objects with new objects
	Function FillObjectArray:Void( _arr:T[] )
		Local len:Int = _arr.Length()
		For Local i:Int = 0 Until len
			_arr[ i ] = New T()
		Next
	End
 	
	
	' Fill a 2D array of objects with new objects
	Function FillObjectArray:Void( _arr:T[][] )
		Local len:Int = _arr.Length()
		For Local i:Int = 0 Until len
			FillObjectArray( _arr[ i ] )
		Next
	End
 	
 	
	' Fill a 3D array of objects with new objects
	Function FillObjectArray:Void( _arr:T[][][] )
		Local len:Int = _arr.Length()
		For Local i:Int = 0 Until len
			FillObjectArray( _arr[ i ] )
		Next
	End

End		
</textarea><br><br><br>So to make a 50x50 array of new Pos objects, I can just write:<br><pre class=code>
grid = Generic&lt; Pos &gt;.AllocateArray( 50, 50 )
Generic&lt; Pos &gt;.FillObjectArray( grid )
</pre><br><br>If it were a 50x50 array of ints rather than objects, only the first line would be needed.<br><br><br>The 1D array has its merits too, though - if you are doing intense calculations such as pathfinding, it should be faster than multidimensional arrays. <br><br></td></tr></table><br>
<a name="2070785"></a>

<a name="2070797"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Markus</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Gerry<br>interesting, thank you very much. <br><br></td></tr></table><br>
<a name="2070929"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ziggy</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> I was a bit bored and made an arbitrary-array size implementation of the above. It seems to work very fast.<br>That's it:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
#Rem monkeydoc Module lemongames.collections.multiarray
	This module contains the MultiArray class, that is a class that allows the creation of multiple dimensions arrays
#END
#REM monkeydoc
	This is a multidimensional array.
#END
Class MultiArray&lt;T&gt;

	#REM monkeydoc
		This method allows the multidimensional array to be set to a given dimensions and sizes.
		Use it like this:
		myArray.Dim([3,10,7])
		This will make this array to be of 3 dimensions, being the first one of 3 items, the second one of 10 items, and the last one of 7 items.
		When a multidimensional array is dimensioned using this method, any previous contents it hast is derefenced.
	#END
	Method Dim(dimensions:Int[]) Final
		Self.dimensions = dimensions
		Local size:Int = dimensions[0]
		For Local i:Int = 1 Until dimensions.Length
			size *= dimensions[i]
		Next
		data = New T[size]
		PrecalculateOffsets
	End

	#REM monkeydoc
		This method allows the multidimensional array to be set to a given dimensions and sizes, and keep its previous contents when possible.
		Use it like this:
		myArray.ReDim([3,10,7])
		This will make this array to be of 3 dimensions, being the first one of 3 items, the second one of 10 items, and the last one of 7 items.
		When a multidimensional array is dimensioned using this method, any previous contents it has is not dereferenced unles it gets out of bounds.
	#END
	Method ReDim(dimensions:Int[]) Final
		Self.dimensions = dimensions
		
		Local size:Int = dimensions[0]
		For Local i:Int = 1 Until dimensions.Length
			size *= dimensions[i]
		Next
		data = data.Resize(size)
		PrecalculateOffsets
	End
	#REM monkeydoc
		This method will return the size of the indexed dimension
	#END
	Method GetDimensionSize(index:Int) Final
		Return dimensions[index]
	End
	
	#REM monkeydoc
		This method will return the number of dimensions the array has at this moment.
	#END
	Method GetNumberOfDimensions:Int() Final
		Return dimensions.Length
	End
		
	#REM monkeydoc
		This method will return the number of items contained by the array.
	#END
	Method Length() Final
		Return data.Length
	End
	
	#REM monkeydoc
		This method will return the item at the given offset. That's a o(1) operation that does not generate garbage, so it is FAST.
	#END
	Method GetItemByOffset:T(item:Int) Final
		Return data[item]
	End
	
	#REM monkeydoc
		This method will return the item at the given position. That's a o(dimensions-1) operation that does not generate garbage, so it is FAST.
		Use it like this:
		myArray.Get([3,5,8]) 
		so you get the item at (3,5,8) in your 3 dimensional array
	#END
	Method Get:T(index:Int[]) Final
		Local val:Int = CalculateIndex(index) 'y * width + x
		Return data[val]
	End
	
	#REM monkeydoc
		This method will set a given value at the given position. That's a o(dimensions-1) operation that does not generate garbage, so it is FAST.
		Use it like this:
		myArray.Set([3,5,8], MyNewValue) 
		so you the item at (3,5,8) in your 3 dimensional array gets the value MyNewValue.
	#END
	Method Set(index:Int[], value:T) Final
		Local val:Int = CalculateIndex(index) 'y * width + x
		data[val] = value
	End

	Method Set(offset:Int, value:T)
		'Local val:Int = CalculateIndex(index) 'y * width + x
		data[offset] = value
	End

	#REM monkeydoc
		This method will return the absolute offset of a item at a given position.
	#End
	Method GetOffsetForIndex(index:Int[])
		Return CalculateIndex(index)
	End
	
	'Summary:This is internaly used to allow EachIn iterations&lt;br&gt; This generates one object for the garbage collector, use index iteration instead of ForEach when possible.
	Method ObjectEnumerator:MultiArrayEnumerator&lt;T&gt;()
		Local mae:= New MultiArrayEnumerator&lt;T&gt;
		mae.InitEnumerator(data)
		Return mae
	End
	
	Private
	Field data:T[]
	Field dimensions:Int[]
	Field preCalc:Int[]

	'This gets a given vector (x,y,x or whatever) and returns the corresponding index into the linear one dimensional array.
	'Despite having a loop, it has as many iterations as dimensions - 1, so it may be usualy no iterations for 2 dimensional arrays, or 1 iteration at 3 dimensional arrays.
	Method CalculateIndex(position:Int[]) Final
		#if CONFIG="debug"
			If position.Length &lt;&gt; dimensions.Length Then
				Error("Wrong number of dimensions, Expecting " + dimensions.Length + " but got " + position.Length)
			EndIf
		#End
		Local val = position[0]
		For Local i:Int = 1 Until preCalc.Length
			val += preCalc[i - 1] * position[i]
		Next
		Return val
	End
	
	'This makes some pre-calculation to speed up index resultion.
	Method PrecalculateOffsets() Final
		Self.preCalc = New Int[dimensions.Length]
		preCalc[0] = dimensions[0]
		For Local i:Int = 1 Until dimensions.Length
			preCalc[i] = preCalc[i - 1] * dimensions[i]
		Next
	End
End

Class MultiArrayEnumerator&lt;T&gt;
	Method InitEnumerator(data:T[])
		index = 0
		Self.data = data
	End
	Method HasNext:Bool()
		Return index &lt; data.Length
	End
	Method NextObject:T()
		index += 1
		Return data[index - 1]
	End
	Private
	Field data:T[]
	Field index:Int = 0
End
</textarea><br><br>Usage example:<br><pre class=code>'USAGE EXAMPLE:
Function Main()
	'We create a multidimensional array:
	Local myArray:= New MultiArray&lt;String&gt;
	
	'We set it to be three dimensions:
	myArray.Dim([3, 5, 7])
	
	Print "Length of the array: " + myArray.Length
	Print "Number of dimensions: " + myArray.GetNumberOfDimensions()
	For Local i:Int = 0 Until myArray.GetNumberOfDimensions
		Print "Dimension " + i + " has a size of " + myArray.GetDimensionSize(i)
	Next
	
	'Just a counter to set be used later:
	Local i:Int = 0

	'We iterate the array using its x, y, and z indexes, and set and get values from/to it:
	For Local z:Int = 0 Until myArray.GetDimensionSize(2)
		For Local y:Int = 0 Until myArray.GetDimensionSize(1)
			For Local x:Int = 0 Until myArray.GetDimensionSize(0)
			
				'We set the value "Value of 1, 2, 3, etc... to each item of the array:
				myArray.Set([x, y, z], "Value of " + i)
				
				'We get the value of each item of the array, once set:
				Print "Iterating item (" + x + ", " + y + ", " + z + ") = " + myArray.Get([x, y, z])
			
				'We increase the counter:	
				i += 1
			Next
		Next
	Next
	'This should generate an error in debug mode, wrong number of dimensions, as it's a x,y,z array:
	'Print myArray.Get([3, 3])
End

</pre>Now if we could get some syntactic sugar to make this a bit better (somethinf like paramarrays) it would be very sexy.<br><br>EDITED with a small fix. <br><br></td></tr></table><br>
<a name="2070880"></a>

<a name="2070879"></a>

<a name="2070850"></a>

<a name="2070849"></a>

<a name="2070847"></a>

<a name="2070844"></a>

<a name="2070843"></a>

<a name="2070839"></a>

<a name="2070867"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Loofadawg</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ziggy, If I only understood a fraction of what you know...<br><br>It is like my brain can only take in so much before it pushes something else out.  Lol.  Still I will study this and try to understand it. <br><br></td></tr></table><br>
<a name="2070883"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ziggy</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> It's very very easy to use.<br><br>See a small example that covers mostly anything: (latest edit of the above required)<br><br><pre class=code>Function Main()
	
	'Example 1: Creating a 3 dimensions array:
	Local myArray:= New MultiArray&lt;MyClass&gt;

	'We set the number of dimensions, and the size of each dimension in one go:
	myArray.Dim([255, 100, 24])
	
	
	'We set a value at a given position of the array:
		'1.- Create Then item:
		Local myItem:= New MyClass
		myItem.name = "George"
		myItem.age = 24
	
		'2.- Put it on a array position:
		myArray.Set([23, 45, 12], myItem)

		
	'We get a value at a given position of the array:
	Local myItem2:= myArray.Get([23, 45, 12])
	Print "The name of the item at 23, 45, 12 is " + myItem2.name
		
	'Get the length of the array:
	Print "size= " + myArray.Length
	
	'Get the number of dimensions
	Print "dimensions= " + myArray.GetNumberOfDimensions()
	
	'Get the size of the first dimension: 
	Print "size of first dimension= " + myArray.GetDimensionSize(0)

	'Get the size of the second dimension: 
	Print "size of second dimension= " + myArray.GetDimensionSize(1)
	
	'Iterate the array:
	For Local myc:= EachIn myArray
		If myc &lt;&gt; Null Then Print myc.name
	Next
	
	'Resize the array keeping items:
	myArray.ReDim([128, 128, 128])
	Print "Size of the array after resizing= " + myArray.Length
	
	
End

Class MyClass
	Field name:String, age:Int
	
End</pre> <br><br></td></tr></table><br>
<a name="2070882"></a>

<a name="2070886"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >muddy_shoes</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> That might be convenient but I doubt very much that it's fast except for the initial allocation. Using a 1D array in pursuit of speed and then wrapping it with accessor functions seems to have some fatal attraction. The call overhead by itself likely wipes out any advantage from using 1D. A call that involves constructing an array to pass in is going to be far worse. <br><br></td></tr></table><br>
<a name="2070888"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ziggy</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> The call overhead by itself likely wipes out any advantage from using 1D. A call that involves constructing an array to pass in is going to be far worse.  <br></div>Well... it should not. Calling a function is not *that* slow, and also, creating an array to call a method is a bad idea. That's true, but you can just not do it.<br>Of couse accesing a 1D array is faster. Not sure comparing my method to calling an array of arrays of arrays, and also this allows for other things like resizing, iteration, etc. I think it is handy but, as with any collection-like abstraction, it has some drawbacks and the thing is to choose the right one on each stituation. <br><br></td></tr></table><br>
<a name="2070889"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >muddy_shoes</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> It doesn't have to be *that* slow, whatever value for "that" you're imagining. It just has to be slower than the advantage you gain from the 1D access, which is somewhere between 0 and not a lot.<br><br>As I said, it's convenient, but you made a claim that it is also "very fast" when it seems likely to be anything but if someone uses the accessors you have provided. I'm just flagging that fact to maybe avoid someone taking your claim at face value and then wondering why they've got performance issues. <br><br></td></tr></table><br>
<a name="2070892"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ziggy</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes, I see your point! I was expecting it to be slower than what I got after some testing, so that's why I said it was very fast, becouse it looked like it could be slow and, to my surprise, it was "very fast" at last on chrome using JavaScript. I haven't realy spent any efforts on benchmarking it or anything, and I don't expect anything like this to be as fast as direct 1D array. That's obvious!<br>EDIT: After making some changes such as making the methods Final (they're pure, so they could be inlined by the compiler), on a small test I've done, on release mode, it's a bit faster to use the multidim class than to use an array of arrays to access 3 dimensions indexed item. The more dimensions you add, the better the class behaves, but the diference is almost negligible.<br>On JavaScript, accesing arrays of arrays is always slightly faster than using the MultimDim class, but the difference is again very little.<br>Haven't tested Flash or Objective-C, but I would say it's not bad! <br><br></td></tr></table><br>
<a name="2070891"></a>

<a name="2070890"></a>

<a name="2070893"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gerry Quinn</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've experimented with this in the past, and if you want to do straight Dijkstra pathfinding,the 1D array is 2X faster than the native Monkey 2D array.  99% of the time, this will make no difference whatsoever. <br><br></td></tr></table><br>
<a name="2070934"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ziggy</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> That's very interesting. Using this version of the Multidim array:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">#Rem monkeydoc Module lemongames.collections.multiarray
	This module contains the MultiArray class, that is a class that allows the creation of multiple dimensions arrays
#END
#REM monkeydoc
	This is a multidimensional array.
#END
Class MultiArray&lt;T&gt;

	#REM monkeydoc
		This method allows the multidimensional array to be set to a given dimensions and sizes.
		Use it like this:
		myArray.Dim([3,10,7])
		This will make this array to be of 3 dimensions, being the first one of 3 items, the second one of 10 items, and the last one of 7 items.
		When a multidimensional array is dimensioned using this method, any previous contents it hast is derefenced.
	#END
	Method Dim(dimensions:Int[]) Final
		Self.dimensions = dimensions
		Local size:Int = dimensions[0]
		For Local i:Int = 1 Until dimensions.Length
			size *= dimensions[i]
		Next
		data = New T[size]
		PrecalculateOffsets
	End

	#REM monkeydoc
		This method allows the multidimensional array to be set to a given dimensions and sizes, and keep its previous contents when possible.
		Use it like this:
		myArray.ReDim([3,10,7])
		This will make this array to be of 3 dimensions, being the first one of 3 items, the second one of 10 items, and the last one of 7 items.
		When a multidimensional array is dimensioned using this method, any previous contents it has is not dereferenced unles it gets out of bounds.
	#END
	Method ReDim(dimensions:Int[]) Final
		Self.dimensions = dimensions
		
		Local size:Int = dimensions[0]
		For Local i:Int = 1 Until dimensions.Length
			size *= dimensions[i]
		Next
		data = data.Resize(size)
		PrecalculateOffsets
	End
	#REM monkeydoc
		This method will return the size of the indexed dimension
	#END
	Method GetDimensionSize(index:Int) Final
		Return dimensions[index]
	End
	
	#REM monkeydoc
		This method will return the number of dimensions the array has at this moment.
	#END
	Method GetNumberOfDimensions:Int() Final
		Return dimensions.Length
	End
		
	#REM monkeydoc
		This method will return the number of items contained by the array.
	#END
	Method Length() Final
		Return data.Length
	End
	
	#REM monkeydoc
		This method will return the item at the given offset. That's a o(1) operation that does not generate garbage, so it is FAST.
	#END
	Method GetItemByOffset:T(item:Int) Final
		Return data[item]
	End
	
	#REM monkeydoc
		This method will return the item at the given position. That's a o(dimensions-1) operation that does not generate garbage, so it is FAST.
		Use it like this:
		myArray.Get([3,5,8]) 
		so you get the item at (3,5,8) in your 3 dimensional array
	#END
	Method Get:T(index:Int[]) Final
		Local val:Int = CalculateIndex(index) 'y * width + x
		Return data[val]
	End
	
	#REM monkeydoc
		This method will set a given value at the given position. That's a o(dimensions-1) operation that does not generate garbage, so it is FAST.
		Use it like this:
		myArray.Set([3,5,8], MyNewValue) 
		so you the item at (3,5,8) in your 3 dimensional array gets the value MyNewValue.
	#END
	Method Set(index:Int[], value:T) Final
		Local val:Int = CalculateIndex(index) 'y * width + x
		data[val] = value
	End

	Method Set(offset:Int, value:T)
		'Local val:Int = CalculateIndex(index) 'y * width + x
		data[offset] = value
	End

	#REM monkeydoc
		This method will return the absolute offset of a item at a given position.
	#End
	Method GetOffsetForIndex(index:Int[])
		Return CalculateIndex(index)
	End
	
	'Summary:This is internaly used to allow EachIn iterations&lt;br&gt; This generates one object for the garbage collector, use index iteration instead of ForEach when possible.
	Method ObjectEnumerator:MultiArrayEnumerator&lt;T&gt;()
		Local mae:= New MultiArrayEnumerator&lt;T&gt;
		mae.InitEnumerator(data)
		Return mae
	End
	
	Private
	Field data:T[]
	Field dimensions:Int[]
	Field preCalc:Int[]

	'This gets a given vector (x,y,x or whatever) and returns the corresponding index into the linear one dimensional array.
	'Despite having a loop, it has as many iterations as dimensions - 1, so it may be usualy no iterations for 2 dimensional arrays, or 1 iteration at 3 dimensional arrays.
	Method CalculateIndex(position:Int[]) Final
		#if CONFIG="debug"
			If position.Length &lt;&gt; dimensions.Length Then
				Error("Wrong number of dimensions, Expecting " + dimensions.Length + " but got " + position.Length)
			EndIf
		#End
		Local val = position[0]
		For Local i:Int = 1 Until preCalc.Length
			val += preCalc[i - 1] * position[i]
		Next
		Return val
	End
	
	'This makes some pre-calculation to speed up index resultion.
	Method PrecalculateOffsets() Final
		Self.preCalc = New Int[dimensions.Length]
		preCalc[0] = dimensions[0]
		For Local i:Int = 1 Until dimensions.Length
			preCalc[i] = preCalc[i - 1] * dimensions[i]
		Next
	End
End

Class MultiArrayEnumerator&lt;T&gt;
	Method InitEnumerator(data:T[])
		index = 0
		Self.data = data
	End
	Method HasNext:Bool()
		Return index &lt; data.Length
	End
	Method NextObject:T()
		index += 1
		Return data[index - 1]
	End
	Private
	Field data:T[]
	Field index:Int = 0
End
</textarea><br><br>And running this benchmark that measures random access to elements, comparing the multidim array with an array of arrays of arrays (3 dimensions) it seems that the multidim array is faster in HTML5, and the same on C++ (release mode)<br><br><pre class=code>Import lemongames.collections.multiarray
Import mojo
Global data:String
Function Main()
	Const ArrSize:Int = 200
	Const Iterations:Int = 500000
	Const cycles:Int = 10
	Print "Filling a 3 dim array of array"
	Local ms:= Millisecs()
	'We create a 3 dim array using monkey array of arrays method:
	Local a:= New MyClass[ArrSize][][]
	For Local i:Int = 0 Until ArrSize
		a[i] = New MyClass[ArrSize][]
		For Local j:Int = 0 Until ArrSize
			a[i][j] = New MyClass[ArrSize]
			For Local k:Int = 0 Until ArrSize
				a[i][j][k] = New MyClass
			Next
		Next
	Next
	Print "Done in " + (Millisecs - ms)
	Print "Filling a 3 dim multidim array"
	ms = Millisecs()
	'We create a 3 dimensional array using Multarray
	Local b:= New MultiArray&lt;MyClass&gt;
	b.Dim([ArrSize, ArrSize, ArrSize])
	For Local i:= 0 Until b.Length
		b.Set(i, New MyClass)
	Next
	Print "Done in " + (Millisecs - ms)
	For Local c:= 0 Until cycles
		ms = Millisecs
		For Local i:= 0 Until Iterations
			Local x:Int = Rnd(0, ArrSize)
			Local y:Int = Rnd(0, ArrSize)
			Local z:Int = Rnd(0, ArrSize)
			data = a[x][y][z].name
		Next
		Print "Arrray of arrays took: " + (Millisecs - ms)
		ms = Millisecs
		Local arr:= New Int[3]
		For Local i:= 0 Until Iterations
			arr[0] = Rnd(0, ArrSize)
			arr[1] = Rnd(0, ArrSize)
			arr[2] = Rnd(0, ArrSize)
			data = b.Get(arr).name
		Next
		Print "Multidim array took: " + (Millisecs - ms)
	Next
End

Class MyClass
	Field name:String = "Monkey!"
End</pre><br><br>I got this results on GLFW:<br><div class="quote"> Filling a 3 dim array of array<br>Done in 1778<br>Filling a 3 dim multidim array<br>Done in 1830<br>Arrray of arrays took: 208<br>Multidim array took: 200<br>Arrray of arrays took: 201<br>Multidim array took: 208<br>Arrray of arrays took: 208<br>Multidim array took: 198<br>Arrray of arrays took: 204<br>Multidim array took: 199<br>Arrray of arrays took: 213<br>Multidim array took: 200<br>Arrray of arrays took: 206<br>Multidim array took: 200<br>Arrray of arrays took: 207<br>Multidim array took: 205<br>Arrray of arrays took: 206<br>Multidim array took: 199<br>Arrray of arrays took: 204<br>Multidim array took: 195<br>Arrray of arrays took: 206<br>Multidim array took: 202 <br></div><br><br>And I got this output on chrome HTML5:<br><div class="quote"> Filling a 3 dim array of array<br>Done in 2563<br>Filling a 3 dim multidim array<br>Done in 4437<br>Arrray of arrays took: 266<br>Multidim array took: 187<br>Arrray of arrays took: 235<br>Multidim array took: 203<br>Arrray of arrays took: 219<br>Multidim array took: 187<br>Arrray of arrays took: 219<br>Multidim array took: 203<br>Arrray of arrays took: 219<br>Multidim array took: 203<br>Arrray of arrays took: 219<br>Multidim array took: 187<br>Arrray of arrays took: 219<br>Multidim array took: 203<br>Arrray of arrays took: 219<br>Multidim array took: 203<br>Arrray of arrays took: 219<br>Multidim array took: 203<br>Arrray of arrays took: 219<br>Multidim array took: 203<br> <br></div><br><br>Thhis is on FireFox, and the difference is even higer<br><div class="quote"> <br>Filling a 3 dim array of array<br>Done in 2715<br>Filling a 3 dim multidim array<br>Done in 2967<br>Arrray of arrays took: 250<br>Multidim array took: 162<br>Arrray of arrays took: 247<br>Multidim array took: 160<br>Arrray of arrays took: 278<br>Multidim array took: 183<br>Arrray of arrays took: 278<br>Multidim array took: 159<br>Arrray of arrays took: 251<br>Multidim array took: 160<br>Arrray of arrays took: 248<br>Multidim array took: 160<br>Arrray of arrays took: 245<br>Multidim array took: 161<br>Arrray of arrays took: 249<br>Multidim array took: 160<br>Arrray of arrays took: 245<br>Multidim array took: 157<br>Arrray of arrays took: 251<br>Multidim array took: 160<br> <br></div><br>So the MultiDim array class seems to be <strike>marginaly</strike> faster than an array of arrays for 3 dimensional arrays but the diference is very very little, but the class provides some additional functionalities. <br><br></td></tr></table><br>
<a name="2070933"></a>

<a name="2070932"></a>

<a name="2070931"></a>

<a name="2070930"></a>

<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
