<!DOCTYPE html><html lang="en" ><head ><title >THeap</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Lightweight THeap datastructure for priority queues etc, language=bmx, category=Algorithms'><meta name='author' content='Dreamora'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=6>Algorithms</a>/THeap</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1799.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >THeap by Dreamora</td><td align="right">2006 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Lightweight THeap datastructure for priority queues etc </td></tr><tr ><td class="cell"><pre class="code">Strict
Rem
    bbdoc: Heap
    about: Heap class. Can be used for sorting as well as "get always the smallest / largest"
End Rem
Rem
Module dreamora.ds_heap

ModuleInfo "Version: 1.0"
ModuleInfo "Author: Marc 'Dreamora' Schaerer"
ModuleInfo "License: Public domain"
ModuleInfo "Copyright: 2006  Marc 'Dreamora' Schaerer"
ModuleInfo "Modserver: dreamora"

ModuleInfo "History:"
moduleinfo "-   1.0 Release"
End Rem
Import brl.retro

Rem
	bbdoc: THeap
	about: Heap class
End Rem
Type THeap
	Field _data:Object[]
	Field _ascending:Int
	Field _length:Int = 1
	Field _sortingFunction:Int(one:Object,two:Object)
	
	Rem
		bbdoc: Create
		about: Creates a new THeap and returns the reference to it.&lt;br&gt;
		&lt;b&gt;elements:&lt;/b&gt; Number of elements you want the heap to initialize with.&lt;br&gt;
		The heap will dynamically resize if more elements are added than needed and that in a very performant way.&lt;br&gt;
		This is only if you now that you will push x object directly onto it.&lt;br&gt;
		Defaults to 1.&lt;br&gt;
		&lt;b&gt;maximum:&lt;/b&gt; Defines if the heap has maximum on top (true) or minimum.&lt;br&gt;
		Defaults to maximum.&lt;br&gt;
		&lt;b&gt;ComparisionFunction:int( one:Object,two:Object):&lt;/b&gt; Lets you define an own comparision function.&lt;br&gt;
		The example shows how to use it and for what type of stuff it might be usefull after all.&lt;br&gt;
		Defaults to BMs internal compare Objects functionality.
		returns: A valid reference to a THeap object
	End Rem
	Function Create:THeap(elements:Int = 16, maximum:Int = True, ComparisionFunction:Int( one:Object,two:Object)=CompareObjects)
		Local result:THeap	= New THeap
		result._data		= New Object[elements]
		result._ascending	= maximum
		result._sortingFunction	= ComparisionFunction
		Return result
	End Function
	
	Rem
		bbdoc: Add
		about: Adds a new object to the heap.&lt;br&gt;
		&lt;b&gt;obj:&lt;/b&gt; Object you want to add to the heap.
	End Rem
	Method Add(obj:Object)
		If _length = _data.length		_data	= _data[.._data.length*2]
		_data[_length] = obj
		Local ret:Int = _bubbleUp(_length)
		If ret &gt; 0		_siftDown(ret)
		_length	:+ 1
	End Method
	
	Rem
		bbdoc: Top
		about: Returns the top element on the heap, without removing it.
		returns: Top element on the heap
	End Rem
	Method Top:Object()
		If isEmpty()			Throw "THeap.Top: Not supported on an empty heap!"
		Return _data[1]
	End Method
	
	Rem
		bbdoc: GetTop
		about: Returns the top element on the heap, but with removing it!
		returns: Top element on the heap
	End Rem
	Method GetTop:Object()
		If isEmpty()		Throw "THeap.GetTop: Not supported on an empty heap!"
		Local tmp:Object	= _data[1]
		_data[1]	= _data[_length-1]
		_length	:- 1
		_siftDown(1)
		If _data.length &lt;= 0.35 * _length		_data	= _data[.._data.length/2]
		Return tmp
	End Method
	
	Rem
		bbdoc: Count
		about: Returns the number of elements on the heap.
		returns: Number of elements on the heap.
	End Rem
	Method Count:Int()
		Return _length-1
	End Method
	
	Rem
		bbdoc: isEmpty
		about: Returns if the heap is empty.
		returns: True if it is empty, otherwise false.
	End Rem
	Method isEmpty:Int()
		Return _length = 1
	End Method
	
	Method _bubbleUp:Int(index:Int)
		Local tmp:Object		= _data[index]
		If _ascending
			While index &gt; 1 And _sortingFunction(tmp,_data[index/2]) &gt; 0
				_data[index]	= _data[index/2]
				index			:/ 2
			Wend
		Else
			While index &gt; 1 And _sortingFunction(tmp,_data[index/2]) &lt; 0
				_data[index]	= _data[index/2]
				index			:/ 2
			Wend
		EndIf
		_data[index]		= tmp
		Return index
	End Method
	
	Method _siftDown(index:Int)
		Local tmp:Object			= _data[index]
		Local j:Int
		Local N:Int					= _length - 1
		If _ascending
			While index &lt;= N/2
				j	= index+index
				If _data[j+1] And _sortingFunction(_data[j + 1],_data[j]) &gt; 0	j :+ 1
				If _sortingFunction(tmp,_data[j]) &gt;  0					Exit
				_data[index]		= _data[j]
				index				= j
			Wend
		Else
			While index &lt;= N/2
				j	= index+index
				If _data[j+1] And _sortingFunction(_data[j + 1],_data[j]) &lt; 0	j :+ 1
				If _sortingFunction(tmp,_data[j]) &lt;  0					Exit
				_data[index]		= _data[j]
				index				= j
			Wend
		EndIf
		_data[index]			= tmp
	End Method
End Type



Function CompareObjects:Int(o1:Object, o2:Object)
	Return o1.compare(o2)
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >NoOdle</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> thanks, useful code! What would be the fastest way to amend a priority and resort the heap if you really needed to? <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
