<!DOCTYPE html><html lang="en" ><head ><title >Rooms and Doors generator</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Make maps/dungeons, language=bb, category=Algorithms'><meta name='author' content='Pakz'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=6>Algorithms</a>/Rooms and Doors generator</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="3273.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rooms and Doors generator by Pakz</td><td align="right">2016 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This code I made for me to try and make a 3d level out of it using cubes. This is the 2d part where the map is stored in a array.<br><br>I have made something similar as this in Monkey. It did take me a while to get this working again without looking at the monkey version. </td></tr><tr ><td class="cell"><pre class="code">Graphics 800,600,32,2
SetBuffer BackBuffer()

AppTitle "Map generator"

; mapwidthheight
Global mw=100
Global mh=100
;tilewidthheight
Global tw=GraphicsWidth()/mw
Global th=GraphicsHeight()/mh
;min/maxroomsizewh
Global minroomsize = 5
Global maxroomsize = 15

Dim map(mw,mh)

SeedRnd MilliSecs()

makemap

Global timer=CreateTimer(10)

While KeyDown(1) = False
	WaitTimer timer
	Cls
	drawmap
	If KeyDown(57) Or cnt&gt;20 Then newmap:cnt=0
	cnt=cnt+1
	Color 255,255,255
	Text 0,0,"Press space to generate new - esc = exit"
	Flip
Wend
End

Function newmap()
	For y=0 To mh
	For x=0 To mw
		map(x,y)=0
	Next
	Next
	makemap
End Function

Function makemap()
	map(mw/2,mh/2) = 3
	Local total=Rand(20000,150000)
	For i=0 To total
		x = Rand(maxroomsize+2,mw-(maxroomsize+2))
		y = Rand(maxroomsize+2,mh-(maxroomsize+2))
		If map(x,y) = 3
			a = Rand(0,4)
			w=Rand(minroomsize,maxroomsize)
			h=Rand(minroomsize,maxroomsize)
			Select a
				Case 0;nroom
				If fits(x-w/2,y-h,w,h-1) = True
					mr(x,y-h,x+w/2,y-h/2,x,y,x-w/2,y-h/2)
				EndIf
				Case 1;eroom
				If fits(x+1,y-h/2,w,h) = True
					mr(x+w/2,y-h/2,x+w,y,x+w/2,y+h/2,x,y)
				EndIf
				Case 2;sroom
				If fits(x-w/2,y+1,w,h) = True
					mr(x,y,x+w/2,y+h/2,x,y+h,x-w/2,y+h/2)
				EndIf
				Case 3;wroom
				If fits(x-w-1,y-h/2,w,h) = True
					mr(x-w/2,y-h/2,x,y,x-w/2,y+h/2,x-w,y)
				EndIf
			End Select
		End If
	Next
	; here we remove left over doors
	For y=2 To mh-2
	For x=2 To mw-2
		If map(x,y) = 3
			; if into darkness then remove
			If map(x-1,y) = 0 Or map(x+1,y) = 0
				map(x,y) = 2
			End If
			If map(x,y-1) = 0 Or map(x,y+1) = 0
				map(x,y) = 2
			End If
			cnt=0
			; every door if blocked remove
			For y1=y-1 To y+1
			For x1=x-1 To x+1
			If map(x1,y1) = 2 Then cnt=cnt+1
			Next
			Next
			If cnt&gt;2 Then map(x,y)=2
		End If
	Next
	Next
End Function

; makeroom
Function mr(x1,y1,x2,y2,x3,y3,x4,y4)
	For y5=y1 To y3
	For x5=x4 To x2
		map(x5,y5) = 1
	Next
	Next
	For y5=y1 To y3
		map(x4,y5) = 2
		map(x2,y5) = 2		
	Next
	For x5=x4 To x2
		map(x5,y1) = 2
		map(x5,y3) = 2
	Next
	map(x1,y1) = 3
	map(x2,y2) = 3
	map(x3,y3) = 3
	map(x4,y4) = 3

End Function

; Is there anything in the map
Function fits(x,y,w,h)
	; if outside
	If x&lt;0 Or y&lt;0 Or x+w&gt;mw Or y+h&gt;mh Then Return False	
	; if inside
	For y1=y To y+h
	For x1=x To x+w
		If map(x1,y1)&gt;0 Then Return False
	Next
	Next
	Return True
End Function

Function drawmap()
	For y=0 To mh
	For x=0 To mh
		Select map(x,y)
			Case 0;nothing
			Color 0,0,0
			Case 1;floor
			Color 255,255,255
			Case 2;wall
			Color 100,100,100
			Case 3;door
			Color 255,0,0
		End Select
		Rect x*tw,y*th,tw,th
	Next
	Next
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Andy_A</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> Nicely done.<br><br>Thanks! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rick Nasher</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> Good stuff, now it only needs to savable and cube-ified. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dna</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> Nice Job<br><br>Useful for many other things <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dan</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> Just4fun, i have written loading and saving function and while it i saw a small error in the above code:<br><br><pre class=code>
Function drawmap()
	For y=0 To mh
	For x=0 To mh
</pre><br>the second mh should be mw. ( For x=0 To mw )<br><br>Map Generator with loading and saving, make sure to change the <br><pre class=code>
Global f_path$="r:\" , f_name$="room" , f_num=0 , f_ext$=".map"
</pre><br>to wherever you want them to be saved.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Map Generator - pakz
;load/save - dan

Graphics 800,600,32,2
SetBuffer BackBuffer()

AppTitle "Map generator"

Global f_path$="r:\" , f_name$="room" , f_num=0 , f_ext$=".map"
Global key_delay=MilliSecs()
Global RN_Max=CheckFile()

; mapwidthheight
Global mw=100
Global mh=100
;tilewidthheight
Global tw=GraphicsWidth()/mw
Global th=GraphicsHeight()/mh
;min/maxroomsizewh
Global minroomsize = 5
Global maxroomsize = 15

Dim map(mw,mh)

SeedRnd MilliSecs()

makemap

Global timer=CreateTimer(10)

While KeyDown(1) = False
	WaitTimer timer
	Cls
	drawmap
	If KeyDown(57) Then newmap : AppTitle "Map generator" ;" "
	
	If KeyDown(31) And KD(200) ;"s"
		KD()
		b$=savemap() 
	EndIf
	
	If (KeyDown(38) Or KeyDown(28)) And KD(200) ;"l"
		KD()
	    If RN_Max=&gt;0 Then 
			If loadmap(lr) = 1 
			 drawmap
			 AppTitle "Viewing: "+f_path$+f_name$+getnum$(lr)+f_ext$
			EndIf
		EndIf
	EndIf
	
	If KeyDown(200) And KD(200) ;"Arrow Up"
		KD()
		LR=LR+1
		If LR&gt;RN_Max Then LR=0
	EndIf
	
	If KeyDown(208) And KD(200) ;"Arrow Down"
		KD()
		LR=LR-1
		If LR&lt;0 Then LR=RN_Max
		If lr&lt;0 Then lr=0
	EndIf
	
	
	Color 255,255,255
	Text 0,0,"Press space to generate new - esc = exit s=save L,Enter=Load map - Arrow Up-Down select"
	Text 0,14,"FileSaved="+RSet(b$,13)+"      Load room number: "+LSet(getnum$(LR),5)
    
	Flip
Wend
End

Function newmap()
	For y=0 To mh
		For x=0 To mw
			map(x,y)=0
		Next
	Next
	makemap
End Function

Function makemap()
	map(mw/2,mh/2) = 3
	Local total=Rand(20000,150000)
	For i=0 To total
		x = Rand(maxroomsize+2,mw-(maxroomsize+2))
		y = Rand(maxroomsize+2,mh-(maxroomsize+2))
		If map(x,y) = 3
			a = Rand(0,4)
			w=Rand(minroomsize,maxroomsize)
			h=Rand(minroomsize,maxroomsize)
			Select a
				Case 0;nroom
					If fits(x-w/2,y-h,w,h-1) = True
						mr(x,y-h,x+w/2,y-h/2,x,y,x-w/2,y-h/2)
					EndIf
				Case 1;eroom
					If fits(x+1,y-h/2,w,h) = True
						mr(x+w/2,y-h/2,x+w,y,x+w/2,y+h/2,x,y)
					EndIf
				Case 2;sroom
					If fits(x-w/2,y+1,w,h) = True
						mr(x,y,x+w/2,y+h/2,x,y+h,x-w/2,y+h/2)
					EndIf
				Case 3;wroom
					If fits(x-w-1,y-h/2,w,h) = True
						mr(x-w/2,y-h/2,x,y,x-w/2,y+h/2,x-w,y)
					EndIf
			End Select
		End If
	Next
	; here we remove left over doors
	For y=2 To mh-2
		For x=2 To mw-2
			If map(x,y) = 3
			; if into darkness then remove
				If map(x-1,y) = 0 Or map(x+1,y) = 0
					map(x,y) = 2
				End If
				If map(x,y-1) = 0 Or map(x,y+1) = 0
					map(x,y) = 2
				End If
				cnt=0
			; every door if blocked remove
				For y1=y-1 To y+1
					For x1=x-1 To x+1
						If map(x1,y1) = 2 Then cnt=cnt+1
					Next
				Next
				If cnt&gt;2 Then map(x,y)=2
			End If
		Next
	Next
End Function

; makeroom
Function mr(x1,y1,x2,y2,x3,y3,x4,y4)
	For y5=y1 To y3
		For x5=x4 To x2
			map(x5,y5) = 1
		Next
	Next
	For y5=y1 To y3
		map(x4,y5) = 2
		map(x2,y5) = 2		
	Next
	For x5=x4 To x2
		map(x5,y1) = 2
		map(x5,y3) = 2
	Next
	map(x1,y1) = 3
	map(x2,y2) = 3
	map(x3,y3) = 3
	map(x4,y4) = 3
	
End Function

; Is there anything in the map
Function fits(x,y,w,h)
	; if outside
	If x&lt;0 Or y&lt;0 Or x+w&gt;mw Or y+h&gt;mh Then Return False	
	; if inside
	For y1=y To y+h
		For x1=x To x+w
			If map(x1,y1)&gt;0 Then Return False
		Next
	Next
	Return True
End Function

Function drawmap()
	For y=0 To mh
		For x=0 To mw
			Select map(x,y)
				Case 0;nothing
					Color 0,0,0
				Case 1;floor
					Color 255,255,255
				Case 2;wall
					Color 100,100,100
				Case 3;door
					Color 255,0,0
			End Select
			Rect x*tw,y*th,tw,th
		Next
	Next
End Function

Function savemap$()
    Repeat
	    f_out$=f_path$+f_name$+getnum$(f_num)+f_ext$
		check=FileType(f_out$)
		If check=0
		    RN_Max=f_num
			file=WriteFile (f_out$)
			Exit
		ElseIf check=1
			f_num=f_num+1
		EndIf
		Delay 1
	Forever
	WriteByte file,mh
	WriteByte file,mw
	For y=0 To mh
		For x=0 To mw
			WriteByte file,map(x,y)
		Next
	Next
	CloseFile file
	Return f_out$
End Function

Function loadmap$(x)
    Repeat
	    f_out$=f_path$+f_name$+getnum$(x)+f_ext$
		check=FileType(f_out$)
		If check=1
			file=ReadFile (f_out$)
			Exit
		ElseIf check=0 Or check=2
			Return 0
		EndIf
		Delay 1
	Forever
	mh=ReadByte (file)
	mw=ReadByte (file)
	For y=0 To mh
		For x=0 To mw
			map(x,y)=ReadByte(file)
		Next
	Next
	CloseFile file
	Return 1
End Function

Function getnum$(number)
    ;adds "0" infront of the number, 
	b=5
	If Len(number)&lt;=b
		a$=String("0",b-Len(number))+Str$(number)
	Else
		RuntimeError "File numbers are out"
	EndIf
	Return a$
End Function

Function KD(x=0)
	;Keyboard delay
	If x&lt;=0 Then
		key_delay=MilliSecs()
	Else
		If MilliSecs()-key_delay=&gt;x Then Return 1
	EndIf 
	
	Return 0
End Function

Function CheckFile()
	;Check files for extension
	myDir=ReadDir(f_path$) 
	Local num=-1
	
	Repeat 
		
		file$=NextFile$(myDir) 
		If file$="" Then Exit 
		
		If FileType(f_path$+"\"+file$) = 1 Then 
			If Right$(file$,Len(f_ext$)) = f_ext Then num=num+1
		EndIf 
	Forever 
	
	CloseDir myDir 
	Return num
End Function
</textarea> <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
