<!DOCTYPE html><html lang="en" ><head ><title >Replaying user input using a collection of Types </title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Replaying user input using a collection of Types , language=bb, category=Algorithms'><meta name='author' content='matt!'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=6>Algorithms</a>/Replaying user input using a collection of Types </h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="354.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Replaying user input using a collection of Types  by matt!</td><td align="right">2002 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> 23/24 June 2002, version 0.10, by Matt Sephton (matt@...)<br>26/27 June 2002, version 0.20, tracks multiple computer controlled elements<br><br>to do: enable early quit of replay to go back to game<br><br>*** feel free to use this method as long as you give me credit<br><br>this method can be used to replay user input. this simple demo records the player control of a dot. we choose to do the correct branch of the main loop (moving or drawing) depending on if we are recording (game) or replaying (watching). this could be extended to keep track of multiple items, such as computer/AI controlled characters.<br><br>:) matt </td></tr><tr ><td class="cell"><pre class="code">;
; REPLAYING USER INPUT USING A COLLECTION OF TYPES
; 23/24 June 2002, version 0.10, tracks user controlled element
; 26/27 June 2002, version 0.20, tracks multiple computer controlled elements
;
; to do: enable early quit of replay to go back to game
; 
; *** by Matt Sephton (matt@ewtoo.org)
; *** feel free to use this method as long as you give me credit
;
; this method can be used to replay user input. this simple demo records the player control of a dot.
;
; we choose to do the correct branch of the main loop (moving or drawing) depending on if we are 
; recording (game) or replaying (watching).
;
; this could be extended To keep track of multiple items, such as computer/AI controlled characters.
;

AppTitle &quot;cursors to move, R to replay&quot;

Type coord		;standard element type
	Field x,y
End Type

Type complex	;complex element type
	Field x,y
	Field r		;has additional properties
End Type

Type replay		;replay data type (notice all tracked properties are combined in this one type)
	Field x,y
	Field r
End Type

Const W = 320, H = 200		;screen dimensions

ball.coord = New coord		;position player controlled element at screen centre
ball\x = W/2
ball\y = H/2

ai.complex = New complex	;ai &quot;player&quot; #1
ai\x = 180
ai\y = 180
ai\r = 4

ai2.complex = New complex	;ai &quot;player&quot; #2
ai2\x = 0
ai2\y = 0
ai2\r = 4

Graphics W,H,16,2		;gfx mode, small 16-bit window
SetBuffer BackBuffer()	;set backbuffer
ClsColor 31,127,255		;cls blue

Global replay_counter = 0		;current buffer size (number of types in collection)
Global playback_pointer = 0		;where we are in the buffer at playback
Global replaying = False		;are we replaying data or not?

Const target_fps = 24			;so we can reuse this constant

Const total_objects = 3			;total number of objects to track
Const replay_length = 5			;

;max buffer size, limited only by memory size
Const max_buffer_size = total_objects * target_fps * replay_length

fps = CreateTimer(target_fps)	;limit drawing speed to 25fps

While Not KeyDown(1)
	Cls						;clear screen
	Color 255,255,255			;draw white

	If KeyDown(19) Then			;R = replay mode on
		Delay 200	;pause
		replaying = True			;flag that we're want to go through the main loop in replay mode
	EndIf
	
	If (replaying = False)			;record mode
		;move ball with cursor keys
		ball\x = ball\x - KeyDown(203) + KeyDown(205)	;left/right
		ball\y = ball\y - KeyDown(200) + KeyDown(208)	;up/down

		;ai
		For team.complex = Each complex
			team\x = (team\x - 2) Mod 360
			team\y = (team\y - 2) Mod 360
		Next

		;limit replay data to max_buffer_size by deletingg oldest data item
		If (replay_counter &gt;= max_buffer_size)
			Delete First replay
			Delete First replay
			Delete First replay
			replay_counter = replay_counter - total_objects		;less data used
		EndIf

		;record new data item (player)
		replay.replay = New replay
		replay\x = ball\x
		replay\y = ball\y
		replay_counter = replay_counter + 1		;more data

		;record new data item (multiple ai)
		For team.complex = Each complex
			replay.replay = New replay
			replay\x = team\x
			replay\y = team\y
			replay_counter = replay_counter + 1		;more data
		Next
	
		Text 10,10,&quot;R&quot;+ replay_counter

	Else				;replay mode
		If (playback_pointer = 0) Then rd.replay = First replay	;rd = start of replay collection

		;draw replay cursor
		If (playback_pointer Mod target_fps &gt; target_fps/2) Then Text W-20,10,&quot;R&quot;
		
		If (playback_pointer &lt; replay_counter) Then			;still in replay data
			;set player draw position to be replay data
			ball\x = rd\x
			ball\y = rd\y
			rd = After rd			;go to next replay data item in collection
			playback_pointer = playback_pointer + 1

			For team.complex = Each complex
				team\x = rd\x
				team\y = rd\y
				rd = After rd							;go to next replay data item in collection
				playback_pointer = playback_pointer + 1	;move forward through replay data
			Next

		Else 												;end of the replay data
			playback_pointer = 0	;rewind pointer to beginning of replay data
			replaying = False		;turn off replay
			
			Delay 200
			SetBuffer FrontBuffer()	;show a message to make obvious the transition from replay to game
			Cls
			Text W/2,(H/2)-10,&quot;RESUMING GAME&quot;,True,True
			Delay 800
			SetBuffer BackBuffer()
		EndIf

		Text 10,10,&quot;@&quot; + playback_pointer + &quot;/&quot; + max_buffer_size

		Delay 75	;slow motion replay!
	EndIf

	Rect ball\x, ball\y, 4,4,True

	Color 196,196,196				;draw paler grey
	Oval (W/2)-48, (H/2)-48, 100,100,False

	Color 255,255,0				;draw yellow
	For team.complex = Each complex
		Oval Sin(team\x)*50+(W/2), Cos(team\y)*50+(H/2), team\r,team\r,True
	Next

	k = WaitTimer(fps)			;limit drawing speed
	
	Flip
Wend

End
;</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
