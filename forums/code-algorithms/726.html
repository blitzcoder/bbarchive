<!DOCTYPE html><html lang="en" ><head ><title >Homing physics ( 2D )</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Allow enemies to chase player, with velocity and acceleration., language=bb, category=Algorithms'><meta name='author' content='Jeppe Nielsen'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=6>Algorithms</a>/Homing physics ( 2D )</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="726.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Homing physics ( 2D ) by Jeppe Nielsen</td><td align="right">2003 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Allow enemies to chase player, with velocity and acceleration. </td></tr><tr ><td class="cell"><pre class="code">;Homing example, by Jeppe Nielsen 2003

Global playerx#
Global playery#
Global distance#=100

Type enemy

Field x#,y#
Field vx#,vy#
Field ax#,ay#

Field vmax#
Field amax#

End Type

Graphics3D 800,600,16,2

;create ten enemies at random locations
For n=1 To 10				
;play with last number
;enemynew(x,y,vmax,amax)
enemynew(Rnd(800),Rnd(600),Rnd(0.5,4.5),Rnd(0.08,0.1))
Next


Repeat
Cls

Text 10,10,"Move player with mouse"
Text 10,30,"LMB - Resize allowed distance to player"
Text 10,50,"RMB - Add enemies"

If click=0
	
	playerx=MouseX()
	playery=MouseY()
	
EndIf

If MouseDown(1) And click=0
	
	click=1
	clickx=MouseX()
	clicky=MouseY()
	
EndIf

If MouseDown(1) And click=1
	
	dx=(MouseX()-clickx)
	dy=(MouseY()-clicky)
	
	distance#=Sqr(dx*dx+dy*dy)
		
EndIf

If MouseDown(1)=False And click=1
	click=0
EndIf

If MouseDown(2)&gt;0
	enemynew(Rnd(800),Rnd(600),Rnd(2.5,2.5),Rnd(0.08,0.1))
EndIf


enemyupdate()
enemydraw()


Rect playerx-5,playery-5,10,10,0

Oval playerx-distance#,playery-distance#,distance#*2,distance#*2,0

Flip


Until KeyDown(1)
End

Function enemynew.enemy(x,y,vmax#,amax#)

e.enemy=New enemy

e\x#=x
e\y#=y

e\vmax#=vmax#
e\amax#=amax#

Return e

End Function

Function enemyupdate()

For e.enemy=Each enemy

dx#=(playerx-e\x)
dy#=(playery-e\y)

l#=Sqr(dx#*dx#+dy#*dy#)

dx#=(dx#/l#)*e\amax#
dy#=(dy#/l#)*e\amax#

;if close enough escape target
If l#&lt;=distance#
dx#=-dx#
dy#=-dy#
EndIf

;check against all other enemies, to avoid them
dxx#=0
dyy#=0
co=0
For ee.enemy=Each enemy
	
	If ee&lt;&gt;e
		
		dex#=(e\x-ee\x)
		dey#=(e\y-ee\y)
		
		l#=Sqr(dex#*dex#+dey#*dey#)
		
		dxx#=dxx#+(dex#/l#)*e\amax#
		dyy#=dyy#+(dey#/l#)*e\amax#
		
		co=co+1
	EndIf
Next

dxx#=dxx#/Float(co)
dyy#=dyy#/Float(co)

dx#=(dx#+dxx#)/2
dy#=(dy#+dyy#)/2

e\ax#=e\ax#+dx#
e\ay#=e\ay#+dy#

acc#=Sqr(e\ax#*e\ax#+e\ay#*e\ay#)

;Check if current acceleration is more than allowed
If acc#&gt;e\amax#
	
	e\ax#=(e\ax#/acc#)*e\amax
	e\ay#=(e\ay#/acc#)*e\amax
	
EndIf

e\vx#=e\vx#+e\ax#
e\vy#=e\vy#+e\ay#

vel#=Sqr(e\vx#*e\vx#+e\vy#*e\vy#)

;Check if current velocity is more than allowed
If vel#&gt;e\vmax#
	e\vx#=(e\vx#/vel#)*e\vmax
	e\vy#=(e\vy#/vel#)*e\vmax
EndIf

; add velocity to position
e\x#=e\x#+e\vx#
e\y#=e\y#+e\vy#

Next
End Function

Function enemydraw()
For e.enemy=Each enemy
	Rect e\x-3,e\y-3,6,6,0
Next
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TWH</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> <textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'Blitzmax:
Strict
'Homing example, by Jeppe Nielsen 2003
'mouse position:
Global playerx# 
Global playery#
Global distance#=100
Global enemylist:TList = CreateList()

Type enemy
	Field x#,y#
	Field vx#,vy#
	Field ax#,ay#
	Field vmax#
	Field amax#
	
	Function enemynew:enemy(x,y,vmax#,amax#)
	Local e:enemy=New enemy
	enemylist.addlast(e)
	e.x#=x
	e.y#=y
	e.vmax#=vmax#
	e.amax#=amax#
	Return e
	End Function

	Function enemydraw()
	For Local e:enemy=EachIn enemylist
		SetColor 0,0,255
		DrawRect e.x-3,e.y-3,6,6
	Next
	End Function
	
	Function enemyupdate()
		For Local e:enemy=EachIn enemylist
	
		Local dx#=(playerx-e.x)
		Local dy#=(playery-e.y)	
		Local l#=Sqr(dx#*dx#+dy#*dy#)
		dx#=(dx#/l#)*e.amax#
		dy#=(dy#/l#)*e.amax#
		
		'If close enough escape target
		If l#&lt;=distance#
		dx#=-dx#
		dy#=-dy#
		EndIf
		
		'check against all other enemies, To avoid them
		Local dxx#=0
		Local dyy#=0
		Local co=0
		For Local otherEnemy:enemy=EachIn enemylist
			If otherEnemy&lt;&gt;e
				Local dex#=(e.x-otherEnemy.x)
				Local dey#=(e.y-otherEnemy.y)
				
				'Avoid other enemies. Comment out these 3 lines to remove avoidance.
				l#=Sqr(dex#*dex#+dey#*dey#)
				dxx#=dxx#+(dex#/l#)*e.amax#
				dyy#=dyy#+(dey#/l#)*e.amax#
				
				co=co+1
			EndIf
		Next
		
		dxx#=dxx#/Float(co)
		dyy#=dyy#/Float(co)
		dx#=(dx#+dxx#)/2
		dy#=(dy#+dyy#)/2
		e.ax#=e.ax#+dx#
		e.ay#=e.ay#+dy#
		
		Local acc#=Sqr(e.ax#*e.ax#+e.ay#*e.ay#)
		'Check If current acceleration is more than allowed
		If acc#&gt;e.amax#
			e.ax#=(e.ax#/acc#)*e.amax
			e.ay#=(e.ay#/acc#)*e.amax
		EndIf
		
		e.vx#=e.vx#+e.ax#
		e.vy#=e.vy#+e.ay#
		
		Local vel#=Sqr(e.vx#*e.vx#+e.vy#*e.vy#)
		
		'Check If current velocity is more than allowed
		If vel#&gt;e.vmax#
			e.vx#=(e.vx#/vel#)*e.vmax
			e.vy#=(e.vy#/vel#)*e.vmax
		EndIf
		
		' add velocity To position
		e.x#=e.x#+e.vx#
		e.y#=e.y#+e.vy#
		
		Next
		End Function
End Type

'SetGraphicsDriver GLMax2DDriver()
Graphics 640,480

'Create ten enemies at random locations
Local n:Int
For n=1 To 10				
	'play with last number
	'enemynew(x,y,vmax,amax)
	enemy.enemynew(Rnd(800),Rnd(600),Rnd(5,5), 10)'Rnd(0.08,0.1))
Next

Local click:Int = 0
Local clickx:Int = 0
Local clicky:Int = 0
Repeat
	Cls
	DrawText "Move player with mouse",10,10
	DrawText "LMB - Resize allowed distance to player",10,30
	DrawText "RMB - Add enemies",10,50
	
	If click=0
		playerx=MouseX()
		playery=MouseY()
	EndIf
	
	If MouseDown(1) And click=0
		click=1
		clickx=MouseX()
		clicky=MouseY()
	EndIf
	
	If MouseDown(1) And click=1
		Local dx:Int=(MouseX()-clickx)
		Local dy:Int=(MouseY()-clicky)
		distance#=Sqr(dx*dx+dy*dy)
	EndIf
	
	If MouseDown(1)=False And click=1
		click=0
	EndIf
	
	If MouseDown(2)&gt;0
		enemy.enemynew(Rnd(800),Rnd(600),Rnd(2.5,2.5),Rnd(0.08,0.1))
	EndIf
	
	
	enemy.enemyupdate()
	enemy.enemydraw()
	
	'DrawRect playerx-5,playery-5,10,10
	
	SetAlpha 0.8
	SetColor 255,0,0
	SetBlend(LIGHTBLEND)
	DrawOval playerx-distance#,playery-distance#,distance#*2,distance#*2
	SetAlpha 1
	
	Flip
Until KeyDown(KEY_ESCAPE) Or AppTerminate()
End
</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >markcw</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> <a href="http://www.blitzbasic.com/faq/faq_entry.php?id=2" target="_blank">http://www.blitzbasic.com/faq/faq_entry.php?id=2</a> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> Dud! I've been here many time and I had not seen the original code. Nice. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
