<!DOCTYPE html><html lang="en" ><head ><title >filter a list using reflection</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='get all objects in a list satisfying a condition, language=bmx, category=Algorithms'><meta name='author' content='Warpy'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=6>Algorithms</a>/filter a list using reflection</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2479.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >filter a list using reflection by Warpy</td><td align="right">2009 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> I have an ambition of making a clever object database system.<br><br>This is a first step towards that - a way of filtering a list of objects according to a set of conditions written as a string in normal blitzmax-ish notation.<br><br>The idea is that the filter function returns a tquery object which you can either iterate through directly or keep for later. It only works out what belongs to the query when you evaluate it, so it's always up to date. </td></tr><tr ><td class="cell"><pre class="code">Rem
filter lists!

usage:
the filter function works on any list or tquery object, and takes a condition string and returns a tquery which is a collection of all the objects in the list satisfying the condition
conditions are pretty much as you write them in blitzmax, except I haven't made it do function calls yet

EndRem


'shunting yard algorithm - turns normal notation into RPN for easier parsing
Type shuntingyard
	Field in$
	Field out$
	Field ops$[][]
	Field token$[]
	
	Method parse$()
		While in
			nexttoken()
			Select token[0]
			Case "literal"
				output
			Case "function"
				push
			Case ","
				While ops[0][0]&lt;&gt;"("
					pop
					If token[0]&lt;&gt;"(" output
				Wend
			Case "("
				push
			Case ")"
				While pop()&lt;&gt;"("
					output
				Wend
				If Len(ops) And ops[0][0]="function"
					pop
					output
				EndIf
			Default
				If token[0][..2]="op"
					Local otoken$[]=token
					op=Int(token[0][2..])
					While Len(ops) And ops[0][0][..2]="op" And Int(ops[0][0][2..])&lt;op
						pop
						output
					Wend
					token=otoken
					push
				EndIf
			End Select
		Wend
		While Len(ops)
			pop
			output
		Wend
		Return out
	End Method
	
	Method findnexttoken$()
		i=0
		While i&lt;Len(in) And in[i]=32
			i:+1
		Wend
		in=in[i..]
		i=0
		
		While i&lt;Len(in)
			Select Chr(in[i])
			Case "!","&amp;","|","=","&lt;","&gt;","(",")",","
				If i=0
					t$=Chr(in[0])
					in=in[1..]
					Return t
				Else
					t$=in[..i]
					in=in[i..]
					Return t
				EndIf
			Case " "
				t$=in[..i]
				in=in[i..]
				Return t
			End Select
			i:+1
		Wend
		t$=in
		in=""
		Return t
	End Method
	
	Method nexttoken()
		tok$=findnexttoken()
		'Print "TOK: "+tok
		Select tok
		Case "!","not"
			token=["op2",tok]
		Case "&amp;","|","and","or"
			token=["op3",tok]
		Case "=","&lt;","&gt;"
			token=["op1",tok]
		Case "("
			token=["(","("]
		Case ")"
			token=[")",")"]
		Case ","
			token=[",",","]
		Default
			If Len(in) And Chr(in[0])="("
				token=["function",tok]
				'in=in[1..]
			Else
				token=["literal",tok]
			EndIf
		End Select
		
		'Print token[0]+" : "+token[1]
	End Method
				
	Method pop$()
		token=ops[0]
		ops=ops[1..]
		Return token[0]
	End Method
	
	Method push()
		ops=[token]+ops
	End Method	
	
	Method output()
		out:+token[1]+" "
	End Method
End Type

Function shunt$(in$)
	s:shuntingyard=New shuntingyard
	s.in=in
	Return s.parse()
End Function




'the important bit! call this with a list or a tquery, and a condition
'if you pass in a tquery, the new condition will be added to any previous ones.
Function filter:tquery(o:Object,condition$)
	condition=Trim(shunt(condition))
	'Print condition
	Local bits$[]=condition.split(" ")
	Local stack:TList=New TList
	While i&lt;Len(bits)
		Select bits[i]
		Case "!","not"
			c:tcondition=tcondition(stack.removelast())
			stack.addlast notcondition.Create(c)
		Case "&amp;","and"
			c1:tcondition=tcondition(stack.removelast())
			c2:tcondition=tcondition(stack.removelast())
			stack.addlast andcondition.Create(c1,c2)
		Case "|","or"
			c1:tcondition=tcondition(stack.removelast())
			c2:tcondition=tcondition(stack.removelast())
			stack.addlast orcondition.Create(c1,c2)
		Case "="
			value$=String(stack.removelast())
			fields$=String(stack.removelast())
			'Print fields+" = "+value
			stack.addlast eqcondition.Create(fields,value)
		Case "&lt;"
			value$=String(stack.removelast())
			fields$=String(stack.removelast())
			'Print fields+" = "+value
			stack.addlast ltcondition.Create(fields,Double(value))
		Case "&gt;"
			'Print "EQUAL"
			value$=String(stack.removelast())
			fields$=String(stack.removelast())
			'Print fields+" = "+value
			stack.addlast gtcondition.Create(fields,Double(value))
		Default
			stack.addlast bits[i]
		End Select			
		i:+1
	Wend
	c:tcondition = tcondition(stack.removelast())
	If tquery(o)
		Return tquery(o).addcondition(c)
	ElseIf TList(o)
		q:tquery=New tquery
		q.data=TList(o)
		q.condition=c
		Return q
	EndIf
End Function

'object enumerator type for tquery
Type queryenum
	Field obj:Object
	Field l:TLink
	Field query:tquery
	
	Function Create:queryenum(q:tquery)
		qe:queryenum=New queryenum
		qe.query=q
		qe.l=q.data.firstlink()
		Return qe
	End Function
	
	Method nextobject:Object()
		If obj
			d:Object=obj
			obj=Null
			Return d
		Else
			Return findnext()
		EndIf
	End Method
	
	Method hasnext()
		If obj
			Return True
		Else
			Return findnext()&lt;&gt;Null
		EndIf
	End Method
	
	Method findnext:Object()
		While l
			d:Object=l.value()
			l=l.nextlink()
			If query.contains(d)
				obj=d
				Return obj
			EndIf
		Wend
		Return Null
	End Method
End Type

'represents a condition to be applied to a list of objects
Type TQuery
	Field condition:tcondition
	Field data:TList
	
	Method objectenumerator:queryenum()
		qe:queryenum=queryenum.Create(Self)
		Return qe
	End Method
	
	Method contains(d:Object)
		'If Not data.contains(d) Return false
		If Not condition Return True
		Return condition.appliesto(d)
	End Method
	
	Method addcondition:tquery(c:tcondition)
		qe:tquery=New tquery
		qe.data=data
		If condition
			qe.condition=andcondition.Create(condition,c)
		Else
			qe.condition=c
		EndIf
		Return qe
	End Method
End Type

Type tcondition
	Method appliesto(d:Object) Abstract
End Type

Type andcondition Extends tcondition
	Field c1:tcondition,c2:tcondition
	
	Function Create:andcondition(c1:tcondition,c2:tcondition)
		ac:andcondition=New andcondition
		ac.c1=c1
		ac.c2=c2
		Return ac	
	End Function
	
	Method appliesto(d:Object)
		If c1.appliesto(d) And c2.appliesto(d)
			Return True
		Else
			Return False
		EndIf
	End Method
End Type

Type orcondition Extends tcondition
	Field c1:tcondition,c2:tcondition
	
	Function Create:orcondition(c1:tcondition,c2:tcondition)
		oc:orcondition=New orcondition
		oc.c1=c1
		oc.c2=c2
		Return oc	
	End Function
	
	Method appliesto(d:Object)
		If c1.appliesto(d) Return True
		If c2.appliesto(d) Return True
		Return False
	End Method
End Type

Type notcondition Extends tcondition
	Field c:tcondition
	
	Function Create:notcondition(c:tcondition)
		oc:notcondition=New notcondition
		oc.c=c
		Return oc	
	End Function
	
	Method appliesto(d:Object)
		Return (Not c.appliesto(d))
	End Method
End Type

Type fieldcondition Extends tcondition
	Field fields$[]
	
	Method getfield:Object(d:Object)
		i=0
		While i&lt;Len(fields)
			d=TTypeId.ForObject(d).findField(fields[i]).get(d)
			i:+1
		Wend
		Return d
	End Method
End Type	

Type eqcondition Extends fieldcondition
	Field value$
	
	Function Create:eqcondition(fields$,value$)
		ec:eqcondition=New eqcondition
		ec.fields=fields.split(".")
		ec.value=value
		Return ec
	End Function

	Method appliesto(d:Object)
		'Print " ".join(fields)+" = "+value+"? "+(String(getfield(d))=value)
		Return String(getfield(d))=value
	End Method
End Type

Type ltcondition Extends fieldcondition
	Field value:Double
	Function Create:ltcondition(fields$,value:Double)
		lc:ltcondition=New ltcondition
		lc.fields=fields.split(".")
		lc.value=value
		Return lc
	End Function

	Method appliesto(d:Object)
		Return Double(String(getfield(d)))&lt;value
	End Method
End Type

Type gtcondition Extends fieldcondition
	Field value:Double
	Function Create:gtcondition(fields$,value:Double)
		gc:gtcondition=New gtcondition
		gc.fields=fields.split(".")
		gc.value=value
		Return gc
	End Function

	Method appliesto(d:Object)
		Return Double(String(getfield(d)))&gt;value
	End Method
End Type





'example

Type dude
	Field name$
	Field number
	Function Create:dude(name$,number)
		d:dude=New dude
		d.name=name
		d.number=number
		Return d
	End Function
End Type

l:TList=New TList
l.addlast dude.Create("Jim",1)
l.addlast dude.Create("Mike",2)
l.addlast dude.Create("Bubba",3)

Print "DUDES~n------------"
For d:dude=EachIn l
	Print d.number+": "+d.name
Next
Print "------------~n"

Print "LAZY EVALUATION"
condition$="number=1"
q:tquery=filter(l,condition)
Print "dudes where "+condition
For d:dude=EachIn q
	Print d.name
Next
Print "~n(add 1: Wanda)~n"
l.addlast dude.Create("Wanda",1)
Print "dudes where "+condition
For d:dude=EachIn q
	Print d.name
Next

Print "~n~nnow you try!~n"
While 1
	condition$=Input("condition&gt; ")
	'Print condition
	q:tquery=filter(l,condition)
	For d:dude=EachIn q
		Print d.number+": "+d.name
	Next
Wend</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >N</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> Nifty. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
