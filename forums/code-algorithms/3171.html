<!DOCTYPE html><html lang="en" ><head ><title >Single Byte Arrays</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Memory Efficient Single-Byte Arrays Using Banks, language=bb, category=Algorithms'><meta name='author' content='_PJ_'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=6>Algorithms</a>/Single Byte Arrays</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="3171.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Single Byte Arrays by _PJ_</td><td align="right">2015 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> All numerical values stored in Blitz variables are stored as signed, long Integers. This means, each uses 4 bytes of memory even if the value of the variable never requires more than a single byte.<br>Also, typically, Blitz Arrays are compiled first and memory reserved even if they are never fully used.<br><br>The code here can be used instead of the Dim() function or even Global BlitzArray[] function to create single or two dimensional arrays where the array elements each use up only a single byte.<br><br>Applications may be for example, A* pathfinding cell data or character ability stats etc. </td></tr><tr ><td class="cell"><pre class="code">;Single-Byte Array functions by PJ 2015


;Example:

a=DefineArray(16777215)
Print "Please check memory"
WaitKey()
FreeArray a

Dim b(16777215)
Print "Please check memory again"
WaitKey()
End





;Declarations

Type Array
	Field ArrayBankHandle
	Field ArrayX
	Field ArrayY
End Type

;Functions
Function DefineArray(x=1,y=1)
	y=Abs(y)
	x=Abs(x)
	
	If (Not (x Or y))
		RuntimeError "Array must have dimensions"
	End If
	
	Local NewArray.Array=New Array
	NewArray\ArrayX=x
	NewArray\ArrayY=y
	NewArray\ArrayBankHandle=CreateBank(x*y)
	Return Handle(NewArray)
End Function

Function SetArrayElement(Array, Value, x=1, y=1)
	Local Instance.Array=Object.Array(Array)
	Local Reference=ArrayReference(x,y,Instance\ArrayX)
	PokeByte Instance\ArrayBankHandle,Reference,Value
End Function

Function GetArrayElement(Array,x=1,y=1)
	Local Instance.Array=Object.Array(Array)
	Local Reference=ArrayReference(x,y,Instance\ArrayX)
	Local Element=PeekByte(Instance\ArrayBankHandle,Reference)
	Return Element
End Function

Function ResizeArray(Array,x,y=1)
	Local Instance.Array=Object.Array(Array)
	
	Local OldSize=BankSize(Instance\ArrayBankHandle)
	Local NewSize=(x*y)
	
	If (NewSize=OldSize)
		Return
	End If
	
	If (NewSize&lt;OldSize)
		Local Byte
		For Byte=NewSize To OldSize-1
			PokeByte Instance\ArrayBankHandle,Byte,0
		Next
		OldSize=NewSize
	End If
	
	Local NewBank=CreateBank(NewSize)
	CopyBank Instance\ArrayBankHandle,0,NewBank,0,OldSize-1
	
	FreeBank Instance\ArrayBankHandle
	
	Instance\ArrayBankHandle=NewBank
	
	Instance\ArrayX=x
	Instance\ArrayY=y
	
End Function

Function FreeArray(Array)
	Local Instance.Array=Object.Array(Array)
	FreeBank Instance\ArrayBankHandle
	Instance\ArrayX=0
	Instance\ArrayY=0
	Delete Instance
End Function

Function ArrayReference(x,y,xmax)
	Local Ref=y-1
	Ref=Ref*xmax
	Ref=Ref+x
	Ref=Ref-1
	Return Ref
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>2015</font></td></tr></table></td></tr><tr ><td class="posttext"> A more complete version, allowing for short (2-byte) arrays as well as resizeable integer arrays.<br><br><pre class=code>
;Single, Double Byte Arrays
;PJ 2015

Type Array
	Field Bank
	Field Width
	Field X
	Field Y
End Type

Function DefineArray(Width=1,x=1,y=1)
	y=Abs(y)
	x=Abs(x)
	
	If (Not (x Or y))
		RuntimeError "Array must have dimensions"
	End If
	
	Local NewArray.Array=New Array
	NewArray\Width=Width
	NewArray\X=x
	NewArray\Y=y
	NewArray\Bank=CreateBank(x*y*Width)
	Return Handle(NewArray)
End Function

Function SetArrayElement(Array, Value, x=1, y=1)
	Local Instance.Array=Object.Array(Array)
	Local Offset=(((y-1) * (Instance\X*Instance\Width))+(x*Instance\Width))-1
	
	Select (Instance\Width)
		Case 1:
			PokeByte Instance\Bank,Offset,Value And 255
		Case 2:
			PokeShort Instance\Bank,Offset,Value And 65535
	;	Case 3:
	;		PokeByte Instance\Bank,Offset,(Value Shr 24) And 255
	;		PokeShort Instance\Bank,Offset+1,Value And 65535
		Case 4:
			PokeInt Instance\Bank,Offset,Value
		Default:
			;Invalid Width
	End Select
End Function

Function GetArrayElement(Array,x=1,y=1)
	Local Instance.Array=Object.Array(Array)
	Local Offset=(((y-1) * (Instance\X*Instance\Width))+(x*Instance\Width))-1
	
	Local Element
	Select (Instance\Width)
		Case 1:
			Element=PeekByte(Instance\Bank,Offset) And 255
		Case 2:
			Element=PeekShort(Instance\Bank,Offset) And 65535
	;	Case 3:
	;		Element=PeekByte(Instance\Bank,Offset) Shl 24
	;		Element=Element+(PeekShort(Instance\Bank,Offset+1) And 65535)
		Case 4:
			Element=PeekInt(Instance\Bank,Offset)
		Default:
			;Invalid Width
	End Select
	
	Return Element
End Function

Function ResizeArray(Array,x,y=1)
	Local Instance.Array=Object.Array(Array)
	
	Local OldSize=BankSize(Instance\Bank)
	Local NewSize=(x*y*Instance\Width)
	
	If (NewSize=OldSize)
		Return
	End If
	
	If (NewSize&lt;OldSize)
		Local Byte
		For Byte=NewSize To OldSize-1
			PokeByte Instance\Bank,Byte,0
		Next
		OldSize=NewSize
	End If
	
	Local NewBank=CreateBank(NewSize)
	CopyBank Instance\Bank,0,NewBank,0,OldSize-1
	
	FreeBank Instance\Bank
	
	Instance\Bank=NewBank
	
	Instance\X=x
	Instance\Y=y
	
End Function

Function FreeArray(Array)
	Local Instance.Array=Object.Array(Array)
	FreeBank Instance\Bank
	Instance\X=0
	Instance\Y=0
	Instance\Width=0
	Delete Instance
End Function</pre> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >virtlands</td><td align="right"><font class=tiny>2015</font></td></tr></table></td></tr><tr ><td class="posttext"> This is fascinating...<br><br>I added a 3rd case, where one assumes to read + write the byte values directly from an INT array,  C(..),  in byte fashion.<br><br>Here are memory usage statistics from your program, ( as studied from Task Manager, or otherwise... ). <br><br>1st case, using <b>a=DefineArray(16777215)</b>,  :: memory usage = ~ 1 byte  required to store 1 byte <br>2nd case, using <b>Dim b(16777215)</b>,          :: memory usage = ~ 4 bytes required to store 1 byte<br>3rd case, using <b>Dim c(4194304)</b>,           :: memory usage = ~ 1 byte  required to store 1 byte<br><br>----&gt; Dim C(4194304) is sufficient for storing 16777216 BYTES.<br><br>;;; -_- There is some compact math here;  Took me about 2 hours to get it working... I'm still shocked this works... : <img src="http://3.bp.blogspot.com/-UrCVd-uHlgY/UZFSnLF4K1I/AAAAAAAADoY/dIzdwC608pY/s1600/confused-emoticon-wtf-symbol-for-facebook.png"><br><br><pre class=code> ;; Coded by VirtLands,  { Jan 06, 2015 }

 Graphics 500,640,32,2
 Print
 Print

 Global ByteMask[4]

 ByteMask[0] = $FFFFFF00
 ByteMask[1] = $FFFF00FF
 ByteMask[2] = $FF00FFFF
 ByteMask[3] = $00FFFFFF

 ;;  Use an  INT array, and access it at BYTE offsets ... ( As if we were accessing a BYTE array )

 Dim C(4194304)   ;;   Create an array with enough room for 16777216 bytes.

 ;;____  ( 4194304 * 4 = 16777216 )

 ;;____  Do some testing to prove that the functions work.

 For z = 0 To 20
           StoreByte_at_index( z*2, z )       ;; Take the first 21 numbers and multiply x 2.            
 Next 

 For z = 0 To 20       
           Print " z = "+z+" : "+ GetByte_at_index( z )  ;;; .. Read back the results ..
 Next 

 Print 

 For z = 0 To 20
           StoreByte_at_index( z+100, z )     ;; Take the first 21 numbers and add +100.
 Next 

 For z = 0 To 20          
           Print " z = "+z+" : "+ GetByte_at_index( z )  ;;; .. Read back the results ..
 Next 

 WaitKey():End 


;; ... Stores a byte value at a byte index,  ( starting at offset zero, and using Little-Endian storage methods ) 
Function StoreByte_at_index( byte, index )

        div  = index Shr 2    ;; divide it by 4
        rem = index Mod 4  ;; find the remainder

        v = C(div)                 ;; read the current INT C(..) value

        v = v And ByteMask[rem]       ;; Clear the target byte to zero.

        v = v Or byte Shl (rem Shl 3)  ;; Update that byte to a new value.  

        C(div) = v   ;; upate that INT memory.

End Function 

;; ... Retrieves a byte value, using a byte offset index,  (starting at offset zero). 
Function GetByte_at_index%( index )

      div = index Shr 2
      rem = index Mod 4

      v = C(div)     
      v = (v Shr (rem Shl 3)) And $FF   ;; .... 0_0 

      Return v

End Function </pre> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>2015</font></td></tr></table></td></tr><tr ><td class="posttext"> Interesting - the reason I didn't bother making a facility for 4xByte integers was because of Blitz always using 32-bit signed int's for its integers therebye using all 4bytes anyway.<br><br>There might be a danger with trying to vary sizes of overwriting another value if you exceed the 8th bit of the high byte... <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
