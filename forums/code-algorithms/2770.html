<!DOCTYPE html><html lang="en" ><head ><title >Elite Planet Name Generator</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='The algorithm the game 'Elite' used to create planet names, language=bb, category=Algorithms'><meta name='author' content='Krischan'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=6>Algorithms</a>/Elite Planet Name Generator</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2770.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Elite Planet Name Generator by Krischan</td><td align="right">2010 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This small code creates random but predictable planet names using the algorithm from the game "Elite" from 1984 (as an example, the start planet "Lave" is Nr. 7!). Here the first 20 planet names from Elite (Galaxy #1) using its seed (use elite$ variable = False to create completely different names):<br><br>Tibedied Qube Leleer Biarge Xequerin Tiraor Rabedira Lave Zaatxe Diusreza Teaatis Riinus Esbiza Ontimaxe Cebetela Ceedra Rizala Atriso Teanrebi Azaqu Retila<br><br>For further investigation I suggest the <a href="http://wiki.alioth.net/index.php/Oolite_planet_list" target="_blank">Oolite Planet List</a> </td></tr><tr ><td class="cell"><pre class="code">AppTitle "Elite Planet Name Generator"

Global seed%[2],elite$=True
Global syllables$="..lexegezacebisousesarmaindirea.eratenberalavetiedorquanteisrion"

If (elite) Then
    seed[0]=$5A4A
    seed[1]=$0248
    seed[2]=$B753
Else
    seed[0]=Rand($ffff)
    seed[1]=Rand($ffff)
    seed[2]=Rand($ffff)
    
EndIf

For i=0 To 20
    
    Local output$=CreateName()
    
    If output="Lave" Or output="Zaonce" Or output="Isinor" Then
        Color 0,255,0
        Print output+" ["+i+"]"
    Else
        Color 255,255,255
        Print output
    EndIf
    
Next


WaitKey
End

Function Tweakseed()
    
    Local temp%=(seed[0]+seed[1]+seed[2]) Mod $10000
    seed[0]=seed[1]
    seed[1]=seed[2]
    seed[2]=temp
    
End Function

Function CreateName$()
    
    Local longnameflag=seed[0] And $40
    Local planetname$=""
    Local c%,n%,d%
    
    For n=0 To 3
        
        d=((seed[2] Shr 8) And $1f) Shl 1
        Tweakseed()
        
        If n&lt;3 Or longnameflag Then
			
            planetname=planetname+Mid(syllables,1+d,2)
            planetname=Replace(planetname,".","")
            
        EndIf
        
    Next
    
    planetname=Upper(Mid(planetname,1,1))+Mid(planetname,2,Len(planetname)-1)
    
    Return planetname
    
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >puki</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> God that is exciting.  I never knew they did it that way.  I added a cursory SeedRnd MilliSecs() to it (just to see what would happen). <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >*</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> Lave is in the right place seeing as the planet list goes from 0.. so position 8 is actually 7 :) <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> Just to entertain the audience, here is a complete Planet generator using the Elite algorithm, including<br><br>- original Elite planet names<br>- galaxy map image<br>- position in XYZ space<br>- government type<br>- economy<br>- tech level<br>- population<br>- inhabits description<br>- planet radius<br>- productivity<br>- and the local stock market<br><br><img src="http://www.christianhart.de/bb/elite/elite.png"><br><br>Have fun to play around with it. The only thing I failed is the famous "goatsoup" description of the local special feature of the planet. This is far too complex to me.<br><br>But for the moment have fun to play around with:<br><br><b>Update 1. Now you can switch very easy through all galaxies/planets using the arrow keys.</b><br><br><b>Update 2. Fixed a bug and added market quantities. Changing market prices can be achieved to call fluct=CRAND()</b><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Const border%  = 5     ; galaxy map border
Const width%   = 400   ; galaxy map width
Const height%  = 400   ; galaxy map height
Const highrad% = 5     ; planet highlight radius
Const elite$   = True  ; use original Elite seed or not
Const rseed%   = 42    ; random seed for non-Elite galaxy

Local galaxy%   = 1    ; set galaxy (1...8)
Local planet%   = 7    ; set planet (0...255)

Global seed%[2],units$[2]
Global nxt%=12345
Global syllables$="..lexegezacebisousesarmaindirea.eratenberalavetiedorquanteisrion"
Global fluct%

units[0]="t ":units[1]="kg":units[2]="g "

Local i%,font%
Local p.planet
Local c.commodity

Type planet
    
    Field galaxy%
    Field planet%
    Field name$
    Field x#,y#,z#
    Field gov%
    Field eco%
    Field tech%
    Field pop#
    Field radius%
    Field prod%
    Field inhabit$
    Field comm$[16]
    Field price#[16]
    Field unit%[16]
    Field quantity%[16]
    
End Type

Type commodity
    
    Field baseprice%
    Field gradient%
    Field basequant%
    Field mask%
    Field unit%
    Field name$
    
End Type

; read commodity data
Restore commodities
For i=0 To 16
    
    c.commodity = New commodity
    Read c\baseprice,c\gradient,c\basequant,c\mask,c\unit,c\name
    
Next

Graphics 800,440,32,2

; dynamic market
fluct=CRAND()

; initialize
Local map%=GenerateMap(galaxy)
p=CreatePlanet(galaxy,planet)
font=LoadFont("Blitz",13) : SetFont font

While Not KeyHit(1)
    
    Cls
    
    ; store current galaxy/planet
    Local oldgalaxy%=galaxy
    Local oldplanet%=planet
    
    ; arrows = change galaxy/planet
    galaxy=galaxy+KeyHit(200)-KeyHit(208)
    planet=planet+KeyHit(205)-KeyHit(203)
    
    ; limit galaxy/planet
    If galaxy&lt;1 Then galaxy=8 Else If galaxy&gt;8 Then galaxy=1
    If planet&lt;0 Then planet=255 Else If planet&gt;255 Then planet=0
    
    ; galaxy or planet has changed? recalculate current galaxy/planet
    If galaxy&lt;&gt;oldgalaxy Or planet&lt;&gt;oldplanet Then
        map%=GenerateMap(galaxy)
        p=CreatePlanet(galaxy,planet)
    EndIf
    
    ; show current planet on map
    HighlightPlanet(p.planet,highrad,(2*border)+border,(2*border)+border)
    
    ; draw map
    DrawImage map,(2*border),(2*border)
    
    ; output data
    PlanetData(p,435,(2*border))
    MarketData(p,435,(2*border)+156)
    
    Flip
    
    AppTitle "Elite Planet Generator - Galaxy #"+galaxy
    
Wend

End

End

Function Normalize#(value#=128.0,value_min#=0.0,value_max#=255.0,norm_min#=0.0,norm_max#=1.0)
    
    Return ((value#-value_min#)/(value_max#-value_min#))*(norm_max#-norm_min#)+norm_min#
    
End Function

; show current planet position on map
Function HighlightPlanet(p.planet,size%=5,offx%=0,offy%=0)
    
    Color 0,255,0
    Oval offx+(Normalize(p\x,0,255,0,width-1))-size,offy+(Normalize(p\z,0,255,0,height-1))-size,(size*2)+1,(size*2)+1,0
    
End Function

; set elite or random galaxy (always with same rseed seed)
Function Initseed()
    
    SeedRnd rseed
    
    If (elite) Then
        seed[0]=$5A4A
        seed[1]=$0248
        seed[2]=$B753
    Else
        seed[0]=Rand($ffff)
        seed[1]=Rand($ffff)
        seed[2]=Rand($ffff)
    EndIf
    
End Function

; output planet data
Function PlanetData(p.planet,offx%=0,offy%=0)
    
    Color 255,255,255
    
    Text offx,offy+  0,"Galaxy:Planet:  "+p\galaxy+":"+p\planet
    Text offx,offy+ 15,"Planet.......:  "+p\name
    Text offx,offy+ 30,"Position XYZ.:  "+ExactInt(p\x)+","+ExactInt(p\y)+","+ExactInt(p\z)
    Text offx,offy+ 45,"Government...:  "+Government(p\gov)
    Text offx,offy+ 60,"Economy......:  "+Economy(p\eco)
    Text offx,offy+ 75,"Tech Level...:  "+p\tech
    Text offx,offy+ 90,"Population...:  "+p\pop+" Billion"
    Text offx,offy+105,"Inhabits.....:  "+p\inhabit
    Text offx,offy+120,"Planet Radius:  "+p\radius+"km"
    Text offx,offy+135,"Productivity.:  "+p\prod
    
End Function

; output current planet market data
Function MarketData(p.planet,offx%=0,offy%=0)
    
    Local i%
    
    Color 255,255,255
    
    For i=0 To 16
        
        Text offx,offy+(i*15),p\comm[i]+" :  "+AddSpaces(p\quantity[i],3)+units[p\unit[i]]+" "+AddSpaces(p\price[i],3)+"/"+units[p\unit[i]]
        
    Next
    
End Function

; add spaces in front of a string
Function AddSpaces$(value$,num%)
    
    Local p%=Instr(value,".")
    
    If p&gt;0 Then p=num-p+1 Else p=num-Len(value)
    
    Local add$,i%
    
    For i=1 To p
        
        add=add+" "
        
    Next
    
    
    Return add+value
    
End Function

; better INT()
Function ExactInt%(x#)
    
    If x&gt;=0 Then Return Floor(x) Else Return Ceil(x)
    
End Function

; shuffle seed
Function Tweakseed()
    
    Local temp%=(seed[0]+seed[1]+seed[2]) Mod $10000
    seed[0]=seed[1]
    seed[1]=seed[2]
    seed[2]=temp
    
End Function

; set current galaxy
Function SetGalaxy(num%=1)
    
    Local L1%=(seed[0] Shr 8)
    Local R1%=(seed[0] And $ff)
    
    Local L2%=(seed[1] Shr 8)
    Local R2%=(seed[1] And $ff)
    
    Local L3%=(seed[2] Shr 8)
    Local R3%=(seed[2] And $ff)
    
    seed[0]=ExactInt(L1/2^(9-num)+(L1 Mod 2^(9-num)*(2^(num-1))))*256+ExactInt(R1/2^(9-num)+(R1 Mod 2^(9-num)*(2^(num-1))))
    seed[1]=ExactInt(L2/2^(9-num)+(L2 Mod 2^(9-num)*(2^(num-1))))*256+ExactInt(R2/2^(9-num)+(R2 Mod 2^(9-num)*(2^(num-1))))
    seed[2]=ExactInt(L3/2^(9-num)+(L3 Mod 2^(9-num)*(2^(num-1))))*256+ExactInt(R3/2^(9-num)+(R3 Mod 2^(9-num)*(2^(num-1))))
    
End Function

; set current planet
Function SetPlanet(num%=0)
    
    Local i%
    
    For i=0 To num-1
        
        Tweakseed()
        Tweakseed()
        Tweakseed()
        Tweakseed()
        
    Next
    
End Function

; generate galaxy map image
Function GenerateMap(galaxy%)
    
    Local i%,rgb%
    Local image%=CreateImage(width+(2*border),height+(2*border))
    Local buffer%=ImageBuffer(image)
    
    Initseed()
    
    SetBuffer buffer
    LockBuffer buffer
    
    SetGalaxy(galaxy)
    
    For i=0 To 255
        
        Local p.planet = New planet
        
        p\x=(seed[1]/256)
        p\z=(seed[0]/256)
        p\y=128-(p\z/2)
        
        Tweakseed()
        Tweakseed()
        Tweakseed()
        Tweakseed()
        
        rgb=255*$10000+255*$100+255
        WritePixelFast (Normalize(p\x,0,255,0,width-1))+border,(Normalize(p\z,0,255,0,height-1))+border,rgb
        
    Next
    
    UnlockBuffer buffer
    
    Color 64,64,64
    Rect 0,0,width+(2*border),height+(2*border),0
    
    SetBuffer BackBuffer()
    
    Return image
    
End Function

; get planet data
Function CreatePlanet.planet(galaxy%,planet%)
    
    Local m%,c.commodity,product%,changing%,q%
    
    Initseed()
    
    SetGalaxy(galaxy)
    SetPlanet(planet)
    
    Local p.planet = New planet
    
    p\galaxy=galaxy
    p\planet=planet
    
    Local longnameflag=seed[0] And $40
    Local n%,d%,tmp%
    Local i1$,i2$,i3$,i4$
    Local v1%,v2%,v3%,v4%
    
    ; position
    p\x=(seed[1]/256)
    p\z=(seed[0]/256)
    p\y=128-(p\z/2)
    
    ; government
    p\gov=((seed[1] Mod 256)/8) Mod 8
    
    ; economy
    tmp=(seed[0]/256) Mod 8
    If p\gov&lt;2 Then p\eco=(tmp/4)*4+2+(tmp Mod 2) Else p\eco=tmp
    
    ; tech level
    p\tech=(1-(p\eco/4))*4+(1-((p\eco/2) Mod 2))*2+1-(p\eco Mod 2)+((seed[1]/256) Mod 4)+(p\gov Mod 2)+(p\gov/2)+1
    
    ; population in billions
    p\pop=(p\tech*4+p\gov+p\eco+1)/10.0
    
    ; productivity
    p\prod=((1-(p\eco/4))*4+(1-((p\eco/2) Mod 2))*2+1-(p\eco Mod 2)+3)*(p\gov+4)*p\pop*80
    
    ; planet radius
    p\radius=(((seed[2]/256) Mod 16)+11)*256+(seed[1]/256)
    
    ; inhabits name
    tmp=((((seed[0]/256) Mod 2)+((seed[1]/256) Mod 2)) Mod 2)+(((((seed[0]/512) Mod 2)+((seed[1]/512) Mod 2)) Mod 2)*2)+(((((seed[0]/1024) Mod 2)+((seed[1]/1024) Mod 2)) Mod 2)*4)
    v1=(ExactInt((seed[2]/256)/4) Mod 8)+1
    v2=(ExactInt((seed[2]/256)/32))+1
    v3=tmp+1
    v4=((tmp+(seed[2]/256) Mod 4) Mod 8)+1
    i1=IN1(MIN(v1,4))
    i2=IN2(MIN(v2,7)) : If i2&lt;&gt;"" Then i2=" "+i2
    i3=IN3(MIN(v3,7)) : If i3&lt;&gt;"" Then i3=" "+i3
    i4=IN4(v4) : If i4&lt;&gt;"" Then i4=" "+i4
    If (seed[2] Mod 256)&lt;127 Then p\inhabit="Human Colonists" Else p\inhabit=Trim(i1+i2+i3+i4)
    
    ; planet name
    For n=0 To 3
        
        d=(((seed[2] Shr 8) And $1f) Shl 1)
        
        Tweakseed()
        
        If n&lt;3 Or longnameflag Then p\name=Replace(p\name+Mid(syllables,1+d,2),".","")
        
    Next
    p\name=Upper(Mid(p\name,1,1))+Mid(p\name,2,Len(p\name)-1)
    
    m=0
    
    ; calculate market prices and quantities
    For c.commodity = Each commodity
        
        product=p\eco*c\gradient
        changing=fluct And c\mask
        q=c\basequant+changing-product
        q=q And $FF
        If q And $80 Then q=0
        p\quantity[m]=q And $3f
        
        q=c\baseprice+changing+product
        q=q And $ff
        
        p\comm[m]=c\name
        p\price[m]=(q*4)/10.0
        p\unit[m]=c\unit
        
        ; alien items always 0, only sellable
        If m=16 Then p\quantity[m]=0
        
        m=m+1
        
    Next
    
    Return p
    
End Function

; return the smaller value out of two
Function MIN%(num1%,num2%)
    
    If num1&lt;num2 Then Return num1 Else Return num2
    
End Function

; first inhabitant part
Function IN1$(num%)
    
    Select num
            
        Case 1:        Return "Large"
        Case 2:        Return "Fierce"
        Case 3:        Return "Small"
            
    End Select
    
End Function

; second inhabitant part
Function IN2$(num%)
    
    Select num
            
        Case 1:        Return "Green"
        Case 2:        Return "Red"
        Case 3:        Return "Yellow"
        Case 4:        Return "Blue"
        Case 5:        Return "Black"
        Case 6:        Return "Harmless"
            
    End Select
    
End Function

; third inhabitant part
Function IN3$(num%)
    
    Select num
            
        Case 1:        Return "Slimy"
        Case 2:        Return "Bug-Eyed"
        Case 3:        Return "Horned"
        Case 4:        Return "Bony"
        Case 5:        Return "Fat"
        Case 6:        Return "Furry"
            
    End Select
    
End Function

; fourth inhabitant part
Function IN4$(num%)
    
    Select num
            
        Case 1:        Return "Rodents"
        Case 2:        Return "Frogs"
        Case 3:        Return "Lizards"
        Case 4:        Return "Lobsters"
        Case 5:        Return "Birds"
        Case 6:        Return "Humanoids"
        Case 7:        Return "Felines"
        Case 8:        Return "Insects"
            
    End Select
    
End Function

; government list
Function Government$(num%)
    
    Select num
            
        Case 0:        Return "Anarchy"
        Case 1:        Return "Feudal"
        Case 2:        Return "Multi-Government"
        Case 3:        Return "Dictatorship"
        Case 4:        Return "Communist"
        Case 5:        Return "Confederacy"
        Case 6:        Return "Democracy"
        Case 7:        Return "Corporate State"
            
    End Select
    
End Function

; economy list
Function Economy$(num%)
    
    Select num
            
        Case 0:        Return "Rich Industrial"
        Case 1:        Return "Average Industrial"
        Case 2:        Return "Poor Industrial"
        Case 3:        Return "Mainly Industrial"
        Case 4:        Return "Mainly Agricultural"
        Case 5:        Return "Rich Agricultural"
        Case 6:        Return "Average Agricultural"
        Case 7:        Return "Poor Agricultural"
            
    End Select
    
End Function

; BBC Micro Rand compatibility
Function CRAND%()
    
    nxt=(213013*nxt+2531011)
    
    Return (nxt Shr 16) And $7fff
    
End Function

.commodities
Data $13,-$02,$06,$01,0,"Food        "
Data $14,-$01,$0A,$03,0,"Textiles    "
Data $41,-$03,$02,$07,0,"Radioactives"
Data $28,-$05,$E2,$1F,0,"Slaves      " ; illegal
Data $53,-$05,$FB,$0F,0,"Liquor/Wines"
Data $C4,+$08,$36,$03,0,"Luxuries    "
Data $EB,+$1D,$08,$78,0,"Narcotics   " ; illegal
Data $9A,+$0E,$38,$03,0,"Computers   "
Data $75,+$06,$28,$07,0,"Machinery   "
Data $4E,+$01,$11,$1F,0,"Alloys      "
Data $7C,+$0d,$1D,$07,0,"Firearms    " ; illegal
Data $B0,-$09,$DC,$3F,0,"Furs        "
Data $20,-$01,$35,$03,0,"Minerals    "
Data $61,-$01,$42,$07,1,"Gold        "
Data $AB,-$02,$37,$1F,1,"Platinum    "
Data $2D,-$01,$FA,$0F,2,"Gem-Stones  "
Data $35,+$0F,$C0,$07,0,"Alien Items "</textarea><br><br>If anybody feels like adding the goatsoup to it, here is my data field and <a href="http://automaticromantic.com/static/misc/pytxtelite.txt" target="_blank">a link to a python version of the Elite source</a>:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">.goatsoup
Data "$81","fabled"                    ,"notable"                    ,"well known"                ,"famous"                            ,"noted"
Data "$82","very"                    ,"mildly"                    ,"most"                        ,"reasonably"                        ,""
Data "$83","ancient"                ,"$95"                        ,"great"                    ,"vast"                                ,"pink"
Data "$84","$9E $9D plantations"    ,"mountains"                ,"$9C"                        ,"$94 forests"                        ,"oceans"
Data "$85","shyness"                ,"silliness"                ,"mating traditions"        ,"loathing of $86"                    ,"love for $86"
Data "$86","food blenders"            ,"tourists"                    ,"poetry"                    ,"discos"                            ,"$8E"
Data "$87","talking tree"            ,"crab"                        ,"bat"                        ,"lobst"                            ,"$B2"
Data "$88","beset"                    ,"plagued"                    ,"ravaged"                    ,"cursed"                            ,"scourged"
Data "$89","$96 civil war"            ,"$9B $98 $99s"                ,"a $9B disease"            ,"$96 earthquakes"                    ,"$96 solar activity"
Data "$8A","its $83 $84"            ,"the $B1 $98 $99"            ,"its inhabitants' $9A $85"    ,"$A1"                                ,"its $8D $8E"
Data "$8B","juice"                    ,"brandy"                    ,"water"                    ,"brew"                                ,"gargle blasters"
Data "$8C","$B2"                    ,"$B1 $99"                    ,"$B1 $B2"                    ,"$B1 $9B"                            ,"$9B $B2"
Data "$8D","fabulous"                ,"exotic"                    ,"hoopy"                    ,"unusual"                            ,"exciting"
Data "$8E","cuisine"                ,"night life"                ,"casinos"                    ,"sit coms"                            ," $A1 "
Data "$8F","$B0"                    ,"The planet $B0"            ,"The world $B0"            ,"This planet"                        ,"This world"
Data "$90","n unremarkable"            ," boring"                    ," dull"                    ," tedious"                            ," revolting"
Data "$91","planet"                    ,"world"                    ,"place"                    ,"little planet"                    ,"dump"
Data "$92","wasp"                    ,"moth"                        ,"grub"                        ,"ant"                                ,"$B2"
Data "$93","poet"                    ,"arts graduate"            ,"yak"                        ,"snail"                            ,"slug"
Data "$94","tropical"                ,"dense"                    ,"rain"                        ,"impenetrable"                        ,"exuberant"
Data "$95","funny"                    ,"wierd"                    ,"unusual"                    ,"strange"                            ,"peculiar"
Data "$96","frequent"                ,"occasional"                ,"unpredictable"            ,"dreadful"                            ,"deadly"
Data "$97","$82 $81 for $8A"        ,"$82 $81 for $8A and $8A"    ,"$88 by $89"                ,"$82 $81 for $8A but $88 by $89"    ,"a$90 $91"
Data "$98","$9B"                    ,"mountain"                    ,"edible"                    ,"tree"                                ,"spotted"
Data "$99","$9F"                    ,"$A0"                        ,"$87oid"                    ,"$93"                                ,"$92"
Data "$9A","ancient"                ,"exceptional"                ,"eccentric"                ,"ingrained"                        ,"$95"
Data "$9B","killer"                    ,"deadly"                    ,"evil"                        ,"lethal"                            ,"vicious"
Data "$9C","parking meters"            ,"dust clouds"                ,"ice bergs"                ,"rock formations"                    ,"volcanoes"
Data "$9D","plant"                    ,"tulip"                    ,"banana"                    ,"corn"                                ,"$B2weed"
Data "$9E","$B2"                    ,"$B1 $B2"                    ,"$B1 $9B"                    ,"inhabitant"                        ,"$B1 $B2"
Data "$9F","shrew"                    ,"beast"                    ,"bison"                    ,"snake"                            ,"wolf"
Data "$A0","leopard"                ,"cat"                        ,"monkey"                    ,"goat"                                ,"fish"
Data "$A1","$8C $8B"                ,"$B1 $9F $A2"                ,"its $8D $A0 $A2"            ,"$A3 $A4"                            ,"$8C $8B"
Data "$A2","meat"                    ,"cutlet"                    ,"steak"                    ,"burgers"                            ,"soup"
Data "$A3","ice"                    ,"mud"                        ,"Zero-G"                    ,"vacuum"                            ,"$B1 ultra"
Data "$A4","hockey"                    ,"cricket"                    ,"karate"                    ,"polo"                                ,"tennis"</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >puki</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> Ooh, and all done in tiny-ickle code. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> I'm gonna take a guess that EdZup has already done all the above ;)<br><br>Nice ot see the actual algrithms and precisely how they are used. It can be really useful in working on ones' own generators <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> &gt; I'm gonna take a guess that EdZup has already done all the above ;)<br><br>I think EdZup must be very very old to know what Elite is and remember Lave's exact position. How's going being a retiree? ;-) Did you burn out the <a href="http://wiki.alioth.net/index.php/Trumble" target="_blank">Trumbles</a> near a sun or lost your ship and used your escape capsule?<br><br>I'm still stuck at the goat soup. Any ideas how to convert this to blitzbasic (I'm not good in Python):<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">    def gen_rnd_number(self):
        """Generate a random number for goat-soup.
           Uses own algorithm so results are consistent and
           platform independant.
        """
        x = (self.gs[0] * 2) &amp; 0xFF
        a = x + self.gs[2]
        if (self.gs[0] &gt; 127):
            a = a + 1
        self.gs[0] = a &amp; 0xFF
        self.gs[2] = x
    
        a = a / 256 # a = any carry left from above
        x = self.gs[1]
        a = (a + x + self.gs[3]) &amp; 0xFF
        self.gs[1] = a
        self.gs[3] = x
        return a</textarea><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">    def goatsoup(self,source):
        """Return the famous goatsoup string describing the planet"""
        ## Ian Bell: "Goat Soup" planetary description string code -
        ##           adapted from Christian Pinder's
        ##           reverse engineered sources.         
        
        desc_list = {   
          "\x81":["fabled", "notable", "well known", "famous", "noted"],
          "\x82":["very", "mildly", "most", "reasonably", ""],
          "\x83":["ancient", "\x95", "great", "vast", "pink"],
          "\x84":["\x9E \x9D plantations", "mountains", "\x9C", "\x94 forests", "oceans"],
          "\x85":["shyness", "silliness", "mating traditions", "loathing of \x86", "love for \x86"],
          "\x86":["food blenders", "tourists", "poetry", "discos", "\x8E"],
          "\x87":["talking tree", "crab", "bat", "lobst", "\xB2"],
          "\x88":["beset", "plagued", "ravaged", "cursed", "scourged"],
          "\x89":["\x96 civil war", "\x9B \x98 \x99s", "a \x9B disease", "\x96 earthquakes", "\x96 solar activity"],
          "\x8A":["its \x83 \x84", "the \xB1 \x98 \x99","its inhabitants' \x9A \x85", "\xA1", "its \x8D \x8E"],
          "\x8B":["juice", "brandy", "water", "brew", "gargle blasters"],
          "\x8C":["\xB2", "\xB1 \x99", "\xB1 \xB2", "\xB1 \x9B", "\x9B \xB2"],
          "\x8D":["fabulous", "exotic", "hoopy", "unusual", "exciting"],
          "\x8E":["cuisine", "night life", "casinos", "sit coms", " \xA1 "],
          "\x8F":["\xB0", "The planet \xB0", "The world \xB0", "This planet", "This world"],
          "\x90":["n unremarkable", " boring", " dull", " tedious", " revolting"],
          "\x91":["planet", "world", "place", "little planet", "dump"],
          "\x92":["wasp", "moth", "grub", "ant", "\xB2"],
          "\x93":["poet", "arts graduate", "yak", "snail", "slug"],
          "\x94":["tropical", "dense", "rain", "impenetrable", "exuberant"],
          "\x95":["funny", "wierd", "unusual", "strange", "peculiar"],
          "\x96":["frequent", "occasional", "unpredictable", "dreadful", "deadly"],
          "\x97":["\x82 \x81 for \x8A", "\x82 \x81 for \x8A and \x8A", "\x88 by \x89", "\x82 \x81 for \x8A but \x88 by \x89","a\x90 \x91"],
          "\x98":["\x9B", "mountain", "edible", "tree", "spotted"],
          "\x99":["\x9F", "\xA0", "\x87oid", "\x93", "\x92"],
          "\x9A":["ancient", "exceptional", "eccentric", "ingrained", "\x95"],
          "\x9B":["killer", "deadly", "evil", "lethal", "vicious"],
          "\x9C":["parking meters", "dust clouds", "ice bergs", "rock formations", "volcanoes"],
          "\x9D":["plant", "tulip", "banana", "corn", "\xB2weed"],
          "\x9E":["\xB2", "\xB1 \xB2", "\xB1 \x9B", "inhabitant", "\xB1 \xB2"],
          "\x9F":["shrew", "beast", "bison", "snake", "wolf"],
          "\xA0":["leopard", "cat", "monkey", "goat", "fish"],
          "\xA1":["\x8C \x8B", "\xB1 \x9F \xA2","its \x8D \xA0 \xA2", "\xA3 \xA4", "\x8C \x8B"],
          "\xA2":["meat", "cutlet", "steak", "burgers", "soup"],
          "\xA3":["ice", "mud", "Zero-G", "vacuum", "\xB1 ultra"],
          "\xA4":["hockey", "cricket", "karate", "polo", "tennis"]
        }
                                 
        index = -1
        out = []
        while True:
            index += 1
            try:
                c = source[index]
            except IndexError:
                break

            #Got a byte, what does it look like?
            oc = ord(c)

            if oc &lt; 128:
                #Just a letter, work with it.
                out.append(c)
                continue
            else:
                if oc &lt;= 164:
                    rnd = self.gen_rnd_number()
                    num = oc
                    
                    pos =  0
                    if rnd &gt;= 51:
                        pos += 1
                    if rnd &gt;= 102:
                        pos +=1
                    if rnd &gt;= 153:
                        pos += 1
                    if rnd &gt;= 204:
                        pos += 1
                    res = self.goatsoup(desc_list[chr(num)][pos])
                    out.append(res)
                    continue
            
            if c == "\xB0":
                #Planet name, leading capital, rest lower
                out.append(self.name[0] + self.name.lower()[1:])
                continue
            
            if c == "\xB1":
                #planet-name +"ian"
                out.append(self.name[0])
                out.append(self.name[1:-1].lower())
                if not self.name[-1] in ['I','E']:
                    out.append(self.name[-1].lower())
                out.append('ian')
                continue
            
            if c == "\xB2":
                #Random name. Bug in this area, probably related to way
                #C allows overrun between character arrays. The numbers
                #are right, the character selected sometimes isn't.
                #See ARARUS in Galaxy 1. DISO/BEMAERA correct..
                length = self.gen_rnd_number() &amp; 3
                for i in xrange(0,length+1):
                    x = self.gen_rnd_number() &amp; 0x3e
                    if pairs1[x-1] != '.':
                        out.append(pairs1[x-1])
                    if i and pairs1[x] !='.':
                        out.append(pairs1[x])
                continue
                          
            #There are (were?) some forward references that are not complete.      
            out.append('&lt;bad char in data [%X]&gt;' % oc)
            continue

            
        return ''.join(out)</textarea><br><br>The result for Lave (1,7) should be <b>Lave is most famous for its vast rain forests and the Laveian tree grub.</b> and for Reorte(1,39) <b>This planet is mildly fabled for its inhabitants' eccentric love for tourists but plagued by deadly earthquakes.</b><br><br>I still bow to the genius of David Braben and Ian Bell and will lighten a virtual candle in my retro shrine now. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> I'm working through it, I don't know python, but the code there seems pretty straightforward.<br>It's taking me a while getting the data parsed correctly, with recoginising the hex tokens compared to the standard text, but the sorting function seems to be okay so far. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>
Const HEX_VALUE$="0123456789ABCDEF"

Type Self
	Field num = -1 ;Planetary number
	Field x = 0
	Field y = 0
	Field economy = 0
	Field govtype = 0
	Field techlev = 0
	Field population = 0
	Field productivity = 0
	Field radius = 0 ;Not used by game at all
	Field name$
	Field goatsoupseed1[0]
	Field goatsoupseed2[0]
	Field goatsoupseed3[0]
	Field goatsoupseed4[0]
	Field gs%;A goat soup number
End Type	

Function Generate_Random(Instance.Self)
	x = ((Instance\gs Shr 24 And 255) Shr 1) And 255
	a = x + (Instance\gs Shr 8 And 255)
	If ((Instance\gs Shr 24 And 255) &gt; 127) Then a = a + 1
	Instance\gs = Instance\gs + (a Shr 24 And 255);And 255
	Instance\gs = Instance\gs + x Shr 8 And 255
	a = a Shl 8; a = any carry Left from above
	x = Instance\gs Shr 16 And 255
	a = (a + x + Instance\gs And 255) And 255
	Instance\gs = a Shr 16 And 255
	Instance\gs = Instance\gs+x And 255
    Return a
End Function

Function ConvertHex%(HexString$)
	HexString=Right(HexString,Len(HexString)-1)
	Local IterDigit%=1
	Local nReturn%=0
	Local Digit%
	For IterDigit=1 To Len(HexString)
		Digit=GetHexDigit(Mid(HexString,IterDigit,1),Len(HexString)-(IterDigit-1))
		nReturn=nReturn+Digit
	Next
	Return nReturn	
End Function

Function GetHexDigit%(DigitString$,Pos%)
	Local IterChar%
	For IterChar= 0 To 15
		If (Mid(HEX_VALUE,IterChar+1,1)=Upper(Left(DigitString,1))) Then Return IterChar Shl 4*(Pos-1)
	Next
	;Fail?
	Return 0
End Function</pre> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> Malice, is it complete? How do you parse the data fields? <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> No,. it's not complete yet. I'm kinda hitting a brick wall, but thought I'd post what I'd gotten so far. Which is the random generator as per python, and a converter to convert the hex strings of "\xA9" etc. to more useable decimal equivalents.<br>The idea being, that ultimately, I intend to not have to read strings to identify the tokens.<br>These 'tokens' allow for related words to be 'remembered' and re-used, for example, if the planet is 'Lave' then the token can be used as the adjective 'Lavian', or, referring to other references to allow for phrases as in the example of <br><br>"/x94 forests"<br>where, as you see along from "/x94" that the options for text here are types of forest such as 'tropical', 'dense' etc.<br><br>The format of reading the strings then the data seems a little unnecessary with modern computers, so ideally, the hex token strings might be best replaced with pure byte values, though the text lists are good enough as Data. It's taking me some time to correct it all but should get back to you soon. I can't yet find why the values start at 131. Presumably the previous values must be used for somerthing, since it would seem wasteful (and wasted space was certainly not on the Elite agenda!) the values after A4 are used for other text fields, some of which are evident in the earlier code (i.e. political data or such) but otherwise I'm just not sure how to deal with the values reproduced.<br><br>My headache really comes in with the memory management of large integers.<br><br>The Python function and the original versions of course did not use 32-bit bytes, so there was a splitting of values into separate 8-Bit bytes.<br><br>Instead, I substituted gs[4] for a single 32-bit Integer and used Bit-shifting to separate the individual bytes.<br>In the same way, goatsoupseed is another array of 4 8-bit values, but I have split this into separate ints, but not a single 32-bit one just in case as I'm not yet sure where that comes into play, or 'how' its used as a seed value. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
