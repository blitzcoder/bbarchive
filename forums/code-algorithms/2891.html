<!DOCTYPE html><html lang="en" ><head ><title >BlitzMax Big Numbers</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Add and Subtract Big Numbers, language=bb, category=Algorithms'><meta name='author' content='Spencer'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=6>Algorithms</a>/BlitzMax Big Numbers</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2891.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzMax Big Numbers by Spencer</td><td align="right">2011 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> The code below represents a set of functions for BlitzMax that can be used to add and subtract big integers without loss of precision. Values are stored in decimal format as strings.(x ="42" )  The code below has both the functions and a simple program demonstrating the addition and subtraction functions. The subtraction function is simplified by using the nine's complement (see Wikipedia: Method of complements) of the minuend. Basically, it's a math trick that allows the function the calculate subtraction by doing addition. It avoids the need to "borrow" when subtracting. Currently, this version can only handle positive integers. One possible use of these functions is to keep track of a player's very large score without loss of precision. This code has not been rigorously tested. So, if you find any bugs, please post them in the comments below. <br>Thanks, <br>Spencer </td></tr><tr ><td class="cell"><pre class="code">'-------------------------------------------------
'Example Program

	Global x:String = "0"
	Global y:String = "0"
	Global R:String = "0"
	
	x = "9999999999999999999999999999999999999"
	y = "1"
	R = BigNumbers.Add(x,y)
	Print R
	
	x = "11900"
	y = "11800"
	R = BigNumbers.Subtract(x,y)
	Print R
	
End
'-------------------------------------------------



'***************************************************************************************************************
'BigNumbers 
'***************************************************************************************************************
Type BigNumbers	

	Const EnumCompareResultLessThan = -1
	Const EnumCompareResultEqualTo = 0
	Const EnumCompareResultGreaterThan = 1

	Function GetNinesComplement:String(Value:String,MinuendDigitCount:Int)
		Local Result:String
		For Local CharPos:Int = 0 To Value.Length-1  Step 1
			Result = Result + Trim(String( 9-Int(Chr(Value[CharPos]))) )	
		Next
		While Result.Length &lt; MinuendDigitCount
			Result = "9" + Result
		Wend
		Return Result
	EndFunction

	Function StringPadLeft:String(SourceValue:String,Value:String,Count:Int)
		For Local x:Int = 1 To Count Step 1
			SourceValue = Value + SourceValue
		Next
		Return SourceValue	
	EndFunction
	
	Function StripLeadingZeros:String(Value:String)
		For Local CharPos:Int = 0 To Value.Length-1 Step 1
			If( Chr(Value[CharPos]) &lt;&gt; " " ) And ( Chr(Value[CharPos]) &lt;&gt; "0" ) Then
				Return Trim(Value[CharPos..])
			EndIf
		Next
		Return "0" 'value was all spaces and/or zeros
	EndFunction
	
	Function CompareNumbers:Int(Number1:String,Number2:String)
		Local Difference:Int = Number2.Length - Number1.Length
		Local Number1Padded:String = BigNumbers.StringPadLeft(Number1,"0",(Difference&gt;0)*Difference) 
		Local Number2Padded:String = BigNumbers.StringPadLeft(Number2,"0",(Difference&lt;0)*(Abs(Difference))) 
		Local Digit1:Byte
		Local Digit2:Byte
		For Local CharPos:Int = 0 To Number1Padded.Length-1 Step 1
			Digit1 = Int(Chr(Number1Padded[CharPos]))
			Digit2 = Int(Chr(Number2Padded[CharPos]))
			If(Digit1 &gt; Digit2)Then 
				Return BigNumbers.EnumCompareResultGreaterThan
			ElseIf(Digit2 &gt; Digit1)Then
				Return BigNumbers.EnumCompareResultLessThan 
			EndIf
		Next
		Return BigNumbers.EnumCompareResultEqualTo
	EndFunction
	
	Function GetLargest:String(Number1:String,Number2:String)
		If(BigNumbers.CompareNumbers(Number2,Number1) = BigNumbers.EnumCompareResultGreaterThan)Then
			Return Number2
		Else
			Return Number1
		EndIf
	EndFunction
	
	Function GetSmallest:String(Number1:String,Number2:String)
		If(BigNumbers.CompareNumbers(Number2,Number1) = BigNumbers.EnumCompareResultLessThan)Then
			Return Number2
		Else
			Return Number1
		EndIf
	EndFunction

	Function Add:String(Number1:String,Number2:String)
		Local Difference:Int = Number2.Length - Number1.Length
		Local Number1Padded:String = BigNumbers.StringPadLeft(Number1,"0",(Difference&gt;0)*Difference) 
		Local Number2Padded:String = BigNumbers.StringPadLeft(Number2,"0",(Difference&lt;0)*(Abs(Difference))) 
		Local Result:String
		Local A:Byte
		Local B:Byte
		Local C:Byte
		Local R:Byte	
		For Local CharPos:Int = Number1Padded.Length-1 To 0 Step -1
			C = (R &gt; 9)
			A = Int(Chr(Number1Padded[CharPos]))
			B = Int(Chr(Number2Padded[CharPos]))
			R = A + B + C
			Result = Right(String(R),1) + Result
		Next
		If(C &gt; 0) Then
			Result = "1" + Result
		EndIf
		Return BigNumbers.StripLeadingZeros(Result)	
	EndFunction
	
	Function Subtract:String(Number1:String,Number2:String)
		Local Smallest:String = BigNumbers.GetSmallest(Number1,Number2)
		Local Largest:String = BigNumbers.GetLargest(Number1,Number2)
		Local NinesComp:String = BigNumbers.GetNinesComplement(Smallest,Largest.Length)
		Local TempResult:String = BigNumbers.Add(Largest,NinesComp)  'nines complement of smaller number
		TempResult = BigNumbers.Add(TempResult,"1")
		TempResult = BigNumbers.StripLeadingZeros(TempResult)
		TempResult = TempResult[1..]
		Return BigNumbers.StripLeadingZeros(TempResult)
	EndFunction

EndType
'***************************************************************************************************************
'End of BigNumbers
'***************************************************************************************************************</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
