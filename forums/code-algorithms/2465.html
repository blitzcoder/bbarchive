<!DOCTYPE html><html lang="en" ><head ><title >Hash Table</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='String-keyed hash table, language=bmx, category=Algorithms'><meta name='author' content='Otus'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=6>Algorithms</a>/Hash Table</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2465.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hash Table by Otus</td><td align="right">2009 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> I find myself rewriting some kind of a hash table too often, so now I'll post this here and hopefully be able to use it next time. :)<br><br>Interface is similar to that of TMap, except only strings are allowed as keys. Significantly faster than TMap (30+%) on most operations. Also, has slightly less overhead, when it contains reasonably many (100+) objects.<br><br>Note: Enumerators loop entries in table order - not key order. </td></tr><tr ><td class="cell"><pre class="code">SuperStrict

' String-keyed dynamically resizing HashTable

' Collision algorithm adapted from Python dict

Type TEntry
	
	Field _key:String, _value:Object
	
End Type

Type THashTable
	
	Const INIT_SIZE:Int = 256
	Const HASH_SHIFT:Int = 5
	Const HASH_PRIME:Int = 108301
	
	' = _table.length-1
	Field _last:Int
	
	' Total count and dummy count
	' real = _count - _dummies
	Field _count:Int, _dummies:Int
	
	' Data array, size a power of two
	Field _table:TEntry[]
	
	Method New()
		Clear
	End Method
	
	Method Clear()
		_last = INIT_SIZE - 1
		_count = 0 
		_dummies = 0
		_table = New TEntry[INIT_SIZE]
	End Method
	
	Method Contains:Int(key:String)
		Return _table[_FindIndex(key)]&lt;&gt;Null
	End Method
	
	Method Insert(key:String, value:Object)
		' Keep table &lt; 50% full
		If _count &gt; _last Shr 1 Then _Resize ((_last+1) Shl 1)
		
		Local i% = _FindIndex(key)
		
		If _table[i]
			_table[i]._value = value
		Else
			_count :+ 1
			_table[i] = New TEntry
			_table[i]._key = key
			_table[i]._value = value
		End If
	End Method
	
	Method IsEmpty:Int()
		Return _count=_dummies
	End Method
	
	Method Remove:Int(key:String)
		Local i% = _FindIndex(key)
		If Not _table[i] Then Return False
		_table[i] = _dummy
		_dummies :+ 1
		Return True
	End Method
	
	Method ValueForKey:Object(key:String)
		Local i% = _FindIndex(key)
		If Not _table[i] Then Return Null
		Return _table[i]._value
	End Method
	
	Method Keys:THashTableEnum()
		Local e:THashTableEnum = New THashKeysEnum
		e._ht = Self
		For e._i = 0 To _last
			If _table[e._i] And _table[e._i]&lt;&gt;_dummy Return e
		Next
		e._ht = Null
		Return e
	End Method
	
	Method Values:THashTableEnum()
		Local e:THashTableEnum = New THashValuesEnum
		e._ht = Self
		For e._i = 0 To _last
			If _table[e._i] And _table[e._i]&lt;&gt;_dummy Return e
		Next
		e._ht = Null
		Return e
	End Method
	
	Method ObjectEnumerator:THashTableEnum()
		Local e:THashTableEnum = New THashTableEnum
		e._ht = Self
		For e._i = 0 To _last
			If _table[e._i] And _table[e._i]&lt;&gt;_dummy Return e
		Next
		e._ht = Null
		Return e
	End Method
	
	' Private
	
	Method _FindIndex:Int(key:String)
		' Mul-Xor all chars
		Local h%
		For Local i% = 0 Until key.length
			h = h * HASH_PRIME ~ key[i]
		Next
		
		' Initial hash uses just the low bits
		Local j% = h &amp; _last
		Repeat
			Local e:TEntry = _table[j]
			If Not e Return j
			If e._key = key Return j
			
			' Next iteration uses higher bits
			h :Shr HASH_SHIFT
			
			' Even if we run out of bits this is nonlinear
			' and (5*j + 1) visits every array index.
			j = (5*j + 1 + h) &amp; _last
		Forever
	End Method
	
	Method _Resize(size:Int)
		Local t:TEntry[] = _table
		_last = size-1
		_table = New TEntry[size]
		_count = 0
		_dummies = 0
		
		For Local i% = 0 Until t.length
			Local e:TEntry = t[i]
			If (Not e) Or (e = _dummy) Then Continue
			
			Local j% = _FindIndex(e._key)
			_table[j] = e
			_count :+ 1
		Next
	End Method
	
End Type

Type THashTableEnum
	
	Field _ht:THashTable, _i:Int
	
	Method HasNext:Int()
		Return _ht&lt;&gt;Null
	End Method
	
	Method NextObject:Object()
		Local o:Object = _ht._table[_i]
		For _i = _i+1 To _ht._last
			If _ht._table[_i] And _ht._table[_i]&lt;&gt;_dummy Then Return o
		Next
		_ht = Null
		Return o
	End Method
	
	Method ObjectEnumerator:THashTableEnum()
		Return Self
	End Method
	
End Type

Type THashKeysEnum Extends THashTableEnum
	
	Method NextObject:Object()
		Local o:Object = _ht._table[_i]._key
		For _i = _i+1 To _ht._last
			If _ht._table[_i] And _ht._table[_i]&lt;&gt;_dummy Then Return o
		Next
		_ht = Null
		Return o
	End Method
	
End Type

Type THashValuesEnum Extends THashTableEnum
	
	Method NextObject:Object()
		Local o:Object = _ht._table[_i]._value
		For _i = _i+1 To _ht._last
			If _ht._table[_i] And _ht._table[_i]&lt;&gt;_dummy Then Return o
		Next
		_ht = Null
		Return o
	End Method
	
End Type

' For removed entries
Private
Global _dummy:TEntry = New TEntry</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> thanks <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
