<!DOCTYPE html><html lang="en" ><head ><title >Simple Proportional Font Routine</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Font routine for further uses, language=bb, category=Algorithms'><meta name='author' content='WildCat'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=6>Algorithms</a>/Simple Proportional Font Routine</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2541.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Simple Proportional Font Routine by WildCat</td><td align="right">2009 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This routine was based on the proportional bitmap font format by Adreas Jonsson (www.AngelCode.com, as he reffers). Maybe I invented a bicycle, but this is really usable in many ways. It works in 2D modes and features string divisioning at space [aka chr$(32) :)]. Global variable 'fontDir$' may be used to place your fonts somewhere else. This code needs optimization in a couple of places, so let's improve it together, 'cause as I said it's usable widely. One little example will be posted very soon... </td></tr><tr ><td class="cell"><pre class="code">Type WSymbol
	Field x
	Field y
	Field w
	Field h
	Field ox
	Field oy
	Field xa
	Field page
End Type

Type WFont
	Field pages[10]
	Field symbols[256]
	Field name$
End Type

Function WSymbolNew.WSymbol (x, y, w, h, ox, oy, xa, page)
	sym.WSymbol= New WSymbol
	sym\x = x
	sym\y = y
	sym\w = w
	sym\h = h
	sym\ox = ox
	sym\oy = oy
	sym\xa = xa
	sym\page = page
	
	Return (sym)
End Function

Function WFontNew.WFont (filename$)
	font.WFont = New WFont
	
	font\name$ = filename$
	
	If fontDir$ &lt;&gt; "" Then
		filename$ = fontDir$ + filename$
	EndIf
	
	file = ReadFile (filename$)
	a$ = ReadLine (file)
	a$ = ReadLine (file)
	
	
	b = Instr (a$, "pages=")
	pages = Mid (a$, b+6)
	
	If Left$(filename$,2) = ".." Then 
		beginFrom = 3
	Else
		beginFrom = 1
	EndIf
	
	c = Instr (filename$, ".", beginfrom)
	basename$ = Mid (filename$, 1, c-1)
	
	For b = 1 To pages
		nam$ = b-1
		While Len(nam$) &lt; 2 
			nam$ = "0"+nam$
		Wend
		nam$ = basename$+"_"+nam$+".png"
		
		;If fontDir$ &lt;&gt; "" Then nam$ = fontDir$ + nam$
		
		font\pages[b] = LoadImage (nam$)
		DebLog("Page Load: "+nam$+": handle="+font\pages[b])
	Next
	
	While Eof (file) = 0 
		a$ = ReadLine (file)
		
		sp = Instr (a$, " ")
		If sp Then 
			fw$ = Mid (a$, 1, sp-1)
			If fw$ = "kerning" Then Goto wfontcycend
			If fw$ = "mask" Then
				sp2 = Instr (a$, " ", sp+1)
				sp3 = Instr (a$, " ", sp2+1)
				
				red = Mid (a$, sp+1, sp2-sp)
				green = Mid (a$, sp2+1, sp3-sp2)
				blue = Mid (a$, sp3+1)
				
				For b = 1 To pages 
					MaskImage (font\pages[b], red, green, blue)
				Next
				
				Goto wfontcycend
			EndIf
		EndIf
		
		b = Instr (a$, "id=")
		If b = 0 Then Goto wfontcycend
		
		c = Instr (a$, " ", b)
		id = Mid (a$, b+3, c-b-3)
		
		b = Instr (a$, "x=",c)
		c = Instr (a$, " ", b)
		x = Mid (a$, b+2, c-b-2)
		
		b = Instr (a$, "y=",c)
		c = Instr (a$, " ", b)
		y = Mid (a$, b+2, c-b-2)		
		
		b = Instr (a$, "width=",c)
		c = Instr (a$, " ", b)
		w = Mid (a$, b+6, c-b-6)		
		
		b = Instr (a$, "height=",c)
		c = Instr (a$, " ", b)
		h = Mid (a$, b+7, c-b-7)		
		
		b = Instr (a$, "xoffset=",c)
		c = Instr (a$, " ", b)
		ox = Mid (a$, b+8, c-b-8)		
		
		b = Instr (a$, "yoffset=",c)
		c = Instr (a$, " ", b)
		oy = Mid (a$, b+8, c-b-8)		
		
		b = Instr (a$, "xadvance=",c)
		c = Instr (a$, " ", b)
		xa = Mid (a$, b+9, c-b-9)		
		
		b = Instr (a$, "page=",c)
		page = Mid (a$, b+5)		
		
		font\symbols[id+1] = Handle (WSymbolNew(x, y, w, h, ox, oy, xa, page))
		
		.wfontcycend
	Wend
	
	Return (font)
End Function

Function WFontText (font.WFont, s$, x, y)
	For a = 1 To Len(s$)
		b = Asc (Mid(s$, a, 1))
		If font\symbols[b+1] Then
			sym.WSymbol = Object.WSymbol(font\symbols[b+1])
			DrawImageRect (font\pages[sym\page+1], x+sym\ox, y+sym\oy, sym\x, sym\y, sym\w, sym\h)			
			x = x + sym\xa
		EndIf
	Next
End Function

Function WFontWidth (font.WFont, s$)
	w = 0
	For a = 1 To Len(s$)
		b = Asc (Mid(s$, a, 1))
		If font\symbols[b+1] Then
			sym.WSymbol = Object.WSymbol(font\symbols[b+1])
			w = w + sym\xa
		EndIf
	Next
	Return w
End Function

Function WFontHeight (font.WFont, s$)
	h = 0
	For a = 1 To Len(s$)
		b = Asc (Mid(s$, a, 1))
		If font\symbols[b+1] Then
			sym.WSymbol = Object.WSymbol(font\symbols[b+1])
			If sym\oy+sym\h &gt; h Then h = sym\oy+sym\h
		EndIf
	Next
	Return h		
End Function

Function WFontByName.WFont (name$)
	For font.WFont = Each WFont
		If font\name$ = name$ Then Return font
	Next
	Return Null
End Function

Function WFontSpare (font.WFont, s$, width, atSpace = True)
	w = 0
	For a = 1 To Len(s$)
		b = Asc (Mid(s$, a, 1))
		If font\symbols[b+1] Then
			sym.WSymbol = Object.WSymbol(font\symbols[b+1])
			wc = w + sym\ox + sym\w + sym\xa ;strange formula, but everything is clear this way :)
			w = w + sym\xa
			If wc &gt; width Then 
				If atSpace Then
					b = a
					Repeat
						If Mid(s$, b, 1) = " " Then Return b+1
						b = b - 1
						If b = 0 Then Return WFontSpare (font, s$, width, False)
					Forever
					Return a
				Else
					Return a
				EndIf
			EndIf
		EndIf
	Next	
	
	Return 0
End Function

Function WFontFree (font.WFont)
	For page = 1 To 10
		If font\pages[page] Then FreeImage (font\pages[page])
	Next
	
	For a = 1 To 256
		If font\symbols[a] Then
			sym.WSymbol = Object.WSymbol(font\symbols[b+1])
			Delete sym	
		EndIf
	Next
	
	Delete font
End Function

Function WFontsFree ()
	For font.WFont = Each WFont
		WFontFree(font)
	Next
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >WildCat</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> Here goes the example:<br><pre class=code>
Include "wfont.bb"

Function DebLog (s$)
	;this belongs to 'service.bb', but cut out of there to reduce code
	If debMode = 1 Then
		fname$ = "debug.log"
		If FileSize(fname$) = 0 Then
			file = WriteFile (fname$) 
		Else
			file = OpenFile (fname$)
		EndIf
		SeekFile (file, FileSize(fname$))
		WriteLine (file, s$)
		CloseFile (file)	
		DebugLog (s$)			
	Else 
		DebugLog (s$)
	EndIf
End Function

Graphics 200,140,32,2
ClsColor 255, 255, 255
Cls

tahoma24black.WFont = WFontNew ("tahoma24black.fnt")
WFontText (tahoma24black, "Hello World!", 45, 55)

Flip
WaitKey
End
</pre><br><br>And here goes media: <a href="http://project-a.ru/files/tahoma24black.zip" target="_blank">http://project-a.ru/files/tahoma24black.zip</a> <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
