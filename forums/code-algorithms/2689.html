<!DOCTYPE html><html lang="en" ><head ><title >TFactory Class</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Extend TMap to a Factory class designed for Game Engine and else, language=bmx, category=Algorithms'><meta name='author' content='Bobysait'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=6>Algorithms</a>/TFactory Class</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2689.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >TFactory Class by Bobysait</td><td align="right">2010 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Similar to TMap, as it is an extension from TMap<br>TFactory override the Insert method to rename automatically the keys.<br><br>So every entries are single<br><br>It can be used to store Brushes/Materials/Textures like TV3D do, and many else.<br><br><br>It is fully "bbDoced" and is coded to be safe for users<br>-&gt; use of private / public to protect wrong use of the __TFactory__ </td></tr><tr ><td class="cell"><pre class="code">SuperStrict

'#region TFactory
	
	'#region Factory Base Type
	
	' - Protected from user -
		Private
		
		Type __TFactory__ extends tmap
			
			Field _AutoInc		:Int		=	0
			Field _Default		:String		=	""
			Global _LastName	:String		=	""
			
			' - generate a valid unique name to insert in a factory hashmap -
			
			Method AutoName		:String			( Name:String="" )
				
				' if empty name -&gt; fill it with the default name
				If Name="" Then Name = _Default
				
				Local OrigName:String = Name
				
				' check if the map countais this name
				If Not(Contains(Name)) Then If Name&lt;&gt;_Default Then Return Name
				
				Repeat
					
					Name = OrigName+_AutoInc
					
					If Not(Contains(Name)) Then Return Name
					
					_AutoInc :+ 1
					
					If _AutoInc=0 Then Return ""
					
				Forever
				
				Return ""
				
			End Method
			
			Method Insert ( Key:Object,Value:Object )
				Insert2 ( String(Key),Value )
			End Method
			
			' - Insert a new object in the factory -
			
			Method Insert2	:String		( Name:String, obj:Object, autoname:Byte=True )
				
				_LastName = Name
				
				' no empty name
				If Name=""
					
					If autoname=False Then Return ""
					
					_LastName = Self.autoname ( Name )
					
				EndIf
				
				' no entries with same name
				If Contains ( _LastName )
					
					If autoname=False Then Return ""
					
					_LastName = Self.autoname ( _LastName )
					
				EndIf
				
				' remove old link to the Hack map
				Remove ( Name )
				
				' insert the name in the hash- map
				Super.Insert ( _LastName, obj )
				
				Return _LastName
				
			End Method
			
		End Type
		
	'#end region
	
	
	'#region public Factory
	
	' - Public factory SDK -
		
		Public
		
		Rem
			bbdoc: TFactory class is an extension of TMap
			about: TFactory is an extension of TMap, and provide an easy way to deal with "unique" named object
		EndRem
		Type TFactory Extends __TFactory__
			
			Rem
				bbdoc: Create a factory object
				about: create a factory with parameters @DefaultName as the defaultname for autonaming and @CounterStart for auto-increment
			EndRem
			Function Create :TFactory ( DefaultName:String, CounterStart:Int = 0 )
				
				Local factory:TFactory		=	New TFactory
				factory._AutoInc			=	CounterStart
				factory._Default			=	DefaultName
				
				Return factory
				
			End Function
			
			Rem
				bbdoc: Insert an object in the specified @factory
				about: insert an object initialised with the name @Name, the name will then be automaticaly formated to be unique
				check the formated name with #FactorylastRegistered
			endrem
			Method InsertName		:String		( Name:String, obj:Object, autoname:Byte=True )
				Return Super.Insert2( Name,obj,autoname)
			End Method
			
			Rem
				bbdoc: Return the last registered name inserted in the specified @factory
				about: 
			endrem
			Method LastRegistered	:String		( )
				Return _LastName
			End Method
			
		End Type
		
		Rem
			bbdoc: Create a factory object
			about: create a factory with parameters @DefaultName as the defaultname for autonaming and @CounterStart for auto-increment
		EndRem
		Function CreateFactory :TFactory( DefaultName:String="", CounterStart:Int = 0 )
			Return TFactory.Create(DefaultName,CounterStart)
		End Function
		
		Rem
			bbdoc: Insert an object in the specified @factory
			about: insert an object initialised with the name @Name, the name will then be automaticaly formated to be unique
			check the formated name with #FactorylastRegistered
		endrem
		Function FactoryInsert :String ( Factory:TFactory, Obj:Object, Name :String, autoname:Byte = True )
			Return Factory.InsertName ( Name, Obj, autoname )
		End Function
		
		Rem
			bbdoc: Return the last registered name inserted in the specified @factory
			about: 
		endrem
		Function FactorylastRegistered:String(factory:TFactory)
			Return factory.LastRegistered()
		End Function
		
	'#end region
	
'#end region</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bobysait</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> And here is a small exemple on how to use it<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' - Sample -

	' Create a factory -&gt; default name is "Material_"
	Local Myfactory :TFactory = CreateFactory ( "Material_", 0 )
	
	' register some objects ( we'll use string objetcs for this sample )
	For Local nb:Int = 1 To 10
		' name is automatic
		Myfactory.Insertname( "", String(nb) )
		' print the automatic registered name
		Print Myfactory.LastRegistered()
	Next
	
	' get the last name registered -&gt; use the factory as a tmap using LastRegistered() as key
	Local Name1:String = String(MyFactory.ValueForKey(MyFactory.LastRegistered()))
	Print "Name1="+Name1
	
	' let's try to register an object with an existing name ( "Material_5" should have been registered with the previous instertions )
	Local Obj:String="My Obj"
	' I can use the TMap method "Insert", as it has been override to match the TFactory SDK
	Myfactory.Insert ( "Material_5",Obj)
	
	' Let's get the name registered for "Material_5"
	Local RegName:String = FactorylastRegistered( Myfactory )
	Local Name2:String = String(MyFactory.ValueForKey(RegName))
	Print "Name2="+Name2
	Print "Registered name = "+RegName
	
	' delete Factory ^^
	Myfactory = Null
</textarea> <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
