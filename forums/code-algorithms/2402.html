<!DOCTYPE html><html lang="en" ><head ><title >Number sequences and special ratios</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Algorithms/Formulas for number sequences (Fibonacci, Lucas, etc.), the Golden and Silver ratios, and an IsPrime function, language=bmx, category=Algorithms'><meta name='author' content='plash'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=6>Algorithms</a>/Number sequences and special ratios</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2402.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Number sequences and special ratios by plash</td><td align="right">2009 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Example code:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Framework brl.blitz

Import brl.standardio
'Import brl.math

Include "sequences.bmx"

Local processcount:Int = 32, time:Int
Local output:String, elements:Long[], element:Long

Local test_names:String[] = ["Lucas Sequence","Fibonacci Sequence","Perrin Sequence","Pell Sequence","Padovan Sequence"]
Local test_pointers:Long[](count:Int)[] = [LucasSequence, FibonacciSequence, PerrinSequence, PellSequence, PadovanSequence]

For Local index:Int = 0 Until test_pointers.Length
	output = Null
	time = MilliSecs()
	elements = test_pointers[index](processcount)
	For element = EachIn elements
		output:+ element + " "
	Next
	Print(test_names[index] + "[" + processcount + "] {" + String(MilliSecs() - time) + "ms}: " + output[..output.Length - 1])
	
	output = Null; time = MilliSecs()
	For element = EachIn elements
		If IsPrime(element)
			output:+ element + " "
		End If
	Next
	Print("Primes {" + String(MilliSecs() - time) + "ms}: " + output[..output.Length - 1] + "~n")
Next
</textarea> </td></tr><tr ><td class="cell"><pre class="code">Rem
Description: Some number sequence functions (Lucas, Fibonacci, Perrin, Pell and Padovan), a fast IsPrime function
			and the Golden and Silver ratios.
Author: Plash
Credits: Toby Herring for the IsPrime function (see function header down yonder)
		All other formulae is taken from Wikipedia.
End Rem

' (Phi) The Golden Ratio; "extreme and mean ratio"
' http://en.wikipedia.org/wiki/Golden_ratio
Global golden_ratio:Double = 1.6180339887498949		' Algorithm: (1 + Sqr(5)) / 2

' (DeltaS) The Silver Ratio
' http://en.wikipedia.org/wiki/Silver_ratio
Global silver_ratio:Double = 2.4142135623730949		' Algorithm: (1 + Sqr(2))
' The inverse (?) of the Silver Ratio (used in the closed form Pell numbers formula)
Global isilver_ratio:Double = -0.41421356237309503	' Algorithm: (1 - Sqr(2))


' Fast prime-finder algorithm by Toby Herring; converted and adopted from: 
' http://www.freevbcode.com/ShowCode.asp?ID=1059
Function IsPrime:Int(testprime:Long)
	' Going by the Wiki prime number list, and eliminating even numbers
	If (testprime &lt; 2) Or (testprime Mod 2) = 0 Then Return False Else If testprime = 2 Then Return True
	
	' Loop through odd numbers starting with 3
	Local testnum:Long = 3
	Local testlimit:Long = testprime
	While (testlimit &gt; testnum)
		If (testprime Mod testnum) = 0    
			Return False
		End If
		testlimit = testprime / testnum ' There's logic to this. Think about it.
		testnum:+ 2 ' Only check odd numbers
	End While
	Return True
End Function

' The Lucas numbers (http://en.wikipedia.org/wiki/Lucas_number)
Function LucasSequence:Long[](count:Int)
	If count = 0 Then Return Null
	Local L:Long[] = New Long[count]
	For Local n:Int = 0 Until count
		If n = 0
			L[n] = 2
		Else If n = 1
			L[n] = 1
		Else
			L[n] = L[n - 1] + L[n - 2]
			' Or L[n] = (golden_ratio^n) + ((1 - golden_ratio)^n)
		End If
	Next
	Return L
End Function

' The Fibonacci numbers (http://en.wikipedia.org/wiki/Fibonacci_number)
Function FibonacciSequence:Long[](count:Int)
	If count = 0 Then Return Null
	Local F:Long[] = New Long[count]
	For Local n:Int = 0 Until count
		If n &lt; 2
			F[n] = n ' F[0] = 0; F[1] = 1
		Else
			F[n] = F[n - 1] + F[n - 2]
		End If
	Next
	Return F
End Function

' The Perrin numbers (http://en.wikipedia.org/wiki/Perrin_number)
Function PerrinSequence:Long[](count:Int)
	If count = 0 Then Return Null
	Local P:Long[] = New Long[count]
	For Local n:Int = 0 Until count
		' P[0] = 3, P[1] = 0, P[2] = 2
		If n = 0
			P[n] = 3
		Else If n = 1
			P[n] = 0
		Else If n = 2
			P[n] = 2
		Else
			P[n] = P[n - 2] + P[n - 3]
		End If
	Next
	Return P
End Function

' The Pell numbers (http://en.wikipedia.org/wiki/Pell_number)
Function PellSequence:Long[](count:Int)
	If count = 0 Then Return Null
	Local P:Long[] = New Long[count]
	For Local n:Int = 0 Until count
		' P[0] = 0, P[1] = 1
		If n &lt; 2
			P[n] = n
		Else
			P[n] = 2 * (P[n - 1]) + P[n - 2]
			' Or P[n] = ((silver_ratio^n) - (isilver_ratio^n)) / 2^2
		End If
	Next
	Return P
End Function

' The Padovan sequence (http://en.wikipedia.org/wiki/Padovan_sequence)
Function PadovanSequence:Long[](count:Int)
	If count = 0 Then Return Null
	Local P:Long[] = New Long[count]
	For Local n:Int = 0 Until count
		If n &lt; 3
			P[n] = 1 ' P[0 to 2] = 1
		Else
			P[n] = P[n - 2] + P[n - 3]
		End If
	Next
	Return P
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
