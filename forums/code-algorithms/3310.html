<!DOCTYPE html><html lang="en" ><head ><title >LCG 32bits Cipher</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='A simple and classical cipher, considered weak for initiated people, but fast and light for non-crucial data, with GUI app example., language=bmx, category=Algorithms'><meta name='author' content='Flanker'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=6>Algorithms</a>/LCG 32bits Cipher</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="3310.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >LCG 32bits Cipher by Flanker</td><td align="right">April </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> A first simple cipher, based on 2 functions (lib), and a working GUI example (app). A little documentation in LCG 32bits_lib.bmx, more infos about LCG : <a href="https://en.wikipedia.org/wiki/Linear_congruential_generator" target="_blank">https://en.wikipedia.org/wiki/Linear_congruential_generator</a><br><br><img src="https://i.imgbox.com/KS7Y8i5G.png"><br><br>LCG 32bits Cipher_app.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">''' INIT '''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Import MaxGui.Drivers

SuperStrict
SeedRnd MilliSecs()

Local version:String = "v0.9"
AppTitle = "LCG 32bits Cipher " + version

Include "LCG 32bits Cipher_lib.bmx"





''' LCG presets ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Type TLCG

	Global list:TList = CreateList()
	
	Field name:String
	Field a:Int ' multiplier
	Field c:Int ' increment
	Field m:Int ' modulo
	
	Function Create(name:String,a:Int,c:Int,m:Int)
		Local lcg:TLCG = New TLCG
		ListAddLast TLCG.list,lcg
		lcg.name = name
		lcg.a = a
		lcg.c = c
		lcg.m = m
	End Function
	
End Type

TLCG.Create("glibc (used by GCC)",1103515245,12345,2^31-1)
TLCG.Create("ANSI C: Watcom, Digital Mars, CodeWarrior, IBM VisualAge C/C++",1103515245,12345,2^31)
TLCG.Create("Microsoft Visual Basic (6 And earlier)",1140671485,12820163,2^24)
TLCG.Create("RtlUniform from Native API",2147483629,2147483587,2^31-1)
TLCG.Create("Apple CarbonLib, C++11's minstd_rand0 (default)",16807,0,2^31-1)
TLCG.Create("C++11's minstd_rand",48271,0,2^31-1)
TLCG.Create("random0",8121,28411,134456)
TLCG.Create("cc65",65793,4282663,2^23)
TLCG.Create("RANDU (not recommended)",65539,0,2^31)





''' Max GUI ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Local window:TGadget = CreateWindow("LCG 32bits Cipher "+version,0,0,400,300,Null,WINDOW_TITLEBAR|WINDOW_CLIENTCOORDS|WINDOW_CENTER|WINDOW_ACCEPTFILES)

Local label:TGadget = CreateLabel("Input file :",20,15,100,18,window)
Global fileInTextfield:TGadget = CreateTextField(20,35,360,22,window)
label:TGadget = CreateLabel("(you can also use drag and drop)",20,60,200,18,window)
Local browseButton:TGadget = CreateButton("Browse",280,60,100,24,window,BUTTON_PUSH)

label:TGadget = CreateLabel("Output file :",20,95,100,18,window)
Global fileOutTextfield:TGadget = CreateTextField(20,115,360,22,window)

label:TGadget = CreateLabel("LCG parameters :",20,155,100,18,window)
Local lcgCombobox:TGadget = CreateComboBox(20,175,360,22,window)
For Local lcg:TLCG = EachIn TLCG.list
	If Instr(lcg.name,"(default)")
		AddGadgetItem(lcgCombobox,lcg.name,GADGETITEM_DEFAULT)
	Else
		AddGadgetItem(lcgCombobox,lcg.name)
	EndIf
Next

label:TGadget = CreateLabel("Seed/Key :",20,215,100,18,window)
Global seedTextfield:TGadget = CreateTextField(20,235,120,22,window)
SetGadgetText seedTextfield,"0"

Local cipherButton:TGadget = CreateButton("Cipher",160,234,100,24,window,BUTTON_PUSH)
Local decipherButton:TGadget = CreateButton("Decipher",280,234,100,24,window,BUTTON_PUSH)

Global progressBar:TGadget=CreateProgBar(20,270,360,20,window)





''' Main Loop ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
While WaitEvent()
	
	Select EventID()
		Case EVENT_GADGETACTION
			
			Select EventSource()
				Case browseButton
					Local fileName:String = RequestFile( "Select file to import","All Files:*" )
					If fileName &lt;&gt; ""
						SetGadgetText fileInTextfield,fileName
						SetGadgetText fileOutTextfield,fileName + ".lcg"
					EndIf
				
				Case cipherButton
					DisableGadget(window)
					If CheckPaths() And CheckSeed()
						For Local lcg:TLCG = EachIn TLCG.list
							If lcg.name = GadgetItemText(lcgCombobox,SelectedGadgetItem(lcgCombobox))
								Local fileIn:TBank = LoadBank(TextFieldText(fileInTextfield))
								Local fileOut:TBank = LCG32Cipher(fileIn,lcg.a,lcg.c,lcg.m,TextFieldText(seedTextfield).ToInt())
								UpdateProgBar(progressBar,1)
								SaveBank(fileOut,TextFieldText(fileOutTextfield))
								fileIn = Null
								fileOut = Null
								GCCollect()
								Notify("File successfully ciphered with provided parameters.")
								Exit
							EndIf
						Next						
					EndIf
					UpdateProgBar(progressBar,0)
					EnableGadget(window)
				
				Case decipherButton
					DisableGadget(window)
					If CheckPaths() And CheckSeed()
						For Local lcg:TLCG = EachIn TLCG.list
							If lcg.name = GadgetItemText(lcgCombobox,SelectedGadgetItem(lcgCombobox))
								Local fileIn:TBank = LoadBank(TextFieldText(fileInTextfield))
								Local fileOut:TBank = LCG32Decipher(fileIn,lcg.a,lcg.c,lcg.m,TextFieldText(seedTextfield).ToInt())
								UpdateProgBar(progressBar,1)
								If BankSize(fileOut) &gt; 0
									SaveBank(fileOut,TextFieldText(fileOutTextfield))
									fileIn = Null
									fileOut = Null
									GCCollect()
									Notify("Attempt to decipher with provided parameters successful.")
								Else
									fileIn = Null
									fileOut = Null
									GCCollect()
									Notify("Failed to decipher with provided parameters.",True)
								EndIf
								Exit
							EndIf
						Next						
					EndIf
					UpdateProgBar(progressBar,0)
					EnableGadget(window)

			End Select
		
		Case EVENT_WINDOWACCEPT
			SetGadgetText fileInTextfield,EventExtra().ToString()
			SetGadgetText fileOutTextfield,EventExtra().ToString() + ".lcg"
			
		Case EVENT_WINDOWCLOSE
			End
			
	End Select
	
Wend





''' Functions ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Function CheckPaths:Int()

	If FileType(TextFieldText(fileInTextfield)) &lt;&gt; 1
		Notify("Input file doesn't exist !",True)
		Return False
	EndIf
	
	If FileType(ExtractDir(TextFieldText(fileOutTextfield))) &lt;&gt; 2
		Notify("Output directory doesn't exist !",True)
		Return False
	EndIf
	
	If FileType(TextFieldText(fileOutTextfield)) = 1
		Local result:Int = Confirm("Output LCG file already exists. Overwrite ?",True)
		Return result
	EndIf
	
	Return True

End Function

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Function CheckSeed:Int()
	
	If Len(TextFieldText(seedTextfield)) = 0
		Notify("Invalid seed value !",True)
		Return False
	EndIf
	
	For Local i:Int = 1 To Len(TextFieldText(seedTextfield))
		If Asc(Mid(TextFieldText(seedTextfield),i,1)) &lt; 48 Or Asc(Mid(TextFieldText(seedTextfield),i,1)) &gt; 57
			Notify("Invalid seed value !",True)
			Return False
		EndIf
	Next
	
	Return True

End Function</textarea> </td></tr><tr ><td class="cell"><pre class="code">' LCG 32bits Cipher_lib.bmx
'
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Linear Congruential Generator (LCG) Cipher/Decipher
'
' LCG is an algorithm to generate a sequence of pseudorandom numbers. Each number in
' the sequence is based on the previous one. For the first number, a seed is used.
'
' number = (seed * a + c) Mod m
' then seed = number
' etc...
'
' Different parameters affect the quality of the randomness :
' - a : multiplier
' - c : increment
' - m : modulo
'
' You can find more infos about LCG and proven parameters on wikipedia :
' (linear congruential generator)
' 
' A cipher based on LCG is considered weak to frequency analysis because of the
' linearity wich permits to break m, a and c values with mathematics. However, it
' will be enough protection against a non initiated curious user, and combined with
' XOR it is suitable to protect non crucial files. Also, it's fast and light.
'
' Here, numbers are simply ciphered 4 bytes by 4 bytes (32 bits), using XOR and LCG.
' It could be extended to cipher 8 bytes by 8 bytes (64 bits), using Long, but one
' would break 64 bits as easily as 32 bits, if he knows what he is doing.
'
' Both functions work with banks, you can read a ciphered file with LoadBank(file$),
' call the decipher function and read the original file in the returned bank. You
' can write the original file back with SaveBank(decipherBank,file) if you want.
'
' The seed is like a secret key. It is the first number of the random sequence, so
' deciphering with a wrong key will mess up everything after. If you don't provide
' a key when ciphering (0), a random one is chosen and stored in the ciphered file.
' All you'll have to know to decipher are the parameters used to cipher. If you
' provide a key when ciphering, it won't be stored in the ciphered file so you'll
' have to remember and provide both key and parameters to decipher (stronger).
'
' Flanker, 2017 (this is my first try in ciphering, take it easy :)
'
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Function LCG32Cipher:TBank(dataIn:TBank,a:Int=16807,c:Int=2^31-1,m:Int=0,seed:Int=0)
	
	Local pollTimer:Int = MilliSecs() ' &lt;&lt;&lt;&lt;&lt; Only used for the GUI example, remove if necessary
	
	Local dataHeader:Int
	Local dataOffset:Int
	
	Local dataSize:Int = BankSize(dataIn)
	Local dataOut:TBank = CreateBank(4 + dataSize)

	If dataSize Mod 4 &gt; 0
		ResizeBank(dataIn,dataSize + 4-(dataSize Mod 4))
		ResizeBank(dataOut,BankSize(dataOut) + 4-(dataSize Mod 4))
	EndIf	
	
	PokeInt dataOut,dataOffset,dataSize
	dataOffset = dataOffset + 4
	
	If seed = 0
		seed = Rand(2^31-1)
		ResizeBank(dataOut,BankSize(dataOut)+4)
		PokeInt dataOut,dataOffset,seed
		dataOffset = dataOffset + 4
	EndIf
	
	dataHeader = dataOffset
	
	While dataOffset-dataHeader &lt; BankSize(dataIn)
		
		Local valueIn:Int = PeekInt(dataIn,dataOffset-dataHeader)
		Local valueOut:Int = valueIn~seed ' XOR
		PokeInt(dataOut,dataOffset,valueOut)
		
		seed = ( seed * a + c ) Mod m
		dataOffset = dataOffset + 4
		
		' Only used for the GUI example &gt;&gt;&gt;&gt;&gt;
		If MilliSecs()-pollTimer &gt; 50
			UpdateProgBar(progressBar,Float(dataOffset-dataHeader)/Float(BankSize(dataIn)))
			PollSystem()
			pollTimer = MilliSecs()
		EndIf
		' &lt;&lt;&lt;&lt;&lt; Only used for the GUI example
		
	Wend
	
	Return dataOut	
	
End Function

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Function LCG32Decipher:TBank(dataIn:TBank,a:Int=16807,c:Int=2^31-1,m:Int=0,seed:Int=0)
	
	Local pollTimer:Int = MilliSecs() ' &lt;&lt;&lt;&lt;&lt; Only used for the GUI example, remove if necessary
	
	Local dataHeader:Int
	Local dataOffset:Int
	
	Local dataSize:Int = PeekInt(dataIn,dataOffset)
	dataOffset = dataOffset + 4
	
	If dataSize &lt; 0 Then Return CreateBank(0)
	
	Local dataOut:TBank = CreateBank(dataSize)
	
	If dataSize Mod 4 &gt; 0 Then ResizeBank(dataOut,BankSize(dataOut) + 4-(dataSize Mod 4))
	
	If seed = 0
		seed = PeekInt(dataIn,dataOffset)
		dataOffset = dataOffset + 4
	EndIf
	
	dataHeader = dataOffset
	
	While dataOffset &lt; BankSize(dataIn)
		
		If dataOffset-dataHeader &gt; BankSize(dataOut) Then Return CreateBank(0)
		
		Local valueIn:Int = PeekInt(dataIn,dataOffset)
		Local valueOut:Int = valueIn~seed ' XOR
		PokeInt(dataOut,dataOffset-dataHeader,valueOut)
		
		seed = ( seed * a + c ) Mod m
		dataOffset = dataOffset + 4
		
		' Only used for the GUI example &gt;&gt;&gt;&gt;&gt;
		If MilliSecs()-pollTimer &gt; 50
			UpdateProgBar(progressBar,Float(dataOffset)/Float(BankSize(dataIn)))
			PollSystem()
			pollTimer = MilliSecs()
		EndIf
		' &lt;&lt;&lt;&lt;&lt; Only used for the GUI example

	Wend

	ResizeBank(dataOut,dataSize)
	
	Return dataOut

End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
