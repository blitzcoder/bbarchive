<!DOCTYPE html><html lang="en" ><head ><title >Generic Enumerator</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Generic enumerator that will let you enumerate over anything, including enumerators, language=bmx, category=Algorithms'><meta name='author' content='N'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=6>Algorithms</a>/Generic Enumerator</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2544.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Generic Enumerator by N</td><td align="right">2009 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This is inspired by Warpy's code here: <a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=2496" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=2496</a><br><br>It's sort of the same idea, except that you can iterate over strings, and it should ideally support multidimensional arrays, strings (iterates over each character of the string), and it should be faster when iterating over the contents of TLists, TMaps, etc. since it's not going through the reflection module when calling the methods for their own enumerators.<br><br>This was mainly written so I could have an Inject method along the same lines as Ruby's <a href="http://ruby-doc.org/core/classes/Enumerable.html#M003129" target="_blank">Enumerable#inject</a>.  Works pretty well, I think, so I figured I'd share this bit of code, at least.<br><br>To use this in Lua with LuGI, just use this function on the TGenericEnumerator object returned by <i>Enum():Init(obj)</i>:<br><pre class=code>function Iterator(obj)
	local function _iter_next(enum, var)
		if not enum then
			return nil
		end
		local n = enum:HasNext()
		if (type(n) == "number" and n &lt; .5) or not n then
			return nil
		end
		return enum:NextObject()
	end
	
	local succ, enumobj = pcall(function (t) return t:ObjectEnumerator() end, obj)
	
	return _iter_next, enumobj, nil
end</pre><br><br>This will work like so:<br><pre class=code>local enum = Enum():Init(list)

for value in Iterator(enum) do
	if type(value) == "userdata" then
		print(value:ToString())
	else
		print(value)
	end
end</pre> </td></tr><tr ><td class="cell"><pre class="code">SuperStrict

Import brl.Reflection

'buildopt:threads
'buildopt:clean

Public

' Creates a generic enumerator and returns it
' This is pretty much the only function that matters
Function EnumForObject:TGenericEnumerator(obj:Object)
	Return New TGenericEnumerator.InitWithObject(obj)
End Function

' BMax: EnumForObject(obj)  OR  New TGenericEnumerator.Init(obj)
' Lua: Enum():Init(obj)
Type TGenericEnumerator {expose category="cower.generic.iterator" constructor="Enum"}
	Method HasNext:Int() {bool}
		Throw "Not implemented in TGenericEnumerator"
	End Method
	Method NextObject:Object()
		Throw "Not implemented in TGenericEnumerator"
	End Method
	
	Method InitWithObject:TGenericEnumerator(obj:Object) {rename="Init"}
		Assert obj Else "Object is Null"
		Local tid:TTypeID = TTypeID.ForObject(obj)
		Select tid._class
			Case ArrayTypeId._class
				Return New TArrayEnumerator.InitWithObjectAndTypeId(obj, tid)
			Case StringTypeId._class
				Return New TStringEnumerator.InitWithObject(obj)
			Default
				Local enum:TGenericEnumerator = TGenericEnumerator(obj)
				If enum Then
					Return enum
				ElseIf tid.ExtendsType(ObjectTypeId) Then
					If tid.FindMethod("HasNext") And tid.FindMethod("NextObject") Then
						Return New TObjectEnumerator.InitWithEnumeratorAndTypeId(obj, tid)
					Else
						Return New TObjectEnumerator.InitWithObjectAndTypeId(obj, tid)
					EndIf
				EndIf
		End Select
		Throw "Object is not enumerable"
	End Method
	
	Method ObjectEnumerator:TGenericEnumerator()
		Return Self
	End Method
End Type

Private

Type TObjectEnumerator Extends TGenericEnumerator
	Field _enum:Object
	Field _enumNextObject:Object(enum:Object)
	Field _enumHasNext:Int(enum:Object)
	
	Method InitWithEnumeratorAndTypeId:TObjectEnumerator(obj:Object, typeid:TTypeId)
		Assert obj Else "Enumerator is null"
		
		_enum = obj
		
		Local meth:TMethod = typeid.FindMethod("NextObject")
		Assert meth Else "Enumerator does not implement NextObject"
		
		If meth._index &lt; 65536 Then
			_enumNextObject = Byte Ptr Ptr(typeid._class+meth._index)[0]
		Else
			_enumNextObject = Byte Ptr(meth._index)
		EndIf
		
		meth = typeid.FindMethod("HasNext")
		Assert meth Else "Enumerator does not implement HasNext"
		
		If meth._index &lt; 65536 Then
			_enumHasNext = Byte Ptr Ptr(typeid._class+meth._index)[0]
		Else
			_enumHasNext = Byte Ptr(meth._index)
		EndIf
		
		Return Self
	End Method
	
	Method InitWithObjectAndTypeId:TObjectEnumerator(obj:Object, typeid:TTypeId)
		' Get the enumerator object
		Local objenum:TMethod = typeid.FindMethod("ObjectEnumerator")
		Assert objenum Else "Object is not enumerable"
		Local enumObject:Object = objenum.Invoke(obj, New Object[0])
		
		' Get enumerator methods
		typeid = TTypeID.ForObject(enumObject)
		
		Return InitWithEnumeratorAndTypeId(enumObject, typeid)
	End Method
	
	Method HasNext:Int()
		Return _enumHasNext(_enum)
	End Method
	
	Method NextObject:Object()
		Return _enumNextObject(_enum)
	End Method
End Type

Type TArrayEnumerator Extends TGenericEnumerator
	Field _typeid:TTypeID
	Field _object:Object
	Field _idx:Int, _length:Int
	
	Method InitWithObjectAndTypeId:TArrayEnumerator(obj:Object, typeid:TTypeId)
		_object = obj
		_typeid = typeid
		_idx = 0
		_length = _typeid.ArrayLength(_object, 0)
		Return Self
	End Method
	
	Method HasNext:Int()
		While _idx &lt; _length And Not _typeid.GetArrayElement(_object, _idx)
			_idx :+ 1
		Wend
		Return _idx &lt; _length
	End Method
	
	Method NextObject:Object()
		Local val:Object = _typeid.GetArrayElement(_object, _idx)
		_idx :+ 1
		Return val
	End Method
End Type

Type TStringEnumerator Extends TGenericEnumerator
	Field _string:String
	Field _idx:Int

	Method InitWithObject:TStringEnumerator(obj:Object)
		_string = String(obj)
		_idx = 0
		Return Self
	End Method

	Method HasNext:Int()
		Return _idx &lt; _string.Length
	End Method

	Method NextObject:Object()
		Local nidx:Int = _idx + 1
		Local val:Object = _string[_idx .. nidx]
		_idx = nidx
		Return val
	End Method
End Type</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >N</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> Added LuGI metadata and an example of using the enumerator in a for-in loop in Lua. <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
