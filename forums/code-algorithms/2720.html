<!DOCTYPE html><html lang="en" ><head ><title >Linear Binary Search Algorithm</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Performs a binary search up to an arbitrary precision based on an initial interval and an evaluator callback., language=bmx, category=Algorithms'><meta name='author' content='beanage'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=6>Algorithms</a>/Linear Binary Search Algorithm</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2720.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Linear Binary Search Algorithm by beanage</td><td align="right">2010 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This algorithm has been developed, to find the interpolation value of a spline in the range of [0..1] closest to an arbitrary 2D point. This is a good example for a problem, where no satisfying analytical solution exists for all cases. Therefore, a procedural searching technique has to be used.<br><br>To perform a search with this algorithm, custom callbacks have to be provided for a specific linear search Problem. So called 'probe' callbacks are used to find an initial search interval. A so called 'evaluator' callback is used to determine, whether one value is potentially "better" than another. You must also tell a maximum numerical range for the result.<br><br>Once an initial interval is found using the 'probe' callbacks, a binary search will be performed up to an arbitrary precision.<br><br>Example Code:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Import beanage.BSearch

Type TBezier
	
	Global cspline:TBezier
	Global px:Double
	Global py:Double
	
	Field _a:Double
	Field _b:Double
	Field _c:Double
	Field _d:Double
	Field _e:Double
	Field _f:Double
	Field _g:Double
	Field _h:Double
	Field _x1:Double
	Field _y1:Double
	Field _x2:Double
	Field _y2:Double
	Field _px:Double
	Field _py:Double
	Field _qx:Double
	Field _qy:Double
	
	'-----------------------
	'Binary Search Callbacks
	'-----------------------
	
	Function _bsearchEvaluator:Byte( this_:Double, that_:Double )
		Local x_:Double, y_:Double
		
		cspline.GetCoords this_, x_, y_
		Local this_distance_squared_:Double = (x_- px)^2 + (y_- py)^2
		
		cspline.GetCoords that_, x_, y_
		Return this_distance_squared_ &lt;= ( (x_- px)^2 + (y_- py)^2 )'* cspline.GetCurvature( that_ ) * cspline.GetCurvature( this_ )
	End Function
	
	Function _0_up:Double[]( suggestion_:Double[] )
		If IsNan( suggestion_[1] )
			suggestion_[0] = .0 'return initial guess if no suggestion
		Else
			suggestion_[0]:+ .05
		
		End If
		cspline.HighlightCoordsAt suggestion_[0]
		Return suggestion_
	End Function
	
	Function _0_5_down:Double[]( suggestion_:Double[] )
		If IsNan( suggestion_[1] )
			suggestion_[0] = .5 'return initial guess if no suggestion
		Else
			suggestion_[0]:- .05
		
		End If
		cspline.HighlightCoordsAt suggestion_[0]
		Return suggestion_
	End Function

	Function _0_5_up:Double[]( suggestion_:Double[] )
		If IsNan( suggestion_[1] )
			suggestion_[0] = .5 'return initial guess if no suggestion
		Else
			suggestion_[0]:+ .05
		
		End If
		cspline.HighlightCoordsAt suggestion_[0]
		Return suggestion_
	End Function

	Function _1_down:Double[]( suggestion_:Double[] )
		If IsNan( suggestion_[1] )
			suggestion_[0] = 1.0 'return initial guess if no suggestion
		Else
			suggestion_[0]:- .05
		
		End If
		cspline.HighlightCoordsAt suggestion_[0]
		Return suggestion_
	End Function
	
	'------------
	'Spline Stuff
	'------------
	
	Method _updateParameters()
		_a = _x2 - 3.0*_qx + 3.0*_px - _x1
		_b = 3.0*_qx - 6.0*_px + 3.0*_x1
		_c = 3.0*_px - 3.0*_x1
		_d = _x1
		_e = _y2 - 3.0*_qy + 3.0*_py - _y1
		_f = 3.0*_qy - 6.0*_py + 3.0*_y1
		_g = 3.0*_py - 3.0*_y1
		_h = _y1
	End Method
	
	Method HighlightCoordsAt( t_:Double )
		Local x_:Double, y_:Double
		
		GetCoords t_, x_, y_
		DrawOval x_- 3, y_- 3, 6, 6
	End Method
		
	Method SetCoords( x1_:Double, y1_:Double, px_:Double, py_:Double, x2_:Double, y2_:Double, qx_:Double, qy_:Double )
		_x1 = x1_
		_y1 = y1_
		_x2 = x2_
		_y2 = y2_
		_px = px_
		_py = py_
		_qx = qx_
		_qy = qy_
		_updateParameters
	End Method
	
	Method DrawTangent( s_:Double )
		Local x_:Double, y_:Double
		
		GetCoords s_, x_, y_
		PlotLine x_, y_, GetSlope( s_ )
	End Method

	Method GetCoords( s_:Double, out_x_:Double Var, out_y_:Double Var )
		out_x_ = ( ( _a*s_ + _b )*s_ + _c )*s_ + _d
		out_y_ = ( ( _e*s_ + _f )*s_ + _g )*s_ + _h
	End Method
	
	Method GetSlope:Double( s_:Double )
		Return ( ( 3.0*_e*s_ + 2.0*_f )*s_ + _g )/( ( 3.0*_a*s_ + 2.0*_b )*s_ + _c )
	End Method
	
	Method GetCurvature:Double( s_:Double )
		 Return ( 6*_e*s_ + 2*_f )/( 3*_a*s_^2 + 2*_b*s_ + _c ) - ( 3*_e*s_^2 + 2*_f*s_ + _g )/( 3*_a*s_^2 + 2*_b*s_ + _c )^2 * ( 6*_a*s_ + 2*_b )
	End Method
	
	Method Draw( steps_:Int )
		Local x0_:Double, y0_:Double
		Local x1_:Double, y1_:Double
		
		GetCoords Null, x0_, y0_
		For Local i_:Int = 1 To steps_
			GetCoords Double i_/ Double steps_, x1_, y1_
			DrawLine x0_, y0_, x1_, y1_
			x0_ = x1_
			y0_ = y1_
		Next
		SetAlpha .2
		DrawLine _x1, _y1, _x2, _y2
		PlotLine _x1, _y1, ( _y1- _py )/( _x1- _px )
		PlotLine _x1, _y1, -( _x1- _px )/( _y1- _py )
		PlotLine _x2, _y2, ( _y2- _qy )/( _x2- _qx )
		PlotLine _x2, _y2, -( _x2- _qx )/( _y2- _qy )
		SetAlpha 1
	End Method
	
	Method GetInterpolationClosestToPoint:Double( px_:Double, py_:Double, out_iterations_:Int Ptr=Null, guess_:Double=.0 )
		'==============
		'BSEARCH METHOD
		'==============
		cspline = Self
		px = px_
		py = py_
		Local test_:BSearch = BSearch.Create( .0, 1.0, _bsearchEvaluator, .001, [ _0_up, _0_5_down, _0_5_up, _1_down ] )

		test_.Search
		guess_ = test_.GetResult()
		DrawText "t = ["+String(test_.info_interval[0])[..4]+".."+String(test_.info_interval[1])[..4]+"]", 10, 10
		DrawText "j = "+test_.info_best_probe_index, 10, 40
		
		If out_iterations_ Then out_iterations_[0] = test_.info_num_cycles
		Return guess_
	End Method
	
	Method Interact( mx_:Int, my_:Int, tol_:Double )
		Local dmx_:Int = MouseXSpeed()
		Local dmy_:Int = MouseYSpeed()
		
		If MouseDown(1)
			If PointDistance( mx_, my_, _x1, _y1 )&lt;= tol_
				_x1:+ dmx_
				_y1:+ dmy_
				_updateParameters
			
			ElseIf PointDistance( mx_, my_, _x2, _y2 )&lt;= tol_
				_x2:+ dmx_
				_y2:+ dmy_
				_updateParameters
	
			ElseIf PointDistance( mx_, my_, _px, _py )&lt;= tol_
				_px:+ dmx_
				_py:+ dmy_
				_updateParameters
	
			ElseIf PointDistance( mx_, my_, _qx, _qy )&lt;= tol_
				_qx:+ dmx_
				_qy:+ dmy_
				_updateParameters
	
			End If
		End If
	End Method

End Type

Function PlotLine( x_:Double, y_:Double, m_:Double )
	DrawLine 0, m_*-x_ + y_, 800, m_*( 800- x_ ) + y_
End Function

Function InterpolateLine( ax_:Double, ay_:Double, bx_:Double, by_:Double, t_:Double, out_x_:Double Var, out_y_:Double var )
	out_x_ = ( bx_- ax_ )* t_ + ax_
	out_y_ = ( by_- ay_ )* t_ + ay_
End Function

Function PointDistance:Double( px_:Double, py_:Double, qx_:Double, qy_:Double )
	Return Sqr( ( px_- qx_ )^2 + ( py_- qy_ )^2 )
End Function

Function DoubleToNAN( ptr_:Byte Ptr )
	ptr_[0] = $00
	ptr_[1] = $00
	ptr_[2] = $00
	ptr_[3] = $00
	ptr_[4] = $00
	ptr_[5] = $00
	ptr_[6] = $F8
	ptr_[7] = $7F
End Function

Function DoubleToInf( ptr_:Byte Ptr )
	ptr_[0] = $00
	ptr_[1] = $00
	ptr_[2] = $00
	ptr_[3] = $00
	ptr_[4] = $00
	ptr_[5] = $00
	ptr_[6] = $F0
	ptr_[7] = $7F
End Function

Function GuessDistanceToBezier( px_:Double, py_:Double, spline_:TBezier )
	SetColor 255, 0, 0
	DrawLine spline_._x1, spline_._y1, spline_._px, spline_._py
	DrawLine spline_._x2, spline_._y2, spline_._qx, spline_._qy
	DrawLine spline_._qx, spline_._qy, spline_._px, spline_._py
	Local i_:Int
	Local guess_:Double = spline_.GetInterpolationClosestToPoint( px_, py_, Int Ptr VarPtr i_, .0 )
	Local x_:Double
	Local y_:Double
	
	spline_.GetCoords guess_, x_, y_
	SetColor 0, 0, 255
	DrawLine x_, y_, px_, py_
	SetColor 255, 0, 0
	SetAlpha .5
	spline_.DrawTangent guess_
	SetAlpha 1
	SetColor 255, 255, 255
	DrawText "t = "+String( guess_ )[..5], 10, 20
	DrawText "i = "+i_, 10, 30
End Function

Graphics 800, 600
SetBlend ALPHABLEND

Local spline:TBezier = New TBezier
spline.SetCoords 100, 200, 200, 400, 600, 400, 700, 200

Repeat
	Cls
	Local x_:Double, y_:Double
	
	spline.GetCoords .5, x_, y_
	DrawOval x_- 2, y_- 2, 4, 4
	spline.GetCoords .25, x_, y_
	DrawOval x_- 2, y_- 2, 4, 4
	spline.GetCoords .75, x_, y_
	DrawOval x_- 2, y_- 2, 4, 4
	
	DrawOval spline._x1- 5, spline._y1- 5, 10, 10
	DrawOval spline._x2- 5, spline._y2- 5, 10, 10
	DrawOval spline._px- 5, spline._py- 5, 10, 10
	DrawOval spline._qx- 5, spline._qy- 5, 10, 10
	DrawOval x_- 2, y_- 2, 4, 4
	spline.Interact MouseX(), MouseY(), 5
	GuessDistanceToBezier MouseX(), MouseY(), spline
	spline.Draw 100
	Flip False
Until KeyHit( KEY_ESCAPE ) Or AppTerminate()
</textarea><br>Example Screenshot:<br><img src="http://www.beanage.bplaced.net/QuickImageUpload/SplineDistance.JPG"> </td></tr><tr ><td class="cell"><pre class="code">'------------------------------
'Linear binary search algorithm
'------------------------------

'#####################
'    C 2008-2009 by
'    B.e.A.n.A.g.e.
'       L.a.b.s.
'#####################

'//////////
SuperStrict
'//////////

'------------------------------------------------------------------------------------
Rem
bbdoc: Binary Linear Search Algorithm
about: 
EndRem
Module beanage.BSearch

ModuleInfo "Version: 1.0.00"
ModuleInfo "License: GNU GPL"
ModuleInfo "Copyright: BeAnAge Labs 2010"
ModuleInfo "Author: Joseph Birkner"
ModuleInfo "Modserver: beanage"

'#Region "History"
ModuleInfo "History: 20/05/10 &lt; 1.0.00 &lt; Initial Release"
'#End Region 
'------------------------------------------------------------------------------------

Import brl.standardio

Type BSearch
	
	Field _min:Double
	Field _max:Double
	Field _result:Double
	Field _result_tolerance:Double
	Field _evaluator:Byte( a_:Double, b_:Double ) 'Callback shall return &lt;True&gt; if &lt;this&gt; is better than &lt;than&gt;

	Field info_interval:Double[]
	Field info_num_cycles:Int = 0
	Field info_best_probe_index:Int = 0
	
	Rem
	A &lt;probe&gt; callback shall return an initial guess on the first execution, or a potentially better guess based on &lt;sugg&gt; if otherwise. 
	The &lt;sugg&gt; parameter will be "NaN" on the first execution of a &lt;probe&gt; callback.
	The callback will be executed until its new guess is "worse" than the the corresponding suggestion.
	The last results of all probe callbacks will be compared to find the best probe result.
	End Rem
	Field _probes:Double[]( sugg_:Double[] )[] 'returns [new_guess, suggestion]
	
	Function _doubleToNAN( ptr_:Byte Ptr )
?LittleEndian
		ptr_[0] = $00
		ptr_[1] = $00
		ptr_[6] = $F8
		ptr_[7] = $7F
?BigEndian
		ptr_[0] = $7F
		ptr_[1] = $F8
		ptr_[6] = $00
		ptr_[7] = $00
?
		Int Ptr( VarPtr ptr_[2] )[0] = Null
	End Function
	
	Function Create:BSearch( min_result_:Double, max_result_:Double, evaluator_:Byte(a_:Double,b_:Double), result_tolerance_:Double=.01, probes_:Double[](sugg_:Double[])[]= Null )
		Local ret_:BSearch = New BSearch
		ret_._min = min_result_
		ret_._max = max_result_
		ret_._evaluator = evaluator_
		ret_._result_tolerance = result_tolerance_
		ret_._probes = probes_
		
		_doubleToNAN VarPtr ret_._result
		Return ret_
	End Function
	
	Method ThisIsBetterThan:Byte( this_:Double, than_:Double )
		info_num_cycles:+ 1
		Return _evaluator( this_, than_ )
	End Method
	
	Method Search()
		info_num_cycles = Null
		Local interval_:Double[]
		
		'-----------------------------------------------------------------
		'Find an initial search interval using the given probing callbacks
		'-----------------------------------------------------------------
		If _probes
			Local results_:Double[][_probes.Length ]
			'perform provided initial test probes
			For Local i_:Int = 0 Until _probes.Length
				Local probe_:Double[2]
				
				'get initial probe guess
				_doubleToNAN VarPtr probe_[1]
				probe_ = _probes[ i_ ]( probe_ )
				Local prev_worse_guess_:Double = probe_[0]
				
				Repeat
					probe_[1] = probe_[0] 'make the new gues the suggestion
					probe_ = _probes[ i_ ]( probe_ ) 'get the new guess based on the suggestion
					If probe_[0]&gt;= _max Or probe_[0]&lt;= _min Or Not ThisIsBetterThan( probe_[0], probe_[1] )
						Exit
					Else
						prev_worse_guess_ = probe_[1] 'save the old suggestion
					End If
				Forever
				If prev_worse_guess_&lt;&gt; probe_[1] And ThisIsBetterThan( prev_worse_guess_, probe_[0] ) 'maybe the old suggestion is actually better than the new one? (tho both aint optimal)
					probe_[0] = prev_worse_guess_
				End If
				results_[ i_ ] = probe_
			Next
			'find best of probe results
			Local j_:Int = 0
			
			For Local result_:Double[] = EachIn results_
				If interval_
					If ( result_[0]&gt;= _min ) And ( result_[0]&lt;= _max )
						If ThisIsBetterThan( result_[0], interval_[0] )
							interval_ = result_
							info_best_probe_index = j_
							
						End If
					
					End If
				Else
					interval_ = result_
					info_best_probe_index = j_
				
				End If
				j_:+ 1
			Next
		Else
			interval_ = [ _min, _max ]
		
		End If
		info_interval = [ interval_[0], interval_[1] ]
		'-----------------------------------------------------------------
		'binary search the solution within the estimated solution interval
		'-----------------------------------------------------------------
		Local dstep_:Double = Abs( interval_[0]- interval_[1] )
		
		While dstep_&gt; _result_tolerance
			'interval[1] = Worse of Interval; Interval[0] = Better of Interval
			If ThisIsBetterThan( interval_[1], interval_[0] )
				Local better_:Double = interval_[1]
				
				interval_[1] = interval_[0]
				interval_[0] = better_
			
			End If
			'binary division
			interval_[1] = ( interval_[0]+ interval_[1] )/ 2
			dstep_ = Abs( interval_[0]- interval_[1] )
		Wend
		_result = interval_[1]
	End Method
	
	Method GetResult:Double()
		Return _result
	End Method

End Type</pre></td></tr></table><br><a name="comments">Comments</a><br><br>None.<br><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
