<!DOCTYPE html><html lang="en" ><head ><title >Interpolation library</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='linear/bilinear/trilinear interpolations for Object  Arrays, language=bmx, category=Algorithms'><meta name='author' content='Bobysait'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=6>Algorithms</a>/Interpolation library</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="3295.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Interpolation library by Bobysait</td><td align="right">2016 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Here is a library to create interpolation between arrays of objects<br>-&gt; Extends the types (BGradient, BGradient2, and BGradient3) to use it<br>there is already 2 extensions for Int[] and Float[]<br>You can create your own "interpolator" for Vector or any kind of objects<br>you'll need to implement a method for the lowest interpolation <br><pre class=code>
Method Interpolate:Object(a:Object,b:Object, v:Double) Abstract
</pre><br>See the extended types for more details (they use linear interpolation, but they can be replaced by cos/sin/square interpolation etc ...)<br> <br><br>-&gt; use it as standalone include :<br> save the file and include it ...<br><br>-&gt; use it as a module :<br>Add "Module yourname.interpolation" at the top of the file and save it to "interpolation.bmx" in a "yourname.mod" folder in your blitzmax mod's folder to build it as a module<br>Then just import it -&gt; Import yourname.interpolation<br><br><br><br>The sample will show an interpolation of 3 levels of arrays<br>using color templates for Season, time of the day and altitude of the sky.<br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

' ***************************************************************
' * SAMPLE *
' ***************************************************************

	' Smart function to convert Int32 RGB to an Int32[4] ([R,G,B,A] -&gt; alpha filled with "255")
		Function IToI4:Int[](l:Long, a:Long=$FF)
			Local c:Int = l + a Shl(24);
			Return [c Shr(16) &amp; $FF,c Shr(8) &amp; $FF,c &amp; $FF, c Shr(24) &amp; $FF];
		End Function
		

	Graphics 800,600,0,0
	
	' - 3 dimensions gradient
	' season - Night/Sunrise/Sun/Sunset - Emissive/Diffuse/Specular/Refraction/Ambient
	Local grd:Int4Gradient3 = Int4Gradient3.Create(5,5,5);
		' Winter					- Emissive		- Diffuse		- Specular		- Refraction	- Ambient
		grd.Gradient(0,0).SetArray( [ IToI4($000000), IToI4($020202), IToI4($051015), IToI4($041014), IToI4($0a101a) ] ); ' night
		grd.Gradient(0,1).SetArray( [ IToI4($171a24), IToI4($d08040), IToI4($b08050), IToI4($607080), IToI4($204060) ] ); ' sunrise
		grd.Gradient(0,2).SetArray( [ IToI4($101217), IToI4($ecddc7), IToI4($cddbfc), IToI4($6090c0), IToI4($204060) ] ); ' day
		grd.Gradient(0,3).SetArray( [ IToI4($181d23), IToI4($a06080), IToI4($906030), IToI4($80a0b0), IToI4($304080) ] ); ' sunset
		grd.Gradient(0,4).SetArray( grd.Gradient(0,4).Objects ); ' copy first to last -&gt; cyclic
		
		' Springtime
		grd.Gradient(1,0).SetArray( [ IToI4($000000), IToI4($01080c), IToI4($070e17), IToI4($0a161e), IToI4($081924) ] );
		grd.Gradient(1,1).SetArray( [ IToI4($01080c), IToI4($e0a507), IToI4($c165ba), IToI4($5c267b), IToI4($23083f) ] );
		grd.Gradient(1,2).SetArray( [ IToI4($212529), IToI4($bce1e2), IToI4($105b73), IToI4($02316c), IToI4($000764) ] );
		grd.Gradient(1,3).SetArray( [ IToI4($000000), IToI4($d34608), IToI4($d846a6), IToI4($55218e), IToI4($1b1057) ] );
		grd.Gradient(1,4).SetArray( grd.Gradient(1,4).Objects );
		
		' Summer
		grd.Gradient(2,0).SetArray( [ IToI4($000000), IToI4($01080c), IToI4($070e17), IToI4($0a161e), IToI4($081924) ] );
		grd.Gradient(2,1).SetArray( [ IToI4($01080c), IToI4($e0a507), IToI4($c165ba), IToI4($5c267b), IToI4($23083f) ] );
		grd.Gradient(2,2).SetArray( [ IToI4($212529), IToI4($e8f0ff), IToI4($306070), IToI4($013070), IToI4($002050) ] );
		grd.Gradient(2,3).SetArray( [ IToI4($000000), IToI4($d04010), IToI4($d840a0), IToI4($502090), IToI4($201050) ] );
		grd.Gradient(2,4).SetArray( grd.Gradient(2,4).Objects );
		
		' Automn
		grd.Gradient(3,0).SetArray( [ IToI4($000000), IToI4($01080c), IToI4($070e17), IToI4($0a161e), IToI4($081924) ] );
		grd.Gradient(3,1).SetArray( [ IToI4($01080c), IToI4($e0d5c7), IToI4($c1959a), IToI4($5c265b), IToI4($401040) ] );
		grd.Gradient(3,2).SetArray( [ IToI4($212529), IToI4($dce1e2), IToI4($405b63), IToI4($12415c), IToI4($103754) ] );
		grd.Gradient(3,3).SetArray( [ IToI4($000000), IToI4($d3c608), IToI4($d88636), IToI4($65413e), IToI4($4b2017) ] );
		grd.Gradient(3,4).SetArray( grd.Gradient(3,4).Objects );
		
		' cycle season
		grd.Gradient(4,0).SetArray( grd.Gradient(0,0).Objects );
		grd.Gradient(4,1).SetArray( grd.Gradient(0,1).Objects );
		grd.Gradient(4,2).SetArray( grd.Gradient(0,2).Objects );
		grd.Gradient(4,3).SetArray( grd.Gradient(0,3).Objects );
		grd.Gradient(4,4).SetArray( grd.Gradient(0,4).Objects );
		
	Local day:int = 100
	Local hour:Int = 8
	
	Local update:Byte = True;
	Repeat
		
		If MouseDown(1)
			If MouseX()&gt;300 And MouseX()&lt;300+365
				If MouseY()&gt;10 And MouseY()&lt;170
					day = MouseX()-300
					hour = Float(1 - Float(MouseY()-10)/160) * 24
					Update = True
				EndIf
			EndIf
		EndIf
		
		If update
			Cls()
			
			SetColor 100,100,100
			DrawRect 300,10,365,160
			
			SetColor 255,100,100
			DrawLine 300,5,300,170
			DrawLine 300,170,670,170
			
			SetColor 255,255,000
			DrawLine 300+day, 170, 300+day, 170-hour*170/24
			DrawLine 300, 170-hour*170/24, 300+day, 170-hour*170/24
			
			Local v:Float;
			For v = 0 To 160
				Local a:Int[] = grd.Get(Float(day)/365, Float(hour)/24, v/160);
				SetColor a[0],a[1],a[2]
				DrawRect 10,170-v,200,1
			Next
			
			SetColor 255,255,255
			DrawText "Day : "+day+" hour : "+hour, 10, 190
			DrawText "Days", 650, 190
			DrawText "Hours ", 220, 10
			
			Flip True
			update = False
		EndIf;
		
	Until KeyDown(KEY_ESCAPE)
	End
</textarea> </td></tr><tr ><td class="cell"><pre class="code">SuperStrict

Rem
undocument the @Module line to build as a module.
Save it as YOUR_BLITZMAX_PATH/mod/MDT.mod/Gradient.bmx
EndRem

'Module MDT.Gradient

' Abstract type ! Don't create instance of BGradient without extending it first !
' for purpose, see the Int4Gradient and Float4Gradient below
Type BGradient Abstract
	
	Field Objects:Object[];
	
	Method Interpolate:Object(a:Object,b:Object, v:Double) Abstract
	
	Method NewInstance:Object() Abstract
	
	Method OnCreate:BGradient(Size:Long=2)
		Self.Objects = New Object[Size];
		Local i:Int; For i=0 Until Size; Self.Objects[i] = Self.NewInstance(); Next;
		Return Self;
	End Method
	
	Method SetArray:BGradient( array:Object[])
		Self.Objects = array;
		Return Self;
	End Method
	
	Method Set:BGradient(id:Long, o:Object)
		If (id&lt;0 Or id&gt;=Self.Objects.Length) Then Return Self;
		Self.Objects[id] = o;
		Return Self;
	End Method
	
	Method Get:Object(v:Double)
		If (v&lt;=0) Then Return Self.Objects[0];
		If (v&gt;=1.0) Then Return Self.Objects[Self.Objects.Length-1];
		Local i:Double = v * (Self.Objects.Length-1), i0:Int = Floor(i); If (i0=i) Then Return	Self.Objects[i0];
		Return	Self.Interpolate (Self.Objects[i0], Self.Objects[i0+1], i-i0);
	End Method
End Type

Type BGradient2 Abstract
	Field Gradients:BGradient[];
	
	Method NewInstance:BGradient(SizeJ:Long) Abstract
	
	Method OnCreate:BGradient2(SizeI:Long=2, SizeJ:Long=2)
		Self.Gradients = New BGradient[SizeI];
		Local i:Int; For i=0 Until SizeI; Self.Gradients[i]=Self.NewInstance(SizeJ); Next;
		Return Self;
	End Method
	
	Method Get:Object(x:Double, y:Double)
		If (x&lt;=0) Then Return Self.Gradients[0].Get(y);
		If (x&gt;=1) Then Return Self.Gradients[Self.Gradients.Length-1].Get(y);
		Local i:Double = x * (Self.Gradients.Length-1), i0:Int = Floor(i);
		Return Self.Gradients[0].Interpolate (Self.Gradients[i0].Get(y), Self.Gradients[i0+1].Get(y), i-i0);
	End Method
	
	Method Gradient:BGradient(pId:Long)
		If (pId&lt;0 or pId&gt;=Self.Gradients.Length) Then Return Null;
		Return Self.Gradients[pId];
	End Method
End Type

Type BGradient3 Abstract
	Field Gradients:BGradient2[];
	
	Method NewInstance:BGradient2(SizeJ:Long, SizeK:Long) Abstract
	
	Method OnCreate:BGradient3(SizeI:Long=2, SizeJ:Long=2, SizeK:Long=2)
		Self.Gradients = New BGradient2[SizeI];
		Local i:Int; For i=0 Until SizeI; Self.Gradients[i] = Self.NewInstance(SizeJ,SizeK); Next;
		Return Self;
	End Method
	
	Method Get:Object(x:Double, y:Double, z:Double)
		If (x&lt;=0) Then Return Self.Gradients[0].Get(y,z);
		If (x&gt;=1) Then Return Self.Gradients[Self.Gradients.Length-1].Get(y,z);
		Local i:Double = x * (Self.Gradients.Length-1);
		Local i0:Int = Floor(i);
		Return Self.Gradients[0].Gradients[0].Interpolate (Self.Gradients[i0].Get(y,z), Self.Gradients[i0+1].Get(y,z), i-i0);
	End Method
	
	Method Gradient2:BGradient2(pId:Long)
		If (pId&lt;0 or pId&gt;=Self.Gradients.Length) Then Return Null;
		Return Self.Gradients[pId];
	End Method
	
	Method Gradient:BGradient(pIdI:Long, pIdJ:Long)
		If (pIdI&lt;0 or pIdI&gt;=Self.Gradients.Length) Then Return Null;
		If (Self.Gradients[pIdI]=Null) Then Return Null;
		Return Self.Gradients[pIdI].Gradient(pIdJ);
	End Method
End Type






' Prototype for Int4 And Float4 arrays
' for colors or vector position ...

Type Int4Gradient Extends BGradient
	Method NewInstance:Object()
		Return New Int[4];
	End Method
	
	Function Create:Int4Gradient(Size:Long=2)
		Return Int4Gradient(New Int4Gradient.OnCreate(Size));
	End Function
	
	' Mandatory -&gt; interpolate between two objects and returns an object
	Method Interpolate:Object(o1:Object,o2:Object,v:Double)
		Local a:Int[] = Int[](o1)
		Local b:Int[] = Int[](o2)
		' returns a new array containing a linear interpolated color/position/...
		Return [Int(a[0]+(b[0]-a[0])*v), Int(a[1]+(b[1]-a[1])*v), Int(a[2]+(b[2]-a[2])*v), Int(a[3]+(b[3]-a[3])*v)];
	End Method
	
	Method Int4:Int[](x:Double)
		Return Int[] (Self.Get(x));
	End Method
End type

Type Int4Gradient2 Extends BGradient2
	Method NewInstance:BGradient(SizeJ:Long)
		Return New Int4Gradient.OnCreate(SizeJ);
	End Method
	
	Function Create:Int4Gradient2(SizeI:Long=2, SizeJ:Long=2)
		Return Int4Gradient2(New Int4Gradient2.OnCreate(SizeI,SizeJ));
	End Function
	
	Method Int4:Int[](x:Double,y:Double)
		Return Int[] (Self.Get(x,y));
	End Method
End Type

Type Int4Gradient3 Extends BGradient3
	Method NewInstance:BGradient2(SizeJ:Long, SizeK:Long)
		Return New Int4Gradient2.OnCreate(SizeJ, SizeK);
	End Method
	
	Function Create:Int4Gradient3(SizeI:Long=2, SizeJ:Long=2, SizeK:Long=2)
		Return Int4Gradient3(New Int4Gradient3.OnCreate(SizeI,SizeJ,SizeK));
	End Function
	
	Method Int4:Int[](x:Double,y:Double,z:Double)
		Return Int[] (Self.Get(x,y,z));
	End Method
End Type




Type Float4Gradient Extends BGradient
	Method NewInstance:Object()
		Return New Float[4];
	End Method
	
	Function Create:Float4Gradient(Size:Long=2)
		Return Float4Gradient(New Float4Gradient.OnCreate(Size));
	End Function
	
	Method Interpolate:Object(o1:Object,o2:Object,d:Double)
		Local a:Float[] = Float[](o1);
		Local b:Float[] = Float[](o2);
		Local v:Float = d;
		Return [a[0]+(b[0]-a[0])*v, a[1]+(b[1]-a[1])*v, a[2]+(b[2]-a[2])*v, a[3]+(b[3]-a[3])*v];
	End Method
	
	Method Float4:Float[](x:Double)
		Return Float[] (Self.Get(x));
	End Method
End type

Type Float4Gradient2 Extends BGradient2
	Method NewInstance:BGradient(SizeJ:Long)
		Return New Float4Gradient.OnCreate(SizeJ);
	End Method
	
	Function Create:Float4Gradient2(SizeI:Long=2, SizeJ:Long=2)
		Return Float4Gradient2(New Float4Gradient2.OnCreate(SizeI,SizeJ));
	End Function
	
	Method Float4:Float[](x:Double,y:Double)
		Return Float[] (Self.Get(x,y));
	End Method
End Type

Type Float4Gradient3 Extends BGradient3
	Method NewInstance:BGradient2(SizeJ:Long, SizeK:Long)
		Return New Float4Gradient2.OnCreate(SizeJ, SizeK);
	End Method
	
	Function Create:Float4Gradient3(SizeI:Long=2, SizeJ:Long=2, SizeK:Long=2)
		Return Float4Gradient3(New Float4Gradient3.OnCreate(SizeI,SizeJ,SizeK));
	End Function
	
	Method Float4:Float[](x:Double,y:Double,z:Double)
		Return Float[] (Self.Get(x,y,z));
	End Method
End Type</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> By use of cosine, you can produce smoother gradients between similar values or at extremeties.<br><br>Assume scale value of 0 to 1<br>Cos(value*2*Pi)=1 to -1<br><br>So Smooth= - (Cos(Value * 2 *Pi)+1) * 0.5 <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bobysait</td><td align="right"><font class=tiny>2016</font></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>See the extended types for more details (they use linear interpolation, but they can be replaced by cos/sin/square interpolation etc ...)<br> <br></div><br>it's up to you to set the interpolate method to use cos interpolation. I used linear because it's faster and only for purpose.<br><br>For example, in the Float4Gradient Interpolate method, we just replace<br><pre class=code>
		Local v:Float = d;
</pre><br><br>by this<br><br>( -&gt; result of sin [-Pi/2, +Pi/2] goes from -1 to 1<br> -&gt; multiply the result by 0.5 to get [-0.5,0.5] range then add 0.5 to get [0.0,1.0]. And simple as that, you have your smooth interpolation )<br><br><pre class=code>
		Local v:Float = 0.5+0.5*Sin(d*180-90); ' sinus interpolation smooth up -&gt; higher medium -&gt; smooth down
</pre> <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
