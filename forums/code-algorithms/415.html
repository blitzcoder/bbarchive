<!DOCTYPE html><html lang="en" ><head ><title >Billiards Style Collision Physics</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='2D circle collision response which can be used for almost anything where 2 objects collide in 2 dimensions, language=bb, category=Algorithms'><meta name='author' content='Phish'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=6>Algorithms</a>/Billiards Style Collision Physics</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="415.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Billiards Style Collision Physics by Phish</td><td align="right">2002 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This type of collision response isn't just useful for pool and billiard style games, and in fact, I didn't write it for that reason at all. I wrote it because I needed to have collision for the space ships in my game 'Unity'. When their shields are up, they use this relatively simple method. It can be used for almost anything - player character bouncing off enemies in a platform game, to space ship collision, to a game which does actually involve balls. The basic algorithm which I used would work for both 2d and 3d so if you would like to see it, email me at phish@.... I think that realistic physics, even if pretty simple, can make a game feel a lot more immersive and interesting. The idea is that you should read my code, see how I use the collision response section, hack it up, and use it in your own game :-) </td></tr><tr ><td class="cell"><pre class="code">;------------------------------------------
;2D COLLISION DEMO CODE
;------------------------------------------
; By Joseph 'Phish' Humfrey
; This type of collision response isn't
; just useful for pool and billiard style
; games, and in fact, I didn't write it for
; that reason at all. I wrote it because I
; needed to have collision for the space
; ships in my game 'Unity'. When their
; shields are up, they use this relatively
; simple method. It can be used for almost
; anything - player character bouncing off
; enemies in a platform game, to space
; ship collision, to a game which does
; actually involve balls. The basic
; algorithm which I used would work for
; both 2d and 3d, so if you would like to
; see it, email me at phish@aelius.com
;------------------------------------------
; The actual code which works out what
; happens after a collision is in the
; UpdateCirclePhysics() function.
;------------------------------------------
; Enjoy!
;------------------------------------------
;------------------------------------------

Graphics 800, 600, 16, 2
SetBuffer BackBuffer()
SeedRnd MilliSecs()



;------------------------------------------
; MAIN DATA TYPE
;------------------------------------------
; This exact type isn't supposed to be used
; Instead, you should use some of the fields
; in your own type, or just use this one
; for reference, to see what each field does
Type circle
	Field x#, y#		;position
	Field dx#, dy#		;x and y speeds
	
	Field radius#		;radius of circle
	Field mass#			;mass of circle
End Type
;------------------------------------------
;------------------------------------------






;------------------------------------------
; SET UP BALLS INTO A POOL STYLE ARRANGEMENT
; FOR DEMO
;------------------------------------------
.Setup
ballTriangleSize=5
For xloop = ballTriangleSize To 1 Step -1
	For yloop = 1 To xloop
		c.circle = New circle
		c\x = (5-xloop)*27 + 200
		c\y = yloop*31-(xloop*31)/2.0 + 300
		c\dx=0
		c\dy=0
		c\radius = 15
		c\mass = 50
	Next
Next

;Cue ball (smaller so you know which it is :)
cue.circle = New circle
cue\x = 800
cue\y = 300 +20
cue\dx = -20
cue\dy = Rnd(4)-2
cue\radius = 14
cue\mass = 50
;------------------------------------------
;------------------------------------------







;------------------------------------------
;MAIN LOOP
;------------------------------------------
; This is the main While..Wend game loop
While Not KeyDown(1)

	Cls
	
	UpdateCirclePhysics()
	RenderCircles()
	
	;------------
	; Reset button
	Text 10, 10, &quot;Press a mouse button to reset.&quot;
	Text 10, 25, &quot;Press Esc to exit.&quot;
	If GetMouse() Then
		For c.circle = Each circle
			Delete c
		Next
		Goto setup
	End If
	;------------
	
	Flip
	
Wend
;------------------------------------------
;------------------------------------------
End









;------------------------------------------
Function UpdateCirclePhysics()
;------------------------------------------
; This is the main physics function for the
; circles. It contains the very basic
; movement physics as well as the collision
; response code.
;------------------------------------------

	For c.circle = Each circle
	
		;update positions
		c\x=c\x+c\dx
		c\y=c\y+c\dy
		
		;gradually slow down
		c\dx=c\dx*0.99
		c\dy=c\dy*0.99
		
		;------------------------------------------
		;COLLISION CHECKING
		;------------------------------------------
		; Check each circle in the loop against
		; every other (c against c2)
		For c2.circle = Each circle
		
			collisionDistance# = c\radius+c2\radius
			actualDistance# = Sqr((c2\x-c\x)^2+(c2\y-c\y)^2)
			
			;Collided or not?
			If actualDistance&lt;collisionDistance Then
				
				collNormalAngle#=ATan2(c2\y-c\y, c2\x-c\x)

				;Position exactly touching, no intersection
				moveDist1#=(collisionDistance-actualDistance)*(c2\mass/Float((c\mass+c2\mass)))
				moveDist2#=(collisionDistance-actualDistance)*(c\mass/Float((c\mass+c2\mass)))
				c\x=c\x + moveDist1*Cos(collNormalAngle+180)
				c\y=c\y + moveDist1*Sin(collNormalAngle+180)
				c2\x=c2\x + moveDist2*Cos(collNormalAngle)
				c2\y=c2\y + moveDist2*Sin(collNormalAngle)
				
				
				;------------------------------------------
				;COLLISION RESPONSE
				;------------------------------------------
				;n = vector connecting the centers of the circles.
				;we are finding the components of the normalised vector n
				nX#=Cos(collNormalAngle)
				nY#=Sin(collNormalAngle)
				
				;now find the length of the components of each movement vectors
				;along n, by using dot product.
				a1# = c\dx*nX  +  c\dy*nY
				a2# = c2\dx*nX +  c2\dy*nY
				
				;optimisedP = 2(a1 - a2)
				;             ----------
				;              m1 + m2
				optimisedP# = (2.0 * (a1-a2)) / (c\mass + c2\mass)
				
				;now find out the resultant vectors
				;r1 = c1\v - optimisedP * mass2 * n
				c\dx = c\dx - (optimisedP*c2\mass*nX)
				c\dy = c\dy - (optimisedP*c2\mass*nY)
				
				;r2 = c2\v - optimisedP * mass1 * n
				c2\dx = c2\dx + (optimisedP*c\mass*nX)
				c2\dy = c2\dy + (optimisedP*c\mass*nY)

			End If

		Next
		;------------------------------------------
		;------------------------------------------
		
		
		;Simple Bouncing off walls.
		If c\x&lt;c\radius Then
			c\x=c\radius
			c\dx=c\dx*-0.9
		End If
		If c\x&gt;GraphicsWidth()-c\radius Then
			c\x=GraphicsWidth()-c\radius
			c\dx=c\dx*-0.9
		End If
		If c\y&lt;c\radius Then
			c\y=c\radius
			c\dy=c\dy*-0.9
		End If
		If c\y&gt;GraphicsHeight()-c\radius Then
			c\y=GraphicsHeight()-c\radius
			c\dy=c\dy*-0.9
		End If
		
	Next

End Function





;------------------------------------------
Function RenderCircles()
;------------------------------------------
; Simple function draws all the circles
; on the screen.
;------------------------------------------

	For c.circle = Each circle
		If c\radius=15 Then Color 200, 50, 50 Else Color 255, 255, 255
		Oval c\x-c\radius, c\y-c\radius, c\radius*2, c\radius*2
	Next
	
End Function
;------------------------------------------
;------------------------------------------</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ching</td><td align="right"><font class=tiny>2012</font></td></tr></table></td></tr><tr ><td class="posttext"> I could have scored much better in my advanced graphics module if I'd found this code a couple of years ago.<br>This code is really good. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matty</td><td align="right"><font class=tiny>2012</font></td></tr></table></td></tr><tr ><td class="posttext"> The magic numbers in there for friction and elasticity of collision would be better replaced with parameters/fields that can be set as required. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>2012</font></td></tr></table></td></tr><tr ><td class="posttext"> This is great stuff! Here are some quick-and-dirty BMX/Monkey ports...<br><br>BlitzMax:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

' By Joseph 'Phish' Humfrey

Graphics 800, 600

SeedRnd MilliSecs()



'------------------------------------------
' Main DATA Type
'------------------------------------------
' This exact Type isn't supposed to be used
' Instead, you should use some of the fields
' in your own Type, Or just use this one
' For reference, To see what each Field does
Type circle
	Field x#, y#		'position
	Field dx#, dy#		'x And y speeds
	
	Field radius#		'radius of circle
	Field mass#			'mass of circle
End Type

Global Circles:TList = CreateList ()

'------------------------------------------
'------------------------------------------






'------------------------------------------
' SET UP BALLS INTO A POOL STYLE ARRANGEMENT
' For DEMO
'------------------------------------------
Function Setup ()

	ballTriangleSize=5

	For xloop = ballTriangleSize To 1 Step -1
		For yloop = 1 To xloop
			c:circle = New circle
			c.x = (5-xloop)*27 + 200
			c.y = yloop*31-(xloop*31)/2.0 + 300
			c.dx=0
			c.dy=0
			c.radius = 15
			c.mass = 50
			ListAddLast Circles, c
		Next
	Next

	'Cue ball (smaller so you know which it is :)
	cue:circle = New circle
	cue.x = 800
	cue.y = 300 +20
	cue.dx = -20
	cue.dy = Rnd(4)-2
	cue.radius = 14
	cue.mass = 50
	ListAddLast Circles, cue

End Function

'------------------------------------------
'------------------------------------------



Setup



'------------------------------------------
'Main LOOP
'------------------------------------------
' This is the Main While..Wend game loop
While Not KeyDown(KEY_ESCAPE)

	Cls
	
	UpdateCirclePhysics()
	RenderCircles()
	
	'------------
	' Reset button
	DrawText "Press a mouse button to reset.", 10, 10
	DrawText "Press Esc to exit.", 10, 25
	If MouseHit(1) Then
		For c:circle = EachIn Circles
			ClearList Circles
		Next
		Setup
	End If
	'------------
	
	Flip
	
Wend
'------------------------------------------
'------------------------------------------
End









'------------------------------------------
Function UpdateCirclePhysics()
'------------------------------------------
' This is the Main physics Function For the
' circles. It contains the very basic
' movement physics as well as the collision
' response code.
'------------------------------------------

	For c:circle = EachIn Circles
	
		'update positions
		c.x=c.x+c.dx
		c.y=c.y+c.dy
		
		'gradually slow down
		c.dx=c.dx*0.99
		c.dy=c.dy*0.99
		
		'------------------------------------------
		'COLLISION CHECKING
		'------------------------------------------
		' Check each circle in the loop against
		' every other (c against c2)
		For c2:circle = EachIn Circles
		
			collisionDistance# = c.radius+c2.radius
			actualDistance# = Sqr((c2.x-c.x)^2+(c2.y-c.y)^2)
			
			'Collided Or Not?
			If actualDistance&lt;collisionDistance Then
				
				collNormalAngle#=ATan2(c2.y-c.y, c2.x-c.x)

				'Position exactly touching, no intersection
				moveDist1#=(collisionDistance-actualDistance)*(c2.mass/Float((c.mass+c2.mass)))
				moveDist2#=(collisionDistance-actualDistance)*(c.mass/Float((c.mass+c2.mass)))
				c.x=c.x + moveDist1*Cos(collNormalAngle+180)
				c.y=c.y + moveDist1*Sin(collNormalAngle+180)
				c2.x=c2.x + moveDist2*Cos(collNormalAngle)
				c2.y=c2.y + moveDist2*Sin(collNormalAngle)
				
				
				'------------------------------------------
				'COLLISION RESPONSE
				'------------------------------------------
				'n = vector connecting the centers of the circles.
				'we are finding the components of the normalised vector n
				nX#=Cos(collNormalAngle)
				nY#=Sin(collNormalAngle)
				
				'now find the length of the components of each movement vectors
				'along n, by using dot product.
				a1# = c.dx*nX  +  c.dy*nY
				a2# = c2.dx*nX +  c2.dy*nY
				
				'optimisedP = 2(a1 - a2)
				'             ----------
				'              m1 + m2
				optimisedP# = (2.0 * (a1-a2)) / (c.mass + c2.mass)
				
				'now find out the resultant vectors
				'r1 = c1.v - optimisedP * mass2 * n
				c.dx = c.dx - (optimisedP*c2.mass*nX)
				c.dy = c.dy - (optimisedP*c2.mass*nY)
				
				'r2 = c2.v - optimisedP * mass1 * n
				c2.dx = c2.dx + (optimisedP*c.mass*nX)
				c2.dy = c2.dy + (optimisedP*c.mass*nY)

			End If

		Next
		'------------------------------------------
		'------------------------------------------
		
		
		'Simple Bouncing off walls.
		If c.x&lt;c.radius Then
			c.x=c.radius
			c.dx=c.dx*-0.9
		End If
		If c.x&gt;GraphicsWidth()-c.radius Then
			c.x=GraphicsWidth()-c.radius
			c.dx=c.dx*-0.9
		End If
		If c.y&lt;c.radius Then
			c.y=c.radius
			c.dy=c.dy*-0.9
		End If
		If c.y&gt;GraphicsHeight()-c.radius Then
			c.y=GraphicsHeight()-c.radius
			c.dy=c.dy*-0.9
		End If
		
	Next

End Function





'------------------------------------------
Function RenderCircles()
'------------------------------------------
' Simple Function draws all the circles
' on the screen.
'------------------------------------------

	For c:circle = EachIn Circles
		If c.radius=15 Then SetColor 200, 50, 50 Else SetColor 255, 255, 255
		DrawOval c.x-c.radius, c.y-c.radius, c.radius*2, c.radius*2
	Next
	
End Function
</textarea><br><br>Monkey:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

' By Joseph 'Phish' Humfrey

Import mojo

Global Circles:List &lt;circle&gt;

Class Pool Extends App

	Method OnCreate ()
		SetUpdateRate 60
		Circles = New List &lt;circle&gt;
		Seed = Millisecs ()
		Setup
	End

	Method OnUpdate ()

		UpdateCirclePhysics

		If MouseHit ()

			For Local c:circle = Eachin Circles
				Circles.Clear
			Next

			Setup

		Endif
		
	End

	Method OnRender ()

		Cls
		
		RenderCircles()
		
		DrawText "Press a mouse button to reset.", 10, 10
		DrawText "Press Esc to exit.", 10, 25

	End

End




'------------------------------------------
' Main DATA Class
'------------------------------------------
' This exact Class isn't supposed to be used
' Instead, you should use some of the fields
' in your own Class, Or just use this one
' For reference, To see what each Field does
Class circle
	Field x#, y#		'position
	Field dx#, dy#		'x And y speeds
	
	Field radius#		'radius of circle
	Field mass#			'mass of circle
End


'------------------------------------------
' SET UP BALLS INTO A POOL STYLE ARRANGEMENT
' For DEMO
'------------------------------------------
Function Setup ()

	Local ballTriangleSize=5

	For Local xloop = ballTriangleSize To 1 Step -1
		For Local yloop = 1 To xloop
			Local c:circle = New circle
			c.x = (5-xloop)*27 + 200
			c.y = yloop*31-(xloop*31)/2.0 + 300
			c.dx=0
			c.dy=0
			c.radius = 15
			c.mass = 50
			Circles.AddLast c
		Next
	Next

	'Cue ball (smaller so you know which it is :)
	Local cue:circle = New circle
	cue.x = 800
	cue.y = 300 +20
	cue.dx = -20
	cue.dy = Rnd(4)-2
	cue.radius = 14
	cue.mass = 50
	Circles.AddLast cue

End

Function Main ()
	New Pool
End









'------------------------------------------
Function UpdateCirclePhysics()
'------------------------------------------
' This is the Main physics Function For the
' circles. It contains the very basic
' movement physics as well as the collision
' response code.
'------------------------------------------

	For Local c:circle = Eachin Circles
	
		'update positions
		c.x=c.x+c.dx
		c.y=c.y+c.dy
		
		'gradually slow down
		c.dx=c.dx*0.99
		c.dy=c.dy*0.99
		
		'------------------------------------------
		'COLLISION CHECKING
		'------------------------------------------
		' Check each circle in the loop against
		' every other (c against c2)
		For Local c2:circle = Eachin Circles
		
			Local collisionDistance# = c.radius+c2.radius
			Local actualDistance# = Sqrt(Pow(c2.x-c.x, 2)+Pow(c2.y-c.y, 2))
			
			'Collided Or Not?
			If actualDistance&lt;collisionDistance Then
				
				Local collNormalAngle#=ATan2(c2.y-c.y, c2.x-c.x)

				'Position exactly touching, no intersection
				Local moveDist1#=(collisionDistance-actualDistance)*(c2.mass/Float((c.mass+c2.mass)))
				Local moveDist2#=(collisionDistance-actualDistance)*(c.mass/Float((c.mass+c2.mass)))
				c.x=c.x + moveDist1*Cos(collNormalAngle+180)
				c.y=c.y + moveDist1*Sin(collNormalAngle+180)
				c2.x=c2.x + moveDist2*Cos(collNormalAngle)
				c2.y=c2.y + moveDist2*Sin(collNormalAngle)
				
				
				'------------------------------------------
				'COLLISION RESPONSE
				'------------------------------------------
				'n = vector connecting the centers of the circles.
				'we are finding the components of the normalised vector n
				Local nX#=Cos(collNormalAngle)
				Local nY#=Sin(collNormalAngle)
				
				'now find the length of the components of each movement vectors
				'along n, by using dot product.
				Local a1# = c.dx*nX  +  c.dy*nY
				Local a2# = c2.dx*nX +  c2.dy*nY
				
				'optimisedP = 2(a1 - a2)
				'             ----------
				'              m1 + m2
				Local optimisedP# = (2.0 * (a1-a2)) / (c.mass + c2.mass)
				
				'now find out the resultant vectors
				'r1 = c1.v - optimisedP * mass2 * n
				c.dx = c.dx - (optimisedP*c2.mass*nX)
				c.dy = c.dy - (optimisedP*c2.mass*nY)
				
				'r2 = c2.v - optimisedP * mass1 * n
				c2.dx = c2.dx + (optimisedP*c.mass*nX)
				c2.dy = c2.dy + (optimisedP*c.mass*nY)

			End If

		Next
		'------------------------------------------
		'------------------------------------------
		
		
		'Simple Bouncing off walls.
		If c.x&lt;c.radius Then
			c.x=c.radius
			c.dx=c.dx*-0.9
		End If
		If c.x&gt;DeviceWidth()-c.radius Then
			c.x=DeviceWidth()-c.radius
			c.dx=c.dx*-0.9
		End If
		If c.y&lt;c.radius Then
			c.y=c.radius
			c.dy=c.dy*-0.9
		End If
		If c.y&gt;DeviceHeight()-c.radius Then
			c.y=DeviceHeight()-c.radius
			c.dy=c.dy*-0.9
		End If
		
	Next

End





'------------------------------------------
Function RenderCircles()
'------------------------------------------
' Simple Function draws all the circles
' on the screen.
'------------------------------------------

	For Local c:circle = Eachin Circles
		If c.radius=15 Then SetColor 200, 50, 50 Else SetColor 255, 255, 255
		DrawOval c.x-c.radius, c.y-c.radius, c.radius*2, c.radius*2
	Next
	
End
</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matty</td><td align="right"><font class=tiny>2012</font></td></tr></table></td></tr><tr ><td class="posttext"> In the monkey example my browser freezes when compiled either for html5 or flash as soon as the user clicks the mouse after the balls have moved.  I'm using Chrome, and Monkey 45c.  Otherwise looks good. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>2012</font></td></tr></table></td></tr><tr ><td class="posttext"> Bit odd -- I'm on Chrome and it's fine here. I also ran it without problems on GLFW and my Android phone. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>2012</font></td></tr></table></td></tr><tr ><td class="posttext"> works fine here in Safari and Chrome using monkey 53.<br><br>I have been using this code for a few years although not for pool. Actually this code was what motivated me to do more research on vector physics and start learning all about vectors.<br><br>it's good for fairly simple simulation. I wasn't able to use it as it moves balls apart when they over lap(causes a collision inaccuracy) instead of finding the exact point of collision and it fails completely if the speed of a ball exceed its diameter. I needed something that works dynamically.<br><br>I find it useful for other things but was not able to use it for my pool game where I needed to find the exact point of collision.<br><br>I am not nocking it down as it has it's uses and the collision response is really good but not for acceptable pool physics, at least for me it wasn't. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hardcoal</td><td align="right"><font class=tiny>2012</font></td></tr></table></td></tr><tr ><td class="posttext"> great submission <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Captain Wicker (crazy hillbilly)</td><td align="right"><font class=tiny>2012</font></td></tr></table></td></tr><tr ><td class="posttext"> lovely! :) <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
