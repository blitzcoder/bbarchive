<!DOCTYPE html><html lang="en" ><head ><title >HC-256</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='HC-256 stream cipher in BlitzMax, language=bmx, category=Algorithms'><meta name='author' content='Otus'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=6>Algorithms</a>/HC-256</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2292.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >HC-256 by Otus</td><td align="right">2008 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> <a href="http://en.wikipedia.org/wiki/HC-256" target="_blank">HC-256</a> is a stream cipher designed for easy and efficient software implementation. It has a 256 bit key and uses two S-boxes of 1024 32-bit integers. It uses 32-bit integer operations to get best speed on current machines, and can be implemented in parallel, due to low dependence between operations. It is not covered by any patents.<br><br>This BlitzMax implementation is a naive unoptimized version that may not be 100% compatible. It's still fast enough, if you don't switch keys very often. Inlining functions and unrolling some loops could increase speed 10-fold.<br><br>Initialize, EncryptString and DecryptString are nonstandard examples to show how it can be used. </td></tr><tr ><td class="cell"><pre class="code">SuperStrict

Extern "C"

	Function Ror:Int(i:Int, n:Int) = "_rotr"
	
End Extern

Type THcContext
	
	'S-boxes
	Field _P:Int[]
	Field _Q:Int[]
	
	'Counter
	Field _i:Int
	
	Method Init(k:Int[] , iv:Int[])
		Assert (k.Length = 8) And (iv.Length = 8), "K and IV must be 8 Ints long!"
		Local W:Int[] = k + iv + New Int[2544]
		
		For Local i:Int = 16 To 2559
			W[i] = _f2(W[i-2]) + W[i-7] + _f1(W[i-15]) + W[i-16] + i
		Next
		
		_P = W[512..1536]
		_Q = W[1536..]
		
		Assert _P.Length = 1024 , "P size: " + _P.Length
		Assert _Q.Length = 1024 , "Q size: " + _Q.Length
		
		For Local i:Int = 0 Until 4096
			Local j:Int = i &amp; 1023
			If (i &amp; 2047) &lt; 1024
				_P[j] :+ _P[(j - 10) &amp; 1023] ..
					+ _g1( _P[(j-3) &amp; 1023], _P[(j-1023) &amp; 1023] )
			Else
				_Q[j] :+ _Q[(j - 10) &amp; 1023] ..
					+ _g2( _Q[(j-3) &amp; 1023], _Q[(j-1023) &amp; 1023] )
			End If
		Next
		
		_i = 0
	End Method
	
	Method Initialize(key:String , salt:String) 
		If key.Length &lt; 8 Then key = key[..8]
		If salt.Length &lt; 8 Then salt = salt[..8]
		Local k:Int[8] , iv:Int[8]
		Local l:Int = key.Length
		For Local i:Int = 0 Until l
			k[i &amp; 7] = key[i] + _f1( key[(i+3) Mod l] ) + _f2( key[(i+5) Mod l] )
		Next
		l = salt.Length
		For Local i:Int = 0 Until l
			iv[i &amp; 7] = salt[i] + _f1( salt[(i+3) Mod l] ) + _f2( salt[(i+5) Mod l] )
		Next
		Init k, iv
	End Method
	
	Method Output:Int() 
		Local j:Int = _i &amp; 1023
		If (_i &amp; 2047) &lt; 1024
			_i :+ 1
			_P[j] :+ _P[(j - 10) &amp; 1023] ..
				+ _g1( _P[(j-3) &amp; 1023], _P[(j-1023) &amp; 1023] ) 
			Return _h1( _P[(j-12) &amp; 1023] ) ~ _P[j]
		Else
			_i :+ 1
			_Q[j] :+ _Q[(j - 10) &amp; 1023] ..
				+ _g2( _Q[(j-3) &amp; 1023], _Q[(j-1023) &amp; 1023] ) 
			Return _h2( _Q[(j-12) &amp; 1023] ) ~ _Q[j]
		End If
	End Method
	
	Method DecryptString:String(s:String)
		Local s2:String , i:Int, k:Int
		Local j:Int, slen:Int
		
		For k = 0 To 3
			j :+ s[k] Shl (k Shl 3)
		Next
		slen = j ~ Output()
		s = s[4..]
		
		While i &lt; slen
			j = s[i] + (s[i + 1] Shl 8) + (s[i + 2] Shl 16) + (s[i + 3] Shl 24) 
			j :~ Output() 
			For k = 0 To 3
				If s2.Length = slen Then Exit
				s2 :+ Chr( (j Shr (k Shl 3) ) &amp; 255 )
			Next
			i :+ 4
		Wend
		
		Return s2
	End Method
	
	Method EncryptString:String(s:String)
		Local s2:String , i:Int, k:Int
		Local j:Int = s.Length ~ Output() 
		
		For k = 0 To 3
			s2 :+ Chr( (j Shr (k Shl 3)) &amp; 255 ) 
		Next
		
		While i &lt; s.Length
			j = s[i] + (s[i + 1] Shl 8) + (s[i + 2] Shl 16) + (s[i + 3] Shl 24) 
			j :~ Output() 
			For k = 0 To 3
				s2 :+ Chr( (j Shr (k Shl 3)) &amp; 255 ) 
			Next
			i :+ 4
		Wend
		
		Return s2
	End Method
	
	Function _f1:Int(x:Int) 
		Return Ror(x , 7) ~ Ror(x , 18) ~ Ror(x , 3) 
	End Function
	
	Function _f2:Int(x:Int) 
		Return Ror(x , 17) ~ Ror(x , 19) ~ Ror(x , 10) 
	End Function
	
	Method _g1:Int(x:Int , y:Int) 
		Return ( Ror(x , 10) ~ Ror(y , 23) ) + _Q[ (x ~ y) &amp; 1023 ]
	End Method
	
	Method _g2:Int(x:Int , y:Int) 
		Return ( Ror(x , 10) ~ Ror(y , 23) ) + _P[ (x ~ y) &amp; 1023 ]
	End Method
	
	Method _h1:Int(x:Int) 
		Local b:Byte Ptr = Byte Ptr( Varptr x ) 
		Return _Q[ b[0] ] + _Q[ 256 + b[1] ] + _Q[ 512 + b[2] ] + _Q[ 768 + b[3] ]
	End Method
	
	Method _h2:Int(x:Int) 
		Local b:Byte Ptr = Byte Ptr( Varptr(x) ) 
		Return _P[ b[0] ] + _P[ 256 + b[1] ] + _P[ 512 + b[2] ] + _P[ 768 + b[3] ]
	End Method
	
End Type</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Otus</td><td align="right"><font class=tiny>2008</font></td></tr></table></td></tr><tr ><td class="posttext"> Test program. Update the CLOCK_SPEED constant to get the speeds on your computer correctly.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict

Framework BRL.StandardIO

Import "hc-256.bmx"

Local e:THcContext = New THcContext

Const PASS:String = "Something about a fox jumping..."
Const SALT:String = "amgo3b7kjd02k.;*'4n6¤!v]jhd92?=0"
Const CLOCK_SPEED:Float = 1000000000

e.Initialize PASS , SALT

Local s2:String = e.EncryptString("Hello World")
Print "&lt;"+s2+"&gt;"

e.Initialize PASS , SALT

Print "&lt;" + e.DecryptString(s2) + "&gt;"

'Test speed

Local time:Int = MilliSecs()
For Local i:Int = 1 To 1000
	e.Initialize Pass , Salt
Next
Print "Init took: " + ( CLOCK_SPEED * (MilliSecs() - time) / 1000000.0) + " cycles"

time = MilliSecs()
For Local i:Int = 1 To 1000000
	e.Output()
Next
Print "Output took: " + ( CLOCK_SPEED * (MilliSecs() - time) / 100000000.0 / 32 ) + " cycles/bit"</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Blueapples</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> I get this error on Mac OS 10.6 when I try to build this:<br><br><pre class=code>
Building baGrabbar
Compiling:ini.bmx
Compiling:md5.bmx
Compiling:hc256.bmx
Compiling:parseparams.bmx
Compiling:baGrabbar.bmx
Linking:baGrabbar.debug
Undefined symbols:
  "__rotr", referenced from:
      __bb_THcContext__f1 in hc256.bmx.debug.macos.x86.o
      __bb_THcContext__f1 in hc256.bmx.debug.macos.x86.o
      __bb_THcContext__f1 in hc256.bmx.debug.macos.x86.o
      __bb_THcContext__f2 in hc256.bmx.debug.macos.x86.o
      __bb_THcContext__f2 in hc256.bmx.debug.macos.x86.o
      __bb_THcContext__f2 in hc256.bmx.debug.macos.x86.o
      _522 in hc256.bmx.debug.macos.x86.o
      _522 in hc256.bmx.debug.macos.x86.o
      _530 in hc256.bmx.debug.macos.x86.o
      _530 in hc256.bmx.debug.macos.x86.o
ld: symbol(s) not found
collect2: ld returned 1 exit status
Build Error: Failed to link /Users/isaac/Documents/Projects/BlitzMax/baGrabbar/baGrabbar.debug.app/Contents/MacOS/baGrabbar.debug
Process complete
</pre><br><br>Any ideas? <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> _rotr is probably not exposed on that platform.. try removing or adding some underscores. if that fails your sol ;)<br>Unless you can find an appropriate replacement that is. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Otus</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> Well, you can just implement it yourself. Somethings like:<br><pre class=code>Function Ror:Int(i:Int, n:Int)
	Return (i Shr n) | (i Shl (32-n))
End Function</pre><br>(Untested.) <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Blueapples</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> Thank Otus, that makes up with implementations I have seen in C but had trouble translating (and finding against after I saw them once). We'll see if it works! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Blueapples</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> I had to make some additions to get the demo code to run. EncryptString now pads out the input string to a multiple of 4 (due to the way it is implemented), and DecryptString now calls .Trim() on it's result.<br><br>Marked with begin iraway ... end iraway<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
	Method DecryptString:String(s:String)
		Local s2:String , i:Int, k:Int
		Local j:Int, slen:Int
		
		For k = 0 To 3
			j :+ s[k] Shl (k Shl 3)
		Next
		slen = j ~ Output()
		s = s[4..]
		
		While i &lt; slen
			j = s[i] + (s[i + 1] Shl 8) + (s[i + 2] Shl 16) + (s[i + 3] Shl 24) 
			j :~ Output() 
			For k = 0 To 3
				If s2.Length = slen Then Exit
				s2 :+ Chr( (j Shr (k Shl 3) ) &amp; 255 )
			Next
			i :+ 4
		Wend
		
		' begin iraway: remove extra spaces (may have been used to pad to multiple of 4)
		Return s2.Trim()
		' end iraway
	End Method
	
	Method EncryptString:String(s:String)
		Local s2:String , i:Int, k:Int
		
		' begin iraway: pad input string to be a multiple of 4
		For i = s.Length To (Ceil(1.0 * s.Length / 4) * 4) - 1
			s :+ " "
		Next
		Print "[" + s + "]"
		i = 0
		' end iraway
		
		Local j:Int = s.Length ~ Output() 
		
		For k = 0 To 3
			s2 :+ Chr( (j Shr (k Shl 3)) &amp; 255 ) 
		Next
		
		While i &lt; s.Length
			j = s[i] + (s[i + 1] Shl 8) + (s[i + 2] Shl 16) + (s[i + 3] Shl 24) 
			j :~ Output() 
			For k = 0 To 3
				s2 :+ Chr( (j Shr (k Shl 3)) &amp; 255 ) 
			Next
			i :+ 4
		Wend
		
		Return s2
	End Method
</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Blueapples</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> Otherwise this is awesome and super easy to use. Thanks for the contribution, Otus! <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
