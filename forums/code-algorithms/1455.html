<!DOCTYPE html><html lang="en" ><head ><title >Binary Decision Tree</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Binary Decision Tree AI Algorithm, language=bb, category=Algorithms'><meta name='author' content='Techlord'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=6>Algorithms</a>/Binary Decision Tree</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="1455.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Binary Decision Tree by Techlord</td><td align="right">2005 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Blitz3D Version of Binary Decision Tree AI Algorithm found in <a href="http://www.generation5.org/content/2004/bdt-implementation.asp" target="_blank">Article</a> and <a href="http://www.generation5.org/content/2004/data/DecisionTree.zip" target="_blank">C++ Source Code</a>. </td></tr><tr ><td class="cell"><pre class="code">;//TreeNode 

Type TreeNode
	Field m_strQuestOrAns$;
	Field m_iNodeID%
	Field m_pYesBranch.TreeNode
	Field m_pNoBranch.TreeNode
End Type

Function TreeNodeNew.TreeNode(nodeID%=0,newQorA$="");
	;set the objects pointers To Null in Default constructor
	this.TreeNode = New TreeNode
	this\m_iNodeID = nodeID
	this\m_strQuestOrAns = newQorA
	this\m_pYesBranch = Null
	this\m_pNoBranch= Null
	Return this
End Function

Function TreeNodeDelete(this.TreeNode)
	Delete this
End Function

;// DecisionTree

Type DecisionTree
	Field m_pRootNode.TreeNode;
End Type

Function DecisionTreeRemoveNode(this.DecisionTree,node.TreeNode)
	If(node &lt;&gt; Null)
		If(node\m_pYesBranch &lt;&gt; Null) DecisionTreeRemoveNode(this,node\m_pYesBranch);
		If(node\m_pNoBranch &lt;&gt; Null) DecisionTreeRemoveNode(this,node\m_pNoBranch);
		Print("deleting node "+node\m_iNodeID);
		Delete node;
	EndIf
End Function

Function DecisionTreeOutputBinaryTree(this.DecisionTree,tag$,currentNode.TreeNode)
	If(currentNode = Null) Return
		
	Write("[" + tag$ + "] node id = ")
	Write(currentNode\m_iNodeID)
	Write(", question/answer = ")
	Print(currentNode\m_strQuestOrAns)
	
	; Go down yes branch
	DecisionTreeOutputBinaryTree(this,tag$+".1",currentNode\m_pYesBranch);
	; Go down no branch
	DecisionTreeOutputBinaryTree(this,tag$+".2",currentNode\m_pNoBranch);
End Function

Function DecisionTreeOutput(this.DecisionTree)
	DecisionTreeOutputBinaryTree(this,"1", this\m_pRootNode);
End Function

Function DecisionTreeAskQuestion(this.DecisionTree,node.Treenode)
	Print(node\m_strQuestOrAns + " (enter yes or no)")
	answer$=Input()
	If(answer = "yes")
		DecisionTreeQueryBinaryTree(this,node\m_pYesBranch)
	ElseIf(answer = "no")
		DecisionTreeQueryBinaryTree(this,node\m_pNoBranch)
	Else
		Print("Error please answer yes or no.")
		DecisionTreeAskQuestion(this,node)
	EndIf
End Function

Function DecisionTreeQuery(this.DecisionTree)
	DecisionTreeQueryBinaryTree(this,this\m_pRootNode);
End Function

Function DecisionTreeQueryBinaryTree(this.DecisionTree,currentnode.TreeNode)
	If(currentNode\m_pYesBranch = Null)
		;If both the yes And no branch pointers are Null 
		;the tree is at a decision outcome state so output
		;the String
		If(currentNode\m_pNoBranch = Null)
			Print(currentNode\m_strQuestOrAns)
		Else
			Print("Missing yes branch at " + currentNode\m_strQuestOrAns + " question")
		EndIf		
		Return
	EndIf
	If(currentNode\m_pNoBranch = Null)
		Print("Missing no branch at " + currentNode\m_strQuestOrAns + " question")
		Return
	EndIf
	;otherwise Default To asking the question at the currentNode
	DecisionTreeAskQuestion(this,currentNode);
End Function

Function DecisionTreeAddYesNode(this.DecisionTree,existingNodeID%,newNodeID%,newQorA$)
	;If you dont have a root node you cant add another node
	If(this\m_pRootNode = Null)
		Write("Error - no root node in AddYesNode()")
		Return
	EndIf
	;otherwise query tree And add node
	If(DecisionTreeSearchTreeAndAddYesNode(this,this\m_pRootNode,existingNodeID%,newNodeID%,newQorA$))
		Write("Added 'yes' node")
		Write(newNodeID)
		Write(" onto 'yes' branch of node ")
		Print(existingNodeID)
	Else
		Write("'yes' Node ")
		Write(existingNodeID)
		Print(" not found")
	EndIf
End Function

Function DecisionTreeAddNoNode(this.DecisionTree,existingNodeID%,newNodeID%,newQorA$)
	If(this\m_pRootNode = Null)
		Print("Error no root node in AddNoNode()")
		Return
	EndIf
	If(DecisionTreeSearchTreeAndAddNoNode(this,this\m_pRootNode, existingNodeID, newNodeID, newQorA))
		Write("Added 'no' node")
		Write(newNodeID)
		Write(" onto 'no' branch of node ")
		Print(existingNodeID)
	Else
		Write("'no' Node ")
		Write(existingNodeID)
		Print(" not found")
	EndIf
End Function

Function DecisionTreeSearchTreeAndAddYesNode(this.DecisionTree,currentNode.TreeNode,existingNodeID%,newNodeID%,newQorA$)
	If(currentNode\m_iNodeID = existingNodeID)
		;create node
		If(currentNode\m_pYesBranch = Null)
			currentNode\m_pYesBranch = TreeNodeNew(newNodeID%,newQorA$)
		Else
			currentNode\m_pYesBranch = TreeNodeNew(newNodeID%,newQorA$)
		EndIf
		Return True
	Else
		;try yes branch If it exists
		If(currentNode\m_pYesBranch &lt;&gt; Null)
			If(DecisionTreeSearchTreeAndAddYesNode(this,currentNode\m_pYesBranch,existingNodeID,newNodeID,newQorA))
				Return True
			Else
				;try no branch If it exists
				If(currentNode\m_pNoBranch &lt;&gt; Null)
					Return(DecisionTreeSearchTreeAndAddYesNode(this,currentNode\m_pNoBranch,existingNodeID,newNodeID,newQorA))
				Else
					Return False
				EndIf	
			EndIf
		EndIf
		Return False
	EndIf
End Function

Function DecisionTreeSearchTreeAndAddNoNode%(this.DecisionTree,currentNode.TreeNode,existingNodeID%,newNodeID%,newQorA$)
	If(currentNode\m_iNodeID = existingNodeID)
		If(currentNode\m_pNoBranch = Null)
			currentNode\m_pNoBranch = TreeNodeNew(newNodeID,newQorA$)
		Else
			currentNode\m_pNoBranch = TreeNodeNew(newNodeID,newQorA$)
		EndIf
		Return True
	Else
		If(currentNode\m_pYesBranch &lt;&gt; Null)
			If(DecisionTreeSearchTreeAndAddNoNode(this,currentNode\m_pYesBranch,existingNodeID%,newNodeID%,newQorA$))
				Return True
			Else
				If(currentNode\m_pNoBranch &lt;&gt; Null)
					Return(DecisionTreeSearchTreeAndAddNoNode(this,currentNode\m_pNoBranch,existingNodeID%,newNodeID%,newQorA$))
				Else
					Return False
				EndIf
			EndIf

		Else 
			Return False
		EndIf	
	EndIf
End Function

Function DecisionTreeCreateRootNode(this.DecisionTree,nodeID%,newQorA$);
	this\m_pRootNode = TreeNodeNew(nodeID%,newQorA$);
End Function
	
	
Function DecisionTreeNew.DecisionTree()
	this.DecisionTree = New DecisionTree
	this\m_pRootNode = Null
	Return this
End Function

Function DecisionTreeDelete(this.DecisionTree)
	 DecisionTreeRemoveNode(this,this\m_pRootNode)
End Function

;;main
Graphics(800,600,16,2)

;create he New decision tree Object
newTree.DecisionTree = DecisionTreeNew();

;add the required root node
DecisionTreeCreateRootNode(newTree,1,"Have you got a weapon?");
	
;add subsequent nodes based on problem definition
DecisionTreeAddYesNode(newTree,1,2,"Are you close enough to attack?");
DecisionTreeAddNoNode(newTree,1,3,"Can you tackle bare-handed?");
DecisionTreeAddYesNode(newTree,2,4,"Attack!!!");
DecisionTreeAddNoNode(newTree,2,5,"Don't attack!!!");
DecisionTreeAddYesNode(newTree,3,6,"Attack!!!");
DecisionTreeAddNoNode(newTree,3,7,"Don't attack!!!");

;output the created tree 
DecisionTreeOutput(newTree);

;query the tree
DecisionTreeQuery(newTree);

Print("Press any key to quit...")

;pause
WaitKey()

Delete newTree</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>2005</font></td></tr></table></td></tr><tr ><td class="posttext"> this is useful thanks! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rook Zimbabwe</td><td align="right"><font class=tiny>2006</font></td></tr></table></td></tr><tr ><td class="posttext"> AI in Blitz... whoa!!! :) <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
