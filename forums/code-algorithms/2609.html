<!DOCTYPE html><html lang="en" ><head ><title >neural network</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Some bmax types to make/simulate neural networks, language=bmx, category=Algorithms'><meta name='author' content='Nate the Great'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=6>Algorithms</a>/neural network</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2609.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >neural network by Nate the Great</td><td align="right">2009 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This is a little code I wrote to create and solve neural networks.  It only has the basics as this is a rapidly new/changing field and too hard to keep up with or understand all of the ideas about how it should be implemented.  So I summed the basics up in a nice neat package for my own use and figured I would release it.  It is very good for game ai's especially if you use backpropagation or evolution to make the neural networks improve. examples coming soon.<br><br>edit: ok new fish example! :) </td></tr><tr ><td class="cell"><pre class="code">SuperStrict
SeedRnd(MilliSecs())
Graphics 800,600,0,60

For Local i:Int = 0 To 20
	food.Create(Rnd(800),Rnd(600))
Next

For Local b:Int = 0 To 20
	Local f:fish = fish.Create(Rnd(800),Rnd(600),0,fish.makebrain:neuralnet())
Next

Global generation:Int = 0
Global simstep:Long = 0

Global finalsimstep:Long
Global finalflag:Byte

Global render:Int = True
Global gopast10:Int = True

Global selected:fish

While Not AppTerminate()
	If KeyHit(key_space) Then
		render = 1-render
	EndIf
	If KeyHit(key_f) Then
		gopast10 = 1-gopast10
	EndIf
	If render Then Cls
	simstep = simstep + 1
	
	food.update()
	fish.update()
	
	Local sf:fish
	For sf:fish = EachIn fish.fishlist
		If sf:fish = selected:fish And sf.alive Then
			sf.drawnnet()
		EndIf
	Next
	
	If MouseHit(1)
		selected:fish = Null
		For sf:fish = EachIn fish.fishlist
			Local dx# = MouseX()-sf.x
			Local dy# = MouseY()-sf.y
			Local dist# = Sqr(dx*dx + dy*dy)
			If dist &lt;= 20 Then
				selected:fish = sf:fish
			EndIf
		Next
	EndIf
	
	If KeyHit(key_p) And selected:fish  &lt;&gt; Null Then
		neuralnet.printnnet(selected.brain:neuralnet)
	EndIf
	neuralnet.update()
	
	SetColor 255,255,255
	DrawText "Generation: "+generation,1,1
	DrawText "Simulation Step: "+simstep,1,20
	If gopast10 = True Then
		DrawText "Simulating full generation (press f to change)",1,40
	Else
		DrawText "Only simulating generations to 10 (press f to change)",1,40
	EndIf
	
	If fish.livecount &lt;= 10 And finalflag = False Then
		finalsimstep = simstep-1
		finalflag = True
	EndIf
	If (finalflag And gopast10 = False) Or fish.livecount = 0 Then
		generation :+ 1
		
		finalflag = False
		Print (generation-1)+ "  length: "+simstep
				
		For Local f:fish = EachIn fish.fishlist
			If f.life &gt;= finalsimstep And f.life &gt; 0 Then
				'Print "good "+f.life
				f.alive = True
				f.hunger = 0
				Local ff:fish = fish.Create(Rnd(800),Rnd(600),0,neuralnet.copy(f.brain:neuralnet))
				ff.makemistake()
			ElseIf f.life &gt; 0
				'Print "bad "+f.life
				f.remove()
			EndIf
		Next
		simstep = 0
	EndIf
	
	If render Then Flip
Wend

Type food
	Global foodlist:TList = New TList
		
	Field x#
	Field y#
	Field dist#
	
	Function Create:food(x#,y#)
		Local f:food = New food
		f.x = x
		f.y = y
		foodlist.addlast(f:food)
		Return f:food
	End Function
	
	Function update()
		For Local f:food = EachIn foodlist
			If render Then f.draw()
		Next
	End Function
	
	Method draw()
		SetColor(0,255,0)
		DrawOval(x-5,y-5,10,10)
	End Method
	
	Function getfoodsmell:Float(x#,y#)		'returns the amount of smell of food there is at a certain point
		Local smell:Float = 0
		For Local f:food = EachIn foodlist
			Local dx:Float = x-f.x
			Local dy:Float = y-f.y
			Local dist:Float = Sqr(dx*dx + dy*dy)
			If dist &lt; 50 And dist &gt; 15 Then
				smell :+ (50-dist)/50
			ElseIf dist &lt;= 15 Then
				smell :+ 1
			EndIf
		Next
		Return smell#
	End Function
	
	Function getclosefood:food(x#,y#)	'returns the food closest to given point x,y
		Local closest:Float = 10000
		Local closestfood:food
		Local dist:Float
		For Local f:food = EachIn foodlist
			Local dx:Float = x-f.x
			Local dy:Float = y-f.y
			dist:Float = Sqr(dx*dx + dy*dy)
			If dist &lt; closest Then
				closestfood:food = f:food
				closest = dist
			EndIf
		Next
		If closest &lt; 25 Then Return closestfood:food
	End Function
End Type

Type Fish
	Global fishlist:TList = New TList
	Global livecount:Int
	
	Field x#
	Field y#
	Field angle#
	Field anglespd#
	Field hunger#
	Field heartcount:Int
	Field hearton:Byte
	Field brain:neuralnet
	Field alive:Byte
	Field life:Long
	Field gen:Int
	
	Function update()
		livecount = 0
		Local onedied:Int = 0
		Local totcnt:Int
		For Local f:fish = EachIn fishlist
			totcnt = totcnt + 1
			If f.alive Then
				livecount :+ 1
				f.hunger = f.hunger + .001
				f.heartcount = f.heartcount + 1
				If f.heartcount &gt; 20 Then
					f.hearton = 1-f.hearton
					f.heartcount = 0
				EndIf
				Local hrt:neuron = f.brain.findneuron("heart")
				hrt.setenergy(f.hearton)
				Local hng:neuron = f.brain.findneuron("hunger")
				hng.setenergy(f.hunger)
				Local lft:neuron = f.brain.findneuron("left")
				Local rgt:neuron = f.brain.findneuron("right")
				f.anglespd# = f.anglespd - lft.getenergy()*5 + rgt.getenergy()*5	'calculate steering
				f.anglespd = f.anglespd * .9
				If f.anglespd &gt; 4 Then f.anglespd = 4
				If f.anglespd &lt; -4 Then f.anglespd = -4
				f.angle = f.angle + f.anglespd
				Local spd# = (lft.getenergy()*7+rgt.getenergy()*7)
				If spd &gt; 5 Then spd = 5
				If spd &lt; -5 Then spd = -5
				f.x = f.x + Cos(f.angle)*spd
				f.y = f.y + Sin(f.angle)*spd
				If render Then f.draw()
				f.checkfood()
				f.checkenemy()
				f.life = simstep
				If Abs(f.hunger) &gt; 1 And onedied = 0 Then
					f.alive = False
					f.life = simstep
					onedied = 1
				EndIf
				If f.x &lt; -20 Then f.x = 820
				If f.x &gt; 820 Then f.x = -20
				If f.y &lt; -20 Then f.y = 620
				If f.y &gt; 620 Then f.y = -20
			'	neuralnet.printnnet(f.brain:neuralnet)		
			EndIf
		Next
	End Function
	
	Method draw()
		SetColor(100,255,100)
		DrawLine(x,y,x+Cos(angle+30)*30,y+Sin(angle+30)*30)
		DrawLine(x,y,x+Cos(angle-30)*30,y+Sin(angle-30)*30)
		SetColor(0,50,255)
		DrawOval(x-20,y-20,40,40)
		SetColor(255,255,255)
		DrawLine(x,y,x+Cos(angle)*30,y+Sin(angle)*30)
		SetColor ((hunger+1)/2)*255,((-hunger+1)/2)*255,0
		SetBlend alphablend
		SetAlpha .5
		DrawRect x-20,y-35,((-hunger+1)/2)*40,5
		SetAlpha 1
		SetColor 255,255,255
		DrawText gen,x-10,y-10
	End Method
	
	Method checkfood()
		Local x1:Float = x+Cos(angle-30)*30
		Local y1:Float = y+Sin(angle-30)*30
		Local smell1# = food.getfoodsmell(x1,y1)
		Local foodleft:neuron = brain.findneuron("foodlft")
		foodleft.setenergy(smell1)
		Local x2:Float = x+Cos(angle+30)*30
		Local y2:Float = y+Sin(angle+30)*30
		Local smell2# = food.getfoodsmell(x2,y2)
		Local foodright:neuron = brain.findneuron("foodrgt")
		foodright.setenergy(smell2)
		Local f:food = food.getclosefood(x,y)
		If f:food &gt;&lt; Null Then
			f.x = Rnd(800)
			f.y = Rnd(600)
			hunger :- .7
		EndIf
	End Method
	
	Method checkenemy()
		Local smell1#
		Local smell2#
		For Local f:fish = EachIn fishlist
			If f:fish &gt;&lt; Self Then
				Local dx# = (x+Cos(angle-30)*30)-f.x
				Local dy# = (y+Sin(angle-30)*30)-f.y
				Local dist# = Sqr(dx*dx + dy*dy)
				If dist &lt;= 50 Then
					smell1# = smell1# + (50-dist#)/50.0
				EndIf
				dx# = (x+Cos(angle+30)*30)-f.x
				dy# = (y+Sin(angle+30)*30)-f.y
				dist# = Sqr(dx*dx + dy*dy)
				If dist &lt;= 50 Then
					smell2# = smell2# + (50-dist#)/50.0
				EndIf
			EndIf
		Next
		brain.findneuron("enemylft").setenergy(smell1)
		brain.findneuron("enemyrgt").setenergy(smell2)
	End Method
	
	Function Create:fish(x#,y#,angle#,brain:neuralnet)
		Local f:fish = New fish
		f.x = x
		f.y = y
		f.angle = angle
		f.brain = brain
		f.heartcount = Rnd(0,19)
		f.hearton = Rnd(0,1)
		f.alive = True
		f.gen = generation
		fishlist.addlast(f:fish)
		Return f:fish
	End Function
	
	Method drawnnet()
		SetAlpha .4
		SetColor 255,255,255
		DrawRect x+20,y+20,150,150
		SetAlpha 1
		
		SetColor 0,0,(brain.findneuron("heart").getenergy()*255)
		DrawOval x+87,y+92,6,6
		
		SetColor ((-brain.findneuron("hunger").getenergy())*255),0,(brain.findneuron("hunger").getenergy()*255)
		DrawOval x+97,y+92,6,6
		
		SetColor ((-brain.findneuron("n1").getenergy())*255),0,(brain.findneuron("n1").getenergy()*255)
		DrawOval x+92,y+40,6,6
		
		SetColor ((-brain.findneuron("n2").getenergy())*255),0,(brain.findneuron("n2").getenergy()*255)
		DrawOval x+52,y+115,6,6
		
		SetColor ((-brain.findneuron("n3").getenergy())*255),0,(brain.findneuron("n3").getenergy()*255)
		DrawOval x+132,y+115,6,6
		
		SetColor ((-brain.findneuron("c1").getenergy())*255),0,(brain.findneuron("c1").getenergy()*255)
		DrawOval x+92,y+140,6,6
		
		SetColor ((-brain.findneuron("left").getenergy())*255),0,(brain.findneuron("left").getenergy()*255)
		DrawOval x+30,y+70,6,6
		
		SetColor ((-brain.findneuron("right").getenergy())*255),0,(brain.findneuron("right").getenergy()*255)
		DrawOval x+157,y+70,6,6
		
		SetColor ((-brain.findneuron("foodlft").getenergy())*255),0,(brain.findneuron("foodlft").getenergy()*255)
		DrawOval x+30,y+30,6,6
		
		SetColor ((-brain.findneuron("foodrgt").getenergy())*255),0,(brain.findneuron("foodrgt").getenergy()*255)
		DrawOval x+157,y+30,6,6
	End Method
	
	Function MakeBrain:neuralnet()
		Local n:neuralnet = neuralnet.Create()
		Local hrt:neuron = neuron.Create(0,"heart")	'this neuron will "beat" at a steady rate
		Local hngr:neuron = neuron.Create(-1,"hunger")	'this neuron will slowly turn on if a fish has not eaten in a while, negative if a fish is over full
		
		Local n1:neuron = neuron.Create(Rnd(-1,1.0),"n1")	'neurons that do the logic
		Local n2:neuron = neuron.Create(Rnd(-1,1.0),"n2")
		Local n3:neuron = neuron.Create(Rnd(-1,1.0),"n3")
		
		Local nc1:neuron = neuron.Create(Rnd(-1,1.0),"c1")
				
		Local lft:neuron = neuron.Create(-1,"left")		'neurons that control movement
		Local rgt:neuron = neuron.Create(-1,"right")
						
		Local foodlft:neuron = neuron.Create(-1,"foodlft")	'left and right food detectors
		Local foodrgt:neuron = neuron.Create(-1,"foodrgt")
		
		Local enemyrgt:neuron = neuron.Create(-1,"enemyrgt")	'left and right enemy sensors
		Local enemylft:neuron = neuron.Create(-1,"enemylft")
		
		n.addneuron(hrt)
		n.addneuron(hngr)
		n.addneuron(n1)
		n.addneuron(n2)
		n.addneuron(n3)
		n.addneuron(lft)
		n.addneuron(rgt)
		n.addneuron(foodlft)
		n.addneuron(foodrgt)
		n.addneuron(nc1)
		n.addneuron(enemyrgt)
		n.addneuron(enemylft)
		
		
		Local c1:connection = connection.Create(hrt,n1,Rnd(-1,1.0))
		Local c2:connection = connection.Create(hrt,n2,Rnd(-1,1.0))
		Local c3:connection = connection.Create(hrt,n3,Rnd(-1,1.0))

		Local c4:connection = connection.Create(hngr,n1,Rnd(-1,1.0))
		Local c5:connection = connection.Create(hngr,n2,Rnd(-1,1.0))
		Local c6:connection = connection.Create(hngr,n3,Rnd(-1,1.0))


		Local c7:connection = connection.Create(n1,n2,Rnd(-1,1.0))
		Local c8:connection = connection.Create(n2,n1,Rnd(-1,1.0))

		Local c9:connection = connection.Create(n2,n3,Rnd(-1,1.0))
		Local c10:connection = connection.Create(n3,n2,Rnd(-1,1.0))

		Local c11:connection = connection.Create(n1,n3,Rnd(-1,1.0))
		Local c12:connection = connection.Create(n3,n1,Rnd(-1,1.0))
		
		Local c13:connection = connection.Create(foodlft,n1,Rnd(-1,1.0))
		Local c14:connection = connection.Create(foodlft,n2,Rnd(-1,1.0))

		Local c15:connection = connection.Create(foodrgt,n3,Rnd(-1,1.0))
		Local c16:connection = connection.Create(foodrgt,n1,Rnd(-1,1.0))
		
		Local c17:connection = connection.Create(n1,lft,Rnd(-1,1.0))
		Local c18:connection = connection.Create(n2,lft,Rnd(-1,1.0))

		Local c19:connection = connection.Create(n3,rgt,Rnd(-1,1.0))
		Local c20:connection = connection.Create(n1,rgt,Rnd(-1,1.0))
		
		Local c21:connection = connection.Create(n1,nc1,Rnd(-1,1.0))
		Local c22:connection = connection.Create(n2,nc1,Rnd(-1,1.0))
		Local c23:connection = connection.Create(n3,nc1,Rnd(-1,1.0))
		
		Local c24:connection = connection.Create(enemyrgt,n1,Rnd(-1,1.0))
		Local c25:connection = connection.Create(enemyrgt,n3,Rnd(-1,1.0))
		
		Local c26:connection = connection.Create(enemylft,n1,Rnd(-1,1.0))
		Local c27:connection = connection.Create(enemylft,n2,Rnd(-1,1.0))
		
		n.addconnection(c1)
		n.addconnection(c2)
		n.addconnection(c3)
		n.addconnection(c4)
		n.addconnection(c5)
		n.addconnection(c6)
		n.addconnection(c7)
		n.addconnection(c8)
		n.addconnection(c9)
		n.addconnection(c10)
		n.addconnection(c11)
		n.addconnection(c12)
		n.addconnection(c13)
		n.addconnection(c14)
		n.addconnection(c15)
		n.addconnection(c16)
		n.addconnection(c17)
		n.addconnection(c18)
		n.addconnection(c19)
		n.addconnection(c20)
		n.addconnection(c21)
		n.addconnection(c22)
		n.addconnection(c23)
		n.addconnection(c24)
		n.addconnection(c25)
		n.addconnection(c26)
		n.addconnection(c27)

		Return n:neuralnet
	End Function
	
	Method makemistake()
		Local mvar:Int
		For Local n:neuron = EachIn brain.neuronlist
			mvar = Rnd(50)
			If mvar = 10 Then
				n.setthreshold(Rnd(-1,1.0))
				Print "changed threshold"
			EndIf
		Next
		For Local c:connection = EachIn brain.connectionlist
			mvar = Rnd(50)
			If mvar = 10 Then
				c.setweight(Rnd(-1,1.0))
				Print "changed weight"
			EndIf
		Next
		mvar = Rnd(10)
		If mvar = 2 Then
			Local nn:neuron = neuron.Create(Rnd(-1,1),"x")
			Local n1:neuron
			mvar = Rnd(1,brain.neuronlist.count())
			Local cnt:Int = 0
			For Local n:neuron = EachIn brain.neuronlist
				cnt :+ 1
				If cnt = mvar Then n1:neuron = n:neuron
			Next
			Local cc:connection = connection.Create(n1,nn,Rnd(-1,1.0))
			brain.addconnection(cc)
			Local n2:neuron
			mvar = Rnd(1,brain.neuronlist.count())
			cnt:Int = 0
			For Local n:neuron = EachIn brain.neuronlist
				cnt :+ 1
				If cnt = mvar Then n2:neuron = n:neuron
			Next
			cc:connection = connection.Create(nn,n2,Rnd(-1,1.0))
			brain.addconnection(cc)
			brain.addneuron(nn)
			Print "new neuron!"
		EndIf
		mvar = Rnd(10)
		If mvar = 3 Then
			Print "new connection"
			Local n1:neuron
			Local n2:neuron
			mvar = Rnd(1,brain.neuronlist.count())
			Local cnt:Int = 0
			For Local n:neuron = EachIn brain.neuronlist
				cnt :+ 1
				If cnt = mvar Then n1:neuron = n:neuron
			Next
			mvar = Rnd(1,brain.neuronlist.count())
			cnt:Int = 0
			For Local n:neuron = EachIn brain.neuronlist
				cnt :+ 1
				If cnt = mvar Then n2:neuron = n:neuron
			Next
			If n1:neuron &gt;&lt; n2:neuron Then
				Local cc:connection = connection.Create(n1,n2,Rnd(-1,1.0))
				brain.addconnection(cc)
			EndIf
		EndIf
	End Method
	
	Method remove()
		brain.remove()
		fishlist.remove(Self)
	End Method
	
End Type


Type neuralnet
	Global neuralnetlist:TList = New TList
	
	Field neuronlist:TList
	Field connectionlist:TList
	
	Function printnnet(n:neuralnet)
		For Local nn:neuron = EachIn n.neuronlist
			Print "neuron: "+nn.getname()+" Energy: "+nn.getenergy()+" Threshold: "+nn.getthreshold()
		Next
		For Local c:connection = EachIn n.connectionlist
			Print "connection from "+c.getsource().getname()+" to "+c.getdestination().getname()+"  Weight: "+c.getweight()
		Next
	End Function
	
	Function Create:neuralnet()
		Local n:neuralnet = New neuralnet
		n.neuronlist:TList = New TList
		n.connectionlist:TList = New TList
		neuralnetlist.addlast(n:neuralnet)
		Return n:neuralnet
	End Function
	
	Method addneuron(n:neuron)
		neuronlist.addlast(n:neuron)
	End Method
	
	Method addconnection(c:connection)
		connectionlist.addlast(c:connection)
	End Method
	
	Method removeneuron(n:neuron)
		neuronlist.remove(n:neuron)
	End Method
	
	Method removeconnection(c:connection)
		connectionlist.remove(c:connection)
	End Method
	
	Method remove()
		neuralnetlist.remove(Self)
		For Local n:neuron = EachIn neuronlist
			n.remove()
		Next		
		For Local c:connection = EachIn connectionlist
			c.remove()
		Next		
	End Method
	
	Method findneuron:neuron(name$)
		For Local n:neuron = EachIn neuronlist
			If n.name$ = name$
				Return n:neuron
			EndIf
		Next
	End Method
	
	Function update()
		connection.clearenergy()
		connection.doweight()
		neuron.clearenergy()
		connection.dotransfer()
	End Function
	
	Function copy:neuralnet(n:neuralnet)
		Local nn:neuralnet = neuralnet.Create()
		For Local ne:neuron = EachIn n.neuronlist
			Local ne2:neuron = neuron.copy(ne:neuron)
			nn.neuronlist.addlast(ne2:neuron)
		Next
		For Local c:connection = EachIn n.connectionlist
			Local srcn:neuron
			Local destn:neuron
			Local cntgoal:Int = n.getlistposition(c.getsource())
			Local cnt:Int = 0
			For srcn:neuron = EachIn nn.neuronlist
				cnt:+1
				If cnt = cntgoal Then
					Exit
				EndIf
			Next
			cntgoal:Int = n.getlistposition(c.getdestination())
			cnt:Int = 0
			For destn:neuron = EachIn nn.neuronlist
				cnt:+1
				If cnt = cntgoal Then
					Exit
				EndIf
			Next

			Local cc:connection = connection.Create(srcn:neuron,destn:neuron,c.weight#)
			nn.connectionlist.addlast(cc:connection)
		Next
		Return nn:neuralnet
	End Function
	
	Method getlistposition:Int(nn:neuron)
		Local cnt:Int = 0
		For Local n:neuron = EachIn neuronlist
			cnt:+1
			If n:neuron = nn:neuron Then
				Return cnt
			EndIf
		Next
	End Method
End Type


Type neuron
	Global neuronlist:TList = New TList
	
	Field threshold#
	Field energy#
	Field name$
	
	Function Create:neuron(threshold#,name$ = "")
		Local n:neuron = New neuron
		n.threshold = threshold
		n.energy = 0
		n.name$ = name$
		neuronlist.addlast(n:neuron)
		Return n:neuron		
	End Function
	
	Method getthreshold:Float()
		Return threshold
	End Method
	
	Method setthreshold(t#)
		threshold = t
	End Method
	
	Method getenergy:Float()
		Return energy#
	End Method
	
	Method getenergyt:Float()
		If energy =&gt; threshold Then Return energy
		Return 0
	End Method
	
	Method setenergy(e#)
		energy = e
	End Method
	
	Method remove()
		neuronlist.remove(Self)
	End Method
	
	Method getname:String()
		Return name$
	End Method
	
	Function clearenergy()							'''''''''''Corrected this function'''''''''''''''''''''
		For Local n:neuron = EachIn neuronlist
			If n.getenergy() =&gt; n.getthreshold() Then
				n.setenergy(0)
			Else
				n.setenergy(n.getenergy()*.98)
			EndIf
			
		Next
	End Function
	
	
	Function copy:neuron(n:neuron)
		Local nn:neuron = neuron.Create(n.getthreshold(),n.getname())
		Return nn:neuron
	End Function
	
End Type

Type connection
	Global connectionlist:TList = New TList
	
	Field src:neuron
	Field dest:neuron
	Field weight#
	Field energy#
	
	Function Create:connection(src:neuron,dest:neuron,weight#)
		Local c:connection = New connection
		c.src:neuron = src:neuron
		c.dest:neuron = dest:neuron
		c.weight = weight
		c.energy = 0
		connectionlist.addlast(c:connection)
		Return(c:connection)
	End Function
	
	Method getweight:Float()
		Return weight#
	End Method
	
	Method setweight(w#)
		weight = w
	End Method
	
	Method getenergy:Float()
		Return energy#
	End Method
	
	Method setenergy(e#)
		energy = e
	End Method
	
	Method getsource:neuron()
		Return src:neuron
	End Method
	
	Method setsource(s:neuron)
		src:neuron = s:neuron
	End Method
	
	Method getdestination:neuron()
		Return dest:neuron
	End Method
	
	Method setdestination(d:neuron)
		dest:neuron = d:neuron
	End Method
	
	Method remove()
		connectionlist.remove(Self)
	End Method
	
	Function clearenergy()
		For Local c:connection = EachIn connectionlist
			c.setenergy(0)
		Next
	End Function
	
	Function doweight()
		For Local c:connection = EachIn connectionlist
			c.setenergy(c.getsource().getenergyt()*c.getweight())
		Next
	End Function
	Function dotransfer()
		For Local c:connection = EachIn connectionlist
			c.getdestination().setenergy(c.getdestination().getenergy() + c.getenergy())
		Next
	End Function
	
End Type</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> ok here is an xor example:<br>edit: moved above ^^ <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >slenkar</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> thanks for the code so far<br><br>have you got a more detailed example? <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> well for the past day or two ive been working on making a "fish" example with the fish that have brains that "evolve" through survivall of the fittest etc... as soon as its done ill post it here and in the blitz showcase. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> can u please convert the 'survival of the fittest' to blitzbasic? that would be awesome! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> well idk, Ill try after im done with it... <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> ok cool. let us know when its done if u dont mind ^^ thanks! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GIB3D</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> I'm gonna try changing that BlitzMax code to Blitz3D. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> well before you do, ill post a correction I discovered last night, gimme a few minutes to change it.<br><br>edit fixed <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GIB3D</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> B3D Convert<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;set up the structure of the neural network
Local in1.Neuron = Create_Neuron(0,"in1")
Local in2.Neuron = Create_Neuron(0,"in2")

Local h1.Neuron = Create_Neuron(1.5,"mid1")
Local h2.Neuron = Create_Neuron(.5,"mid2")

Local out.Neuron = Create_Neuron(0,"out")

Create_Connection(in1,h1,1)
Create_Connection(in1,h2,1)
Create_Connection(in2,h1,1)
Create_Connection(in2,h2,1)

Create_Connection(h1,out,-1)
Create_Connection(h2,out,1)
;k we are finished setting up the structure

;set the energy values To 1 Or 0 For the Xor inputs
Neuron_SetEnergy(in1,1)
Neuron_SetEnergy(in2,1)

;update the neural net 2 times For 3 layers
For i = 0 To 1
	NeuralNet_Update()
Next

;Print the energy of the output neuron (our answer)
Print Neuron_GetEnergy(out)


Type NeuralNet
End Type

Function Create_NeuralNet.NeuralNet()
	Local a.NeuralNet = New NeuralNet
	Return a
End Function

Function NeuralNet_AddNeuron(a.NeuralNet,b.Neuron)
	b\T_NeuralNet = a
End Function

Function NeuralNet_AddConnection(a.NeuralNet,b.Connection)
	b\T_NeuralNet = a
End Function

Function NeuralNet_RemoveNeuron(a.Neuron)
	Delete a
End Function

Function NeuralNet_RemoveConnection(a.Connection)
	Delete a
End Function

Function NeuralNet_FindNeuron.Neuron(name$)
	Local a.Neuron
	
	For a.Neuron = Each Neuron
		If a\name = name$
			Return a
		EndIf
	Next
End Function

Function NeuralNet_Update()
	Neuron_DoThreshold()
	Connection_ClearEnergy()
	Connection_DoWeight()
	Neuron_ClearEnergy()
	Connection_DoTransfer()
	Neuron_DoThreshold()
End Function

Function NeuralNet_Copy.NeuralNet(n.NeuralNet)
	Local c.Connection
	Local ne.Neuron,ne2.Neuron
	Local nn.NeuralNet = Create_NeuralNet()
	
	For ne = Each Neuron
		ne = Neuron_Copy(ne)
		NeuralNet_AddNeuron(nn,ne2)
	Next
	
	For c = Each Connection
		Local srcn.Neuron
		Local destn.Neuron
		Local cntgoal = NeuralNet_GetListPosition(Connection_GetSource(c))
		Local cnt = 0
		
		For srcn.Neuron = Each Neuron
			If srcn\T_NeuralNet = nn
				cnt=cnt+1
				If cnt = cntgoal Then
					Exit
				EndIf
			EndIf
		Next
		
		cntgoal = NeuralNet_GetListPosition(Connection_GetDestination(c))
		cnt = 0
		For destn.Neuron = Each Neuron
			If destn\T_NeuralNet = nn
				cnt=cnt+1
				
				If cnt = cntgoal Then
					Exit
				EndIf
			EndIf
		Next
		
		Local cc.Connection = Create_Connection(srcn.Neuron,destn.Neuron,c\weight)
		NeuralNet_AddConnection(nn,cc)
	Next
	
	Return nn.NeuralNet
End Function

Function NeuralNet_GetListPosition(b.Neuron)
	Local a.Neuron
	Local Count = 0
	
	For a = Each Neuron
		If a\T_NeuralNet = b\T_NeuralNet
			Count=Count+1
			If a = b Then
				Return Count
			EndIf
		EndIf
	Next
End Function

Type Neuron
	Field T_NeuralNet.NeuralNet
	Field threshold#
	Field energy#
	Field name$
End Type

Function Create_Neuron.Neuron(threshold#,name$ = "")
	Local a.Neuron = New Neuron
		
		a\threshold = threshold
		a\energy = 0
		a\name = name
		
	Return a
End Function

Function Neuron_GetThreshold#(a.Neuron)
	Return a\threshold
End Function

Function Neuron_SetThreshold(a.Neuron,t#)
	a\threshold = t
End Function

Function Neuron_GetEnergy#(a.Neuron)
	Return a\energy
End Function

Function Neuron_SetEnergy(a.Neuron,e#)
	a\energy = e
End Function

Function Neuron_Remove(a.Neuron)
	Delete a
End Function

Function Neuron_GetName$(a.Neuron)
	Return a\name
End Function

Function Neuron_ClearEnergy()
	Local a.Neuron
	
	For a.Neuron = Each Neuron
		Neuron_SetEnergy(a,0)
		
		If Neuron_GetEnergy(a) =&gt; Neuron_GetThreshold(a) Then
			Neuron_SetEnergy(a,0)
		Else
			Neuron_SetEnergy(a,Neuron_GetEnergy(a)*.98)
		EndIf
	Next
End Function

Function Neuron_DoThreshold()
	Local a.Neuron
	
	For a.Neuron = Each Neuron
		If Neuron_GetEnergy(a) &lt; Neuron_GetThreshold(a) Then
			Neuron_SetEnergy(a,0)
		EndIf
	Next
End Function

Function Neuron_Copy.Neuron(a.Neuron)
	Local b.Neuron = Create_Neuron(Neuron_GetThreshold(a),Neuron_GetName(a))
	Return b.Neuron
End Function


Type Connection
	Field T_NeuralNet.NeuralNet
	Field src.Neuron
	Field dest.Neuron
	Field weight#
	Field energy#
End Type

Function Create_Connection.Connection(src.Neuron,dest.Neuron,weight#)
	Local c.Connection = New Connection
		
		c\src.Neuron = src.Neuron
		c\dest.Neuron = dest.Neuron
		c\weight = weight
		c\energy = 0
		
	Return c
End Function

Function Connection_GetWeight#(a.Connection)
	Return a\weight
End Function

Function Connection_SetWeight(a.Connection,w#)
	a\weight = w
End Function

Function Connection_GetEnergy#(a.Connection)
	Return a\energy#
End Function

Function Connection_SetEnergy(a.Connection,e#)
	a\energy = e
End Function

Function Connection_GetSource.Neuron(a.Connection)
	Return a\src
End Function

Function Connection_SetSource(a.Connection,b.Neuron)
	a\src = b
End Function

Function Connection_GetDestination.Neuron(a.Connection)
	Return a\dest
End Function

Function Connection_SetDestination(a.Connection,b.Neuron)
	a\dest = b
End Function

Function Connection_Remove(a.Connection)
	Delete a
End Function

Function Connection_ClearEnergy()
	Local a.Connection
	For a = Each Connection
		Connection_SetEnergy(a,0)
	Next
End Function

Function Connection_DoWeight()
	Local a.Connection,b.Neuron
	
	For a = Each Connection
		b = Connection_GetSource(a)
		Connection_SetEnergy(a,Neuron_GetEnergy(b) * Connection_GetWeight(a))
	Next
End Function

Function Connection_DoTransfer()
	Local a.Connection,b.Neuron
	
	For a = Each Connection
		b = Connection_GetDestination(a)
		Neuron_SetEnergy(b,Neuron_GetEnergy(b) + Connection_GetEnergy(a))
	Next
End Function

</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> Nice job, Greenfire! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> thanks GIA,<br><br>I hope you are still bored enough/enthusiastic enough when I get done with the fish brain evolution example lol :) <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> unfortunately my computer crashed and I lost all recent work on this, so I must start back from what is on here. Sorry for the delay :( <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GIB3D</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> Awe :( <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >*</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> Interesting things Neural networks and can make one hell of a gaming opponent :) Great stuff. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> yeah heres an example I finally finished!<br><br><a href="http://www.blitzbasic.com/Community/posts.php?topic=87941" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=87941</a> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> nate can u post the new code if u havent already so someone can convert it to b3d? ^^ afterall, fishes do need brains XD <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >schilcote</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> Aww. And just as I was about to finish up my genetic algorithm code. Ah well. I'll just make a better one! <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Aww. And just as I was about to finish up my genetic algorithm code. Ah well. I'll just make a better one! <br> <br></div><br><br>go ahead and finish it then!<br><br><div class="quote"> <br>nate can u post the new code if u havent already so someone can convert it to b3d? ^^ afterall, fishes do need brains XD <br> <br></div><br><br>done. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> how can u use this to give fish brains?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">;set up the structure of the neural network
Local in1.Neuron = Create_Neuron(0,"in1")
Local in2.Neuron = Create_Neuron(0,"in2")

Local h1.Neuron = Create_Neuron(1.5,"mid1")
Local h2.Neuron = Create_Neuron(.5,"mid2")

Local out.Neuron = Create_Neuron(0,"out")

Create_Connection(in1,h1,1)
Create_Connection(in1,h2,1)
Create_Connection(in2,h1,1)
Create_Connection(in2,h2,1)

Create_Connection(h1,out,-1)
Create_Connection(h2,out,1)
;k we are finished setting up the structure

;set the energy values To 1 Or 0 For the Xor inputs
Neuron_SetEnergy(in1,1)
Neuron_SetEnergy(in2,1)

;update the neural net 2 times For 3 layers
For i = 0 To 1
	NeuralNet_Update()
Next

;Print the energy of the output neuron (our answer)
Print Neuron_GetEnergy(out)


Type NeuralNet
End Type

Function Create_NeuralNet.NeuralNet()
	Local a.NeuralNet = New NeuralNet
	Return a
End Function

Function NeuralNet_AddNeuron(a.NeuralNet,b.Neuron)
	b\T_NeuralNet = a
End Function

Function NeuralNet_AddConnection(a.NeuralNet,b.Connection)
	b\T_NeuralNet = a
End Function

Function NeuralNet_RemoveNeuron(a.Neuron)
	Delete a
End Function

Function NeuralNet_RemoveConnection(a.Connection)
	Delete a
End Function

Function NeuralNet_FindNeuron.Neuron(name$)
	Local a.Neuron
	
	For a.Neuron = Each Neuron
		If a\name = name$
			Return a
		EndIf
	Next
End Function

Function NeuralNet_Update()
	Neuron_DoThreshold()
	Connection_ClearEnergy()
	Connection_DoWeight()
	Neuron_ClearEnergy()
	Connection_DoTransfer()
	Neuron_DoThreshold()
End Function

Function NeuralNet_Copy.NeuralNet(n.NeuralNet)
	Local c.Connection
	Local ne.Neuron,ne2.Neuron
	Local nn.NeuralNet = Create_NeuralNet()
	
	For ne = Each Neuron
		ne = Neuron_Copy(ne)
		NeuralNet_AddNeuron(nn,ne2)
	Next
	
	For c = Each Connection
		Local srcn.Neuron
		Local destn.Neuron
		Local cntgoal = NeuralNet_GetListPosition(Connection_GetSource(c))
		Local cnt = 0
		
		For srcn.Neuron = Each Neuron
			If srcn\T_NeuralNet = nn
				cnt=cnt+1
				If cnt = cntgoal Then
					Exit
				EndIf
			EndIf
		Next
		
		cntgoal = NeuralNet_GetListPosition(Connection_GetDestination(c))
		cnt = 0
		For destn.Neuron = Each Neuron
			If destn\T_NeuralNet = nn
				cnt=cnt+1
				
				If cnt = cntgoal Then
					Exit
				EndIf
			EndIf
		Next
		
		Local cc.Connection = Create_Connection(srcn.Neuron,destn.Neuron,c\weight)
		NeuralNet_AddConnection(nn,cc)
	Next
	
	Return nn.NeuralNet
End Function

Function NeuralNet_GetListPosition(b.Neuron)
	Local a.Neuron
	Local Count = 0
	
	For a = Each Neuron
		If a\T_NeuralNet = b\T_NeuralNet
			Count=Count+1
			If a = b Then
				Return Count
			EndIf
		EndIf
	Next
End Function

Type Neuron
	Field T_NeuralNet.NeuralNet
	Field threshold#
	Field energy#
	Field name$
End Type

Function Create_Neuron.Neuron(threshold#,name$ = "")
	Local a.Neuron = New Neuron
		
		a\threshold = threshold
		a\energy = 0
		a\name = name
		
	Return a
End Function

Function Neuron_GetThreshold#(a.Neuron)
	Return a\threshold
End Function

Function Neuron_SetThreshold(a.Neuron,t#)
	a\threshold = t
End Function

Function Neuron_GetEnergy#(a.Neuron)
	Return a\energy
End Function

Function Neuron_SetEnergy(a.Neuron,e#)
	a\energy = e
End Function

Function Neuron_Remove(a.Neuron)
	Delete a
End Function

Function Neuron_GetName$(a.Neuron)
	Return a\name
End Function

Function Neuron_ClearEnergy()
	Local a.Neuron
	
	For a.Neuron = Each Neuron
		Neuron_SetEnergy(a,0)
		
		If Neuron_GetEnergy(a) =&gt; Neuron_GetThreshold(a) Then
			Neuron_SetEnergy(a,0)
		Else
			Neuron_SetEnergy(a,Neuron_GetEnergy(a)*.98)
		EndIf
	Next
End Function

Function Neuron_DoThreshold()
	Local a.Neuron
	
	For a.Neuron = Each Neuron
		If Neuron_GetEnergy(a) &lt; Neuron_GetThreshold(a) Then
			Neuron_SetEnergy(a,0)
		EndIf
	Next
End Function

Function Neuron_Copy.Neuron(a.Neuron)
	Local b.Neuron = Create_Neuron(Neuron_GetThreshold(a),Neuron_GetName(a))
	Return b.Neuron
End Function


Type Connection
	Field T_NeuralNet.NeuralNet
	Field src.Neuron
	Field dest.Neuron
	Field weight#
	Field energy#
End Type

Function Create_Connection.Connection(src.Neuron,dest.Neuron,weight#)
	Local c.Connection = New Connection
		
		c\src.Neuron = src.Neuron
		c\dest.Neuron = dest.Neuron
		c\weight = weight
		c\energy = 0
		
	Return c
End Function

Function Connection_GetWeight#(a.Connection)
	Return a\weight
End Function

Function Connection_SetWeight(a.Connection,w#)
	a\weight = w
End Function

Function Connection_GetEnergy#(a.Connection)
	Return a\energy#
End Function

Function Connection_SetEnergy(a.Connection,e#)
	a\energy = e
End Function

Function Connection_GetSource.Neuron(a.Connection)
	Return a\src
End Function

Function Connection_SetSource(a.Connection,b.Neuron)
	a\src = b
End Function

Function Connection_GetDestination.Neuron(a.Connection)
	Return a\dest
End Function

Function Connection_SetDestination(a.Connection,b.Neuron)
	a\dest = b
End Function

Function Connection_Remove(a.Connection)
	Delete a
End Function

Function Connection_ClearEnergy()
	Local a.Connection
	For a = Each Connection
		Connection_SetEnergy(a,0)
	Next
End Function

Function Connection_DoWeight()
	Local a.Connection,b.Neuron
	
	For a = Each Connection
		b = Connection_GetSource(a)
		Connection_SetEnergy(a,Neuron_GetEnergy(b) * Connection_GetWeight(a))
	Next
End Function

Function Connection_DoTransfer()
	Local a.Connection,b.Neuron
	
	For a = Each Connection
		b = Connection_GetDestination(a)
		Neuron_SetEnergy(b,Neuron_GetEnergy(b) + Connection_GetEnergy(a))
	Next
End Function</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> because thats no tthe code that gave them brains... this is<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Function MakeBrain:neuralnet()
		Local n:neuralnet = neuralnet.Create()
		Local hrt:neuron = neuron.Create(0,"heart")	'this neuron will "beat" at a steady rate
		Local hngr:neuron = neuron.Create(-1,"hunger")	'this neuron will slowly turn on if a fish has not eaten in a while, negative if a fish is over full
		
		Local n1:neuron = neuron.Create(Rnd(-1,1.0),"n1")	'neurons that do the logic
		Local n2:neuron = neuron.Create(Rnd(-1,1.0),"n2")
		Local n3:neuron = neuron.Create(Rnd(-1,1.0),"n3")
		
		Local nc1:neuron = neuron.Create(Rnd(-1,1.0),"c1")
				
		Local lft:neuron = neuron.Create(-1,"left")		'neurons that control movement
		Local rgt:neuron = neuron.Create(-1,"right")
						
		Local foodlft:neuron = neuron.Create(-1,"foodlft")	'left and right food detectors
		Local foodrgt:neuron = neuron.Create(-1,"foodrgt")
		
		Local enemyrgt:neuron = neuron.Create(-1,"enemyrgt")	'left and right enemy sensors
		Local enemylft:neuron = neuron.Create(-1,"enemylft")
		
		n.addneuron(hrt)
		n.addneuron(hngr)
		n.addneuron(n1)
		n.addneuron(n2)
		n.addneuron(n3)
		n.addneuron(lft)
		n.addneuron(rgt)
		n.addneuron(foodlft)
		n.addneuron(foodrgt)
		n.addneuron(nc1)
		n.addneuron(enemyrgt)
		n.addneuron(enemylft)
		
		
		Local c1:connection = connection.Create(hrt,n1,Rnd(-1,1.0))
		Local c2:connection = connection.Create(hrt,n2,Rnd(-1,1.0))
		Local c3:connection = connection.Create(hrt,n3,Rnd(-1,1.0))

		Local c4:connection = connection.Create(hngr,n1,Rnd(-1,1.0))
		Local c5:connection = connection.Create(hngr,n2,Rnd(-1,1.0))
		Local c6:connection = connection.Create(hngr,n3,Rnd(-1,1.0))


		Local c7:connection = connection.Create(n1,n2,Rnd(-1,1.0))
		Local c8:connection = connection.Create(n2,n1,Rnd(-1,1.0))

		Local c9:connection = connection.Create(n2,n3,Rnd(-1,1.0))
		Local c10:connection = connection.Create(n3,n2,Rnd(-1,1.0))

		Local c11:connection = connection.Create(n1,n3,Rnd(-1,1.0))
		Local c12:connection = connection.Create(n3,n1,Rnd(-1,1.0))
		
		Local c13:connection = connection.Create(foodlft,n1,Rnd(-1,1.0))
		Local c14:connection = connection.Create(foodlft,n2,Rnd(-1,1.0))

		Local c15:connection = connection.Create(foodrgt,n3,Rnd(-1,1.0))
		Local c16:connection = connection.Create(foodrgt,n1,Rnd(-1,1.0))
		
		Local c17:connection = connection.Create(n1,lft,Rnd(-1,1.0))
		Local c18:connection = connection.Create(n2,lft,Rnd(-1,1.0))

		Local c19:connection = connection.Create(n3,rgt,Rnd(-1,1.0))
		Local c20:connection = connection.Create(n1,rgt,Rnd(-1,1.0))
		
		Local c21:connection = connection.Create(n1,nc1,Rnd(-1,1.0))
		Local c22:connection = connection.Create(n2,nc1,Rnd(-1,1.0))
		Local c23:connection = connection.Create(n3,nc1,Rnd(-1,1.0))
		
		Local c24:connection = connection.Create(enemyrgt,n1,Rnd(-1,1.0))
		Local c25:connection = connection.Create(enemyrgt,n3,Rnd(-1,1.0))
		
		Local c26:connection = connection.Create(enemylft,n1,Rnd(-1,1.0))
		Local c27:connection = connection.Create(enemylft,n2,Rnd(-1,1.0))
		
		n.addconnection(c1)
		n.addconnection(c2)
		n.addconnection(c3)
		n.addconnection(c4)
		n.addconnection(c5)
		n.addconnection(c6)
		n.addconnection(c7)
		n.addconnection(c8)
		n.addconnection(c9)
		n.addconnection(c10)
		n.addconnection(c11)
		n.addconnection(c12)
		n.addconnection(c13)
		n.addconnection(c14)
		n.addconnection(c15)
		n.addconnection(c16)
		n.addconnection(c17)
		n.addconnection(c18)
		n.addconnection(c19)
		n.addconnection(c20)
		n.addconnection(c21)
		n.addconnection(c22)
		n.addconnection(c23)
		n.addconnection(c24)
		n.addconnection(c25)
		n.addconnection(c26)
		n.addconnection(c27)

		Return n:neuralnet
	End Function
</textarea> <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> how can u translate that function to b3d? <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> sorry, I havent used b3d in soo long... there were so many changes to the program, I think you will just have to re translate the entire thing to b3d unfortunately I dont know how to  do that :( <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>2009</font></td></tr></table></td></tr><tr ><td class="posttext"> could someone please convert the fish brains (LOL) to b3d plz &amp; thank u? <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Serpent</td><td align="right"><font class=tiny>2010</font></td></tr></table></td></tr><tr ><td class="posttext"> Neural networks are really interesting ways to create AI.  It's amazing that a simple neuron network with 4 inputs could become effective at searching for food. <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MusicianKool</td><td align="right"><font class=tiny>2011</font></td></tr></table></td></tr><tr ><td class="posttext"> I know it's been a year sense the request but here is the direct conversion to b3d, there is probably some bugs but it works right out of the box.  it needs the llist.bb to work found (here-&gt;) <a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=2873" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=2873</a> (&lt;-here)<br><br>the code:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SeedRnd(MilliSecs())
Graphics 800,600,0,2
Include "llist.bb"

Global Fish_livecount

Local i
For i = 0 To 20
	Food_Create(Rnd(800),Rnd(600))
Next

Local b
For b = 0 To 20
	Local f.Fish
	f = Fish_Create(Rnd(800),Rnd(600),0,Fish_MakeBrain())
Next

Global generation = 0
Global simstep = 0

Global finalsimstep
Global finalflag

Global render = True
Global gopast10 = True

Global selected.Fish

Global key_space = 57
Global key_f = 33
Global key_p = 25

While Not KeyHit(1)
	If KeyHit(key_space) Then
		render = 1-render
	EndIf
	If KeyHit(key_f) Then
		gopast10 = 1-gopast10
	EndIf
	If render Then Cls
	simstep = simstep + 1
	
	Food_update()
	Fish_update()
	
	Local sf.Fish
	For sf = Each Fish
		If sf = selected And sf\alive Then
			Fish_drawnnet(sf)
		EndIf
	Next
	
	If MouseHit(1)
		selected = Null
		For sf = Each Fish
			Local dx# = MouseX()-sf\x
			Local dy# = MouseY()-sf\y
			Local dist# = Sqr(dx*dx + dy*dy)
			If dist &lt;= 20 Then
				selected = sf
			EndIf
		Next
	EndIf
	
	If KeyHit(key_p) And selected  &lt;&gt; Null Then
		NeuralNet_printnnet(selected\brain)
	EndIf
	NeuralNet_update()
	
	Color 255,255,255
	Text "Generation: "+generation,1,1
	Text "Simulation Step: "+simstep,1,20
	If gopast10 = True Then
		Text "Simulating full generation (press f to change)",1,40
	Else
		Text "Only simulating generations to 10 (press f to change)",1,40
	EndIf
	
	If Fish_livecount &lt;= 10 And finalflag = False Then
		finalsimstep = simstep-1
		finalflag = True
	EndIf
	If (finalflag And gopast10 = False) Or Fish_livecount = 0 Then
		generation = generation+ 1
		
		finalflag = False
		Print (generation-1)+ "  length: "+simstep
		
		For f.Fish = Each Fish
			If f\life &gt;= finalsimstep And f\life &gt; 0 Then
				f\alive = True
				f\hunger = 0
				Local ff.Fish = Fish_Create(Rnd(800),Rnd(600),0,NeuralNet_copy(f\brain))
				Fish_makemistake(ff)
			ElseIf f\life &gt; 0
				Fish_remove(f)
			EndIf
		Next
		simstep = 0
	EndIf
	
	If render Then Flip
Wend


Type food
	Field x#
	Field y#
	Field dist#
End Type	

Function Food_Create.food(x#,y#)
	Local f.food = New food
	f\x = x
	f\y = y
	Return f
End Function

Function Food_update()
	Local f.food
	For f = Each food
		If render Then Food_draw(f)
	Next
End Function

Function Food_draw(f.food)
	Color(0,255,0)
	Oval(f\x-5,f\y-5,10,10)
End Function

Function Food_getfoodsmell#(x#,y#);		'returns the amount of smell of food there is at a certain point
	Local smell# = 0
	Local f.food
	For f = Each food
		Local dx = x-f\x
		Local dy = y-f\y
		Local dist = Sqr(dx*dx + dy*dy)
		If dist &lt; 50 And dist &gt; 15 Then
			smell = smell + (50-dist)/50
		ElseIf dist &lt;= 15 Then
			smell = smell + 1
		EndIf
	Next
	Return smell#
End Function

Function Food_getclosefood.food(x#,y#);	'returns the food closest To given point x,y
	Local closest# = 10000
	Local closestfood.food
	Local dist#
	Local f.food
	For f = Each food
		Local dx = x-f\x
		Local dy = y-f\y
		dist = Sqr(dx*dx + dy*dy)
		If dist &lt; closest Then
			closestfood = f
			closest = dist
		EndIf
	Next
	If closest &lt; 25 Then Return closestfood
End Function



Type Fish
	Field x#
	Field y#
	Field angle#
	Field anglespd#
	Field hunger#
	Field heartcount
	Field hearton
	Field brain.neuralnet
	Field alive
	Field life
	Field gen
End Type

Function Fish_update()
	Fish_livecount = 0
	Local onedied = 0
	Local totcnt
	Local f.Fish
	For f = Each Fish
		totcnt = totcnt + 1
		If f\alive Then
			Fish_livecount = Fish_livecount+ 1
			f\hunger = f\hunger + .001
			f\heartcount = f\heartcount + 1
			If f\heartcount &gt; 20 Then
				f\hearton = 1-f\hearton
				f\heartcount = 0
			EndIf
			Local hrt.neuron = NeuralNet_findneuron(f\brain,"heart")
			hrt\energy=f\hearton
			Local hng.neuron = NeuralNet_findneuron(f\brain,"hunger")
			hng\energy=f\hunger
			Local lft.neuron = NeuralNet_findneuron(f\brain,"left")
			Local rgt.neuron = NeuralNet_findneuron(f\brain,"right")
			f\anglespd# = f\anglespd - lft\energy*5 + rgt\energy*5	;calculate steering
			f\anglespd = f\anglespd * .9
			If f\anglespd &gt; 4 Then f\anglespd = 4
			If f\anglespd &lt; -4 Then f\anglespd = -4
			f\angle = f\angle + f\anglespd
			Local spd# = (lft\energy*7+rgt\energy*7)
			If spd &gt; 5 Then spd = 5
			If spd &lt; -5 Then spd = -5
			f\x = f\x + Cos(f\angle)*spd
			f\y = f\y + Sin(f\angle)*spd
			If render Then Fish_draw(f)
			Fish_checkfood(f)
			Fish_checkenemy(f)
			f\life = simstep
			If Abs(f\hunger) &gt; 1 And onedied = 0 Then
				f\alive = False
				f\life = simstep
				onedied = 1
			EndIf
			If f\x &lt; -20 Then f\x = 820
			If f\x &gt; 820 Then f\x = -20
			If f\y &lt; -20 Then f\y = 620
			If f\y &gt; 620 Then f\y = -20
			;NeuralNet_printnnet(f\brain)		
		EndIf
	Next
End Function

Function Fish_draw(f.Fish)
	Color(100,255,100)
	Line(f\x,f\y,f\x+Cos(f\angle+30)*30,f\y+Sin(f\angle+30)*30)
	Line(f\x,f\y,f\x+Cos(f\angle-30)*30,f\y+Sin(f\angle-30)*30)
	Color(0,50,255)
	Oval(f\x-20,f\y-20,40,40)
	Color(255,255,255)
	Line(f\x,f\y,f\x+Cos(f\angle)*30,f\y+Sin(f\angle)*30)
	Color ((f\hunger+1)/2)*255,((-f\hunger+1)/2)*255,0
	Rect f\x-20,f\y-35,((-f\hunger+1)/2)*40,5
	Color 255,255,255
	Text f\x-10,f\y-10,f\gen
End Function

Function Fish_checkfood(f.Fish)
	Local x1# = f\x+Cos(f\angle-30)*30
	Local y1# = f\y+Sin(f\angle-30)*30
	Local smell1# = Food_getfoodsmell(x1,y1)
	Local foodleft.neuron = NeuralNet_findneuron(f\brain,"foodlft")
	foodleft\energy=smell1
	Local x2# = f\x+Cos(f\angle+30)*30
	Local y2# = f\y+Sin(f\angle+30)*30
	Local smell2# = Food_getfoodsmell(x2,y2)
	Local foodright.neuron = NeuralNet_findneuron(f\brain,"foodrgt")
	foodright\energy=smell2
	Local fo.food = Food_getclosefood(f\x,f\y)
	If fo &gt;&lt; Null Then
		fo\x = Rnd(800)
		fo\y = Rnd(600)
		f\hunger =f\hunger- .7
	EndIf
End Function

Function Fish_checkenemy(self.Fish)
	Local smell1#
	Local smell2#
	Local F.Fish
	For F.Fish = Each Fish
		If F &gt;&lt; self Then
			Local dx# = (self\x+Cos(self\angle-30)*30)-F\x
			Local dy# = (self\y+Sin(self\angle-30)*30)-F\y
			Local dist# = Sqr(dx*dx + dy*dy)
			If dist &lt;= 50 Then
				smell1# = smell1# + (50-dist#)/50.0
			EndIf
			dx# = (self\x+Cos(self\angle+30)*30)-F\x
			dy# = (self\y+Sin(self\angle+30)*30)-F\y
			dist# = Sqr(dx*dx + dy*dy)
			If dist &lt;= 50 Then
				smell2# = smell2# + (50-dist#)/50.0
			EndIf
		EndIf
	Next
	Local n.neuron = NeuralNet_findneuron(self\brain,"enemylft")
	n\energy =smell1
	n = NeuralNet_findneuron(self\brain,"enemyrgt")
	n\energy=smell2
End Function

Function Fish_Create.Fish(x#,y#,angle#,brain.neuralnet)
	Local f.Fish = New Fish
	f\x = x
	f\y = y
	f\angle = angle
	f\brain = brain
	f\heartcount = Rnd(0,19)
	f\hearton = Rnd(0,1)
	f\alive = True
	f\gen = generation
	Return f
End Function

Function Fish_drawnnet(f.Fish)
	Color 255,255,255
	Rect f\x+20,f\y+20,150,150
	
	Local tn.neuron = NeuralNet_findneuron(f\brain,"heart")
	
	Color 0,0,(tn\energy*255)
	Oval f\x+87,f\y+92,6,6
	
	tn = NeuralNet_findneuron(f\brain,"hunger")
	Color ((-tn\energy)*255),0,(tn\energy*255)
	Oval f\x+97,f\y+92,6,6
	
	tn = NeuralNet_findneuron(f\brain,"n1")
	Color ((-tn\energy)*255),0,(tn\energy*255)
	Oval f\x+92,f\y+40,6,6
	
	tn = NeuralNet_findneuron(f\brain,"n2")
	Color ((-tn\energy)*255),0,(tn\energy*255)
	Oval f\x+52,f\y+115,6,6
	
	tn = NeuralNet_findneuron(f\brain,"n3")
	Color ((-tn\energy)*255),0,(tn\energy*255)
	Oval f\x+132,f\y+115,6,6
	
	tn = NeuralNet_findneuron(f\brain,"c1")
	Color ((-tn\energy)*255),0,(tn\energy*255)
	Oval f\x+92,f\y+140,6,6
	
	tn = NeuralNet_findneuron(f\brain,"left")
	Color ((-tn\energy)*255),0,(tn\energy*255)
	Oval f\x+30,f\y+70,6,6
	
	tn = NeuralNet_findneuron(f\brain,"right")
	Color ((-tn\energy)*255),0,(tn\energy*255)
	Oval f\x+157,f\y+70,6,6
	
	tn = NeuralNet_findneuron(f\brain,"foodlft")
	Color ((-tn\energy)*255),0,(tn\energy*255)
	Oval f\x+30,f\y+30,6,6
	
	tn = NeuralNet_findneuron(f\brain,"foodrgt")
	Color ((-tn\energy)*255),0,(tn\energy*255)
	Oval f\x+157,f\y+30,6,6
End Function

Function Fish_MakeBrain.neuralnet()
	Local n.neuralnet = Neuralnet_Create()
	Local hrt.neuron = Neuron_Create(0,"heart")	;this neuron will "beat" at a steady rate
	Local hngr.neuron = Neuron_Create(-1,"hunger")	;this neuron will slowly turn on If a fish has Not eaten in a While, negative If a fish is over full
	
	Local n1.neuron = Neuron_Create(Rnd(-1,1.0),"n1")	;neurons that do the logic
	Local n2.neuron = Neuron_Create(Rnd(-1,1.0),"n2")
	Local n3.neuron = Neuron_Create(Rnd(-1,1.0),"n3")
	
	Local nc1.neuron = Neuron_Create(Rnd(-1,1.0),"c1")
			
	Local lft.neuron = Neuron_Create(-1,"left")		;neurons that control movement
	Local rgt.neuron = Neuron_Create(-1,"right")
					
	Local foodlft.neuron = Neuron_Create(-1,"foodlft")	;Left And Right food detectors
	Local foodrgt.neuron = Neuron_Create(-1,"foodrgt")
	
	Local enemyrgt.neuron = Neuron_Create(-1,"enemyrgt")	;Left And Right enemy sensors
	Local enemylft.neuron = Neuron_Create(-1,"enemylft")
	
	NeuralNet_addneuron(n,hrt)
	NeuralNet_addneuron(n,hngr)
	NeuralNet_addneuron(n,n1)
	NeuralNet_addneuron(n,n2)
	NeuralNet_addneuron(n,n3)
	NeuralNet_addneuron(n,lft)
	NeuralNet_addneuron(n,rgt)
	NeuralNet_addneuron(n,foodlft)
	NeuralNet_addneuron(n,foodrgt)
	NeuralNet_addneuron(n,nc1)
	NeuralNet_addneuron(n,enemyrgt)
	NeuralNet_addneuron(n,enemylft)
	
	
	Local c1.connection = Connection_Create(hrt,n1,Rnd(-1,1.0))
	Local c2.connection = Connection_Create(hrt,n2,Rnd(-1,1.0))
	Local c3.connection = Connection_Create(hrt,n3,Rnd(-1,1.0))
	
	Local c4.connection = Connection_Create(hngr,n1,Rnd(-1,1.0))
	Local c5.connection = Connection_Create(hngr,n2,Rnd(-1,1.0))
	Local c6.connection = Connection_Create(hngr,n3,Rnd(-1,1.0))


	Local c7.connection = Connection_Create(n1,n2,Rnd(-1,1.0))
	Local c8.connection = Connection_Create(n2,n1,Rnd(-1,1.0))

	Local c9.connection = Connection_Create(n2,n3,Rnd(-1,1.0))
	Local c10.connection = Connection_Create(n3,n2,Rnd(-1,1.0))

	Local c11.connection = Connection_Create(n1,n3,Rnd(-1,1.0))
	Local c12.connection = Connection_Create(n3,n1,Rnd(-1,1.0))
	
	Local c13.connection = Connection_Create(foodlft,n1,Rnd(-1,1.0))
	Local c14.connection = Connection_Create(foodlft,n2,Rnd(-1,1.0))

	Local c15.connection = Connection_Create(foodrgt,n3,Rnd(-1,1.0))
	Local c16.connection = Connection_Create(foodrgt,n1,Rnd(-1,1.0))
	
	Local c17.connection = Connection_Create(n1,lft,Rnd(-1,1.0))
	Local c18.connection = Connection_Create(n2,lft,Rnd(-1,1.0))

	Local c19.connection = Connection_Create(n3,rgt,Rnd(-1,1.0))
	Local c20.connection = Connection_Create(n1,rgt,Rnd(-1,1.0))
	
	Local c21.connection = Connection_Create(n1,nc1,Rnd(-1,1.0))
	Local c22.connection = Connection_Create(n2,nc1,Rnd(-1,1.0))
	Local c23.connection = Connection_Create(n3,nc1,Rnd(-1,1.0))
	
	Local c24.connection = Connection_Create(enemyrgt,n1,Rnd(-1,1.0))
	Local c25.connection = Connection_Create(enemyrgt,n3,Rnd(-1,1.0))
	
	Local c26.connection = Connection_Create(enemylft,n1,Rnd(-1,1.0))
	Local c27.connection = Connection_Create(enemylft,n2,Rnd(-1,1.0))
	
	NeuralNet_addconnection(n,c1)
	NeuralNet_addconnection(n,c2)
	NeuralNet_addconnection(n,c3)
	NeuralNet_addconnection(n,c4)
	NeuralNet_addconnection(n,c5)
	NeuralNet_addconnection(n,c6)
	NeuralNet_addconnection(n,c7)
	NeuralNet_addconnection(n,c8)
	NeuralNet_addconnection(n,c9)
	NeuralNet_addconnection(n,c10)
	NeuralNet_addconnection(n,c11)
	NeuralNet_addconnection(n,c12)
	NeuralNet_addconnection(n,c13)
	NeuralNet_addconnection(n,c14)
	NeuralNet_addconnection(n,c15)
	NeuralNet_addconnection(n,c16)
	NeuralNet_addconnection(n,c17)
	NeuralNet_addconnection(n,c18)
	NeuralNet_addconnection(n,c19)
	NeuralNet_addconnection(n,c20)
	NeuralNet_addconnection(n,c21)
	NeuralNet_addconnection(n,c22)
	NeuralNet_addconnection(n,c23)
	NeuralNet_addconnection(n,c24)
	NeuralNet_addconnection(n,c25)
	NeuralNet_addconnection(n,c26)
	NeuralNet_addconnection(n,c27)

	Return n
End Function

Function Fish_makemistake(Self.Fish)
	Local mvar,n.neuron,i.Iterator = GetIterator(Self\brain\neuronlist)
	Local n1.neuron,n2.neuron,nn.neuron
	Local c.connection,cc.connection
	Local cnt
	While EachIn(i)
		n = Object.neuron(i\Value)
		mvar = Rnd(50)
		If mvar = 10 Then
			n\threshold=Rnd(-1,1.0)
			Print "changed threshold"
		EndIf
	Wend
	Delete i
	
	i = GetIterator(Self\brain\connectionlist)
	While EachIn(i)
		c = Object.connection(i\Value)
		mvar = Rnd(50)
		If mvar = 10 Then
			c\weight=(Rnd(-1,1.0))
			Print "changed weight"
		EndIf
	Wend
	Delete i
	
	mvar = Rnd(10)
	If mvar = 2 Then
		nn = Neuron_Create(Rnd(-1,1),"x")
		mvar = Rnd(1,ListLength(Self\brain\neuronlist))
		cnt = 0
		i = GetIterator(Self\brain\neuronlist)
		While EachIn(i)
			n = Object.neuron(i\Value)
			cnt =cnt+ 1
			If cnt = mvar Then n1 = n
		Wend
		Delete i
		cc = Connection_Create(n1,nn,Rnd(-1,1.0))
		NeuralNet_addconnection(Self\brain,cc)
		mvar = Rnd(1,ListLength(Self\brain\neuronlist))
		cnt = 0
		
		i = GetIterator(Self\brain\neuronlist)
		While EachIn(i)
			n = Object.neuron(i\Value)
			cnt = cnt + 1
			If cnt = mvar Then n2 = n
		Wend
		Delete i
		cc = Connection_Create(nn,n2,Rnd(-1,1.0))
		NeuralNet_addconnection(Self\brain,cc)
		NeuralNet_addneuron(Self\brain,nn)
		Print "new neuron!"
	EndIf
	mvar = Rnd(10)
	If mvar = 3 Then
		Print "new connection"
		mvar = Rnd(1,ListLength(Self\brain\neuronlist))
		cnt = 0
		i = GetIterator(Self\brain\neuronlist)
		While EachIn(i)
			n = Object.neuron(i\Value)
			cnt = cnt + 1
			If cnt = mvar Then n1 = n
		Wend
		Delete i
		mvar = Rnd(1,ListLength(Self\brain\neuronlist))
		cnt = 0
		i = GetIterator(Self\brain\neuronlist)
		While EachIn(i)
			n = Object.neuron(i\Value)
			cnt = cnt + 1
			If cnt = mvar Then n2 = n
		Wend
		If n1 &gt;&lt; n2 Then
			cc = Connection_Create(n1,n2,Rnd(-1,1.0))
			NeuralNet_addconnection(Self\brain,cc)
		EndIf
	EndIf
End Function

Function Fish_remove(Self.Fish)
	NeuralNet_remove(Self\brain)
	Delete Self
End Function
	


Type neuralnet
	Field neuronlist.LList
	Field connectionlist.LList
End Type

Function NeuralNet_printnnet(n.neuralnet)
	Local nn.neuron,i.Iterator = GetIterator(n\neuronlist),c.connection
	While  EachIn(i)
		nn = Object.neuron(i\Value)
		Print "neuron: "+nn\name+" Energy: "+nn\energy+" Threshold: "+nn\threshold
	Wend
	Delete i
	i = GetIterator(n\connectionlist)
	While EachIn(i)
		c = Object.connection(i\Value)
		Print "connection from "+c\src\name+" to "+c\dest\name+"  Weight: "+c\weight
	Wend
End Function

Function Neuralnet_Create.neuralnet()
	Local n.neuralnet = New neuralnet
	n\neuronlist = CreateList()
	n\connectionlist = CreateList()
	Return n
End Function

Function NeuralNet_addneuron(nn.neuralnet,n.neuron)
	ListAddLast(nn\neuronlist,Handle(n))
End Function

Function NeuralNet_addconnection(nn.neuralnet,c.connection)
	ListAddLast(nn\connectionlist,Handle(c))
End Function

Function NeuralNet_removeneuron(NN.neuralnet,n.neuron)
	ListRemove(NN\neuronlist,Handle(n))
	Delete n
End Function

Function NeuralNet_removeconnection(nn.neuralnet,c.connection)
	ListRemove(nn\connectionlist,Handle(c))
	Delete c
End Function

Function NeuralNet_remove(nn.neuralnet)
	Local n.neuron,c.connection,i.Iterator
	i = GetIterator(nn\neuronlist)
	While EachIn(i)
		n = Object.neuron(i\Value)
		IteratorRemove(i)
		Delete n
	Wend		
	Delete i
	i = GetIterator(nn\connectionlist)
	While EachIn (i)
		c= Object.connection(i\Value)
		IteratorRemove(i)
		Delete c
	Wend
	Delete nn
	Delete i
End Function

Function NeuralNet_findneuron.neuron(nn.neuralnet,name$)
	Local n.neuron,i.Iterator = GetIterator(nn\neuronlist)
	
	While EachIn(i)
		n = Object.neuron(i\Value)
		If n\name = name Then IteratorBreak(i):Delete i:Return n
	Wend
End Function

Function NeuralNet_update()
	Connection_clearenergy()
	Connection_doweight()
	Neuron_clearenergy()
	Connection_dotransfer()
End Function

Function NeuralNet_copy.neuralnet(n.neuralnet)
	Local nn.neuralnet = Neuralnet_Create(),i.Iterator = GetIterator(n\neuronlist)
	While EachIn(i)
		Local ne.neuron = Object.neuron(i\Value)
		Local ne2.neuron = Neuron_copy(ne)
		ListAddLast(nn\neuronlist,Handle(ne2))
	Wend
	Delete i
	Local c.connection
	i = GetIterator(n\connectionlist)
	While EachIn(i)
		c = Object.connection(i\Value)
		Local srcn.neuron
		Local destn.neuron
		Local cntgoal = Neuralnet_getlistposition(n,c\src)
		Local cnt = 0
		Local i2.Iterator = GetIterator(nn\neuronlist)
		While EachIn(i2)
			srcn = Object.neuron(i2\Value)
			cnt=cnt+1
			If cnt = cntgoal Then
				Exit
			EndIf
		Wend
		Delete i2
		cntgoal = Neuralnet_getlistposition(n,c\dest)
		cnt = 0
		i2 = GetIterator(nn\neuronlist)
		While EachIn(i2)
			destn = Object.neuron(i2\Value)
			cnt=cnt+1
			If cnt = cntgoal Then
				Exit
			EndIf
		Wend
		Delete i2

		Local cc.connection = Connection_Create(srcn,destn,c\weight#)
		ListAddLast(nn\connectionlist,Handle(cc))
	Wend
	Return nn
End Function

Function Neuralnet_getlistposition(nn.neuralnet,n.neuron)
	Local cnt = 0,i.Iterator = GetIterator(nn\neuronlist)
	While EachIn(i)
		Local tn.neuron = Object.neuron(i\Value)
		cnt=cnt+1
		If tn = n Then IteratorBreak(i):Delete i:Return cnt
	Wend
End Function



Type neuron
	Field threshold#
	Field energy#
	Field name$
End Type

Function Neuron_Create.neuron(threshold#,name$ = "")
	Local n.neuron = New neuron
	n\threshold = threshold
	n\energy = 0
	n\name$ = name$
	Return n		
End Function

Function Neuron_getthreshold#(n.neuron)
	Return n\threshold
End Function

Function Neuron_setthreshold(n.neuron,t#)
	n\threshold = t
End Function

Function Neuron_getenergy#(n.neuron)
	Return n\energy#
End Function

Function Neuron_getenergyt#(N.neuron)
	If N\energy =&gt; N\threshold Then Return N\energy
	Return 0
End Function

Function Neuron_setenergy(n.neuron,e#)
	n\energy = e
End Function

Function Neuron_remove(N.neuron)
	Delete N
End Function

Function Neuron_getname$(n.neuron)
	Return n\name$
End Function

Function Neuron_clearenergy()							;'''''''''''Corrected this Function'''''''''''''''''''''
	Local n.neuron
	For n = Each neuron
		If n\energy =&gt; n\threshold Then
			n\energy=0
		Else
			n\energy = n\energy*.98
		EndIf
	Next
End Function


Function Neuron_copy.neuron(n.neuron)
	Local nn.neuron = Neuron_Create.neuron(n\threshold,n\name)
	Return nn
End Function
	


Type connection
	Field src.neuron
	Field dest.neuron
	Field weight#
	Field energy#
End Type
	
Function Connection_Create.connection(src.neuron,dest.neuron,weight#)
	Local c.connection = New connection
	c\src = src
	c\dest = dest
	c\weight = weight
	c\energy = 0
	Return(c)
End Function

Function Connection_getweight#(C.connection)
	Return C\weight#
End Function

Function Connection_setweight(C.connection,w#)
	C\weight = w
End Function

Function Connection_getenergy#(C.connection)
	Return C\energy#
End Function

Function Connection_setenergy(C.connection,e#)
	C\energy = e
End Function

Function Connection_getsource.neuron(C.connection)
	Return C\src
End Function

Function Connection_setsource(C.connection,s.neuron)
	C\src = s
End Function

Function Connection_getdestination.neuron(C.connection)
	Return C\dest
End Function

Function Connection_setdestination(C.connection,d.neuron)
	C\dest = d
End Function

Function Connection_remove(C.connection)
	Delete C
End Function

Function Connection_clearenergy()
	Local C.connection
	For C = Each connection
		C\energy = 0
	Next
End Function

Function Connection_doweight()
	Local C.connection
	For C = Each connection
		C\energy = C\src\energy*C\weight
	Next
End Function
Function Connection_dotransfer()
	Local C.connection
	For C = Each connection
		C\dest\energy = C\dest\energy+C\energy
	Next
End Function
	

</textarea> <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
