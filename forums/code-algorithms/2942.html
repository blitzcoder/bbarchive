<!DOCTYPE html><html lang="en" ><head ><title >Akima spline</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Make splines with akima interpolation method, language=bmx, category=Algorithms'><meta name='author' content='Jur'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=6>Algorithms</a>/Akima spline</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2942.bmx">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Akima spline by Jur</td><td align="right">2012 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> Make splines with akima interpolation method </td></tr><tr ><td class="cell"><pre class="code">SuperStrict

Graphics 800,600
SeedRnd(MilliSecs())	'Different each time
SetLineWidth(2)

Global SelectedPoint:Point
Global Points:Point[10]
MakePoints()

Function MakePoints()
	For Local p:Int=0 Until Points.length
		Points[p]=New Point
		Points[p].x=Rand(50,750)
		Points[p].y=Rand(100,550)
	Next
EndFunction

While Not KeyHit(KEY_ESCAPE)
	Cls
	
	For Local p:Int=0 Until Points.length
		If Abs(MouseX()-Points[p].x)&lt;4 And Abs(MouseY()-Points[p].y)&lt;4 Then
			If MouseDown(1) Then
				SelectedPoint = Points[p]
			EndIf
		EndIf	
	Next
	
	If SelectedPoint Then
		SelectedPoint.x=MouseX()
		SelectedPoint.y=MouseY()
		If MouseDown(1) = False Then 
			SelectedPoint = Null
			FlushMouse()
		EndIf
	EndIf
	
	If KeyHit(KEY_SPACE) Then MakePoints()

	Local xAprev:Float, yAprev:Float, xCprev:Float, yCprev:Float
	For Local i:Int=2 Until Points.length-3
		For Local mu:Float=0 To 1 Step 0.05
			'DebugStop()

			Local xC:Float = CubicInterpolate(Points[i-1].x, Points[i].x, Points[i+1].x, Points[i+2].x, mu)
			Local yC:Float = CubicInterpolate(Points[i-1].y, Points[i].y, Points[i+1].y, Points[i+2].y, mu)
			
			Local yA:Float = AkimaInterpolate(Points[i-2].y, Points[i-1].y, Points[i].y, Points[i+1].y, Points[i+2].y, Points[i+3].y, mu)
			Local xA:Float = AkimaInterpolate(Points[i-2].x, Points[i-1].x, Points[i].x, Points[i+1].x, Points[i+2].x, Points[i+3].x, mu)

			If xAprev=0 And yAprev=0 Then
				xAprev = xA
				yAprev = yA
			EndIf
			If xCprev=0 And yCprev=0 Then
				xCprev = xC
				yCprev = yC
			EndIf
			
			SetColor 220,220,200
			DrawLine xCprev,yCprev, xC,yC
			
			SetColor 255,200,0
			DrawLine xAprev,yAprev, xA,yA

			xAprev = xA
			yAprev = yA
			xCprev = xC
			yCprev = yC

		Next
	Next
	
	
	'points
	SetColor 255,255,255
	For Local i:Int=2 Until Points.length-3
		DrawOval(Points[i].x-2, Points[i].y-2, 4,4)
		DrawText i,Points[i].x+7,Points[i].y+7
	Next
	
	SetColor 220,220,200
	DrawText "cubic spline",10,10
	SetColor 255,200,0
	DrawText "akima spline",10,30
	SetColor 200,200,200
	DrawText "move points with mouse, make new points with &lt;space&gt; ",10,50
	
	Flip
Wend


' akima interpolation requires 6 points 
' interpolate between y3 And y4 at position mu (0..1)
'----------------------------------------------------------------------------
Function AkimaInterpolate:Float(y1:Float, y2:Float, y3:Float, y4:Float, y5:Float, y6:Float, mu:Float)

	Local num1:Float = Abs((y5-y4)- (y4-y3))*(y3-y2) + Abs((y3-y2) - (y2-y1))*(y4-y3)
	Local den1:Float = Abs((y5-y4) - (y4-y3)) + Abs((y3-y2) - (y2-y1))
	
	Local num2:Float = Abs((y6-y5)- (y5-y4))*(y4-y3) + Abs((y4-y3) - (y3-y2))*(y5-y4)
	Local den2:Float = Abs((y6-y5) - (y5-y4)) + Abs((y4-y3) - (y3-y2))
	
	Local t1:Float
	If den1&gt;0.00001 Then   '0
		t1=num1/den1
	Else
		t1=0.0
	EndIf
	
	Local t2:Float
	If den2&gt;0.00001 Then   '0
		t2=num2/den2
	Else
		t2=0.0
	EndIf
	
	Local C:Float = (3*(y4-y3) - 2*t1 - t2)
	Local D:Float = (t1 + t2 - 2*(y4-y3))
	
	Return  y3 + (t1 + (C + D*mu)*mu)*mu

EndFunction




'interpolate between y1 and y2
Function CubicInterpolate:Float(y0:Float, y1:Float, y2:Float, y3:Float, mu:Float)
   Local a0#,a1#,a2#,a3#,mu2#

   mu2 = mu*mu
   a0 = y3-y2-y0+y1
   a1 = y0-y1-a0
   a2 = y2-y0
   a3 = y1
   
   Return a0*mu*mu2+a1*mu2+a2*mu+a3
End Function

Type Point
	Field x:Int
	Field y:Int
	
	Function Create:point(x#,y#)
		Local p:point=New point
		p.x=x
		p.y=y
		Return p
	End Function
End Type</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TAS</td><td align="right"><font class=tiny>2013</font></td></tr></table></td></tr><tr ><td class="posttext"> Interesting <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
