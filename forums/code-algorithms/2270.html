<!DOCTYPE html><html lang="en" ><head ><title >Generate Nice Terrain+Lightmap</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='Terrain Generator , with ColorMap and ultra fast Lightmaping, language=bb, category=Algorithms'><meta name='author' content='Bobysait'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 ><a href=codearcs.php>Code archives</a>/<a href=codearcs.php?cat=6>Algorithms</a>/Generate Nice Terrain+Lightmap</h1><div class="quote">This code has been declared by its author to be Public Domain code.</div><br><a href="2270.bb">Download source code</a><br><br><table width="100%" cellspacing="0" cellpadding="0"><tr ><td width="100%"><table width=100% cellspacing=0 cellpadding=0><tr ><td class=headleft></td><td class=head><table width=100% cellspacing=0 cellpadding=0><tr ><td >Generate Nice Terrain+Lightmap by Bobysait</td><td align="right">2008 </td></tr></table></td><td class=headright></td></tr></table></td></tr><tr ><td class="cell"> This Code makes paradisiac islands, generates the colormap and the lightmap.<br><br>We can update the lightmap in realtime, it may use about 50 ms to render a 256*256 Terrain, so it 's very fast.<br>For bigger Terrain, we could use multiple pass to render the lightmap in realtime.<br><br>This code is here for free, you may use it for your own project (commercial or not), but eventually, credit are always welcome.<br><br>Comments are also welcome.<br>ps : Yes, Of course, I love when people say I'm clever ^^<br><br><br>Features :<br>the ColorMap process use a simple "height to ColorLevel" system.<br>the Lightmap use an algorythm of ray to check the height of the terrain along the Sun vector.<br>The generated terrain is a little bit rocky, so you might want to blur the heightmap before any other process. </td></tr><tr ><td class="cell"><pre class="code">Graphics3D 800,600,0,2
SetBuffer BackBuffer()

Local World%=	CreatePivot		();

Local piv	=	CreatePivot		(World);
Local cam	=	CreateCamera	(piv);
Local CamY#	=	10.0
				PositionEntity	(cam, 0,CamY,0,0);
				CameraRange		(cam, 1,10000);
				CameraClsColor	(cam, 100,150,255);

Local Sun%	=	CreateSphere	(10,World)
				PositionEntity	(Sun,1000,500,1000)
				ScaleEntity		(Sun,15,15,15)
				EntityColor		(Sun,250,230,220)
				EntityFX		(Sun,1)
				PointEntity		(Sun,World)
Local TSize%=	512
Local Terr	=	CreateTerrain	(TSize,World)
				Generation_Terrain(Terr,4,250,4,150,.01,.15,20,TSize/3)
				TerrainColore	(Terr)
;				ExtractLightMap	(Terr,4,250,Sun)
				EntityFX		(Terr,1)

Local Plane%=	CreatePlane		(1,World)
				EntityColor		(Plane,80,50,10)
				EntityFX		(Plane,1)

Local Sea%	=	CreatePlane		(1,World)
				EntityColor		(Sea,10,50,100)
				EntityFX		(Sea,1)
				EntityAlpha		(Sea,.5)
				MoveEntity		(Sea,0,250*.08,0)

Local MainTime%=MilliSecs()
Local OldTime%=0
Repeat
	msx=MouseXSpeed()
	msy=MouseYSpeed()
	msz=MouseZSpeed()
	TurnEntity piv,0,-msx,0
	TurnEntity cam,+msy,0,0
	Local vz# = Float ( ( ( KeyDown(200)+KeyDown(17) ) &gt; 0 ) - ( ( KeyDown(208)+KeyDown(31) ) &gt; 0 ) )
	Local vx# = Float ( ( ( KeyDown(205)+KeyDown(32) ) &gt; 0 ) - ( ( KeyDown(203)+KeyDown(30) ) &gt; 0 ) )
	MoveEntity piv,.1*vx*CamY,0,.1*vz*CamY
	Px#=EntityX(piv,1)
	Pz#=EntityZ(piv,1)
	Py#=TerrainY(Terr,Px,0,Pz)
	PositionEntity piv,pX,Py,Pz
	If msz CamY=CamY+CamY*.1*Float(msz):PositionEntity cam,0,CamY,0,0

	Time=MilliSecs()-MainTime
	; 4*360° / Min
		AT%=Float(time)*.001
		Tour%=4
		Dec#=.360
		Ang#=Dec*Float(AT*Tour)
		Rayon#=1500.0
		SunPosX#=Rayon*Cos(Ang):SunPosZ#=Rayon*Sin(Ang)
		PositionEntity Sun,SunPosX,500,SunPosZ,1:PointEntity Sun,World

		If Time&gt;OldTime	ExtractLightMap(Terr,4,250,Sun):OldTime=Time+5000

	MoveMouse 400,300
	RenderWorld
		Text 10,10,"Ang="+Ang
	Flip
Until KeyHit(1)
FreeEntity World
End

Function Generation_Terrain(Terrain%,sclX#,sclY#,sclZ#,nbc#,COEF_HAUT1#=.005,COEF_HAUT2#=.02,Zone1#=15,Zone2#=50, smoothterrain%=2)
	Local Taille#=TerrainSize(Terrain)
	SeedRnd (MilliSecs())
	Local a
	For a = 0 To nbc
		; varie la hauteur de coef1 à coef2 =&gt; plus la surface est grande, plus on adoucie !
		; inversement, plus la surface est petite, plus on generera de "pics"
		Local COEF_HAUT#	=	Rnd(COEF_HAUT1,COEF_HAUT2)
		Local ZONE#			=	Rnd(Zone1/2,Zone2/2);*COEF_HAUT*10
		
		Local Force# = 1.0 / (ZONE*COEF_HAUT);
		If Force&lt;1 Then Force=1;
		If Force&gt;10 Then Force=10;
		
		;ZONE=ZONE/2
		Local COEF#=90.0/ZONE
		Local pX#=Rand(ZONE+Taille/10,Taille#-ZONE-Taille/10)
		Local pZ#=Rand(ZONE+Taille/10,Taille#-ZONE-Taille/10)
		TFormPoint pX,0,pZ,0,Terrain : pX=TFormedX():pZ=TFormedZ()
		
		Local frc%
		For frc=1 To Force
			pX=pX+Rand(-ZONE/Force,ZONE/Force)
			pZ=pZ+Rand(-ZONE/Force,ZONE/Force)
			Local X#, Z#
			Local AH_X#, AH_Y#, AH_Z#, AH_T#
			For X = -ZONE To ZONE
				AH_X	=	Cos(X*COEF)*COEF_HAUT
				For Z# = -ZONE To ZONE
					AH_Z	=	Cos(Z*COEF)
					AH_T	=	AH_X * AH_Z + TerrainHeight(Terrain,pX+X,pZ+Z)
					If AH_T&gt;1	Then AH_T=1;
					ModifyTerrain Terrain, pX+X,pZ+Z,AH_T
				Next
			Next
		Next
	Next
	
	For i = smoothterrain To TerrainSize(Terrain)-1-smoothterrain
		For j = smoothterrain To TerrainSize(Terrain)-1-smoothterrain
			Local sum# = 0.0, nb=0
			For tx = -smoothterrain To smoothterrain
				For ty = -smoothterrain To smoothterrain
					sum=sum+TerrainHeight(Terrain, i+tx,j+ty)
					nb=nb+1
				Next
			Next
			ModifyTerrain(Terrain, i,j, sum/nb, True);
		Next
	Next
	ScaleEntity (Terrain,sclX#,sclY#,sclZ#)
	MoveEntity	(Terrain,-sclX*Taille*.5,0,-sclZ*Taille*.5)

End Function


Function TerrainColore(Terrain%)
	Tsz	=	TerrainSize		(Terrain)
	Tex	=	CreateTexture	(Tsz,Tsz):ScaleTexture(Tex,Tsz,Tsz)
	CBuf%=	GraphicsBuffer	()
	TBuf%=	TextureBuffer	(Tex):SetBuffer(TBuf):LockBuffer(TBuf)
	For i = 0 To Tsz-1
		For j = 0 To Tsz-1
			AH_T#=TerrainHeight(Terrain,i,j)
			If AH_T&lt;.08		; Sol-&gt;Mer =&gt; Sol-&gt;Sable
				Dh#=AH_T*1.0/.08	:R=080+030*Dh:G=050+030*Dh:B=010+040*Dh	; fin = 110 / 080 / 050
			ElseIf AH_T&lt;.1	; Sable
				Dh#=(AH_T-.08)*1/.02:R=110+080*Dh:G=080+080*Dh:B=050+070*Dh	; fin = 190 / 160 / 120
			ElseIf AH_T&lt;.15	; Herbe
				Dh#=(AH_T-.1)*1/.05	:R=190-170*Dh:G=160-120*Dh:B=120-110*Dh	; fin = 020 / 040 / 010
			ElseIf AH_T&lt;.7	; Roche
				Dh#=(AH_T-.17)*1/.55:R=020+080*Dh:G=040+060*Dh:B=010+040*Dh	; fin = 120 / 100 / 050
			Else			; neige
				Dh#=(AH_T-.72)*1/.3	:R=120+020*Dh:G=100+050*Dh:B=050+160*Dh	; fin = 180 / 150/ 210
			EndIf
			If R&lt;0 R=0
			If G&lt;0 G=0
			If B&lt;0 B=0
			WritePixelFast i,Tsz-j-1,R Shl(16) + G Shl(8) + B
		Next
	Next
	UnlockBuffer(TBuf):SetBuffer(CBuf)
	EntityTexture	(Terrain,Tex,0,0):FreeTexture(Tex)
End Function

Dim Terr_Shd#(0,0)
Function ExtractLightMap%(Terrain%,Scx#,Scy#,Sun%=0)
	Local Sz#	=	TerrainSize(Terrain)
	Dim Terr_Shd(Sz,Sz)
	Local LMap	=	CreateTexture(Sz,Sz):ScaleTexture(LMap,Sz,Sz):EntityTexture(Terrain,LMap,0,2)
	Local CBuff%=	GraphicsBuffer(),TBuff%=TextureBuffer(LMap)
	If Sun&lt;&gt;0	:TFormNormal(0,0,1,Sun,0)
	Else		:TFormNormal(-1,-.5,-.8,0,0)
	EndIf
	Local SunVx#=TFormedX(),SunVy#=TFormedY(),SunVz#=TFormedZ()
	Scy#=Scy/Scx
	For i = 0 To Sz-1
		For j = 0 To Sz-1
			dx#=0.0:dy#=0.0:dz#=0.0:dh#=TerrainHeight(Terrain,i,j)*Scy
			Repeat
				dx=dx+SunVx*.95:dy=dy+SunVy*.95:dz=dz+SunVz*.95
				If i+dx&gt;=0 And i+dx&lt;Sz And j+dz&gt;=0 And j+dz&lt;Sz
					TrY#=TerrainHeight(Terrain,i+dx,j+dz)*Scy
					If TrY&gt;dh+dy Exit
					Terr_Shd(i+dx,j+dz)=.9-.4*(dh-Try)/Scy
				Else	:Exit
				EndIf
			Forever
		Next
	Next
	SetBuffer(TBuff):LockBuffer(TBuff)
	For i= 0 To Sz-1:For j= 0 To Sz-1
		If Terr_Shd(i,j)&lt;&gt;0	:rgb=255*Terr_Shd(i,j)
		Else				:rgb=255
		EndIf
		WritePixelFast i,Sz-j-1,RGB Shl(16) + RGB Shl(8) + RGB + 255 Shl(24)
	Next:Next
	UnlockBuffer(TBuff):SetBuffer CBuff
	FreeTexture			(LMap)
	Dim Terr_Shd(0,0)
End Function</pre></td></tr></table><br><a name="comments">Comments</a><br><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bobysait</td><td align="right"><font class=tiny>2008</font></td></tr></table></td></tr><tr ><td class="posttext"> and here is the multi pass for shadowing the terrain in realtime<br><br><pre class=code>

Dim Terr_Shd#(0,0)
Global LightmapExtracted%=False
Function ExtractLightMap%(Terrain%,Scx#,Scy#,Sun%=0,FreeLightmap%=True)
	Local Sz#	=	TerrainSize(Terrain)
	Dim Terr_Shd(Sz,Sz)
	Local LMap	=	CreateTexture(Sz,Sz,1+16+32):ScaleTexture(LMap,Sz,Sz):EntityTexture(Terrain,LMap,0,2)
	Local CBuff%=	GraphicsBuffer(),TBuff%=TextureBuffer(LMap)
	If Sun&lt;&gt;0 :TFormNormal(0,0,1,Sun,0) :Else :TFormNormal(-1,-.5,-.8,0,0) :EndIf
	Local SunVx#=TFormedX(),SunVy#=TFormedY(),SunVz#=TFormedZ()
	scy#=Scy/Scx
	For i = 0 To Sz-1:For j = 0 To Sz-1 :dx#=0.0:dy#=0.0:dz#=0.0:dh#=TerrainHeight(Terrain,i,j)*scy :Repeat
		dx=dx+SunVx*.95:dy=dy+SunVy*.95:dz=dz+SunVz*.95
		If i+dx&gt;=0 And i+dx&lt;Sz And j+dz&gt;=0 And j+dz&lt;Sz
			TrY#=TerrainHeight(Terrain,i+dx,j+dz)*scy:If TrY&gt;dh+dy:Exit:EndIf:Terr_Shd(i+dx,j+dz)=.9-.4*(dh-Try)/Scy
		Else :Exit :EndIf
	Forever :Next:Next
	SetBuffer(TBuff):LockBuffer(TBuff)
	For i= 0 To Sz-1:For j= 0 To Sz-1
		If terr_Shd(i,j)&lt;&gt;0 :rgb=255*terr_Shd(i,j) :Else :rgb=255 :EndIf
		WritePixelFast i,Sz-j-1,RGB Shl(16) + RGB Shl(8) + RGB + 255 Shl(24)
	Next:Next
	UnlockBuffer(TBuff):SetBuffer CBuff:TextureBlend(LMap,5)
	If FreeLightmap	:FreeTexture(LMap):Dim Terr_Shd(0,0):LightmapExtracted=False
	Else			:LightmapExtracted=True:Return LMap
	EndIf
End Function

Global CurseurI%,CurseurJ%
Global CurseurFromI%,CurseurToI
Global CurseurFromJ%,CurseurToJ
Function TerrainRealtimeShadowMap(Terrain%,LMap%,Scx#,scy#,Sun%=0)
	Local Sz#	=	TerrainSize(Terrain)
	If LightmapExtracted=False	LMap=ExtractLightMap(Terrain,Scx,Scy,Sun,False) :CurseurToI=Sz-1 :CurseurToJ=Sz-1 :CurseurFromI=0: CurseurFromJ=0
	Local CBuff%=	GraphicsBuffer(),TBuff%=TextureBuffer(LMap)
	If Sun&lt;&gt;0 :TFormNormal(0,0,1,Sun,0) :Else :TFormNormal(-1,-.5,-.8,0,0) :EndIf
	Local SunVx#=TFormedX(),SunVy#=TFormedY(),SunVz#=TFormedZ():scy#=Scy/Scx
	CurseurFromI=0 :CurseurToI=Sz-1
	If CurseurFromJ=Sz-1 :CurseurFromJ=0 :Else :CurseurFromJ=CurseurFromJ+1 :EndIf
	If CurseurFromJ&gt;Sz-1 CurseurFromJ=Sz-1
	CurseurToJ=CurseurFromJ
	Local dx#,dy#,dz#,dh#
	For i = CurseurFromI To CurseurToI :For j = CurseurFromJ To CurseurToJ:dx=0:dy=0:dz=0:dh=TerrainHeight(Terrain,i,j)*scy :Repeat
		dx=dx+SunVx*.95:dy=dy+SunVy*.95:dz=dz+SunVz*.95
		If i+dx&gt;=0 And i+dx&lt;Sz And j+dz&gt;=0 And j+dz&lt;Sz
			TrY#=TerrainHeight(Terrain,i+dx,j+dz)*scy :If TrY&gt;dh+dy:Exit:EndIf:Terr_Shd(i+dx,j+dz)=.9-.4*(dh-Try)/Scy
		Else :Exit :EndIf
	Forever:Next:Next
	SetBuffer(TBuff):LockBuffer(TBuff)
	For i = CurseurFromI To CurseurToI:For j = CurseurFromJ To CurseurToJ
		If terr_Shd(i,j)&lt;&gt;0	:rgb=255*terr_Shd(i,j) :Else :rgb=255 :EndIf
		WritePixelFast i,Sz-j-1,RGB Shl(16) + RGB Shl(8) + RGB; + 255 Shl(24)
	Next:Next
	UnlockBuffer(TBuff):SetBuffer CBuff:Return LMap
End Function
</pre><br><br><br>=&gt; TerrainRealtimeShadowMap(Terrain%,LMap%,Scx#,scy#,Sun%=0)<br>Specify the LightMap Texture applyied to the terrain<br>So, you 'll have 2 choices :<br>choise 1 : -&gt; Make a first complete rendering using "ExtractLightMap" function<br>For convinience, I had an extra "LightmapExtracted" global, if the Lightmap has not yet been extracted, then the lightmap will be automatically generated...<br><br>choise 2 : -&gt; Make a cleared texture, apply it to the terrain, and specify the texture to LMap parameter for TerrainRealtimeShadowMap(Terrain%,LMap%,Scx#,scy#,Sun%=0)<br><br>the function TerrainRealtimeShadowMap() is made to be used inside the main loop<br>it performs line after line shadow render<br>the larger the terrain, most time it needs.<br><br>It ran it at 500 FPS on a 1024*1024 TerrainSize (Config in Sig) <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DareDevil</td><td align="right"><font class=tiny>2008</font></td></tr></table></td></tr><tr ><td class="posttext"> Good !!!<br><br>if you store in array the value TerrainHeight(Terrain,i+dx,j+dz) obtainend over speed<br><br>bye <br><br></td></tr></table><br><table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DareDevil</td><td align="right"><font class=tiny>2008</font></td></tr></table></td></tr><tr ><td class="posttext"> Hi have changed your code for litle speed up<br><br>Bye<br><br><pre class=code>
Graphics3D 800,600,0,2
SetBuffer BackBuffer()

Local World%=	CreatePivot		()

Local piv	=	CreatePivot		()
Local cam	=	CreateCamera	()
Local CamY#	=	1000.0
PositionEntity	(cam,0,CamY,0,0)
CameraRange		(cam,1,5000)

Local Sun%	=	CreateSphere	(10,World)
PositionEntity	(Sun,1000,500,1000)
ScaleEntity		(Sun,15,15,15)
EntityColor		(Sun,250,230,220)
EntityFX		(Sun,1)
PointEntity		(Sun,World)
Local TSize%=	256
Local Terr	=	CreateTerrain	(TSize,World)
Generation_Terrain(Terr,4,250,4,150,.01,.15,20,TSize/3)
TerrainColore	(Terr)
;				ExtractLightMap	(Terr,4,250,Sun)
EntityFX		(Terr,1)

Local Plane%=	CreatePlane		(1,World)
EntityColor	(Plane,80,50,10)
EntityFX		(Plane,1)

Local Sea%	=	CreatePlane		(1,World)
EntityColor	(Sea,10,50,100)
EntityFX		(Sea,1)
EntityAlpha	(Sea,.5)
MoveEntity	(Sea,0,250*.08,0)


Global OldTime%=MilliSecs()
;Stop
Global TLightMap = ExtractLightMap(Terr,4,250,Sun,False)
Repeat
	spd#=5
	
	;TurnEntity piv,0,-msx,0
	;TurnEntity cam,+msy,0,0
	;MoveEntity piv,0,0,.1*(KeyDown(200)-KeyDown(208))*CamY
	
	MoveEntity cam,(KeyDown(205)-KeyDown(203))*spd,0,((MouseDown(1)Or KeyDown(201))-(MouseDown(2)Or KeyDown(207)))*spd
	TurnEntity cam,-MouseYSpeed()*0.1,-MouseXSpeed()*0.1,0
	RotateEntity cam,EntityPitch(cam,True),EntityYaw(cam,True),0
	MoveMouse GraphicsWidth()*.5,GraphicsHeight()*.5
	
	;PositionEntity cam,0,CamY,0,0
		
;	Time=MilliSecs()-MainTime
; 4*360° / Min
	
	
	Dec#=Dec#+1.0
	Local Rayon#=1000.0
	Local SunPosX#=Rayon*Cos(Dec)
	Local SunPosZ#=Rayon*Sin(Dec)
	PositionEntity Sun,SunPosX,500,SunPosZ,1:PointEntity Sun,World
	
	;If Time&gt;OldTime	TerrainRealtimeShadowMap(Terr,TLightMap,4,250,Sun):OldTime=Time+1
		
	ExtractLightMap(Terr,4,250,Sun,False)
	;TerrainRealtimeShadowMap(Terr,TLightMap,4,250,Sun)
	
	MoveMouse 400,300
	RenderWorld
	Text 10,10,"Ang : "+Dec
	Text 10,20,"Fps : "+(MilliSecs()-OldTime) : OldTime%=MilliSecs()
	Flip(True)
Until KeyHit(1)
FreeEntity World
End
		
Function Generation_Terrain(Terrain%,sclX#,sclY#,sclZ#,nbc#,COEF_HAUT1#=.005,COEF_HAUT2#=.02,Zone1#=15,Zone2#=50)
	Local Taille#=TerrainSize(Terrain)
	SeedRnd (MilliSecs())
	For a=0 To nbc#
		; varie la hauteur de coef1 à coef2 =&gt; plus la surface est grande, plus on adoucie !
		; inversement, plus la surface est petite, plus on generera de "pics"
		Local COEF_HAUT#=Rnd(COEF_HAUT1,COEF_HAUT2)
		Local ZONE#=Rand(Zone1/2,Zone2/2);*COEF_HAUT*10
		Force=1/(ZONE*COEF_HAUT)
		If Force&lt;1 Force=1
			If Force&gt;10 Force=10
		;ZONE=ZONE/2
				Local COEF#=90.0/ZONE
				pX#=Rand(ZONE+Taille/10,Taille#-ZONE-Taille/10)
				pZ#=Rand(ZONE+Taille/10,Taille#-ZONE-Taille/10)
				TFormPoint Px,0,Pz,0,Terrain
				PX=TFormedX():PZ=TFormedZ()
				For frc=1 To Force
					Px=Px+Rand(-ZONE/Force,ZONE/Force)
					Pz=Pz+Rand(-ZONE/Force,ZONE/Force)
					For X# = -ZONE To ZONE
						AH_X#	=	Cos(X*COEF)*COEF_HAUT
						For Z# = -ZONE To ZONE
							AH_Z#	=	Cos(Z*COEF)
							AH_T#	=	AH_X*AH_Z+TerrainHeight(Terrain,Px+X,Pz+Z)
							If AH_T&gt;1 AH_T=1
								ModifyTerrain Terrain, Px+X,Pz+Z,AH_T
							Next
						Next
					Next
				Next
				ScaleEntity (Terrain,sclX#,sclY#,sclZ#)
				MoveEntity	(Terrain,-sclX*Taille*.5,0,-sclZ*Taille*.5)
				
End Function


Function TerrainColore(Terrain%)
	Local px%,py%
	Tsz	=	TerrainSize		(Terrain)
	Tex	=	CreateTexture	(Tsz,Tsz):ScaleTexture(Tex,Tsz,Tsz)
	CBuf%=	GraphicsBuffer	()
	TBuf%=	TextureBuffer	(Tex):SetBuffer(TBuf):LockBuffer(TBuf)
	For px = 0 To Tsz-1
		For py = 0 To Tsz-1
			AH_T#=TerrainHeight(Terrain,px,py)
			If AH_T&lt;.08		; Sol-&gt;Mer =&gt; Sol-&gt;Sable
				Dh#=AH_T*1.0/.08	
				R=080+030*Dh
				G=050+030*Dh
				B=010+040*Dh	; fin = 110 / 080 / 050
			ElseIf AH_T&lt;.1	; Sable
				Dh#=(AH_T-.08)*1/.02
				R=110+080*Dh
				G=080+080*Dh
				B=050+070*Dh	; fin = 190 / 160 / 120
			ElseIf AH_T&lt;.15	; Herbe
				Dh#=(AH_T-.1)*1/.05	
				R=190-170*Dh
				G=160-120*Dh
				B=120-110*Dh	; fin = 020 / 040 / 010
			ElseIf AH_T&lt;.7	; Roche
				Dh#=(AH_T-.17)*1/.55
				R=020+080*Dh
				G=040+060*Dh
				B=010+040*Dh	; fin = 120 / 100 / 050
			Else			; neige
				Dh#=(AH_T-.72)*1/.3	
				R=120+020*Dh
				G=100+050*Dh
				B=050+160*Dh	; fin = 180 / 150/ 210
			EndIf
			If R&lt;0 R=0
				If G&lt;0 G=0
					If B&lt;0 B=0
						WritePixelFast px,Tsz-py-1,R Shl(16) + G Shl(8) + B
					Next
				Next
				UnlockBuffer(TBuf):SetBuffer(CBuf)
				EntityTexture	(Terrain,Tex,0,0):FreeTexture(Tex)
End Function




Dim Terr_Shd#(0,0)
Global CurseurI%,CurseurJ%
Global CurseurFromI%,CurseurToI
Global CurseurFromJ%,CurseurToJ
Global LightmapExtracted%=False

Function ExtractLightMap%(Terrain%,Scx#,Scy#,Sun%=0,FreeLightmap%=True)
	;===&gt;
	Local Sz#	=	TerrainSize(Terrain)
	Local LMap	=	CreateTexture(Sz,Sz,1+16+32) : TextureBlend(LMap,2)
	Local CBuff%=	GraphicsBuffer()
	Local TBuff%=TextureBuffer(LMap)
	Local SunVx#, SunVy#, SunVz#;
	Local px%,py%
	Local dx#,dy#,dz#,dh#
	Local fa#,fb#
	Local RGB%
	Local TrY#=0
	;===&gt;
	Dim Terr_Shd(Sz,Sz)
	;===&gt;
	ScaleTexture(LMap,Sz,Sz)
	EntityTexture(Terrain,LMap,0,2)
	If Sun&lt;&gt;0 Then 
		TFormNormal(0,0,1,Sun,0) 
	Else 
		TFormNormal(-1,-.5,-.8,0,0) 
	EndIf
	SunVx#=TFormedX();
	SunVy#=TFormedY();
	SunVz#=TFormedZ();
	Scy#=Scy/Scx;
	
	For px = 0 To Sz-1
		For py = 0 To Sz-1 
			dx#=0.0
			dy#=0.0
			dz#=0.0
			dh#=TerrainHeight(Terrain,px,py)*Scy 
			Repeat
				;===&gt;
				dx = dx+SunVx
				dy = dy+SunVy
				dz = dz+SunVz
				fa = px+dx
				fb = py+dz
				;===&gt;
				If fa&gt;=0 And fa&lt;Sz And fb&gt;=0 And fb&lt;Sz Then 
					TrY#=TerrainHeight(Terrain,fa,fb)*Scy
					If TrY&gt;dh+dy Then 
						Exit
					EndIf
					Terr_Shd(fa,fb)=.9-.4*(dh-TrY)/Scy
				Else 
					Exit 
				EndIf
			Forever
		Next
	Next
	
	PI_Smooth(Terrain)
	
	SetBuffer(TBuff)
	LockBuffer(TBuff)
	For px= 0 To Sz-1
		For py= 0 To Sz-1
			If Terr_Shd(px,py)&gt;0 Then 
				RGB=255-(255*Terr_Shd(px,py)) 
			Else 
				RGB=0 
			EndIf
			WritePixelFast px,Sz-py-1, RGB Shl(16) + RGB Shl(8) + RGB + 255 Shl(24)
		Next
	Next
	;===&gt;
	UnlockBuffer(TBuff)
	SetBuffer CBuff
	;===&gt;
	If FreeLightmap Then 
		;===&gt;
		FreeTexture(LMap)
		;Dim Terr_Shd(0,0)
		LightmapExtracted=False
		;===&gt;
	Else 
		;===&gt;
		LightmapExtracted=True
		Return LMap
	;===&gt;
	EndIf
	
End Function

Function PI_Smooth(Terrain%)
	;===&gt;
	Local Sz#	=	TerrainSize(Terrain)
	Local pix#, pix1#, x, y, offsetY, b
	;===&gt;
	;smoot
	;===&gt;
	For y=0 To Sz-3
		
		For x=0 To Sz-3
			;===&gt;
			Terr_Shd(x+1,y) = (Terr_Shd(x,y)+Terr_Shd(x+1,y)+Terr_Shd(x+2,y))/3
			Terr_Shd(x,y+1) = (Terr_Shd(x,y)+Terr_Shd(x,y+1)+Terr_Shd(x,y+2))/3
			;===&gt;
		Next
		;===&gt;
	Next
	
	;===&gt;
End Function
</pre> <br><br></td></tr></table><br><a href="codearcs.php" >Code Archives Forum</a><table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
