<!DOCTYPE html><html lang="en" ><head ><title >Not drawing multiple images.</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Not drawing multiple images.</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >Not drawing multiple images.</a><br><br>
<a name="1143812"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Awesome</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> So after defeating the nefarious level editor I'm now working on the primary game, I have it pulling the actual level information, everything is going well except for drawning my enemies. If I make it draw a rectangle for each enemy it works fine, but when I try to have it draw the actual "Villain" image it only draws the first one and leaves the rest blank. This tells me that my DrawSprite() is probably the issue, but I can't see the problem. <br><br><pre class=code>SuperStrict

Include "openlevel.bmx"

AppTitle="Platform Game"
Const  gwidth:Int = 1280
Const  gheight:Int = 1024

Graphics gwidth, gheight

Global scn_left:Int = 0
Global scn_top:Int = 0
Global Spritelist:TList = CreateList()

For Local x:Int = 0 To mapx-1
	For Local y:Int = 0 To mapy-1
		For Local z:Int = 0 To mapz-1
			If map[x,y,4] &gt; -1 Then Global H:hero = Hero.Create("images/hero.png",(x*tsize)-scn_left,(y*tsize) - scn_top)
			If map[x,y,3] = 0 Then Global E:Enemy = Enemy.Create("images/Villain.png",(x*tsize)-scn_left,(y*tsize) - scn_top)
		Next 
	Next
Next

Repeat
	
	Cls
	
	For Local H:Hero = EachIn Spritelist
		H.UpdateSprites()
	Next 
	
	For Local a:Sprite = EachIn Spritelist
		a.DrawSprites()
	Next 
	
	Flip

Until AppTerminate() Or KeyHit(Key_escape)

Type Sprite
	Field x:Int
	Field y:Int
	Field I:TImage
	
	Method DrawSprites()
		DrawImage I,x,y
	End Method
	
End Type
	
Type Hero Extends Sprite	
	Function Create:Hero(File:String,xstart:Int,ystart:Int)
		Local H:Hero = New Hero
			H.x=xstart
			H.y=ystart
			H.I=LoadImage(file)
			
			ListAddLast Spritelist, H
		Return H
	End Function
	
	Method UpdateSprites()
		If KeyDown(key_left) Then x = x - 5
		If KeyDown(key_right) Then x = x + 5
	End Method
End Type 
	
Type Enemy Extends Sprite
	Function Create:Enemy(File:String,xstart:Int,ystart:Int)
		Local E:Enemy = New Enemy 
			E.x=xstart
			E.y=ystart
			E.I=LoadImage(file)
			
			ListAddLast Spritelist, E
		Return E
	End Function
	
	Method UpdateSprites()
		
	End Method
End Type

End  </pre> <br><br></td></tr></table><br>
<a name="1143815"></a>

<a name="1143816"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Change your looping and dont create Globals in a loop ;)<br><br><pre class=code>
Global H:Hero
' Dont need Global E:Enemy

For Local x:Int = 0 To mapx-1
	For Local y:Int = 0 To mapy-1
		For Local z:Int = 0 To mapz-1
			If map[x,y,4] &gt; -1 Then H = Hero.Create("images/hero.png",(x*tsize)-scn_left,(y*tsize) - scn_top)
			If map[x,y,3] = 0 Then Enemy.Create("images/Villain.png",(x*tsize)-scn_left,(y*tsize) - scn_top)
		Next 
	Next
Next
</pre><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1143838"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I would replace<br><br><pre class=code>
	For Local H:Hero = EachIn Spritelist
		H.UpdateSprites()
	Next 
	
	For Local a:Sprite = EachIn Spritelist
		a.DrawSprites()
	Next 
</pre><br><br>with<br><br><pre class=code>
	'both types extend from sprite, so we can use that
	'that way we replace a loop of a maybe-big-list with type checks
	'use such style if MORE THAN ONE hero is possible in the list
	For Local a:Sprite = EachIn Spritelist
		if Hero(a) &lt;&gt; null then Hero(a).UpdateSprites()
		a.DrawSprites()
	Next 
</pre><br><br>or better:<br><br><pre class=code>
	'we stored hero as global - no need to look for it.
	H.UpdateSprites()
	For Local a:Sprite = EachIn Spritelist
		a.DrawSprites()
	Next 
</pre><br><br><br>To check the images/drawimage-part - replace:<br><pre class=code>
Type Sprite
	Field x:Int
	Field y:Int
	Field I:TImage
	
	Method DrawSprites()
		DrawImage I,x,y
	End Method
	
End Type
</pre><br><br>with<br><br><pre class=code>
Type Sprite
	Field x:Int
	Field y:Int
	Field I:TImage
	
	Method DrawSprites()
		if I&lt;&gt;null 
			DrawImage(I,x,y)
		else
			DrawRect(x,y,20,20)
			'print "missing sprite image XYZ drawn at x="+x+" y="+y
		endif
	End Method
	
End Type
</pre><br><br><br>Hope that helps a bit.<br><br>bye<br>Ron<br><br>PS: if you know that enemies share images, make a TMap with Sprites and access them that way and not with LoadImage(url) multiple times.<br><br>- create map<br>- check existance of object by url as key<br>- insert object (Timage) to map with url as key<br><br>You can do the same with other resources in your application. <br><br></td></tr></table><br>
<a name="1143962"></a>

<a name="1143963"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Awesome</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Okay so I did everything suggested (aside from the Tmap, I need to learn more about those) and it's working, almost too well. Now for some reason it's drawing multiple images of the Hero sprite, only one of which actually exists as an object created by the hero type, if I remove H from the Spritelist then it deletes the sprite that I have control over and just leaves a static image that's standing at the starting XY coordinates.<br><br>Edit: I tested the sprites for the enemies and they aren't affected by this issue, so it's just an issue with the hero for some reason.<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1144004"></a>

<a name="1144005"></a>

<a name="1144006"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Try this (also I added the TMap stuff that Derron talked about and I added some code so I code actually run (eg mapx, mapy etc):<br><pre class=code>SuperStrict

AppTitle="Platform Game"
Const WIDTH:Int = 1280
Const HEIGHT:Int = 1024

Graphics WIDTH, HEIGHT

Global scn_left:Int = 0
Global scn_top:Int = 0
Global sprite_list:TList = CreateList()

Global mapx:Int = 20
Global mapy:Int = 20
Global mapz:Int = 20
Global tsize:Int = 30
Global map:Int[,,]

Global h:THero 
Global images:TImageBank = New TImageBank
LoadImages()
PopulateMap()

Local heroCreated:Int = False

For Local x:Int = 0 To mapx - 1
	For Local y:Int = 0 To mapy - 1
		For Local z:Int = 0 To mapz - 1
			If map[x,y,4] &gt; -1 And Not heroCreated Then
				h = THero.Create(images.Get("hero"),(x*tsize)-scn_left,(y*tsize) - scn_top)
				heroCreated = True
			EndIf
			If map[x,y,3] = 0 Then TEnemy.Create(images.Get("Villain"),(x*tsize)-scn_left,(y*tsize) - scn_top)
		Next 
	Next
Next

Repeat
	Cls
	h.UpdateSprite()
	For Local a:TSprite = EachIn sprite_list
		a.DrawSprite()
	Next 
	
	Flip
Until AppTerminate() Or KeyHit(KEY_ESCAPE)

Function LoadImages()
	images.Load("images/hero.png")
	images.Load("images/Villain.png")
EndFunction

Function PopulateMap()
	map = New Int[mapx, mapy, mapz]
	
	For Local x:Int = 0 To mapx - 1
		For Local y:Int = 0 To mapy - 1
			For Local z:Int = 0 To mapz - 1
				map[x, y, z] = Rand(0, 10)
			Next
		Next
	Next
EndFunction

Type TSprite
	Field x:Int
	Field y:Int
	Field image:TImage
	
	Method DrawSprite()
		DrawImage image,x,y
	End Method
End Type
	
Type THero Extends TSprite	
	Function Create:THero(image:TImage, xstart:Int, ystart:Int)
		Local h:THero = New THero
		h.x = xstart
		h.y = ystart
		h.image = image

		sprite_list.AddLast(h)
		Return h
	End Function
	
	Method UpdateSprite()
		If KeyDown(key_left) Then x = x - 5
		If KeyDown(key_right) Then x = x + 5
	End Method
End Type 
	
Type TEnemy Extends TSprite
	Function Create:TEnemy(image:TImage, xstart:Int, ystart:Int)
		Local e:TEnemy = New TEnemy 
		e.x = xstart
		e.y = ystart
		e.image = image
			
		sprite_list.AddLast(e)
		Return e
	End Function
	
	Method UpdateSprite()
		
	End Method
End Type

Type TImageBank Extends TMap
	Method Load(name:String)
		Local i:TImage = LoadImage(name)
		If i = Null Then RuntimeError "Error - Cannot load image "+name
		Insert(StripAll(Upper(name)), i)
	EndMethod
	
	Method Get:TImage(name:String)
		Local i:TImage = TImage(ValueForKey(Upper(name)))
		If i = Null Then RuntimeError "Error - Cannot find image "+name
		Return i
	EndMethod
	
EndType

End  
</pre><br><br><font class="tiny">Last edited 2012</font><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1144062"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> @therevills<br><br>[bbcode]<br>Global h:THero <br>...<br>Local heroCreated:Int = False<br>...<br>			If map[x,y,4] &gt; -1 And Not heroCreated Then<br>				h = THero.Create(images.Get("hero"),(x*tsize)-scn_left,(y*tsize) - scn_top)<br>				heroCreated = True<br>			EndIf<br>[/bbcode]<br><br>could be stripped by...<br><br>[bbcode]<br>Global h:THero = Null<br>...<br>			If map[x,y,4] &gt; -1 And h = null<br>				h = THero.Create(images.Get("hero"),(x*tsize)-scn_left,(y*tsize) - scn_top)<br>			EndIf<br>[/bbcode]<br><br>Think a "null" check is not that work intensive :D<br><br><br><br>@awesome<br>Try to get some code which you can post here for us to test.<br><br>Another hint:<br>instead of calling a type's method "UpdateSprite<b>s</b>" you should consider calling it "Update" and "Draw" and the object containing the list<br>can then have a function (methods if instances) like "UpdateAll".<br><br>If you run into problems like "but Hero extends sprite and has to have a method 'draw' too" try something like :<br><br>[bbcode]<br>Type THero<br>	field sprite:TSprite<br>	field name:string="My Hero"<br>...<br>	Method Draw()<br>		self.sprite.Draw()<br>	End Method<br>End Type<br>[/bbcode]<br><br>That's needed as you won't be able to extend a class and overwrite the parents methods in Blitzmax (for adding further parameters and so on).<br><br>Extend types if they share something - so eg. Renderables but do not extend from attributes they have.<br><br>[bbcode]<br><br>'object having some values associated but needs some nifty helpers<br>Type TCoord<br>	field x:float<br>	field y:float<br>...<br>	Method IsSame(otherTCoord)<br>	...<br>	Method IsSameXY(x:float,y:float)<br>	...<br>End Type<br><br><br>Type TRenderable<br>	field coord:TCoord<br>	...<br>	'abstract methods are defined in all the ancestors<br>	Method Draw() abstract<br>	...<br>End Type<br><br>Type TSprite extends TRenderable<br>	field image:TImage<br>	...<br>	Method Draw()<br>		DrawImage(self.image, self.coord.x, self.coord,y)<br>		...<br>	End Method<br>End Type<br><br>Type THero<br>	field sprite:TSprite<br>	'here we can store the coord of last cycle<br>	'or before we started some moving action - a backup :D <br>	'but this won't be that easy if extending the things<br>	field lastCoord:TCoord<br><br>	Method Draw(additionalParameterLikeOverwriteSomething:int = 0)<br>		self.sprite.Draw()<br>	End Method<br>End Type<br><br>'do the same for Type TEnemy<br>'but this time you can extend different Enemies... TFireElemental extends TEnemy<br>'then just change sprites, or update-behaviour<br><br>Type TLevel<br>	field blocksAndEntityList...<br>	field enemiesList:TList<br>	...<br>End Type<br><br>Type TGame<br>	field hero:THero<br>	field currentLevel:TLevel<br>	...<br>	Method Update()<br>		hero.update()<br>		for local enemy:TEnemy = eachin self.currentLevel.enemiesList<br>			enemy.update()			<br>		next<br>	End Method<br>	...<br>	'same for Method Draw()...<br>End Type<br><br>...<br>[/bbcode]<br><br>Ok it's not tested as I wrote it now and I might be run into offtopic or some kind of tutorial for "oop in BlitzMax" ... think I finish my posting here :D<br><br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
