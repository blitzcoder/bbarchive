<!DOCTYPE html><html lang="en" ><head ><title >my vertical scrolling is slow</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >my vertical scrolling is slow</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >my vertical scrolling is slow</a><br><br>
<a name="733031"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >hub</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi !<br><br> i'm interested only by vertical scrolling. i use this piece of code into my project. When i increase the number of tiles, the scrolling is slow. Coz the for-next loop inside  the Compute_one_row_Scrolling() and Draw_one_row (). How to avoid the for-next loop ? I'm searching a better solution.<br><br>i my game i use an editor to produce a level, next i read a file. so i must keep this structure.<br><br>Col = 0 to 32, Row 0 to 1000 or more.<br><br>Type TBrick<br><br>	Field Col<br>	Field Row<br><br>End Type<br><br>(indexed by Row, and Col)<br>	<br>i know scrolling examples into tutorials. i prefer some help to correct this code.<br><br>Many thanks for your help !<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Global List_bricks : TList = CreateList()

Type TBrick

	Field Col
	Field Row
	
	Field r
	Field g
	Field b
	
	Field x
	Field y
	
	' ----------------------------------------------------------------------------------------------
	
	Function Create:TBrick (X,y)
	
		Local b:TBrick = New TBrick
		
		b.Col = x
		b.Row = y 
		
		b.r = Rand (100,255)
		b.g = Rand (100,255)
		b.b = Rand (100,255)

		ListAddLast List_bricks, b
		
		Return b
		
	End Function

	' ----------------------------------------------------------------------------------------------

	Function Compute_one_row_Scrolling (Row, ScrollY)
	
		For Local b:TBrick = EachIn List_bricks
		
			If b.Row = Row Then
				b.x = b.Col * 32
				b.y = ScrollY 
			End If
			
		Next
		
	End Function

	' ----------------------------------------------------------------------------------------------

	Function Draw_one_row (Row)
	
		For Local b:TBrick = EachIn List_bricks
		
			If b.Row = Row Then
			
				SetColor b.r, b.g, b.b
				DrawRect b.x, b.y, 32,32
				
			End If
			
		Next
		
	End Function
	
	' ----------------------------------------------------------------------------------------------

	Function MySort:Int(o1:Object, o2:Object)
	
        Local diff% = TBrick(o1).Row - TBrick(o2).Row
        If diff = 0 Then
             diff = TBrick(o1).Col - TBrick(o2).Col
        EndIf
        Return diff	

	End Function

	' ----------------------------------------------------------------------------------------------

	Function Sort_the_list ()
	
		SortList(List_bricks,True,MySort)
		
	End Function		

	' ----------------------------------------------------------------------------------------------

End Type

' create some tiles for this example (i use an editor for my game)

' if you increase max_row, the scrolling is slow, coz the for-next loop into the 
' Compute_one_row_Scrolling().

Local Max_row = 100

For Local y=0 To Max_Row

	For Local x=0 To 31

		TBrick.Create (x, y)

	Next

Next

' sort them by y and x (not usefull here, but the game editor don't sort them...)

TBrick.Sort_the_list()

' go

Graphics 1024,768


Local t
Local ScrollY = 0
Local Row = 0
Local TimeScroll = MilliSecs()

While Not KeyDown(KEy_ESCAPE)

	' compute scrolling
	
	If TimeScroll + 20 &lt; MilliSecs() Then

		If ScrollY &lt; 31 Then
		
			ScrollY = SCrollY + 1
			
		Else

			ScrollY = 0

			If Row &lt; Max_row + 25 Then
			
				Row = Row + 1
				
			Else
			
				Row = 0
				
			End If
			
		End If
		
		t = MilliSecs()
		
		Local Decal = -32
		
		For Local r = row To (row-24) Step - 1
		
			TBrick.Compute_one_row_Scrolling(r,ScrollY + Decal)
			
			Decal = Decal + 32
			
		Next
		
		t = MilliSecs() - t		
		
		TimeScroll = MilliSecs()
		
	End If
	
	' draw
	
	Cls

	For Local r = row To (row-24) Step - 1
	
		TBrick.Draw_one_row(r)
		
	Next
	
	DrawText "time=" + String(t), 0,0
	DrawText "row=" + String(row), 0,30
	
	Flip

Wend 
</textarea> <br><br></td></tr></table><br>
<a name="733040"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> 32000 tile images being drawn? Not surprised it's slow. <br><br></td></tr></table><br>
<a name="733048"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >hub</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> no : it's an example. Of course only visibles tiles being drawn to screen !!!. Please test the code to see the pb. <br><br></td></tr></table><br>
<a name="733084"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SoggyP</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello.<br><br>Personally, I'd:<br>- hold the tiles in an array, not a list;<br>- apply a global decal offset to the values;<br>- create images instead of drawing rects.<br><br>The difficulty is that every inner loop you're iterating through all the tiles 24 times, regardless of if they're on screen or not, and then iterating through them once again 24 times to display them (again regardless of if they're on screen or not).  That's a lot of tiles.<br><br>If you're not sure what I mean, reduce the number of rows to 1 and then increase the number of rows to 10000 to see a change in the time taken.  t remains constant for each value.<br><br>Goodbye. <br><br></td></tr></table><br>
<a name="733090"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SoggyP</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello.<br><br>Take a look at visits and tiles in the following:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Global List_bricks : TList = CreateList()
Global visits
Global tiles

Type TBrick

	Field Col
	Field Row
	
	Field r
	Field g
	Field b
	
	Field x
	Field y
	
	' ----------------------------------------------------------------------------------------------
	
	Function Create:TBrick (X,y)
	
		Local b:TBrick = New TBrick
		
		b.Col = x
		b.Row = y 
		
		b.r = Rand (100,255)
		b.g = Rand (100,255)
		b.b = Rand (100,255)

		ListAddLast List_bricks, b
		
		Return b
		
	End Function

	' ----------------------------------------------------------------------------------------------

	Function Compute_one_row_Scrolling (Row, ScrollY)
		visits:+1	
		For Local b:TBrick = EachIn List_bricks
			tiles:+1
					
			If b.Row = Row Then
				b.x = b.Col * 32
				b.y = ScrollY 
			End If
			
		Next
		
	End Function

	' ----------------------------------------------------------------------------------------------

	Function Draw_one_row (Row)
		visits:+1	
		For Local b:TBrick = EachIn List_bricks
			tiles:+1
			If b.Row = Row Then
			
				SetColor b.r, b.g, b.b
				DrawRect b.x, b.y, 32,32
				
			End If
			
		Next
		
	End Function
	
	' ----------------------------------------------------------------------------------------------

	Function MySort:Int(o1:Object, o2:Object)
	
        Local diff% = TBrick(o1).Row - TBrick(o2).Row
        If diff = 0 Then
             diff = TBrick(o1).Col - TBrick(o2).Col
        EndIf
        Return diff	

	End Function

	' ----------------------------------------------------------------------------------------------

	Function Sort_the_list ()
	
		SortList(List_bricks,True,MySort)
		
	End Function		

	' ----------------------------------------------------------------------------------------------

End Type

' create some tiles for this example (i use an editor for my game)

' if you increase max_row, the scrolling is slow, coz the for-next loop into the 
' Compute_one_row_Scrolling().

Local Max_row = 100

For Local y=0 To Max_Row

	For Local x=0 To 31

		TBrick.Create (x, y)

	Next

Next

' sort them by y and x (not usefull here, but the game editor don't sort them...)

TBrick.Sort_the_list()

' go

Graphics 1024,768


Local t
Local ScrollY = 0
Local Row = 0
Local TimeScroll = MilliSecs()

While Not KeyDown(KEy_ESCAPE)

	' compute scrolling
	
	If TimeScroll + 1 &lt; MilliSecs() Then ' NOTE CHANGE TO 1 MILLISEC HERE TO GIVE FASTEST TRY

		If ScrollY &lt; 31 Then
		
			ScrollY = SCrollY + 1
			
		Else

			ScrollY = 0

			If Row &lt; Max_row + 25 Then
			
				Row = Row + 1
				
			Else
			
				Row = 0
				
			End If
			
		End If
		
		t = MilliSecs()
		
		Local Decal = -32
		
		For Local r = row To (row-24) Step - 1
		
			TBrick.Compute_one_row_Scrolling(r,ScrollY + Decal)
			
			Decal = Decal + 32
			
		Next
		
		t = MilliSecs() - t		
		
		TimeScroll = MilliSecs()
		
	End If
	
	' draw
	
	Cls

	For Local r = row To (row-24) Step - 1
	
		TBrick.Draw_one_row(r)
		
	Next
	
	DrawText "time=" + String(t), 0,0
	DrawText "row=" + String(row), 0,30
	DrawText "Visits=" + String(visits),0,48	
	DrawText "Tiles=" + String(tiles),0,64
	Flip
	visits = 0
	tiles = 0
Wend 
</textarea><br><br>And try changing the number of rows from 100 to 1000 to 10000.<br><br>Goodbye. <br><br></td></tr></table><br>
<a name="733097"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >hub</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> The difficulty is that every inner loop you're iterating through all the tiles 24 times, regardless of if they're on screen or not, and then iterating through them once again 24 times to display them (again regardless of if they're on screen or not). That's a lot of tiles. <br></div>agree with this it's my pb !<br><br><div class="quote"> <br>create images instead of drawing rects.<br> <br></div><br>i use images into my game<br><br><img src="http://www.bayre.com/zigwigwis/capt_1002.jpg"><br><br><div class="quote"> <br>The difficulty is that every inner loop you're iterating through all the tiles 24 times, regardless of if they're on screen or not, and then iterating through them once again 24 times to display them <br></div><br>note that i separate logic and drawing into the game to apply delta time.<br><br><div class="quote"> <br>- hold the tiles in an array, not a list;<br>- apply a global decal offset to the values; <br></div><br>good idea. <br><br>Many thanks for your idea, is somebody know how to avoid this for-next loops ??? Finally i'm searching a better code structure ! My first idea produce bad code !!! <br><br></td></tr></table><br>
<a name="733107"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SoggyP</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello.<br><br>Ok, what is the sort function there for?<br><br>Goodbye. <br><br></td></tr></table><br>
<a name="733109"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >hub</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Ok, what is the sort function there for? <br></div>oups... for nothing in this case. i use it into my game as the tiles are not yet sorted when you create them into the editor.<br><br><img src="http://www.bayre.com/zigwigwis/capt_70.png"> <br><br></td></tr></table><br>
<a name="733114"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Put the Tbricks in an array with dimensions the width/height of your 'wall'. Then keep track of screen_offset and draw from that index to the number of bricks that can fit on a screen. <br><br></td></tr></table><br>
<a name="733131"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >hub</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Put the Tbricks in an array with dimensions the width/height of your 'wall'. Then keep track of screen_offset and draw from that index to the number of bricks that can fit on a screen.  <br></div><br>no sure to understand your idea. Is it this :<br>use an one dimension array, determine an index for each tile ?  i've thinking about this idea to direct access to a tile. The problem is that produce a huge array... <br>32 x 24 x 1000. with a lot of null values (have not a tile for each array cell).<br><br>Not that the previous editor screen is a test, there are no 'walls' into the game. See the ingame screenshoot picture. <br><br></td></tr></table><br>
<a name="733145"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> I was responding to the code you posted but I would still consider a two dimension array even with the null values. <br>I might not be thinking straight by why would it be 32*24*1000? If there are 1000 rows and each row contains 32 bricks isn't that array[32,1000]?<br>Somebody else might be able to say whether a tmap might help with your tilelist indexed against it's x position (can you have duplicate indexes in a tmap?) converted to a string.<br>You can then use your screen_pos (x value for topleft of your world map) and compare it against the indexes drawing anything that fits between screen_pos and screen_pos+max_number_per_screen.<br>I haven't really used tmaps but it might be possible.<br>This might be a bit too much for the Beginner's forum and I'm sure many people have written games which resemble you screenshots so there could be a simple answer. <br><br></td></tr></table><br>
<a name="733199"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >hub</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote">  If there are 1000 rows and each row contains 32 bricks isn't that array[32,1000]? <br></div><br>oups, yes you're right. Sorry for the mystake.<br><br><div class="quote"> This might be a bit too much for the Beginner's forum and I'm sure many people have written games which resemble you screenshots so there could be a simple answer <br></div><br>hope they also read this forum ! <br><br></td></tr></table><br>
<a name="733272"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >hub</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks,  time to me to code something with your ideas... i'll post my results here. <br><br></td></tr></table><br>
<a name="733281"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >hub</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> First attempt to code with arrays :<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Const Max_TILEMAP_HEIGHT = 1000

Type TScreen

	Field array_cells   : TCell[32 * MAX_TILEMAP_HEIGHT]
	Field array_tiles   : TTile[]
	Field array_cannons : TCannon[]
		
	Field NbTiles
	Field NbCannons
	
	Field ScrollY
	Field EllapsedTime
	
	Field t
		
	Function Init:TScreen ()
	
		Local s:TScreen = New TScreen
		
		' fill the array with empy values
		
		For Local y = 0 To MAX_TILEMAP_HEIGHT - 1
		
			For Local x=0 To 32 - 1
			
				Local i = y * 32 + x
				
				s.array_cells[i] = TCell.Create (x,y)
					
			Next
			
		Next
		
		s.NbTiles = 0
		s.NbCannons = 0
		
		s.EllapsedTime = MilliSecs()
		s.ScrollY = 0
	
		Return s
	
	End Function
	
	Method Add_tile (pPosX, pPosY)
	
		' check index if not out of bound
		
		Local index = pPosY * 32 + pPosX
		
		If index &gt; 32 * MAX_TILEMAP_HEIGHT Then
			RuntimeError "Bad Index"
		End If
		
		array_cells[Index].Id_tile = NbTiles + 1
		
		NbTiles = NbTiles + 1		
		
		array_tiles = array_tiles [..NbTiles+1]
		
		array_tiles [NbTiles] = TTile.Create(pPosX, pPosY)
		
	End Method

	Method Add_cannon (pPosX, pPosY)
	
		' check index if not out of bound
		
		Local index = pPosY * 32 + pPosX
		
		If index &gt; 32 * MAX_TILEMAP_HEIGHT Then
			RuntimeError "Bad Index"
		End If
		
		array_cells[index].Id_cannon = NbCannons + 1
		
		NbCannons = NbCannons + 1		
		
		array_cannons = array_cannons [..NbCannons+1]
		
		array_cannons [NbCannons] = TCannon.Create(pPosX, pPosY)		
	
	End Method
	
	Method Compute_logic()
	
		If EllapsedTime + 20 &lt; MilliSecs() Then
			
			t = MilliSecs()
			
			For Local y=0 To MAX_TILEMAP_HEIGHT - 1
			
				For Local x=0 To 31
				
					Local Index = y * 32 + x
					 
		
					If array_cells[Index].Id_tile &lt;&gt; - 1 Then
					
						Local ix
						ix = array_cells[Index].Id_tile
						array_tiles [ix].PosY = array_tiles [ix].PosY - 1
						
					End If
		
					If array_cells[Index].Id_cannon &lt;&gt; - 1 Then
					
						Local ix
						ix = array_cells[Index].Id_cannon
						array_cannons [ix].PosY = array_cannons [ix].PosY - 1
					
					End If
		
				
				Next
				
			Next
			
			t = MilliSecs() - t
			
		
			EllapsedTime = MilliSecs()

		End If
	
	End Method
	
	Method Draw()
	
		For Local y=0 To MAX_TILEMAP_HEIGHT - 1
		
			For Local x=0 To 31
			
				Local Index = y * 32 + x
				 
	
				If array_cells[Index].Id_tile &lt;&gt; - 1 Then
				
					Local px,py, ix, r,g,b
					ix = array_cells[Index].Id_tile 
					px = array_tiles [ix].PosX
					py = array_tiles [ix].PosY
					r = array_tiles [ix].r
					g = array_tiles [ix].g
					b = array_tiles [ix].b
					
					SetColor r,g,b
					DrawRect px,py, 32,32
					
				End If
	
				If array_cells[Index].Id_cannon &lt;&gt; - 1 Then
				
					Local px,py, angle, ix
					ix = array_cells[Index].Id_cannon
					px = array_cannons [ix].PosX
					py = array_cannons [ix].PosY
					angle = array_cannons [ix].Angle
					
					SetColor 255,0,0
					DrawOval px,py, 32,32
					
					SetColor 0,0,255
					SetRotation angle
					SetLineWidth 3
					DrawLine px+16,py+16,px+16+20,py+16
					SetLineWidth 2
					SetRotation 0
				
				End If
	
			
			Next
			
		Next
		
		SetColor 255,255,255
		DrawText "Time=" + String(t),10,10
		
	End Method
	
	
End Type

Type TCell

	Field PosX
	Field PosY
	
	Field Id_tile
	Field Id_Cannon
	
	Function Create : TCell (PosX, PosY)
	
		Local c:TCell = New TCell
		
		c.PosX = PosX 
		c.PosY = PosY 
		
		c.Id_Tile = -1
		c.Id_Cannon = -1
		
		Return c
	
	End Function
	
End Type

Type TTile

	Field PosX
	Field PosY
	
	Field r
	Field g
	Field b
	
	Function Create:TTile (PosX, PosY)
	
		Local t:TTile = New TTile
		
		t.PosX = PosX * 32
		t.PosY = PosY * 32
		
		t.r = Rand (100,255)
		t.g = Rand (100,255)
		t.b = Rand (100,255)
		
		Return t
	
	End Function
	
End Type

Type TCannon

	'Field Id_cannon : Int

	Field PosX
	Field PosY
	
	Field angle
	
	Function Create:TCannon (PosX, PosY)
	
		Local c:TCannon = New TCannon
		
		c.PosX = PosX * 32
		c.PosY = PosY * 32
		
		c.angle = Rand (0,360)
		
		Return c
	
	End Function	
	
End Type


' main program

Graphics 1024, 768

Global Screen : TScreen = TScreen.init()

DrawText "please wait, computing random tiles and canon positions...",0,0
Flip

For Local i=1 To 10000

	Local x = Rand (0,31)
	Local y = Rand (0,MAX_TILEMAP_HEIGHT-1)
	Screen.Add_tile (x,y)

	x = Rand (0,31)
	y = Rand (0,MAX_TILEMAP_HEIGHT-1)
	Screen.Add_cannon (x,y)

Next


While Not KeyDown (KEY_ESCAPE)

	Screen.Compute_logic()
	Cls
	Screen.Draw()
	Flip
	
Wend
</textarea> <br><br></td></tr></table><br>
<a name="733287"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Is this <br><pre class=code>		For Local y=0 To MAX_TILEMAP_HEIGHT - 1
</pre><br>still looping through everything rather than what's on-screen? <br><br></td></tr></table><br>
<a name="733293"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >hub</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> yes, this is a very basic scrolling code !!! <br><br></td></tr></table><br>
<a name="733301"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >hub</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> update !<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Const Max_TILEMAP_HEIGHT = 1000

Type TScreen

	Field array_cells   : TCell[32 * MAX_TILEMAP_HEIGHT]
	Field array_tiles   : TTile[]
	Field array_cannons : TCannon[]
		
	Field NbTiles
	Field NbCannons
	
	Field ScrollY
	Field LigneMap
	
	Field EllapsedTime
	
	Field t
		
	Function Init:TScreen ()
	
		Local s:TScreen = New TScreen
		
		' fill the array with empy values
		
		For Local y = 0 To MAX_TILEMAP_HEIGHT - 1
		
			For Local x=0 To 32 - 1
			
				Local i = y * 32 + x
				
				s.array_cells[i] = TCell.Create (x,y)
					
			Next
			
		Next
		
		s.NbTiles = 0
		s.NbCannons = 0
		
		s.EllapsedTime = MilliSecs()
		s.ScrollY = 0
		s.LigneMap = 0
	
		Return s
	
	End Function
	
	Method Add_tile (pPosX, pPosY)
	
		' check index if not out of bound
		
		Local index = pPosY * 32 + pPosX
		
		If index &gt; 32 * MAX_TILEMAP_HEIGHT Then
			RuntimeError "Bad Index"
		End If
		
		array_cells[Index].Id_tile = NbTiles + 1
		
		NbTiles = NbTiles + 1		
		
		array_tiles = array_tiles [..NbTiles+1]
		
		array_tiles [NbTiles] = TTile.Create(pPosX, pPosY)
		
	End Method

	Method Add_cannon (pPosX, pPosY)
	
		' check index if not out of bound
		
		Local index = pPosY * 32 + pPosX
		
		If index &gt; 32 * MAX_TILEMAP_HEIGHT Then
			RuntimeError "Bad Index"
		End If
		
		array_cells[index].Id_cannon = NbCannons + 1
		
		NbCannons = NbCannons + 1		
		
		array_cannons = array_cannons [..NbCannons+1]
		
		array_cannons [NbCannons] = TCannon.Create(pPosX, pPosY)		
	
	End Method
	
	Method Compute_logic()
	
	
		If EllapsedTime + 20 &lt; MilliSecs() Then
		
			If ScrollY &lt; 31 Then
			
				ScrollY = ScrollY + 1
				
			Else
			
				ScrollY = 0
				
				If LigneMap &lt; MAX_TILEMAP_HEIGHT Then
					LigneMap = LigneMap + 1
				Else
					LigneMap = 0
				End If
				
			End If
			
			t = MilliSecs()
			
			Local Decal = -32
			
			For Local y= Lignemap+24 To LigneMap Step -1
			
				For Local x=0 To 31
				
					Local Index = y * 32 + x
					
		
					If array_cells[Index].Id_tile &lt;&gt; - 1 Then
					
						Local ix
						ix = array_cells[Index].Id_tile
						array_tiles [ix].PosY = ScrollY + Decal
						
					End If
		
					If array_cells[Index].Id_cannon &lt;&gt; - 1 Then
					
						Local ix
						ix = array_cells[Index].Id_cannon
						array_cannons [ix].PosY = ScrollY + Decal
					
					End If
		
				
				Next
				
				Decal = Decal + 32
				
			Next
			
			t = MilliSecs() - t
			
		
			EllapsedTime = MilliSecs()

		End If
	
	End Method
	
	Method Draw()
	
		For Local y= Lignemap To LigneMap + 24 Step 1
		
			For Local x=0 To 31
			
				Local Index = y * 32 + x
				 
	
				If array_cells[Index].Id_tile &lt;&gt; - 1 Then
				
					Local px,py, ix, r,g,b
					ix = array_cells[Index].Id_tile 
					px = array_tiles [ix].PosX
					py = array_tiles [ix].PosY
					r = array_tiles [ix].r
					g = array_tiles [ix].g
					b = array_tiles [ix].b
					
					SetColor r,g,b
					DrawRect px,py, 32,32
					
				End If
	
				If array_cells[Index].Id_cannon &lt;&gt; - 1 Then
				
					Local px,py, angle, ix
					ix = array_cells[Index].Id_cannon
					px = array_cannons [ix].PosX
					py = array_cannons [ix].PosY
					angle = array_cannons [ix].Angle
					
					SetColor 255,0,0
					DrawOval px,py, 32,32
					
					SetColor 0,0,255
					SetRotation angle
					SetLineWidth 3
					DrawLine px+16,py+16,px+16+20,py+16
					SetLineWidth 2
					SetRotation 0
				
				End If
	
			
			Next
			
		Next
		
		SetColor 255,255,255
		DrawText "Time=" + String(t),10,10
		DrawText "Lignemap=" + String(LigneMap),10,20
		
	End Method
	
	
End Type

Type TCell

	Field PosX
	Field PosY
	
	Field Id_tile
	Field Id_Cannon
	
	Function Create : TCell (PosX, PosY)
	
		Local c:TCell = New TCell
		
		c.PosX = PosX 
		c.PosY = PosY 
		
		c.Id_Tile = -1
		c.Id_Cannon = -1
		
		Return c
	
	End Function
	
End Type

Type TTile

	Field PosX
	Field PosY
	
	Field r
	Field g
	Field b
	
	Function Create:TTile (PosX, PosY)
	
		Local t:TTile = New TTile
		
		t.PosX = PosX * 32
		t.PosY = PosY * 32
		
		t.r = Rand (100,255)
		t.g = Rand (100,255)
		t.b = Rand (100,255)
		
		Return t
	
	End Function
	
End Type

Type TCannon

	'Field Id_cannon : Int

	Field PosX
	Field PosY
	
	Field angle
	
	Function Create:TCannon (PosX, PosY)
	
		Local c:TCannon = New TCannon
		
		c.PosX = PosX * 32
		c.PosY = PosY * 32
		
		c.angle = Rand (0,360)
		
		Return c
	
	End Function	
	
End Type


' main program

Graphics 1024 , 768 , 32 , 75

Global Screen : TScreen = TScreen.init()

Global MaxTiles = 10000

For Local i=1 To MaxTiles

	Local x = Rand (0,31)
	Local y = Rand (0,MAX_TILEMAP_HEIGHT-1)
	Screen.Add_tile (x,y)

	x = Rand (0,31)
	y = Rand (0,MAX_TILEMAP_HEIGHT-1)
	Screen.Add_cannon (x,y)
	
	If KeyDown (KEY_ESCAPE) Then End
	
	Cls
	DrawText "please wait, computing " + String(MaxTiles) + " random tiles And canons positions... i=" + String(i),0,0
	Flip

Next


While Not KeyDown (KEY_ESCAPE) Or MouseHit(1)

	Screen.Compute_logic()
	Cls
	Screen.Draw()
	Flip
	
Wend
</textarea> <br><br></td></tr></table><br>
<a name="733385"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> I suggest you do a flip(0) with the first flip to speed up the calculations and map creation.<br><br>I am working on a game using  tile scrolling of size 32 x 32 is a random map generator if you want to try it out  is here:<br><a href="http://www.gamedev.net/community/gds/viewentry.asp?projectid=427815" target="_blank">http://www.gamedev.net/community/gds/viewentry.asp?projectid=427815</a> <br><br></td></tr></table><br>
<a name="734481"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >hub</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> good suggestion jesse. Thanks. Good game too. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
