<!DOCTYPE html><html lang="en" ><head ><title >LuaInvaders - Learning Lua with BlitzMax</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >LuaInvaders - Learning Lua with BlitzMax</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >LuaInvaders - Learning Lua with BlitzMax</a><br><br>
<a name="973162"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi All,<br><br>After reading about Lua, I thought I would try to learn it with BlitzMax.<br><br>I couldnt really find any real world examples to help me. So I thought I would put this here as sort of a community project and we can all learn together...<br><br>LuaInvaders.bmx:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' --------------------------------------------
' LuaInvaders
' --------------------------------------------

SuperStrict
SeedRnd MilliSecs()

Const APP_NAME$ = "LuaInvaders"
Const VERSION$ = "v0.1"
Const FULL_NAME$ = APP_NAME + " " +VERSION

Const SCREEN_WIDTH% = 800
Const SCREEN_HEIGHT% = 600

AppTitle = FULL_NAME

Graphics SCREEN_WIDTH, SCREEN_HEIGHT, 0

' --------------------------------------------
' load game images
' --------------------------------------------

AutoMidHandle(1)
Global playerImg:TImage = LoadImage("player.png")
Global alienImg:TImage = LoadImage("alien.png")

' --------------------------------------------
' game objects
' --------------------------------------------

Global player:TPlayer = TPlayer.createPlayer(SCREEN_WIDTH/2,SCREEN_HEIGHT/2)

'For Local i% = 0 To 9
'	TAlien.Create(Rand(0,SCREEN_WIDTH), Rand(0, SCREEN_HEIGHT))
'Next

' --------------------------------------------
' lua stuff
' --------------------------------------------

LuaRegisterObject New TAlien,"alienType"
Local luaAlienClass:TLuaClass=TLuaClass.Create(LoadString("alien_script.lua"))
Local luaAlienInstance:TLuaObject=TLuaObject.Create(luaAlienClass,Null)
luaAlienInstance.Invoke "create_aliens",Null

' --------------------------------------------
' game loop
' --------------------------------------------

While Not AppTerminate() And Not KeyHit(KEY_ESCAPE) 
	player.update()	
	Cls
		TAlien.drawAll()
		player.draw()
	Flip
Wend

' --------------------------------------------
' types
' --------------------------------------------

Type TAlien Extends TSprite
	Global list:TList

	Method Create:TAlien(x#, y#)
		Local a:TAlien = New TAlien
		If TAlien.list = Null TAlien.list = New TList
		Print "Creating alien at x &gt;"+x+" , y &gt;"+y
		a.x = x; a.y = y
		a.rotation = 0
		a.scaleX = 1; a.scaleY = 1
		a.speed = 0
		a.image = alienImg
		TAlien.list.addLast a
		Return a
	End Method 
	
	Function drawAll()
		If Not list Return
		For Local a:TAlien = EachIn List
			a.draw()
		Next
	End Function
	
End Type

' --------------------------------------------

Type TPlayer Extends TSprite
	Field score%
	
	Function createPlayer:TPlayer(x#, y#)
		Local p:TPlayer = New TPlayer
		p.x = x; p.y = y
		p.rotation = 0
		p.scaleX = 1; p.scaleY = 1
		p.speed = 0
		p.image = playerImg
		Return p
	End Function
	
	Method update()
		controls()
		moveForward()
	End Method

	Method controls()
		Local rotationSpeed# = 2
		If KeyDown(KEY_LEFT) Then rotation:-rotationSpeed
		If KeyDown(KEY_RIGHT) Then rotation:+rotationSpeed
		If KeyDown(KEY_UP) Then speed:+0.1
		If KeyDown(KEY_DOWN) Then speed:-0.1
	End Method
End Type

' --------------------------------------------

Type TSprite
	Field image:TImage
	Field x#, y#
	Field dx#, dy#
	Field rotation#
	Field scaleX#, scaleY#
	Field speed#
	
	Method draw()
		SetRotation rotation
		SetScale scaleX, scaleY
		DrawImage image, x, y
		SetRotation 0
		SetScale 1, 1
	End Method
	
	Method moveForward()
		dx=Sin(rotation)*speed
		dy=-Cos(rotation)*speed
		move()
	End Method
	
	Method move()
		x:+dx
		y:+dy
		' wrap
		If x &lt; 0 Then x = SCREEN_WIDTH
		If x &gt; SCREEN_WIDTH Then x = 0
		If y &lt; 0 Then y = SCREEN_HEIGHT
		If y &gt; SCREEN_HEIGHT Then y = 0		
	End Method
End Type
</textarea><br><br>alien_script.lua:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
-- alien_script
math.randomseed (os.clock()*1000)

function create_aliens()
	for a = 1,10 do
		luatype=alienType.create(math.random(800),math.random(600))
	end
end
</textarea><br><br>As you can see, all my Lua script does is create 10 aliens...<br><br>Could someone please show me how would I use Lua to move the 10 aliens around?<br><br>I've uploaded the source/exe/images to here:<br><a href="http://therevills.syntaxbomb.com/LuaInvaders/" target="_blank">http://therevills.syntaxbomb.com/LuaInvaders/</a> <br><br></td></tr></table><br>
<a name="973349"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Heres an updated version...<br><br>LuaInvaders.bmx:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' --------------------------------------------
' LuaInvaders
' --------------------------------------------

SuperStrict
SeedRnd MilliSecs()

Const APP_NAME$ = "LuaInvaders"
Const VERSION$ = "v0.2"
Const FULL_NAME$ = APP_NAME + " " +VERSION

Const SCREEN_WIDTH% = 800
Const SCREEN_HEIGHT% = 600

AppTitle = FULL_NAME

Graphics SCREEN_WIDTH, SCREEN_HEIGHT, 0

' --------------------------------------------
' load game images
' --------------------------------------------

AutoMidHandle(1)
Global playerImg:TImage = LoadImage("player.png")
Global alienImg:TImage = LoadImage("alien.png")

' --------------------------------------------
' game objects
' --------------------------------------------

Global player:TPlayer = TPlayer.createPlayer(SCREEN_WIDTH/2,SCREEN_HEIGHT/2)

' --------------------------------------------
' lua stuff
' --------------------------------------------

LuaRegisterObject New TAlien,"alienType"
Global luaAlienClass:TLuaClass=TLuaClass.Create(LoadString("alien_script.lua"))
Global luaAlienInstance:TLuaObject=TLuaObject.Create(luaAlienClass,Null)
luaAlienInstance.Invoke "create_aliens",Null

' --------------------------------------------
' game loop
' --------------------------------------------

While Not AppTerminate() And Not KeyHit(KEY_ESCAPE)
	' refresh the lua file on the fly...
	If KeyHit(KEY_F5)
		luaAlienClass=TLuaClass.Create(LoadString("alien_script.lua"))
		luaAlienInstance=TLuaObject.Create(luaAlienClass,Null)
	End If

	player.update()
	TAlien.updateAll()
	Cls
		TAlien.drawAll()
		player.draw()
	Flip
Wend

' --------------------------------------------
' types
' --------------------------------------------

Type TAlien Extends TSprite
	Global list:TList

	Method Create:TAlien(x#, y#)
		Local a:TAlien = New TAlien
		If TAlien.list = Null TAlien.list = New TList
		Print "Creating alien at x &gt;"+x+" , y &gt;"+y
		a.x = x; a.y = y
		a.rotation = 0
		a.scaleX = 1; a.scaleY = 1
		a.speed = 0
		a.image = alienImg
		TAlien.list.addLast a
		Return a
	End Method 
	
	Function drawAll()
		If Not list Return
		For Local a:TAlien = EachIn List
			a.draw()
		Next
	End Function
	
	Function updateAll()
		If Not list Return
		LuaRegisterObject player,"playerType"
		For Local a:TAlien = EachIn List
			LuaRegisterObject a,"alienType"
			luaAlienInstance.Invoke "update_alien",Null
		Next
	End Function
		
End Type

' --------------------------------------------

Type TPlayer Extends TSprite
	Field score%
	
	Function createPlayer:TPlayer(x#, y#)
		Local p:TPlayer = New TPlayer
		p.x = x; p.y = y
		p.rotation = 0
		p.scaleX = 1; p.scaleY = 1
		p.speed = 0
		p.image = playerImg
		Return p
	End Function
	
	Method update()
		controls()
		moveForward()
	End Method

	Method controls()
		Local rotationSpeed# = 2
		If KeyDown(KEY_LEFT) Then rotation:-rotationSpeed
		If KeyDown(KEY_RIGHT) Then rotation:+rotationSpeed
		If KeyDown(KEY_UP) Then speed:+0.1
		If KeyDown(KEY_DOWN) Then speed:-0.1
	End Method
End Type

' --------------------------------------------

Type TSprite
	Field image:TImage
	Field x#, y#
	Field dx#, dy#
	Field rotation#
	Field scaleX#, scaleY#
	Field speed#
	
	Method draw()
		SetRotation rotation
		SetScale scaleX, scaleY
		DrawImage image, x, y
		SetRotation 0
		SetScale 1, 1
	End Method
	
	Method moveForward()
		dx=Sin(rotation)*speed
		dy=-Cos(rotation)*speed
		move()
	End Method
	
	Method move()
		x:+dx
		y:+dy
		' wrap
		If x &lt; 0 Then x = SCREEN_WIDTH
		If x &gt; SCREEN_WIDTH Then x = 0
		If y &lt; 0 Then y = SCREEN_HEIGHT
		If y &gt; SCREEN_HEIGHT Then y = 0		
	End Method
End Type
</textarea><br><br>alien_script.lua:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
-- alien_script
math.randomseed (os.clock()*1000)

function create_aliens()
	for a = 1,10 do
		alien_lua_type=alienType.create(math.random(800),math.random(600))
		alien_lua_type.rotation = 101
		alien_lua_type.scalex = 1
		alien_lua_type.scaley = 1
		alien_lua_type.speed = 1
	end
end

function update_alien()	
	if alienType.x &lt; playerType.x then
		alienType.x = alienType.x + 1
	end
	if alienType.x &gt; playerType.x then
		alienType.x = alienType.x - 1
	end
	if alienType.y &lt; playerType.y then
		alienType.y = alienType.y + 1
	end
	if alienType.y &gt; playerType.y then
		alienType.y = alienType.y - 1
	end	
end
</textarea><br><br>So now the aliens will follow the player coded via the Lua...<br><br>Also if you alter the Lua file while the program is running, just hit F5 to refresh the script... <br><br></td></tr></table><br>
<a name="973400"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi therevills,<br><br>Don't think nobody's interested or listening.  I'm very interested in adding lua scripting to my games, right now I haven't got the time to learn lua properly but I'll be following the progress here so please keep it up!<br><br>Cheers<br>Matt<br><br><br>[ps - completely O.T. - I've been hunting for animation software and bumped into Munchies on the front page of the Pro Motion website - small world :) ] <br><br></td></tr></table><br>
<a name="973404"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jkrankie</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm interested, although i haven't really had time to experiment yet. enjoying reading this though :)<br><br>Cheers<br>Charlie <br><br></td></tr></table><br>
<a name="973571"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Arowx</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi thervills this looks cool would you like to contribute this as an article in BlitzMaxCoder 0.2?<br><br><img src="http://www.blitzmaxcoder.com/bmcneedsyou2.png"> <br><br></td></tr></table><br>
<a name="973578"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >N</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> therevills: You probably don't want to use MaxLua, as you seem to be doing.  You're not going to learn much about how to actually use Lua that way, and it's not exactly the best way to use Lua in the first place.  You'd probably be better starting off with something simple that just exposes a handful of functions to the script that then do something or other in the game. <br><br></td></tr></table><br>
<a name="973579"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> @matibee &amp; jkrankie: Thanks for the comments guys...<br><br>@Merx:<br><div class="quote"> would you like to contribute this as an article in BlitzMaxCoder 0.2 <br></div><br>It'll be a pretty small article, maybe for 0.3 or 0.4.... Im still learning about this stuff... BTW great first issue!<br><br>@Nilium:<br><div class="quote"> don't want to use MaxLua <br></div><br>Why? There seems to be a lot of Lua Modules for BlitzMax...<br><div class="quote"> it's not exactly the best way to use Lua in the first place <br></div><br>What is? Could you take what Ive done and so me how you would do it? As I stated in the first post, I cant find any real world examples...<br><div class="quote"> You'd probably be better starting off with something simple that just exposes a handful of functions to the script that then do something or other in the game.  <br></div><br>Examples please? <br><br></td></tr></table><br>
<a name="973581"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >N</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'll see if I can refit it to how I'd do it (without any extra modules), since I haven't been able to find any tutorials that are still up illustrating it.  (My own is gone, I don't remember why - probably a hissyfit I had years ago.) <br><br></td></tr></table><br>
<a name="973606"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >N</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Righto, example.  This does not use anything other than straight Lua functions.  No dependency on MaxLua (it'd have proven to be much more restricting with it).<br><br>I removed the TPlayer and TAlien types because they're not needed, the player and aliens are constructs of the script and not the host application.  <br><br>LuaInvaders.bmx:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' ==================================================================================================
' =========================================== LuaInvaders ==========================================
' ==================================================================================================

' ========================================== Build Options =========================================

'buildopt: GUI
'buildopt: debug

SuperStrict


' =========================================== Constants ===========================================

Const APP_NAME:String						= "LuaInvaders"

Const VERSION_MAJOR:Int						= 0
Const VERSION_MINOR:Int						= 2
Const VERSION_BRANCH:String				= "pure"
Const VERSION:String							= VERSION_MAJOR+"."+VERSION_MINOR+":"+VERSION_BRANCH

Const FULL_NAME:String						= APP_NAME + " " +VERSION

Const SCREEN_WIDTH:Int						= 800
Const SCREEN_HEIGHT:Int						= 600


' ============================================ Globals ============================================

Global lua_state:Byte Ptr					= Null


' ========================================= Initialization =========================================

Function KillLuaState()
	If lua_state Then
		lua_close(lua_state)
		lua_state = Null
	EndIf
End Function


Function Init()
	
	' Seed random number generator
	SeedRnd(MilliSecs())
	
	' Set application title
	AppTitle = FULL_NAME
	
	' Set up graphics
	Graphics( SCREEN_WIDTH, SCREEN_HEIGHT, 0 )

	' All images should do this thing whatever it does too lazy to look it up
	AutoMidHandle(True)
	
	' Kill Lua state once the program ends
	OnEnd(KillLuaState)
	
	' Create the Lua state
	lua_state = luaL_newstate()
	
	' Load base, math, table, and os libraries
	luaopen_base(lua_state)
	luaopen_math(lua_state)
	luaopen_table(lua_state)
	luaopen_os(lua_state)
	
	' I'll use the simple method for registering C functions here
	
	' Register sprite functions
	lua_register( lua_state, "NewSprite", lNewSprite )
	lua_register( lua_state, "ReleaseSprite", lReleaseSprite )
	lua_register( lua_state, "DrawSprites", lDrawSprites )
	
	lua_register( lua_state, "SpritePosition", lSpritePosition )
	lua_register( lua_state, "SetSpritePosition", lSetSpritePosition )
	
	lua_register( lua_state, "SpriteRotation", lSpriteRotation )
	lua_register( lua_state, "SetSpriteRotation", lSetSpriteRotation )
	
	lua_register( lua_state, "SetSpriteImage", lSetSpriteImage )
	
	' Register an input function
	lua_register( lua_state, "KeyDown", lKeyDown )
	
	' Set some globals for the screen size
	lua_pushinteger( lua_state, SCREEN_WIDTH )
	lua_setglobal( lua_state, "SCREEN_WIDTH" )
	lua_pushinteger( lua_state, SCREEN_HEIGHT )
	lua_setglobal( lua_state, "SCREEN_HEIGHT" )
	
	' Run the script so it can set things up
	If luaL_dofile( lua_state, "alien_script.lua" ) Then
		Local error:String = lua_tostring( lua_state, -1 )
		lua_pop( lua_state, 1 )
		RunTimeError(error)
	EndIf
	
	' And enable polled input
	EnablePolledInput()
	
End Function


' ============================================== Main ==============================================

Function Main()
	
	?Not Debug
	Try
	?
	
	Init()
	
	Local Running:Int = True
	
	While Not AppTerminate() And Running
		
		' Call update function
		lua_getglobal( lua_state, "Update" )
		If lua_type( lua_state, -1 ) = LUA_TFUNCTION Then
			If lua_pcall( lua_state, 0, 1, 0 ) Then
				Local error:String = lua_tostring( lua_state, -1 )
				lua_pop( lua_state, 1 )
				RunTimeError(error)
			Else
				' Get the result
				Running = lua_toboolean( lua_state, -1 )
			EndIf
		EndIf
		lua_pop( lua_state, 1 )
		
		Cls
		
		' Call drawing function
		lua_getglobal( lua_state, "DrawScreen" )
		If lua_type( lua_state, -1 ) = LUA_TFUNCTION Then
			If lua_pcall( lua_state, 0, 0, 0 ) Then
				Local error:String = lua_tostring( lua_state, -1 )
				lua_pop( lua_state, 1 )
				RunTimeError(error)
			EndIf
		Else
			lua_pop( lua_state, 1 )
		EndIf
		
		Flip
		
	Wend
	
	?Not Debug
	Catch ex:Object
		
		Notify(ex.ToString())
		End
		
	End Try
	?
	
End Function
Main


' ============================================= Sprite =============================================

Type TSprite
	Field image:TImage
	Field x#, y#
	Field rotation#
	Field scaleX#=1, scaleY#=1
	
	Method draw()
		' Get original values
		Local ssx#, ssy#
		GetScale(ssx, ssy)
		Local sr# = GetRotation()
		
		' Set values for sprite
		SetRotation rotation
		SetScale scaleX, scaleY
		DrawImage image, x, y
		
		' Reset to original values
		SetRotation 0
		SetScale ssx, ssy
	End Method
End Type


' ========================================= Glue Functions =========================================

' Just wraps KeyDown, returns booleans for each key code passed to it
Function lKeyDown:Int( L:Byte Ptr )
	Local keys:Int = lua_gettop(L)
	For Local stackidx:Int = 1 To keys
		lua_pushboolean( L, KeyDown(lua_tointeger( L, stackidx )) )
	Next
	Return keys
End Function


' Creates and pushes 1 sprite, or as many sprites as are indicated by the first argument
' function NewSprite( sprites=1 )
Function lNewSprite:Int( L:Byte Ptr )
	Local sprites:Int = luaL_optint( L, 1, 1 )
	For Local i:Int = 1 To sprites
		' Push the handle as a light userdata
		lua_pushlightuserdata( L, Byte Ptr(HandleFromObject(New TSprite)) )
	Next
	Return sprites
End Function


' Releases all sprites passed to it
' function ReleaseSprite( ... )
Function lReleaseSprite:Int( L:Byte Ptr )
	For Local stackidx:Int = 1 To lua_gettop(L)
		If lua_islightuserdata( L, stackidx ) Then
			Local handle:Int = Int(lua_touserdata( L, stackidx ))
			Assert TSprite(HandleToObject(handle)) Else "Attempt to release handle for object that is not a sprite"
			
			Release handle
		EndIf
	Next
	Return 0
End Function


' Sets the image of a sprite
' function SetSpriteImage(sprite, imagename)
Function lSetSpriteImage:Int( L:Byte Ptr )
	Local img:TImage
	Local imgName:String
	Local sprite:TSprite
	Local handle:Int
	
	handle = Int(lua_touserdata( L, 1 ))
	sprite = TSprite(HandletoObject(handle))
	
	imgName = lua_tostring( L, 2 )
	img = LoadImage(imgName)
	
	sprite.image = img
	
	Return 0
End Function


' Returns the X and Y position of one or more sprites
' e.g., x,y = SpritePosition(sprite)
Function lSpritePosition:Int( L:Byte Ptr )
	Local sprites:int = lua_gettop(L)
	For Local stackidx:Int = 1 To sprites
		If lua_islightuserdata( L, stackidx ) Then
			Local handle:Int = Int(lua_touserdata( L, stackidx ))
			Local sprite:TSprite = TSprite(HandleToObject(handle))
			Assert sprite Else "Attempt to get position of object that is not a sprite"
			
			lua_pushnumber( L, sprite.x )
			lua_pushnumber( L, sprite.y )
		EndIf
	Next
	
	Return sprites*2
End Function


' Sets the position of a single sprite
' function SetSpritePosition(sprite, x, y)
Function lSetSpritePosition:Int( L:Byte Ptr )
	Local sprite:TSprite
	Local handle:Int
	
	handle = Int(lua_touserdata( L, 1 ))
	sprite = TSprite(HandletoObject(handle))
	
	sprite.x = lua_tonumber( L, 2 )
	sprite.y = lua_tonumber( L, 3 )
	
	Return 0
End Function


' Returns the rotation of one or more sprites (angle in degrees)
' e.g., angle = SpritePosition(sprite)
Function lSpriteRotation:Int( L:Byte Ptr )
	Local sprites:int = lua_gettop(L)
	For Local stackidx:Int = 1 To sprites
		If lua_islightuserdata( L, stackidx ) Then
			Local handle:Int = Int(lua_touserdata( L, stackidx ))
			Local sprite:TSprite = TSprite(HandleToObject(handle))
			Assert sprite Else "Attempt to get position of object that is not a sprite"
			
			lua_pushnumber( L, sprite.rotation )
		EndIf
	Next
	
	Return sprites
End Function


' Sets the rotation of a single sprite (angle in degrees)
' function SetSpriteRotation(sprite, angle)
Function lSetSpriteRotation:Int( L:Byte Ptr )
	Local sprite:TSprite
	Local handle:Int
	
	handle = Int(lua_touserdata( L, 1 ))
	sprite = TSprite(HandletoObject(handle))
	
	sprite.rotation = lua_tonumber( L, 2 )
	
	Return 0
End Function


' Draws any sprites passed to it
' function DrawSprites(...)
Function lDrawSprites:Int( L:Byte Ptr )
	For Local stackidx:Int = 1 To lua_gettop(L)
		If lua_islightuserdata( L, stackidx ) Then
			Local sprite:TSprite = TSprite(HandleToOBject(Int(lua_touserdata( L, stackidx ))))
			Assert sprite Else "Attempt to draw object that is not a sprite"
			sprite.Draw()
		EndIf
	Next
	Return 0
End Function
</textarea><br><br>alien_script.lua: (edit: for some reason I wrote the wrong name for this earlier...)<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">-- alien_script
math.randomseed(os.clock())

function clamp(a, b, c)
	local d = c-b
	while a &lt; b do
		a = a + d
	end
	while a &gt; c do
		a = a - d
	end
	return a
end

function Init()

	-- Init some globals
	
	Frozen = false		-- Frozen = whether or not the game is frozen (e.g., updates)
	
	-- Initialize the aliens
	
	Aliens = {}
	do
		local function updateAlien(self)
			local x, y = SpritePosition(self.Sprite)
			local px, py = SpritePosition(Player.Sprite)

			if x &lt; px then
				x = x + 1
			elseif px &lt; x then
				x = x - 1
			end

			if y &lt; py then
				y = y + 1
			elseif py &lt; y then
				y = y - 1
			end
			
			x = clamp(x, 0, SCREEN_WIDTH)
			y = clamp(y, 0, SCREEN_HEIGHT)
			
			SetSpritePosition(self.Sprite, x, y)

			-- return false if the alien is going to be removed
			return true
		end
		
		local k, v
		for k,v in pairs({ NewSprite(10) }) do
			SetSpriteImage(v, "alien.png")
			SetSpritePosition(v, math.random(SCREEN_WIDTH), math.random(SCREEN_HEIGHT))
			Aliens[k] = {
				Sprite = v;
				Speed = 0;
			
				Update = updateAlien;
				Move = objectMove;
			}
		end
	end
	
	-- Initialize the player
	
	Player = {
		Sprite = NewSprite();
		Score = 0;
		Speed = 0;
		
		Update = function(self)
			self:Controls()
			self:Move()
		end;
		
		Controls = function(self)
			local rotationSpeed = 0
			local movementSpeed = 0
			
			local left, right, up, down = KeyDown(KEY_LEFT, KEY_RIGHT, KEY_UP, KEY_DOWN)

			if left then
				rotationSpeed = rotationSpeed - 2
			end
			if right then
				rotationSpeed = rotationSpeed + 2
			end

			if up then
				movementSpeed = movementSpeed + 0.2
			end
			if down then
				movementSpeed = movementSpeed - 0.2
			end
			
			SetSpriteRotation(self.Sprite, SpriteRotation(self.Sprite) + rotationSpeed)

			self.Speed = self.Speed * 0.98 + movementSpeed
		end;
		
		Move = function(self)
			x, y = SpritePosition(self.Sprite)
			angle = -math.rad(SpriteRotation(self.Sprite))

			x = x + math.sin(angle) * self.Speed
			y = y + math.cos(angle) * self.Speed
			
			x = clamp(x, 0, SCREEN_WIDTH)
			y = clamp(y, 0, SCREEN_HEIGHT)

			SetSpritePosition(self.Sprite, x, y)
		end
	}
	SetSpritePosition(Player.Sprite, SCREEN_WIDTH * 0.5, SCREEN_HEIGHT * 0.5)
	SetSpriteImage(Player.Sprite, "player.png")
	
	-- Set the UpdateIn method to nil (this only gets called once per script load)
	UpdateIn = nil
end

UpdateIn = Init
UpdateOut = nil

KEY_LEFT = 37
KEY_UP = 38
KEY_RIGHT = 39
KEY_DOWN = 40
KEY_ESCAPE = 27

-- Executes the method only if the conditions are met
function CondExec(cond, method, ...)
	if method == nil then
		method = cond
	end
	if cond and method then
		method(...)
	end
end

function UpdateAliens()
	local key
	local alien
	
	for key, alien in pairs(Aliens) do
		if not alien:Update() then
			-- Remove the alien from the Aliens table - you can clear fields in tables during
			-- traversal, but you should not add fields that do not already exist (changing existing
			-- values is fine).
			-- Please read the documentation for the next(table,[idx]) function for information about
			-- modifying tables during traversal: <a href="http://www.lua.org/manual/5.1/manual.html#pdf-next" target="_blank">http://www.lua.org/manual/5.1/manual.html#pdf-next</a>
			ReleaseSprite(alien.Sprite)
			Aliens[key] = nil
		end
	end
end

function Update()
	CondExec(UpdateIn)
	
	-- I only added the 'Frozen' bit so I could take a picture of it
	if not Frozen then
		
		Player:Update()
	
		UpdateAliens()
		
	end
	
	CondExec(UpdateOut)
	
	return not KeyDown(KEY_ESCAPE)
end

function DrawScreen()
	
	local sprites = {}
	local key, alien
	
	for key, alien in pairs(Aliens) do
		table.insert(sprites, alien.Sprite)
	end
	
	table.insert(sprites, Player.Sprite)

	-- unpack essentially takes every value in a table and returns them
	DrawSprites(unpack(sprites))
	
end
</textarea><br><br><img src="http://img.skitch.com/20090723-qakhjy3u8wgec11j6ywb1e8g4w.png"><br>I also decided not to use your images, because I was too lazy to download them. <br><br></td></tr></table><br>
<a name="973618"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >N</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> And, for my own personal amusement, the LuGI version.  There's really not a whole lot to wrap in this, so it feels small.  But, there are less functions to wrap and you have direct access to fields, so it works out.<br><br>LuaInvaders.bmx:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' ==================================================================================================
' =========================================== LuaInvaders ==========================================
' ==================================================================================================

' ========================================== Build Options =========================================

'buildopt: GUI
'buildopt: Debug

SuperStrict

Import LuGI.Core
?Debug
Import LuGI.Generator
?

' UNCOMMENT THIS ONCE THE GLUE CODE IS GENERATED
'Include "glue.bmx"


' =========================================== Constants ===========================================

Const APP_NAME:String						= "LuaInvaders"

Const VERSION_MAJOR:Int						= 0
Const VERSION_MINOR:Int						= 3
Const VERSION_BRANCH:String				= "lugi"
Const VERSION:String							= VERSION_MAJOR+"."+VERSION_MINOR+":"+VERSION_BRANCH

Const FULL_NAME:String						= APP_NAME + " " +VERSION

Const SCREEN_WIDTH:Int						= 800
Const SCREEN_HEIGHT:Int						= 600

' SET THIS TO TRUE TO GENERATE THE INITIAL GLUE CODE
Const GENERATE_GLUE:Int						= False


' ============================================ Globals ============================================

Global lua_state:Byte Ptr					= Null


' ========================================= Initialization =========================================

Function KillLuaState()
	If lua_state Then
		lua_close(lua_state)
		lua_state = Null
	EndIf
End Function


Function Init()
	
	' Seed random number generator
	SeedRnd(MilliSecs())
	
	' Set application title
	AppTitle = FULL_NAME
	
	' Set up graphics
	Graphics( SCREEN_WIDTH, SCREEN_HEIGHT, 0 )

	' All images should do this thing whatever it does too lazy to look it up
	AutoMidHandle(True)
	
	' Kill Lua state once the program ends
	OnEnd(KillLuaState)
	
	' Create the Lua state
	lua_state = luaL_newstate()
	
	' Load base, math, table, and os libraries
	luaopen_base(lua_state)
	luaopen_math(lua_state)
	luaopen_table(lua_state)
	luaopen_os(lua_state)
	
	' Register an input function
	lua_register( lua_state, "KeyDown", lKeyDown )
	
	' Set some globals for the screen size
	lua_pushinteger( lua_state, SCREEN_WIDTH )
	lua_setglobal( lua_state, "SCREEN_WIDTH" )
	lua_pushinteger( lua_state, SCREEN_HEIGHT )
	lua_setglobal( lua_state, "SCREEN_HEIGHT" )
	
	' Initialize LuGI
	InitLuGI(lua_state)
	
	' Run the script so it can set things up
	If luaL_dofile( lua_state, "alien_script.lua" ) Then
		Local error:String = lua_tostring( lua_state, -1 )
		lua_pop( lua_state, 1 )
		RunTimeError(error)
	EndIf
	
	' And enable polled input
	EnablePolledInput()
	
End Function


' ============================================== Main ==============================================

Function Main()
	
	?Debug
	
	If GENERATE_GLUE Then
		GenerateGlueCode("glue.bmx")
		End
	EndIf
	
	?
	
	?Not Debug
	Try
	?
	
	Init()
	
	Local Running:Int = True
	Local BadScript:Int = False
	Local ScriptError:String = ""
	
	While Not AppTerminate() And Running
		
		If KeyHit(KEY_F5) Then
			If luaL_dofile( lua_state, "alien_script.lua" ) Then
				BadScript = True
				ScriptError = lua_tostring( lua_state, -1 )
				lua_pop( lua_state, 1 )
			Else
				BadScript = False
				ScriptError = ""
			EndIf
		EndIf
		
		If Not BadScript Then
		
			' Call update function
			lua_getglobal( lua_state, "Update" )
			If lua_type( lua_state, -1 ) = LUA_TFUNCTION Then
				If lua_pcall( lua_state, 0, 1, 0 ) Then
					Local error:String = lua_tostring( lua_state, -1 )
					lua_pop( lua_state, 1 )
					RunTimeError(error)
				Else
					' Get the result
					Running = lua_toboolean( lua_state, -1 )
				EndIf
			EndIf
			lua_pop( lua_state, 1 )
		
		EndIf
		
		Cls
		
		If Not BadScript Then
		
			' Call drawing function
			lua_getglobal( lua_state, "DrawScreen" )
			If lua_type( lua_state, -1 ) = LUA_TFUNCTION Then
				If lua_pcall( lua_state, 0, 0, 0 ) Then
					Local error:String = lua_tostring( lua_state, -1 )
					lua_pop( lua_state, 1 )
					RunTimeError(error)
				EndIf
			Else
				lua_pop( lua_state, 1 )
			EndIf
		
		Else
			
			' Display the script error
			Local tx:Float, ty:Float
			tx = SCREEN_WIDTH*.5 - TextWidth(ScriptError)*.5
			ty = SCREEN_HEIGHT*.5 - TextHeight(ScriptError)*.5
			DrawText( ScriptError, tx, ty )
			
		EndIf
		
		Flip
		
	Wend
	
	?Not Debug
	Catch ex:Object
		
		Notify(ex.ToString())
		End
		
	End Try
	?
	
End Function
Main


' ============================================= Sprite =============================================

Type TSprite {expose constructor="CreateSprite"}
	Field Image:TImage	{hidden}
	Field X#, Y#
	Field Rotation#
	Field ScaleX#=1, ScaleY#=1
	
	Method Delete()
		Notify "Deleted sprite"
	End Method
	
	' Loads an image for the sprite
	Method SetImage(name$)
		Image = LoadImage(name)
	End Method
	
	Method Draw()
		If Not Image Then
			Return
		EndIf
		
		' Get original values
		Local ssx#, ssy#
		GetScale(ssx, ssy)
		Local sr# = GetRotation()
		
		' Set values for sprite
		SetRotation( Rotation )
		SetScale( ScaleX, ScaleY )
		DrawImage( Image, X, Y )
		
		' Reset to original values
		SetRotation(sr)
		SetScale( ssx, ssy )
	End Method
End Type


' ========================================= Glue Functions =========================================

' Leaving this one in because using LuGI to wrap it would be more tedious than it's worth
' Just wraps KeyDown, returns booleans for each key code passed to it
Function lKeyDown:Int( L:Byte Ptr )
	Local keys:Int = lua_gettop(L)
	For Local stackidx:Int = 1 To keys
		lua_pushboolean( L, KeyDown(lua_tointeger( L, stackidx )) )
	Next
	Return keys
End Function
</textarea><br><br>alien_script.lua:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">-- alien_script
math.randomseed(os.clock() * 1000); math.random()

function clamp(a, b, c)
	local d = c-b
	while a &lt; b do
		a = a + d
	end
	while a &gt; c do
		a = a - d
	end
	return a
end

function Init()

	-- Init some globals
	
	Frozen = false		-- Frozen = whether or not the game is frozen (e.g., updates)
	
	-- Initialize the aliens
	
	Aliens = {}
	do
		local function updateAlien(self)
			local sp = self.Sprite
			local px, py = Player.Sprite.X, Player.Sprite.Y

			if sp.X &lt; px then
				sp.X = clamp(sp.X + 1, 0, SCREEN_WIDTH)
			elseif px &lt; self.Sprite.X then
				sp.X = clamp(sp.X - 1, 0, SCREEN_WIDTH)
			end

			if sp.Y &lt; py then
				sp.Y = clamp(sp.Y + 1, 0, SCREEN_HEIGHT)
			elseif py &lt; sp.Y then
				sp.Y = clamp(sp.Y - 1, 0, SCREEN_HEIGHT)
			end
			
			-- return false if the alien is going to be removed
			return true
		end
		
		local sp, idx
		for idx = 1, 10 do
			sp = CreateSprite()
			sp:SetImage("alien.png")
			sp.X, sp.Y, sp.Rotation = math.random(SCREEN_WIDTH), math.random(SCREEN_HEIGHT), math.random(360)
			
			table.insert(Aliens, {
				Sprite = sp;
				Speed = 0;
			
				Update = updateAlien;
				Move = objectMove;
			} )
		end
	end
	
	-- Initialize the player
	
	Player = {
		Sprite = CreateSprite();
		Score = 0;
		Speed = 0;
		
		Update = function(self)
			self:Controls()
			self:Move()
		end;
		
		Controls = function(self)
			local rotationSpeed = 0
			local movementSpeed = 0
			
			local left, right, up, down = KeyDown(KEY_LEFT, KEY_RIGHT, KEY_UP, KEY_DOWN)

			if left then
				rotationSpeed = rotationSpeed - 2
			end
			if right then
				rotationSpeed = rotationSpeed + 2
			end

			if up then
				movementSpeed = movementSpeed + 0.2
			end
			if down then
				movementSpeed = movementSpeed - 0.2
			end
			
			self.Sprite.Rotation = self.Sprite.Rotation + rotationSpeed
			
			self.Speed = self.Speed * 0.98 + movementSpeed
		end;
		
		Move = function(self)
			x, y = self.Sprite.X, self.Sprite.Y
			angle = -math.rad(self.Sprite.Rotation)

			x = x + math.sin(angle) * self.Speed
			y = y + math.cos(angle) * self.Speed
			
			self.Sprite.X = clamp(x, 0, SCREEN_WIDTH)
			self.Sprite.Y = clamp(y, 0, SCREEN_HEIGHT)
		end
	}
	Player.Sprite.X, Player.Sprite.Y = SCREEN_WIDTH * 0.5, SCREEN_HEIGHT * 0.5
	Player.Sprite:SetImage("player.png")
	
	-- Set the UpdateIn method to nil (this only gets called once per script load)
	UpdateIn = nil
end

UpdateIn = Init
UpdateOut = nil

KEY_LEFT = 37
KEY_UP = 38
KEY_RIGHT = 39
KEY_DOWN = 40
KEY_ESCAPE = 27

-- Executes the method only if the conditions are met
function CondExec(cond, method, ...)
	if method == nil then
		method = cond
	end
	if cond and method then
		method(...)
	end
end

function UpdateAliens()
	local key
	local alien
	
	for key, alien in pairs(Aliens) do
		if not alien:Update() then
			-- Remove the alien from the Aliens table - you can clear fields in tables during
			-- traversal, but you should not add fields that do not already exist (changing existing
			-- values is fine).
			-- Please read the documentation for the next(table,[idx]) function for information about
			-- modifying tables during traversal: <a href="http://www.lua.org/manual/5.1/manual.html#pdf-next" target="_blank">http://www.lua.org/manual/5.1/manual.html#pdf-next</a>
			Aliens[key] = nil
		end
	end
end

function Update()
	CondExec(UpdateIn)
	
	-- I only added the 'Frozen' bit so I could take a picture of it
	if not Frozen then
		
		Player:Update()
	
		UpdateAliens()
		
	end
	
	CondExec(UpdateOut)
	
	return not KeyDown(KEY_ESCAPE)
end

function DrawScreen()
	
	local sprites = {}
	local key, alien
	
	for key, alien in pairs(Aliens) do
		table.insert(sprites, alien.Sprite)
	end
	
	table.insert(sprites, Player.Sprite)

	-- unpack essentially takes every value in a table and returns them
	for k,v in pairs(sprites) do
		v:Draw()
	end
	
end

</textarea> <br><br></td></tr></table><br>
<a name="973699"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GW</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Pretty darn cool! <br><br></td></tr></table><br>
<a name="973784"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Now thats an example! Thanks a lot Nilium!<br><br>Ill have to have a deeper look at the weekend... seems pretty full on just to get Lua to work with BlitzMax doing it your way... <br><br></td></tr></table><br>
<a name="973788"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >N</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Edit: Rewritten now that I have internet access from my Mac again.<br><br>The way I'd normally do this is I'll have all the rendering, sound, etc. - basically, the stuff that isn't really game logic - in the host application and the remaining game logic (e.g., UI, player movement, etc. - physics simulations would probably be part of the host application unless the physics stuff was really, really simple) will all be in Lua.  You could shift the line in either direction with more being in Lua or BlitzMax, it's a personal choice in this case.<br><br>My way, I feel I get a much more reusable body of code, since I don't have to tear out any of the game logic from the host application and can just start from scratch with a new set of scripts.  That way the back-end is already there and I just have to decide what the game does.  Essentially, the end result should be like an engine, and your game is just built on top of that.<br><br>Like I said though, this is just how I'd do it.  You're free to put as much code in the host/script as you want, since you're the only person who will probably know what you want in the first place. <br><br></td></tr></table><br>
<a name="1022865"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >EOF</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Bumping this one in light of Zekes recent Lua module release<br>Looks like there  is a some great learning material here <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
