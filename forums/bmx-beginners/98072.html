<!DOCTYPE html><html lang="en" ><head ><title >Level editor.</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Level editor.</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >Level editor.</a><br><br>
<a name="1143259"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Awesome</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey, I'm having an issue with some code for a level editor, everything worked fine until I tried to put in the ability for it to load a previously saved file. After that the array for drawing the tiles on the screen decided it didn't need to work anymore.<br><br>For Local y:Int = 0 To mapy-1<br>	For Local x:Int = 0 To mapx-1<br>		Local Backtile:Int = map[x,y,0]<br>		If Backtile &gt; -1 Then DrawImage img_tiles:TImage,(x*tsize)-scn_left,(y*tsize) - scn_top,Backtile<br>	Next<br>Next<br><br>It works if I take off the last "Backtile" and replace it with pretty much any number or other variable I've found, but then of course it draws only the tile associated with that number, or the drawn tile changes as the variable changes.<br><br>This is the full code: <br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
AppTitle="Level Editor"
Const  gwidth:Int = 1024
Const  gheight:Int = 768

Graphics gwidth, gheight

Const  tsize:Int = 32
Global mapx:Int 
Global mapy:Int 
Global mapz:Int 
Global scn_top:Int = (mapy*tsize)
Global numtiles:Int = 30
Global scn_left:Int = 0
Global CurrentTile:Int = 0
'AutoMidHandle(True)

Global img_tiles:TImage = LoadAnimImage("images\tiles.png",tsize,tsize,0,numtiles)

Local file:TStream = OpenFile("map1.txt",True,True)

If file &lt;&gt; Null Then 
	mapx = ReadInt(file)
	mapy = ReadInt(file)
	mapz = ReadInt(file)
Else 
	mapx = 75
	mapy = 50
	mapz = 4
EndIf 

Global map:Int[mapx,mapy,mapz]

If file &lt;&gt; Null Then 
	For Local x:Int = 0 To mapx-1
		For Local y:Int = 0 To mapy-1
			For Local z:Int = 0 To mapz-1
				map[x,y,z] = ReadInt(file)
			Next 
		Next
	Next
Else 
	For Local x:Int = 0 To mapx-1
		For Local y:Int = 0 To mapy-1
			For Local z:Int = 0 To mapz-1
				map[x,y,z] = -1
			Next 
		Next
	Next
EndIf 



Repeat
	
	Cls
	
	''''' Use WASD keys to move the screen '''''
	If KeyDown(key_down) And scn_top &lt; (mapy*tsize-gheight) Then scn_top = scn_top+32
	If KeyDown(key_right) And scn_left &lt; (mapx*tsize-gwidth) Then scn_left = scn_left+32
	If KeyDown(key_left) And scn_left &gt; 0 Then scn_left = scn_left-32
	If KeyDown(key_up) And scn_top &gt; 0 Then scn_top = scn_top-32
	If scn_top &gt; (mapy*tsize-gheight)-1 Then scn_top = (mapy*tsize-gheight)-1
	If scn_left &gt; (mapx*tsize-gwidth)-1 Then scn_left = (mapx*tsize-gwidth)-1
	''''''''''''''''''''''''''''''''''''''''''''
	
	'Local Layer:Int
		
	'If KeyHit(key_1) Then Layer=1
	'If KeyHit(key_2) Then Layer=2
	'If KeyHit(key_3) Then Layer=3
	'If KeyHit(key_4) Then Layer=4
		
	'Select Layer
	'	Case 1 'Foreground
	'	Case 2 'Collision
	'	Case 3 'Background
	'	Case 4
	'End Select	
	
	If MouseDown(1) And MouseX() &gt; 0 And (MouseX()+scn_left)/tsize &lt; mapx And MouseY() &gt; 0 And (MouseY()+scn_top)/tsize &lt; mapy Then map[(MouseX()+scn_left)/tsize,(MouseY()+scn_top)/tsize,0] = CurrentTile
	If MouseDown(2) And MouseX() &gt; 0 And (MouseX()+scn_left)/tsize &lt; mapx And MouseY() &gt; 0 And (MouseY()+scn_top)/tsize &lt; mapy Then map[(MouseX()+scn_left)/tsize,(MouseY()+scn_top)/tsize,0] = -1
	
	''''' Use mousewheel to change tile '''''
	If MouseZ() &gt; 0 Then CurrentTile = CurrentTile + 1 ; FlushMouse 
	If MouseZ() &lt; 0 Then CurrentTile = CurrentTile - 1 ; FlushMouse
	If MouseHit(3) Then CurrentTile = CurrentTile +1

	If CurrentTile &gt; numtiles-1 Then CurrentTile = 0
	If CurrentTile &lt; 0 Then CurrentTile = numtiles-1
	'''''''''''''''''''''''''''''''''''''''''

	''''' Press C to Clear the map '''''
	If KeyHit(key_c) Then 
		For Local x:Int = 0 To mapx-1
			For Local y:Int = 0 To mapy-1
				For Local z:Int = 0 To 1
					map[x,y,z] = -1
				Next 
			Next
		Next
	EndIf 
	''''''''''''''''''''''''''''''''''''
	
	''''' Press F to Fill the map '''''
	If KeyHit(key_f) Then 
		For Local x:Int = 0 To mapx-1
			For Local y:Int = 0 To mapy-1
				For Local z:Int = 0 To 1
					map[x,y,z] = CurrentTile
				Next 
			Next
		Next
	EndIf 
	''''''''''''''''''''''''''''''''''''
	
	''''' Press S to save the map '''''
	If KeyHit(key_s) Then 
		Local fileout:TStream = WriteFile("map1.txt")
		'write map dimensions
			WriteInt fileout,mapx
			WriteInt fileout,mapy
			WriteInt fileout,mapz
			WriteInt fileout,tsize

			For Local x:Int = 0 To mapx-1
				For Local y:Int = 0 To mapy-1
					For Local z:Int = 0 To mapz-1
						WriteInt fileout,map[x,y,z]
					Next 
				Next
			Next
	
		CloseFile fileout
	EndIf 
	
	'''''''''''''''''''''''''''''''''''
	
	''''''''''''''''''''''''''''''''''' Draw images '''''''''''''''''''''''''''''''''''
	
	For Local y:Int = 0 To mapy-1
		For Local x:Int = 0 To mapx-1
			Local Backtile:Int = map[x,y,0]
			If Backtile &gt; -1 Then DrawImage img_tiles:TImage,(x*tsize)-scn_left,(y*tsize) - scn_top,Backtile
		Next
	Next
	
	If CurrentTile &gt; -1 Then DrawImage (img_tiles:TImage,(MouseX()/tsize)*tsize,(MouseY()/tsize)*tsize,CurrentTile)
	
	Flip

Until AppTerminate() Or KeyHit(key_escape)

End </textarea> <br><br></td></tr></table><br>
<a name="1143267"></a>

<a name="1143268"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >col</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hiya,<br><br>When you're saving the file you're saving the 4 values before the map data :-<br><br><pre class=code>
WriteInt fileout,mapx
WriteInt fileout,mapy
WriteInt fileout,mapz
WriteInt fileout,tsize
</pre><br><br>When loading them back in you're only loading back in 3 before the map data :-<br><br><pre class=code>
If file &lt;&gt; Null Then 
	mapx = ReadInt(file)
	mapy = ReadInt(file)
	mapz = ReadInt(file)
	tsize = ReadInt(file) '      &lt;--------- This was missing
Else 
	mapx = 75
	mapy = 50
	mapz = 4
EndIf
</pre><br><br>Because you have <b>tsize</b> as <b>Const</b> BMax will probably complain about this, so either don't save it or make it a variable.<br>Also, you're not closing the file after loading the data :-<br><br><pre class=code>
If file &lt;&gt; Null Then 
	For Local x:Int = 0 To mapx-1
		For Local y:Int = 0 To mapy-1
			For Local z:Int = 0 To mapz-1
				map[x,y,z] = ReadInt(file)
			Next 
		Next
	Next
	CloseFile file '&lt;-------------- Don't forget to close
Else 
	For Local x:Int = 0 To mapx-1
		For Local y:Int = 0 To mapy-1
			For Local z:Int = 0 To mapz-1
				map[x,y,z] = -1
			Next 
		Next
	Next
EndIf
</pre><br><br>Have fun :D<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1143269"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Awesome</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Perfect! Thank you very much! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
