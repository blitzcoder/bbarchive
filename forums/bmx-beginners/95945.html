<!DOCTYPE html><html lang="en" ><head ><title >Beginners' Coding Challenge, Week #1</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Beginners' Coding Challenge, Week #1</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >Beginners' Coding Challenge, Week #1</a><br><br>
<a name="1108157"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nest</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> <i>One of my biggest challenges to programming is motivation. Therefore I propose a weekly coding challenge for beginners (although anyone welcome). Let's see if there's enough interest to make this a regular thing!</i><br><br>Weekly Beginners' Coding Challenge<br><br>Week #1<br><br>Create a game that involves the player avoiding falling objects. It can be as simple or complex as you wish. Share your code in your post, and links to any image files required.<br><br>Closing date 8th October, good luck! <br><br></td></tr></table><br>
<a name="1108195"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AltanilConard</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Heya Nest, nice initiative. I like these kind of competetions, so I'll put something together this week (not really a beginner though). <br>Curious how much interest there is; practicing is the best way to learn :) <br><br></td></tr></table><br>
<a name="1108245"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I wouldn't make such a challenge 'exclusive' for beginners though - even though you state that anyone is welcome. Especially beginners may need a few good examples from experienced coders. Also, some experienced coders may be app/tool coders, for instance using MaxGUI, and have not much experience with games. It's very hard to decide who's experienced or not..<br><br>+ as for closing date, you may want to specify a time zone ^_^ <br><br></td></tr></table><br>
<a name="1108251"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomShep</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Im a beginner so IM defo in. <br><br></td></tr></table><br>
<a name="1108262"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nest</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Anyone can have a go of course :) And there's no 'fixed' end date really - just if there is enough interest we'll move on to the next one in a week's time! I've made a start on mine - may go for the 'gameboy' style colours with low res graphics. <br><br></td></tr></table><br>
<a name="1108284"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AltanilConard</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I wanted to do a pseudo-3d like object dodger, but realize it's hard to implement 'falling' objects. I'll see where it goes... <br><br></td></tr></table><br>
<a name="1108465"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >craigmon</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> That sounds pretty interesting, and something I could do. <br><br></td></tr></table><br>
<a name="1108470"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt Vinyl</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I recall a similar thing back in the days of BlitzCoder (*sniff!*) and it was great fun. Was it run by Jayenkai (spelling?) Apologies if not!<br><br>It was indeed a great motivational 'thing'. Always interesting to see other peoples 'mini-games' and the employed code too - which is very handy for those starting out, as they can pick-about with it and see what effects are achieved by changing variables, etc. ;) <br><br></td></tr></table><br>
<a name="1108480"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kanati</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am most definitely not going to do it...  but I just had a mental image of a game like tetris.  Where you don't control the tetris pieces.  But are a character running around at the bottom avoiding the falling pieces.  You have to jump on the ones that have fallen and avoid the new ones appearing all the time.  Just like in tetris, when a line is created, the rest fall down.<br><br>It would need an AI tetris player that would keep the game going, and the player would need only survive the levels.<br><br>Odd premise, but I'd love to see someone do it.  :D <br><br></td></tr></table><br>
<a name="1108482"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt Vinyl</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> This was a little-known 'unique' game back in the 80's that I recall playing:<br><br>(Youtube Link)<br><br><a href="http://www.youtube.com/watch?v=Mp-eBYduG9U" target="_blank">http://www.youtube.com/watch?v=Mp-eBYduG9U</a><br><br>Might be an idea for someone to adapt? <br><br></td></tr></table><br>
<a name="1108486"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Twinprogrammer</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sounds neat. <br><br></td></tr></table><br>
<a name="1108561"></a>

<a name="1108567"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oh No!  The sky really is falling!<br>Help Chicken Little avoid all the falling objects.  Use the mouse to control his movement.<br><br>The source and resources are included in the download.  The .exe is compiled for Windows, but the source should compile on any platform.<br><br>Edit: Removed the link since the file is now out of date<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1108562"></a>

<a name="1108563"></a>

<a name="1108568"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> oops, just read that I should share the code directly in here.  So for those who don't want to download the .rar file above, here is the code and images<br><br>Images<br><img src="http://i194.photobucket.com/albums/z157/TomToad_bucket/ChickenLittle.png"><br><br>Code<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict
'Framework is used to pull only the modules I will be using in this program.  That will make the final code much smaller.
'These framework commands were generated by "frameWord Assistant"
Framework BRL.GLMax2D
Import BRL.RamStream
Import BRL.PNGLoader
Import BRL.Random

' modules which may be required:
' Import BRL.BMPLoader
' Import BRL.TGALoader
' Import BRL.JPGLoader

Incbin "Chicken Little.png"



SetGraphicsDriver GLMax2DDriver() 'Will be using OpenGL to make it portable to Linux and Mac

Const Gravity:Float = .15 'speed at which objects accelerate due to gravity
Const TerminalVelocity:Float = 20.0 'fastest an object will fall
Const ProgressionTime:Int = 2000 'determines how often the game speeds up


AutoMidHandle True 'midhandle all images

Global Pixmap:TPixmap = LoadPixmap("incbin::Chicken Little.png") 'This pixmap contains all the graphics. LoadAnimImage() will be used to split it to individual components
Local GrassTopImage:TImage = LoadAnimImage(Pixmap,64,64,4,1) 'Grass
Local GrassImage:TImage = LoadAnimImage(Pixmap,64,64,10,1)

Local Timer:Int'This will determine how fast the game progresses.  At every tick, the NextObjectTimer increases in speed
Local NextObjectTimer:Int  'The amount of time between objects falling.  this decreases as the game progresses
Local NextObject:Int 'when the next object actually falls
Local Hit:Int = False 'Flag for when Chicken Little is hit by an object


'Now for the Chicken Little info
Type TChicken
	Field x:Int 'Chicken Little's current position
	Field FaceRight:Int = False 'the direction the chicken is facing
	Field Lives:Int = 3 'Begin with 3 lives
	Field Score:Int 'Chicken Little's Score
	
	Field ChickenImage:TImage
		
	Method New()
		ChickenImage = LoadAnimImage(Pixmap,64,64,0,4)
	End Method
	
	'This method will draw chicken little and will also test for collision, returning true or false
	Method Draw:Int()
		Local TestResult:Int 'We will hold the result of the collision test here.
		
		Local MX:Int = MouseX() 'The current mouse position
		If MX &gt; x Then FaceRight = True
		If MX &lt; x Then FaceRight = False
		Local Frame:Int = MX &amp; 3 ' the frame being drawn for the chicken
		
		If FaceRight Then SetScale -1,1 'reverse the image if Chicken little moves to the right
		x = MX
		
		DrawImage ChickenImage,x,504,Frame 'draw the chicken
		If CollideImage(ChickenImage,x,504,Frame,1,0)
			TestResult = True
		Else
			TestResult = False
		End If
		
		SetScale 1,1 'make sure the scale is reset before returning from the method
		
		Return TestResult
	End Method
		
End Type

Local ChickenLittle:TChicken = New TChicken 'create the chicken type

'This is the type holding info on the sky objects
Type TSkyObject
	Global List:TList = CreateList() 'The list of all the objects falling to earth
	
	Global SkyImage:TImage[5] 'This holds the images that could fall from the sky

	
	Field x:Int, y:Float 'The position of the object
	Field Angle:Float, RotationSpeed:Float, RotationDirection:Float 'the angle, speed and direction the object spins on it's decent
	Field FallSpeed:Float 'the speed the object is falling
	Field Image:Int 'the image to draw
	
	Function Init()
		For Local i:Int = 0 To 4
			SkyImage[i] = LoadAnimImage(Pixmap,64,64,5+i,1) 'Here we load in each of the individual sky objects
		Next
	End Function

	'this function will create an object and add it to the list
	Function Create()
		Local SkyObject:TSkyObject = New TSkyObject 'create new object
		SkyObject.x = Rand(0,800) 'A random position in the sky
		SkyObject.y = -64 'Begin off the top of the screen
		SkyObject.Angle = Rnd(0,360) 'The angle of the object
		SkyObject.RotationSpeed = Rnd(0,5) 'how fast it spins
		SkyObject.RotationDirection = Rand(-1,1) 'the direction the object spins
		SkyObject.Image = Rand(0,4) 'The image displayed.
		
		List.AddLast(SkyObject) 'adds the object to the list
	End Function
	
	'this function will update the position of all the objects
	Function Update()
		For Local SkyObject:TSkyObject = EachIn List
			SkyObject.Angle :+ SkyObject.RotationSpeed * SkyObject.RotationDirection 'update the rotation
			If SkyObject.Angle &gt;= 360 Then SkyObject.Angle :- 360 'keep the angle within 0-360
			If SkyObject.Angle &lt; 0 Then SkyObject.Angle :+ 360
			
			SkyObject.FallSpeed :+ Gravity 'Increase the fall speed due to gravity
			If SkyObject.FallSpeed &gt; TerminalVelocity Then SkyObject.FallSpeed = TerminalVelocity 'Fastest it will fall
			
			SkyObject.y :+ SkyObject.FallSpeed 'move the object
			If SkyObject.y &gt; 664 Then List.Remove(SkyObject) 'if it falls below the screen, then remove from the list
		Next
	End Function
	
	'This function will draw the objects to the screen and add them to the collision layer
	Function Draw()
		ResetCollisions() 'reset the collision layer
		
		For Local SkyObject:TSkyObject = EachIn List
			SetRotation SkyObject.Angle 'set the rotation to the object
			
			DrawImage SkyImage[SkyObject.image],SkyObject.x,SkyObject.y 'draws the image to the screen
			
			CollideImage(SkyImage[SkyObject.Image],SkyObject.X,SkyObject.y,0,0,1) 'Adds to the collision layer
			SetRotation 0 'reset the rotation
		Next
	End Function
End Type

TSkyObject.Init()

Graphics 800,600,32 'set graphics mode
HideMouse

Repeat
	'we display a countdown to give the palyer time to get ready
	Drawmessage("Get Ready!",3000)
	DrawMessage("3",1000)
	DrawMessage("2",1000)
	DrawMessage("1",1000)
	
	NextObjectTimer = 1000 'set the initial values of the timers
	NextObject = MilliSecs() + NextObjectTimer
	Timer = MilliSecs()+ProgressionTime
	
	TSkyObject.List.Clear() 'Clear the list
	ChickenLittle.Score = 0 'Reset the score
	ChickenLittle.Lives = 3 'Reset the lives
	
	
	While Not KeyHit(KEY_ESCAPE) 'The main game loop
		Cls
		For Local i:Int = 0 To 13 'Let's Draw the grass
			DrawImage GrassImage,i*64,568
			DrawImage GrassTopImage,i*64,504
		Next
		
		TSkyObject.Update() 'Update all the objects
		TSkyObject.Draw() 'Draw all the objects
		
		Hit = ChickenLittle.Draw() 'Draw Chicken Little
		
		SetColor 128,128,128 'draw score and lives
		DrawRect 0,568,800,32 'draw the score background
		SetColor 255,255,255
		DrawText "SCORE: "+ChickenLittle.Score,10,578
		DrawText "LIVES: "+ChickenLittle.Lives,400,578
		Flip
		
		If Hit 'Was he hit?
			Delay(1000)
			ChickenLittle.Lives :- 1 'decrease the lives counter
			If ChickenLittle.Lives = 0 Then Exit 'If lives drops to 0, game over
			TSkyObject.List.Clear() 'Clear the object list
			NextObjectTimer = 1000 'reset the difficulty
			NextObject = MilliSecs() + NextObjectTimer 'reset the timer
		End If
		
		If MilliSecs() &gt;= Timer 'Time to progress teh game?
			Timer :+ ProgressionTime 'Next time the game gets harder
			NextObjectTimer :- 25 'subtract the delay between objects
			If NextObjectTimer &lt; 1 Then NextObjectTimer = 1 'Make sure the delay isn't less tahn 1 millisecs()
			ChickenLittle.Score :+ 1 'Increase thescore by 1
		End If
		
		If MilliSecs() &gt;= NextObject 'Time for another object?
			TSkyObject.Create() 'Create the new object
			NextObject :+ NextObjectTimer 'Add the time for the next object
		End If
	Wend
	
	'Game over
	Repeat
		DrawMessage("Game Over!~nYour Score Is "+ChickenLittle.Score+"~nPress ESC to Exit~nPress SPACE to try again",0)
		If KeyHit(KEY_ESCAPE) Then End 'end the program if the escape key is hit
	Until KeyHit(KEY_SPACE)
Forever
	

'This function will simply display a message centered on the screen for the specified time.
Function DrawMessage(Message:String,Time:Int)
	Local MessageArray:String[] = Message.Split("~n")
	Local Lines:Int = MessageArray.Length
	Local FontHeight:Int = TextHeight(Message) * 3 + 30
	Local Top:Int = (600-FontHeight*Lines)/2
	Time :+ MilliSecs()
	SetScale 3,3
	
	Repeat
		Cls
		
		For Local i:Int = 0 Until Lines
			DrawText MessageArray[i],(800-TextWidth(MessageArray[i])*3)/2,Top+i*FontHeight
		Next
		Flip
	Until MilliSecs() &gt;= Time
	SetScale 1,1
End Function

</textarea><br><br>Edit:  BTW, the timing is linked to the refresh rate.  If you don't have your graphics card refresh set to Application Controled, the game might run a bit fast.<br><br>Edit again:  Fixed a small bug and a small tweek<br><br><font class="tiny">Last edited 2011</font><br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1109270"></a>

<a name="1109271"></a>

<a name="1109272"></a>

<a name="1109273"></a>

<a name="1109274"></a>

<a name="1109276"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's one for ya :)  I've got a weekend off so I decided to knock this up today.<br><br>I'm not a beginner but it should be handy to learn from.  It shows:<br><br>Player control and animation<br>Simple physics<br>Collision detection using circles<br>Application flow (menu, game, pause etc)<br>Decent code layout (IMO)<br><br>Assets here:  <a href="http://www.matibee.co.uk/temp/popperz.zip" target="_blank">http://www.matibee.co.uk/temp/popperz.zip</a><br><br>Questions and comments always welcome.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict 

Graphics 800, 600, 32
HideMouse()
Delay( 800 )

'=========================================================================================================	
'
'	Load and initialise the global resources..
'		
AutoImageFlags( 0 )
AutoMidHandle( False )
SetClsColor( 210, 210, 210 )

Global deltaTime:Float
Global noise:TImage = LoadImage( "noise.png" )
Global ballImages:TImage[4]
For Local i:Int = 0 To 3
	ballImages[i] = LoadImage( "ball" + i + ".png" )
Next 

Global logoImage:TImage = LoadImage( "logo.png" )

Global fontImage:TImage = LoadAnimImage( "font.png", 12, 12, 0, 36 )

Global soundfx:TSound[4]
soundfx[0] = LoadSound( "fire.ogg" )
soundfx[1] = LoadSound( "pop.ogg" )
soundfx[2] = LoadSound( "newlevel.ogg" )
soundfx[3] = LoadSound( "dead.ogg" )

'=========================================================================================================	
' Main game loop
'
Global myGame:Game = game.Create()

While Not AppTerminate()
	Cls
		SetVirtualResolution 200, 150
		myGame.Draw()	
		SetColor( 0, 0, 0 )
		SetAlpha( 0.075 )
		SetVirtualResolution 800, 600
		For Local i:Int = 2 To 600 Step 4
			DrawLine( 0, i, 800, i )
		Next 
		SetAlpha( 0.01 )
		For Local i:Int = 2 To 800 Step 4
			DrawLine( i, 0, i, 600 )
		Next 
	Flip 1
	myGame.Update( deltaTime )
Wend 

'=========================================================================================================
Function MyDrawText( t$, x:Int, y:Int )
	SetColor( 109, 115, 147 )
	SetAlpha( 1 ) 
	Local s$ = t$.ToUpper()
	For Local i:Int = 0 To Len( s$ ) - 1
		Local char:Int = Asc( Mid$( s$, i + 1, 1 ) )
		If ( char &gt;= 65 And char &lt;= 90 )
			DrawImage( fontImage, x + i * 10, y, char - 65 )		
		Else If ( char &gt;= 48 And char &lt;= 57 )
			DrawImage( fontImage, x + i * 10, y, char - 48 + 26 )
		End If 
	Next 
End Function 

'=========================================================================================================
Type Vec2D
	Field x:Float
	Field y:Float
	
	Function Create:Vec2D( x:Float, y:Float )
		Local v:Vec2D = New Vec2D
		v.x = x
		v.y = y
		Return v
	End Function 
	
	Method Length:Float()
		Return Sqr( x * x + y * y )
	End Method
End Type 

'=========================================================================================================
Type Ball
	
	Global GRAVITY:Float = 0.075
	Global BOUNCE_VELOCITY:Float[] = [ -1.2, -1.85, -2.1, -2.7 ]
	
	Field position:Vec2D
	Field direction:Vec2D
	Field size:Int
	
	Field fallTime:Float 
	
	Function Create:Ball( x:Float, y:Float, size:Int, xspeed:Float )
		Local b:Ball = New Ball
		b.position = Vec2d.Create( x, y )
		b.direction = New Vec2D
		b.direction.x = xspeed
		b.size = size
		Return b
	End Function 
	
	Method Draw()
		DrawImage( ballImages[size - 1], position.x, position.y )
	End Method 
	
	Method Update( ftime:Float )
		If ( direction.y &lt; 0.001 And direction.y &gt; -0.001 )
			fallTime = 0.1
		End If 
		fallTime :+ ftime
		direction.y :+ GRAVITY * fallTime * fallTime
		position.y :+ direction.y
		If ( position.y &gt; (140 - size * 4) ) 
			direction.y = BOUNCE_VELOCITY[ size - 1 ]
			fallTime = 0.1
			position.y = 139.9 - size * 4
		End If 
		
		position.x :+ ftime * direction.x * size
		If ( position.x &gt;= 200 - 4 * size )
			direction.x :* -1
			position.x = 199.5 - 4 * size			
		End If 
		
		If ( position.x &lt; 0 )
			direction.x :* -1
			position.x = 0.1
		End If 
	
	End Method 
End Type 
'=========================================================================================================
Type Level
	Field balls:TList
	
	Function Create:Level( levelNumber:Int )
		Local l:Level = New Level

		RestoreData level_data
		Local data$
		ReadData data$
		For Local i:Int = 0 To levelNumber
			ReadData data$
		Next 
		
		l.balls = New TList
		
		SeedRnd( levelNumber )
		
		Local leveldata$[] = data$.split( ":" )
		For Local i:Int = 0 To leveldata$[0].ToInt() - 1
			Local speed:Float = Rand( 7, 14 )
			Local x:Float = leveldata$[2*i+1].ToFloat()
			If ( x &lt; 100 ) speed :* -1
			Local size:Int = leveldata$[2*i+2].ToInt()			
			l.balls.AddLast( Ball.Create( x, 0, size, speed ) )
		Next 

		Return l
	End Function 	
	
	Method Draw()
		SetColor( 255, 255, 255 )
		For Local b:Ball = EachIn balls
			b.Draw()
		Next 
	End Method 
	
	Method Update( ftime:Float )
		For Local b:Ball = EachIn balls
			b.Update( ftime )
		Next 
	End Method 
	
	Method PopBalls:Int( bulletList:TList )
		Local score:Int 
		For Local b:Ball = EachIn balls
			For Local p:Bullet = EachIn bulletList
				If ( p.CollidesCircle( b.position.x + (b.size * 0.5), b.position.y + (b.size * 0.5), b.size * 3 ) )
					If ( b.size &gt; 1 )
						balls.AddLast( Ball.Create( b.position.x, b.position.y, b.size - 1, b.direction.x * 1.3))
						balls.AddLast( Ball.Create( b.position.x, b.position.y, b.size - 1, b.direction.x * -1.3))
					End If 
					PlaySound( soundfx[1] )
					balls.Remove( b )
					p.Kill()
					score :+ 25 - (5 * b.size)
				End If 
			Next 
		Next 
		Return score
	End Method 
	
	Method BallsLeft:Int ()
		Return balls.Count()
	End Method 
	
	Method GetBallList:TList()
		Return balls
	End Method 

End Type 

'=========================================================================================================
Type Bullet
	Field position:Vec2D
	
	Function Create:Bullet( x:Float, y:Float )
		Local b:Bullet = New Bullet
		b.position = Vec2D.Create( x, y )
		Return b		
	End Function 
	
	Method CollidesCircle:Int( x:Float, y:Float, r:Float )
		Local v:Vec2D = Vec2D.Create( position.x - x, position.y - y)
		Return v.Length() &lt;= r
	End Method 
	
	Method IsOffScreen:Int ()
		Return position.y &lt; -3
	End Method 
	
	Method Update( ftime:Float )
		position.y :- 150 * ftime
	End Method 
	
	Method Draw()
		SetColor( 0, 0, 0 )
		DrawRect( position.x, position.y, 2, 2 )
	End Method 
	
	Method Kill()
		position.y = -10
	End Method 

End Type 

'=========================================================================================================
Type Player
	
	Field x:Float
	Field img:TImage
	Field frame:Int
	Field frameTimer:TTimer
	Field fireTimer:Float
	
	Field bulletList:TList
	
	Field STATE:Int 
	Const WALKING_LEFT:Int = 	1
	Const WALKING_RIGHT:Int = 	2
	Const AIMING:Int = 			4
	Const FIRING:Int = 			5
	Const DEAD:Int	= 			6
		
	Function Create:Player ()
		Local p:Player = New Player
		p.img = LoadAnimImage( "player.png", 28, 28, 0, 13 )
		p.State = AIMING
		p.x = 100
		p.frameTimer = CreateTimer( 8 )
		p.bulletList = New TList 
		Return p
	End Function 
	
	Method Update( ftime:Float )
		
		If ( KeyHit( KEY_SPACE ) And fireTimer &lt;= 0 )
			bulletList.AddLast( Bullet.Create( x + 14, 125 ) )
			PlaySound( soundfx[0] )
			fireTimer = 0.10
			STATE = FIRING
		End If 
		
		Select STATE
			Case WALKING_LEFT
				x :- 70 * ftime
				If ( x &lt; 0 ) x = 0
				frame = 7 + (TimerTicks( frameTimer ) Mod 5 )
				If ( Not KeyDown( KEY_LEFT ) ) STATE = AIMING
			Case WALKING_RIGHT
				x :+ 70 * ftime
				If ( x &gt; 180 ) x = 180
				frame = TimerTicks( frameTimer ) Mod 5
				If ( Not KeyDown( KEY_RIGHT ) ) STATE = AIMING
			Case AIMING
				frame = 5
				If ( KeyDown( KEY_LEFT ) )
					STATE = WALKING_LEFT
				Else If ( KeyDown( KEY_RIGHT ) )
					STATE = WALKING_RIGHT
				End If 				
			Case FIRING
				fireTimer :- ftime
				If ( fireTimer &lt;= 0 )
					STATE = AIMING
				End If 
				frame = 12
		End Select 	
		
		For Local b:Bullet = EachIn bulletList
			b.Update( ftime )
			If ( b.IsOffscreen() )
				bulletList.Remove( b )
			End If 
		Next 	
		
	End Method
	
	Method Draw()
		For Local b:Bullet = EachIn bulletList
			b.Draw()
		Next 
		If ( STATE = DEAD And ((MilliSecs() / 200) Mod 2) )
			Return 
		End If 			
		SetColor( 255, 255, 255 )
		DrawImage( img, x, 115, frame )
	End Method 
	
	Method GetBulletList:TList()
		Return bulletList
	End Method 
	
	Method CheckCollisions:Int( balls:TList )
		For Local b:Ball = EachIn balls
			Local v:Vec2D = Vec2D.Create( x + 14 - (b.position.x + b.size * 2), 127 - (b.position.y + b.size * 2) )
			If v.Length() &lt; ( 4.0 + (b.size * 2) )
				STATE = DEAD
				frame = 6
				Return True 
			End If 
		Next 
		Return False 
	End Method 
	
End Type 

'=========================================================================================================
Type Game

	Field STATE:Int
	
	Const GAME_MAINMENU:Int =	1
	Const GAME_PLAYING:Int = 	2
	Const GAME_OVER:Int = 		3
	Const GAME_PAUSED:Int = 		4
		
	Field currentplayer:Player
	Field levelNumber:Int
	Field currentLevel:Level 
	Field totalLevels:Int
	Field currentScore:Int
	Field highScore:Int

	Function Create:Game()
		deltaTime = 1 / Float( GraphicsHertz() )
		Local g:Game = New Game
		RestoreData level_data
		ReadData g.totalLevels
		g.currentplayer = Player.Create()
		g.NextLevel()
		g.STATE = GAME_MAINMENU
		Return g
	End Function 

	Method NextLevel()
		PlaySound( soundfx[2] )
		If ( levelNumber Mod totalLevels ) = 0
			deltatime :* 1.33
		End If 
		currentLevel = Level.Create( levelNumber Mod totalLevels )
		levelNumber :+ 1
	End Method 
	
	Method Draw()
		SetColor ( 255, 255, 255 )
		SetAlpha ( 1 )
		DrawRect ( 0, 142, 200, 3 )
		
		Select STATE 
		Case GAME_MAINMENU
			DrawImage( logoImage, 70, 20 )
			MyDrawText( "High Score: " + highScore, 2, 2 )
			MyDrawText ( "SPACE : NEW GAME ", 20, 90 )
			MyDrawText ( "  ESC : EXIT ", 20, 105 )		
		Case GAME_OVER
			currentplayer.Draw()
			currentLevel.Draw()
			SetColor( 255, 255, 255 )
			MyDrawText( "GAME OVER!", 54, 50 )
			MyDrawText( "PRESS ENTER", 46, 70 )
		Case GAME_PLAYING, GAME_PAUSED
			currentplayer.Draw()
			currentLevel.Draw()
			MyDrawText( "Score: " + currentScore, 2, 2 )
			MyDrawText( "Level: " + levelNumber, 2, 16 )
			If ( STATE = GAME_PAUSED )
				SetColor( 20, 20, 20 )
				SetAlpha( 0.3 )
				DrawRect ( 0, 0, 200, 150 )
				If ((MilliSecs() / 300) Mod 2 )
					MyDrawText( "PAUSED", 65, 60 )
				End If 
			End If 
		End Select 
		
		SetColor( 255, 255, 255 )
		SetBlend( ALPHABLEND )
		SetAlpha( 0.08 )
		DrawImage( noise, 0, 0 )
	End Method 
	
	Method Update( ftime:Float )
		Select STATE
		Case GAME_MAINMENU
			If ( KeyHit( KEY_ESCAPE ) )
				End 
			Else If ( KeyHit( KEY_SPACE ) )
				FlushKeys()
				STATE = GAME_PLAYING
			End If 
		Case GAME_OVER
			If ( KeyHit( KEY_ENTER ) )
				FlushKeys()
				Local hs:Int = myGame.highScore
				Local sc:Int = myGame.currentScore
				If ( hs &lt; sc ) 
					hs = sc
				End If 
				myGame:Game = game.Create()
				myGame.highScore = hs				
			End If 
		Case GAME_PLAYING 
			currentLevel.Update( ftime )
			currentPlayer.Update( ftime )
			currentScore :+ currentLevel.PopBalls( currentPlayer.GetBulletList() )
			If ( currentPlayer.CheckCollisions( currentLevel.GetBallList() ) )
				PlaySound( soundfx[3] )
				STATE = GAME_OVER
				FlushKeys()
			End If 
			If ( currentLevel.BallsLeft() = 0 Or KeyHit( KEY_G ) )
				NextLevel()
			End If 
			If ( KeyHit( KEY_ESCAPE ) )
				STATE = GAME_MAINMENU
				Local hs:Int = myGame.highScore
				myGame:Game = game.Create()
				myGame.highScore = hs
				FlushKeys()
			End If 
			If ( KeyHit( KEY_P ) )
				STATE = GAME_PAUSED
				FlushKeys()
			End If 
		Case GAME_PAUSED
			If ( KeyHit( KEY_P ) )
				STATE = GAME_PLAYING
				FlushKeys()
			End If 
		End Select  
	End Method 	
	
End Type 

#level_data
DefData 8
DefData "1:100.0:3"
DefData "2:40.0:3:150:4"
DefData "3:20.0:3:180:3:90:4"
DefData "3:20.0:4:180:4:90:4"
DefData "4:20.0:3:180:3:90:2:40:1"
DefData "4:20.0:2:180:3:90:1:40:1"
DefData "5:20.0:4:90:4:180:3:90:2:40:1"
DefData "5:20.0:3:100:4:180:3:90:2:40:1"
</textarea><br><br><font class="tiny">Last edited 2011</font><br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1109298"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> TomToad, why not make ChickenImage and the others globals, and load them once rather than each time, will be faster and use less memory. <br><br></td></tr></table><br>
<a name="1109299"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nest</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Two really nice examples! Here's my newbie basic effort:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics 800,600
SeedRnd MilliSecs()
HideMouse()
Global HighScore = 0
Global T
Global GameObjectList:TList=CreateList()
Global reset_game
#start
reset_game=0
Cls
DrawText "You are Colin, the sad snowman.",50,50
DrawText "You are trying to melt yourself away, but the heavy snow is building you higher!",50,80
DrawText "Your goal is to avoid the snow, preventing yourself from growing to the height of ",50,110
DrawText "the screen for as long as possible.",50,140
DrawText "Use the mouse to control Colin!",50,170
DrawText "PRESS A MOUSE BUTTON CONTINUE",300,200
DrawText "Current High Score = "+HighScore,300,300
Flip
WaitMouse
T = MilliSecs()
Player:TPlayer = TPlayer.Create()


' main game loop
Repeat
Cls
If Rand(10)=10 Then
	For i = 1 To Rand(5)
		flake:TSnowFlake = TSnowFlake.Create()
	Next
End If

For ob:TGameObject=EachIn GameObjectList
ob.drawself()
ob.updatestate()
Next
If reset_game=1 Then Goto start
DrawText (MilliSecs()-T)/1000,10,10
Flip
Until KeyHit(KEY_ESCAPE) Or AppTerminate()
' end of main game loop

Type TGameObject
	Field x,y,w,h
	Method updatestate() Abstract
	Method drawself()
		DrawRect x,y,w,h
	End Method
End Type

Type TPlayer Extends TGameObject
	Function Create:TPlayer()
		Local player:TPlayer = New TPlayer
		player.x=390
		player.y=500
		player.w=40
		player.h=80
		ListAddFirst GameObjectList, player
		Return player
	End Function

	Method updatestate()
		SetColor 0,0,0
		DrawOval x+3,y+3,15,15
		DrawOval x+w-17,y+3,15,15
		SetColor 255,255,255
		x=MouseX()
		If x&lt;0 Then x=0
		If x&gt;800-w Then x=800-w
		y=600-h
		For obj:TGameObject = EachIn GameObjectList
			If obj&lt;&gt;Self
				If RectsOverlap(x,y,w,h,obj.x,obj.y,obj.w,obj.h) Then
					h:+(obj.w*obj.h)/40
					ListRemove GameObjectList,obj
				End If
			End If
		Next
		If h&gt;600
		Cls
		DrawText "Game Over!",50,50
		DrawText "You Lasted for "+(MilliSecs()-T)/1000+" seconds!",50,100
		DrawText "PRESS A MOUSE BUTTON CONTINUE",300,200

		If (MilliSecs()-T)/1000 &gt; HighScore And T&gt;0 Then HighScore = (MilliSecs()-T)/1000
		Flip
		WaitMouse
		For obj:TGameObject = EachIn GameObjectList
			ListRemove GameObjectList,obj
		Next
		reset_game=1
		End If
	End Method

End Type

Type TSnowFlake Extends TGameObject
Field xs,ys

Function Create:TSnowFlake()
	Local flake:TSnowFlake = New TSnowFlake
	flake.x=Rand(0,800)
	flake.y=0
	flake.w=Rand(10,30)
	flake.h=Rand(10,30)
	flake.xs=0
	flake.ys=Rand(1,10)
	ListAddLast GameObjectList, flake
	Return flake
End Function

Method updatestate()
	x:+xs
	y:+ys
	If y&gt;600 Then ListRemove GameObjectList,Self
End Method

End Type

' (c) Assari 2006
Function RectsOverlap:Int(x0, y0, w0, h0, x2, y2, w2, h2)
    If x0 &gt; (x2 + w2) Or (x0 + w0) &lt; x2 Then Return False
    If y0 &gt; (y2 + h2) Or (y0 + h0) &lt; y2 Then Return False
    Return True
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1109302"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AltanilConard</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> Couldn't manage to implement falling objects, so guess I failed the challenge. Still a fun object dodger:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Import brl.glmax2d

SetGraphicsDriver GLMax2DDriver()

Const WIDTH = 640, HEIGHT = 480, HWIDTH = 320
Const Y_WORLD = -2000

Graphics WIDTH,HEIGHT

SetBlend ALPHABLEND

Type Entity
	Field x#, z#

	Method setPos(newX#, newZ#)
		x = newX
		z = newZ
	EndMethod
EndType

'=============================
' Pseudo-3d plane
'=============================
Type Plane3D		
	Field horzHeight = 240
	Field color[] = [50,200,50]	
	Field zmap#[]	
	Field strokeSize# = 4.0
	Field zScroll# = 0.0
	Field strokeCol = False
	
	Method planeZToScreenY(z) 
		Return (Y_WORLD / z) + (horzHeight+15)
	EndMethod	
	
	Method screenYToPLaneZ#(y)
		Return Y_WORLD / (y - horzHeight-15)
	EndMethod	
	
	Method depth#()
		Return zmap[horzHeight-1]
	EndMethod
	
	Method draw()
		SetColor color[0], color[1], color[2]
		DrawRect -WIDTH/2,  HEIGHT/2, WIDTH,  HEIGHT/2
		SetColor 255,255,255
	EndMethod
	
	Method scroll(amount#)
		zScroll :+ amount
		If zScroll &gt; strokeSize
			strokeCol = Not strokeCol
			zScroll = 0.0
		EndIf
	EndMethod
	
	Method drawStrokes()	
		SetColor 0,0,0
		SetAlpha 0.04
		Local skip = strokeCol	
		Local count# = zScroll
		For Local y = 1 Until horzHeight
			If count &gt; strokeSize Then 
				skip = Not skip
				count = 0.0
			EndIf
			count :+ zmap[y] - zmap[y-1]
			If Not skip Then DrawLine -WIDTH/2, HEIGHT - y, WIDTH, HEIGHT - y
		Next
		SetColor 255,255,255
		SetAlpha 1.0
	EndMethod
	
	Method New()
		zmap = New Float[horzHeight]
		For Local y = 0 Until horzHeight
			zmap[y] = Abs(screenYToPlaneZ(y))	
		Next
	EndMethod
EndType


'=============================
' GAME LOOP
'=============================
Const SPAWN_MODE_NORMAL = 1
Const SPAWN_MODE_SQUEEZE = 2
Const SPAWN_MODE_ROAD = 3

' load data
AutoMidHandle True
Local plane:Plane3D = New Plane3D
Local tri:TImage = CreateTriangle(80, 160, [0, 100, 0])
Local craftImg:TImage = CreateCraft()
SetImageHandle tri, ImageWidth(tri)/2, ImageHeight(tri)
Global skyCol#[] = [0.8, 1.0, 1.0, 1.0], fogCol#[] = [0.8, 1.0, 1.0, 0.4]

' holds entities on screen
Global entityList:TList = CreateList()

' current AI spawn mode
Global spawnMode = SPAWN_MODE_NORMAL

' speed - used for difficulty
Global speed# = 1.0

' timers
SeedRnd MilliSecs()
Local lastTime = MilliSecs()
Global spawnTimer = 0, modeTimer = 0, ftime# = 60.0/GraphicsHertz()

' game variables
Local xscroll# = 0.0, rotation# = 0.0
Local dead = False, count = 0, score# = 0.0, fps = 0
Global spawnRate# = 50.0, deltaTime = 0.0
Global roadCenter# = 0, roadSlope# = 0.0, roadWidth# = 50.0, maxSlope = 1.0

' get some calcs outside loop
Local shadowY = HEIGHT - 40 - ImageHeight(craftImg)/2
Local shadowX = -1*ImageWidth(craftImg)/2
Local depthInv# = 1.0/plane.depth()

' intro
SetOrigin WIDTH/2, 0
Cls
DrawText "Press [space] to start", -100, HEIGHT/2
Flip; WaitKey

While Not dead
	Cls		
	
	' draw sky
	VertGradientRect(0, 30, WIDTH, HEIGHT-plane.horzHeight+150, [0.0, 0.0, 0.0, 1.0], skyCol)	
	SetColor 0,0,0; DrawRect(0, 0, WIDTH, 30)
	
	' update timing
	Local newTime = MilliSecs()
	Local dtime = newTime - lastTime
	lastTime = newTime	
	spawnTimer :+ dtime
	modeTimer :+ dtime
	score :+ Float(dtime)/1000.0
	fps = 1/(dtime/1000.0)
	
	' handle input
	If KeyDown(KEY_RIGHT) 
		xscroll = -300.0 * ftime
		rotation = 20.0
	ElseIf KeyDown(KEY_LEFT) 
		xscroll = 300.0
		rotation = -20.0
	Else
		xscroll = 0.0
		rotation = 0.0
	EndIf
	
	' spawn entities
	If modeTimer &gt; 0
		Select spawnMode
		Case SPAWN_MODE_NORMAL
			If spawnTimer &gt; spawnRate And Rnd(0, 100) &gt; 10
				spawnEnt(Rnd(-HWIDTH, HWIDTH), plane.depth())
				spawnTimer = 0
			EndIf
			If modeTimer &gt; 15000
				spawnMode = SPAWN_MODE_SQUEEZE
				modeTimer = -1000
				count = HWIDTH
			EndIf
		Case SPAWN_MODE_SQUEEZE
			If(count &lt;= roadWidth+5) Then 
				spawnMode = SPAWN_MODE_ROAD
				modeTimer = 0; spawnTimer = 0; roadSlope = 0
			EndIf
			roadCenter :+ xscroll * depthInv
			If spawnTimer &gt; 50
				spawnTimer = 0; count :- 5
				spawnEnt(roadCenter - count, plane.depth()); spawnEnt(roadCenter + count,plane.depth())
			EndIf
		Case SPAWN_MODE_ROAD
			roadCenter :+ xscroll * depthInv + roadSlope
			count :+ dtime
			If spawnTimer &gt; 100
				spawnTimer = 0
				spawnEnt(roadCenter - roadWidth, plane.depth()); spawnEnt(roadCenter + roadWidth, plane.depth())
			EndIf
			If modeTimer &gt; 1000 Then roadSlope = maxSlope
			If modeTimer &gt; 6000 Then roadSlope = -maxSlope
			If modeTimer &gt; 11000 Then roadSlope = 0.0 
			If modeTimer &gt; 13000 Then NextStage()
		EndSelect
	EndIf
	
	' draw plane
	plane.draw()	
	plane.drawStrokes()		
	plane.scroll(speed)	
	VertGradientRect(0, plane.horzHeight, WIDTH, 80, fogCol, [1.0,1.0,1.0,0.0])
	
	' update entities
	For Local e:Entity = EachIn entityList	
		' update position	
		e.x :+ (e.x + xscroll) * (1.0/e.z)	*ftime
		e.z :- 1.0*ftime

		' see if it kills player
		If e.z &lt; 10.0 And e.x &gt; -ImageWidth(craftImg)+20 And e.x &lt; ImageWidth(craftImg)-20
			dead = True
			Exit
		EndIf

		' draw if still alive
		If e.z &lt;= 0
			ListRemove entityList, e
		Else	
			' distance scaling factor
			Local scalf# = 1.0/e.z * 23			
			SetScale scalf, scalf
			DrawImage tri, e.x, HEIGHT - plane.planeZToScreenY(e.z)
		EndIf
	Next
	RestoreGfx()

	' draw player
	SetRotation rotation
	DrawImage(craftImg, 0, HEIGHT - 170)
	SetColor 0,0,0
	SetAlpha 0.1; SetRotation 0.0
	DrawOval(shadowX+Abs(rotation)/2, shadowY, ImageWidth(craftImg)-Abs(rotation), ImageHeight(craftImg))
	RestoreGfx()	
	DrawText "Score: " + Int(score*10), 0-HWIDTH + 10, 10
	If fps &gt; GraphicsHertz()-5 DrawText "FPS: "+GraphicsHertz(), HWIDTH - 100, 10 Else DrawText "FPS: " + Int (fps), HWIDTH - 100, 10	
	
	Flip
Wend

SetColor 255,20,20; SetAlpha 0.4
DrawRect -HWIDTH, 0, WIDTH, HEIGHT
RestoreGfx()
SetScale 4.0,4.0
DrawText "GAME OVER", -130, HEIGHT/2-40
SetScale 2.0,2.0
DrawText "score: " + Int(score*10), -70, HEIGHT/2 +100
Flip
WaitKey()

Function NextStage()
	speed :* 1.3
	spawnRate :* 0.75
	roadWidth = Max(roadWidth * 0.9, 10)
	roadSlope = Max(roadSlope*1.3, 3)
	modeTimer = -2000
	skyCol = [skyCol[0]-0.1, skyCol[1]-0.08, skyCol[2]-0.01, skyCol[3]]
	fogCol = [fogCol[0]-0.1, fogCol[1]-0.08, fogCol[2]-0.01, fogCol[3]]
	spawnMode = SPAWN_MODE_NORMAL
EndFunction

Function spawnEnt(x, y)
	Local e:Entity = New Entity
	e.setPos(x, y)
	entityList.AddFirst(e)
EndFunction	


'=============================
' GRAPHICS
'=============================
Function RestoreGfx()
	SetColor 255,255,255
	SetRotation 0.0
	SetAlpha 1.0
	SetScale 1.0, 1.0
EndFunction

Function CreateTriangle:TImage(w#, h#, col[])
	Cls
	SetColor col[0], col[1], col[2]
	DrawPoly([0.0, h, w, h, w/2.0, 0.0])
	Local ret:TImage = CreateImage(w, h, 1, DYNAMICIMAGE|MASKEDIMAGE)
	GrabImage(ret, 0, 0)
	Return ret 
EndFunction

Function CreateCraft:TImage()
	Cls
	Local w#=150.0, h#=40.0, hw#=w/2.0, y#=h
	SetColor 160,160,160
	DrawPoly([hw-30, y, hw-30, y-30, hw, y-30, hw, y]) 'center
	DrawPoly([0.0, y-12.5, 0.0, y-17.5, hw-30, y-30, hw-30, y]) 'wing
	SetColor 120,120,140
	DrawPoly([hw-30, y-30, hw, y-30, hw, 0.0, hw-5, 0.0]) 'point
	SetColor 200,150,150; SetAlpha 0.5
	DrawPoly([hw-5, y, hw, y, hw, y-30, hw-5, y-30]) 'low stripe
	DrawPoly([hw-3, y-30, hw, y-30, hw, 0.0, hw-0.5, 0.0]) 'point stripe
	
	' mirror in y	
	RestoreGfx()
	Local ret:TImage = CreateImage(w, h, 1, DYNAMICIMAGE|MASKEDIMAGE)
	GrabImage(ret, 0, 0)
	SetScale -1.0, 1.0; DrawImage ret, w/2, h/2
	SetScale 1.0, 1.0; GrabImage ret, 0, 0
	Return ret 
EndFunction

Function VertGradientRect(x, y, w, h, col1#[], col2#[])
	glDisable(GL_TEXTURE_2D)
	glBegin(GL_QUADS)
		glColor4f(col1[0], col1[1], col1[2], col1[3])
		glVertex2f(x, y)
		glVertex2f(x+w, y)
		glColor4f(col2[0], col2[1], col2[2], col2[3])
		glVertex2f(x+w, y+h)
		glVertex2f(x, y+h)
	glEnd()
	RestoreGfx()
EndFunction
</textarea><br>Could use lots more work, but getting the aspect right was harder then I thought. Nice examples above :) I especially like the snow man concept, very original <br><br></td></tr></table><br>
<a name="1109315"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nest</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> That's awesome, reminds me of the old Return of the Jedi arcade game! <a href="http://www.youtube.com/watch?v=uX31VFMuNLM" target="_blank">http://www.youtube.com/watch?v=uX31VFMuNLM</a> <br><br></td></tr></table><br>
<a name="1109364"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nest</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> Would anybody like to suggest an idea for Week #2 challenge? :) <br><br></td></tr></table><br>
<a name="1109378"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Czar Flavious: If you look closely, I don't load the graphics each time. All my graphics are inside of one single image, and instead of loading it off the HD and breaking into it's individual components for each object, I just load it into a pixmap and load it from there.  Not a big of a deal with such a small sample of graphics, but if you have several animated objects, it can save a lot of time over loading each one from the HD individually. <br><br></td></tr></table><br>
<a name="1109513"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> How about "Chain reaction" for a week #2 challenge? <br><br></td></tr></table><br>
<a name="1109522"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hotshot2005</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> Or the Smallest Game or Demo code challenge for Week #3 Challenge :) <br><br></td></tr></table><br>
<a name="1109589"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#23">[#23]</a></td></tr></table></td></tr><tr ><td class="posttext"> That'd be like the 256-char challenges from a few years ago. <br><br></td></tr></table><br>
<a name="1109595"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#24">[#24]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Or the Smallest Game or Demo code challenge for Week #3 Challenge :)  <br></div><br><div class="quote"> That'd be like the 256-char challenges from a few years ago.  <br></div><br><br>Yup.  And I don't think they're suitable here because a) they're not suitable for beginners, and b) the fugly nature of the code makes them pointless to learn from. <br><br></td></tr></table><br>
<a name="1109608"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AltanilConard</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#25">[#25]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>Yup. And I don't think they're suitable here because a) they're not suitable for beginners, and b) the fugly nature of the code makes them pointless to learn from. <br></div><br>YEP<br><div class="quote"> <br>How about "Chain reaction" for a week #2 challenge? <br></div><br>I can see that one work, gets my vote <br><br></td></tr></table><br>
<a name="1109612"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nest</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#26">[#26]</a></td></tr></table></td></tr><tr ><td class="posttext"> Right! New thread starting! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
