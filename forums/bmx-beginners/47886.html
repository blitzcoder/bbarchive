<!DOCTYPE html><html lang="en" ><head ><title >Pool Game</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Pool Game</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >Pool Game</a><br><br>
<a name="532273"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Qweeg</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi - complete newbie to Blitzmax, but I have worked with DarkBasic Pro (apologies for mentioning this in the blitz forum).  I am looking at writing a pool / snooker game and am a little stuck.  I have got a basic collision and motion routine working, so I can fire the cueball into the pack and send the balls bouncing around the table.<br><br>Only problem is the balls bounce off the pockets too, instead of getting potted.  Also the standard bounce angles are fine, but I need to consider the different bounce when a ball hits very close to the pocket.<br><br>currently I am just using an bmp image for the table so will need to use some form of calculation to work out whether the ball is pocketed or needs a different rebound calculation, but am not sure of the best way forward on this. <br><br>Anyway any help would be much appreciated.<br>Thanks, Tim<br><br><pre class=code>
Strict

'Declare a user defined type to hold information about the balls
Type typBall

	Field varXPos : Float
	Field varYPos : Float
	Field varXVel : Float
	Field varYVel : Float
	
EndType

'Delcare constants the number of balls, the ball radius and the wall positions
Const intNumberOfBalls : Int = 16
Const intBallRadius : Int = 10

Rem
Const intWallLeft : Int = 10
Const intWallTop : Int = 10
Const intWallRight : Int = 630
Const intWallBottom : Int = 470
EndRem

Const intWallLeft : Int = 40
Const intWallTop : Int = 40
Const intWallRight : Int = 984
Const intWallBottom : Int = 472


'Now some constants for the motion calculation
Const IntNumberOfSteps : Int = 30
Const varTableFriction : Float = 0.025
Const varWallDecay : Float = 1.0

'An array to hold the ball information using the new type declared above
Global arrBalls : typBall[intNumberOfBalls]
For Local i = 0 Until intNumberOfBalls
    arrBalls[i] = New typBall
Next

'Variables for taking a shot
Local varShotAngle : Float
Local varShotPower : Float
Local intMouseHold : Int
Local varMouseHoldX : Float
Local varMouseHoldY : Float

'General variables to do with the angles and velocities of the balls.
Local varDistance : Float
Local varAngleBetween : Float
Local varAngle1 : Float
Local varAngle2 : Float
Local varVel1 : Float
Local varVel2 : Float
Local varVelX1 : Float
Local varVelX2 : Float
Local varVelY1 : Float
Local varVelY2 : Float
Local varTotalVel : Float


'Load up the table
'Incbin "snookertable.png"
'Global imgTable = LoadImage ("incbin:: snookertable.png")




'Next call function to position the balls
_place_balls()


'Graphics 640,480,0
Graphics 1024,768,0
SetBlend solidblend

Local imgTable = LoadImage("TableSmall.bmp")
SetBlend alphablend
Local imgBlackBall = LoadImage("snookerball.png")


Repeat
SetColor 255,255,255
DrawImage imgTable,0,0
'DrawImage imgBlackBall,50,50


	'SetColor 255,255,255
	'DrawRect intWallLeft - 1, intWallTop - 1, intWallRight - intWallLeft + 2, intWallBottom - intWallTop + 2
	'SetColor 0,0,0
	'DrawRect intWallLeft, intWallTop, intWallRight - intWallLeft, intWallBottom - intWallTop



	'loop through the main calculation for the ball motion - more steps greater accuracy
	For Local i = 1 To intNumberOfSteps
	
		'loop through each of the balls
		For Local j = 0 Until intNumberOfBalls
		
			If arrBalls[j].varXVel &lt;&gt; 0.0 Then arrBalls[j].varXPos :+ arrBalls[j].varXVel / intNumberOfSteps
			If arrBalls[j].varYVel &lt;&gt; 0.0 Then arrBalls[j].varYPos :+ arrBalls[j].varYVel / intNumberOfSteps
			
		
			'Control the shooting of the ball
			If MouseDown(1)
				If intMouseHold = 0
					intMouseHold = 1
					varMouseHoldX = MouseX()
					varMouseHoldY = MouseY()
				Else
					varShotPower = Sqr((varMouseHoldX - MouseX())^2 + (varMouseHoldY - MouseY())^2) * 0.2
					If varShotPower &gt; 100.0 Then varShotPower = 100.0
					varShotAngle = 360-(180 - ATan2(varMouseHoldX - MouseX(), varMouseHoldY - MouseY()))
				EndIf
			Else
				If Not MouseDown(1) And intMouseHold = 1
					intMouseHold = 0
					arrBalls[0].varXVel = Sin(varShotAngle) * varShotPower
					arrBalls[0].varYVel = Cos(varShotAngle) * varShotPower
				EndIf
			EndIf

		
			'Replace the balls If space is pressed
			If KeyHit(32)
				_place_balls()
			EndIf


			'Control balls' collision with walls (rebounding and energy loss)
			If arrBalls[j].varXPos - intBallRadius &lt; intWallLeft
				arrBalls[j].varXPos = intWallLeft + intBallRadius
				arrBalls[j].varXVel = -arrBalls[j].varXVel * varWallDecay
			EndIf
		
			If arrBalls[j].varXPos + intBallRadius &gt; intWallRight
				arrBalls[j].varXPos = intWallRight - intBallRadius
				arrBalls[j].varXVel = -arrBalls[j].varXVel * varWallDecay
			EndIf
		
			If arrBalls[j].varYPos - intBallRadius &lt; intWallTop
				arrBalls[j].varYPos = intWallTop + intBallRadius
				arrBalls[j].varYVel = -arrBalls[j].varYVel * varWallDecay
			EndIf
	
			If arrBalls[j].varYPos + intBallRadius &gt; intWallBottom
				arrBalls[j].varYPos = intWallBottom - intBallRadius
				arrBalls[j].varYVel = -arrBalls[j].varYVel * varWallDecay
			EndIf
			
			
			'Loop through all balls
			For Local k = 0 Until intNumberOfBalls

								
				'Don't check collision between the same ball
				If j &lt;&gt; k
				
					'Get distance of balls from each other
					varDistance = Sqr((arrBalls[j].varXPos - arrBalls[k].varXPos)^2 + (arrBalls[j].varYPos - arrBalls[k].varYPos)^2)
					
					'If they are touching
					If varDistance &lt; intBallRadius * 2
						'Get the angle between them
						varAngleBetween = ATan2(arrBalls[j].varXPos - arrBalls[k].varXPos,arrBalls[j].varYPos - arrBalls[k].varYPos)

						'Move the first ball back where it was
						If arrBalls[j].varXVel &lt;&gt; 0 Then arrBalls[j].varXPos :- arrBalls[j].varXVel / intNumberOfSteps
						If arrBalls[j].varYVel &lt;&gt; 0 Then arrBalls[j].varYPos :- arrBalls[j].varYVel / intNumberOfSteps
	
						'Get the angles of the balls velocities
						varAngle1 = ATan2(arrBalls[j].varXVel,arrBalls[j].varYVel)
						varAngle2 = ATan2(arrBalls[k].varXVel,arrBalls[k].varYVel)
	
						'Get the speed of the balls velocities
						varVel1 = Sqr(arrBalls[j].varXVel^2 + arrBalls[j].varYVel^2) * 0.9
						varVel2 = Sqr(arrBalls[k].varXVel^2 + arrBalls[k].varYVel^2) * 0.9

						'Work out the New velocities of the balls using trig
						varVelX1 = Sin(varAngleBetween + 90) * Sin(360 - (varAngleBetween - varAngle1)) * varVel1 + Sin(varAngleBetween) * Cos(varAngleBetween - varAngle2) * varVel2
						varVelY1 = Cos(varAngleBetween + 90) * Sin(360 - (varAngleBetween - varAngle1)) * varVel1 + Cos(varAngleBetween) * Cos(varAngleBetween - varAngle2) * varVel2
						varVelX2 = Sin(varAngleBetween) * Cos(varAngleBetween - varAngle1) * varVel1 + Sin(varAngleBetween + 90) * Sin(360 - (varAngleBetween - varAngle2)) * varVel2
						varVelY2 = Cos(varAngleBetween) * Cos(varAngleBetween - varAngle1) * varVel1 + Cos(varAngleBetween + 90) * Sin(360 - (varAngleBetween - varAngle2)) * varVel2
						
					
						'Set the balls' velocities
						arrBalls[j].varXVel = varVelX1
						arrBalls[j].varYVel = varVelY1
						arrBalls[k].varXVel = varVelX2
						arrBalls[k].varYVel = varVelY2
						
						
						
					EndIf
				EndIf
			Next
			
			
			'If the loop is on the last step...			
			If i = intNumberOfSteps
				'Draw Graphics
				If j = 0
					SetColor 255,255,255	
				Else
					SetColor 255,0,0	
				EndIf
				
				DrawOval arrBalls[j].varXPos - intBallRadius, arrBalls[j].varYPos - intBallRadius, intBallRadius * 2, intBallRadius * 2

				If MouseDown(1) Then DrawLine arrBalls[0].varXPos, arrBalls[0].varYPos, arrBalls[0].varXPos + Sin(varShotAngle) * varShotPower * 3, arrBalls[0].varYPos + Cos(varShotAngle) * varShotPower * 3
	
				'Control ball friction
				varTotalVel = Sqr(arrBalls[j].varXVel^2 + arrBalls[j].varXVel^2)
				If arrBalls[j].varXVel &lt;&gt; 0.0 Then arrBalls[j].varXVel :- varTableFriction * varTotalVel * (arrBalls[j].varXVel / varTotalVel)
				If arrBalls[j].varYVel &lt;&gt; 0.0 Then arrBalls[j].varYVel :- varTableFriction * varTotalVel * (arrBalls[j].varYVel / varTotalVel)
			EndIf			
			
	
		Next
		
	Next
	
	
	Flip
	Cls
	
	
Until KeyHit(key_escape) = 1


End


Function _place_balls()

	'Replace the balls

	RestoreData ballPositions
	For Local p = 0 Until intNumberOfBalls

		ReadData arrBalls[p].varXPos
		ReadData arrBalls[p].varYPos
		arrBalls[p].varXVel = 0.0
		arrBalls[p].varYVel = 0.0
	
	Next
	

EndFunction

'Position data For balls
#ballPositions
DefData 100,240
DefData 400,240
DefData 421,230,421,250
DefData 442,220,442,240,442,260
DefData 463,210,463,230,463,250,463,270
DefData 484,200,484,220,484,240,484,260,484,280
DefData 505,190,505,210,505,230,505,250,505,270,505,290
</pre> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
