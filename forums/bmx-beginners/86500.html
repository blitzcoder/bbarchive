<!DOCTYPE html><html lang="en" ><head ><title >Method sort breaks removelist?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Method sort breaks removelist?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >Method sort breaks removelist?</a><br><br>
<a name="980679"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >coffeedotbean</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Took me an hour to work out what was wrong, I added some sort code to sort my TList and suddenly listremove no longer works.<br><br>Commenting out the Sort.List() call does not work I have to comment out the whole method block.<br><br>Broken code (added method &amp; list.sort()) list is sorted<br><br><pre class=code>
Graphics 640,480,0,0
SeedRnd MilliSecs()

Global LBobs:TList=New TList
Type TBobs
	Field x,y
	
	Method Compare:Int(Other:Object)
		If y &lt; TBobs(Other).y Then Return 1 Else Return -1
	End Method
	
End Type

For x = 0 To 999 ; make() ; Next

Repeat ; Delay 5 ; Cls
	
	LBobs.sort()
	
	For b:TBobs = EachIn LBobs
		DrawRect b.x,b.y,10,10 		
	Next
	
	For b:TBobs = EachIn LBobs
		If MouseX()&gt;b.x And MouseX()&lt;b.x+10 And MouseY()&gt;b.y And MouseY()&lt;b.y+10 Then ListRemove(LBobs,b) 		
	Next
	
	DrawText CountList(LBobs),10,10
	
Flip ; Until KeyDown(KEY_ESCAPE) ; End 

Function make()
	
	b:TBobs = New TBobs
	ListAddLast LBobs , b
		b.x	= Rand(0,640)
		b.y	= Rand(0,480)
	
End Function
</pre><br><br>working code (without method) but list not sorted<br><br><pre class=code>
Graphics 640,480,0,0
SeedRnd MilliSecs()

Global LBobs:TList=New TList
Type TBobs
	Field x,y
End Type

For x = 0 To 999 ; make() ; Next

Repeat ; Delay 5 ; Cls
	
	'LBobs.sort()
	
	For b:TBobs = EachIn LBobs
		DrawRect b.x,b.y,10,10 		
	Next
	
	For b:TBobs = EachIn LBobs
		If MouseX()&gt;b.x And MouseX()&lt;b.x+10 And MouseY()&gt;b.y And MouseY()&lt;b.y+10 Then ListRemove(LBobs,b) 		
	Next
	
	DrawText CountList(LBobs),10,10
	
Flip ; Until KeyDown(KEY_ESCAPE) ; End 

Function make()
	
	b:TBobs = New TBobs
	ListAddLast LBobs , b
		b.x	= Rand(0,640)
		b.y	= Rand(0,480)
	
End Function
</pre><br><br>I really cannot get my noddle around why it breaks removelist. <br><br></td></tr></table><br>
<a name="980700"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Interesting...<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Graphics 640,480,0,0
SeedRnd MilliSecs()

Global LBobs:TList=New TList
Type TBobs
	Field x:Int,y:Int
	Field link:TLink
	Field color:Int[]=[255,255,255]
	
	Method Compare:Int(Other:Object)

		If other=Null Return 0

		If y &lt; TBobs(Other).y Then Return 1 Else Return -1
	End Method
	
End Type

For Local aa:Int = 0 To 99 ; make() ; Next

Repeat 
	Cls
	
	'LBobs.sort()
	
	For Local ab:TBobs = EachIn LBobs
		'SetColor b.color[0],b.color[1],b.color[2]
		DrawRect ab.x,ab.y,10,10 		
		'SetColor 255,255,255
	Next
	
	For Local b:TBobs = EachIn LBobs
		If MouseX()&gt;b.x And MouseX()&lt;b.x+10 And MouseY()&gt;b.y And MouseY()&lt;b.y+10 And MouseDown(1)=True
			
			'b.color=[255,0,0]
			b.link.remove()
			'ListRemove(lbobs,b)
				
			
			lbobs.sort()
			
			
		End If
	Next
	
	DrawText CountList(LBobs),10,10
	
Flip 

Until KeyDown(KEY_ESCAPE) ; End 

Function make()
	
	Local b:TBobs = New TBobs
		b.link=lbobs.addlast(b)
		b.x	= Rand(0,640)
		b.y	= Rand(0,480)
End Function
</textarea><br>I changed your code to manage LINK and its method Remove to detach it from the list. It seems to work.<br>I dont' know why with ListRemove command it doesnt' work..<br><br>edit:<br><br>This is the original source code from brl.LinkedList - the method FindLink is used by ListRemove.<br><pre class=code>
Rem
	bbdoc: Returns the first link in the list with the given value, or null if none found.
	End Rem
	Method FindLink:TLink( value:Object )
		Local link:TLink=_head._succ
		While link&lt;&gt;_head
			If link._value.Compare( value )=0 Return link
			link=link._succ
		Wend
	End Method
</pre><br>It seems that your Compare Method 'override' the default behaviour: there'll be never a '0' value, so no link, no remove.<br>So the solution - if you want to use ListRemove is to change your method<br><br><pre class=code>
	Method Compare:Int(Other:Object)

		If other=Null Return 0

		If y=tbobs(other).y Return 0
		If y &lt; TBobs(Other).y Then Return 1 Else Return -1
	End Method
</pre> <br><br></td></tr></table><br>
<a name="980712"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >coffeedotbean</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> WOW, thanks degac.<br><br>Still so much to learn about lists. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
