<!DOCTYPE html><html lang="en" ><head ><title >first project.. verlet physics</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >first project.. verlet physics</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >first project.. verlet physics</a><br><br>
<a name="941100"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi I am doing a small verlet physics engine as my first project in Bmax to get me used to doing types and lists and just about every part of blitz max that I dont really understand fully.. well now I think I understand it. :)<br><br>I wrote the following code in about 30 minutes after buying bmax but I have a few questions and weird happenings...<br><br>controls.. spacebar = add object<br>arrow keys to control verlet #2<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics 800,600,0,60
AppTitle = "Verlet PhysX"


Global Verletlist:TList = New TList 'Verlet List
Global ConstraintList:TList = New TList 'Constraint List

Const Friction:Float = .985		'Friction should be close to 1 unless you want very slow movement
Const Gravity:Float = .1			'Gravity
Const Waterlevel:Int = 400		'Waterlevel in pixels
Const Waterdensity:Float = 10		'The depth at wich boyancy maxes out
Const BoyancyForce:Float = .1		'Best if close to gravity
Const WaterFriction:Float = .01	'Needs to be really low for a very subtle effect
Const ConstraintIterations:Int = 10	'constraint iterations



v1:verlet = Verlet.Create(100,100,21,10,1)' x, y, size, mass, ID, ox offset, oy offset
v2:verlet = Verlet.Create(200,150,11,1,1)' x, y, size, mass, ID, ox offset, oy offset
v3:verlet = Verlet.Create(200,100,11,1,1)' x, y, size, mass, ID, ox offset, oy offset
v4:verlet = Verlet.Create(100,150,21,10,1)' x, y, size, mass, ID, ox offset, oy offset

Constraint.Create(v1:verlet,v2:verlet)	'Verlet1 and verlet2
Constraint.Create(v1:verlet,v3:verlet)
Constraint.Create(v3:verlet,v2:verlet)
Constraint.Create(v1:verlet,v4:verlet)
Constraint.Create(v4:verlet,v2:verlet)

v5:verlet = Verlet.Create(300,100,21,10,2)' x, y, size, mass, ID, ox offset, oy offset
v6:verlet = Verlet.Create(400,150,11,1,2)' x, y, size, mass, ID, ox offset, oy offset
v7:verlet = Verlet.Create(400,100,11,1,2)' x, y, size, mass, ID, ox offset, oy offset
v8:verlet = Verlet.Create(300,150,21,10,2)' x, y, size, mass, ID, ox offset, oy offset

Constraint.Create(v5:verlet,v6:verlet)	'Verlet1 and verlet2
Constraint.Create(v5:verlet,v7:verlet)
Constraint.Create(v7:verlet,v6:verlet)
Constraint.Create(v5:verlet,v8:verlet)
Constraint.Create(v8:verlet,v6:verlet)

Function createtriverl(x#,y#,siz#,mass#,id)

	v1:verlet = Verlet.Create(x,y,siz,mass,id)' x, y, size, mass, ID, ox offset, oy offset
	v2:verlet = Verlet.Create(x+siz,y+Rnd(-20,20)+siz,siz,mass,id)' x, y, size, mass, ID, ox offset, oy offset
	v3:verlet = Verlet.Create(x-siz,y+Rnd(-20,20)+siz,siz,mass,id)' x, y, size, mass, ID, ox offset, oy offset
	
	constraint.Create(v1:verlet,v2:verlet)
	constraint.Create(v2:verlet,v3:verlet)
	constraint.Create(v3:verlet,v1:verlet)


End Function
phybodycnt = 2

While Not KeyDown(Key_Escape)
Cls

If KeyHit(key_space) Then
	phybodycnt = phybodycnt + 1
	createtriverl(Rnd(600)+100,-100,Rnd(5,30),Rnd(.1,20),phybodycnt)
EndIf

If KeyDown(key_right) Then
	v2.ox = v2.ox - .7
EndIf
If KeyDown(key_left) Then
	v2.ox = v2.ox + .7
EndIf

If KeyDown(key_up) Then
	v2.oy = v2.oy + .7
EndIf
If KeyDown(key_down) Then
	v2.oy = v2.oy - .7
EndIf


SetAlpha .5
SetColor 0,0,255

DrawRect 0,waterlevel,800,480		'Temporary.. draws the waterline

SetColor 255,255,255
SetAlpha 1

UpdateVerlets()
UpdateConstraints()

Flip
Wend


Type Constraint
	
	Field v1:Verlet
	Field v2:Verlet
	Field Length:Float
	Field Elasticity:Float
	
	Function Create:Constraint(v1:Verlet, v2:Verlet, Length:Float = -1)
		
		CC:Constraint = New Constraint
		CC.v1:Verlet = v1:Verlet
		CC.v2:Verlet = v2:verlet
		If length &lt; 0 Then
			CC.Length = Sqr( (v1.x - v2.x)^2 + (v1.y - v2.y)^2 )	'Distance formula for the length
		Else
			CC.Length = Length
		EndIf
		
		CC.Elasticity = 0
		Constraintlist.addlast(CC:Constraint)	'verlet list
				
		Return CC:Constraint
	End Function
	
	
End Type

Function UpdateConstraints()

	For a = 1 To ConstraintIterations
		For c:Constraint = EachIn ConstraintList
			
			totalmass# = c.v1.mass# + c.v2.mass#
			
			v1prop# = (c.v1.mass#/totalmass#) * 2
			v2prop# = (c.v1.mass#/totalmass#) * 2
			
			dx# = c.v1.x - c.v2.x
			dy# = c.v1.y - c.v2.y
			
			length# = Sqr(dx#*dx# + dy#*dy#)
			
			If length# &lt;&gt; 0 Then
				diff# = (length# - c.length) / length#
			Else
				diff# = 0
			EndIf
			
			dx# = dx# * .5
			dy# = dy# * .5
			
			c.v1.x = c.v1.x - (diff# * dx#) * v2prop#
			c.v1.y = c.v1.y - (diff# * dy#) * v2prop#
			
			c.v2.x = c.v2.x + (diff# * dx#) * v1prop#
			c.v2.y = c.v2.y + (diff# * dy#) * v1prop#
			
			DrawLine(c.v1.x,c.v1.y,c.v2.x,c.v2.y)
		Next
	Next

End Function



Type Verlet				'Verlet Type

	Field X!
	Field Y!
	Field OX!
	Field OY!
	Field VX!
	Field VY!
	Field Siz:Float
	Field Collided
	Field ID = 0
	Field mass:Float
	
	Function Create:Verlet(x!,y!,siz:Float,mass:Float,ID,ox! = 0,oy! = 0)
		VV:Verlet = New Verlet
		VV.x! = x!			'verlet x
		VV.y! = y!			'verlet y
		VV.ox! = x! + ox!		'verlet old x
		VV.oy! = y! + oy!		'verlet old y
		VV.siz = siz			'verlet radius
		VV.Collided = False		'collision variable
		VV.ID = ID			'Verlet ID
		VV.mass = mass			'Verlet mass
		VV.vx! = 0			'Verlet velocity x
		VV.vy! = 0			'Verlet velocity y
		Verletlist.addlast(VV:Verlet)	'verlet list
		
		Return VV:Verlet
	End Function
	
End Type

Function UpdateVerlets()
	
	SetScale 1,1	'sets the color rotation alpha scale and blend mode for the verlets
	SetRotation 0
	SetAlpha .5
	SetBlend 3
	SetColor 255,255,255
	For V:Verlet = EachIn VerletList
		
		V.collided = False		'Resets collision variable
		
		If v.y &gt; waterlevel Then	'Detects if verlet is below water for water friction flag
			wfric = True	'water friction flag set to true
		Else
			wfric = False	'water friction flag set to false
		EndIf
		
		v.vx = (v.x - v.ox) * (Friction-(wfric*WaterFriction)) 'Gets velocities of verlets
		v.vy = (v.y - v.oy) * (Friction-(wfric*WaterFriction))
		
		v.ox = v.x 'Updates O values O=old
		v.oy = v.y
		
		v.x = v.x + v.vx			'Keeps track of X velocity
		v.y = v.y + v.vy + Gravity	'Applies Gravity
		
		
		For vv:verlet = EachIn verletlist
			If v &lt;&gt; vv And v.id &lt;&gt; vv.id   ' If Not the same verlet Or group
				dx# = v.x - vv.x
				dy# = v.y - vv.y
				dist# = Sqr ( dx#*dx# + dy#*dy#)		
				totalr# = v.siz# + vv.siz#
				If dist# &lt; totalr# Then
				
					
					Diffx# = ( dist# - totalr# ) * ( dx# / dist# )
					Diffy# = ( dist# - totalr# ) * ( dy# / dist# )

					v.x = v.x - Diffx# '* .5
					v.y = v.y - Diffy# '* .5

					vv.x = vv.x + Diffx# '* .5
					vv.y = vv.y + Diffy# '* .5
				EndIf 				
			EndIf
		Next 


		If v.y &gt; waterlevel Then			'This does water calculations I came up with
			dtmp# = waterlevel-v.y
			If dtmp# &gt; Waterdensity Then
				v.oy = v.oy - (BoyancyForce*(1/v.mass#))
			Else
				v.oy = v.oy - BoyancyForce * (dtmp#/Waterdensity) * (1/v.mass#)
			EndIf
		EndIf
		
		DrawOval V.x-V.siz , V.y-V.siz , V.siz*2 , V.siz*2  'Draws the verlet (temporary)
	Next
	
End Function
</textarea><br><br><br>I know this is a really general question but I was just wondering if this is what a typical bmax program looks like or am I doing it wrong... well it works so I guess I am doing something right... I cant get strict to work but I dont know if it is really important. build time isnt an issue for me<br><br>also.. how is frame limiting controlled in bmax..  I know in the graphics command sets the frame rate but in my program things randomly get choppy and then go back to normal..<br><br>sorry for the noobish questions but I have only had bmax for a few days so I can hardly consider myself a pro <br><br></td></tr></table><br>
<a name="941104"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DavidDC</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Neat demo!<br><br>Definitely use SuperStrict. You were just missing some locals and :Int entries. This compiles:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict
Graphics 800,600,0,60
AppTitle = "Verlet PhysX"


Global Verletlist:TList = New TList 'Verlet List
Global ConstraintList:TList = New TList 'Constraint List

Const Friction:Float = .985		'Friction should be close to 1 unless you want very slow movement
Const Gravity:Float = .1			'Gravity
Const Waterlevel:Int = 400		'Waterlevel in pixels
Const Waterdensity:Float = 10		'The depth at wich boyancy maxes out
Const BoyancyForce:Float = .1		'Best if close to gravity
Const WaterFriction:Float = .01	'Needs to be really low for a very subtle effect
Const ConstraintIterations:Int = 10	'constraint iterations



Local v1:verlet = Verlet.Create(100,100,21,10,1)' x, y, size, mass, ID, ox offset, oy offset
Local v2:verlet = Verlet.Create(200,150,11,1,1)' x, y, size, mass, ID, ox offset, oy offset
Local v3:verlet = Verlet.Create(200,100,11,1,1)' x, y, size, mass, ID, ox offset, oy offset
Local v4:verlet = Verlet.Create(100,150,21,10,1)' x, y, size, mass, ID, ox offset, oy offset

Constraint.Create(v1:verlet,v2:verlet)	'Verlet1 and verlet2
Constraint.Create(v1:verlet,v3:verlet)
Constraint.Create(v3:verlet,v2:verlet)
Constraint.Create(v1:verlet,v4:verlet)
Constraint.Create(v4:verlet,v2:verlet)

Local v5:verlet = Verlet.Create(300,100,21,10,2)' x, y, size, mass, ID, ox offset, oy offset
Local v6:verlet = Verlet.Create(400,150,11,1,2)' x, y, size, mass, ID, ox offset, oy offset
Local v7:verlet = Verlet.Create(400,100,11,1,2)' x, y, size, mass, ID, ox offset, oy offset
Local v8:verlet = Verlet.Create(300,150,21,10,2)' x, y, size, mass, ID, ox offset, oy offset

Constraint.Create(v5:verlet,v6:verlet)	'Verlet1 and verlet2
Constraint.Create(v5:verlet,v7:verlet)
Constraint.Create(v7:verlet,v6:verlet)
Constraint.Create(v5:verlet,v8:verlet)
Constraint.Create(v8:verlet,v6:verlet)

Function createtriverl(x#,y#,siz#,mass#,id:Int)

	Local v1:verlet = Verlet.Create(x,y,siz,mass,id)' x, y, size, mass, ID, ox offset, oy offset
	Local v2:verlet = Verlet.Create(x+siz,y+Rnd(-20,20)+siz,siz,mass,id)' x, y, size, mass, ID, ox offset, oy offset
	Local v3:verlet = Verlet.Create(x-siz,y+Rnd(-20,20)+siz,siz,mass,id)' x, y, size, mass, ID, ox offset, oy offset
	
	constraint.Create(v1:verlet,v2:verlet)
	constraint.Create(v2:verlet,v3:verlet)
	constraint.Create(v3:verlet,v1:verlet)


End Function
Local phybodycnt:Int = 2

While Not KeyDown(Key_Escape)
	Cls
	
	If KeyHit(key_space) Then
		phybodycnt = phybodycnt + 1
		createtriverl(Rnd(600)+100,-100,Rnd(5,30),Rnd(.1,20),phybodycnt)
	EndIf
	
	If KeyDown(key_right) Then
		v2.ox = v2.ox - .7
	EndIf
	If KeyDown(key_left) Then
		v2.ox = v2.ox + .7
	EndIf
	
	If KeyDown(key_up) Then
		v2.oy = v2.oy + .7
	EndIf
	If KeyDown(key_down) Then
		v2.oy = v2.oy - .7
	EndIf
	
	
	SetAlpha .5
	SetColor 0,0,255
	
	DrawRect 0,waterlevel,800,480		'Temporary.. draws the waterline
	
	SetColor 255,255,255
	SetAlpha 1
	
	UpdateVerlets()
	UpdateConstraints()
	
	Flip 1
Wend


Type Constraint
	
	Field v1:Verlet
	Field v2:Verlet
	Field Length:Float
	Field Elasticity:Float
	
	Function Create:Constraint(v1:Verlet, v2:Verlet, Length:Float = -1)
		
		Local CC:Constraint = New Constraint
		CC.v1:Verlet = v1:Verlet
		CC.v2:Verlet = v2:verlet
		If length &lt; 0 Then
			CC.Length = Sqr( (v1.x - v2.x)^2 + (v1.y - v2.y)^2 )	'Distance formula for the length
		Else
			CC.Length = Length
		EndIf
		
		CC.Elasticity = 0
		Constraintlist.addlast(CC:Constraint)	'verlet list
				
		Return CC:Constraint
	End Function
	
	
End Type

Function UpdateConstraints()

	For Local a:Int = 1 To ConstraintIterations
		For Local c:Constraint = EachIn ConstraintList
			Local totalmass# = c.v1.mass# + c.v2.mass#
			
			Local v1prop# = (c.v1.mass#/totalmass#) * 2
			Local v2prop# = (c.v1.mass#/totalmass#) * 2
			
			Local dx# = c.v1.x - c.v2.x
			Local dy# = c.v1.y - c.v2.y
			
			Local length# = Sqr(dx#*dx# + dy#*dy#)
			Local diff#
			
			If length# &lt;&gt; 0 Then
				diff# = (length# - c.length) / length#
			Else
				diff# = 0
			EndIf
			
			dx# = dx# * .5
			dy# = dy# * .5
			
			c.v1.x = c.v1.x - (diff# * dx#) * v2prop#
			c.v1.y = c.v1.y - (diff# * dy#) * v2prop#
			
			c.v2.x = c.v2.x + (diff# * dx#) * v1prop#
			c.v2.y = c.v2.y + (diff# * dy#) * v1prop#
			
			DrawLine(c.v1.x,c.v1.y,c.v2.x,c.v2.y)
		Next
	Next

End Function



Type Verlet				'Verlet Type

	Field X!
	Field Y!
	Field OX!
	Field OY!
	Field VX!
	Field VY!
	Field Siz:Float
	Field Collided:Int
	Field ID:Int = 0
	Field mass:Float
	
	Function Create:Verlet(x!,y!,siz:Float,mass:Float,ID:Int,ox! = 0,oy! = 0)
		Local  VV:Verlet = New Verlet
		VV.x! = x!			'verlet x
		VV.y! = y!			'verlet y
		VV.ox! = x! + ox!		'verlet old x
		VV.oy! = y! + oy!		'verlet old y
		VV.siz = siz			'verlet radius
		VV.Collided = False		'collision variable
		VV.ID = ID			'Verlet ID
		VV.mass = mass			'Verlet mass
		VV.vx! = 0			'Verlet velocity x
		VV.vy! = 0			'Verlet velocity y
		Verletlist.addlast(VV:Verlet)	'verlet list
		
		Return VV:Verlet
	End Function
	
End Type

Function UpdateVerlets()
	
	SetScale 1,1	'sets the color rotation alpha scale and blend mode for the verlets
	SetRotation 0
	SetAlpha .5
	SetBlend 3
	SetColor 255,255,255
	For Local V:Verlet = EachIn VerletList
		
		V.collided = False		'Resets collision variable
		
		Local wfric:Int
		
		If v.y &gt; waterlevel Then	'Detects if verlet is below water for water friction flag
			wfric = True	'water friction flag set to true
		Else
			wfric = False	'water friction flag set to false
		EndIf
		
		v.vx = (v.x - v.ox) * (Friction-(wfric*WaterFriction)) 'Gets velocities of verlets
		v.vy = (v.y - v.oy) * (Friction-(wfric*WaterFriction))
		
		v.ox = v.x 'Updates O values O=old
		v.oy = v.y
		
		v.x = v.x + v.vx			'Keeps track of X velocity
		v.y = v.y + v.vy + Gravity	'Applies Gravity
		
		
		For Local vv:verlet = EachIn verletlist
			If v &lt;&gt; vv And v.id &lt;&gt; vv.id   ' If Not the same verlet Or group
				Local dx# = v.x - vv.x
				Local dy# = v.y - vv.y
				Local dist# = Sqr ( dx#*dx# + dy#*dy#)		
				Local totalr# = v.siz# + vv.siz#
				If dist# &lt; totalr# Then
				
					
					Local Diffx# = ( dist# - totalr# ) * ( dx# / dist# )
					Local Diffy# = ( dist# - totalr# ) * ( dy# / dist# )

					v.x = v.x - Diffx# '* .5
					v.y = v.y - Diffy# '* .5

					vv.x = vv.x + Diffx# '* .5
					vv.y = vv.y + Diffy# '* .5
				EndIf 				
			EndIf
		Next 


		If v.y &gt; waterlevel Then			'This does water calculations I came up with
			Local dtmp# = waterlevel-v.y
			If dtmp# &gt; Waterdensity Then
				v.oy = v.oy - (BoyancyForce*(1/v.mass#))
			Else
				v.oy = v.oy - BoyancyForce * (dtmp#/Waterdensity) * (1/v.mass#)
			EndIf
		EndIf
		
		DrawOval V.x-V.siz , V.y-V.siz , V.siz*2 , V.siz*2  'Draws the verlet (temporary)
	Next
	
End Function</textarea> <br><br></td></tr></table><br>
<a name="941161"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> ok thanks a million DavidDC.  I guess I will just study the code you modified so I can figure out superstrict and strict :)  What are the advantages/disadvantages of superstrict and strict besides compile time? <br><br></td></tr></table><br>
<a name="941176"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gabriel</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> What are the advantages/disadvantages of superstrict and strict besides compile time?  <br></div><br>You'll never, ever have an (hard to find) error where you have mistyped a variable name and accidentally found yourself using a variable which does not exist, and thus has a 0 or NULL value. <br><br></td></tr></table><br>
<a name="941194"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think I get it now! thanks! <br><br></td></tr></table><br>
<a name="941197"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi I have another question.. when I build without quick build or debug on, when I run my program a command prompt window pops up along with my game.  Why is this?  is there a way to get rid of it because it is getting annoying.  I am running vista if that helps any <br><br></td></tr></table><br>
<a name="941207"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warpy</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> you might have "build GUI app" disabled. <br><br></td></tr></table><br>
<a name="941210"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> oh yeah I thought that was for Max GUI which I dont have so I disabled it.  my bad... blitz max is so addicting!  thanks for the help. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
