<!DOCTYPE html><html lang="en" ><head ><title >Mouse movement woes</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Mouse movement woes</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >Mouse movement woes</a><br><br>
<a name="655637"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Zolyx</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm trying to create a system of moving the player by mouse, although not in the standard way. I am aiming for a method that is similar to the one used in games like XQuest (PC) and Crystal Quest (Mac) whereby you essentially control the player's inertia within a frictionless environment. Moving the mouse right would begin sliding the player in that direction until you moved left to come to a stop.<br><br>The basic code I have for this at the moment looks like this...<br><pre class=code>
SuperStrict

Framework BRL.GLMax2D
Import BRL.FileSystem
Import BRL.FreeTypeFont
Import BRL.PNGLoader
Import BRL.Retro

'Create graphics interface
SetGraphicsDriver GLMax2DDriver()
Graphics 800, 600
SetBlend ALPHABLEND
SeedRnd MilliSecs()

'Types
Global MEngine:XQ_MouseEngine   'Engine to control mouse movement of the player
Global Player:XQ_Player         'The player ship

'Lists
Global lstEntities:TList = New TList

'Types
Type XQ_MouseEngine
' ----------------------------------------------------------------------------------
' DESCRIPTION - Centralises all of the mouse-related functions into one engine.
' ----------------------------------------------------------------------------------

	Field M1:Short, M2:Short        'Counter for mouse button actions
	Field tmx:Short, tmy:Short      'Mouse position on this tick
	Field pmx:Short, pmy:Short      'Mouse position on previous tick
	Field offx:Float, offy:Float    'Offset of last tick
	Field focx:Float, focy:Float    'Current X and Y movement focus
	Field sensitivity:Float         'How sensitive is movement?


	Method Destroy()
		Destroy()
	End Method

	Method Update()
	' -----------------------------------------------------------------------------
	' DESCRIPTION - Every game tick, this function is called to compare the mouse's
	'               current position with it's last position. The values are used
	'               to manipulate the direction focus of the player. Also handles
	'               mouse click counters.
	' -----------------------------------------------------------------------------
		pmx = tmx
		pmy = tmy
	
		tmx = MouseX()
		tmy = MouseY()
		
		offx = (tmx - pmx) * sensitivity
		offy = (tmy - pmy) * sensitivity
		
		If MouseDown(1) Then M1 :+ 1 Else M1 = 0
		If MouseDown(2) Then M2 :+ 1 Else M2 = 0
	End Method
	
	
	Function Create:XQ_MouseEngine(sense:Float = 0.1)
		Local me:XQ_MouseEngine = New XQ_MouseEngine
		me.sensitivity = sense
		Return me
	End Function

End Type


Type XQ_Entity
' ----------------------------------------------------------------------------------
' DESCRIPTION - All entities in the game - such as the player, the enemies and any
'               and any bullets - as well some that don't move are created from this
'               base type.
' ----------------------------------------------------------------------------------

	Field x:Float       'X position
	Field y:Float       'Y position
	Field velx:Float    'X velocity
	Field vely:Float    'Y velocity
	Field ang!          'Angle
	Field speed!        'Directional speed



	Method New()
		lstEntities.AddLast Self
	End Method

	Method Destroy()
		lstEntities.Remove Self
	End Method

	Method Update()
		UpdatePositions()
		CheckCollisions()
	End Method

	Method UpdatePositions()
		x :+ velx
		y :+ vely
	End Method

	Method UpdateVelocities(ang!, speed!)
	    	velx = Sin(ang) * speed
     	vely = Cos(ang) * speed
	End Method

	Method CheckCollisions()
		If x &lt; 0 Or x &gt; (GraphicsWidth() - 16) Then
			ang = 360 - ang
			If ang &lt; 0 Then ang :+ 360
			UpdateVelocities(ang, speed)
		EndIf

		If y &lt; 0 Or y &gt; (GraphicsHeight() - 16) Then
			ang = 180 - ang
			If ang &lt; 0 Then ang :+ 360
			UpdateVelocities(ang, speed)
		EndIf
	End Method
	
	Method Draw()
		DrawOval x-10, y-10, 20, 20
	End Method



	Function UpdateAll()
		For Local e:XQ_Entity = EachIn lstEntities
			e.Update()
			e.Draw()
		Next
     End Function

	Function DestroyAll()
		For Local e:XQ_Entity = EachIn lstEntities
			e.Destroy()
		Next
	End Function

End Type


Type XQ_Player Extends XQ_Entity
' ----------------------------------------------------------------------------------
' DESCRIPTION - The player entity.
' ----------------------------------------------------------------------------------

	Function Create:XQ_Player(xpos:Short, ypos:Short)
		Local te:XQ_Player = New XQ_Player

		te.x = xpos
		te.y = ypos
		te.ang   = 0
		te.speed = 0

		te.UpdateVelocities(te.ang, te.speed)

		Return te
	End Function

End Type










MEngine = XQ_MouseEngine.Create()
Player = XQ_Player.Create(400, 300)

'Main program loop
Repeat
	Cls
	MEngine.Update()
		
	DrawText "Entity count: " + lstEntities.Count(), 200, 10
	DrawText "Offset X:     " + MEngine.offx, 200, 25
	DrawText "Offset Y:     " + MEngine.offy, 200, 40

	Player.velx :+ MEngine.offx
	Player.vely :+ MEngine.offy
	
	XQ_Entity.UpdateAll()
	'MoveMouse 400, 300
	
	Flip
Until KeyDown(KEY_ESCAPE)



XQ_Entity.DestroyAll()
End
</pre><br><br>That code sort of works, but you have to move the mouse up-left a bit at the start to see the oval you're moving about. Also, because the mouse itself is unconstrained, when it hits the edge of the window, player control is lost, so there needs to be some way of resetting the mouse position cleanly without affecting the updates. If you uncomment the MoveMouse line within the main loop to do this, weirdness starts happening, and maybe I've just been looking at it too long today to see why... :(<br><br>Any advice gratefully received. <br><br></td></tr></table><br>
<a name="655670"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Are you trying to do something like this?<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics 800,600

Type TOval
 Field X:Float
 Field Y:Float

 Method Move()
  X :+ .01*(MouseX() - 400)
  If X &lt; 0 Then X = 0
  If X &gt; 799 Then X = 799
  Y :+ .01*(MouseY() - 300)
  If Y &lt; 0 Then Y = 0
  If Y &gt; 599 Then Y = 599
 End Method

 Method Draw()
  DrawOval x-10,y-10,20,20
 End Method
End Type

Local Oval:TOval = New TOval

While Not KeyHit(KEY_ESCAPE)
 Cls
 Oval.Move()
 Oval.Draw()
 Flip 1
Wend
</textarea> <br><br></td></tr></table><br>
<a name="655729"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Zolyx</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes... that's exactly it :) Thanks very much! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
