<!DOCTYPE html><html lang="en" ><head ><title >Accessing user32 without decls</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Accessing user32 without decls</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >Accessing user32 without decls</a><br><br>
<a name="619344"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >D4NM4N</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> if i wanted to paste a string+string+string etc to the clipboard, how do i do it? I have a .decl file for b3d that does this and need the equivalent.<br><br>the following read and write funcs work in b3d with the api decs in the userlibs:<br><br><pre class=code>
Extern "win32"
	Function api_CloseClipboard:Int ()= "CloseClipboard"
	Function api_EmptyClipboard:Int ()= "EmptyClipboard"
	Function api_GetClipboardData:Int (wFormat%) = "GetClipboardDataA"
	Function api_GetClipboardtext:String(format%)="GetClipboardData"
	Function api_SetClipboardData:Int (wFormat%, hMem%) = "SetClipboardDataA"
	Function api_SetClipboardText:Int(format%,txt$)="SetClipboardData"
	Function api_OpenClipboard:Int (hwnd%)="OpenClipboard"
	Function api_IsClipboardFormatAvailable:Int (wFormat%) = "IsClipboardFormatAvailable"

End Extern
'-----------------------------------

Function WriteClipboardText(txt$)
	Local cb_TEXT:Int=1
	If txt$="" Then Return 
	If api_OpenClipboard(0)
		api_EmptyClipboard
		api_SetClipboardtext%(cb_TEXT,txt$)
		api_CloseClipboard
	EndIf
End Function

'-----------------------------------

Function ReadClipboardText$()
	Local cb_TEXT:Int=1
	Local txt$=""
	If api_OpenClipboard(0)
		If api_IsClipboardFormatAvailable(cb_TEXT) 
			txt$=api_GetClipboardtext$(cb_TEXT)
		EndIf
		api_CloseClipboard
	EndIf
	Return txt$
End Function
</pre> <br><br></td></tr></table><br>
<a name="619358"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I had some trouble getting your example to work properly, and after i read up on it on MSDN it became a little clearer ;)<br><br><b>SetClipboardData</b> takes over ownership if its data so passing it anything other than memory handles from <b>GlobalAlloc/LocalAlloc</b> resulted in failure (atleast on my end ;)<br><br>this is how i got it work:<br><pre class=code>
SuperStrict

'
' Clipboard
'
Const CF_TEXT:Int = 1
Const CF_BITMAP:Int = 2
Const CF_METAFILEPICT:Int = 3
Const CF_SYLK:Int = 4
Const CF_DIF:Int = 5
Const CF_TIFF:Int = 6
Const CF_OEMTEXT:Int = 7
Const CF_DIB:Int = 8
Const CF_PALETTE:Int = 9
Const CF_PENDATA:Int = 10
Const CF_RIFF:Int = 11
Const CF_WAVE:Int = 12
Const CF_UNICODETEXT:Int = 13
Const CF_ENHMETAFILE:Int = 14
Const CF_HDROP:Int = 15
Const CF_LOCALE:Int = $10
Const CF_MAX:Int = 17
Const CF_DIBV5:Int = 17

Extern "win32"
	Function OpenClipboard:Int( hwnd:Int = 0) = "OpenClipboard@4"
	Function CloseClipboard:Int() = "CloseClipboard@0"
	Function GetClipboardData:Int( format:Int) = "GetClipboardData@4"
	Function SetClipboardData:Int( format:Int, handle:Int) = "SetClipboardData@8"
	Function EmptyClipboard:Int() = "EmptyClipboard@0"		
	Function IsClipboardFormatAvailable:Int( format:Int) = "IsClipboardFormatAvailable@4"
EndExtern


'
' Global Memory
'
Const GMEM_FIXED:Int = 0  
Const GMEM_MOVEABLE:Int = 2  
Const GMEM_NOCOMPACT:Int = $10  
Const GMEM_NODISCARD:Int = $20  
Const GMEM_ZEROINIT:Int = $40  
Const GMEM_MODIFY:Int = $80
Const GMEM_DISCARDABLE:Int = $100  
Const GMEM_NOT_BANKED:Int = $1000  
Const GMEM_SHARE:Int = $2000  
Const GMEM_DDESHARE:Int = $2000  
Const GMEM_NOTIFY:Int = $4000  
Const GMEM_LOWER:Int = GMEM_NOT_BANKED  
Const GMEM_VALID_FLAGS:Int = 32626  
Const GMEM_INVALID_HANDLE:Int = $8000
Const GHND:Int = GMEM_MOVEABLE | GMEM_ZEROINIT  
Const GPTR:Int = GMEM_FIXED | GMEM_ZEROINIT

Extern "win32"
	Function GlobalAlloc:Int( flags:Int, size:Int) = "GlobalAlloc@8"
	Function GlobalReAlloc:Int( mem:Int, size:Int, flags:Int) = "GlobalReAlloc@8"
	Function GlobalSize:Int( mem:Int) = "GlobalSize@4"
	Function GlobalFlags:Int( mem:Int) = "GlobalFlags@4"
	Function GlobalLock:Byte Ptr( mem:Int) = "GlobalLock@4"
	Function GlobalHandle:Int( mem:Byte Ptr) = "GlobalHandle@4"
	Function GlobalUnlock:Int( mem:Int) = "GlobalUnlock@4"
	Function GlobalFree:Int( mem:Int) = "GlobalFree@4"
EndExtern


Function WriteClipboardText( s:String)
	If s = Null Then Return 
	If OpenClipboard() Then
		Local data:Int = GlobalAlloc( GMEM_MOVEABLE, s.Length + 1)
		Local p:Byte Ptr = GlobalLock( data)
		MemCopy( p, s.ToCString(), s.Length + 1)
		GlobalUnlock( data)
		EmptyClipboard()
		SetClipboardData( CF_TEXT, data)
		CloseClipboard()
		GlobalFree( data)
	EndIf
EndFunction

Function ReadClipboardText:String()
	Local s:String
	If OpenClipboard() Then
		If IsClipboardFormatAvailable( CF_TEXT) Then
			Local data:Int = GetClipboardData( CF_TEXT)
			Local p:Byte Ptr = GlobalLock( data)
			s = String.FromCString( p)
			GlobalUnlock( data)
		EndIf
		CloseClipboard()
	EndIf
	Return s
EndFunction

WriteClipboardText( "Hello World!")
Print "read: " + ReadClipboardText()
</pre><br>If i understood your question properly you want to append data to the clipboard? than this should suffice.<br><pre class=code>
Function AppendClipboardText( s:String)
	WriteClipboardText( ReadClipboardText() + s)
EndFunction
</pre><br><br>hope this helps =) <br><br></td></tr></table><br>
<a name="619619"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >D4NM4N</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> thanks, ill take a long hard look at this <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
