<!DOCTYPE html><html lang="en" ><head ><title >Beginners' Challenge, Week #2 - "Chain Reaction"</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Beginners' Challenge, Week #2 - "Chain Reaction"</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >Beginners' Challenge, Week #2 - "Chain Reaction"</a><br><br>
<a name="1109613"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nest</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Beginners' Coding Challenge, Week #2<br><br>The theme for this week is "Chain Reaction"!<br><br>It can be as simple or complex as you wish. Share your code in your post, and links to any assets (e.g. image/sound) if required!<br><br>Closing Date - Sunday 16th October! Good luck! <br><br></td></tr></table><br>
<a name="1109665"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt Vinyl</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> This should throw up some interesting mini-games, hopefully. :)<br><br>If I can get time I will try to build something - have an inkling of an idea. ;) <br><br></td></tr></table><br>
<a name="1109892"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Time for the bomb squad to render safe all those bombs flying about. :)<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict
'Some Constants
Const InitialRate:Float = 4.0 'The rate that the explosion initially expands
Const Deceleration:Float = 0.075 'the rate in which the explosion decelerates
Const BombMinimum:Int = 30 'the minimum number of bombs on the screen
Const MaxSpeed:Float = 1.5 'Maximum speed a bomb can move

'This will be our bomb type.  These little bombs will be moving around the screen just waiting for something to set them off.
Type TBomb
	Global List:TList = CreateList() 'This will hold a list of all the bombs moving on the screen
	
	Field x:Float, y:Float 'The x and y position of the bomb
	Field dx:Float, dy:Float 'the direction the bomb will be moving
	
	'This function will create a number of bombs and place them into the list
	Function Create(Number:Int)
		List.Clear() 'Clear all bombs from the list
		For Local i:Int = 1 To Number
			Local Bomb:TBomb = New TBomb 'Create the bomb
			Bomb.x = Rnd(1,798) 'randomly place the bomb
			Bomb.y = Rnd(1,598)
			Bomb.dx = Rnd(-MaxSpeed,MaxSpeed) 'pick a random direction for the bomb to move
			bomb.dy = Rnd(-MaxSpeed,MaxSpeed)
			List.Addlast(Bomb) 'Add it to the list
		Next
	End Function
	
	'This function will update all the bombs
	Function Update()
		For Local Bomb:TBomb = EachIn List
			Bomb.Move() 'move the bomb
			If Bomb.Test() 'test to see if the bomb needs to explode
				TExplosion.Create(Bomb.x,Bomb.y) 'create the explosion
				List.Remove(Bomb) 'remove the bomb from the list
			End If
		Next
	End Function
	
	'This function will draw all the bombs on the screen
	Function Draw()
		For Local Bomb:TBomb = EachIn List
			DrawOval Bomb.x-5,Bomb.y-5,10,10
		Next
	End Function
	
	'This method will move the bomb
	Method Move()
		x :+ dx 'move in the x direction
		If x &lt; 0 Or x &gt; 799 Then dx = -dx 'reverse direction at the border
		y :+ dy 'move in the y direction
		If y &lt; 0 Or y &gt; 599 Then dy = -dy 'reverse direction at the border
	End Method
	
	'This method sees if the bomb collides with any explosions
	Method Test:Int()
		Local Diffx:Float, Diffy:Float 'the difference in the center of teh bomb and explosion
		Local Distance:Float 'The distance between the bomb center and the explosion center
		
		For Local Explosion:TExplosion = EachIn TExplosion.List 'loop through all the explosions
			Diffx = x - Explosion.x
			Diffy = y - Explosion.y
			Distance = Sqr(Diffx*Diffx+Diffy*Diffy)
			If Distance &lt;= 5 + Explosion.Rad Then Return True 'if the distance between the explosion and bomb is less than the sum of their radii, then an explosion has occurred
		Next
		
		Return False 'No explosion has occurred
	End Method
End Type

'This type is for the explosions
Type TExplosion
	Global List:TList = CreateList()
	Global Number:Int = 0 'the number of explosion currently being set off
	Global Exploding:Int = False 'Are thier currently any explosions going off?
	
	Field x:Float, y:Float 'The location of the epicenter
	Field Rad:Float 'the radius of the explosion
	Field rate:Float 'the speed at which the explosion expands.  This will be reduced over time
	
	'this function will create an explosion and add it to the list
	Function Create(x:Float,y:Float)
		Local Explosion:TExplosion = New TExplosion 'create new explosion
		Explosion.x = x 'copy the epicenter coordinates to the explosion
		Explosion.y = y	
		Explosion.rate = InitialRate 'Set the rate of explosion to the initial rate
		List.AddLast(Explosion) 'add to the list
		Number :+ 1 'increase the count
		Exploding = True 'We now have explosions going off
	End Function
	
	'This function updates all the explosions
	Function Update:Int()
		If Exploding 'Are there currently any explosions?
			For Local Explosion:TExplosion = EachIn List
				Explosion.Rad :+ Explosion.rate 'Expand the radius
				If Explosion.Rad &lt;= 0 'If the explosion has completely depleted
					List.Remove(Explosion) 'remove it from the list
					Number :- 1 'Decrease the count
					If Number = 0 Then Return True 'All explosions have depleted
				End If
				Explosion.rate :- Deceleration 'Adjust the rate
			Next
		End If
		Return False 'at least one explosion still going
	End Function
	
	'This function draws the explosions
	Function Draw()
		For Local Explosion:TExplosion = EachIn list
			SetColor Rand(0,255),Rand(0,255),Rand(0,255) 'set a random color
			DrawOval Explosion.x-Explosion.Rad,Explosion.y-Explosion.Rad,Explosion.Rad*2,Explosion.Rad*2 'draw the explosion
		Next
	End Function
End Type

Graphics 800,600,32 'set the resolution
SetClsColor 0,255,255 'screen color is cyan

Local Timer:Int 'used for a timer.

Repeat
	SetColor 0,0,0
	Repeat 'print instructions, wait for mouseclick
		DrawMessage "Carefully place bomb on screen~nDestroy 70% to advance to next level~nLeft click to start"
	Until MouseHit(1)
	
	Local Bombs:Int = 100 'number of bombs
	Local Level:Int = 1 'Level reached
	
	Repeat
		TBomb.Create(Bombs) 'Create the bombs for this level
		
		Repeat
			If KeyHit(KEY_ESCAPE) Then Exit 'end the game if the escape key is pressed
			
			Cls 'clear the screen
			SetColor 0,0,0 'set to black
			TBomb.Draw() 'draw the bombs
			TExplosion.Draw() 'draw the explosions
			Flip
			
			TBomb.Update() 'Update the bomb positions
			If TExplosion.Exploding And TExplosion.Update() Then Exit'Check to see if the explosions have died down and exit the loop
			
			If MouseHit(1) And Not TExplosion.Exploding 'Has the mouse button been pressed?
				TExplosion.Create(MouseX(),MouseY()) 'create the first explosion
			End If
		Forever
		
		Timer = MilliSecs() + 2000
		SetColor 0,0,0
		Repeat
			Cls
			TBomb.Draw() 'draw the remaining bombs for a couple of seconds	
			Flip
		Until MilliSecs() &gt;= Timer
		
		Local BombsHit:Int = Bombs-TBomb.List.Count() 'How many bombs exploded?
		TBomb.List.Clear() 'clear the bomb list
		TExplosion.Exploding = False 'reset the exploding flag
		Local Percent:Int = BombsHit*100.0/Bombs
		
		FlushMouse() 'flush any mouse clicks that may have occured
		SetColor 0,0,0
		Repeat
			DrawMessage	"You have blasted "+BombsHit+" bombs~nOut of "+Bombs+"~n"+Percent+"% Hit~nLeft Click to continue"
			If KeyHit(KEY_ESCAPE) Then End
		Until MouseHit(1)
		
		If Percent &lt; 70 'Game lost
			Drawmessage "Game Over~nYou have reached level "+Level+"~nLeft Click to play again~nESC to exit"
			Repeat
				If KeyHit(KEY_ESCAPE) Then End
			Until MouseHit(1)
			Exit
		End If
		
		Level :+ 1 'go to next level
		Bombs :- 10 'decrease the number of bombs by 1
		If Bombs &lt; BombMinimum Then Bombs = BombMinimum
	Forever
Forever
		

'This function will simply display a message centered on the screen for the specified time.
Function DrawMessage(Message:String)
	Local MessageArray:String[] = Message.Split("~n")
	Local Lines:Int = MessageArray.Length
	Local FontHeight:Int = TextHeight(Message) * 2.5 + 25
	Local Top:Int = (600-FontHeight*Lines)/2
	SetScale 2.5,2.5
	
		Cls
		
		For Local i:Int = 0 Until Lines
			DrawText MessageArray[i],(800-TextWidth(MessageArray[i])*2.5)/2,Top+i*FontHeight
		Next
		Flip
	SetScale 1,1
End Function

</textarea> <br><br></td></tr></table><br>
<a name="1109921"></a>

<a name="1109922"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SLotman</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Heh, that looks like a game I made in Flash some time ago... <a href="http://www.icongames.com.br/flash/playgame.php?game=Infection&amp;w=550&amp;h=400" target="_blank">Infection</a><br><br>I'm constantly amazed by the level of games people post here, using no external files whatsoever ;)<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1109924"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Htbaa</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice one TomToad :-). I got to level 8. <br><br></td></tr></table><br>
<a name="1109937"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AltanilConard</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Too bad you can't catch all of the bombs Tom, pretty inescapable trap there,  do the bombs explode the boms? how do I get out? hmm.. <br><br></td></tr></table><br>
<a name="1109943"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Too bad you can't catch all of the bombs Tom, pretty inescapable trap there, do the bombs explode the boms? how do I get out? hmm..  <br></div><br>Not sure what you mean by "catch all the bombs"  To end the game early, just press ESC key <br><br></td></tr></table><br>
<a name="1110343"></a>

<a name="1110486"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nest</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Updated...<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
AppTitle="CHAIN REACTION"
Graphics 500,400
HideMouse()
SetBlend alphablend
Global GameObjectList:TList=CreateList()
Global Grid:TBlock[20,20]
Global currentblock:Int
Global nextblock:Int
Global score:Int
Global chain:Int
Global lastchain:Int
Global moves:Int
Global maxcol:Int=4
resetgrid()

Repeat
Cls

drawblocks()
checkmatch()
kill()
fall()
newblock()
updateblock()

For o:TBoom=EachIn GameObjectList
o.UpdateState()
o.DrawSelf()
Next

player_control()

Flip
Until KeyHit(KEY_ESCAPE) Or AppTerminate()

Function player_control()
SetColor 0,0,0
DrawOval MouseX()-8,MouseY()-8,16,16
	Select currentblock
		Case 0	SetColor 0,0,0
		Case 1	SetColor 255,0,0
		Case 2	SetColor 255,255,0
		Case 3	SetColor 255,255,255
		Case 4	SetColor 0,255,0
		Case 5	SetColor 0,255,255
		Case 6	SetColor 0,0,255
		Case 7	SetColor 255,0,255
	End Select
DrawOval MouseX()-5,MouseY()-5,10,10
If KeyHit(KEY_F1) Then resetgrid()
If MouseHit(1) And MouseX()&gt;0 And MouseX()&lt;400 And MouseY()&gt;0 And MouseY()&lt;400 Then
	moves:+1
	grid[MouseX()/20,MouseY()/20].c=currentblock
	currentblock=nextblock
	nextblock=Rand(1,maxcol)

	lastchain=chain
	score:+chain
	chain=0
End If
End Function



' block type

Type TBlock
	Field X:Int,Y:Int,C:Int
	Field state:Int '1 = normal, 2 = dying
	
	Function Create:TBlock()
		Local block:TBlock = New TBlock
		block.x=0
		block.y=0
		block.c=Rand(1,maxcol)
		block.state=1
		Return block
	End Function
	
	Method drawself(a,b)
	
	Select c
		Case 0	SetColor 0,0,0
		Case 1	SetColor 255,0,0
		Case 2	SetColor 255,255,0
		Case 3	SetColor 255,255,255
		Case 4	SetColor 0,255,0
		Case 5	SetColor 0,255,255
		Case 6	SetColor 0,0,255
		Case 7	SetColor 255,0,255
	End Select
	
	DrawOval(a*20+x+1,b*20+y+1,18,18)

	End Method
End Type

Type TBoom

Field x,y,r,c,n

Function Create:TBoom(x:Int,y:Int,c:Int,n:Int)
	Local boom:TBoom = New TBoom
	boom.x=x
	boom.y=y
	boom.r=20
	boom.c=c
	boom.n=n
	ListAddLast GameObjectList,boom
	Return boom
End Function

Method updatestate()
r:+2
'x:-1
y:-.5
If r&gt;50 And Rand(1,5)=1 Then ListRemove gameobjectlist,Self
End Method

Method drawself()
SetAlpha 0.4
SetColor Rand(200,255),Rand(200,255),Rand(200,255)

	DrawOval x,y,r,r
SetAlpha 1
SetColor 255,255,0
DrawText n,x+5,y+5
End Method

End Type

Function resetGrid()
	For a = 0 To 19
		For b = 0 To 19
			Grid[a,b] = TBlock.Create()
		Next
	Next
	currentblock=Rand(1,maxcol)
	nextblock=Rand(1,maxcol)
	score=0
	lastchain=0
	chain=0
	moves=0
End Function

Function drawBlocks()
	For a = 0 To 19
		For b = 0 To 19
			grid[a,b].drawself(a,b)
		Next
	Next

SetColor 255,255,255
DrawText ("Score",405,40)
DrawText score,405,60
DrawText ("Last Chain",405,80)
DrawText lastchain,405,100
DrawText("Moves",405,120)
DrawText moves,405,140
DrawText("Average",405,160)
If moves&gt;0 Then DrawText score/moves,405,180

End Function

Function checkmatch()

'horizontal check
	For a = 0 To 17 'end-2
		For b = 0 To 19
			If grid[a,b].c&gt;0 And grid[a,b].c=grid[a+1,b].c And grid[a,b].c=grid[a+2,b].c And grid[a,b].y=0 And grid[a+1,b].y=0 And grid[a+2,b].y=0 And grid[a,Min(b,19)].state=1 And grid[a+1,Min(b,19)].state=1 And grid[a+2,Min(b,19)].state=1 Then
				
				grid[a,b].state=2
				grid[a+1,b].state=2
				grid[a+2,b].state=2
				boom:TBoom = TBoom.Create(a*20,b*20,grid[a,b].c,chain+1)
				boom:TBoom = TBoom.Create(a*20+20,b*20,grid[a+1,b].c,chain+2)
				boom:TBoom = TBoom.Create(a*20+40,b*20,grid[a+2,b].c,chain+3)
				chain:+3
			End If
		Next
	Next

'vertical check
	For a = 0 To 19
		For b = 0 To 17 'end-2
			If grid[a,b].c&gt;0 And grid[a,b].c=grid[a,b+1].c And grid[a,b].c=grid[a,b+2].c And grid[a,b].y=0 And grid[a,b+1].y=0 And grid[a,b+2].y=0 And grid[a,Min(b,17)].state=1 And grid[a,Min(b,18)].state=1 And grid[a,Min(b,19)].state=1 Then

				grid[a,b].state=2
				grid[a,b+1].state=2
				grid[a,b+2].state=2
				boom:TBoom = TBoom.Create(a*20,b*20,grid[a,b].c,chain+1)
				boom:TBoom = TBoom.Create(a*20,b*20+20,grid[a,b+1].c,chain+2)
				boom:TBoom = TBoom.Create(a*20,b*20+40,grid[a,b+2].c,chain+3)
								chain:+3
			End If
		Next
	Next

End Function

Function kill()
	For a = 0 To 19
		For b = 0 To 19
			If grid[a,b].state=2

				grid[a,b].c=0
				grid[a,b].state=1
			End If
		Next
	Next
End Function

Function fall()
	For a = 0 To 19
		For b = 0 To 18
			If grid[a,b+1].c=0 And grid[a,b].c&gt;0 And grid[a,b].y=0 Then
				grid[a,b+1].c=grid[a,b].c
				grid[a,b+1].y=-19
				grid[a,b].state=2
				grid[a,b].c=0

			End If
		Next
	Next
End Function

Function newblock()
	For a = 0 To 19
		If grid[a,0].c=0 Then
			grid[a,0].c=Rand(1,maxcol)
			grid[a,0].state=1
		End If
	Next
End Function

Function updateblock()
	For a = 0 To 19
	For b = 0 To 19
	If grid[a,b].y&lt;0 grid[a,b].y:+2
	If grid[a,b].y&gt;0 grid[a,b].y=0
	Next
	Next
End Function

</textarea><br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
