<!DOCTYPE html><html lang="en" ><head ><title >Ques for people who have made a shooter game</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Ques for people who have made a shooter game</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >Ques for people who have made a shooter game</a><br><br>
<a name="1093087"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Zacho</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> As we all start out programming I know everyone wants to make some shooter/platformer game out there. I know, for I am one of them. I have a question for all of you that have made a shooter (or any game that uses lists for that matter); concerning checks between bullets and the computer. How is it that you can check for collision detection between multiple instances of bullets and multiple instances of CPU? I have been talking with someone (Jesse) in particular and he has helped me. We (he actually, I don't know his level of expertise - I am far from it) came up with a nested for...next loop within another for...next loop.<br><br>I know from a programming point of view that you have to check each bullet and see if it hits any of the multiple targets, this is why a nested for...next loop within another makes sense to me (albeit it works with some bugs) but it doesn't seem right. I know there are different ways to approach this question. I would like to hear your input on any ways this is possible (I want to thoroughly understand programming so I don't run before I can walk). Please take note that I don't know too well: methods, polymorphism, oo, lists (I started to learn and I am getting the hang of it) but I do know types, global, local, ifs, elses, while wend loops.<br><br>Again thanks for helping :) <br><br></td></tr></table><br>
<a name="1093111"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >H&amp;K</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Jesses solution was simple and maintainable. <br>Its only downside, the amount of stuff you need to loop through, is practicability irrelevant with the speed of today's computers.<br><br>However if you think you have too many "cpu" and bullets that the game is slowing down, then there are other ways but these are more complicated and and really not worth it unless you have to. Plus nine time out of ten you would go through all the effort to write a different collision detection, to find its no faster.<br><br>Jesses solution works. and ATM its better to keep working on the rest of the game. If in the end it seems the bullet collision detection is a bottle neck go back and improve it then. (When you have more experience if nothing else. Just make sure you have compartmentalised the program so (for example) the rest of the program is just expecting a "Yes hit, Cpunumer x" (Flag:byte,CPU:int) or less if possible. <br><br></td></tr></table><br>
<a name="1093112"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ima747</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> There are always tradeoffs between speed and structure (readability in this case). But there are also always ways to optimize things.<br><br>For this type of interaction I will typically be operating multiple lists, such as a list of all MOBs (short for mobiles, legacy term for bad guys at the code level) and a list of all bullets (projectiles of any type).<br><br>All MOBs are either defined in the same class, or atleast derived from the same super class. As such they typically will have an "Update()" method which is called once per cycle to allow their AI to do it's thing, update their positions, etc. Optionally you can have each MOB check each bullet to see if it's been hit in it's update cycle. This method is nice because you can trigger death animations etc. right away which may be handy depending on your structure.<br><br>All Bullets follow the same structure. They are either of the same class or atleast derived from the same super class. They also have an Update() method that is called once per frame and moves them based on their momentum, possibly applies AI in the case of seeking missles, etc. If the mobs don't check to see if they're hit then the bullets have to check to see if they've collided with each mob. This can be handy as the bullets are responsible for deducting health etc. rather than the mob directly (though you still may call some form of mob.WasHitBy(thisBullet) method...) which can be a nice layout.<br><br>You have to compare each thing against each other thing to see if it's hit, there' no way around that. However there are higher level optimizations one can do that are generally dependent on the structure of the game. For example:<br><br>If you are updating things off the screen as well as on the screen, you may choose to have all off screen entities skip their bullet check loop... you may even group all offscreen mobs and offscreen bullets into seperate lists so you can just ignore them entirely in collision tests...<br><br>You may split the screen up into sections and create a list for each section so that you only have to check for things that you know are comparatively close for collisions.<br><br>You almost certainly should do some form of bounding box collision test before a pixel perfect collision test, though I think the built in pixel level collision test has a bounding box pass built in (not 100% sure on that...).<br><br>You may be able to just get by with bounding box for many types of collision (lots of shmups do this...). Though this may make you approach your MOB and art asset structures differently if you really need the speed. <br><br></td></tr></table><br>
<a name="1093127"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> You need some spacial partitioning to act as an acceleration structure, such as a simple grid, where each grid cell can contain a linked list of objects which are within the cell. Then you only need to brute-force compare collisions between the objects in that cell and the surrounding cells. <br><br></td></tr></table><br>
<a name="1093198"></a>

<a name="1093199"></a>

<a name="1093203"></a>

<a name="1093204"></a>

<a name="1093205"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> look Zacho this is so you will have an idea what I am working on(on and off) this is an older version than what I have. I would post the newer version but it's a mac and you probably have a pc:<br><a href="http://www.mediafire.com/?g3qtyvruxc3hcnv" target="_blank">http://www.mediafire.com/?g3qtyvruxc3hcnv</a><br>it's a shooter that can handle quite a bit of enemies,  bullets and player all interacting with each other. <br><br>its not complete but the up to date version has a tile scroller in the background and and multiple other extras.<br><br>and this is the main loop that control bullet and enemy interaction:<br><pre class=code>
	Method updatePlayerBullets()
		For Local b:Tbullet = EachIn playerbulletList
			If b.update() = False
				b.remove()
				Continue
			EndIf
			For Local e:tbase = EachIn EnemyList
				If b.collided(e,True)
					e.life:-b.damage
					b.remove()
					If e.life &lt;= 0
						e.remove();
						If e.found
							If e.found.found
								e.found.found = Null
							EndIf
							e.found = Null
						EndIf
						If e.name = "BOSS"
							Local ex:TexplosionGroup = e.explode(10)
							ex.link = ExplosionGroupList.addlast(ex)
						Else
							e.explode();
						EndIf
						score :+ e.value
					Else
						b.explode()
					EndIf
					Exit
				EndIf
			Next
		Next
	End Method
</pre><br>these code takes care of all types of enemies.<br>there are other ways of doing it but all of them use a loop nested in a loop. it may not be so obvious and/or may be quite a bit more complicated but at the core all of them do the same thing. There is no way around it.<br><br>what I would suggest is that you don't  combine graphics displaying along with logic processing.<br>You would need to be careful not to repeat display the same image on the screen as that would really deteriorate performance.<br><br><br>a little in on the game:<br>the game coding is 100% designed by me, the graphics are 90% designed by me, the other 10% are from a graphics library I bought a few years back.  I don't have any skills in creating animations and that's why I am using  the bought graphics.<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1093209"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matty</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> As ImaginaryHuman says - <br>if you have thousands of bullets (possible in some games such as my old multiplayer space combat sim) it is best to subdivide your world/universe into regions, be they a grid or otherwise, and only check for collisions between objects within the grid cells (or sometimes the adjacent grid cells as well).  You would create and maintain a list of objects within each grid cell, only updating the grid cells' lists when an object moves between the boundaries of one grid cell to another(remove from one cell, add to the other), or when an object is destroyed or created (add/remove from current cell).  <br><br>As Jesse says you will still need to do a nested for loop check...it just will be between a smaller number of items which is a good thing.<br><br>There is usually a trade off between memory and speed - ie often greater speed is gained at the cost of additional memory, while greater memory savings are gained at the cost of additional processing power...my style is to gain speed at the cost of memory - as mostly memory is cheaper... <br><br></td></tr></table><br>
<a name="1093432"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Zacho</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you for responding everyone. My internet did not want me to come on for quite some time :P but now I am able to get online. <br><br>@H&amp;K: I will not be having an immense ammount of bullets (~50 approx.) and I tested my code with my game and it works.<br><br>@ima747: I do not know how to do the superclass as you said earlier. It is something with abstract and method, am I right? At the moment I am getting my collision code right (which it is - I had to rewrite my whole main loop and functions) and then I will work at specializing my code<br><br>@ImaginaryHuman: I have no idea what you said :D (special partitioning act, acceleration structure) I am a bit more low-key than this, but the concept makes some sense. I just don't know how to code it and that is why I will be saving your sugggesstion for when I am better at programming.<br><br>@Jesse: Nice piece of code there. Now, I haven't gotten into methods yet so they are not in use in my code. I also followed your suggestion and didn't combine my graphics/logics, everything is split up, and it works! I have to thank you, a nested for...next for...next loop was what I needed, and I understand it too. It sounds interesting, you're game. yes i do have a pc so I couldn't help ya out by that. I will have to check out your game soon<br><br>@Matty: <pre class=code> You would create and maintain a list of objects within each grid cell, only updating the grid cells' lists when an object moves between the boundaries of one grid cell to another(remove from one cell, add to the other), or when an object is destroyed or created (add/remove from current cell). 
</pre> I understand everything but that ^ =D. Um, well.. I suppose I could do checks for x and y and then put whatever is in those in list300,300 or something like that. Yeah I suppose but I don't see why I would need to ever do this unless my code didn't work for me. I appreciate your feedback but this way of programming is a bit foreign to me BUT i will keep it in mind if I should ever use it.<br><br><br>To all of you, here is complete code that is working (constructive criticism :)) I would llike to thank you all for helping me get this far! I appreciate your time:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

'
'RAPTURE [UniversalRecon]
'
'
'Version 0.9 (DEBUG/INCOMPLETE)
'
'------------------------------------------
'TABLE OF CONTENTS (USE Ctrl + F to search)
'
'[1.1] = Framework
'[1.2] = Constants
'[1.3] = Types
'[1.4] = Images
'[1.5] = Lists
'
'[2.1] = Main Loop
'
'[3.1] = Functions
'[3.a] = StartGame ()
'
'------------------------------------------
'BUGS
'##########
'None =)
'
'TO-DO LIST
'##########
'Add cpu AI
'
'

'[1.1]
'GAME FRAMEWORK:

Strict 
AppTitle = "RaptURe - UniversalRecon [{Gam3rs Unit3}: ZaChO]"
AutoMidHandle(True)
Graphics 800,600 
SeedRnd MilliSecs()

'[1.2]
'CONSTANTS
Const LEFTKEY:Int = 37
Const RIGHTKEY:Int = 39
Const SPACEBAR:Int = 32
Const VKEY:Int = 86
Const ESC:Int = 27

'Change these later
	Const playerspeed:Int = 4
	Const cpuspeed:Int = 3

'[1.3]
'TYPES
Type spaceship
	Field x:Int,y:Int,frame:Int
	Field health:Int = 1, shield:Int
EndType 

Type bullettype
	Field x:Int
	Field y:Int = 560
	Field yv:Int = 6
	Field frame:Int
EndType

'
'MASK IMAGES' COLOR
SetMaskColor(0,0,0) 'Mask the color black


'[1.4]
'LOAD IMAGES)
Global playerimage:TImage=LoadImage("C:/Users/Zachary/Pictures/Rapture/Player.BMP")
Global cpuimage:TImage=LoadImage("C:/Users/Zachary/Pictures/Rapture/Cpu.BMP")
Global Cbulletimage:TImage=LoadAnimImage("C:/Users/Zachary/Pictures/Rapture/Cbullet.BMP",14,22,0,4)
Global Pbulletimage:TImage=LoadImage("C:/Users/Zachary/Pictures/Rapture/Pbullet.BMP")


'
'CREATE TYPES
Global player:spaceship = New spaceship
Global cpu:spaceship = New spaceship
Global Cbullet:bullettype = New bullettype
Global Pbullet:bullettype = New bullettype

'[1.5]
'CREATE THE LISTS
Global cpuList:TList = CreateList () 'List for cpu's
Global CbulletList:TList = CreateList () 'List for computer bullets
Global PbulletList:TList = CreateList () 'List for player bullets



StartGame ()



'[2.1]
' MAIN LOOP IS BELOW                                         MAIN LOOP IS BELOW                                 MAIN LOOP IS BELOW
' MAIN LOOP IS BELOW                                         MAIN LOOP IS BELOW                                 MAIN LOOP IS BELOW
' MAIN LOOP IS BELOW                                         MAIN LOOP IS BELOW                                 MAIN LOOP IS BELOW
'






'
'MAIN LOOP
While Not KeyHit(ESC) 
Cls

UserInput ()
CreateProjectiles ()
UpdateProjectiles ()
	'Includes CheckCollisons ()
CheckBoundry ()
Draw ()
	'Includes CheckCpu ()
	'Includes CheckPlayer ()
	'Includes HealthChecks ()

Flip 
Wend








'
' FUNCTIONS ARE BELOW                                    FUNCTIONS ARE BELOW                                   FUNCTIONS ARE BELOW
' FUNCTIONS ARE BELOW                                    FUNCTIONS ARE BELOW                                   FUNCTIONS ARE BELOW
' FUNCTIONS ARE BELOW                                    FUNCTIONS ARE BELOW                                   FUNCTIONS ARE BELOW
'










'[3.1]
'FUNCTIONS

Function StartGame () '[3.a]
 
	player.x = 10
	player.y = 550
	player.health = 100
	player.shield = 100
	
For Local cpuNum:Int = 1 To 10
	
	Local cpu:spaceship = New spaceship
		cpu.x = Rand(50,650) 
        cpu.y = Rand(50,130)
        cpu.health = Rand(5) * 5 + 20
  	ListAddLast(cpuList,cpu)

Next

	
End Function 

Function UserInput ()


'	
'PLAYER MOVEMENT		
	If KeyDown(LEFTKEY)
		player.x:-playerspeed
	EndIf 
	If KeyDown(RIGHTKEY)
		player.x:+playerspeed
	EndIf 

EndFunction 

Function CreateProjectiles ()

'
'FIRING 
	If KeyHit(SPACEBAR)
		Local Cbullet:bullettype = New bullettype
		Cbullet.x = Rand(0,700)
		Cbullet.y = cpu.y
		Cbullet.frame = 0
		ListAddLast(CbulletList,Cbullet)
	EndIf
	
	
	If KeyHit(VKEY) 
		Local Pbullet:bullettype = New bullettype
		Pbullet.x = player.x + 15
		Pbullet.y = player.y + 10
		ListAddLast(PbulletList,Pbullet)
	EndIf 
	

	
EndFunction 

Function UpdateProjectiles ()

'
'PLAYER BULLETS
	For Local Pbullet:bullettype = EachIn PbulletList
		Pbullet.y:- Pbullet.yv
	Next

'	
'COMPUTER BULLETS			
	For Local Cbullet:bullettype = EachIn CbulletList
		Cbullet.y:+ Cbullet.yv
		Cbullet.frame:+1
			If Cbullet.frame &gt; 3
				Cbullet.frame = 0
			EndIf
	Next 


CheckCollisions ()


EndFunction 

Function Draw ()
	

'		
'Pbullet CHECKLIST		
		
	For Local Pbullet:bullettype = EachIn PbulletList	
		CheckCpu ()
	Next 		

'		
'Cbullet CHECKLIST						
	For Local Cbullet:bullettype = EachIn CbulletList
		CheckPlayer ()
		Exit		
	Next 
	 		
	HealthChecks () 			
						
'
'DEBUG
DrawText"Player x" + player.x,55,0
DrawText"player y" + player.y,55,10
DrawText"x" + MouseX(),300,0
DrawText"y" + MouseY(),300,10
DrawText"Player hp =" + player.health,650,30

For Local cpu:spaceship = EachIn cpuList
	DrawText"Cpu hp =" + cpu.health,cpu.x,cpu.y - 10
Next
	
End Function 


Function CheckCpu ()

	For Local cpu:spaceship = EachIn cpuList
			
	'	
	'BULLET COLLISION BETWEEN CPU AND PLAYER BULLETS	
		If ImagesCollide(cpuimage,cpu.x,cpu.y,0,Pbulletimage,Pbullet.x,Pbullet.y,0)
			cpu.health:-1
			ListRemove(PbulletList,Pbullet)
			Exit
		EndIf 
		
	Next 
EndFunction 

Function CheckPlayer ()

	If ImagesCollide(Cbulletimage,Cbullet.x,Cbullet.y,0,playerimage,player.x,player.y,0)		
		ListRemove (CbulletList,Cbullet) 
	EndIf 



EndFunction 
 
Function CheckBoundry ()

'	
'COMPUTER BULLETS			
	For Local Cbullet:bullettype = EachIn CbulletList
		If Cbullet.y &gt; 575
			ListRemove(CbulletList,Cbullet)
		Else
			DrawImage Cbulletimage:TImage,Cbullet.x,Cbullet.y,Cbullet.frame
		EndIf 
	Next

'	
'PLAYER BULLETS			
	For Local Pbullet:bullettype = EachIn PbulletList
		If Pbullet.y &lt; 0
			ListRemove(PbulletList,Pbullet)
		Else
			DrawImage Pbulletimage:TImage,Pbullet.x,Pbullet.y
		EndIf
	Next 
	
'	
'PLAYER
	If player.x &lt;= 0
		player.x = 0
	Else If player.x &gt;= 756
		player.x = 756
	EndIf 	 
	
	
EndFunction



Function CheckCollisions ()

	For Local Cbullet:bullettype = EachIn CbulletList
	
		If ImagesCollide(Cbulletimage,Cbullet.x,Cbullet.y,0,playerimage,player.x,player.y,0)
				player.health:-1
				
					If player.health &lt; 0
						player.health = 0
					EndIf
				
				ListRemove (CbulletList,Cbullet)
		EndIf 
	Next

	For Local Pbullet:bullettype = EachIn PbulletList
	For Local cpu:spaceship = EachIn cpuList

		If ImagesCollide(Pbulletimage,Pbullet.x,Pbullet.y,0,cpuimage,cpu.x,cpu.y,0)
				cpu.health:-1 
				ListRemove (PbulletList,Pbullet)
				Exit 
			EndIf
	Next 
	Next


EndFunction 

Function HealthChecks ()
	
	If player.health &gt; 0
		DrawImage playerimage,player.x,player.y
	ElseIf player.health &lt; 0
	
	EndIf 
	
	
'	
'CPU HEALTH CHECKS	
	For Local cpu:spaceship = EachIn cpuList	
		If cpu.health &gt; 0
			DrawImage cpuimage:TImage,cpu.x,cpu.y
		ElseIf cpu.health &lt; 1
			ListRemove(cpuList,cpu)
		EndIf
	Next
	
EndFunction 
</textarea><br><br>Annddd to keep this thread alive, my next step in my game is cpu AI. I want to have the ships either meander about in their initial x,y position or follow the player (the obvious reason for the first choice so the player isn't overwhelmed in higher levels). I am curious on how to write the code for this, as I can't have cpu x,y like a jitterbug (updating/changing position at each loop). Do I need a timer (and I haven't worked with timer's before :o) and once this expires then update the cpu x,y? What are your beginner-friendly suggestions? <br><br></td></tr></table><br>
<a name="1093450"></a>

<a name="1093454"></a>

<a name="1093455"></a>

<a name="1093457"></a>

<a name="1093460"></a>

<a name="1093472"></a>

<a name="1093473"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I noticed that you have checkplayer and checkcpu that do the same as checkcollision. just get rid of the checkplayer and checkcpu and remove checkcollision from the createProjectiles and put it in the main loop after updateProjectiles that way you are not doing redundant checks.<br><br>methods are really easy and if you want to get an idea on how to use them I'll show you in your code<br><br>see how you have this code in your program:<br><pre class=code>
Type bullettype
	Field x:Int
	Field y:Int = 560
	Field yv:Int = 6
	Field frame:Int
EndType
</pre><br>and this:<br><pre class=code>
	For Local Cbullet:bullettype = EachIn CbulletList
		Cbullet.y:+ Cbullet.yv
		Cbullet.frame:+1
			If Cbullet.frame &gt; 3
				Cbullet.frame = 0
			EndIf
	Next 

</pre><br><br>you can move the bullet type variables into a method:<br><pre class=code>

Type bullettype
	Field x:Int
	Field y:Int = 560
	Field yv:Int = 6
	Field frame:Int
	
	Method update()
		y:+ yv
		frame:+1
		If frame &gt; 3
			frame = 0
		EndIf
		
	End Method
EndType
</pre><br>notice how it doesn't have the cbullet variabel. It changes into a direct access.<br>and then use it like this:<br><pre class=code>
	For Local Cbullet:bullettype = EachIn CbulletList
			Cbullet.update()
	Next 
</pre><br>it's that simple.<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1093493"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Zacho</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> So calling a method inside of a type is like a function. This function will be specific to only that type. Operations inside of a method don't need to use the type name (Cbullet.y : the Cbullet part) because it is called within that type. And then this method is called similar to a function, using the type name followed by a . And then the method? Did I get this all right? <br><br>If this is true then isn't it possible for me to put the collision detection in a method as well? <br><br></td></tr></table><br>
<a name="1093502"></a>

<a name="1093503"></a>

<a name="1093505"></a>

<a name="1093508"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes, All true. <br><br>Just remember that <b>only</b> fields with in that type can be accessed directly. Different "type" variables can not be accessed directly within the current "type method".<br><br>This is the brake down:<br>Only the fields belonging to the instance can be accessed directly from with in the method.<br>Methods are only accessible if there is an instance of an object for example creating an object with "New".<br>A method can not access fields from other objects(Types) unless you pass them as parameters or are declared as global similar to the way functions work. <br><br>Any object(type) that is passed to a method must be used with the complete name conventions.<br>such as "bullet.xpos = 33".<br><br>the idea about objects is to try to keep everything related to the object with in the object itself. that include a field for the image. this will make collision a lot easier to process:<br><pre class=code>
Type spaceship
	Field x:Int,y:Int,frame:Int
	Field health:Int = 1, shield:Int
	Field image:TImage '&lt;----------------added this
EndType 
</pre><br>and this:<br><pre class=code>	
Type bullettype
	Field x:Int
	Field y:Int = 560
	Field yv:Int = 6
	Field frame:Int
	Field image:TImage '&lt;----------------added this
EndType

</pre><br><br>that way when you have to modify or add code you won't need to remember which image is for what.<br>but when you instantiate  the object(create with "New") you <b>will</b> have to assign the specific image to the instance and is the only time you will have to worry about it:<br><pre class=code>
	If KeyHit(SPACEBAR)
		Local Cbullet:bullettype = New bullettype
		Cbullet.x = Rand(0,700)
		Cbullet.y = cpu.y
		Cbullet.frame = 0
		Cbullet.Image = CbulletImage '&lt;---------------added this
		ListAddLast(CbulletList,Cbullet)
	EndIf
</pre><br><br>that way when you add the collision to the "player type", it will be a lot simpler:<br><pre class=code>

Type spaceship
	Field x:Int,y:Int,frame:Int
	Field health:Int = 1, shield:Int
	Field image:TImage '&lt;----------------added this

   Method collided:Int(bullet:bulletType)
         If ImagesCollide(image,x,y,0,   bullet.image,bullet.x,bullet.y,bullet.frame)
			health :- 1
			If health &lt; 0
				health = 0
			EndIf
             Return True
        EndIf
        Return False
    End Method

End Type
</pre><br><br>and use it like this:<br><pre class=code>

	For Local Cbullet:bullettype = EachIn CbulletList
		If player.collided(cbullet)			
			ListRemove (CbulletList,Cbullet)
		EndIf 
	Next
</pre><br><br><font class="tiny">Last edited 2011</font><br><br><font class="tiny">Last edited 2011</font><br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1093506"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Zacho</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you Jesse. Your explanation was superb but I am getting a little confused with <br><br><pre class=code>
Field image:TImage
</pre><br><br>inside of the Type spaceship. It doesn't click with me. I will look at it later, I just need some time for the "Method" to sink in with me so I can get familiar with that first. I will look over and reply later. thanks again! <br><br></td></tr></table><br>
<a name="1093509"></a>

<a name="1093510"></a>

<a name="1093515"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> I just noticed that I copied the spaceship type twice. this has been corrected in the samples. was that what was confusing you?<br>other than Timage is a type and image holds the address where the actual Timage instance  is stored.<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1093517"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Zacho</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> No, I figured that that was a typo. I don't quite understand drawing the actual image if I put the image in a type in the bullettype. These three lines are particularly confusing me and I don't know if you can help explain them.<br><br><pre class=code>
Field image:TImage '&lt;----------------added this

   Method collided:Int(bullet:bulletType)
         If ImagesCollide(image,x,y,0,   bullet.image,bullet.x,bullet.y,bullet.frame)
</pre><br><br>Starting with the first line, you declare a new field in the type of the bullettype named image. I know a colon (:) identifies what the field is, either a number, string or etc. I understand that part. What I don't or never understood was why you have to put TImage there. It is never in yellow font or anything so I didn't think BMax recognized it.<br><br>The Second line. You declare a method called "collided" that is a integer. Why is it an integer? Another question I never understood and didn't want to get confused with is why you have "bullet:bulletType" in ( ) after calling the method. If I use F1 when coding over "Method" the help description is no help. Is "bullet:bulletType" after the method supposed to help coding but provide no other purpose. I am just confused as what this is used for<br><br>Lastly I don't know where to load the image and stuff with this line. If you put in a field called "image:TImage" do you put and = sign after and then the image url? Is the image created/loaded after? Does this change the way I have to load the types in conjunction with the images? Particularly the "bullet.image" part I am confused with, where do I load it? Within the method? type? <br><br></td></tr></table><br>
<a name="1093518"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> I just read the first post (not the entire thread) but if you want to switch, the most efficient Nearest Neighbor search I have found is using a grid.  Its really fast especially if you have 10's of thousands of bullets on the screen.  If I used a nested loop for my fluid engine it would only be able to handle a few hundred particles instead of thousands of particles. (not sure why you would want thousands of bullets on the screen though) ;) <br><br></td></tr></table><br>
<a name="1093520"></a>

<a name="1093522"></a>

<a name="1093523"></a>

<a name="1093525"></a>

<a name="1093526"></a>

<a name="1093528"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> note that you still have to load the globals for the images:<br><pre class=code>
Global playerimage:TImage=LoadImage("C:/Users/Zachary/Pictures/Rapture/Player.BMP")
</pre><br>and the spaceship type will now include the image field:<br><pre class=code>
Type spaceship
	Field x:Int,y:Int,frame:Int
	Field health:Int = 1, shield:Int
	Field image:TImage '&lt;----------------added this
EndType 
</pre><br>now when you create the player type, you create it like this:<br><pre class=code>
	global player = new spaceship
	player.x = 10
	player.y = 550
	player.health = 100
	player.shield = 100
	player.image = playerImage '&lt;--------------added this
</pre><br>you asign the image to the player just like you asign the any other variable to the player<br>now if you want to display it all you have to do is this:<br><pre class=code>
DrawImage player.image,player.x,player.y
</pre><br>now if you want to display it from within the type instead of directly, you first have to create a method with in the type.<br>remember that player is a spaceship type. so you can do this:<br><pre class=code>
Type spaceship
	Field x:Int,y:Int,frame:Int
	Field health:Int = 1, shield:Int
	Field image:TImage '&lt;----------------added this

	Method display()
		DrawImage image,x,y '&lt;------------note this will display the current object image
	End Method

EndType
</pre><br><br>so if I want to display it through the type, all I have to do is do this.<br><pre class=code>
player.display()
</pre><br><br>and so you will know, any type can be assigned to a variable field.<br><pre class=code>
field monster:Tenemy
</pre><br>and is perfectly valid.<br><br>about Timage:<br>Timage is a module in blitzmax. it is highlited in the newst release only.  it was not highlighted because of blitzmax inconsistencies. <br>the reason it don't show or was highlited is because it was not documented by Mark at all by. <br>you can look through BlitxMax source and you will see a file name "image.bmx"  that is where the information about TImage is stored. but unlike any other module by BRL it is not documented.<br><br><font class="tiny">Last edited 2011</font><br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1093532"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> a method works exactly like a function <br>this is a function:<br><pre class=code>
function addition()
  print "ok"
end function
</pre><br>that is a basic function and I am sure you know how to use it.<br><br>but if I want to send a value to a function I create it like this:<br><pre class=code>
function show(a:int)
   print a
end function
</pre><br><br>I can call it like tis:<br><pre class=code>
show(20)
</pre><br><br>and 20 will be assigned to the variable a in the function and when it executes it will display "20"<br>and that is how you pass values to a function.<br>now you can also pass values in a variable to a function:<br><pre class=code>
function show(a:int)
    print a
end function

local b:int = 20
show(b)
</pre><br>this code will pass the value in variable "b" to the variable "a" in the function show<br>and the function wil print "20" to the console<br><br>a function can receive and can return values:<br>this function will return a value:<br><br><pre class=code>
function get:int()
      local n = 10
      return n
end function

local b:int = get()
print b
</pre><br>the "get:int" tells the function it needs to return anteger.<br>and the "return" tells the function what to return<br>now methods work the same way:<br><pre class=code>
Method collided:Int(bullet:bulletType)
</pre><br>in this case the collided method expect to receive the object( of bulletType)  and return an integer<br>1 if there was a collision and 0 if there was none.<br>0 = false<br>1 = true<br>that is what is returned from the test from imagesCollide. <br><br></td></tr></table><br>
<a name="1093535"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Zacho</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> This<br><br><pre class=code>

Global <b>playerimage</b>:TImage=LoadImage("C:/Users/Zachary/Pictures/Rapture/Player.BMP")


	global player = new spaceship
               ....
               ....
               ....
	player.image = <b>playerImage</b> '&lt;--------------added this
</pre><br><br>made LOADS of sense to me. By creating a new player, and setting the player.image field to the actual image; you can then draw the player through drawing the player.image field. Makes sense now :)<br><br>And it makes sense that I can call player.display() and cpu.display() from: <br><pre class=code>
Type spaceship
	Field x:Int,y:Int,frame:Int
	Field health:Int = 1, shield:Int
	Field image:TImage '&lt;----------------added this

	Method display()
		DrawImage image,x,y '&lt;------------note this will display the current object image
	End Method

EndType
</pre> <br>to make drawing the enemy, the coding more compacted. Thank you. I can see myself re-doing this whole coding process, re-doing my whole game. I don't know if I should do this or if I should try to finish my game and re-do it after... I don't know what is better for my programming learning proccess. I know I want to get some more practice with an animage and maybe a method or two. I do not think I will implore the use of a function calling an integer (for now). I will save that for later :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
