<!DOCTYPE html><html lang="en" ><head ><title >Bouncing collision response</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Bouncing collision response</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >Bouncing collision response</a><br><br>
<a name="985708"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Leo Santos</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi everybody. I'm having trouble achieving proper collision response based on a collision normal. I'm a complete noob when it comes to math, so even though I've looked through all examples I could find (Snooker balls physics, the Billiards example by Joseph 'Phish' Humfrey, etc.) I end up in trouble trying to adapt their solutions to mine, simply because I'm doing it blindly - I don't really understand what's going on, so when something doesn't work, it's very hard to know what to do...<br><br>That said, I got much farther than I had before, this time using polygonal/circle collisions, mostly thanks to Dave 'Oddball' Williamson poly collisions code. The ONLY thing I'm still having trouble is achieving a bounce against the collision object.<br><br>Here's some example code I took from the main program and simplified so it only has circle to circle collisions. Please note that I'm not trying to move both objects in the collision, only the "MainBall" object.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'******************* Basic setup ********************************
SuperStrict
Const Xres:Int = 800
Const Yres:Int = 600
Graphics Xres,Yres,0,60

'******************* Create Scene ********************************
Local BallList:TList = CreateList()

For Local N:Int = 1 To 100
	Local NewBall:Ball = New Ball
	NewBall.X = Rand(0,Xres)
	NewBall.Y = Rand(0,Yres)
	NewBall.R = Rand(5,20)
	BallList.Addlast(NewBall)
Next

Local MainBall:Ball = New Ball
MainBall.X = Xres/2
MainBall.Y = Yres/2
MainBall.R = 10
MainBall.SpeedX = 1
MainBall.SpeedY = 1

'******************* Main Loop ************************************
While Not AppTerminate() And Not KeyDown(Key_ESCAPE)
	Cls
	MainBall.Move()
	For Local Obj:Ball = EachIn BallList
		Obj.Draw()
		MainBall.Collide(Obj)
	Next
	MainBall.Draw()
	MainBall.DisplayInfo()
	Flip
Wend

'******************* Types ****************************************
Type Ball

	Field X:Float
	Field Y:Float
	Field R:Float
	Field SpeedX:Float
	Field SpeedY:Float
	Field SpeedTotal:Float
	Field CollisionAngle:Float

	Method Move()		'Applies speed and keeps ball inside screen
		X :+ SpeedX
		Y :+ SpeedY
		SpeedTotal = Sqr((SpeedX*SpeedX)+(SpeedY*SPeedY))
		If X &gt; Xres
			X= Xres
			SpeedX :* -1
		EndIf
		If X &lt; 0
			X=0
			SpeedX :*-1
		EndIf
		If Y &gt; Yres
			Y = Yres
			SpeedY :*-1
		EndIf
		If Y &lt; 0
			Y=0
			SpeedY :*-1
		EndIf		
	End Method
	
	Method Draw()		'Draws a hollow circle
		SetColor 0,150,255
		Plot X,Y
		Local PX:Int = X
		Local Py:Int = R + Y		
		For Local Angle:Int = 0 To 360 Step 5
			Local Nx:Int = Sin(Angle) * R + X
			Local Ny:Int = Cos(Angle) * R + Y
			DrawLine (Px, Py, Nx, Ny, False) 
			Px = Nx
			Py = Ny
		Next
	EndMethod

	Method Collide(Obj:Ball)
		Local Distance:Float = Sqr((Obj.X-X)*(Obj.X-X)+(Obj.Y-Y)*(Obj.Y-Y))
		Local TouchDistance:Float = R + Obj.R
		If Distance &lt; TouchDistance
			'Collision Ocurred,
			'Detects angle of collision
			CollisionAngle = ATan2(Obj.Y-Y, Obj.X-X)+180
			'Move out of collision
			X :+ (TouchDistance - Distance) * Cos(CollisionAngle)
			Y :+ (TouchDistance - Distance) * Sin(CollisionAngle)
			'Sets new Speed
			SpeedX = SpeedTotal * Cos(CollisionAngle)
			SpeedY = SpeedTotal * Sin(CollisionAngle)
		EndIf
	EndMethod
	
	Method DisplayInfo()
		SetColor 240,240,0
		DrawText "SpeedX: "+SpeedX,10,Yres-20
		DrawText "SpeedX: "+SpeedY,200,Yres-20
		DrawText "SpeedTotal: "+SpeedTotal,400,Yres-20
		SetColor 0,250,50
		DrawText "CollisionAngle: "+Int(CollisionAngle),10,Yres-40
	EndMethod
	
End Type
</textarea><br><br>It seems to work at first, but then you realize that the ball is bouncing exactly at the collision normal angle, instead of "mirroring" its original movement at the collision point. I know where the problem is (at the "Set new speed" section), I just don't know how to fix it...<br><br>Any help would be greatly appreciated.<br>Thanks! <br><br></td></tr></table><br>
<a name="985710"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warpy</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> <a href="http://warpycode.wordpress.com/2009/06/17/making-things-bounce/" target="_blank">making things bounce</a> <br><br></td></tr></table><br>
<a name="985715"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Leo Santos</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Fantastic! That was quick!  :-D<br>I'm adding a bookmark to your page, Christian, it is really helpful...<br><br>Here's the fixed code, it may help someone else out there:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'******************* Basic setup ********************************
SuperStrict
Const Xres:Int = 800
Const Yres:Int = 600
Graphics Xres,Yres,0,60

'******************* Create Scene ********************************
Local BallList:TList = CreateList()

For Local N:Int = 1 To 100
	Local NewBall:Ball = New Ball
	NewBall.X = Rand(0,Xres)
	NewBall.Y = Rand(0,Yres)
	NewBall.R = Rand(5,10)
	BallList.Addlast(NewBall)
Next

Local MainBall:Ball = New Ball
MainBall.X = Xres/2
MainBall.Y = Yres/2
MainBall.R = 10
MainBall.SpeedX = 2
MainBall.SpeedY = 2

'******************* Main Loop ************************************
While Not AppTerminate() And Not KeyDown(Key_ESCAPE)
	Cls
	MainBall.Move()
	For Local Obj:Ball = EachIn BallList
		Obj.Draw()
		MainBall.Collide(Obj)
	Next
	MainBall.Draw()
	MainBall.DisplayInfo()
	Flip
Wend

'******************* Types ****************************************
Type Ball

	Field X:Float
	Field Y:Float
	Field R:Float
	Field SpeedX:Float
	Field SpeedY:Float
	Field SpeedTotal:Float
	Field CollisionAngle:Float

	Method Move()		'Applies speed and keeps ball inside screen
		X :+ SpeedX
		Y :+ SpeedY
		SpeedTotal = Sqr((SpeedX*SpeedX)+(SpeedY*SPeedY))
		If X &gt; Xres
			X= Xres
			SpeedX :* -1
		EndIf
		If X &lt; 0
			X=0
			SpeedX :*-1
		EndIf
		If Y &gt; Yres
			Y = Yres
			SpeedY :*-1
		EndIf
		If Y &lt; 0
			Y=0
			SpeedY :*-1
		EndIf		
	End Method
	
	Method Draw()		'Draws a hollow circle
		SetColor 0,150,255
		Plot X,Y
		Local PX:Int = X
		Local Py:Int = R + Y		
		For Local Angle:Int = 0 To 360 Step 8
			Local Nx:Int = Sin(Angle) * R + X
			Local Ny:Int = Cos(Angle) * R + Y
			DrawLine (Px, Py, Nx, Ny, False) 
			Px = Nx
			Py = Ny
		Next
	EndMethod

	Method Collide(Obj:Ball)
		Local Distance:Float = Sqr((Obj.X-X)*(Obj.X-X)+(Obj.Y-Y)*(Obj.Y-Y))
		Local TouchDistance:Float = R + Obj.R
		If Distance &lt; TouchDistance
			'Collision Ocurred,
			'Detect angle of collision
			CollisionAngle = ATan2(Obj.Y-Y, Obj.X-X)+180
			'Move out of collision
			X :+ (TouchDistance - Distance) * Cos(CollisionAngle)
			Y :+ (TouchDistance - Distance) * Sin(CollisionAngle)
			'Sets new Speed
			Local Nx:Float = Cos(CollisionAngle)
			Local Ny:Float = Sin(CollisionAngle)
			'This section adjusted with code by Christian "Warpy" Perfect
			'p is the projection of V onto the normal
			Local Px:Float, Py:Float
			Local Dotproduct:Float = SpeedX*Nx+SpeedY*Ny
			Px = Dotproduct*Nx
			Py = Dotproduct*Ny
			'the velocity after hitting the wall is V - 2p, so just subtract 2*p from V
			SpeedX = SpeedX - 2*Px
			SpeedY = SpeedY - 2*Py
		EndIf
	EndMethod
	
	Method DisplayInfo()
		SetColor 240,240,0
		DrawText "SpeedX: "+SpeedX,10,Yres-20
		DrawText "SpeedX: "+SpeedY,200,Yres-20
		DrawText "SpeedTotal: "+SpeedTotal,400,Yres-20
		SetColor 0,250,50
		DrawText "CollisionAngle: "+Int(CollisionAngle),10,Yres-40
	EndMethod
	
End Type
</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
