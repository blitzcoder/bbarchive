<!DOCTYPE html><html lang="en" ><head ><title >online multipayer games</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >online multipayer games</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >online multipayer games</a><br><br>
<a name="947705"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >B</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>I was looking at the commands and such for creating online games that use servers and such.<br><br>I have no idea how to use these at all, and i looked through the forums, and was unable to find anything.<br><br>I would like to know how one goes about creating a game in which two people can connect to a server and player1 can use the arrow keys to move his character (a blue square) and player2 can use the arrow keys to move his character (a green square) around the screen and both players can see each others movements.<br><br>I feel that if I were able to do this then I could implement the rest of my ideas into it without nagging everybody on the forums.<br><br>Thanks!<br><br>B <br><br></td></tr></table><br>
<a name="947738"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AltanilConard</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is how a simple multiplayer game works: <br>There is one server that holds all the information about all the players, like there position. Every client (player) is connected to the server. Lets say there are two players online. When Player A takes a step forward, it will tell the server about his movement. The server will check if the move is legal, and then notify Player B about the movement of Player A. The client of Player B will now process the movement and he will see Player A moving forward.<br><br>This 'talking' between the client and the server happens trough sockets. Every time the player tells the server about his movement, he sends a packet (array of bytes) to the server, wich will hold all the information the server needs to know. You might want to look up about sockets on the internet. Blitzmax natively supports sockets, you can look at the Sockets documentations in the help menu. To start with an online game is quite a big step into the world of network programming, I suggest you first try to create a simple chat client/server or something simmilar. A while ago I posted an example of a client/server structure <a href="/posts.php?topic=69378#776787" target="_blank">here</a> (post #12), you might find it usefull to get started.<br><br>If I find the time I'll create a small example to demonstrate what I've been saying. I hope this helped a little. <br><br></td></tr></table><br>
<a name="947739"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Zethrax</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Your best bet is to have a squizz through the code archives. Theres a few multiplayer examples in there. There may also be some multiplayer source code libs in the toolbox section of the site, which could be worth a look at. <br><br></td></tr></table><br>
<a name="947827"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >B</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> ok thanks guys.<br><br>@Conard<br>ill have a look at the link you posted, and try to wrap my head around all that you said.<br>it makes sense, so now I just have to think about how I would program it.<br>If you have the time a demonstration would be most helpful/appreciated!<br>Thanks! :)<br><br><br>@Bill Stanbrook<br>I have looked in the code archives and didnt find anything, ill have another go however.<br>and Ill also have a go at the toolbox.<br>Thanks!<br><br>THanks again guys.<br>Cheers!<br><br>B <br><br></td></tr></table><br>
<a name="949026"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >B</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> hey Conrad,<br><br>i dont wanna nag, but have you found time to create an example program?<br><br>I have been researching the subject and everything, as well as other people's tutorials/examples, are really confusing me.<br><br>this is alot harder than I thought.<br><br>Sorry if this is nagging.<br><br>Thanks again!<br><br>B <br><br></td></tr></table><br>
<a name="949480"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AltanilConard</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> It is quite complex indeed. I've cleaned up some old code on my computer that demonstrates how multiple players can walk around and see each other. This code is really simple and probably quite inefficient, but it could help you to understand how an ORPG works. To test it, you should compile both the client and the server, copy the client a couple of times, start the server, then open a few clients. Every player represents a white rectangle.<br><br>Just study the code until you understand it a bit, then you could implement some features. You should make the server check each players movement/timing, so all players will actually be positioned the same on every client. Then you could add some features, like account creation, chat system etc.<br><br>If you don't understand the code at all, you should first learn some more about network  programming: create a little chatbox or a multiplayer tic-tac-toe game.<br><br>client.bmx:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

' some constants
Const SERVER_PORT% = 331122
Const SERVER_IP$ = "127.0.0.1"
Const MAX_PLAYERS% = 10
Const TILE_SIZE% = 10
Const DIR_UP:Byte = 1
Const DIR_DOWN:Byte = 2
Const DIR_LEFT:Byte = 3
Const DIR_RIGHT:Byte = 4

' id's of packets
Const P_ID_REQUEST_INFO:Byte = 5
Const P_ID_MOVE:Byte = 8
Const P_ID_ACCEPT:Byte = 11
Const P_ID_DECLINE:Byte = 12
Const P_ID_NEW_PLR:Byte = 3
Const P_ID_PLR_LOGOUT:Byte = 66

' the socket and its stream
Global client:TSocket
Global client_stream:TStream

' array that holds all players and our player id
Global players:TPlayer[]
Global MainPlayerID%

' if we want to quit the main loop
Global quit% = False


'-----------------------
' Setup client
'-----------------------
Graphics 800,600
InitNetwork()
If Not client Or Not client_stream quit = True
players = New TPlayer[MAX_PLAYERS]
If Not quit SendLoginPacket()


'-----------------------
' Main loop
'-----------------------
While Not quit
	
	' check if there is something to read on the socket
	If client.ReadAvail() &gt; 0
		' read the packet
		Local packet_size:Byte = client_stream.ReadByte()
		Local packet_bank:TBank = CreateBank(packet_size)
		ReadBank(packet_bank, client_stream, 0, packet_size)
		HandlePacket(packet_bank)
	EndIf
	
	' handle input
	If KeyHit(KEY_ESCAPE) quit = True
	
	' handle movement
	If Not players[MainPlayerID].moving
		If KeyDown(KEY_UP) 
			players[MainPlayerID].move(DIR_UP)
			SendMovePacket(DIR_UP)
		ElseIf KeyDown(KEY_DOWN) 
			players[MainPlayerID].move(DIR_DOWN)
			SendMovePacket(DIR_DOWN)
		ElseIf KeyDown(KEY_LEFT) 
			players[MainPlayerID].move(DIR_LEFT)
			SendMovePacket(DIR_LEFT)
		ElseIf KeyDown(KEY_RIGHT) 
			players[MainPlayerID].move(DIR_RIGHT)
			SendMovePacket(DIR_RIGHT)
		EndIf
	EndIf
	
	' draw all players
	For Local i%=0 Until MAX_PLAYERS
		If players[i] players[i].draw()
	Next
	
	' update screen
	Flip()
	Cls()
Wend


'-----------------------
' Functions
'-----------------------
Function InitNetwork()
	client = CreateTCPSocket()
	
	If Not client.Connect(HostIp(SERVER_IP), SERVER_PORT)
		Print("Could not connect to the server!")
		client.Close()
		Return
	EndIf
	client_stream = CreateSocketStream(client)
	Print("Connected to server")
End Function

Function HandlePacket(packet:TBank)
	Local id%,i%,player_id%,dir%
	
	' identiy the packet
	id = packet.PeekByte(0)
	Select id
		' add a new player to the game
		Case P_ID_NEW_PLR
			' look for empty slot
			players[packet.PeekByte(1)] = TPlayer.Create()
		
		' handle movment
		Case P_ID_MOVE
			' find the player that moves
			player_id = packet.PeekByte(1)
			dir = packet.PeekByte(2)
			players[player_id].move(dir)
			
		' handle player logout packet
		Case P_ID_PLR_LOGOUT
			' remove the player
			player_id = packet.PeekByte(1)
			players[player_id] = Null
	End Select
End Function

Function SendLoginPacket()
	Local packet_bank:TBank,packet_size:Byte,time:Double
	Local id:Byte,ofs%,i%,numPlayers:Byte,p_id:Byte
	
	' setup the packet
	packet_size = 1 ' currently only our packet ID.
	packet_bank = CreateBank(packet_size + 1) ' + 1 for packet_size byte which we'll also send
	packet_bank.PokeByte(0, packet_size)
	packet_bank.PokeByte(1, P_ID_REQUEST_INFO)
	
	' send the packet
	WriteBank(packet_bank, client_stream, 0, packet_size + 1)
	
	' wait for a respons for about 5 seconds
	time = MilliSecs()
	While 1
		If client.ReadAvail() &gt; 0
			' read in the packet
			ofs = 0
			packet_size = client_stream.ReadByte()
			packet_bank = CreateBank(packet_size)
			ReadBank(packet_bank, client_stream, 0, packet_size)
			id = packet_bank.PeekByte(ofs)
			ofs :+ 1
			MainPlayerID = packet_bank.PeekByte(ofs)
			ofs :+ 1
			numPlayers = packet_bank.PeekByte(ofs)
			ofs :+ 1
			
			If id = P_ID_ACCEPT
				For i=0 Until numPlayers
					' read info about all players
					players[i] = TPlayer.Create()
					ofs :+ 1
					players[i].posX = packet_bank.PeekInt(ofs)
					ofs :+ 4
					players[i].posY = packet_bank.PeekInt(ofs)
					ofs :+ 4
				Next
					
				Return
			ElseIf id = P_ID_DECLINE
				quit = True
				Print "Server is full"
				Return
			Else
				quit = True
				Print "Packet error"
				Return
			EndIf
		EndIf
		
		If MilliSecs()-time &gt; 5000
			Print "Server time-out"
			quit = True
			Return
		EndIf
	Wend
				
End Function

Function SendMovePacket(dir:Byte)
	Local packet_bank:TBank,packet_size%,ofs%
	
	' create packet
	packet_size = 1 + 1 ' id + move_dir
	packet_bank = CreateBank(packet_size + 1)
	packet_bank.PokeByte(0, packet_size) ; ofs :+ 1
	packet_bank.PokeByte(ofs, P_ID_MOVE) ; ofs :+ 1
	packet_bank.PokeByte(ofs, dir)
	
	' send packet
	WriteBank(packet_bank, client_stream, 0, packet_size + 1)
End Function


'-----------------------
' Player type
'-----------------------
Type TPlayer
	Field posX%, posY%
	Field moving%, moveTimer%, speed% ' speed = millisecs/step
	
	Method move(dir:Byte)
		If Not moving
			moving = True
			moveTimer = MilliSecs()
			Select dir
				Case DIR_UP
					posY :- 1
				Case DIR_DOWN
					posY :+ 1
				Case DIR_LEFT
					posX  :- 1
				Case DIR_RIGHT
					posX  :+ 1
				Default
					moving = False
			End Select
		EndIf
	End Method
	
	Method draw()
		If moving
			If (MilliSecs() - moveTimer &gt;= speed) moving = False
		EndIf
		
		DrawRect(posX * TILE_SIZE, posY * TILE_SIZE, 32, 32)
	End Method
	
	Function Create:TPlayer()
		Local p:TPlayer = New TPlayer
		p.moving = False
		p.speed = 200
		Return p
	End Function
End Type
</textarea><br><br>serrver.bmx:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

' some constants
Const MAX_PLAYERS% = 10
Const SERVER_PORT% = 331122
Const DIR_UP:Byte = 1
Const DIR_DOWN:Byte = 2
Const DIR_LEFT:Byte = 3
Const DIR_RIGHT:Byte = 4

' id's of packets
Const P_ID_REQUEST_INFO:Byte = 5
Const P_ID_MOVE:Byte = 8
Const P_ID_ACCEPT:Byte = 11
Const P_ID_DECLINE:Byte = 12
Const P_ID_NEW_PLR:Byte = 3
Const P_ID_PLR_LOGOUT:Byte = 66

' the server socket and server info
Global server_socket:TSocket
Global playersOnline%

' array that contains all players
Global players:TPlayer[]



'-----------------------
' Setup server
'-----------------------
Graphics 500,300
InitServer()
players = New TPlayer[MAX_PLAYERS]


'-----------------------
' Main loop
'-----------------------
While Not KeyHit(KEY_ESCAPE)
	Local  temp_socket:TSocket,temp_stream:TStream,i%
	
	' listen for new players
	temp_socket:TSocket = server_socket.Accept(0)
	If temp_socket
		For i=0 Until MAX_PLAYERS
			If players[i] = Null
				players[i] = TPlayer.Create(temp_socket)
				players[i].status = 1 ' waiting for login
				playersOnline :+ 1
				Exit
			EndIf
		Next
		' TODO: notify the player that the server if full
	EndIf
	
	' update network
	For i=0 Until MAX_PLAYERS
		If players[i]
			' check if stream is still up
			If Not players[i].stream.Eof()
				' check if there is something to read on the stream
				If players[i].socket.ReadAvail()
					' read in the packet
					Local packet_size:Byte = players[i].stream.ReadByte()
					Local packet_bank:TBank = CreateBank(packet_size)
					ReadBank(packet_bank, players[i].stream, 0, packet_size)
					HandlePacket(packet_bank, i)
				EndIf
			Else
				' the players stream is down
				RemovePlayer(i)
			EndIf
		EndIf
	Next	
	
	' display gui
	DrawText("Players online: " + playersOnline, 5, 5)
	Flip()
	Cls()		
Wend

' clean up
server_socket.Close()

'-----------------------
' Functions
'-----------------------
Function InitServer()
	server_socket = CreateTCPSocket()
	
	If Not BindSocket(server_socket, SERVER_PORT) Or Not SocketListen(server_socket)
		Print("Could not start server on port " + SERVER_PORT)
		server_socket.Close()
		Return
	EndIf
	
	Print("Server started successfully!")
End Function

Function HandlePacket(packet:TBank, playerID%)
	Local packet_id%,ofs%,packet_bank:TBank,packet_size:Byte
	
	' identity the packet
	packet_id = packet.PeekByte(0)
	ofs :+ 1
	Select packet_id
		Case P_ID_REQUEST_INFO
			SendInfoPacket(players[playerID].stream, playerID)
			
		Case P_ID_MOVE
			HandleMovePacket(packet, playerID)
	End Select
End Function

Function SendInfoPacket(strm:TStream, playerID%)
	Local packet_bank:TBank,packet_size:Byte,ofs%,i%,send_bank:TBank
	
	' setup packet
	' packet id + player_id + plyrs online + every players x,y-position
	packet_size = 1 + 1 + + 1 +playersOnline * (1 + 4 + 4) 
	packet_bank = CreateBank(packet_size + 1)
	packet_bank.PokeByte(0, packet_size + 1)
	ofs :+ 1
	packet_bank.PokeByte(ofs, P_ID_ACCEPT)
	ofs :+ 1
	packet_bank.PokeByte(ofs, playerID)
	ofs :+ 1
	packet_bank.PokeByte(ofs, Byte(playersOnline))
	ofs :+ 1
	
	' add info about all players
	For i=0 Until MAX_PLAYERS
		If players[i] 
			packet_bank.PokeByte(ofs, Byte(i))
			ofs :+ 1
			packet_bank.PokeInt(ofs, players[i].posX)
			ofs :+ 4
			packet_bank.PokeInt(ofs, players[i].posY)
			ofs :+ 4
		EndIf
	Next
	
	' send the bank
	WriteBank(packet_bank, strm, 0, packet_size + 1)
	
	' notify all users about the new player
	For i=0 Until MAX_PLAYERS
		If i &lt;&gt; playerID And players[i] And Not players[i].stream.Eof()
			players[i].stream.WriteByte(2)
			players[i].stream.WriteByte(P_ID_NEW_PLR)
			players[i].stream.WriteByte(playerID)
		EndIf
	Next
	
	' update player status to ingame
	players[playerID].status = 3
End Function

Function HandleMovePacket(packet_bank:TBank, playerID%)
	Local dir%,send_bank:TBank,packet_size%,i%

	If players[playerID]
		' check the direction and move the player
		dir = packet_bank.PeekByte(1)
		' TODO: do some checking/timing to verify the move
		players[playerID].move(dir)
		
		' setup the packet
		packet_size = 1 + 1 + 1 ' packet_id + user_id + dir
		send_bank = CreateBank(packet_size + 1)
		send_bank.PokeByte(0, packet_size)
		send_bank.PokeByte(1, P_ID_MOVE)
		send_bank.PokeByte(2, playerID)
		send_bank.PokeByte(3, dir)
		
		' send the bank to all online players
		For i=0 Until MAX_PLAYERS
			If i &lt;&gt; playerID And players[i] And players[i].status &gt; 1 And Not players[i].stream.Eof()
				WriteBank(send_bank, players[i].stream, 0, packet_size + 1)
			EndIf
		Next
	EndIf
End Function

Function RemovePlayer(playerID%)
	' remove him from server
	players[playerID] = Null
	playersOnline :- 1
	
	' tell other users to remove this player
	For Local i% = 0 Until MAX_PLAYERS
		If players[i] And i &lt;&gt; playerID
			players[i].stream.WriteByte(2) ' packet size: packet_id + user_id
			players[i].stream.WriteByte(P_ID_PLR_LOGOUT)
			players[i].stream.WriteByte(playerID)
		EndIf
	Next
End Function
	

'-----------------------
' Player type
'-----------------------
Type TPlayer
	Field posX%, posY%
	Field stream:TStream, socket:TSocket, status%
	
	Method move(dir:Byte)
		Select dir
			Case DIR_UP
				posY :- 1
			Case DIR_DOWN
				posY :+ 1
			Case DIR_LEFT
				posX  :- 1
			Case DIR_RIGHT
				posX  :+ 1
		End Select
	End Method
	
	Method setPos(x%, y%)
		posX = x
		posY = y
	End Method
	
	Function Create:TPlayer(socket:TSocket)
		Local p:TPlayer = New TPlayer
		p.socket = socket
		p.stream = CreateSocketStream(p.socket)
		Return p
	End Function
End Type
</textarea> <br><br></td></tr></table><br>
<a name="950566"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >B</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> hey thanks!<br>i havent looked at it or ran it yet, but thanks for going to all that trouble for me! :)<br><br>ill post back when I get it, or when I dont. haha<br><br>cheers!<br><br>B <br><br></td></tr></table><br>
<a name="950852"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >B</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> ok so I sorta understand how it works and all.<br><br>however I had my friend run the client after I ran the server and the client and he couldnt connect to me. I also had us both run the server and then the client and we didnt see eachother. Was the program supposed to do that or was it not meant to do that, as it was only a small sample program to try and get my up to speed.<br><br>thanks for all your help so far, and dealing with my obvious noobness. :P<br><br>B <br><br></td></tr></table><br>
<a name="950910"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >AltanilConard</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well, the client connects to <a href="http://en.wikipedia.org/wiki/Localhost" target="_blank">local host</a> right now. First change the SERVER_IP$ (in the top of the clients code) to <a href="http://www.ipchicken.com" target="_blank">your ip</a>. Then compile the client and send it to your friend. Next you'll have to make sure the SERVER_PORT% on your computer is open, if you're behind a router that is (look on www.portforward.com if you don't know how to do this). Now, when you run the server, your friend should be able to connect just fine (you should be the only one running the server, your friend does not have to open his port to connect to you). <br><br></td></tr></table><br>
<a name="951151"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >B</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> wow thanks! ill definitely try that soon. That makes alot of sense.<br><br>thanks again conrad.<br>ill post back when I get it working, and ill try to make my own modifications to it.<br><br>I like how it senses which button you pressed and sending and recieving to the server,<br>its really interesting!<br><br>thanks again<br><br>B <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
