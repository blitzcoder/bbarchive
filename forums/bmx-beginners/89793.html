<!DOCTYPE html><html lang="en" ><head ><title >It's never to late to write your own editor...</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >It's never to late to write your own editor...</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >It's never to late to write your own editor...</a><br><br>
<a name="1020321"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Arowx</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Or why it pays to keep an eye on how long it's taking you to do stuff you know you can automate...<br><br><a href="http://blog.arowx.com////blog1.php/2010/04/03/haha-now-were-laying" target="_blank">http://blog.arowx.com////blog1.php/2010/04/03/haha-now-were-laying</a> <br><br></td></tr></table><br>
<a name="1020356"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Arowx</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's the code for this very very basic Layout Editor...<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Rem
	Arowx.com - Allan Rowntree 2010 

	A very basic LayoutEditor v0.01
	NB - Not Guaranteed to be any use at all!

	Keyboard Driven controls
	
		[l] load graphical element
		[L] Load layout
		[S] Save layout to text file		

		[+] Increase Screen Size
		[-] Decrease Screen Size
		
		[Del] Remove element
	
	Mouse selection of elements
	
		Done - Select element 
		Done - Select and Drag elements
	
	Project based
		
		File saved as &lt;project&gt;.led
		
		Screen Size [x, y] - first line
			List of elements... 
			[&lt;name&gt;, x, y, &lt;path&gt;] - one per line


	One Step at a time...
	
		Done - Load Sprite
		Done - Draw Sprite
		Done - Select Sprite
		Done - Move Sprite Mouse
		Done - Move selected sprite keyboard 1 pixel nudges
		
		Done - Show Hide Grid
		
		Done Save Project
		Done Load Project
		
End Rem


SuperStrict

'Framework BRL.D3D7Max2D
Framework BRL.GLMax2D

Import BRL.StandardIO
Import BRL.PNGLoader
Import BRL.JPGLoader

'SetGraphicsDriver D3D7Max2DDriver()
SetGraphicsDriver GLMax2DDriver()

Type TSprite

	Field image:TImage
	Field name:String
	Field path:String
	
	
	Field x:Int
	Field y:Int
	Field x2:Int
	Field y2:Int
	
	Field selected:Int
	
	Function LoadFromFile:TSprite(filename:String)
		
		Local sprite:TSprite = New TSprite
		
		sprite.image = LoadImage(filename)
		
		If sprite.Image = Null Then 
			Print "Failed to load Sprite: " + filename
		Else
			sprite.setup(filename)
		End If
		
		Return sprite
		
	End Function
	
	Method draw()
	
		If selected Then 
			
			SetColor 0,255,0
			SetAlpha 0.5
			
			DrawLine x,y,x2,y
			DrawLine x2,y,x2,y2
			DrawLine x2,y2,x2,y
			DrawLine x2,y2,x,y
			
			SetColor 255,255,255
			SetAlpha 1.0
			
		EndIf 
		
		DrawImage(Image,x,y)
		
	End Method
	
	Method overxy:Int(mx:Int,my:Int)
	
		If mx &gt;= x And mx &lt;= x2 Then
			If my &gt;= y And my &lt;= y2 Then
				Return True
			End If
		End If 
	
		Return False
		
	End Method
	
	Method setup(filename:String)
		
		Local namePos:Int = filename.FindLast("\") + 1
		name = filename[namePos..]
		path = filename[..namePos]
	
		DebugLog "Name[" + name + "]"
		DebugLog "Path[" + path + "]"
		
		move(0, 0) ' default to 0,0
		selected = False
		
	End Method
	
	Method move(nx%,ny%)
		x = nx
		y = ny
		x2 = x + Image.Width
		y2 = y + Image.Height
	End Method
	
	Method ToString:String()
	
		'Adjust so we only save screen based coordinates
		Local sx:Int = x - screenoffsetx
		Local sy:Int = y - screenoffsety
		
		Local s:String = name + "," + sx + "," + sy + "," + path

		Return s
				
	End Method
	
	Function CreateFromString:TSprite(s:String)
		
		'Expecting name, x, y, path
		
		DebugLog "CreateFromString[" + s + "]"
		
		Local pos:Int = s.Find(",")
		Local name:String = s[..pos]
		
		DebugLog "Name[" + name + "]"
		
		Local pos2:Int = s.Find(",", pos + 1)
		Local sx:Int = s[pos + 1..pos2].ToInt()
		
		DebugLog "sx[" + sx + "]"
		
		Local pos3:Int = s.Find(",", pos2 + 1)
		Local sy:Int = s[pos2 + 1..pos3].ToInt()
		
		DebugLog "sy[" + sy + "]"
		
		Local path:String = s[pos3 + 1..]
		
		DebugLog "path[" + path + "]"
		
		Local newSprite:TSprite = TSprite.LoadFromFile(path + name)
		If newSprite
			newSprite.move(sx + screenoffsetx, sy + screenoffsety)
		Else
			DebugLog "Failed to load sprite: " + path + name
		End If
		
		Return newSprite
		
	End Function
	
End Type



AppTitle = "Layout Editor 0.1"

' Window dimensions

Global windowWidth:Int = 2400
Global windowHeight:Int = 1500

Graphics windowWidth, windowHeight

SetBlend ALPHABLEND

Global spriteList:TList = New TList

AutoMidHandle(False) 'buttons and ui elements use top left handle

'Grid dimensions

Global screenwidth:Int = 1920
Global screenheight:Int = 1200

Global screenoffsetx:Int = (windowWidth-screenWidth) / 2
Global screenoffsety:Int = (windowHeight-screenHeight) / 2

Global oldx:Double = -1
Global oldy:Double = -1

Global offsetState:String = "0.0"
Global offset:Double = 1.0

Global filter:String = "led Files:led;All Files:*"
Global imageFilter:String = "Image Files:png,jpg;All Files:*"
Global filename:String = ""

Global drawGrid:Int = True

Global selectedSprite:TSprite

setup()


Repeat
	
	' -------------------------------- [ Move the layout 0.5 a step ]
	
	
	If KeyHit(KEY_G)
		drawGrid = Not drawGrid
	End If
	
	' -------------------------------- [ Clear layout ]
	If KeyHit(KEY_D)
		setup()
	End If
	
	' -------------------------------- [ Save file ]
	If KeyHit(KEY_S) ' save the layout to file
		
		filename = RequestFile("Save layout file", filter, True, filename)

		SaveToFile()
		
		Print filename
		
	End If
	
	If KeyHit(KEY_I) 'insert sprite - loads sprite from file
	
		filename = RequestFile("Load sprite file", imageFilter)
		
		Local sprite:TSprite = TSprite.LoadFromFile(filename)
		
		If sprite Then
		
			spriteList.Addlast(sprite)
			sprite.move(screenoffsetx, screenoffsety)
			
		End If
		
	End If
	
	' -------------------------------- [ Load file ]
	If KeyHit(KEY_L)
	
		filename = RequestFile("Load layout file", filter)
		
		LoadFromFile()
		
		Print filename
		
	EndIf
	
	Local mx:Float = MouseX()
	Local my:Float = MouseY()
	
	' adjust mouse pointer using the keyboard
	If KeyHit(KEY_UP) 'Or KeyDown(KEY_UP) Then
		MoveMouse(mx, my - 1)
	End If
	If KeyHit(KEY_DOWN) 'Or KeyDown(KEY_DOWN) Then
		MoveMouse(mx, my + 1)
	End If
	If KeyHit(KEY_LEFT) 'Or KeyDown(KEY_LEFT) Then
		MoveMouse(mx - 1, my)
	End If
	If KeyHit(KEY_RIGHT) 'Or KeyDown(KEY_RIGHT) Then
		MoveMouse(mx + 1, my)
	End If

	mx = MouseX()
	my = MouseY()
	
	If selectedSprite Then
		'move to mx my
		selectedSprite.move(mx, my)
		
		If MouseHit(1)
			selectedSprite.selected = False
			
			selectedSprite = Null 'drop sprite
		End If
	Else
	
		If MouseHit(1) ' select image
		
			For Local s:TSprite = EachIn spriteList
				If s.overxy(mx, my) Then
					selectedSprite = s
				End If
			Next
		
			If selectedSprite Then selectedSprite.selected = True
			
		Else If MouseDown(1)
			
		Else If MouseHit(2)
			
		Else If MouseDown(2)
				
		End If
		
	End If	
	
	Cls
	
	For Local sprite:TSprite = EachIn spriteList
		sprite.draw()
	Next
	
	
	'--------------------------------- Draw Grid -----------------------------------------------------
	'draw a green grid 10x10 
	
	If drawGrid Then
	
		'SetColor 0, 255, 0
		'DrawRect 330, 280, 960, 640
		
		SetAlpha 0.2
		
		Local greenA:Int = 255
		Local greenB:Int = 128
			
		SetColor 0, greenA, 0
		
		Local screenx:Int = screenOffsetx
		Local screenx2:Int = screenx+screenWidth
		
		Local screeny:Int = screenOffsety
		Local screeny2:Int = screeny+screenHeight
		
		
		For Local gridx:Int = screenx To screenx2 Step 50
			DrawLine gridx, screeny, gridx, screeny2
		Next
		
		For Local gridy:Int = screeny To screeny2 Step 50
			DrawLine screenx, gridy, screenx2, gridy
		Next
		
		SetColor 0, greenB, 0
		
		
		For Local gridx:Int = screenx To screenx2 Step 10
			DrawLine gridx, screeny, gridx, screeny2
		Next
		
		For Local gridy:Int = screeny To screeny2 Step 10
			DrawLine screenx, gridy, screenx2, gridy
		Next
		
		SetColor 255, 255, 255
		SetAlpha 1.0
		
	EndIf
	
	
	
	'---------------------------------- Draw the layout -----------------------------------------------
		
	
	
	'---------------------------------- Draw the blank tile -----------------------------------------------
	
	
	SetBlend ALPHABLEND
	
	'-------------------- Draw Coords
		
	DrawText "mx:" + mx, 10, 90
	DrawText "my:" + my, 10, 110
	
	DrawText "x:" + (mx - screenoffsetx), 10, 130
	DrawText "y:" + (my - screenoffsety), 10, 150
	
      DrawText "[I]nsert Sprite", 10, 200
	DrawText "[S]ave", 10, 220
	DrawText "[L]oad", 10, 240
	
	DrawText "File:" + filename, 10, 260
		
	'-------------------- Reset drawing state
	
	SetAlpha 1.0
	
	SetColor 255, 255, 255
	
	Flip

Until AppTerminate() Or KeyHit(KEY_ESCAPE)

'---------------------------- Save to File with sequence of four elements X, Y, Z and length for each fro double precision, comma delimited 1 row per line

Function SaveToFile()

	If filename Then
	
		Local fout:TStream = WriteStream(filename)
		
		If fout Then
			fout.WriteLine(screenwidth + "," + screenheight)
			
			For Local s:TSprite = EachIn spriteList
				fout.WriteLine(s.ToString())
			Next
			
			fout.Close()
		Else
			RuntimeError "Failed to open file :" + filename
		EndIf
		
	EndIf
	
End Function


Function LoadFromFile()

	If filename Then

		Local fin:TStream = ReadStream(filename)
		
		If fin Then
		
			Local firstLine:Int = True	
		
			Repeat
				Local line:String = fin.ReadLine()
			
				If firstLine Then
					firstLine = False
				Else
					Local sprite:TSprite = TSprite.CreateFromString(line)
					
					If sprite Then
						spriteList.AddLast(sprite)
					End If
					
				End If
				
			Until fin.Eof()
		
			fin.Close()
		Else
			RuntimeError "Failed to open file :" + filename
		EndIf
			
	End If
	
End Function


Function setup()
	

	
End Function
</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
