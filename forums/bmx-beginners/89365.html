<!DOCTYPE html><html lang="en" ><head ><title >Print without newline?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Print without newline?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >Print without newline?</a><br><br>
<a name="1014765"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >xlsior</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm looking at putting together a very basic command line program (under Win32), and was wondering if there's an easy way to re-write a line of text in the console window.<br><br>the "Print" command automatically appends a newline so the cursor goes to the next line, but I'm hoping to have a simple progress counter which would require me to update the same line of text a number of times.<br><br>Any idea? <br><br></td></tr></table><br>
<a name="1014785"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Zeke</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>Extern "win32"
	Function SetConsoleCursorPosition:Int(handle:Int , pos:Int)
End Extern

Function Locate(x:Int , y:Int)
	SetConsoleCursorposition(GetStdHandle( STD_OUTPUT_HANDLE) , y Shl 16+x)
End function


locate 10 , 10
Print "test"
locate 20 , 20
Print "it's working"

For Local i:Int = 0 To 100
	locate 30 , 0
	Print "Count: " + i
	Delay 200
Next</pre><br>Here you can find other console functions: <a href="http://msdn.microsoft.com/en-us/library/ms682073%28VS.85%29.aspx" target="_blank">http://msdn.microsoft.com/en-us/library/ms682073%28VS.85%29.aspx</a><br>Note that this wont work when running from IDE, so you must build exe and run that. <br><br></td></tr></table><br>
<a name="1014787"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >xlsior</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Very interesting...<br><br>Although for it to be usable, I'll need to figure out how to parse the current cursor position through GetconsoleScreenBufferInfo first. :-?<br><br>(Blah, I hate pointers... :-? ) <br><br></td></tr></table><br>
<a name="1014790"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >xlsior</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, with some help of other console code by Jim Brown, I've been able to piece together some code that can also read the current location.<br><br>This will print an growing text progressbar on the same line in the active console window:<br>(After printing the progress bar, it will move the cursor back to the same line so the updated version overwrites the previous one)<br><br><pre class=code>
Extern "win32"
	Function SetConsoleCursorPosition:Int(handle:Int , pos:Int)
	Function GetConsoleScreenBufferInfo(handle:Int ,sbinfo:Byte Ptr)
End Extern

Function Locate(x:Int , y:Int)
	SetConsoleCursorposition(GetStdHandle( STD_OUTPUT_HANDLE) , y Shl 16+x)
End Function

Global cursorX:Int
Global cursorY:Int
Global progressbar:String

Print
Print

For Local myloop:Int=0 To 100
	console.GetCursorPos(x,y)
	Locate cursorx,cursory-1
	Delay 100
	progressbar:String=""
	For Local myloop2:Int=0 To myloop/4
		progressbar:String=progressbar:String+"|"
	Next
	Print myloop+"% "+Progressbar:String

Next


Global sbinfo:ConsoleScreenBufferInfo=New ConsoleScreenBufferInfo


Type Console
	Const STD_INPUT_HANDLE% = -10
	Const STD_OUTPUT_HANDLE% = -11
	Global hnd% , stdOUT% , stdIN% , ForeColor%,BackColor%
	Global Width%,Height%
	Global sbinfo:ConsoleScreenBufferInfo=New ConsoleScreenBufferInfo
	Function GetCursorPos(x% Var,y% Var)
		GetConsoleScreenBufferInfo GetStdHandle( STD_OUTPUT_HANDLE),Console.sbinfo
		CursorX = Console.sbinfo.CursorXPos 
		CursorY = Console.sbinfo.CursorYPos
	End Function
End Type

Type ConsoleScreenBufferInfo
	Field numCols:Short , numRows:Short
	Field CursorXPos:Short,CursorYPos:Short
	Field Attrs:Short
    	Field sbuffL:Short,sbuffT:Short,sbuffR:Short,sbuffB:Short
	Field MaxWinXSize:Short,MaxWinYSize:Short
End Type

Function SetCursorPos(x%,y%)
	SetConsoleCursorPosition GetStdHandle( STD_OUTPUT_HANDLE), (y Shl 16) + x
End Function
</pre><br><br>Thanks for the pointer, Zeke! (and Jim, too) <br><br></td></tr></table><br>
<a name="1014801"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >d-bug</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice one, but whats wrong with WriteStdOut? <br><br></td></tr></table><br>
<a name="1014802"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >xlsior</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Nice one, but whats wrong with WriteStdOut?  <br></div><br><br>Absolutely nothing...<br><br>...Had I known it existed. :-?<br><br>Looks like that one works perfectly to re-write the current line without needing to figure out where it's at:<br><br><pre class=code>
WriteStdOut "OLD"
WriteStdOut chr$(8)+chr$(8)+chr$(8)  ' chr$(8) = Backspace
WriteStdOut "NEW"
</pre><br><br>Thanks for the heads up. <br><br></td></tr></table><br>
<a name="1014868"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >EOF</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for the mention. Just a little plug, you can use my console library for doing funky stuff like changing the color attributes, clearing the screen, reading the text buffer ..<br><img src="http://i289.photobucket.com/albums/ll234/syntax_photos/console01.png"><br><br>If anyone's interested:<br><br>DEMO #1<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict
Framework brl.blitz
Import "ConsoleControl.bmx"

Console.Open "CLS test"
For Local clr%=1 To $80
	Console.SetColorAttr clr
	Console.WriteLine "Testing attribute color ["+String(clr)+"]"
Next
Console.SetColor WHITE,BLUE
Console.WaitReturn
Console.SetColor YELLOW,RED
Console.Cls
Console.Write "Final line written"
Delay 1000

End</textarea><br>DEMO #2<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict
Framework brl.blitz
Import "ConsoleControl.bmx"

Console.Open
'Console.CreateScreenBuffer
Console.SetTitle "Console - Input test"
Console.SetColor WHITE,BLUE

Console.WriteLine "   OO    OO   O    O   OOO    OO   O     OOOO"
Console.WriteLine "  O     O  O  OO   O  O      O  O  O     O   "
Console.WriteLine "  O     O  O  O O  O  O      O  O  O     O   "
Console.WriteLine "  O     O  O  O O  O   OOO   O  O  O     OOO "
Console.WriteLine "  O     O  O  O  O O      O  O  O  O     O   "
Console.WriteLine "  O     O  O  O   OO      O  O  O  O     O   "
Console.WriteLine "   OO    OO   O    O   OOO    OO   OOOO  OOOO"

Console.SetColor GREY,BLACK
Console.SetCursorPos 0,10
Local name$=Console.Input("Your name? &gt;")
Console.SetColor BLACK,DYELLOW
Console.Cls
Console.SetCursorPos 4,14
Local t$=Console.Input("Hello ~q"+name+"~q enter some random text &gt;")
Console.write name+", Text written was '"+t+"'"
Console.WaitReturn
Console.Close
End
</textarea><br>ConsoleControl.bmx<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Rem
	Title: ConsoleControl v0.01
	Author: Jim Brown
EndRem

Strict

Const BLACK = 0
Const DBLUE = 1
Const DGREEN = 2
Const DRED = 4
Const INTENSITY = 8
Const DYELLOW = DRED+DGREEN
Const DCYAN = DGREEN+DBLUE
Const DPURPLE = DRED+DBLUE
Const BLUE = DBLUE+INTENSITY
Const GREEN = DGREEN+INTENSITY
Const RED = DRED+INTENSITY
Const WHITE = DBLUE+DGREEN+DRED+INTENSITY
Const GREY = DBLUE+DGREEN+DRED
Const YELLOW = DGREEN+DRED+INTENSITY
Const CYAN = DGREEN+DBLUE+INTENSITY
Const PURPLE = DRED+DBLUE+INTENSITY


Extern "Win32"
	Function WriteConsoleW%(hnd%,lpBuff$w,NChars%,NCharsWritten:Int Ptr,Reserved%)
	Function AllocConsole%()
	Function FreeConsole()
	Function CreateConsoleScreenBuffer%(Access%,ShareMode%,secAttr:Byte Ptr,Flags%,Reserved%)
	Function SetConsoleActiveScreenBuffer(hScreenBuff%)
	Function SetStdHandle(tpy%,hnd%)
	Function GetStdHandle(tpy%)
	Function SetConsoleTitleW(title$w)
	Function SetConsoleCursorPosition(hbuffOUT%,xyPos%)
	Function FillConsoleOutputCharacterW%(hbuffOUT%,char%,numChars%,xyPos%,nCharsWritten:Int Ptr)
	Function FillConsoleOutputAttribute%(hbuffOUT%,attr%,numChars%,xyPos%,nCharsWritten:Int Ptr)
	Function GetConsoleScreenBufferInfo(hbuffOUT%,tCONSOLEINFO:Byte Ptr)
	Function SetConsoleTextAttribute(hbuffOUT%,attr%)
	Function FlushConsoleInputBuffer(hbuffIN%)
	Function ReadConsoleA(hbuffIN%,lBuffAddr:Byte Ptr,numCharsToRead%,nCharsRead:Int Ptr,Reserved%)
	Function ReadConsoleW(hbuffIN%,lBuffAddr:Byte Ptr,numCharsToRead%,nCharsRead:Int Ptr,Reserved%)
	Function PeekConsoleInput(hbuffIN%,lpBuffer:Long Ptr,Length%,numEventsRead%)
	Function ReadConsoleOutputW(hbuffOUT%,lBuffAddr:Byte Ptr,xypos%,buffCoord%,sRect:Int Ptr)
	Function ReadConsoleOutputCharacterA(hbuffOUT%,lBuffAddr:Byte Ptr,numCharsToRead%,buffCoord%,nCharsRead:Int Ptr)
End Extern


Type Console
	Const STD_INPUT_HANDLE% = -10
	Const STD_OUTPUT_HANDLE% = -11
	Global hnd% , stdOUT% , stdIN% , ForeColor%,BackColor%
	Global Width%,Height%
	Global sbinfo:ConsoleScreenBufferInfo=New ConsoleScreenBufferInfo
	'
	Function Open(title$="ConsoleControl Window")
		Assert Console.hnd=Null,"Console already open!"
		Console.hnd=AllocConsole()
		Assert Console.hnd,"Console failed to open"
		Console.ForeColor=7 ; Console.BackColor=0
		Console.stdOUT=GetStdHandle(Console.STD_OUTPUT_HANDLE)
		Console.stdIN=GetStdHandle(Console.STD_INPUT_HANDLE)
		Assert Console.stdOUT,"Failed to get console screen OUT buffer"
		Assert Console.stdIN,"Failed to get console screen IN buffer"
		GetConsoleScreenBufferInfo Console.stdOUT,Console.sbinfo
		Console.Width=Console.sbinfo.numCols ; Console.Height=Console.sbinfo.numRows
		Console.Cls
		Console.SetTitle title
	End Function
	'
	Function CreateScreenBuffer%()
		Const GEN_READ=$80000000 , GEN_WRITE=$40000000
		Local hndSB%=CreateConsoleScreenBuffer(GEN_READ+GEN_WRITE, 1+2,Null,1,Null)
		SetConsoleActiveScreenBuffer hndSB
		SetStdHandle Console.STD_OUTPUT_HANDLE,hndSB
		Console.stdOUT=GetStdHandle(Console.STD_OUTPUT_HANDLE)
		Console.stdIN=GetStdHandle(Console.STD_INPUT_HANDLE)
		Return hndSB
	End Function
	'
	Function Close()
		Assert Console.hnd,"No Console to 'close'"
		FreeConsole() ; Console.hnd=Null
		Console.stdOUT=0 ; Console.stdIN=0
		Console.Width=0 ; Console.Height=0
	End Function
	'
	Function SetCursorPos(x%,y%)
		SetConsoleCursorPosition Console.stdOUT, (y Shl 16) + x
	End Function
	'
	Function GetCursorPos(x% Var,y% Var)
		GetConsoleScreenBufferInfo Console.stdOUT,Console.sbinfo
		x=Console.sbinfo.CursorXPos ; y=Console.sbinfo.CursorYPos
	End Function
	'
	Function Write(txt$)
		Assert Console.hnd,"Console not open - Unable to write text"
		WriteConsoleW Console.stdOUT,txt,txt.Length,Null,0
	End Function
	'
	Function WriteLine(l$)
		Console.Write l$+Chr(10)	
	End Function
	'
	Function Read$(x%,y%,numChars%=1)
		Local inp:Byte[numChars] ' space for character(s) to read
		Local ncr% ' storage variable for number of chars actually read
		DebugLog "R="+ReadConsoleOutputCharacterA(Console.stdOUT%,Varptr(inp[0]),numChars,(y Shl 16) + x, Varptr(ncr))
		DebugLog inp[0]
		Return String.FromCString(inp).Trim()
	End Function
	'
	Function ReadLine$(yPos%)
		Return Console.Read(0,yPos,Console.Width)
	End Function
	'
	Function Cls()
		Local sz%=Console.Width*Console.Height
		FillConsoleOutputCharacterW Console.stdOUT,32,sz,0,Null
		FillConsoleOutputAttribute Console.stdOUT,Console.BackColor*16,sz,0,Null
		SetConsoleCursorPosition Console.stdOUT,0
	End Function
	'
	Function SetColor(fore%,back%=-1)
		If back=-1 Then back=Console.BackColor
		If fore=-1 Then fore=Console.ForeColor
		SetConsoleTextAttribute Console.stdOUT,fore+(back*16)
		Console.ForeColor=fore ; Console.BackColor=back
	End Function
	'
	Function SetColorAttr(index%)
		If index&lt;0 Or index%&gt;128 Then Return
		SetConsoleTextAttribute Console.stdOUT,index
	End Function
	'
	Function WaitReturn()
		Console.SetColor GREY
		Console.WriteLine "~nPress RETURN .."
		FlushConsoleInputBuffer Console.StdIN
		ReadConsoleA Console.StdIN,Null,1,Null,0
	End Function
	'
	Function Input$(defprompt$="&gt; ")
		Local inp:Byte[16384]
		Local ncr% ' storage variable for number of chars actually read
		Console.Write defprompt$
		FlushConsoleInputBuffer Console.StdIN
		ReadConsoleA(Console.StdIN,Varptr(inp[0]),inp.Length,Varptr(ncr),Null)
		Local rs$=String.FromCString(inp)
		If rs.Length&lt;=2 Then Return "" ' user pressed RETURN only
		Return String(rs[..rs.Length-2]) ' strip RETURN and NULL characters
	End Function
	'
	Function SetTitle(newtitle$)
		SetConsoleTitleW newtitle$
	End Function
	'
End Type

Type ConsoleScreenBufferInfo
	Field numCols:Short , numRows:Short
	Field CursorXPos:Short,CursorYPos:Short
	Field Attrs:Short
    	Field sbuffL:Short,sbuffT:Short,sbuffR:Short,sbuffB:Short
	Field MaxWinXSize:Short,MaxWinYSize:Short
End Type

Type tCoord
	Field x%,y%
End Type
</textarea> <br><br></td></tr></table><br>
<a name="1014877"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >xlsior</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I found that in another thread, but both demos failed for me, always saying it could not create a console... (windows 7, x64) <br><br></td></tr></table><br>
<a name="1015088"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >EOF</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Xlsior,<br>Are you running the demos as GUI build? I'm guessing BlitzMax is already opening the console window. My module handles the whole console window opening and closing affair (Console.Open / Console.Close) <br><br></td></tr></table><br>
<a name="1015110"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >xlsior</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> No, I wasn't using it as a GUI build -- if that's a pre-requisite then it won't work for me anyway... The entire point is that I'm creating a command line app that I can run straight from a normal DOS window, interacting with other command line applications.<br><br>If your library tries to create and conotrol its own console window that would explain the error, although that does mean it won't be usable for my purposes... <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
