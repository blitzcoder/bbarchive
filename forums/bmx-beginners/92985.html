<!DOCTYPE html><html lang="en" ><head ><title >laser beam code</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >laser beam code</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >laser beam code</a><br><br>
<a name="1061564"></a>

<a name="1061565"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >hub</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi !<br><br>i'm searching a fast method to draw a long beam. i've the following code. Perhaps have you some idea to improve/optimize it.<br>Thanks !<br><br><pre class=code>
Strict

Graphics 800,600

HideMouse()

Global List_beams:TList = CreateList()

Global hImage:TImage = CreateImage (3,1)

Local pixmap:TPixmap=LockImage(hImage)
For Local i=0 To 2
	WritePixel(pixmap,i,0,$ffffffff)
Next
UnlockImage(hImage)

Type Tbeam

	Field ox#
	Field oy#
	Field dx#
	Field dy#
	Field Width
	Field Speed#
	
	Function Init:Tbeam (x,y, Width, Speed#)
	
		Local l:Tbeam = New Tbeam
		
		l.ox# = x
		l.oy# = y
		l.dx# = x
		l.dy# = y
		l.Width = Width	
		l.Speed = Speed
		
		ListAddLast List_beams, l
	
	End Function	
	
	Function Compute_all()
		For Local l:Tbeam = EachIn List_beams
			If Abs (l.oy-l.dy) &lt;&gt; l.Width Then
				l.dy = l.dy - 1 * l.Speed#
			Else
				If l.dy &gt; 0 Then
					l.dy = l.dy - 1 * l.Speed#
				End If
				If l.oy &gt; 0 Then 
					l.oy = l.oy - 1 * l.Speed#
				End If
			End If
			If l.oy# &lt; 0 Then
				List_beams.remove l
			End If
		Next
	End Function
	
	Function Draw_all()
		For Local l:Tbeam = EachIn List_beams
			'SetColor 0,255,0
			'DrawLine l.ox-10, l.oy,l.dx#-10, l.dy#
			SetColor 255,0,0
			SetScale 1.0,l.dy#-l.oy#
			DrawImage himage, l.ox, l.oy
			SetScale 1,1
			SetColor 255,255,255

		Next
	End Function
	
End Type

While Not KeyDown(KEY_eSCAPE)
	Cls
		SetColor 255,255,255
		Plot MouseX(), MouseY()
		
		If MouseHit(1) Then Tbeam.Init (MouseX(), MouseY(), 200,10)
		
		Tbeam.Compute_all()
		Tbeam.Draw_all()
		
	Flip
Wend

ShowMouse()
</pre><br><br><font class="tiny">Last edited 2010</font> <br><br></td></tr></table><br>
<a name="1061596"></a>

<a name="1061601"></a>

<a name="1061603"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Perturbatio</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> To demonstrate the speed up from my code, here's your one modified to have a simple FPS calc and no refresh limit (with some bug fixes):<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Graphics 800,600,0,0

HideMouse()

Global List_beams:TList = CreateList()

Global hImage:TImage = CreateImage (3,1)

Local pixmap:TPixmap=LockImage(hImage)
For Local i=0 To 2
	WritePixel(pixmap,i,0,$ffffffff)
Next
UnlockImage(hImage)

Type Tbeam

	Field ox#
	Field oy#
	Field dx#
	Field dy#
	Field Width
	Field Speed#
	
	Function Init:Tbeam (x,y, Width, Speed#)
	
		Local l:Tbeam = New Tbeam
		
		l.ox# = x
		l.oy# = y
		l.dx# = x
		l.dy# = y
		l.Width = Width	
		l.Speed = Speed
		
		ListAddLast List_beams, l
	
	End Function	
	
	Function Compute_all()
		For Local l:Tbeam = EachIn List_beams
			If Abs (l.oy-l.dy) &lt;&gt; l.Width Then
				l.dy = l.dy - 1 * l.Speed#
			Else

				l.dy = l.dy - 1 * l.Speed#
				l.oy = l.oy - 1 * l.Speed#

			End If
			If l.oy# &lt; 0 Then
				List_beams.remove l
			End If
		Next
	End Function
	
	Function Draw_all()
		For Local l:Tbeam = EachIn List_beams
			'SetColor 0,255,0
			'DrawLine l.ox-10, l.oy,l.dx#-10, l.dy#
			SetColor 255,0,0
			SetScale 1.0,l.dy#-l.oy#
			DrawImage himage, l.ox, l.oy
			SetScale 1,1
			SetColor 255,255,255

		Next
	End Function
	
End Type

Local frameCount: Int = 0
Local fps:Int = 0
Local startTime:Int = MilliSecs()

While Not KeyDown(KEY_eSCAPE)
	Cls
		SetColor 255,255,255
		Plot MouseX(), MouseY()
		
		If MouseDown(1) Then Tbeam.Init (MouseX(), MouseY(), 200,10)
		
		Tbeam.Compute_all()
		Tbeam.Draw_all()
		DrawText "FPS: " + fps, 10, 10
		DrawText "count: " + CountList(List_beams), 10, 22
		frameCount:+1
		If (MilliSecs()-startTime &gt;= 1000) Then
			fps = frameCount
			frameCount = 0;
			startTime = MilliSecs()
		EndIf
	Flip
Wend

ShowMouse()
</textarea><br><br>And my code:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
Graphics 800,600',0, 0

HideMouse()

Global List_beams:TList = CreateList()
Global hImage:TImage = CreateImage (5,1)
Global laserLength:Float = 20
Global laserSpeed:Float = 10
Global laserRepeatRate:Int = 200
Global laserLastFired:Int = 0

Local pixmap:TPixmap=LockImage(hImage)
Local colourMap:String[hImage.width]
Local stp:Int = 180/(hImage.width-1)
Local i:Float, index:Int


While index &lt; hImage.width

	colourMap[index] = ( "$" + ByteHex( 255*Sin(i) ) + "777777" )
	'Print (colourMap[ index ])

	i:+ stp
	index:+ 1
	
Wend


For Local y:Int=0 To hImage.height-1

	For Local x:Int=0 To hImage.width-1
		
		WritePixel( pixmap,x,y, Int(colourMap[x] ))
		
	Next
Next

UnlockImage(hImage)


Type Tbeam

	Field ox:Float
	Field oy:Float
	Field dx:Float
	Field dy:Float
	Field Length:Float
	Field Speed:Float
	Field link:TLink
	
	Function Init:Tbeam (x:Float,y:Float, Length:Float, Speed:Float)
	
		Local l:Tbeam = New Tbeam
		
		l.ox# = x
		l.oy# = y
		l.dx# = x
		l.dy# = y
		l.Length = Length	
		l.Speed = Speed
		
		l.link:TLink = List_beams.addLast( l ) 'store a TLink reference in the object for fast reference and removal

	
	End Function	
	
	
	Function Compute_all()
		For Local l:Tbeam = EachIn List_beams
		
			If Abs (l.oy-l.dy) &lt; l.Length Then
				l.dy = l.dy - 1 * l.Speed#
			Else
				l.dy = l.dy - 1 * l.Speed#
				l.oy = l.oy - 1 * l.Speed#
			End If
			
			If l.oy# &lt; 0 Then 
				l.link.remove() 'removing a link is much faster since it can just re-assign it's prev and next items to each other
			End If
		Next
	End Function
	
	
	Function Draw_all()
		Local oldR : Int, oldG : Int, oldB : Int
		Local oldBlend :Int = GetBlend()
		GetColor oldR, oldG, oldB
		'SetColor 50,50,255
		SetColor 255,0,0
		SetBlend LIGHTBLEND
		For Local l:Tbeam = EachIn List_beams
			'SetColor 0,255,0
			'DrawLine l.ox-10, l.oy,l.dx#-10, l.dy#
			
			SetAlpha(0.25)
			SetScale 4, (l.dy#-l.oy#)'*1.1
			DrawImage himage, l.ox - ( himage.width * 1.5), l.oy' + (l.length * 0.05)
			
			SetAlpha(0.5)
			SetScale 2, (l.dy#-l.oy#)'*1.05
			DrawImage himage, l.ox - ( himage.width / 2), l.oy' + (l.length * 0.025)
			
			SetAlpha(1)
			
			SetScale 1.0,l.dy#-l.oy#
			DrawImage himage, l.ox, l.oy
			SetScale 1,1


		Next
		SetColor oldR,oldG,oldB
		SetBlend oldBlend
	End Function
	
End Type

SetColor 255,255,255

Local frameCount: Int = 0
Local fps:Int = 0
Local startTime:Int = MilliSecs()

While Not KeyDown(KEY_eSCAPE)
	Cls

		Plot MouseX(), MouseY()
		
		If MouseDown(1) Then 
			If (MilliSecs() - laserRepeatRate &gt; laserLastFired) Then
				Tbeam.Init (MouseX(), MouseY(), laserLength, laserSpeed)
				laserLastFired = MilliSecs()
			EndIf
		EndIf
		
		Tbeam.Compute_all()
		Tbeam.Draw_all()
		DrawText "FPS: " + fps, 10, 10
		'DrawText "count: " + CountList(List_beams), 10, 22
		frameCount:+1
		If (MilliSecs()-startTime &gt;= 1000) Then
			fps = frameCount
			frameCount = 0;
			startTime = MilliSecs()
		EndIf
	Flip
	
	
Wend

ShowMouse()

Function ByteHex:String( val:Byte )
	Local buf:Short[2]
	For Local k:Int=1 To 0 Step -1
		Local n:Int=(val&amp;15)+Asc("0")
		If n&gt;Asc("9") n=n+(Asc("A")-Asc("9")-1)
		buf[k]=n
		val:Shr 4
	Next
	Return String.FromShorts( buf,2 )
End Function

</textarea><br><br>I get a difference of about 2000 FPS between yours and mine (and it looks a little nicer)<br><br>I added the ability to adjust the width of the graphic, added alpha to the graphic edges and created blurred duplicates to try to add a glow.<br><br>Also added laser repeat rate and speed<br><br><font class="tiny">Last edited 2010</font><br><br><font class="tiny">Last edited 2010</font> <br><br></td></tr></table><br>
<a name="1061863"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >hub</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Perturbatio ! <br><br></td></tr></table><br>
<a name="1062008"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Perturbatio</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> yar! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
