<!DOCTYPE html><html lang="en" ><head ><title >Simulating dog racing.</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Simulating dog racing.</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >Simulating dog racing.</a><br><br>
<a name="886072"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Xerra</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've been following Wave's Beginners guide to BlitzMax - mainly on using types, functions and methods as I've always had problems getting my head round those and I've not done any programming for a long while now.<br><br>Anyway, I put this together as I fancied creating a little gambling game with dog racing, betting, odds and so on and wanted to see something moving so I could get an idea on simulating the actual races. It works ok but I'm after some of you experts to have a look over it and see if I'm doing anything sloppy specifically with the way the random number is used to increase a dogs speed of movement.<br><br>I don't think I've really got my head around types totally yet although doing this has helped massively so I'd appreciate any suggestions or advice.<br><br>Thanks.<br><br><pre class=code>
' ** Dog racing by Tony Black - 29/6/08
' ** Created as an example to learn the use of functions, types and methods.
'
Strict
Global YPos:Int
Global N:Int
Global List:TList ' Keeps the dogs inside Dogtype
Global Finished:Byte = False
Global Winner:String

Type DogType
	Field X:Int
	Field Y:Int
	Field Speed:Int
	Field Name:String
	Field Colour:Int

	Method Draw() ' Display the dog on screen.
		DrawOval (X , Y ,50 , 25) 
	End Method

	Method Move() ' Move dog according to Speed along its X axis
		Speed=Rand(1,5)
		X:+ Speed
		'DrawText Name+" "+Colour+" "+X+" "+Y+" "+Speed,0,30 
		If X &gt; 760
			Finished = True
			Winner=Name
		EndIf
	End Method
	
	Method Reset() ' Reset the dogs starting positions for new race
		Speed = 1
		X = 100
	End Method
	
	Function Create() 
		Local NewDog:DogType ' declares a variable to store our Dogs type.
		NewDog = New DogType ' puts the address of the new Dog into the variable NewDog
		NewDog.X = 100 ; NewDog.Y = YPos ' Sets the start position of the Dog on screen.
		' YPos is global so it can have the dogs on different start positions down the screen
		' by incrementing 20 each time a dog is created or they'd all be in the same spot on screen.

		' Let's name the puppies. I couldn't work out how to run through a the type and ammend the
		' type.name field so had to name them on creation of each dog using the following method which
		' is a bit scrappy. N is the counter for/next loop calling the creation function for each dog.
		
		If N = 1
			NewDog.Name = "Patch"
			NewDog.Colour = 20
		EndIf
		If N = 2
			NewDog.Name = "Fido"
			NewDog.Colour = 40
		EndIf
		If N = 3
			NewDog.Name = "Sparky"
			NewDog.Colour = 60
		EndIf
		If N = 4
			NewDog.Name = "Rex"
			NewDog.Colour = 80
		EndIf
		If N = 5
			NewDog.Name = "Blue"
			NewDog.Colour = 100
		EndIf
		If N = 6
			NewDog.Name = "Buddy"
			NewDog.Colour=150
		EndIf
		If N = 7
			NewDog.Name = "Lassie"
			NewDog.Colour = 200
		EndIf
		If N = 8
			NewDog.Name = "Mutley"
			NewDog.Colour = 250
		EndIf
		
		If Not List Then List = CreateList() 
		List.AddLast (NewDog) ' Puts the new dog into our dogs list.
	End Function
	
End Type

Graphics 800 , 600
Global DogList:TList = CreateList() ' Create a list to store all Dogs

' Create the dogs.
For N = 1 To 8 ' Fixed number of dogs as they are all named.
	YPos=100+N*40
	DogType.Create()
Next

While Not KeyHit(Key_Escape)
	Flip ; Cls
	DrawText "Press Esc to Exit...." , 0 , 0
	YPos=140
	For Local Dog:DogType = EachIn List
		Dog.Draw()
		Dog.Move()
		SetColor 255,100,Dog.Colour
		DrawText Dog.Name , 0 , YPos
		YPos:+ 40
	Next
	If Finished=True
		SetColor 255,255,200
		DrawText "The Winner: "+Winner+" ... Press Space to Restart.",0,500
		Flip
		Repeat
		Until KeyHit(Key_Space) 
		Finished = False
		For Local Dog:DogType = EachIn List
			Dog.Reset() 
		Next
	EndIf
Wend
</pre> <br><br></td></tr></table><br>
<a name="886207"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> firt of I am not an expert sence I have about two years of self teaching myself OOP. But I can tell you how I might have done it. and who knows I might be learning something in the process.<br>First thing use the least amount of globals as possible.<br>globals hinders program speed with the exception of glabals in types. It may not be abvious in this sample but it is a nice habit to develop.<br>Second when you create a type, try having everything related to that type included in it. YPos is the same as the y for each dog So yust eliminate it and assign the value directly to the dog's y instead.<br><br>third when you create a type try making it as usable as possible, you don't want to mess with it everytime you want to use new names for the dogs. <br><br>finally I would moved the flip to the bottom of the code. This is a minor thing sence you are flipping the page at the beggining of the program/game with out having drawn anything on the screen which makes it useless.<br><br>I re-did your code to how I would have done it.<br>some of the stuff I did not mention is just my way of doing things. It is not necessarily better than what you had done. <br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' ** Dog racing by Tony Black - 29/6/08
' ** Created as an example to learn the use of functions, types and methods.
'
Strict
Local Winner:String = Null

Type DogType
	Field X:Int
	Field Y:Int
	Field Speed:Int
	Field Name:String
	Field Colour:Int
	Global List:TList  '***********************************

	Method Draw() ' Display the dog on screen.
		SetColor 255,100,Colour '**********************************
		DrawText Name , 0 , Y   '**********************************
		DrawOval (X , Y ,50 , 25) 
	End Method

	Method Move:String() ' Move dog according to Speed along its X axis
		Speed=Rand(1,5)
		X:+ Speed
		'DrawText Name+" "+Colour+" "+X+" "+Y+" "+Speed,0,30 
		If X &gt; 760
			Return name
		EndIf
		Return Null
	End Method
	
	Method Reset() ' Reset the dogs starting positions for new race
		Speed = 1
		X = 100
	End Method
	
	Function Create(n:Int,name:String,color:Int) 
		Local NewDog:DogType ' declares a variable to store our Dogs type.
		NewDog = New DogType ' puts the address of the new Dog into the variable NewDog
		NewDog.X = 100 ; ' Sets the start position of the Dog on screen.
		' YPos is global so it can have the dogs on different start positions down the screen
		' by incrementing 20 each time a dog is created or they'd all be in the same spot on screen.

		' Let's name the puppies. I couldn't work out how to run through a the type and ammend the
		' type.name field so had to name them on creation of each dog using the following method which
		' is a bit scrappy. N is the counter for/next loop calling the creation function for each dog.
		NewDog.Name = name '****************************************
		NewDog.Colour = color'**************************************
		NewDog.Y = 100+n*40 '***************************************
		' the fact that List is a global with in a type, it will be initialized only once.
		If Not NewDog.List Then NewDog.List = CreateList()  '*******************************
		NewDog.List.AddLast (NewDog) ' Puts the new dog into our dogs list. ********************************
	End Function
	
End Type

Graphics 800 , 600
Global DogList:TList = CreateList() ' Create a list to store all Dogs

' Create the dogs.
'********************************************
	DogType.Create(1 , "Patch" , 20) '
	DogType.Create(2 , "Fido" , 40) 
	DogType.Create(3 , "Sparky",60)
	DogType.Create(4 , "Rex" , 80) 
	DogType.Create(5 , "Blue" , 100) 
	DogType.Create(6 , "Buddy" , 150)
	DogType.Create(7 , "Lassie" , 200)
	DogType.Create(8 , "Mutley" , 250)
'********************************************

While Not KeyHit(Key_Escape)
	Cls
	DrawText "Press Esc to Exit...." , 0 , 0
	If winner = Null
		For Local Dog:DogType = EachIn DogType.List
			If winner
				dog.move() 'if there is a winner before the last dog continue moving until the last dog is processed.
			Else
				winner = Dog.Move() 
			EndIf
		Next
	EndIf
	
	For Local dog:DogType = EachIn DogType.List
			dog.draw() 
	Next
	
	If Winner &lt;&gt; Null
		SetColor 255,255,200
		DrawText "The Winner: "+Winner+" ... Press Space to Restart.",0,500
		If KeyHit(Key_Space) 
			For Local Dog:DogType = EachIn DogType.List
				Dog.Reset() 
			Next
			winner = Null
		endif
	EndIf
	Flip()
Wend
</textarea><br><br>[edited]<br>modified the restart loop<br>[/edited] <br><br></td></tr></table><br>
<a name="886222"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >christian223</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> You know, i think you got it allright, after all, objects exist to help us, they exist to simplify things and to make larger projects easier to work with. The key is to divide everything into manageable pieces and to make a big things from little ones.<br><br>I recommend you to instead of doing something simple as dogs, try doing cars for different types of people or maybe different types of robots, for example, do a game about different kinds of people and theyr cars. The important thing for you is to decompose all the parts that make a car that makes it able to do what it does. An engine would be a type, the fuel of the engine another type, the tires another type, and each different type would be used by the "Car" type with its methods.<br><br>What type of car would need a millionaire/veterinarian/war veteran/president/etc ?, what kind of tires?, what kind of seats?, what kind of interior decoration?, what kind of electronics will the car have?, think on all the parts a car would have. You dont even need to code it, just writte it on paper.<br><br>When i was in school, my teacher told us to do as if we where working on a store, and make us writte everything we would need to do from the time we entered the store to the time we closed the store, detail by detail, and thats what you need to think on, on what needs to be done, and its details and all its parts and how they fit in the whole.<br><br>I hope that helps :) <br><br></td></tr></table><br>
<a name="886337"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Xerra</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Jesse<br><br>Thanks for your comments - they're much appreciated. I see where you're going with the redundant bits. I can pick up a few pointers there. The extra flip at the start of the loop you mentioned was there because I'd put the code in to pause everything while displaying the winning dog as I recall. Definetly needed some optimising there, then :-)<br><br>I wasn't aware that the use of globals was that demmanding so I can watch out for that too. It's kind of force of habbit that I've been using them willy-nilly until recently as I thought my programming style was getting a bit messy. I only recently got in the habbit of using strict to combat that as I hate looking at code that isn't as readable as possible to someone else looking at it.<br><br>@Christian223<br><br>Dogs was just an example I was thinking about to test out messing with types as I've recently been to Yarmouth dogs and I also used to love the arcades on a pier at the seaside when I was a kid that had those daft machines you could use to bet on the different colours. I kind of ran with the idea of simulating something similar myself as a programming exersize but now I fancy making a bit of a game out of it.<br><br>I suspect it's very possible to adapt it to the other ideas you mention with not too much work if following what's been said. Using a self contained type to do as much work as it can without having to change other bits of code around it is definetly the way to make the whole job easier.<br><br>I worked out a lot of ideas on paper yesterday to expand on the original concept to see how it works as a game so I'll run with it a bit and see how it comes together as I'm bound to learn a few more new tricks.<br><br>Thanks a lot for the comments, guys. More welcome by all means - i'll take all the advice I can get that'll help me get better at this. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
