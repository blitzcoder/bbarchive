<!DOCTYPE html><html lang="en" ><head ><title >How To Create Window with CreateWindowEx ?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >How To Create Window with CreateWindowEx ?</h1><a href="forums.php" >Archives Forums</a>/<a href="topics.php?forum=108" >Win32 Discussion</a>/<a href="#bottom" >How To Create Window with CreateWindowEx ?</a><br><br>
<a name="557993"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >regaa</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I fail badly when i try to create a window with this api-call.<br><br>here is the code: (i am sure there are lot of bugs or so, but the compiler does not complain) <br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Global CreateWindowEx(window,Title$z,message$z, flags,x,y,w,h,Parent,guiid, hInstance,dunno);

Const WS_OVERLAPPED	= 0
Const WS_VISIBLE	= $10000000

user32 = LoadLibraryA ("user32.dll")
If user32=Null
	Notify("Konnte user32.dll nicht laden.")
Else
	CreateWindowEx=GetProcAddress(user32,"CreateWindowExA")
EndIf

CreateWindowEx(0,"jofgddfggdf","dgfdgf",WS_OVERLAPPED | WS_VISIBLE,0,0,640,480,0,0,hInstance,Null)


While Not KeyDown(KEY_ESCAPE)
Wend
</textarea> <br><br></td></tr></table><br>
<a name="558001"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SillyPutty</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> dont you need to create a WNDCLASSEX structure and call RegisterClassEx before calling the createwindow function ? <br><br></td></tr></table><br>
<a name="558005"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >regaa</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> good question! is there anybody who have basic winapi knowledge with some bm examples? <br><br></td></tr></table><br>
<a name="558007"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SillyPutty</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I just checked in the msdn, it says you need to register the class before makking a call to createwindow. <br><br></td></tr></table><br>
<a name="558009"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >regaa</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, thank you very much. I will post the next error in a few minutes as i will make some errors^^.<br><br>1st problem:<br><br>msdn say:<br><br>he RegisterClassEx function registers a window class for subsequent use in calls to the CreateWindow or CreateWindowEx function.<br><br>Syntax<br><br>ATOM RegisterClassEx(      <br><br>    CONST WNDCLASSEX *lpwcx<br>);<br><br>Parameters<br><br>    lpwcx<br>        [in] Pointer to a WNDCLASSEX structure. You must fill the structure with the appropriate class attributes before passing it to the function. <br><br><br><br><br><br>WNDCLASSEX should look like this:<br><br>typedef struct {<br>    UINT cbSize;<br>    UINT style;<br>    WNDPROC lpfnWndProc;<br>    int cbClsExtra;<br>    int cbWndExtra;<br>    HINSTANCE hInstance;<br>    HICON hIcon;<br>    HCURSOR hCursor;<br>    HBRUSH hbrBackground;<br>    LPCTSTR lpszMenuName;<br>    LPCTSTR lpszClassName;<br>    HICON hIconSm;<br>} WNDCLASSEX, *PWNDCLASSEX;<br><br>How to do this in BM? <br><br></td></tr></table><br>
<a name="558016"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SillyPutty</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> lol yeah, that is where I fell over :)<br><br>I am sure someone here knows how :) <br><br></td></tr></table><br>
<a name="558019"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kev</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> heres a c function from my max gui module thats still in development.<br><br><pre class=code>

// The name of our window class.
TCHAR szClassName[] = TEXT("MicroMax_CLASS") ;

//
int CLASS_GOOD;

// Creates GUI Window.
int MicroMax_CreateWindow(LPCTSTR szWindowText,int x,int y,int width,int height,int group,int flags){
	
	HINSTANCE hThisInstance = GetModuleHandle(0);
	
	if(!CLASS_GOOD){
	
  		// Window class structure.
  		WNDCLASSEX wcx ;

  		// Now we must fill in the members of WNDCLASSEX.
  		wcx.hInstance     = hThisInstance;
  		wcx.lpszClassName = szClassName ;
  		wcx.lpfnWndProc   = (WNDPROC)MicoMaxWinProc;
  		wcx.style         = CS_DBLCLKS;
  		wcx.hIcon         = LoadIcon (NULL, IDI_APPLICATION);
  		wcx.hIconSm       = LoadIcon (NULL, IDI_APPLICATION);
  		wcx.hCursor       = LoadCursor (NULL, IDC_ARROW);
  		wcx.lpszMenuName  = NULL;
  		wcx.cbSize        = sizeof (WNDCLASSEX);
  		wcx.cbClsExtra    = 0;
  		wcx.cbWndExtra    = 0;
  		wcx.hbrBackground = GetSysColorBrush(COLOR_3DFACE) ;

  		// Register our window class with the operating system.
  		// If there is an error, exit program.
  		if ( !RegisterClassEx (&amp;wcx) )
  		{
    		MessageBox( 0, TEXT("Failed to register window class!"),TEXT("Error!"), MB_OK|MB_ICONERROR ) ;
    		return 0;
  		}
		
		// class created.
		CLASS_GOOD = TRUE;
	}

	//	
	int child_window=1;

	if(!group){
		group = (int)HWND_DESKTOP;
		child_window=0;
	}else{
		child_window=1;
	}
	
	HWND hwnd = CreateWindowEx ( 0, szClassName, szWindowText, WS_OVERLAPPEDWINDOW|WS_VISIBLE, x, y, width, height,(HWND)group,  0, hThisInstance, 0 ); 
	
	if(child_window==1){
		SetParent(hwnd,(HWND)group);
	}
	
	if ( !IsWindow(hwnd) ) { 
		return 0;
	}
			
	return (int)hwnd;
	
}

</pre><br><br>kev <br><br></td></tr></table><br>
<a name="558024"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SillyPutty</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> here is my feeble attempt, maybe it might  help :(<br><br><pre class=code>
Global CreateWindowEx(window,Title$z,message$z, flags,x,y,w,h,Parent,guiid, hInstance,dunno);
Global RegisterClassEx(wndclassex:WNDCLASSEX);
Global GetModuleHandle(modname$);

Const WS_OVERLAPPED	= 0
Const WS_VISIBLE	= $10000000

user32 = LoadLibraryA ("user32.dll")
Kernel32 = LoadLibraryA ("Kernel32.dll")
If user32=Null
	Notify("Konnte user32.dll nicht laden.")
Else
	CreateWindowEx=GetProcAddress(user32,"CreateWindowExA")
	RegisterClassEx=GetProcAddress(user32,"RegisterClassExA")
	GetModuleHandle=GetProcAddress(Kernel32,"GetModuleHandleA")
EndIf

If user32=Null
	Notify("Konnte Kernel32.dll nicht laden.")
Else
	GetModuleHandle=GetProcAddress(Kernel32,"GetModuleHandleA")
EndIf




myInstance% = GetModuleHandle(Null)

Type WNDCLASSEX
	Field size%
	Field style%
	Field lpfnWndProc%
	Field extra%
	Field wndExtra%
	Field hInstance%
	Field hIcon%
	Field hCursor%
	Field bg%
	Field menuname$
	Field classname$
	Field hIconSm%
End Type

wnd:WNDCLASSEX = New WNDCLASSEX
wnd.size = SizeOf(WNDCLASSEX)
wnd.style = CS_HREDRAW
wnd.lpfnWndProc = Null
wnd.extra = DLGWINDOWEXTRA
wnd.wndExtra=0
wnd.hInstance = myInstance 
wnd.hIcon=0
wnd.hCursor=0
wnd.bg=COLOR_WINDOW
wnd.menuname=Null
wnd.classname="test"
wnd.hiconsm=Null


If Not RegisterClassEx(wnd) Then Notify("could not register class")


If Not CreateWindowEx(0,"test","test window",WS_OVERLAPPED | WS_VISIBLE,0,0,640,480,0,0,myInstance ,Null) Then Notify("could not create window") 



While Not KeyDown(KEY_ESCAPE)
Wend


</pre> <br><br></td></tr></table><br>
<a name="558036"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kev</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> without testing Deux example, wnd.lpfnWndProc needs to be an address of a function.<br><br>some thing like this, we need the address of the winproc.<br><br><pre class=code>
Local WinProcADDR:Byte Ptr = WinProc
If WinProcADDR &gt; Null Then
	Print "winproc addr obtained "
EndIf

Function WinProc(hWnd:Int,Msg:Int,wParam:Int,lParam:Int)

End Function 
</pre><br><br>kev <br><br></td></tr></table><br>
<a name="558038"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SillyPutty</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> yeah, I just rearranged code, never really tested it myself, sorry for not doing that.<br><br>I have been looking at the user32.bmx code, and I think that may be helpful for regaa, the constants and functions you need are in there I think. <br><br></td></tr></table><br>
<a name="558050"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kev</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> here you go a working example using just blitzmax, this creates a window and waits for the close button.<br><br><pre class=code>

Import "-luser32"
Extern 
	Function GetSysColorBrush:Int(nIndex:Int) = "GetSysColorBrush@4"
End Extern

Const COLOR_BTNFACE = 15

Local WinProcADDR:Byte Ptr = WinProc
If WinProcADDR &gt; Null Then
	Print "winproc addr obtained "
EndIf

Local classname$ = "WindowCLASS"
Local CLASSNAME_ADRR:Byte Ptr = Byte Ptr classname$

Local class:WNDCLASS = New WNDCLASS
Local classADDR:Byte Ptr = Byte Ptr class 
class.hInstance = GetModuleHandleA(0)
class.lpfnWndProc = WinProcADDR
class.lpszClassName = CLASSNAME_ADRR
class.hbrBackground = GetSysColorBrush(COLOR_BTNFACE) 

If(RegisterClassA(classADDR) &lt;&gt; Null) Then
	Print "class reg ok"
EndIf

Local message:MSG = New MSG
Local messageADDR:Byte Ptr = Byte Ptr message

Local windowTitle$ = "Testing"
Local windowTitleADDR:Byte Ptr = Byte Ptr windowTitle$

Const WS_CAPTION = $C00000
Const WS_SYSMENU = $80000
Const WS_VISIBLE = $10000000
Const WS_MINIMIZEBOX = $20000
Const WS_MAXIMIZEBOX = $10000

Const WM_DESTROY = 2

Local lpParamADDR:Byte Ptr

Local window = CreateWindowExA( 0,CLASSNAME_ADRR,windowTitleADDR,WS_CAPTION | WS_MINIMIZEBOX | WS_MAXIMIZEBOX | WS_SYSMENU | WS_VISIBLE,100,100,200,200,0,0,GetModuleHandleA(0),lpParamADDR )

While Not KeyDown(KEY_ESCAPE) 
	GetMessageA(messageADDR,0,0,0)
	TranslateMessage(messageADDR)
	DispatchMessageA(messageADDR)
Wend 


'
'
'
Function WinProc(hWnd:Int,Msg:Int,wParam:Int,lParam:Int)
	
	Select Msg
		
		Case WM_DESTROY
			Notify "Bye!"
			End
			
	End Select
	
	Return DefWindowProcA(hWnd,Msg,wParam,lParam)
	
End Function 

</pre> <br><br>needs cleaning up.<br><br>kev. <br><br></td></tr></table><br>
<a name="558054"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >regaa</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> many thanks to that much examples. i will check'em out. god job dudes :P <br><br></td></tr></table><br>
<a name="558059"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Difference</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Very cool kev. Thanks! <br><br></td></tr></table><br>
<a name="558069"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >regaa</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> I tryed to extend some parts, but whenever i use SendMessage (declared in user32.bmx) i get this error:<br><br>undefined reference to `SendMessage@16<br><br>:( <br><br></td></tr></table><br>
<a name="558113"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kev</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> regaa im not sure why you get the error, but this will work.<br><br>add the sendmessage to the extern ... end extern<br><br><pre class=code> 
	Function SendMessage:Int(hWnd:Int,MSG:Int,wParam:Int,lParam:Int) = "SendMessageA@16"

</pre><br><br>then to test use SendMessage window,WM_DESTROY,0,0 after creating the window.<br><br>kev <br><br></td></tr></table><br>
<a name="558171"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >regaa</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you very much. I've learned some important BM basics :p . <br><br></td></tr></table><br>
<a name="558327"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SillyPutty</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> wow very cool, awsome kev ! <br><br>Thanks <br><br></td></tr></table><br>
<a name="558354"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Difference</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's one with some buttons and a keypress -  you can press [ESC] to quit.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Import "-luser32"
Extern 
	Function GetSysColorBrush:Int(nIndex:Int) = "GetSysColorBrush@4"
	Function SendMessage:Int(hWnd:Int,MSG:Int,wParam:Int,lParam:Int) = "SendMessageA@16"
End Extern

Const COLOR_BTNFACE = 15

Local WinProcADDR:Byte Ptr = WinProc
If WinProcADDR &gt; Null Then
	Print "winproc addr obtained "
EndIf

Local classname$ = "WindowCLASS"

Local class:WNDCLASS = New WNDCLASS
Local classADDR:Byte Ptr = Byte Ptr class 
class.hInstance = GetModuleHandleA(0)

class.lpfnWndProc = WinProcADDR

class.lpszClassName = Byte Ptr classname$


class.hbrBackground = GetSysColorBrush(COLOR_BTNFACE) 

If(RegisterClassA(classADDR) &lt;&gt; Null) Then
	Print "class reg ok"
EndIf

Local message:MSG = New MSG
Local messageADDR:Byte Ptr = Byte Ptr message

Local windowTitle$ = "Testing"

Const WS_CAPTION = $C00000
Const WS_SYSMENU = $80000
Const WS_VISIBLE = $10000000
Const WS_MINIMIZEBOX = $20000
Const WS_MAXIMIZEBOX = $10000

Const WS_CHILD           = $40000000



Const WM_DESTROY = 2

Const WM_KEYDOWN = $100

Const WM_COMMAND = $111



Local lpParamADDR:Byte Ptr



Local window = CreateWindowExA( 0,Byte Ptr classname$,Byte Ptr windowTitle$,WS_CAPTION | WS_MINIMIZEBOX | WS_MAXIMIZEBOX | WS_SYSMENU | WS_VISIBLE,100,100,400,350,0,0,GetModuleHandleA(0),lpParamADDR )



Global  btnOk = CreateWindowExA( 0 ,Byte Ptr "BUTTON",Byte Ptr "Ok",WS_CHILD | WS_VISIBLE,250,250,100,20,window ,0,GetModuleHandleA(0),lpParamADDR )
Global  btnCancel = CreateWindowExA( 0 ,Byte Ptr "BUTTON",Byte Ptr "Cancel",WS_CHILD | WS_VISIBLE,250,250+24,100,20,window ,0,GetModuleHandleA(0),lpParamADDR )





While Not KeyDown(KEY_ESCAPE) 
	GetMessageA(messageADDR,0,0,0)
	TranslateMessage(messageADDR)
	DispatchMessageA(messageADDR)
Wend 


'
'
'
Function WinProc(hWnd:Int,Msg:Int,wParam:Int,lParam:Int)
	
	Select Msg
		
		Case WM_DESTROY
		'	Notify "Bye!"
			End
		Case WM_KEYDOWN
		
			DebugLog "Keydown " + wParam + " " + lParam
			
			Select wParam
				Case 27 ' ESC
				
					SendMessage hWnd,WM_DESTROY,0,0
			End Select
			
		
		Case WM_COMMAND
		
			Select lParam
				Case  btnOk
					Notify "You clicked Ok"
				Case btnCancel
					Notify "You clicked Cancel"
			
			End Select
			
		Default
			'DebugLog Msg
		
			
	End Select
	
	Return DefWindowProcA(hWnd,Msg,wParam,lParam)
	
End Function
</textarea> <br><br></td></tr></table><br>
<a name="558381"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kev</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> just to make a note, under WM_COMMAND the HIWORD(wParam) specifies the notification code if the message is from a control, this will be 0 if the message is from a menu, the ID of the menu is found in the LOWORD(wParam)<br><br>heres 2 functions to get the low and hi words.<br><br><pre class=code>

Function LOWORD(value)
	Return value And $FFFF
End Function

Function HIWORD(value)
	Return (value Shr 16)
End Function

</pre><br><br>kev <br><br></td></tr></table><br>
<a name="558391"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Difference</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm trying to add a font, but my CreateFont call makes Blitz quit:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict
Import "-luser32"
'Import "-lkernel32"
Extern 
	Function GetSysColorBrush:Int(nIndex:Int) = "GetSysColorBrush@4"
	Function SendMessage:Int(hWnd:Int,MSG:Int,wParam:Int,lParam:Int) = "SendMessageA@16"
	Function CreateFontA:Int(nHeight,nWidth,nEscapement,nOrientation ,fnWeight, fdwItalic,dwUnderline , fdwStrikeOut,fdwCharSet, fdwOutputPrecision , fdwClipPrecision,fdwQuality, fdwPitchAndFamily, lpszFace:String) = "CreateFontA@56"
	Function GetLastError() = "GetLastError@0"
	
	'Function CreateFontIndirectA:Int(lpLogFont:LOGFONT) '= "CreateFontIndirectA"
	
End Extern


'Type LOGFONT 
'  Field lfHeight:Int
'  Field lfWidth:Int 
'  Field lfEscapement:Int
'  Field lfOrientation:Int
'  Field lfWeight:Int
'  Field lfItalic:Byte
'  Field lfUnderline:Byte
'  Field lfStrikeOut:Byte
'  Field lfCharSet:Byte
'  Field lfOutPrecision:Byte
'  Field lfClipPrecision:Byte
'  Field lfQuality:Byte
'  Field lfPitchAndFamily:Byte
'  'TCHAR lfFaceName[LF_FACESIZE]; 
'End Type




Const COLOR_BTNFACE = 15

Local WinProcADDR:Byte Ptr = WinProc
If WinProcADDR &gt; Null Then
	Print "winproc addr obtained "
EndIf

Local classname$ = "WindowCLASS"

Local class:WNDCLASS = New WNDCLASS
Local classADDR:Byte Ptr = Byte Ptr class 
class.hInstance = GetModuleHandleA(0)

class.lpfnWndProc = WinProcADDR

class.lpszClassName = Byte Ptr classname$


class.hbrBackground = GetSysColorBrush(COLOR_BTNFACE) 

If(RegisterClassA(classADDR) &lt;&gt; Null) Then
	Print "class reg ok"
EndIf

Local message:MSG = New MSG
'Local messageADDR:Byte Ptr = Byte Ptr message

Local windowTitle$ = "Testing"

Const WS_CAPTION = $C00000
Const WS_SYSMENU = $80000
Const WS_VISIBLE = $10000000
Const WS_MINIMIZEBOX = $20000
Const WS_MAXIMIZEBOX = $10000

Const WS_CHILD           = $40000000



Const WM_DESTROY = 2

Const WM_KEYDOWN = $100

Const WM_COMMAND = $111



Local lpParamADDR:Byte Ptr



Local window = CreateWindowExA( 0,Byte Ptr classname$,Byte Ptr windowTitle$,WS_CAPTION | WS_MINIMIZEBOX | WS_MAXIMIZEBOX | WS_SYSMENU | WS_VISIBLE,100,100,400,350,0,0,GetModuleHandleA(0),lpParamADDR )



Global  btnOk = CreateWindowExA( 0 ,Byte Ptr "BUTTON",Byte Ptr "Ok",WS_CHILD | WS_VISIBLE,250,250,100,20,window ,0,GetModuleHandleA(0),Byte Ptr 0 )
Global  btnCancel = CreateWindowExA( 0 ,Byte Ptr "BUTTON",Byte Ptr "Cancel",WS_CHILD | WS_VISIBLE,250,250+24,100,20,window ,0,GetModuleHandleA(0),Byte Ptr 0)


Global  lblHeadline = CreateWindowExA( 0 ,Byte Ptr "STATIC",Byte Ptr "I am the label, I am",WS_CHILD | WS_VISIBLE,10,10,100,20,window ,0,GetModuleHandleA(0),Byte Ptr 0 )


Local Fontname:String = "Arial" + Chr(0) + Chr(0)


Local myfont =  CreateFontA(-16,0,0,0 ,400, 0,0 , 0,0, 4 , 0,0, 0, Fontname) 


DebugLog "GetLastError() " + GetLastError()
DebugLog "myfont " + myfont 


If Not myfont

	'WaitKey
	'End


EndIf





Local bRet 

Repeat 

	bRet = GetMessageA( message, 0, 0, 0 )
	
	DebugLog "bRet " + bRet 
    
    If bRet = -1
    	'handle the error And possibly Exit
		DebugLog "handle the error And possibly Exit"
	Else
		TranslateMessage(message)
		DispatchMessageA(message)
	EndIf
Until bret =0 






DebugLog "here"

'
'
'
Function WinProc(hWnd:Int,Msg:Int,wParam:Int,lParam:Int)
	
	Select Msg
		
		Case WM_DESTROY
			Notify "Bye!"
			End
		Case WM_KEYDOWN
		
			DebugLog "Keydown " + wParam + " " + lParam
			
			Select wParam
				Case 27 ' ESC
				
		'			SendMessage hWnd,WM_DESTROY,0,0
			End Select
			
		
		Case WM_COMMAND
		
			Select lParam
				Case  btnOk
					Notify "You clicked Ok"
				Case btnCancel
					Notify "You clicked Cancel"
			
			End Select
			
		Default
			'DebugLog Msg
		
			
	End Select
	
	Return DefWindowProcA(hWnd,Msg,wParam,lParam)
	
End Function

</textarea> <br><br></td></tr></table><br>
<a name="558397"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Difference</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> I can add a font to ONE control using this code, adding the font to more than one control makes Blitz quit:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict
Import "-luser32"
'Import "-lkernel32"
Extern 
	Function GetSysColorBrush:Int(nIndex:Int) = "GetSysColorBrush@4"
	Function SendMessage:Int(hWnd:Int,MSG:Int,wParam:Int,lParam:Int) = "SendMessageA@16"
	'Function CreateFontA:Int(nHeight,nWidth,nEscapement,nOrientation ,fnWeight, fdwItalic,dwUnderline , fdwStrikeOut,fdwCharSet, fdwOutputPrecision , fdwClipPrecision,fdwQuality, fdwPitchAndFamily, lpszFace:String) = "CreateFontA@56"
	Function GetLastError() = "GetLastError@0"
	
	Function CreateFontIndirectA:Int(lpLogFont:Byte Ptr) = "CreateFontIndirectA@4"
	
End Extern


'Type LOGFONT 
 ' Field lfHeight:Int
'  Field lfWidth:Int 
'  Field lfEscapement:Int
'  Field lfOrientation:Int
'  Field lfWeight:Int
'  Field lfItalic:Byte
'  Field lfUnderline:Byte
'  Field lfStrikeOut:Byte
'  Field lfCharSet:Byte
'  Field lfOutPrecision:Byte
'  Field lfClipPrecision:Byte
'  Field lfQuality:Byte
'  Field lfPitchAndFamily:Byte
	 
'  'TCHAR lfFaceName[LF_FACESIZE]; 
'End Type


'5*4 + 8*1 + 32







Const COLOR_BTNFACE = 15

Local WinProcADDR:Byte Ptr = WinProc
If WinProcADDR &gt; Null Then
	Print "winproc addr obtained "
EndIf

Local classname$ = "WindowCLASS"

Local class:WNDCLASS = New WNDCLASS
Local classADDR:Byte Ptr = Byte Ptr class 
class.hInstance = GetModuleHandleA(0)

class.lpfnWndProc = WinProcADDR

class.lpszClassName = Byte Ptr classname$


class.hbrBackground = GetSysColorBrush(COLOR_BTNFACE) 

If(RegisterClassA(classADDR) &lt;&gt; Null) Then
	Print "class reg ok"
EndIf

Local message:MSG = New MSG
'Local messageADDR:Byte Ptr = Byte Ptr message

Local windowTitle$ = "Testing"

Const WS_CAPTION = $C00000
Const WS_SYSMENU = $80000
Const WS_VISIBLE = $10000000
Const WS_MINIMIZEBOX = $20000
Const WS_MAXIMIZEBOX = $10000

Const WS_CHILD           = $40000000



Const WM_DESTROY = 2

Const WM_SETFONT = $30

Const WM_KEYDOWN = $100

Const WM_COMMAND = $111



Local lpParamADDR:Byte Ptr



Local window = CreateWindowExA( 0,Byte Ptr classname$,Byte Ptr windowTitle$,WS_CAPTION | WS_MINIMIZEBOX | WS_MAXIMIZEBOX | WS_SYSMENU | WS_VISIBLE,100,100,400,350,0,0,GetModuleHandleA(0),lpParamADDR )



Global  btnOk = CreateWindowExA( 0 ,Byte Ptr "BUTTON",Byte Ptr "Ok",WS_CHILD | WS_VISIBLE,250,250,100,20,window ,0,GetModuleHandleA(0),Byte Ptr 0 )
Global  btnCancel = CreateWindowExA( 0 ,Byte Ptr "BUTTON",Byte Ptr "Cancel",WS_CHILD | WS_VISIBLE,250,250+24,100,20,window ,0,GetModuleHandleA(0),Byte Ptr 0)


Local lpParamADDR2:Byte Ptr


Global  lblHeadline = CreateWindowExA( 0 ,Byte Ptr "STATIC",Byte Ptr "I am the label, I am",WS_CHILD | WS_VISIBLE,10,10,100,20,window ,0,GetModuleHandleA(0),lpParamADDR2 )


Local Fontname:String = "Arial" + Chr(0) + Chr(0)


'Local myfont =  CreateFontA(-16,0,0,0 ,400, 0,0 , 0,0, 4 , 0,0, 0, Fontname) 





Local lf:TBank = CreateBank(60)


PokeInt lf,0,-10
PokeInt lf,4*4,1000

PokeByte lf,28+0,Asc("A")
PokeByte lf,28+1,Asc("r")
PokeByte lf,28+2,Asc("i")
PokeByte lf,28+3,Asc("a")
PokeByte lf,28+4,Asc("l")


Local myfont =CreateFontIndirectA(BankBuf(lf))


DebugLog "GetLastError() " + GetLastError()
DebugLog "myfont " + myfont 


If myfont

	SendMessage btnOk ,WM_SETFONT,myfont ,0
'	SendMessage btnCancel ,WM_SETFONT,myfont ,0
'	SendMessage lblHeadline ,WM_SETFONT,myfont ,True		


'	DebugLog "GetLastError() " + GetLastError()
	
EndIf


DebugLog "here"


Local bRet 

Repeat 




	bRet = GetMessageA( message, 0, 0, 0 )
	
	DebugLog "bRet " + bRet 
    
    If bRet = -1
    	'handle the error And possibly Exit
		DebugLog "handle the error And possibly Exit"
	Else
		TranslateMessage(message)
		DispatchMessageA(message)
	EndIf
Until bret =0 






DebugLog "here"

'
'
'
Function WinProc(hWnd:Int,Msg:Int,wParam:Int,lParam:Int)
	
	Select Msg
		
		Case WM_DESTROY
			Notify "Bye!"
			End
		Case WM_KEYDOWN
		
			DebugLog "Keydown " + wParam + " " + lParam
			
			Select wParam
				Case 27 ' ESC
				
					SendMessage hWnd,WM_DESTROY,0,0
			End Select
			
		
		Case WM_COMMAND
		
			Select lParam
				Case  btnOk
					Notify "You clicked Ok"
				Case btnCancel
					Notify "You clicked Cancel"
			
			End Select
			
		Default
			'DebugLog Msg
		
			
	End Select
	
	Return DefWindowProcA(hWnd,Msg,wParam,lParam)
	
End Function
</textarea> <br><br></td></tr></table><br>
<a name="558438"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kev</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> this is how i do it in winblitz3d. you shold be able to convert this easy.<br><br><pre class=code>

Function OpenFont(fontname$,width=12,height=8,bold=0,italic=0,underline=0)
	
	font = apiCreateFont(width,height,0,0,bold,underline,0,0,0,0,0,0,0,GRAB_StringAddr(fontname$))
	Return font 

End Function

Function Usefont(gadget,font)

	hdc = apiGetWindowDC(gadget)
	hfontprev = apiSelectObject(hdc,font)
	
	apiSendMessage gadget,WM_SETFONT,apiSelectObject(hdc,font),1
		
End Function

</pre><br><br>kev <br><br></td></tr></table><br>
<a name="558486"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Difference</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#23">[#23]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks, kev. I see I've been doing it all wrong, I'll make those SelectObject() changes and try again. <br><br></td></tr></table><br>
<a name="558742"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Difference</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#24">[#24]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm still having troubles: <a href="http://www.blitzbasic.com/Community/posts.php?topic=50237" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=50237</a> :) <br><br></td></tr></table><br>
<a name="558875"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Difference</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#25">[#25]</a></td></tr></table></td></tr><tr ><td class="posttext"> btw : LOWORD needs to be changed to: ("&amp;" not "AND" )<br><br><pre class=code>Function LOWORD(value)
	Return value &amp; $FFFF
End Function</pre> <br><br></td></tr></table><br>
<a name="675624"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ftbass</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#26">[#26]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br>I'm late... <br>Can anyone make this code by Kev work with the latest version of BlitzMax :<br><br><pre class=code>Import "-luser32"
Extern 
	Function GetSysColorBrush:Int(nIndex:Int) = "GetSysColorBrush@4"
End Extern

Const COLOR_BTNFACE = 15

Local WinProcADDR:Byte Ptr = WinProc
If WinProcADDR &gt; Null Then
	Print "winproc addr obtained "
EndIf

Local classname$ = "WindowCLASS"
Local CLASSNAME_ADRR:Byte Ptr = Byte Ptr classname$

Local class:WNDCLASS = New WNDCLASS
Local classADDR:Byte Ptr = Byte Ptr class 
class.hInstance = GetModuleHandleA(0)
class.lpfnWndProc = WinProcADDR
class.lpszClassName = CLASSNAME_ADRR
class.hbrBackground = GetSysColorBrush(COLOR_BTNFACE) 

If(RegisterClassA(classADDR) &lt;&gt; Null) Then
	Print "class reg ok"
EndIf

Local message:MSG = New MSG
Local messageADDR:Byte Ptr = Byte Ptr message

Local windowTitle$ = "Testing"
Local windowTitleADDR:Byte Ptr = Byte Ptr windowTitle$

Const WS_CAPTION = $C00000
Const WS_SYSMENU = $80000
Const WS_VISIBLE = $10000000
Const WS_MINIMIZEBOX = $20000
Const WS_MAXIMIZEBOX = $10000

Const WM_DESTROY = 2

Local lpParamADDR:Byte Ptr

Local window = CreateWindowExA( 0,CLASSNAME_ADRR,windowTitleADDR,WS_CAPTION | WS_MINIMIZEBOX | WS_MAXIMIZEBOX | WS_SYSMENU | WS_VISIBLE,100,100,200,200,0,0,GetModuleHandleA(0),lpParamADDR )

While Not KeyDown(KEY_ESCAPE) 
	GetMessageA(messageADDR,0,0,0)
	TranslateMessage(messageADDR)
	DispatchMessageA(messageADDR)
Wend 


'
'
'
Function WinProc(hWnd:Int,Msg:Int,wParam:Int,lParam:Int)
	
	Select Msg
		
		Case WM_DESTROY
			Notify "Bye!"
			End
			
	End Select
	
	Return DefWindowProcA(hWnd,Msg,wParam,lParam)
	
End Function </pre><br><br>This would be great :) <br><br></td></tr></table><br>
<a name="675862"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >klepto2</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#27">[#27]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just to clarify ;) you all know that these functions are already shipped with Blitzmax? <br> If not, then take a look to the pub.win32 module. this module already gives you everything you need (structs and functions) for these things. <br><br></td></tr></table><br>
<a name="676046"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ftbass</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#28">[#28]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks, I'll take a look :) <br><br></td></tr></table><br>
<a name="684171"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jan_</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#29">[#29]</a></td></tr></table></td></tr><tr ><td class="posttext"> hm, there is an Problem with the Maximize<br><br><pre class=code>
Strict 

Framework PUB.Win32 
Import BRL.System 
Import BRL.EventQueue 
Import BRL.basic 

Extern "Win32" 
   Function UpdateWindow(hWnd) 
End Extern 


Global CLASS_NAME:Byte Ptr="bbW32 Window".ToCString() ' Wie heißt unsere Anwendung 
Global hinst=GetModuleHandleA(0) ' was ist uinsere instance 


Local hwnd%=CreateHWND(600,400,0,"Jan_'s Win32 Application".ToCString()) ' Fenster erstellen 
'showwindow(hwnd,sw_show) ' fenster Zeigen 
'showwindow(hwnd,sw_hide) ' fenster verstecken 
Local ende% 
Repeat 
   WaitEvent() ' warten, das was passiert, das wichtig ist --&gt; 0% CPU last 
   ende=True 
Until (ende=True) 

DestroyWindow(hWnd) ' das Fenster wieder löschen 
End 

Function WndProc( hwnd,message,wp,lp ) "win32" 
   Local event:TEvent 

   Select message ' hier reagieren wir auf die aktionen die der nutzer auslöst 
   Case WM_CLOSE ' schließen 
      event=New TEvent 
      PostEvent(event) ' so, das sendet an Waitevent das es was machen soll (siehe oben) 
      postquitmessage(0) 
      DestroyWindow(hWnd) 
      End 
      Return 
   Case WM_DESTROY ' zerstören 
      event=New TEvent 
      PostEvent(event) ' so, das sendet an Waitevent das es was machen soll (siehe oben) 
      postquitmessage(0) 
      DestroyWindow(hWnd) 
      End 
      Return 
   Case WM_MOVE ' bewegen 
      'updatewindow(hwnd) 
      Return 
   Case WM_SIZE 
      'bbSystemEmitOSEvent hwnd,message,wp,lp,Null 
      'updatewindow(hwnd) 
      Return 
   Case WM_Paint 
      updatewindow(hwnd) 
      Return 
'   Case WM_MOUSEMOVE 
   'Default 
      'bbSystemEmitOSEvent hwnd,message,wp,lp,Null 
   End Select 
   Return DefWindowProcA( hwnd,message,wp,lp ) 
End Function 

Function CreateHWND(width,height,fullscreen,WINDOW_TITLE:Byte Ptr,PosX%=100,PosY%=100) ' Fenster erstellen 
    
   Local style,ex_style,hwnd 
   Local hinst=GetModuleHandleA(0) 
    
   Global wndClas 
    
   If Not wndClas 
      Local wc:WNDCLASS=New WNDCLASS 
      Local hinst=GetModuleHandleA(0) 
      'Local icon:Byte Ptr="IDI_INFORMATION".ToCString() 
      wc.style=CS_HREDRAW Or CS_VREDRAW ' Fensterstyle 
      wc.lpfnWndProc=WndProc 'Zeiger auf die Fensterprozedur 
      wc.cbClsExtra=0 
      wc.cbWndExtra=0 
      wc.hInstance=hinst ' Instanz des Programms 
      wc.hIcon=LoadIconA(Null, Null) 'Handle eines Icons das z.B. mit LoadIcon() geladen wurde 
      wc.hbrBackground = GetStockObject (LTGRAY_BRUSH) 
      wc.hCursor=LoadCursorA( Null,Byte Ptr IDC_ARROW ) 'Handle eines Cursors 
      wc.lpszClassName=CLASS_NAME ' Name der zu registrierenden Fensterklasse 
      wndClas=RegisterClassA( wc ) 
      If Not wndClas 
         Throw "Failed to register window class" 
      EndIf 
   EndIf 

   ex_style=WS_EX_TRANSPARENT 
   style=WS_VISIBLE|WS_SYSMENU|WS_CAPTION|WS_MAXIMIZEBOX|WS_MINIMIZEBOX|WS_SIZEBOX'|WS_SYSMENU 
    
   Local rect[]=[posx,posy,width+posx,height+posy] 

   hWnd=CreateWindowExA(.. 
      ex_style,.. 
      CLASS_NAME,.. 
      WINDOW_TITLE,.. 
      style,.. 
      posx,posy,width,height,.. 
      0,0,hinst,Null) 
   ShowWindow(hWnd,SW_SHOW); 

   MemFree WINDOW_TITLE 
   If Not hwnd Throw "Failed to create window" 
   Return hwnd 
End Function
</pre> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
