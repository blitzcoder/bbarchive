<!DOCTYPE html><html lang="en" ><head ><title >Tilemap Collision</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Tilemap Collision</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >Tilemap Collision</a><br><br>
<a name="977946"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >po</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm in a bit of a pickle. I'm making a platformer Commander Keen/Mario style, using an array of objects for the tiles. My problem is getting the player to accurately collide with the tiles so that he doesn't (appear to) overlap any of the tiles.<br><br>Up until now I've been using this system: <br>There's a field in TPlayer called cTile that holds the TTile object that the player's center is currently overlapping. In other words, it's the tile that the player is currently occupying. I then check if the player is overlapping the tile below him, to the right of him, to the left of him, and above him. For example:<br>If playerY+playerHeight&gt;tileBelowY Then playerY=tileBelowY-playerHeight<br>This works fine for all sides, except when dealing with the edge of say, a drop-off. Where in games like Mario, the player can walk to his very heels on the edge of a drop-off, using my method the player simply falls into the tile half-way through the player, looking and playing quite poorly.<br><br>So I figure the best method would be to use the CollideRect function instead. So I put CollideRect(tileX,tileY,tileWidth,tileHeight,0,1) in every tile's Draw() method, and checked for collision with the player using CollideRect(playerX,playerY,playerWidth,playerHeight,1,0) and found that I can indeed detect the collision, but I have no way of stopping the player from going through the tiles because I don't know which tile I'm colliding with. Is there a way to get which tile collided with the player? I use the function GetTile(x,y) to get tiles based off of coordinates, but coords don't help me much if I'm trying to avoid the player-on-edge-of-drop-off issue. Any ideas? <br><br></td></tr></table><br>
<a name="977985"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Po,<br><br>Heres a quick example of Tilemap collision:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
Graphics 800,600,0
SeedRnd MilliSecs()
Const TILE_SIZE% = 32
Const GRAVITY# = 0.6
Const MAPWIDTH% = 800/TILE_SIZE
Const MAPHEIGHT% = 600/TILE_SIZE

Global map:Int[MAPWIDTH, MAPHEIGHT]

Cls
'create player image
SetColor 1,1,1
DrawOval 0,0,30,30
SetColor 255,255,255
DrawOval 3,3,24,24
SetColor 255,0,0
DrawOval 5,5,20,20
Global playerImg:TImage=CreateImage(30,30,1,DYNAMICIMAGE|MASKEDIMAGE)
GrabImage playerImg,0,0

Cls
' create tile
SetColor 100,30,0
DrawRect 0,0,16,16
DrawRect 16,16,16,16
SetColor 147,70,0
DrawRect 16,0,16,16	
DrawRect 0,16,16,16
Global tile:TImage=CreateImage(TILE_SIZE,TILE_SIZE,1,DYNAMICIMAGE|MASKEDIMAGE)
GrabImage tile,0,0

Cls

MidHandleImage playerImg
ReadLevelData()

Type TSprite
	Field x#,y#
	Field dx#,dy#
	Field xspeed#,yspeed#
	Field image:TImage
	Field w%,h%
	Field w2%,h2%
	
	Method draw()
		DrawImage image, x, y
	End Method

End Type

Type TPlayer Extends TSprite
	Field jumping:Int = 0
	
	Function Create:TPlayer(x#,y#)
		Local p:TPlayer = New TPlayer
		p.x = x
		p.y = y
		p.xspeed = 5
		p.yspeed = 10
		p.image = playerImg
		p.w = ImageWidth(playerImg)
		p.h = ImageHeight(playerImg)
		p.w2 = p.w/2
		p.h2 = p.h/2		
		Return p
	End Function
	
	Method update()
		If KeyDown(KEY_RIGHT)
			Local tmpx# = x + xspeed
			If getTile(tmpx+w2,y+h2-1) = 0 And getTile(tmpx+w2,y-h2) = 0 And getTile(tmpx+w2,y) = 0
				x = tmpx
			Else
				x = Floor(((x+w2)/TILE_SIZE)+0.5)*TILE_SIZE - w2
			EndIf
		End If
		If KeyDown(KEY_LEFT)
			Local tmpx# = x - xspeed
			If getTile(tmpx-w2,y+h2-1) = 0 And getTile(tmpx-w2,y-h2) = 0 And getTile(tmpx-w2,y) = 0
				x = tmpx
			Else
				x = Floor(((x-w2)/TILE_SIZE)+0.5)*TILE_SIZE + w2
			EndIf
		End If
		If KeyDown(KEY_UP) And Not jumping
			dy = 0 - yspeed
			jumping = True
		EndIf
		If Not jumping
			If getTile(x-w2,y+h2) = 0 And getTile(x+w2,y+h2) = 0 And getTile(x,y+H2) = 0
				jumping = True
			EndIf
		EndIf
		If jumping
			dy:+GRAVITY
			Local tmpy# = y + dy
			If dy&lt;&gt;0
				If dy&gt;0
					If getTile(x-w2,tmpy+h2-1) &gt; 0 Or getTile(x+w2-1,tmpy+h2-1) &gt; 0 Or getTile(x,tmpy+h2-1) &gt; 0
						dy = 0
						jumping = False
						y = Floor(((tmpy+h2)/TILE_SIZE)+0.5)*TILE_SIZE - h2
					Else
						y = tmpy
					EndIf
				Else
					If getTile(x-w2,tmpy-h2) &gt; 0 Or getTile(x+w2-1,tmpy-h2) &gt; 0 Or getTile(x,tmpy-h2) &gt; 0
						dy = 0 
						y = Floor(((tmpy-h2)/TILE_SIZE)+0.5)*TILE_SIZE + h2
					Else
						y = tmpy
					EndIf
				EndIf
			End If
		End If
		
	End Method
End Type

SetColor 255,255,255
SetBlend ALPHABLEND

Global player:TPlayer = TPlayer.Create(100,200)

While Not AppTerminate()=1 And Not KeyHit(KEY_ESCAPE)
	player.update()
	Cls
		DrawMap()
		player.draw()
	Flip 1
Wend

Function ReadLevelData()
	For Local y:Int = 0 To MAPHEIGHT - 1
		For Local x:Int = 0 To MAPWIDTH - 1
			Local Data:Int
			ReadData Data
			map[x, y]= Data
		Next
	Next
End Function

Function DrawMap()
	For Local x:Int = 0 To MAPWIDTH - 1
		For Local y:Int = 0 To MAPHEIGHT - 1
			Select map[x, y]
				Case 1
					DrawImage Tile, x * TILE_SIZE, y * TILE_SIZE
			End Select
		Next
	Next
End Function

Function getTile%(x#,y#)
	Return map[Floor(x/TILE_SIZE), Floor(y/TILE_SIZE)]
End Function

DefData 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1
DefData 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1
DefData 1, 1, 1, 1, 1, 1, 0, 0, 0, 0, 0, 0, 1, 1, 1, 1, 1, 0, 0, 0, 0, 0, 0, 0, 1
DefData 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1
DefData 1, 1, 1, 1, 1, 0, 0, 1, 1, 1, 1, 0, 0, 0, 0, 1, 1, 1, 1, 0, 0, 0, 0, 0, 1
DefData 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1
DefData 1, 0, 0, 0, 0, 0, 0, 0, 0, 1, 1, 1, 1, 1, 1, 1, 1, 0, 0, 0, 0, 0, 0, 0, 1
DefData 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1
DefData 1, 1, 1, 1, 1, 0, 0, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1
DefData 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1
DefData 1, 1, 1, 1, 1, 1, 0, 0, 0, 0, 0, 0, 1, 1, 1, 1, 1, 0, 0, 0, 0, 0, 0, 0, 1
DefData 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1
DefData 1, 1, 1, 1, 1, 0, 0, 1, 1, 1, 1, 0, 0, 0, 0, 1, 1, 1, 1, 0, 0, 0, 0, 0, 1
DefData 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1
DefData 1, 0, 0, 0, 0, 0, 0, 0, 0, 1, 1, 1, 1, 1, 1, 1, 1, 0, 0, 0, 0, 0, 0, 0, 1
DefData 1, 0, 1, 0, 1, 1, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1
DefData 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 1, 1, 1, 1, 1, 1
DefData 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1
</textarea><br><br>Cheers! <br><br></td></tr></table><br>
<a name="979122"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >po</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah, thank you. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
