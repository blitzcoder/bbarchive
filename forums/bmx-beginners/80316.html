<!DOCTYPE html><html lang="en" ><head ><title >Verlet physics</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Verlet physics</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >Verlet physics</a><br><br>
<a name="902903"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi I have the demo of blitz max and wanted to try out all of the features before I bought it.  I was seeing how fast and easy to use it was when I came upon a problem.  I program in B3d and I can easily make a type within a type.  I know Bmax can do this but how??  Here is my code so far and I wanted to know how to make two verlet types inside of each constraint type.<br><br>in b3d I do it like this<br><pre class=code>Type Constraint							;Constraints constrain the verlets to certain distances from eachother
	Field v1.verlet						;First verlet in constraint
	Field v2.verlet						;Second verlet in constraint
	Field length#						;Length of the constraint
End Type
</pre><br><br><br>In bmax I can't figure it out  I already tried this and it doesn't work<br><br><pre class=code>Type TConstraint
	Field V1:TVerlet
	Field V2:TVerlet
	Field length#  ;etc...
</pre> <br><br></td></tr></table><br>
<a name="902907"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gabriel</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> PLEASE be more descriptive than "it doesn't work." Provide all the code of whatever "isn't working" and describe the problem you're experiencing if you want help. <br><br>It patently does work, so it's hard to identify your problem without symptoms or more code.<br><br><pre class=code>
Type TConstraint
	Field V1:TVerlet
	Field V2:TVerlet
	Field length#
End Type

Type TVerlet
	
End Type

C:TConstraint=New TConstraint
C.V1=New TVerlet
C.V2=New TVerlet
C.Length=4
</pre> <br><br></td></tr></table><br>
<a name="902919"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I fixed my old problem but now I have a new problem<br>It doesn't recognize addlast<br>here is my code<br><br><pre class=code>Graphics 640,480,0,2

Global Gravity# = .1
VerletList = CreateList()
ConstraintList = CreateList()

Type Tverlet
	Field x#,y#
	Field dx#,dy#
	Field ox#,oy#
	Field ID
	Field VID
	Field active
	Field mass#
	Field collided
	
	Method Update()
	
		If active = True Then
			tx# = x#-ox#
			ty# = y#-oy#
	
			ox# = tx#
			oy# = ty#
			
			x# = x# + tx#
			y# = y# + ty# - Gravity#
			
			If y# &gt; 640 Then
				collided = True
				y# = 640
			EndIf
			
		EndIf
	
	End Method
	Function CreateVerlet(x#,y#,ID,active = True)

		V:Tverlet = New Tverlet
		V.x# = x#
		V.y# = y#
		V.ID = ID
		V.Active = active
		V.AddLast VerletList

	End Function

	
End Type





Type TConstraint
	Field V1:TVerlet
	Field V2:TVerlet
	Field length#
	
	Method Update()
	
		tx# = V1.x#-V2.x#
		ty# = V1.y#-V2.y#
		
		dist# = Sqr(tx#*tx#+ty#*ty#)
		
		tx# = tx# / 2
		ty# = ty# / 2
		
		If dist# &lt;&gt; 0 Then
			diff# = (dist# - Length#) / dist#
		EndIf
		
		V1.x# = V1.x# - diff# * tx#
		V1.y# = V1.y# - diff# * ty#

		V2.x# = V2.x# + diff# * tx#
		V2.y# = V2.y# + diff# * ty#
	
	End Method
	
End Type</pre> <br><br></td></tr></table><br>
<a name="902920"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GfK</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> V isn't a TList. <br><br></td></tr></table><br>
<a name="902933"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok I fixed all of my previous problems but have a new one.  I'm not sure what it is so here is my code<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics 640,480,0,2

Global Gravity# = .1
Global VerletList = New TList
Global ConstraintList = New TList





Type Tverlet
	Field x#,y#
	Field dx#,dy#
	Field ox#,oy#
	Field ID
	Field VID
	Field active
	Field mass#
	Field collided
	
	Method Update()
	
		If active = True Then
			tx# = x#-ox#
			ty# = y#-oy#
	
			ox# = tx#
			oy# = ty#
			
			x# = x# + tx#
			y# = y# + ty# - Gravity#
			
			If y# &gt; 640 Then
				collided = True
				y# = 640
			EndIf
			
		EndIf
	
	End Method
	Method Draw()
	
	SetScale 1,1
	SetColor 255,255,255
	SetRotation 0
	
	DrawOval x# - 10 , y# - 10 , 20 , 20
	
	End Method
	
	
	Method AddLast( list:TList )
		link=list.AddLast( Self )
	End Method
	
End Type


Function CreateVerlet(x#,y#,ID,active = True)

		V:Tverlet = New Tverlet
		V.x# = x#
		V.y# = y#
		V.ID = ID
		V.Active = active
		V.AddLast VerletList
End Function



Type TConstraint
	Field V1:TVerlet
	Field V2:TVerlet
	Field length#
	
	Method Update()
	
		tx# = V1.x#-V2.x#
		ty# = V1.y#-V2.y#
		
		dist# = Sqr(tx#*tx#+ty#*ty#)
		
		tx# = tx# / 2
		ty# = ty# / 2
		
		If dist# &lt;&gt; 0 Then
			diff# = (dist# - Length#) / dist#
		EndIf
		
		V1.x# = V1.x# - diff# * tx#
		V1.y# = V1.y# - diff# * ty#

		V2.x# = V2.x# + diff# * tx#
		V2.y# = V2.y# + diff# * ty#
	
	End Method
	
	Method AddLast( list:TList )
		link=list.AddLast( Self )
	End Method
	
	Function ConstrainVerlets( V1:TVerlet , V2:TVerlet )
	
		C:TConstraint = New TConstraint
		C.V1:TVerlet = V1:TVerlet
		C.V2:TVerlet = V2:TVerlet
		C.Length# = Sqr((C.V1.x#-C.V2.x#)^2 + (C.V1.y#-C.V2.y#)^2)
		C.AddLast ConstraintList
			
	End Function
End Type




Function UpdatePhysics()

For V:TVerlet = EachIn VerletList:TList
	V.Update
Next

For V:TVerlet = EachIn VerletList:TList
	V.draw
Next

End Function
</textarea> <br><br></td></tr></table><br>
<a name="902935"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GfK</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Jesus, man.  At least tell us what the error is.<br><br>People aren't going to mess about sifting through all your code to find out where and what the problem is. <br><br></td></tr></table><br>
<a name="902937"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Terribly Sorry I don't know much at all about bmax<br>I think it is something to do with the Function UpdatePhysics at the very bottom although I am not really sure<br><br><div class="quote"> Jesus, man. At least tell us what the error is.<br> <br></div><br><br>I've only had the demo for a day so I am very sorry But this is the Beginners area <br><br></td></tr></table><br>
<a name="902940"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Htbaa</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Still, without saying what the problem is people can't do very much. <br><br></td></tr></table><br>
<a name="902942"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> I think it is something to do with the Function UpdatePhysics at the very bottom although I am not really sure <br></div><br>Does it crash?<br>Does it not compile?<br>Does it simply not do what you expect it to?<br>etc <br><br></td></tr></table><br>
<a name="902947"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> It says Identifier type doesn't match declared type  sorry I didn't know Exactly what you wanted<br><br>could someone just copy and paste into bmax and run it so they would know what the problem was exactly  I would think that would be the first thing to do. <br><br></td></tr></table><br>
<a name="902949"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> You should add Strict or SuperStrict to the top of the app.<br><br>You should declare each variable with its Type.<br><br>eg.<br><br>Global VerletList = New TList<br><br>would become<br><br>Global VerletList:TList = New TList<br><br><br>It'll help you with all these niggly issues. <br><br></td></tr></table><br>
<a name="902951"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you Bruce  :)  just what I was looking for   I will use strict from now on thank you again <br><br></td></tr></table><br>
<a name="902957"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sorry to come with more problems I actually got it working but Sadly it runs very slow.  I just wanted to know if it ran slow on anyone else's computer or if it was just mine.<br><br>I am very sorry but this time I have no Idea why it does this<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics 640,480,0,2

Global Gravity# = 1
Global VerletList:TList = New TList
Global ConstraintList:TList = New TList

CreateVerlet(100,100,1)

While Not AppTerminate()
Cls

UpdatePhysics()

Flip
Wend

End
Type Tverlet
	Field x#,y#
	Field dx#,dy#
	Field ox#,oy#
	Field ID
	Field VID
	Field active
	Field mass#
	Field collided
	
	Method Update()
	
		If active = True Then
			dx# = x#-ox#
			dy# = y#-oy#
	
			ox# = x#
			oy# = y#
			
			x# = x# + dx#
			y# = y# + dy# + Gravity#
			
			If y# &gt; 640 Then
				collided = True
				y# = 640
			EndIf
			
		EndIf
	
	End Method
	Method Draw()
	
	'SetScale 1,1
	'SetColor 255,255,255
	'SetRotation 0
	
		DrawOval x# - 10 , y# - 10 , 20 , 20
	
	End Method
	
	
	Method AddLast( list:TList )
		link=list.AddLast( Self )
	End Method
	
End Type


Function CreateVerlet(x#,y#,ID,active = True)

		V:Tverlet = New Tverlet
		V.x# = x#
		V.y# = y#
		V.ID = ID
		V.ox# = x#
		V.oy# = y#
		V.Active = active
		V.AddLast VerletList
End Function



Type TConstraint
	Field V1:TVerlet
	Field V2:TVerlet
	Field length#
	
	Method Update()
	
		tx# = V1.x#-V2.x#
		ty# = V1.y#-V2.y#
		
		dist# = Sqr(tx#*tx#+ty#*ty#)
		
		tx# = tx# / 2
		ty# = ty# / 2
		
		If dist# &lt;&gt; 0 Then
			diff# = (dist# - Length#) / dist#
		EndIf
		
		V1.x# = V1.x# - diff# * tx#
		V1.y# = V1.y# - diff# * ty#

		V2.x# = V2.x# + diff# * tx#
		V2.y# = V2.y# + diff# * ty#
	
	End Method
	
	Method AddLast( list:TList )
		link=list.AddLast( Self )
	End Method
	
	Function ConstrainVerlets( V1:TVerlet , V2:TVerlet )
	
		C:TConstraint = New TConstraint
		C.V1:TVerlet = V1:TVerlet
		C.V2:TVerlet = V2:TVerlet
		C.Length# = Sqr((C.V1.x#-C.V2.x#)^2 + (C.V1.y#-C.V2.y#)^2)
		C.AddLast ConstraintList
			
	End Function
End Type




Function UpdatePhysics()

For V:TVerlet = EachIn VerletList:TList
	V.Update
Next

For V:TVerlet = EachIn VerletList:TList
	V.draw
Next

End Function</textarea> <br><br></td></tr></table><br>
<a name="902958"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sledge</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Because your Graphics command calls for a two frame per second update! <br><br></td></tr></table><br>
<a name="902960"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> That one doesn't compile here. <br><br></td></tr></table><br>
<a name="902961"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sledge</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Does here -- oooooh controversy! <br><br></td></tr></table><br>
<a name="902963"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> My fault... I added Strict at the top...  silly of me, I know.<br><br>Yep. Like what Sledge says about the 2 frames/sec thing. <br><br></td></tr></table><br>
<a name="902966"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SebHoll</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> oooooh controversy!  <br></div><br>lol :-P <br><br></td></tr></table><br>
<a name="902972"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> oops  Silly me  I am used to B3d where graphics 640,480,0,2 means windowed mode  LOL  I am truly a beginner at bmax <br><br></td></tr></table><br>
<a name="902974"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sledge</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> Everyone does it -- the <a href="/posts.php?topic=41179" target="_blank">orientation guide</a> is worth a scan if you haven't already given it a squiz. <br><br></td></tr></table><br>
<a name="902983"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok I finished the basics of the physics engine and it works well Thanks for the advice sledge and to everyone that answered my questions which I know were not very good.<br><br>click the mouse to make it bounce around. :)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics 640,480

Global Gravity# = .1
Global VerletList:TList = New TList
Global ConstraintList:TList = New TList

V1:Tverlet = TVerlet.CreateVerlet(100,100,1)
V2:Tverlet = TVerlet.CreateVerlet(150,50,1)
V3:Tverlet = Tverlet.CreateVerlet(100,50,1)
V4:TVerlet = TVerlet.CreateVerlet(150,100,1)
Tconstraint.ConstrainVerlets(V1:Tverlet,V2:TVerlet)
Tconstraint.ConstrainVerlets(V1:TVerlet,V3:TVerlet)
Tconstraint.ConstrainVerlets(V2:TVerlet,V3:TVerlet)
Tconstraint.ConstrainVerlets(V1:Tverlet,V4:TVerlet)
Tconstraint.ConstrainVerlets(V2:Tverlet,V4:TVerlet)
Tconstraint.ConstrainVerlets(V3:Tverlet,V4:TVerlet)

While Not AppTerminate()
Cls

If MouseDown(1) Then
	V1.y# = V1.y# - 1
EndIf
UpdatePhysics()

Flip
Wend

End
Type Tverlet
	Field x#,y#
	Field dx#,dy#
	Field ox#,oy#
	Field ID
	Field VID
	Field active
	Field mass#
	Field collided
	
	Method Update()
	
		If active = True Then
			If collided = True Then
				fric# = .8
			Else
				fric# = 1
			EndIf
			collided = False
			dx# = (x#-ox#)*fric
			dy# = (y#-oy#)*fric
	
			ox# = x#
			oy# = y#
			
			x# = x# + dx#
			y# = y# + dy# + Gravity#
			
			If y# &gt; 480 Then
				collided = True
				y# = 480
				oy# = 480+(dy#/3)
			EndIf
			
		EndIf
	
	End Method
	Method Draw()
	
	'SetScale 1,1
	'SetColor 255,255,255
	'SetRotation 0
		DrawOval x# - 10 , y# - 10 , 20 , 20
	
	End Method
	
	
	Method AddLast( list:TList )
		link=list.AddLast( Self )
	End Method
	
	Function CreateVerlet:TVerlet(x#,y#,ID,active = True)

		V:Tverlet = New Tverlet
		V.x# = x#
		V.y# = y#
		V.ID = ID
		V.ox# = x#
		V.oy# = y#
		V.Active = active
		V.AddLast VerletList
		Return V
	
	End Function


End Type




Type TConstraint
	Field V1:TVerlet
	Field V2:TVerlet
	Field length#
	
	Method Update()
	
		tx# = V1.x#-V2.x#
		ty# = V1.y#-V2.y#
		
		dist# = Sqr(tx#*tx#+ty#*ty#)
		
		tx# = tx# / 2
		ty# = ty# / 2
		
		If dist# &lt;&gt; 0 Then
			diff# = (dist# - Length#) / dist#
		EndIf
		
		V1.x# = V1.x# - diff# * tx#
		V1.y# = V1.y# - diff# * ty#

		V2.x# = V2.x# + diff# * tx#
		V2.y# = V2.y# + diff# * ty#
		
		SetColor 255,255,255
		DrawLine V1.x#,v1.y#,v2.x#,v2.y#
	End Method
	
	Method AddLast( list:TList )
		link=list.AddLast( Self )
	End Method
	
	Function ConstrainVerlets( V1:TVerlet , V2:TVerlet )
	
		C:TConstraint = New TConstraint
		C.V1:TVerlet = V1:TVerlet
		C.V2:TVerlet = V2:TVerlet
		C.Length# = Sqr((C.V1.x#-C.V2.x#)^2 + (C.V1.y#-C.V2.y#)^2)
		C.AddLast ConstraintList
			
	End Function
End Type




Function UpdatePhysics()

For V:TVerlet = EachIn VerletList:TList
	V.Update
Next

For C:TConstraint = EachIn ConstraintList:TList
	C.Update
Next

'For V:TVerlet = EachIn VerletList:TList
'	V.draw
'Next

End Function</textarea> <br><br></td></tr></table><br>
<a name="903199"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kurator</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> you can put the lists into the types, which makes it easier to integrate into another code<br><br><pre class=code>
Graphics 640,480

Global Gravity# = .1

Local BoxStack:Tbox[] = New TBox[50]

For Local i:Int = 0 To 49

	BoxStack[i] = New TBox
	
	BoxStack[i].V1 = TVerlet.CreateVerlet( 0+i*5, 50+i*1, i)
	BoxStack[i].V2 = TVerlet.CreateVerlet(50+i*5,  0+i*1, i)
	BoxStack[i].V3 = Tverlet.CreateVerlet( 0+i*5,  0+i*1, i)
	BoxStack[i].V4 = TVerlet.CreateVerlet(50+i*5, 50+i*1, i)
	
	Tconstraint.ConstrainVerlets(BoxStack[i].V1, BoxStack[i].V2)
	Tconstraint.ConstrainVerlets(BoxStack[i].V1, BoxStack[i].V3)
	Tconstraint.ConstrainVerlets(BoxStack[i].V2, BoxStack[i].V3)
	Tconstraint.ConstrainVerlets(BoxStack[i].V1, BoxStack[i].V4)
	Tconstraint.ConstrainVerlets(BoxStack[i].V2, BoxStack[i].V4)
	Tconstraint.ConstrainVerlets(BoxStack[i].V3, BoxStack[i].V4)
	
Next

Local oldtime:Int

While Not AppTerminate()
	Cls
	If MouseDown(1) Then
		For i = 0 To 49
			BoxStack[i].V1.y = BoxStack[i].V1.y - 1
		Next
	EndIf
	
	oldtime = MilliSecs()
	
	TVerlet.UpdateAll()
	TConstraint.UpdateAll()
	
	DrawText "Calculation Time "+String(MilliSecs()-oldtime)+" ms", 10, 10
	
	Flip
Wend

End

Type TBox
	
	Field V1:Tverlet
	Field V2:Tverlet
	Field V3:Tverlet
	Field V4:Tverlet
	
EndType

Type Tverlet
	
	Global VerletList:TList = New TList

	Field x#,y#
	Field dx#,dy#
	Field ox#,oy#
	Field ID
	Field VID
	Field active
	Field mass#
	Field collided
	
	Method Update()
	
		If active = True Then
			If collided = True Then
				fric# = .8
			Else
				fric# = 1
			EndIf
			collided = False
			dx# = (x#-ox#)*fric
			dy# = (y#-oy#)*fric
	
			ox# = x#
			oy# = y#
			
			x# = x# + dx#
			y# = y# + dy# + Gravity#
			
			If y# &gt; 480 Then
				collided = True
				y# = 480
				oy# = 480+(dy#/3)
			EndIf
			
			If x &lt; 0 Or x &gt; 640
				If x&lt;0   
					x = 0
					ox = 0 + (dx/3)
				Else
					x = 640
					ox = 640 - (dx/3)
				EndIf
			EndIf
			
		EndIf
	
	End Method
	
	
	Function CreateVerlet:TVerlet(x#,y#,ID,active = True)

		V:Tverlet = New Tverlet
		V.x   = x
		V.y   = y
		V.ID  = ID
		V.ox   = x
		V.oy   = y
		V.Active = active
		ListAddLast(VerletList, V)
		Return V
	
	End Function
	
	Function UpdateAll()
		For V:Tverlet = EachIn VerletList:TList
			V.Update()
		Next		
	EndFunction


End Type




Type TConstraint
	
	Global ConstraintList:TList = New TList
	
	Field V1:TVerlet
	Field V2:TVerlet
	Field length#
	
	Method Update()
		
		tx# = V1.x#-V2.x#
		ty# = V1.y#-V2.y#
		
		dist# = Sqr(tx#*tx#+ty#*ty#)
		
		tx# = tx# / 2
		ty# = ty# / 2
		
		If dist# &lt;&gt; 0 Then
			diff# = (dist# - Length#) / dist#
		EndIf
		
		V1.x# = V1.x# - diff# * tx#
		V1.y# = V1.y# - diff# * ty#

		V2.x# = V2.x# + diff# * tx#
		V2.y# = V2.y# + diff# * ty#
		
		SetColor 255,255,255
		DrawLine V1.x#,v1.y#,v2.x#,v2.y#
		
		
	End Method
	
	Function ConstrainVerlets:TConstraint( V1:TVerlet , V2:TVerlet )
	
		C:TConstraint = New TConstraint
		C.V1     = V1:TVerlet
		C.V2     = V2:TVerlet
		C.Length = Sqr((C.V1.x#-C.V2.x#)^2 + (C.V1.y#-C.V2.y#)^2)
		ListAddLast(ConstraintList, C)
		Return C
		
	End Function
	
	Function UpdateAll()
		For C:TConstraint = EachIn ConstraintList:TList
			C.Update()
		Next
	EndFunction
	
End Type


</pre> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
