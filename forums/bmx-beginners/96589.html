<!DOCTYPE html><html lang="en" ><head ><title >AI and pathfinding  ?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >AI and pathfinding  ?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >AI and pathfinding  ?</a><br><br>
<a name="1118322"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Twinprogrammer</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm making a game that needs some artificial intelligence . Any ideas on how to make an artificial intelligent enemy ? I already know how to create a simple chasing enemy ( if playerx &gt; enemyx move the enemy ).<br><br>Twinprogrammer <br><br></td></tr></table><br>
<a name="1118332"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >xlsior</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Greatly depends on the kind of game you're making.<br><br>If it's navigation AI you're looking for, do a search for A* / aStar. There are some blitzmax implementations for that, and it allows you to find efficient routs between two arbitrary points in your map, routing around obstacles. <br><br></td></tr></table><br>
<a name="1118476"></a>

<a name="1118477"></a>

<a name="1118479"></a>

<a name="1118480"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Twinprogrammer</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> How do I use the code in Blitz Max ? Or is it a reference ?<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1120145"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BLaBZ</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I wrote this awhile ago, it's a* path finding, in a grid, you set a start location, and end location, and where the obstacles are, and it finds its way through<br><br><pre class=code>

SuperStrict

Graphics 1680,1050


Global GridSquareSize:Int = 20
Global CurrentPath:TPath



'''''TEMP''''''
TPathNode.SetGridSize(85,52)
For Local i:Int = 0 To 50
	TPathNode.Grid[20,i] = True
Next
For Local o:Int = 1 To 20
	TPathNode.Grid[o,28] = True
Next
For Local p:Int = 5 To 27
	TPathNode.Grid[4,p] = True
Next
For Local r:Int = 4 To 18
	TPathNode.Grid[r,5] = True
Next


Global Time:Int
'''''TEMP''''''


Type TPath

	Field NodeList:TList = CreateList()

End Type

Type TPathNode

	Global OpenList:TList = CreateList()
	Global ClosedList:TList = CreateList()
	
	Global Grid:Int[40,30]
	
	Global ClosedArray:TPathNode[,]
	
	Global _startx:Int 
	Global _starty:Int
	
	Global _endx:Int
	Global _endy:Int
	
	Global MaxX:Int
	Global MaxY:Int 

	
	Field GridX:Int
	Field GridY:Int
	Field Parent:TPathNode
	Field FCost:Int ' F = G + H
	Field GCost:Int	' Movement cost from parent
	Field HCost:Int	' Distance between this square and destination
	
	Function GetLocalNodes(ParentNode:TPathNode)
	
		Local startx:Int = ParentNode.GridX
		Local starty:Int = ParentNode.GridY
		
			For Local agridx:Int = startx - 1 To startx + 1
				For Local agridy:Int = starty - 1 To starty + 1
					If Not (agridx = startx And agridy = starty)
						If agridx &gt;= 0
							If agridx &lt;= MaxX
								If agridy &gt;= 0
									If agridy &lt;= MaxY
										If ClosedArray[agridx,agridy] = Null 
											If Grid[agridx,agridy] = False
												
												Local newNode:TPathNode = New TPathNode
												newNode.GridX = agridx
												newNode.GridY = agridy
												newNode.Parent = ParentNode
												'Is it diagonal or orthagonal
												If ((_startx + _starty) = (agridx + agridy)) Or (startx + starty) - (agridx + agridy) = 2 Or (startx + starty) - (agridx + agridy) = -2
													newNode.GCost = 14
												Else
													newNode.GCost = 10
												EndIf
												newNode.HCost = (Abs(agridx - _endx) + Abs(agridy - _endy))* 10

												newNode.FCost = newNode.GCost + newNode.HCost
												OpenList.AddLast(newNode)
												ClosedArray[agridx,agridy] = newNode
												
													'Draw()
												
											EndIf 
										EndIf 
									EndIf 	
								EndIf		
							EndIf			
						EndIf	
					EndIf
				Next
			Next
			
	End Function
	
	Function GetPath:TPath(startx:Int,starty:Int,endx:Int,endy:Int)
		TPathNode._startx = startx
		TPathNode._starty = starty
		TPathNode._endx = endx
		TPathNode._endy = endy
		
		ClosedArray = New TPathNode[MaxX + 1,MaxY + 1]
	
		'1. Add start square to open list
		Local StartNode:TPathNode = New TPathNode
		StartNode.GridX = _startx
		StartNode.GridY = _starty

		
		Local FindingPath:Byte = True
		Local PathFound:Byte = True 
		While FindingPath
			'2. Add reachable squares to open list with previous square as parent
			'ClosedArray[StartNode.GridX,StartNode.GridY] = StartNode
	
		
			
			'3. Drop previous square from open list and add it to closedlist)
			GetLocalNodes(StartNode)
			
						
			'4. Find lowest F score
			Local NodewithLowestFCost:TPathNode
			For Local aNode:TPathNode = EachIn OpenList
				If NodewithLowestFCost = Null
					NodewithLowestFCost = aNode
				EndIf
				If aNode.FCost &lt; NodewithLowestFCost.FCost
					NodewithLowestFCost = aNode
				EndIf
			Next
			
			
			'5. Drop it from openlist and add it to closedlist
			If NodewithLowestFCost = Null'if no path was found
				PathFound = False
				Exit 
			EndIf
			OpenList.Remove(NodewithLowestFCost)
			ClosedArray[NodewithLowestFCost.GridX,NodewithLowestFCost.GridY] = NodewithLowestFCost

			
			'6. Check if we've reached the end square
			If NodewithLowestFCost.GridX = _endx And NodewithLowestFCost.GridY = _endy Then FindingPath = False
			
			StartNode = NodewithLowestFCost 
		
		Wend 
		
		OpenList.Clear()
		
		'7. Create Path
		If PathFound
			Local Path:TPath = New TPath
			While StartNode.Parent &lt;&gt; Null
				Path.NodeList.AddLast(StartNode)
				StartNode = StartNode.Parent
			Wend
			Return Path
		Else
			Return Null 
		EndIf
		
		
		'6. Check adjacent squares, ignoring those in closedlist and unwalkable, make selected square the parent
		'GetLocalNodes(NodewithLowestFCost)
		
		'7. If adjacent square is already on the openlist check to see if GCost is lower
		
		
	End Function 
	
	Function SetGridSize(x:Int,y:Int)
		Grid = New Int[x,y]
		MaxX = x - 1
		MaxY = y - 1
	End Function
	
EndType


While Not KeyHit(KEY_ESCAPE)

	If KeyHit(KEY_ENTER)
		Local start:Int = MilliSecs()
	'	For Local z:Int = 0 To 1
			CurrentPath = TPathNode.GetPath(5,15,80,1)
	'	Next
		Time = MilliSecs() - start
	EndIf 
	UserInput()
	Draw()

Wend


Function Draw()

	Cls

	For Local x:Int = 0 To 83
		For Local y:Int = 0 To 51
			'Draw occupied squares
			If TPathNode.Grid[x,y]
				SetColor(0,0,255)
				DrawRect((x * GridSquareSize), (y * GridSquareSize) ,GridSquareSize,GridSquareSize)
			EndIf
			
			'Draw line
			SetColor(255,255,255)
			DrawLine(x*GridSquareSize,y*GridSquareSize,x*GridSquareSize,(y+1)*GridSquareSize)
			DrawLine(x*GridSquareSize,y*GridSquareSize,(x+1)*GridSquareSize,y*GridSquareSize)
			
		Next
	Next

	'Draw Start	
	SetColor(0,255,0)
	DrawRect((TPathNode._startx * GridSquareSize), (TPathNode._starty * GridSquareSize) ,GridSquareSize,GridSquareSize)


	'Draw End
	SetColor(255,0,0)
	DrawRect((TPathNode._endx * GridSquareSize) , (TPathNode._endy * GridSquareSize),GridSquareSize,GridSquareSize)
	
	'Draw Path
	SetColor(150,150,150)
	If CurrentPath &lt;&gt; Null
		For Local aNode:TPathNode = EachIn CurrentPath.NodeList
			DrawRect(aNode.GridX *GridSquareSize ,aNode.GridY * GridSquareSize,GridSquareSize,GridSquareSize)
		Next
	EndIf 

	
	If CurrentPath = Null
	SetColor(0,0,150)
	For Local bNode:TPathNode = EachIn TPathNode.OpenList
		DrawRect(bNode.GridX * GridSquareSize,bNode.GridY * GridSquareSize,GridSquareSize,GridSquareSize)
	Next
	SetColor(150,0,0)
	For Local cNode:TPathNode = EachIn TPathNode.ClosedList
		DrawRect(cNode.GridX * GridSquareSize,cNode.GridY * GridSquareSize,GridSquareSize,GridSquareSize)
	Next
	EndIf 
	
	DrawText TPathNode.OpenList.Count(),0,0
	DrawText Time,0,12
	
	Flip 0

End Function


Function UserInput()
	If MouseHit(MOUSE_LEFT)
		If TPathNode.Grid[Floor(MouseX()/20),Floor(MouseY()/20)] = True
			TPathNode.Grid[Floor(MouseX()/20),Floor(MouseY()/20)] = False
		Else
			TPathNode.Grid[Floor(MouseX()/20),Floor(MouseY()/20)] = True
		EndIf 
	EndIf
End Function
</pre> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
