<!DOCTYPE html><html lang="en" ><head ><title >Viewport?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Viewport?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >Viewport?</a><br><br>
<a name="1000309"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hotshot2005</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have got two tileimages that is overlap each other...is there viewport that could hide other the tileimages as I am making parallax scrolling demo <br><br></td></tr></table><br>
<a name="1000316"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hotshot2005</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> let say I have 5 different layers.....but I am confuse when using setviewport<br>could anyone give me example on how setviewport work when come using 5 different layers? <br><br></td></tr></table><br>
<a name="1000348"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> hmm I dont exactly understand your question but viewports dont work on many systems unfortunately... <br><br></td></tr></table><br>
<a name="1000406"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jkrankie</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> No, viewports work on most systems, just a few really old ones cause problems.<br><br>Cheers<br>Charlie <br><br></td></tr></table><br>
<a name="1000415"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hotshot2005</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> so does that mean I have do my own viewport functions? <br><br></td></tr></table><br>
<a name="1000417"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_Skully</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Set your viewport for the top layer, draw your images, set your viewport for the next layer down, draw your images... its really that easy.<br><br><pre class=code>
SetViewport 0,0,Screenwidth,ViewPortheight
SetViewport 0, ViewPortheight,Screenwidth,ViewPortheight
SetViewport 0, ViewPortheight*2,Screenwidth,ViewPortheight
SetViewport 0, ViewPortheight*3,Screenwidth,ViewPortheight
</pre> <br><br></td></tr></table><br>
<a name="1000420"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hotshot2005</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> thank you for tips skully :) <br><br></td></tr></table><br>
<a name="1000445"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> The only reason you'd want to set a separate viewport for each layer is if they are to be restricting in drawing or scaled. Surely you can just set it once and draw the parallax layers from rear to near? <br><br></td></tr></table><br>
<a name="1000449"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_Skully</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think he's talking vertical paralax... so the distant one is nearer the top, the next closest one layer down...  think of platformer type games.<br><br>Overlapping parallax can look messed to.  I've played with that in the TileMax layering system.  You get weird artifacting from the movement of the layers when the layers are complex.  Think of two window screens moving over one another <br><br></td></tr></table><br>
<a name="1000461"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >xlsior</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Moire patterns? <br><br></td></tr></table><br>
<a name="1000513"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok but the viewport defines the scaling operation for translating world coordinates into window coordinates, I don't see that you really need to change it unless a) you want to confine drawing to within a given area, or b) change the size of pixels. If he has parallax layers that are all just regular image pixel, he doesn't need scaling, and I don't see why you'd have to use a viewport to restrict each layer ... surely you'd just use a strip of an image and draw it full? How is having a separate viewport going to help? <br><br></td></tr></table><br>
<a name="1000541"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_Skully</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> The ViewPort just restricts drawing operations to the defined rectangle, so if he draws an image that starts at 1 pixel above the defined bottom, only 1 pixel of it will be drawn... prevents overlap <br><br></td></tr></table><br>
<a name="1000622"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hotshot2005</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> it`s the shadow of beast demo i am doing :) <br><br></td></tr></table><br>
<a name="1000663"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Use the new sub-image drawing command which Mark just added to BlitzMax. That'll be faster than using lots of viewports. <br><br></td></tr></table><br>
<a name="1000930"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >asoed</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> what is the sub-image drawing command, can't find it. i have blitz 1.35 <br><br></td></tr></table><br>
<a name="1000931"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >asoed</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> sorry, never mind. I see 1.36 is up! <br><br></td></tr></table><br>
<a name="1001040"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hotshot2005</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> DrawSubImageRect is good one but I have one problem.....<br><br>the tileimage go on forever when come to scrolling and how can I make DrawSubImageRect to the same job as tileimage? <br><br></td></tr></table><br>
<a name="1001468"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hotshot2005</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> when my background image go left then there is nothings there where if i put tileimage with viewport...it go on forever  do you understand what am trying to say?<br><br><pre class=code>
Graphics 640,480,0

Global T_list:TList = New TList

Global Background1=LoadImage("media\bgd1_ciel.png")
Global Background2=LoadImage("media\bgd2_montagnes.PNG")
Global Background3=LoadImage("media\bgd3_sol1.png")
Global Background4=LoadImage("media\bgd4_sol2.png")
Global Background5=LoadImage("media\bgd5_sol3.png")
Global Background6=LoadImage("media\sprite_barriere.bmp")

Type TBackground
     Field x: Int , y:Int
     Field x1:Int, y1:Int
     Field x2:Int, y2:Int
     Field x3:Int, y3:Int
     Field x4:Int, y4:Int
     Field x5:Int, y5:Int

     Method Draw()
         
            DrawSubImageRect(Background1,x,0,640,480,0,0,640,480,0,0,0 ) 
           
            DrawSubImageRect(Background2,x2,150,640,480,0,0,640,480,0,0,0 ) 

            DrawSubImageRect(Background3,x3,370,640,480,0,0,640,480,0,0,0 ) 

            DrawSubImageRect(Background4,x4,381,640,480,0,0,640,420,0,0,0 ) 

            DrawSubImageRect(Background5,x5,405,640,480,0,0,640,420,0,0,0 ) 

            DrawSubImageRect(Background6,x6,395,640,480,0,0,640,420,0,0,0 ) 


            x=x-1
            x1=x1-2
            x3=x3-3
            x4=x4-4
            x5=x5-5
            x6=x6-6
     EndMethod

End Type

T:Tbackground=New Tbackground

Repeat
      Cls
      T.Draw
      Flip
Until MouseHit(1) 
</pre> <br><br></td></tr></table><br>
<a name="1001489"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> can you post a working demo? so I can see what it's doing wrong.<br>what are the size of your images?<br>I think your best bet is to do tile images on viewport but you need to take into account the tile size and move it no more than the amount of the image width before returning to the beginning of the image<br><pre class=code>
setviewport(0,y,graphicswidth(),image.Height)' 
tileimage image,y,x
x = (x-6) mod image.width
</pre><br><br>I think that is what you are trying to do. if you want you can send me the demo to my email addres and I'll try to figure it out for you. <br><br></td></tr></table><br>
<a name="1001502"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hotshot2005</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have send you email :) <br><br></td></tr></table><br>
<a name="1001539"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> here it is:<br><pre class=code>
SuperStrict
Graphics 640,480,0

Global T_list:TList = New TList

Global Background1:TImage=LoadImage("media\bgd1_ciel.png")
Global Background2:TImage=LoadImage("media\bgd2_montagnes.PNG")
Global Background3:TImage=LoadImage("media\bgd3_sol1.png")
Global Background4:TImage=LoadImage("media\bgd4_sol2.png")
Global Background5:TImage=LoadImage("media\bgd5_sol3.png")
Global Background6:TImage=LoadImage("media\sprite_barriere.bmp")

Type TBackground
     Field x: Int , y:Int
     Field x1:Int, y1:Int
     Field x2:Int, y2:Int
     Field x3:Int, y3:Int
     Field x4:Int, y4:Int
     Field x5:Int, y5:Int
	Field x6:Int, y6:Int

     Method Draw()
         	  SetViewport 0,0,GraphicsWidth(),background1.height
            TileImage Background1,x,0 
            SetViewport 0,150,GraphicsWidth(),background2.height
            TileImage Background2,x2,150
		  SetViewport 0,370,GraphicsWidth(),background3.height 
            TileImage Background3,x3,370 
		  SetViewport 0,380,GraphicsWidth(),background4.height
            TileImage Background4,x4,380 
		  SetViewport 0,400,GraphicsWidth(),background5.height
            TileImage Background5,x5,400 
		  SetViewport 0,390,GraphicsWidth(),background6.height
            TileImage Background6,x6,390 


            x =(x-1 ) Mod background1.width
            x2=(x2-2) Mod background2.width
            x3=(x3-3) Mod background3.width
            x4=(x4-4) Mod background4.width
            x5=(x5-5) Mod background5.width
            x6=(x6-6) Mod background6.width
		 SetViewport 0,0,GraphicsWidth(),GraphicsHeight()
     EndMethod

End Type

Local T:Tbackground=New Tbackground

Repeat
      Cls
      T.Draw
      Flip
Until MouseHit(1) 
</pre> <br><br></td></tr></table><br>
<a name="1001641"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hotshot2005</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> thank you very much and it is work now. I going to add the clouds and trees :) <br><br></td></tr></table><br>
<a name="1001660"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#23">[#23]</a></td></tr></table></td></tr><tr ><td class="posttext"> In my laptop with intel graphics, I noticed that it kind of pauses at times. It seems to me it's the SetViewPort function causing that. I don't know how much processing speed is absorbed by it or what it's making it stall but I suggest you make your own function to scroll the images. Besides making it smoother, It should be faster. It shouldn't be hard to do. <br><br></td></tr></table><br>
<a name="1001739"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hotshot2005</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#24">[#24]</a></td></tr></table></td></tr><tr ><td class="posttext"> need to work on tree and clouds<br><br><img src="http://www5.picturepush.com/photo/a/2644283/img/Anonymous/Shadow-of-the-beast.png"> <br><br></td></tr></table><br>
<a name="1160640"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >iaqsuk</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#25">[#25]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi All, I create a blitzplus version of this viewport or above codes or similar codes.  My question is, is it possible to add another tileimages after reaching the end of the original tileimages or a little before reaching the end of the original?<br><br>my code:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics 800,600,32,2

img_sky = LoadImage("sky.png")
img_clouds = LoadImage("clouds.png")
MaskImage img_clouds,255,255,255
img_mountains = LoadImage("mountains.png")
MaskImage img_mountains,255,255,255
img_hills = LoadImage("hills.png")
MaskImage img_hills,255,255,255
img_grass = LoadImage("grass.png")
MaskImage img_grass,255,255,255
img_trees = LoadImage("trees.png")
MaskImage img_trees,255,255,255

timer = CreateTimer(30)
SetBuffer BackBuffer()

While Not KeyHit(1)
     If KeyDown(203) Then
	x=(x+1) Mod 800
     EndIf 
     If KeyDown(205) Then
	x=(x-1) Mod 800
     EndIf 

     Cls
     ;draw sky
     DrawImage img_sky,0,0

     ;draw clouds
     TileImage img_clouds,x/5,0

     ;draw mountains
     TileImage img_mountains,x/2,0

     ;draw hills
     TileImage img_hills,x/1.1,0

     ;draw grass
     TileImage img_grass,x*1.5,0

     ;draw trees
     TileImage img_trees,x*3,0

WaitTimer timer
Flip
Wend
End
</textarea><br>Lets say all my tileimages are 800 width, just before reaching end of 800 can I add more tileimages to replace current tileimages?  Anyone,  Thanks. <br><br></td></tr></table><br>
<a name="1160644"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#26">[#26]</a></td></tr></table></td></tr><tr ><td class="posttext"> Create your own function that scrolls and attach another image to the end. It should be a good exercise for a beginner.<br><br>if  you try it and can't figure out how to solve it, I'll post some code for you but I think you should figure it out your self. <br><br></td></tr></table><br>
<a name="1160646"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >iaqsuk</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#27">[#27]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, I'll try solve it myself first and let you know by posting my code ok.  Thanks. <br><br></td></tr></table><br>
<a name="1161036"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >iaqsuk</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#28">[#28]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Jesse, been pretty busy lately, finally had a chance to test my code for new tileimages from post #25.<br><br>I just basically added if &amp; then statements if I am correct on the draw tileimages?  My new codes:<br><br>;draw grass<br>	If x &lt; 800 Then<br>	TileImage grass,x/2.5,0<br>	MaskImage grass,255,255,255<br>	ElseIf x &gt; 800 Then <br>		TileImage newgrass,x/2.5,0<br>		MaskImage newgrass,255,255,255<br>	EndIf <br>	<br>;draw trees<br>	If x &lt; 800 Then<br>	TileImage trees,x/2.1,0<br>	MaskImage trees,255,255,255<br>	ElseIf x &gt; 800 Then<br>		TileImage newtrees,x/2.5,0<br>		MaskImage newtrees,255,255,255<br>	EndIf <br><br>I still can't figure out why at the end of the 1st or original image it doesn't  change to new image?  It only changes if I go left or west on the x axis not right?  I wanted it to change at the end going right of the screen.  If you can help.  Thanks. <br><br></td></tr></table><br>
<a name="1161046"></a>

<a name="1161050"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#29">[#29]</a></td></tr></table></td></tr><tr ><td class="posttext"> I guess I didn't explain correctly. instead of using the built in TIleImage function make your own.<br>the built in one only tiles one image at a time so it's not flexible at all.<br><br>I did a quick scroller. all you need is three images to try.<br><br>I don't know how familiar you are with OOP. I hope you understand it.<br><br>use your own images.<br>[monkeycode]<br>Type TScroller<br>	Field x:Float<br>	Field y:Float<br>	<br>	Field speed:Float<br>	<br>	Field list:TList<br>	<br>	Field firstImage:TImage<br>	Field secondImage:TImage<br>	Field thirdImage:TImage<br>	<br>	Method New()<br>		list = New TList<br>	End Method<br>	<br>	Function Create:TScroller(x:Float,y:Float,image1:TImage,image2:TImage,image3:TImage,speed:Float)<br>		Local s:TScroller = New TScroller<br>		s.x = x<br>		s.y = y<br>		s.firstImage = image1<br>		s.secondImage = image2<br>		s.thirdImage = image3<br>		s.speed = speed<br>		Return s<br>	End Function<br>	<br>	Method Update()<br>		<br>		x :- speed<br>		If x+ImageWidth(firstImage) &lt; 0<br>			x :+ ImageWidth(firstImage)<br>			Local temp:TImage = firstImage<br>			firstImage = secondImage<br>			secondImage = thirdImage<br>			thirdImage = temp<br>		EndIf<br>		<br>	End Method<br>	<br>	Method Draw()<br>		Local tx:Float = x<br>		DrawImage firstImage,tx,y<br>		tx :+ ImageWidth(firstImage)<br>		DrawImage secondImage,tx,y<br>		tx :+ ImageWidth(secondImage)<br>		DrawImage thirdImage,tx,y<br>	End Method<br>	<br>End Type<br><br>Graphics 640,480<br><br>Local image1:TImage = LoadImage("mountains.png")<br>Local image2:TImage = LoadImage("sky.png")<br>Local image3:TImage = LoadImage("tree.png")<br>Local scroller:TScroller = TScroller.Create(0,100,image1,image2,image3,3.0)<br><br>Repeat<br>	Cls<br>	scroller.Update()<br>	scroller.Draw()<br>	Flip()<br>Until KeyDown(KEY_ESCAPE)<br>[/monkeycode]<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1161047"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Hotshot2005</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#30">[#30]</a></td></tr></table></td></tr><tr ><td class="posttext"> Neat Coding there Jesse :) <br><br></td></tr></table><br>
<a name="1161051"></a>

<a name="1161052"></a>

<a name="1161053"></a>

<a name="1161054"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#31">[#31]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks HotShot!<br><br>here is one using lists which can use as many images as you like minimum 2 images.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Type TScroller
	Field x:Float
	Field y:Float
	
	Field speed:Float
	
	Field list:TList
	
	Field firstImage:TImage
	Field secondImage:TImage
	Field thirdImage:TImage
	
	Method New()
		list = New TList
	End Method
	
	Function Create:TScroller(x:Float,y:Float,speed:Float)
		Local s:TScroller = New TScroller
		s.x = x
		s.y = y
		s.speed = speed
		Return s
	End Function
	
	Method AddImage(image:TImage)
		list.AddLast(image)
	End Method
	
	Method Update()
		
		x :- speed
		Local image:TImage = TImage(list.First())
		If x+ImageWidth(image) &lt; 0
			x :+ ImageWidth(image)
			image = TImage(list.RemoveFirst()) 
			list.AddLast(image)
		EndIf
		
	End Method
	
	Method Draw()
		Local tx:Float = x
		For Local image:TImage = EachIn list
			DrawImage image,tx,y
			tx :+ ImageWidth(image)
		Next
	End Method
	
End Type

Graphics 640,480

Local scroller:TScroller = TScroller.Create(0,100,3.0)
scroller.AddImage(LoadImage("mountains.png"))
scroller.AddImage(LoadImage("sky.png"))
Scroller.AddImage(LoadImage("tree.png"))


Repeat
	Cls
	scroller.Update()
	scroller.Draw()
	Flip()
Until KeyDown(KEY_ESCAPE)</textarea><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1161141"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >iaqsuk</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#32">[#32]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Jesse, thank you for your reply.  Yes I do understand your OOP Method coding, however, I was thinking more like this and I hope you understand what I am doing:<br><br>for example my key movements will be a plane or helicopter for tileimages left or right.  I would like to set my default location of helicopter in the middle of image size height and width.  If I choose to fly right and my original tileimage is 800 width and once I reach the end of the width tileimage I load another new tileimage.  For example, sky, mountains, hills tileimages will stay the same, but my original grass tileimages is drawn then newgrass is drawn after reaching end of 800 width and new image is desert instead of grass. <br><br>If helicopter goes left and reaches end of 800 width, original grass tileimages is drawn a new tileimages and instead of newgrass it is a water ground.  I was wondering if the post #28 was correct creating new tileimages in that coding? <br><br></td></tr></table><br>
<a name="1161145"></a>

<a name="1161146"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#33">[#33]</a></td></tr></table></td></tr><tr ><td class="posttext"> I saw your code but it really doesn't let me conclude much from it as I don't see what you are doing with the x. And how do you manage to scroll?<br><br>What I can conclude(guess) is that it is going to tile an image across the screen and when x becomes greater than 800 a different image is going to be tiled across the screen. it is going to be a sudden switch instead of a movement transition. In order to get a tile transition you are going to have to create your own function to do that. <br>Play with the TileImage function a bit with different numbers negative large numbers and positive large numbers and you will see that it will still draw images starting from the start of the screen or the view window. if your tileImage is at 1000,1000 it will still be drawing from the start of the screen to the end of the screen. so if you start tiling the second image. The second image won't be drawn from say half of the screen. It will just tile the whole screen and the first image won't be displayed at all. and that is why I posted the code above so you can see how tile sequence is drawn.<br><br>If you think that I still don't understand you than you are going to have to post a bit more  of your code.<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1161148"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >iaqsuk</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#34">[#34]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Jesse, thanks for reply.  Reminder below code is in plus.  Also, all my tileimages are:  7200 width, 600 height.<br><br>Also, the first codebox is what I want to use for my player movements cause it is more realistic as far as using frames and direction movements.<br><br>The 2nd codebox really works for the tileimages but helicopter is only 1 drawimages and movements goes up, down, right, left without frames.  However, the tileimages is better.<br><br>I would like to end up with 1st codebox player movements with 2nd codebox tileimages.  I've been trying and trying to play with the codes of the 1st codebox to keep the helicopter in the middle of the screen while images are being tiled but can't figure it out?<br><br>;;;;;;;;;;;;;;1st code box;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Parallax Strolling
Const screenx = 800
Const screeny = 600

Const North = 72
Const South = 80
Const West = 75
Const East = 77
Const NorthWest = 71
Const SouthWest = 79
Const NorthEast = 73
Const SouthEast = 81

Graphics screenx,screeny,16,1
SetBuffer BackBuffer()

Global Player_North = LoadAnimImage("gfx/helin.png",145,66,0,4)
Global Player_East	= LoadAnimImage("gfx/helie.png",145,66,0,4)
Global Player_South = LoadAnimImage("gfx/helis.png",145,66,0,4)
Global Player_West	= LoadAnimImage("gfx/heliw.png",145,66,0,4)
Global Player_NorthEast = LoadAnimImage("gfx/heline.png",145,66,0,4)
Global Player_NorthWest = LoadAnimImage("gfx/helinw.png",145,66,0,4)
Global Player_SouthEast = LoadAnimImage("gfx/helise.png",145,66,0,4)
Global Player_SouthWest = LoadAnimImage("gfx/helisw.png",145,66,0,4)
Global Player_Image = Player_South

;original tileimages
Global sky = LoadImage("gfx/sky.png")
Global clouds = LoadImage("gfx/clouds.png")
Global jet = LoadImage("gfx/jet.png")
Global mountains = LoadImage("gfx/mountains.png")
Global hills = LoadImage("gfx/hills.png")
Global grass = LoadImage("gfx/grass.png")
Global trees = LoadImage("gfx/trees.png")
Global ground = LoadImage("gfx/ground.png")

;new tileimages
Global newgrass = LoadImage("gfx/newgrass.png")
Global newtrees = LoadImage("gfx/newtrees.png")

; be sure to make all variable FLOAT#s:
Global PlayerX# = screenx/2
Global PlayerY# = screeny/2
Global RisingAdd# = 0
Global Decrease# = 0
Global Break# = 0
Global SpeedFactor# = 1
Global PlayerMaxSpeed# = 2
Global UpDown# = 0
Global RightLeft# = 0
Global Player_Moving# = True
Global Player_Frame# = 0
Global Player_MaxFrames# = 4
Global Player_FrameTimer# = 0
Global Player_FrameDelay# = 120
Global Player_Size# = 32
Global PreDirection# = 0
Global StabilTime# = MilliSecs() + 50
Global StabilDirection# = PreDirection
Global RisingStepTimer# = 0
Global PlayerXPos# = screenx/2
Global Direction# = 0


Global FPS = CreateTimer(200)
While Not KeyDown(1)
	Cls
	Fdrawscrollimages()
	DrawPlayer()
	UpdatePlayer()
	PlayerConditions()
	Flip 0
	WaitTimer FPS
Wend
End 

Function DrawPlayer()
	DrawImage Player_Image,PlayerX,PlayerY,Player_Frame
	MaskImage Player_Image,255,255,255
End Function 

Function UpdatePlayer()
; check the keys first
	Direction=0
	If KeyDown(North) Then
		Direction = 1
		Player_Moving = True
		Player_Image = Player_North
	ElseIf KeyDown(South) Then
		Direction = 2 
		Player_Moving = True
		Player_Image = Player_South
	ElseIf KeyDown(West) Then
		Direction = + 4
		Player_Moving = True
		Player_Image = Player_West
	ElseIf KeyDown(East) Then 
		Direction = + 8
		Player_Moving = True
		Player_Image = Player_East
	ElseIf KeyDown(NorthWest) Then 
		Direction = 1 + 4
		Player_Moving = True
		Player_Image = Player_NorthWest
	ElseIf KeyDown(SouthWest) Then 
		Direction = 2 + 4
		Player_Moving = True
		Player_Image = Player_SouthWest
	ElseIf KeyDown(NorthEast) Then 
		Direction = 1 + 8
		Player_Moving = True
		Player_Image = Player_NorthEast
	ElseIf KeyDown(SouthEast) Then 
		Direction = 2 + 8
		Player_Moving = True
		Player_Image = Player_SouthEast
	ElseIf	Player_Frame = 0
		Player_Moving = False
	ElseIf KeyDown(76) Then  
		Direction = Direction
	EndIf 
	; then check whether the key press situation is stabil during the last 50msec:
	If PreDirection &lt;&gt; Direction
		StabilTime = MilliSecs() + 50
		PreDirection = Direction
	EndIf
	; when stabil then change the real player direction:
	If PreDirection &lt;&gt; StabilDirection
		If StabilTime &lt; MilliSecs()
			StabilDirection = PreDirection
				
			Select StabilDirection
				Case 1 ; N
					UpDown = -1
					RightLeft = 0
				Case 2 ; S
					UpDown = 1
					RightLeft = 0
				Case 4 ; W
					UpDown = 0
					RightLeft = -1
				Case 8 ; E
					UpDown = 0
					RightLeft = 1
				Case 5 ; NW (1 + 4)
					UpDown = -0.7
					RightLeft = -0.7
				Case 6 ; SW (2 + 4)
					UpDown = 0.7
					RightLeft = -0.7
				Case 9 ; NE (1 + 8)
					UpDown = -0.7
					RightLeft = 0.7
				Case 10 ;SE (2 + 8)
					UpDown = 0.7
					RightLeft = 0.7
			End Select
		EndIf
	EndIf
	; now decide to speed up or slow down:	
	If Break = True
			Decrease = 110.90
	 ElseIf StabilDirection &gt; 0
           	RisingAdd = 1.05
     Else
          	RisingAdd = 0.95
     EndIf
	; adjust Speedfactor:
     If RisingStepTimer &lt; MilliSecs()
          RisingStepTimer = MilliSecs() + 100
          SpeedFactor = SpeedFactor * RisingAdd
          If SpeedFactor &gt; 2 Then SpeedFactor = 2
          If SpeedFactor &lt; 1 Then SpeedFactor = 1
     EndIf
	; Simple timer will increase frame count After Player1_FrameDelay (MilliSecs)
	; Will loop frame back To 0 If frame goes over the max frame count.
	If MilliSecs() &gt; Player_FrameTimer And Player_Moving = True
		Player_FrameTimer = MilliSecs() + Player_FrameDelay
		Player_Frame = Player_Frame + 1
		If Player_Frame =&gt; Player_MaxFrames Then Player_Frame = 0
	EndIf
End Function 

Function PlayerConditions()
	; left or right
      PlayerX = PlayerX + PlayerMaxSpeed * (SpeedFactor-1) * RightLeft
	If PlayerX &gt; 14400 Then PlayerX = 0
 	If PlayerX &lt; 0 Then PlayerX = 14400
	
	; up or down
      PlayerY = PlayerY + PlayerMaxSpeed * (SpeedFactor-1) * UpDown
 	If PlayerY &gt; 600 Then PlayerY = 0
	If PlayerY &lt; 0 Then PlayerY = 600
End Function 

Function Fdrawscrollimages()
;draw sky
	DrawImage sky,0,0
	MaskImage sky,255,255,255
	
;draw clouds
	TileImage clouds,PlayerX/6,0
	MaskImage clouds,255,255,255
	
;draw clouds
	TileImage jet,PlayerX/5,0
	MaskImage jet,255,255,255
	
;draw mountains
	TileImage mountains,PlayerX/3,0
	MaskImage mountains,255,255,255
	
;draw hills
	TileImage hills,PlayerX/2.1,0
	MaskImage hills,255,255,255
	
;;;;;;;;;;;;;;;;;;;UPDATED CODES;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;draw grass
	If PlayerX &lt; 7100 Then
		TileImage grass,PlayerX/2.5,0
		MaskImage grass,255,255,255
	EndIf 
	If PlayerX &gt; 7100 Then
		TileImage newgrass,PlayerX/2.5,0
		MaskImage newgrass,255,255,255
	EndIf 
	
;draw trees
	If PlayerX &lt; 7100 Then
		TileImage trees,PlayerX/2.1,0
		MaskImage trees,255,255,255
	EndIf
	If PlayerX &gt; 7100 Then 
		TileImage newtrees,PlayerX/2,1,0
		MaskImage newtrees,255,255,255
	EndIf 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;END UPDATED CODES FOR NEW TILEIMAGES;;;;;;;;;;;;;;;;
;draw ground
	TileImage ground,PlayerX/3,0
	MaskImage ground,255,255,255
End Function
</textarea><br>;;;;;;;;;;;;;;;;end of 1st codebox;;;;;;;;;;;;;;;;;;;;;;;;<br><br>;;;;;;;;;;;;2nd codebox;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Parallax Strolling

;constant keys
Const west=75
Const east=77
Const north=72
Const south=80
Const northwest = 71
Const southwest = 79
Const northeast = 73
Const southeast = 81

;constant graphics
Const screenx = 800
Const screeny = 600

;graphics settings
Graphics screenx,screeny,16,1
SetBuffer BackBuffer()

;helicopter load images
Global hnorth = LoadAnimImage("gfx/heli/north.png",145,66,0,4)
Global hsouth = LoadAnimImage("gfx/heli/south.png",145,66,0,4)
Global heast = LoadAnimImage("gfx/heli/east.png",145,66,0,4)
Global hwest = LoadAnimImage("gfx/heli/west.png",145,66,0,4)
Global hnortheast = LoadAnimImage("gfx/heli/northeast.png",145,66,0,4)
Global hnorthwest = LoadAnimImage("gfx/heli/northwest.png",145,66,0,4)
Global hsoutheast = LoadAnimImage("gfx/heli/southeast.png",145,66,0,4)
Global hsouthwest = LoadAnimImage("gfx/heli/southwest.png",145,66,0,4)


;;;;;;;;;;CHARACTER;;;;;;;;;;
;global character width from tilesets
Global width = 145
;global character length from tilesets
Global height = 66
;global character x screen and character width divided by 2
Global x# = screenx/2-width/2
;global character y screen and character length divided by 2
Global y# = screeny/2-height/2
;global character speed hash default value
Global xspeed# = 0
Global yspeed# = 0
Global maxspeed# = 4
Global xpos#= 400
Global ypos# = 450
Global frame# = 0
Global dir# = 0

Global sky = LoadImage("gfx/bg/sky.png")
Global clouds = LoadImage("gfx/bg/clouds.png")
Global jet = LoadImage("gfx/bg/jet.png")
Global mountains = LoadImage("gfx/bg/mountains.png")
Global hills = LoadImage("gfx/bg/hills.png")
Global grass = LoadImage("gfx/bg/grass.png")
Global trees = LoadImage("gfx/bg/trees.png")
Global ground = LoadImage("gfx/bg/ground.png")
Global newgrass = LoadImage("gfx/bg/newgrass.png")
Global newtrees = LoadImage("gfx/bg/newtrees.png")

timer = CreateTimer(30)

SetBuffer BackBuffer()

While Not KeyHit(1)


Cls
Fdrawscrollimages()
Fdrawheli()
Fflyheli()
Ffcondition()
WaitTimer timer
Flip
Wend
End


Function Fdrawheli()
	DrawImage(hnorth,xpos,ypos)
	MaskImage hnorth,255,255,255
End Function

Function Fdrawscrollimages()
;draw sky
	DrawImage sky,0,0
	MaskImage sky,255,255,255
	
;draw clouds
	TileImage clouds,x/6,0
	MaskImage clouds,255,255,255
	
;draw clouds
	TileImage jet,x/5,0
	MaskImage jet,255,255,255
	
;draw mountains
	TileImage mountains,x/3,0
	MaskImage mountains,255,255,255
	
;draw hills
	TileImage hills,x/2.1,0
	MaskImage hills,255,255,255
	
;draw grass
	If x &lt; 800 Then
	TileImage grass,x/2.5,0
	MaskImage grass,255,255,255
	ElseIf x &gt; 800 Then 
		TileImage newgrass,x/2.5,0
		MaskImage newgrass,255,255,255
	EndIf 
	
;draw trees
	If x &lt; 800 Then
	TileImage trees,x/2.1,0
	MaskImage trees,255,255,255
	ElseIf x &gt; 800 Then
		TileImage newtrees,x/2.5,0
		MaskImage newtrees,255,255,255
	EndIf 
	
;draw ground
	TileImage ground,x/3,0
	MaskImage ground,255,255,255
End Function


Function Fflyheli()
	If KeyDown(west) Then xspeed#=xspeed#+.5 ;this adds acceleration in the correct direction
	If KeyDown(east) Then xspeed#=xspeed#-.5
	If KeyDown(north) Then yspeed#=yspeed#-3.9
	If KeyDown(south) Then yspeed# = yspeed#+3.9
		x#=x#+xspeed# ;this adds the acceleration to the current x,y position
		ypos=y#+yspeed#
		xspeed#=xspeed#*.99 ;this makes the xspeed 99% of what is was ie adds friction and slows you down
		yspeed#=yspeed#+.01 ; here we are adding gravity acceleration to the y direction so you're always being pulled down
		If Not KeyDown(north) Or KeyDown(south) Or KeyDown(west) Or KeyDown(east) Then
		Select dir
			Case 1
				frame = 0
			Case 2
				frame = 4
			Case 3
				frame = 8
			Case 4
				frame = 12
		End Select 
	EndIf 
End Function 

Function Ffcondition()
	If ypos&gt;460 Then ypos=460:yspeed#=-yspeed#/2 ; this is hitting the bottom of the screen and bouncing you up at half the speed you hit it
	If ypos&lt;0 Then ypos=3:yspeed#=0 ; stopping you at the top of the screen
End Function 
</textarea><br>;;;;;;;;;;;end of 2nd codebox;;;;;;;;;;;;;;;;;;;;;;;<br><br>As you can tell, I am trying to make the movements like the 1st codebox movements on the 2nd codebox.  Hope you can also see the new tileimages I am trying to do after reaching the end of original tileimages.  I do know the changes on the new images will be abrupt changes but I eventually will change both original image to match the changes to the 2nd image if I get it to work.<br><br>hope this makes sense or you can help me out?  Thanks. <br><br></td></tr></table><br>
<a name="1161248"></a>

<a name="1161250"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#35">[#35]</a></td></tr></table></td></tr><tr ><td class="posttext"> for this game I created my own scroller function:<br><br><a href="http://www.hexobot.com/html5/MonkeyGame.html" target="_blank">http://www.hexobot.com/html5/MonkeyGame.html</a><br><br>what I did is leave the player in the same x position if the player moved forward then I would pas a variable to the scroller that tells the scroller how much to move left<br>same thing if the player moved left: I would tell the scroller how much to move right.<br>if the player jumps I would just set the player movement to jump up or down while leaving the character x position in the same place.<br><br>I would really like to try your code to see what you are doing wrong but I don't want to spend a bunch of time I don't have to go trough your code and try to process it all. Also  1 all I have installed in my MacBook is blitzMax and Monkey and 2 BlitzPlus doesn't work on my Macs.<br><br>If you want I can send you the monkey source code for the above game to see if that helps you.<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1161256"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >iaqsuk</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#36">[#36]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Jesse,<br><br>That would be cool if you can send the source code either here or monkey forum, cause I just purchased monkey too.  thanks. <br><br>I completely understand that you cant take time to text my codes, but the players movements was when I was getting help from midimaster here on another post:  <a href="http://blitzbasic.com/Community/posts.php?topic=94869" target="_blank">http://blitzbasic.com/Community/posts.php?topic=94869</a> <br><br>I saw you posted in this forum too.  This is where I got help for the codebox 1 more realistic movements.  I'll try and figure out the scrolling issue and player movements myself.  thanks again. <br><br></td></tr></table><br>
<a name="1161261"></a>

<a name="1161262"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#37">[#37]</a></td></tr></table></td></tr><tr ><td class="posttext"> sent to your yahoo Email.<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1161297"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >iaqsuk</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#38">[#38]</a></td></tr></table></td></tr><tr ><td class="posttext"> wow Jesse, thank you for sending the code in monkey.  Great stuff, i will certainly study the coding and try to understand what is going on.  <br><br>The only reason why I purchased monkey is the many platforms it has and it is so similar to max.  Thanks again.  I will continue to look at my above coding when I have time.  later. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
