<!DOCTYPE html><html lang="en" ><head ><title >Arena Shooter! "Arena BMX"</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Arena Shooter! "Arena BMX"</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >Arena Shooter! "Arena BMX"</a><br><br>
<a name="988335"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey!<br><br>I've been working on my next 24 hour project, "Arena BMX" which is an arena style shooter with different weapon types.  You control your guy by using the WASD keys, R to reload, number-keys to change weapons, and SPACE to create random powerups  on the playfield.<br><br>I'd like to share with you what I'm working with roughly 4 hours into development because the code is getting big and I want to make sure I'm coding correctly.  <br><br>This game is still in the works so there are no enemies as of yet. Feel free to take a look and play around with it, and let me know what you think!<br><br>You don't have to download any content, just copy/paste because all the graphics were made with code.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'Arena BMX by Matt McFarland
'Iteration 1: 5:30 to 6:30   
'Iteration 2: 6:45 to 8:00   
'Iteration 3: 8:15 to 9:30   
'Iteration 4: 9:45 to 11:00  

SuperStrict
AppTitle = "Arena Shoot BMX! by Matthew A McFarland, 2009"
REM ************ WEAPON SETUP
I wanted to actually create a list or type to hold all of the weapons
as I felt it would be easier to modify the game and add/subtract an array of weapon types
but I couldn't conceptualize it very well, and so I did it this way so I could code right away
without problem solving.  
End REM
Global HasPistol:Byte = True
Global MaxPistolChamber:Int = 15
Global PistolChamber:Int = 15

Global HasShotGun:Byte = False
Global ShotGunAmmo:Int = 0
Global MaxShotGunChamber:Int = 0
Global ShotGunChamber:Int = 0
Global MaxShotGunAmmo:Int = 160

Global HasMachineGun:Byte = False
Global MachineGunAmmo:Int = 0
Global MaxMachineGunChamber:Int = 0
Global MachineGunChamber:Int = 0
Global MaxMachineGunAmmo:Int = 900

'setup the constants
Const PISTOL:Int = 0
Const SHOTGUN:Int = 1
Const MACHINEGUN:Int = 2

Const PICKUP_SH_AMMO:Int = 0
Const PICKUP_MG_AMMO:Int = 1
Const PICKUP_SHOTGUN:Int = 2
Const PICKUP_MACHINEGUN:Int = 3

Const STATE_TITLE:Int = 0
Const STATE_PLAY:Int = 1
Const STATE_PAUSE:Int = 2
Const STATE_FAIL:Int = 3

Global iShotGunAmmo:TImage
Global iMachineGunAmmo:TImage
Global iShotGun:TImage
Global iMachineGun:TImage
Function Make_Images()
	SetClsColor 255, 255, 255
	AutoMidHandle(True)
	SetBlend ALPHABLEND
	'Make the ShotGun Ammo Box and save it to memory
	Cls
	SetColor 0, 0, 0
	DrawRect 0, 0, 26, 14
	SetColor 254, 254, 254
	SetScale 1, .9
	DrawText "=s=", 0, 0
	setscale 1,1
	iShotGunAmmo:TImage = CreateImage(27, 15, 1, DYNAMICIMAGE | MASKEDIMAGE)
	GrabImage iShotGunAmmo, 0, 0
	'Make the MachineGun Ammo Box and save it to memory
	Cls
	SetColor 0, 0, 0
	DrawRect 0, 0, 30, 14
	SetColor 254, 254, 254
	SetScale.9, .9
	DrawText "=mg=", 0, 0
	setscale 1,1
	iMachineGunAmmo:TImage = CreateImage(31, 15, 1, DYNAMICIMAGE | MASKEDIMAGE)
	GrabImage iMachineGunAmmo, 0, 0
	SetClsColor 0, 0, 0

	'Make the ShotGun and save it to memory
	Cls
	SetColor 1, 1, 1
	DrawRect 0, 0, 4, 10
	DrawRect 4, 0, 12, 4
	SetColor 255, 255, 255
	iShotGun:TImage = CreateImage(16, 13, 1, DYNAMICIMAGE | MASKEDIMAGE)
	GrabImage iShotGun, 0, 0

	'Make the MachineGun and save it to memory
	Cls
	SetColor 1, 1, 1
	DrawRect 0, 0, 14, 16
	DrawRect 8, 0, 34, 12
	SetColor 245, 245, 245
	DrawLine 16, 4, 40, 4
	iMachineGun:TImage = CreateImage(47, 17, 1, DYNAMICIMAGE | MASKEDIMAGE)
	GrabImage iMachineGun, 0, 0
	SetClsColor 255, 255, 255

End Function


'All objects extend from tGameObject
Type tGameObject
	Field x:Float, y:Float, xv:Float, yv:Float, hp:Float
	Method Update() Abstract
End Type

'SET UP THE PLAYER
Type tPlayer Extends tGameObject
	Global List:TList = New TList
	Field ShotDelay:Float
	Field DeadDelay:Float
	Field DelayModifier:Float
	Field Weapon:Int
	'When the player spawns it has a few configurations to it
	Function Spawn(x:Float, y:Float)
		Local plyr:tPlayer = New tPlayer
		plyr.x = x
		plyr.y = y
		plyr.weapon = PISTOL
		plyr.DelayModifier = 1
		tPlayer.List.AddLast(plyr)
	End Function
	'Update the player depending on death/non death
	Method Update()
		If DeadDelay &gt;= 0
			Control()
			Draw()
		End If
	End Method
	'I'm seperating control and draw functions so that I may just simply draw the screen if the game is paused.
	'Control the player
	Method Control()
		If ShotDelay &gt; 0 ShotDelay:-DelayModifier
		
		If KeyDown(KEY_W) And y &gt; 2 y:-2
		If KeyDown(KEY_A) And x &gt; 2 x:-2
		If KeyDown(KEY_S) And y &lt; 390 y:+2
		If KeyDown(KEY_D) And x &lt; 638 x:+2
		If KeyHit(KEY_1) weapon = PISTOL ;shotdelay = 120
		If KeyHit(KEY_2) And HasShotGun = True weapon = SHOTGUN;shotdelay = 120
		If KeyHit(KEY_3) And HasMachineGun = True weapon = MACHINEGUN;shotdelay = 120
		If KeyHit(KEY_R) Then Reload()
		If MouseDown(1) And ShotDelay &lt;= 0 Then Shoot()

	End Method
	'Draw the player
	Method Draw()
		'Highlight Weapon Used
		SetAlpha ALPHABLEND
		SetColor 255, 255, 0
		If weapon = PISTOL DrawRect 0, 400, 100, 480
		If weapon = SHOTGUN DrawRect 100, 400, 100, 480
		If weapon = MACHINEGUN DrawRect 200, 400, 100, 480
		SetColor 255, 255, 255
		'Draw Player
		SetColor 0, 0, 0
		DrawRect x - 4, y - 4, 8, 8
		SetAlpha 1
		Local d:Float = ATan2(y - MouseY(), x - MouseX()) + 180
		SetRotation d
		SetColor 0, 0, 0
		DrawLine x, y, x + 10, y
		SetColor 255, 255, 255
		SetRotation 0
		'Draw ShotDelay
		If shotdelay &gt; 0
			Local ShotBar:Float = shotdelay / 16
			For Local n:Float = 1 To shotbar Step 1
			SetColor 255, 0, 0
			DrawLine x - 4, y + 5, x - 4 + shotbar, y + 5
			Next			
		EndIf
		SetColor 100, 100, 055
		DrawLine(MouseX() - 6, MouseY() , MouseX() + 6, MouseY())
		DrawLine(MouseX(), MouseY() - 6, MouseX(), MouseY() + 6)
		SetColor 255, 255, 255
				
		SetColor 255, 255, 255
	End Method
	Method Shoot()
		SetBlend ALPHABLEND
		SetColor 255, 255, 0
		DrawOval x, y, 2, 2
		Local blasts:Int
		
		If weapon = PISTOL
			If PistolChamber &gt; 0
				ShotDelay = 20
				blasts = 1
				PistolChamber:-1
			Else
				blasts = 0
				ShotDelay = 120
				PistolChamber = MaxPistolChamber
			End If
		End If
		
		If weapon = SHOTGUN
			If ShotGunChamber &gt; 0
				ShotDelay = 40
				blasts = 6
				ShotGunChamber:-1
			End If
			If ShotGunChamber = 0
				blasts = 0
				ShotDelay = 120
				If ShotGunAmmo &lt;= MaxShotGunChamber
					ShotGunChamber = ShotGunAmmo
					ShotGunAmmo = 0
				End If
				If ShotGunAmmo &gt;= MaxShotGunChamber
					ShotGunAmmo:-MaxShotGunChamber
					ShotGunChamber:+MaxShotGunChamber
				End If
			End If
		End If
		
	 	If weapon = MACHINEGUN
			If MachineGunChamber &gt; 0
				ShotDelay = 4
				blasts = 1
				MachineGunChamber:-1
			End If
			If MachineGunChamber = 0
				blasts = 0
				ShotDelay = 120
				If MachineGunAmmo &lt;= MaxMachineGunChamber
					MachineGunChamber = MachineGunAmmo
					MachineGunAmmo = 0
				End If
				If MachineGunAmmo &gt;= MaxMachineGunChamber
					MachineGunAmmo:-MaxMachineGunChamber
					MachineGunChamber:+MaxMachineGunChamber
				End If
			End If
		End If
		For Local MakeCount:Int = 1 To blasts
			tPlayerShot.Make(x, y, weapon)
		Next
	End Method
	Method Reload()
		Local Difference:Int
		Select weapon
			Case PISTOL
				If PistolChamber &lt; MaxPistolChamber
					PistolChamber = MaxPistolChamber
					shotdelay = 100
				End If
			Case SHOTGUN
				If ShotGunChamber &lt; MaxShotGunChamber And ShotGunAmmo &gt; 0					
					Difference:Int = MaxShotGunChamber - ShotGunChamber
					If ShotGunAmmo &gt;= Difference Then ShotGunAmmo:-Difference ; ShotGunChamber:+Difference
					If ShotGunAmmo &lt; Difference Then ShotGunChamber:+ShotGunAmmo ;ShotGunAmmo = 0					
					shotdelay = 100
				End If
			Case MACHINEGUN
				If MachineGunChamber &lt; MaxMachineGunChamber And MachineGunAmmo &gt; 0
					Difference:Int = MaxMachineGunChamber - MachineGunChamber
					If MachineGunAmmo &gt;= Difference Then MachineGunAmmo:-Difference ; MachineGunChamber:+Difference
					If MachineGunAmmo &lt; Difference Then MachineGunChamber:+MachineGunAmmo ;MachineGunAmmo = 0
					shotdelay = 100
				End If

		End Select
	End Method
End Type

Type tPlayerShot Extends tGameObject
	Global List:TList = New TList
	Field speed:Float
	Field weapon:Int
	Function Make(x:Float, y:Float, weapon:Int)
		Local newshot:tPlayerShot = New tPlayerShot
		Local d:Float
		newshot.weapon = weapon
		If weapon = PISTOL
			newshot.x = x
			newshot.y = y
			newshot.speed = 9
			d = ATan2(y - MouseY(), x - MouseX()) + 180
			newshot.xv = Cos(d) * newshot.speed
			newshot.yv = Sin(d) * newshot.speed
			tPlayerShot.List.AddLast(newshot)
		End If
		If weapon = MACHINEGUN
			newshot.x = x
			newshot.y = y
			newshot.speed = 10
			d = ATan2(y - MouseY(), x - MouseX()) + Rand(178, 182)
			newshot.xv = Cos(d) * newshot.speed
			newshot.yv = Sin(d) * newshot.speed		
		End If
		If weapon = SHOTGUN
			newshot.x = x
			newshot.y = y
			newshot.speed = Rand(9, 12)
			d = ATan2(y - MouseY(), x - MouseX()) + Rand(170, 190)
			newshot.xv = Cos(d) * newshot.speed
			newshot.yv = Sin(d) * newshot.speed		
			
		End If
		tPlayerShot.List.AddLast(newshot)

	End Function
	Method Update()
		Control()
		Collide()
		Draw()
	End Method
	Method Control()
		If OutofBounds(x, y) = True list.Remove(Self)
		x:+xv
		y:+yv
	End Method
	Method Draw()
		SetBlend ALPHABLEND
		SetColor 0, 0, 0
		DrawRect(x - 1, y - 1, 2, 2)
	End Method
	Method Collide()
		'Check Collisisons
	End Method
End Type

Type tPickup Extends tGameObject
	Global List:TList = New TList
	Field pickuptype:Int
	Field duration:Int
	Field Swell:Float
	Field Swell_step:Float
	Field Image:TImage
	Function Make(x:Float, y:Float, pickuptype:Int)
		Local newpk:tPickup = New tPickup
		newpk.x = x
		newpk.y = y
		newpk.pickuptype = pickuptype
		If pickuptype = PICKUP_SH_AMMO Then newpk.Image = iShotGunAmmo
		If pickuptype = PICKUP_MG_AMMO Then newpk.Image = iMachineGunAmmo
		If pickuptype = PICKUP_SHOTGUN Then newpk.Image = iShotGun
		If pickuptype = PICKUP_MACHINEGUN Then newpk.Image = iMachineGun			
		newpk.Swell = 1
		newpk.Swell_step =.1
		tPickup.List.AddLast(newpk)
		
	End Function
	Method Update()
		duration:+1
		If duration &gt; 500 Then tPickup.List.Remove(Self)
		check_collisions()
		draw()
	End Method
	Method check_collisions()
		For Local plyr:tPlayer = EachIn tPlayer.List
			If Collision(x, y, plyr.x, plyr.y, Image.Width - 4, Image.Height - 4) = True Then powerup()
		Next
	End Method
	Method powerup()
		Select pickuptype
			Case PICKUP_SH_AMMO
				ShotGunAmmo:+32
				If ShotGunAmmo &gt; MaxShotGunAmmo Then ShotGunAmmo = MaxShotGunAmmo
			Case PICKUP_MG_AMMO
				MachineGunAmmo:+90
				If MachineGunAmmo &gt; MaxMachineGunAmmo Then MachineGunAmmo = MaxMachineGunAmmo
			Case PICKUP_SHOTGUN
				If HasShotGun = True
					ShotGunAmmo:+16
					If ShotGunAmmo &gt; MaxShotGunAmmo Then ShotGunAmmo = MaxShotGunAmmo
				Else
					HasShotGun = True
					MaxShotGunChamber = 16
					ShotGunChamber = MaxShotGunChamber
				EndIf
			Case PICKUP_MACHINEGUN
				If HasMachineGun = True
					MachineGunAmmo:+60
					If MachineGunAmmo &gt; MaxMachineGunAmmo Then MachineGunAmmo = MaxMachineGunAmmo
				Else
					HasMachineGun = True
					MaxMachineGunChamber = 60
					MachineGunChamber = MaxMachineGunChamber
				EndIf
		End Select
		tPickup.List.Remove(Self)
	End Method
	Method draw()
		swell:+swell_step
		If swell &gt; 1 swell_step = -.01
		If swell &lt;.5 swell_step =.01
		SetScale swell, swell
		SetBlend ALPHABLEND
		SetAlpha 1
		DrawImage(Image, x, y)
		SetScale 1, 1
	End Method
	
End Type

'I figured I'd save myself a lot of coding if I simply check if things are out of bounds using a function!
Function OutofBounds:Int(x:Float, y:Float)
	If x &lt; 0 Return True
	If Y &lt; 0 Return True
	If x &gt; 640 Return True
	If Y &gt; 400 Return True
	Return False
End Function

Function Collision:Byte(x:Float, y:Float, collidewith_x:Float, collidewith_y:Float, xsize:Float = 16, ysize:Float = 16)
	If Abs(x - collidewith_x) &lt; xsize And Abs(y - collidewith_y) &lt; ysize Then Return True
	Return False
End Function

HideMouse()
Graphics 640, 480
Make_Images()
SetBlend LIGHTBLEND
MoveMouse 320, 180
tPlayer.Spawn(320, 240)
Function makepowerups()
	For Local gg:Int = 1 To 10
		tPickup.make(Rand(640), Rand(400), Rand(0, 3))
	Next
End Function
Function UpdateInstances()
	For Local pkup:tPickup = EachIn tPickup.List
		pkup.Update()
	Next
	For Local plyr:tPlayer = EachIn tPlayer.List
		plyr.Update()
	Next
	For Local pshot:tPlayerShot = EachIn tPlayerShot.List
		pshot.Update()
	Next

End Function

Function DrawHud()
	'Draw Crosshair
	SetBlend ALPHABLEND
	SetColor 0, 0, 0
	DrawLine 0, 400, 640, 400
	DrawText "PISTOL", 25, 420
	DrawText PistolChamber + "/" + MaxPistolChamber, 25, 430
	DrawText "Infinite", 10, 460
	DrawLine 100, 400, 100, 480
	If HasShotGun = False SetColor 155, 155, 155
	DrawText "SHOTGUN", 125, 420
	DrawText ShotGunChamber + "/" + MaxShotGunChamber, 125, 430
	DrawText ShotGunAmmo + "/" + MaxShotGunAmmo, 110, 460
	SetColor 0, 0, 0
	DrawLine 200, 400, 200, 480
	If HasMachineGun = False SetColor 155, 155, 155
	DrawText "MINIGUN", 225, 420
	DrawText MachineGunChamber + "/" + MaxMachineGunChamber, 225, 430
	DrawText MachineGunAmmo + "/" + MaxMachineGunAmmo, 210, 460

	SetColor 0, 0, 0
	DrawLine 300, 400, 300, 480

End Function

Repeat
	Cls
	UpdateInstances()
	DrawHud()
	If KeyHit(KEY_SPACE) Then makepowerups()
	Flip
	
Until KeyHit(KEY_ESCAPE) Or AppTerminate()
</textarea> <br><br></td></tr></table><br>
<a name="988339"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> That's quite good!<br><br>Try something like this (untested)<br><pre class=code>Type TWeapon
	Field HasThis:Int
	Field Ammo:Int
	Field MaxAmmo:Int
	Field Chamber:Int
	Field MaxChamber:Int

	Method Shoot() Abstract
	Method Reload() &lt;- probably doesn't need to be abstract
	......
End Type

Type TPistol Extends TWeapon
	Method Shoot()
		'code here to update ammo, create blasts etc
	End Method
	..........other methods
End Type TPistol

Type TShotgun Extends TWeapon
and so on

Type TPlayer
	...........
	Field CurrentWeapon:TWeapon
	Field Pistol:TPistol = New TPistol
	Field Shotgun:TShotgun = New TShotgun

	Method ChangeWeapon(weaponid)
		Select weaponid
			Case PISTOLID
				If Pistol.HasThis
					CurrentWeapon = Pistol
				End If
		......
		End Select
	End Method

	Method Shoot()
		CurrentWeapon.Shoot()
	End Method
End Type</pre><br><br>This is just a conceptual suggestion rather than concrete code, but perhaps will give you some ideas?<br><br>An advantage of this method is it shouldn't be too hard to expand the code to allow multiplayer battles (same computer or network) as weapons are stored on a per player basis now!! <br><br></td></tr></table><br>
<a name="988423"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> That's fascinating..  I did not know you could put a Type in a Type as a Field!   I think I'm going to rewrite the weapon code with a concept like this, that is, if I can!  It will take some time, but I feel it's worth it! <br><br></td></tr></table><br>
<a name="988516"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> We learn something new every day :D<br>Yep fields can be anything, objects, arrays..! <br><br></td></tr></table><br>
<a name="988977"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Awesome..<br><br>I've managed to have time to put another hour into this project.  I've completely reworked the weapon code to fit the concept Czar Flavius has helped me realize!  <br><br>I'm also using Regions to better organize the code with BLIDE. <br><br>I don't have the other weapons in yet, nor enemies, but I wanted to share the code thus far as I think it's quite an achievement.  I'll have to plugin the shotgun and minigun again, and I'm not 100% sure how I'm going to go about that, but this is the code thus far.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'#Region TOP
'Arena BMX by Matt McFarland
'Iteration 1: 10/08/2009: 17:30 to 18:30   
'Iteration 2: 10/08/2009: 18:45 to 20:00   
'Iteration 3: 10/08/2009: 20:15 to 21:30   
'Iteration 4: 10/08/2009: 21:45 to 23:00  
'Iteration 5: 10/12/2009: 12:45 to 01:45
SuperStrict
'#End Region

'#Region Declare Variables and Constants
AppTitle = "Arena Shoot BMX! by Matthew A McFarland, 2009"

Const PISTOL:Int = 0
Const SHOTGUN:Int = 1
Const MACHINEGUN:Int = 2

Const PICKUP_SH_AMMO:Int = 0
Const PICKUP_MG_AMMO:Int = 1
Const PICKUP_SHOTGUN:Int = 2
Const PICKUP_MACHINEGUN:Int = 3

Const STATE_TITLE:Int = 0
Const STATE_PLAY:Int = 1
Const STATE_PAUSE:Int = 2
Const STATE_FAIL:Int = 3
Const INFINITE:Int = -9999

Global iShotGunAmmo:TImage
Global iMachineGunAmmo:TImage
Global iShotGun:TImage
Global iMiniGun:TImage
'#End Region

'#Region Types
Type tGameObject
	Field x:Float, y:Float, xv:Float, yv:Float, hp:Float
	Method Update() Abstract
End Type

Type tPlayer Extends tGameObject
	Global List:TList = New TList
	Field CurrentWeapon:tWeapon
	Field DeadDelay:Float
	Field DelayModifier:Float
	'When the player spawns it has a few configurations to it
	Function Spawn(x:Float, y:Float)
		Local plyr:tPlayer = New tPlayer
		plyr.x = x
		plyr.y = y
		plyr.CurrentWeapon = New tPistol
		plyr.DelayModifier = 1
		tPlayer.List.AddLast(plyr)
	End Function
	'Update the player depending on death/non death
	Method Update()
		If DeadDelay &gt;= 0
			ShowWeaponInfo()
			Control()
			Draw()
		End If
	End Method
	'I'm seperating control and draw functions so that I may just simply draw the screen if the game is paused.
	'Control the player
	Method Control()
		
		If KeyDown(KEY_W) And y &gt; 2 y:-2
		If KeyDown(KEY_A) And x &gt; 2 x:-2
		If KeyDown(KEY_S) And y &lt; 390 y:+2
		If KeyDown(KEY_D) And x &lt; 638 x:+2
		If MouseDown(1) And currentweapon.ShotDelay &lt;= 0 Then currentweapon.Shoot(x, y)

	End Method
	'Draw the player
	'Give weapon info
	Method ShowWeaponInfo()
		SetColor 0, 0, 0
		DrawText "Current Weapon: " + currentweapon.WepName, 10, 405
		DrawText "Rounds:", 10, 425
		If CurrentWeapon.Reloading = True
			DrawText "RELOADING!", 75, 425
		Else
			DrawText currentweapon.Rounds + "/" + currentweapon.MaxRounds, 75, 425
		End If
		If currentweapon.MaxAmmo = INFINITE Then DrawText "Ammo: Infinite", 10, 445 Else DrawText "Ammo: " + currentweapon.Ammo + "/" + currentweapon.MaxAmmo, 25, 445
		DrawText "Weapons: " + tWeapon.list.Count(), 10, 465
	End Method
	Method Draw()
		'Highlight Weapon Used
		SetAlpha ALPHABLEND
		SetColor 255, 255, 0
		SetColor 255, 255, 255
		'Draw Player
		SetColor 0, 0, 0
		DrawRect x - 4, y - 4, 8, 8
		SetAlpha 1
		Local d:Float = ATan2(y - MouseY(), x - MouseX()) + 180
		SetRotation d
		SetColor 0, 0, 0
		DrawLine x, y, x + 10, y
		SetColor 255, 255, 255
		SetRotation 0
		'Draw ShotDelay
		If currentweapon.ShotDelay &gt; 0
			Local ShotBar:Float = currentweapon.ShotDelay / 16
			For Local n:Float = 1 To shotbar Step 1
			SetColor 255, 0, 0
			DrawLine x - 4, y + 5, x - 4 + shotbar, y + 5
			Next			
		EndIf
		SetColor 100, 100, 055
		DrawLine(MouseX() - 6, MouseY() , MouseX() + 6, MouseY())
		DrawLine(MouseX(), MouseY() - 6, MouseX(), MouseY() + 6)
		SetColor 255, 255, 255
				
		SetColor 255, 255, 255
	End Method
	Method Shoot()

	End Method
	Method Reload()
	End Method
End Type

Type tWeapon
	Global list:TList = New TList
	Field HasThis:Int
	Field Ammo:Int
	Field MaxAmmo:Int
	Field Rounds:Int
	Field MaxRounds:Int
	Field ShotDelay:Float
	Field DelayModifier:Float
	Field WepName:String
	Field WepID:Int
	Field Reloading:Int
	Method Shoot(x:Float, y:Float) Abstract
	Method Reload(ReloadDelay:Float)
		If MaxAmmo = INFINITE
			ShotDelay = ReloadDelay
			Reloading = True
			Rounds = MaxRounds
		End If
		
	End Method
	Method Update()
		If shotdelay &lt; 10 Reloading = False
		If shotdelay &gt; 0 Then shotdelay:+DelayModifier
		SetColor 0, 0, 0
	End Method
End Type

Type tPistol Extends tWeapon
	Method New()
		HasThis = True
		Ammo = Infinite
		MaxAmmo = Infinite
		Rounds = 15
		MaxRounds = 15
		WepName = "Pistol"
		WepID = PISTOL
		DelayModifier = -1
		list.AddLast(Self)
	End Method
	Method Shoot(x:Float, y:Float)
		If Rounds &gt; 0 And shotdelay = 0
			Rounds:-1
			Local shot:tShot = New tShot
			shot.x = x ; shot.y = y
			shot.speed = 8
			shot.d = ATan2(y - MouseY(), x - MouseX()) + 180
			shot.xv = Cos(shot.d) * shot.speed
			shot.yv = Sin(shot.d) * shot.speed
			shotdelay = 40
		Else
			Reload(120)
		End If
	End Method

End Type

Type tPickup Extends tGameObject
	Global List:TList = New TList
	Field pickuptype:Int
	Field duration:Int
	Field Swell:Float
	Field Swell_step:Float
	Field Image:TImage
	Function Make(x:Float, y:Float, pickuptype:Int)
		Local newpk:tPickup = New tPickup
		newpk.x = x
		newpk.y = y
		newpk.pickuptype = pickuptype
		If pickuptype = PICKUP_SH_AMMO Then newpk.Image = iShotGunAmmo
		If pickuptype = PICKUP_MG_AMMO Then newpk.Image = iMachineGunAmmo
		If pickuptype = PICKUP_SHOTGUN Then newpk.Image = iShotGun
		If pickuptype = PICKUP_MACHINEGUN Then newpk.Image = iMiniGun
		newpk.Swell = 1
		newpk.Swell_step =.1
		tPickup.List.AddLast(newpk)
		
	End Function
	Method Update()
		duration:+1
		If duration &gt; 500 Then tPickup.List.Remove(Self)
		check_collisions()
		draw()
	End Method
	Method check_collisions()
		For Local plyr:tPlayer = EachIn tPlayer.List
			If Collision(x, y, plyr.x, plyr.y, Image.Width - 4, Image.Height - 4) = True Then powerup()
		Next
	End Method
	Method powerup()
	End Method
	Method draw()
		swell:+swell_step
		If swell &gt; 1 swell_step = -.01
		If swell &lt;.5 swell_step =.01
		SetScale swell, swell
		SetBlend ALPHABLEND
		SetAlpha 1
		DrawImage(Image, x, y)
		SetScale 1, 1
	End Method
	
End Type

Type tShot Extends tGameObject
	Global List:TList = New TList
	Field d:Float
	Field speed:Float
	Method New()
		list.AddLast(Self)
	End Method
	Method Update()
		If OutofBounds(x, y) Then list.Remove(Self)
		x:+xv
		y:+yv
		Draw()
	End Method
	Method Draw()
		SetBlend ALPHABLEND
		SetColor 0, 0, 0
		DrawRect(x - 1, y - 1, 2, 2)

	End Method
End Type
'#End Region

'#Region Custom Functions
Function Make_Images()
	SetClsColor 255, 255, 255
	AutoMidHandle(True)
	SetBlend ALPHABLEND
	'Make the ShotGun Ammo Box and save it to memory
	Cls
	SetColor 0, 0, 0
	DrawRect 0, 0, 26, 14
	SetColor 254, 254, 254
	SetScale 1, .9
	DrawText "=s=", 0, 0
	setscale 1,1
	iShotGunAmmo:TImage = CreateImage(27, 15, 1, DYNAMICIMAGE | MASKEDIMAGE)
	GrabImage iShotGunAmmo, 0, 0
	'Make the MachineGun Ammo Box and save it to memory
	Cls
	SetColor 0, 0, 0
	DrawRect 0, 0, 30, 14
	SetColor 254, 254, 254
	SetScale.9, .9
	DrawText "=mg=", 0, 0
	setscale 1,1
	iMachineGunAmmo:TImage = CreateImage(31, 15, 1, DYNAMICIMAGE | MASKEDIMAGE)
	GrabImage iMachineGunAmmo, 0, 0
	SetClsColor 0, 0, 0

	'Make the ShotGun and save it to memory
	Cls
	SetColor 1, 1, 1
	DrawRect 0, 0, 4, 10
	DrawRect 4, 0, 12, 4
	SetColor 255, 255, 255
	iShotGun:TImage = CreateImage(16, 13, 1, DYNAMICIMAGE | MASKEDIMAGE)
	GrabImage iShotGun, 0, 0

	'Make the MachineGun and save it to memory
	Cls
	SetColor 1, 1, 1
	DrawRect 0, 0, 14, 16
	DrawRect 8, 0, 34, 12
	SetColor 245, 245, 245
	DrawLine 16, 4, 40, 4
	iMiniGun:TImage = CreateImage(47, 17, 1, DYNAMICIMAGE | MASKEDIMAGE)
	GrabImage iMiniGun, 0, 0
	SetClsColor 255, 255, 255

End Function
Function OutofBounds:Int(x:Float, y:Float)
	If x &lt; 0 Return True
	If Y &lt; 0 Return True
	If x &gt; 640 Return True
	If Y &gt; 400 Return True
	Return False
End Function

Function Collision:Byte(x:Float, y:Float, collidewith_x:Float, collidewith_y:Float, xsize:Float = 16, ysize:Float = 16)
	If Abs(x - collidewith_x) &lt; xsize And Abs(y - collidewith_y) &lt; ysize Then Return True
	Return False
End Function

Function makepowerups()
	For Local gg:Int = 1 To 10
		tPickup.make(Rand(640), Rand(400), Rand(0, 3))
	Next
End Function

Function UpdateInstances()
	For Local weapon:tWeapon = EachIn tWeapon.List
		weapon.Update()
	Next
	For Local pkup:tPickup = EachIn tPickup.List
		pkup.Update()
	Next
	For Local plyr:tPlayer = EachIn tPlayer.List
		plyr.Update()
	Next
	For Local shot:tShot = EachIn tShot.List
		shot.Update()
	Next
	
End Function

Function DrawHud()
	'Draw Crosshair
	SetBlend ALPHABLEND
	SetColor 0, 0, 0
	DrawLine 0, 400, 640, 400


End Function

'#End Region

'#Region Start Game
HideMouse()
Graphics 640, 480
Make_Images()
SetBlend LIGHTBLEND
MoveMouse 320, 180
tPlayer.Spawn(320, 240)
'#End Region 

'#Region Main Loop
Repeat
	Cls
	UpdateInstances()
	DrawHud()
	If KeyHit(KEY_SPACE) Then makepowerups()
	Flip
	
Until KeyHit(KEY_ESCAPE) Or AppTerminate()
'#End Region
</textarea><br><br>Now I feel freedom to add as many different weapons as I'd like!!  Hmm and maybe multiplayer one day!?!?   My main focus of this project is improving my coding skills so feel free to critique! <br><br></td></tr></table><br>
<a name="988985"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi. How are you planning to add more than one weapon? With only current weapon, if you switch to a new weapon you'll lose the info on the old one. In the code I posted above, you'll see that there is a field in player for each weapon type, so the ammo etc for each weapon is remembered.<br><br>Do you know about type referencing? To begin with you imagine that each type variable contains its type, just like an Int variable contains an integer. But, type variables are actually <i>signposts</i> to their objects. So far you've just had a single signpost for each object, so there's been no difference. But the key difference, is you can have <i>multiple</i> signposts pointing to ("referencing") the <i>same</i> object.<br><br>This is quite a tricky thing to get your head around, and I probably haven't explained it very well. In my example code, CurrentWeapon isn't for storing its own weapon type, it exists only to point to (reference) <i>another</i> weapon type. When you tell CurrentWeapon to fire, it sends the fire command to whatever weapon it is currently pointing to (referencing). <br><br>When the player needs to change weapon, CurrentWeapon is simply told to point to another weapon object. The handy thing of this trick is, the original weapon is not lost! - because it is still pointed to by another variable. It went from having 2 signposts, to 1. So long as you still keep 1 signpost for an object, it isn't lost and can be reused.<br><br>Another implication of this, is you can add the same object to multiple TLists, if you wanted to. Any action or update performed to the object in one list, would update the object in all the other lists too. Or, more correctly, the entry in each list references the same object, so sending a command to any of the references updates the same object that each entry signposts to.<br><br>Here is a bit more beefed up example code.<br><br><pre class=code>Const PistolID:Int = 1, ShotgunID:Int = 2, MachinegunID:Int = 3

Type TPlayer
	...........
	Field CurrentWeapon:TWeapon
	Field Pistol:TPistol
	Field Shotgun:TShotgun
	Field Machinegun:TMachinegun

	Method New()
		Pistol = New TPistol
		Pistol.HasThis = True

		Shotgun = New TShotgun
		Shotgun.HasThis = False

		Machinegun = New TMachinegun
		Machinegun.HasThis = False

		ChangeWeapon(PistolID) 'so the player spawns with a weapon already selected
		'or it will crash if he tries to fire
	End Method

	Method ChangeWeapon(weaponid:Int)
		Select weaponid
			Case PistolID
				If Pistol.HasThis
					CurrentWeapon = Pistol
				End If
			Case ShotgunID
				If Shotgun.HasThis
					CurrentWeapon = Shotgun
				End If
			Case MachinegunID
				If Machinegun.HasThis
					CurrentWeapon = Machinegun
				End If
		End Select
	End Method

	Method Shoot()
		CurrentWeapon.Shoot()
	End Method
End Type</pre><br><br>I added ID on the end of the constants (PistolID), to prevent confusion with the fields (Pistol). (Of course you can call them whatever you wish, so long as the names are unique!)<br><br>About your code, something I noticed. You have a few SetColors on the end of methods and functions. This is a bad idea as it forces you to call your draw functions in a specific order. It is better for any drawing routine to assume the incoming set colour could be anything, and set the colour itself as needed, rather than hoping it gets the right colour coming in. <br><br></td></tr></table><br>
<a name="989023"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TommyH</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> In my opinion it would make more sense to have an array of TWeapon as a field of the player.<br>Every weapon that is "picked up" is added to the array. The current weapon is just an index into the weapons array.<br>The size of the array could/would limit the amount of weapons the player can carry. Or make it a TList of TWeapons if you want the player to carry as many weapons as possible.<br><br>This array/list approach has two advantages:<br>first you get rid of the "HasThis" field in TWeapon. The player owns all the weapons inside the array. That's it.<br>Second you can add different weapons later as they only have to be subclasses of TWeapon. But you don't need to change the TPlayer type to add a field for the new weapon type (field Pistol, field Shotgun, field BFG...). This is unnecessary and not very "OO" ;-)<br><br>I hope you got the point from my germish explanations ;-)<br><br>Tommy <br><br></td></tr></table><br>
<a name="989033"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for your replies.<br><br>@ Czar:  I wasn't' sure how I was planning on adding other weapons, I was gonna wing it and add a Tshotgun and go from there :P <br><br>So instead of creating a new weapon in the player new() function,  I create the new weapons in the player field.  Also, on Player New() I use the Changeweapon method to set the weapon to the pistol.   Finally, I use WeaponID to figure if I have a weapon or not.   Since all weapons are actually fields within the type, I can reference each weapon on the fly, and shoot "currentweapon" when the fire button is pressed.<br><br>Just making sure I get what you're saying Czar.  I'm not 100% on the signpost thing, but maybe if you could point out exactly what lines of code are signpost receive and signpost send I'd better understand.<br><br>@ TommyH: Thanks, I'll consider using arrays sometime someday, but not with this project as of yet. <br><br></td></tr></table><br>
<a name="989034"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Germish?? Do you mean Germanish? Germish = bacteria :P<br><br>As for your suggestion, I was assuming that the total number of weapons would be small (less than 10 once complete?) and a player could only carry one of each, so it would be simpler just to hardcode them as fields. If the player tries to select the shotgun for example, in my opinion it would be simpler and quicker just to say shotgun.hasthis rather than go through a list one by one looking for it. But if the game were more complicated, with many weapons, I might prefer a different way.<br><br>Of course it all depends on you Matt. It is your game after all :D<br><br>Edit: haha we posted at the same time! Ok, new reply incoming.. <br><br></td></tr></table><br>
<a name="989035"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> @ Czar: Well I'd like to code it in a way that it would be easily expandable as far as the weapons go, so maybe going through a list would be a good idea for this project despite the short(or maybe not by the 24th hour) weapon list?  Also, the player should be able to carry all weapons if it has them, but cant carry more than one of each weapon. <br><br></td></tr></table><br>
<a name="989051"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ready? :)<br><br>First, as to the expandability, to add a new weapon to my idea, all you need to do is add a new field to the player type, and a new case to the select sequence. That seems fairly easy to me :) The advantage of this way is, by virtue of its design, it is impossible for a player to carry more than one of the same weapon. The trouble with a list is it can contain repeated objects, so every time the player picks up a new weapon you'll need to make checks that it isn't already in the list, and when the player changes weapon you'll need to search through the list for the weapon. I just don't think it is an elegant solution.<br><br>I hope this code is informative to you.<br><pre class=code>Type Elephant
	Field name:String
	Field age:Int
	
	Method speak()
		Print "I am " + name + " and " + age + " years old."
	End Method
	
	Function Create:Elephant(name:String, age:Int)
		Local temp:Elephant = New Elephant
		temp.name = name
		temp.age = age
		Return temp
	End Function
End Type

'let's create some elephants!
'i have put the ages in numerical order, to help you keep track
Local first:Elephant = Elephant.Create("Dumbo", 1)
Local second:Elephant = Elephant.Create("Big Nose", 2)
Local third:Elephant = Elephant.Create("George Bush", 3)

'Dumbo 1
first.speak()
'Big Nose 2
second.speak()
'George Bush 3
third.speak()

'now let's use a signpost, and point it at the first elephant
Local signpost:Elephant = first

'this will print out the details: Dumbo and 1!
signpost.speak()

signpost = second

'what will this say?
signpost.speak()
'answer: Big Nose and 2

'we can use the signpost to change another variable's object
signpost.name = "Changed"

'this will say Changed and 2
signpost.speak()

'what about second? has it been updated too?? yes it has!
'Changed 2
second.speak()

'the key concept is, we're not just dealing with copies of the objects
'second and signpost both point to the same object
'there is no special signposting variable. signpost could be called anything
'let's use second as a signpost

second.name = "I've changed again"
signpost.speak()
'this says I've changed again and 2!

'let's make second point to the first elephant
second = first
second.name = "Jumbo"
first.speak()
'will it say Jumbo or Dumbo?? (Jumbo!)

signpost = third
'signpost no longer points to I've changed again. what has happened to him?
'can we access him anymore? no we cannot. we've killed him. he will be erased from memory!
'an object dies when it no longer has any signposts to it

first = signpost
second = signpost
'now we have only one elephant left. what is his name?
'will these all say the same thing?
first.speak()
second.speak()
third.speak()
signpost.speak()</pre><br><br>Here's another example: a board game with multiple players to be played on the same computer. You'll have player1:TPlayer, player2:TPlayer etc. and you can have a signposting variable currentplayer:TPlayer, that is made to point to whoever's go it is. Then the code to make a move just needs to be currentplayer.makemove(blah). To change to player 2's turn, just say currentplayer = player2<br><br>I think I'll leave it at that for now, and wait to see if I'm helping or just making you even more confused first :P<br><br>Note: "signpost" is just my personal word for it; the technical term is "reference". So when you talk about it with other programmers, call it that :P<br><br><div class="quote"> So instead of creating a new weapon in the player new() function, I create the new weapons in the player field. <br></div> You can do it either way; it's entirely equivalent. I've edited the code above to that way if you prefer it. I also forgot to tell it which weapons we start with or not.<br><br><div class="quote"> Also, on Player New() I use the Changeweapon method to set the weapon to the pistol. <br></div> Yes, if you don't, CurrentWeapon won't point to anything in the beginning and if the player fires without selecting a weapon, what should happen?<br><br><div class="quote"> Finally, I use WeaponID to figure if I have a weapon or not. Since all weapons are actually fields within the type, I can reference each weapon on the fly, and shoot "currentweapon" when the fire button is pressed. <br></div> Well you use the HasThis field to figure out if the weapon is in his possession or not. If you wanted to take a shortcut, you could change the IDs of the weapons to the keycodes of the number keys, as the key codes are just numbers themselves. IE PistolID = KEY_1. This might make deciding what weapon to use based on the key press easier. <br><br></td></tr></table><br>
<a name="989150"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TommyH</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Czar: I didn't know "germish" was indeed an english noun...I thought it was the common expression for "german english".<br><br>@Matt: No matter if you use arrays, lists or fields to store the weapons: Keep on going and have fun ;-) <br><br></td></tr></table><br>
<a name="989158"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Maybe it is, and I just don't know about it :P But germ means bacteria. <br><br></td></tr></table><br>
<a name="989266"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ahh Czar I see what you mean, you're not even using a list!  I was putting my weapons in a list so I could reference them in that way.  The reason is because I can easily use list.count to show the player how many weapons he has But your solution is STILL better!  I really like not using a list, and I didnt even know that was possible; I thought you always had to use a list because I had no idea you could add other references.  I'd like to somehow tell the player what weapons he has available; like "you have 3 weapons" or something and I'm not sure how to go about that. But thanks alot for the reference lesson.<br><br>EDIT: Understand the reference thing. <br><br></td></tr></table><br>
<a name="989320"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> The simplist way is to have a WeaponCount:Int field which you increase by one every time a player picks up a weapon. You should be careful when using list.count() as it counts each individual object in the list, and will take more time for longer lists.<br><br>Perhaps you can add a PickupWeapon method to the player, which takes the ID of the weapon to give. This sets the HasThis field of the weapon to true, and increases the weapon count by one (after checking the weapon is not already held). To avoid repeating the Select Case, it is better to create a function that takes a weapon ID and returns a reference to that weapon in the player's arsenal. Here is some example code:<br><br><pre class=code>SuperStrict

Const PistolID:Int = 1, ShotgunID:Int = 2, MachinegunID:Int = 3

Type TWeapon
	Field HasThis:Int = False
	Field ammo:Int = 0
	Method Shoot()
		Print "Bang"
	End Method
End Type

Type TPistol Extends TWeapon
End Type

Type TShotgun Extends TWeapon
End Type

Type TMachinegun Extends TWeapon
End Type

Type TPlayer
	Field CurrentWeapon:TWeapon
	Field WeaponCount:Int = 0
	Field Pistol:TPistol = New TPistol
	Field Shotgun:TShotgun = New TShotgun
	Field Machinegun:TMachinegun = New TMachinegun
	
	Method New()
		'the player starts the game with a pistol, selected as his weapon
		PickupWeapon(PistolID)
		ChangeWeapon(PistolID)
		Assert CurrentWeapon, "The attempt to spawn the player with a weapon failed for some reason."
	End Method
	
	Method GetWeapon:TWeapon(weaponID:Int)
		Select weaponID
			Case PistolID
				Return Pistol
			Case ShotgunID
				Return Shotgun
			Case MachinegunID
				Return Machinegun
			Default
				'we've been asked for a weapon ID that doesn't exist, so let's cause an error
				Assert False, "Tried to access weapon id " + weaponID + "but it does not exist!"
				Return Null
		End Select
	End Method
	
	Method PickupWeapon(weaponID:Int)
		Local weapon:TWeapon = GetWeapon(weaponID)
		If Not weapon.HasThis
			weapon.HasThis = True
			WeaponCount :+ 1
		End If
	End Method
	
	Method LoseWeapon(weaponID:Int)
		Local weapon:TWeapon = GetWeapon(weaponID)
		If weapon.HasThis
			weapon.HasThis = False
			'do you want them to lose the ammo too?
			weapon.ammo = 0
			WeaponCount :- 1
		End If
	End Method
	
	Method PickupAmmo(weaponID:Int, amount:Int)
		Assert amount &gt;= 0, "We were told to pick up negative ammo!? amount = " + amount + "."
		Local weapon:TWeapon = GetWeapon(weaponID)
		'if you want to let them pick up ammo for weapons they don't have yet, remove this check
		If weapon.HasThis
			weapon.ammo :+ amount
		End If
	End Method
	
	Method ChangeWeapon(weaponID:Int)
		Local weapon:TWeapon = GetWeapon(weaponID)
		If weapon.HasThis
			CurrentWeapon = weapon
		End If
	End Method
	
	Method Shoot()
		If CurrentWeapon.HasThis
			CurrentWeapon.Shoot()
		End If
	End Method
	
	Method HowManyWeapons()
		Print "I have " + WeaponCount + " weapons."
	End Method
End Type</pre><br><br>Note that the code is not fully tested. Asserts provide you with checks to help you track down any errors or bugs in your code, and provide you with a custom error message. I suggest you use them often, as it can make tracking down an obscure error much easier, before allowing it to propagate through the entire game and becoming impossible to catch!<br><br>For example, if you attempt to shoot CurrentWeapon, but this variable has not been assigned the reference of a weapon yet, it will cause the game to crash as it won't be able to find any object to give the Shoot() command to. (Remember that CurrentWeapon isn't given a "New" when defined, so it starts empty) Important: Asserts are only checked when compiled in Debug mode.<br><br><div class="quote"> I really like not using a list, and I didnt even know that was possible <br></div>I'd just like to point out, I am not anti-list :D It's just important to know there are alternatives, so you can pick the best tool for the job. Like I said in an earlier post, if the number of weapons was very large, you could have more than one etc, lists (or arrays, or even a map!) would become more suitable for this problem.. <br><br></td></tr></table><br>
<a name="990062"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is fascinating.. The GetWeapon:Tweapon(Wep_ID) method was the first method I've ever used to return a Tweapon!! Very good stuff!<br><br>Now, I realize that when changing a weapon to a null id we want a special error.  Especially if down the road I'm doing something I haven't yet planned out.  Here's the thing however, I'm using the mouse wheel to change weapons as well as numerical keys.<br><br>Here's the entire code so far, roughly 8 hours into the project! 552 lines!<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'#Region TOP
'Arena BMX by Matt McFarland
'Iteration 1: 10/08/2009: 17:30 to 18:30 1  
'Iteration 2: 10/08/2009: 18:45 to 20:00 1.25  
'Iteration 3: 10/08/2009: 20:15 to 21:30 1.25  
'Iteration 4: 10/08/2009: 21:45 to 23:00 1.25 
'Iteration 5: 10/12/2009: 12:45 to 01:45 1
'Iteration 6: 10/13/2009: 18:15 to 19:30 1.25
'Iteration 7: 10/13/2009: 20:15 to 21:00 .75
'Iteration 8: 10/18/2009: 22:30 to 23:00 .50 - Added Methods to player to pickup weapons or ammunition

SuperStrict
'#End Region

'#Region Declare Variables and Constants
AppTitle = "Arena Shoot BMX! by Matthew A McFarland, 2009"

Const PISTOL_ID:Int = 0
Const SHOTGUN_ID:Int = 1
Const MINIGUN_ID:Int = 2
Const WEP_UP:Int = -2
Const WEP_DN:Int = -1

Const PICKUP_SH_AMMO:Int = 0
Const PICKUP_MG_AMMO:Int = 1
Const PICKUP_SHOTGUN:Int = 2
Const PICKUP_MINIGUN:Int = 3

Const STATE_TITLE:Int = 0
Const STATE_PLAY:Int = 1
Const STATE_PAUSE:Int = 2
Const STATE_FAIL:Int = 3
Const STATE_DEAD:Int = 0
Const STATE_INVINCIBLE:Int = 1
Const STATE_VULNERABLE:Int = 2
Const SOLID:Int = 0
Const BLINKING:Int = 1
Const INFINITE:Int = -9999

Global iShotGunAmmo:TImage
Global iMiniGunAmmo:TImage
Global iShotGun:TImage
Global iMiniGun:TImage
'#End Region

'#Region Types
Type tGameObject
	Field x:Float, y:Float, xv:Float, yv:Float, hp:Float
	Method Update() Abstract
End Type

Type tPlayer Extends tGameObject
	Global List:TList = New TList
	Field CurrentWeapon:tWeapon
	Field Pistol:tPistol
	Field ShotGun:tShotgun
	Field MiniGun:tMinigun
	Field WeaponCount:Int
	'Player state 0 to 100 = Invincible, Player state 101 = Alive, Playerstate &lt; 0 = Dead
	Field StateCount:Int
	Field PlayerState:Int
	Field State:Int
	Field LastMouseZ:Int
	
	Method New()
		x = 320 ; y = 200
		Pistol = New tPistol
		Shotgun = New tShotgun
		Minigun = New tMinigun
		PickupWeapon(PISTOL_ID)
		list.AddLast(Self)
	End Method
	
	Method PickupWeapon(Wep_ID:Int)
		Local Weapon:tWeapon = GetWeapon(Wep_ID)
		If Not Weapon.HasThis
			Weapon.HasThis = True
			ChangeWeapon(Wep_ID)
			WeaponCount:+1
		End If
		Weapon.Rounds = Weapon.MaxRounds 'Max the Rounds on collect
	End Method
	
	Method LoseWeapon(Wep_ID:Int)
		Local Weapon:tWeapon = GetWeapon(Wep_ID)
		If weapon.HasThis = True Then weapon.HasThis = False ; WeaponCount:-1
	End Method
	
	Method PickupAmmo(Wep_ID:Int, Amount:Int)
		Assert Amount &gt; 0, "Can't add Ammo: " + Amount
		Local Weapon:tWeapon = Getweapon(Wep_ID)
		Weapon.Ammo:+Amount
		If Weapon.Ammo &gt; Weapon.MaxAmmo Then Weapon.Ammo = Weapon.MaxAmmo
	End Method
		
	Method GetNextWeapon()
		Local LastID:Int = CurrentWeapon.WepID
		For Local IDSearch:Int = CurrentWeapon.WepID To 9 Step 1
			ChangeWeapon(IDSearch)
			If CurrentWeapon.WepID &lt;&gt; LastID Then Return
		Next

	End Method
	
	Method GetPrevWeapon()
		Local LastID:Int = CurrentWeapon.WepID
		For Local IDSearch:Int = CurrentWeapon.WepID To 0 Step - 1
			ChangeWeapon(IDSearch)
			If CurrentWeapon.WepID &lt;&gt; LastID Then Return
		Next
		
	End Method
	
	Method GetWeapon:tWeapon(Wep_ID:Int)
		Select Wep_ID

			Case PISTOL_ID
				Return Pistol
			Case SHOTGUN_ID
				Return Shotgun
			Case MINIGUN_ID
				Return Minigun
				
			Default
				Return Pistol
				
		End Select
	End Method
	
	Method ChangeWeapon(Wep_ID:Int)
			Local weapon:tWeapon = GetWeapon(Wep_ID)
			If weapon.HasThis = True Then CurrentWeapon = weapon
	End Method

	'Update the player depending on death/non death
	Method Update()
		GetState()
		If State = STATE_DEAD Or State = STATE_INVINCIBLE StateCount:+1
		If State = STATE_DEAD Return
		CurrentWeapon.Update()
		ShowWeaponInfo()
		Control()
		Draw_Crosshair()
		If CurrentWeapon.ShotDelay &gt; 0 Then Draw_DelayBar()
		If State = STATE_INVINCIBLE Then Draw_Player(BLINKING) Else Draw_Player(SOLID)
		
	End Method
	
	Method GetState()
		If StateCount &lt; 0 Then State = STATE_DEAD
		If StateCount &gt;= 0 And StateCount &lt;= 100 Then State = STATE_INVINCIBLE
		If StateCount &gt; 100 Then State = STATE_VULNERABLE
	End Method
	
	Method Control()		
		'Move
		If KeyDown(KEY_W) And y &gt; 2 y:-2
		If KeyDown(KEY_A) And x &gt; 2 x:-2
		If KeyDown(KEY_S) And y &lt; 390 y:+2
		If KeyDown(KEY_D) And x &lt; 638 x:+2
		'Shoot
		If MouseDown(1) And CurrentWeapon.ShotDelay &lt;= 0 Then CurrentWeapon.Shoot(x, y)
		'Reload
		If KeyHit(KEY_R) CurrentWeapon.Reload(90)
		'ChangeWeapon
		If KeyHit(KEY_1) Then ChangeWeapon(PISTOL_ID)
		If KeyHit(KEY_2) Then ChangeWeapon(SHOTGUN_ID)
		If KeyHit(KEY_3) Then ChangeWeapon(MINIGUN_ID)
		If MouseZ() &lt;&gt; LastMouseZ
			If MouseZ() &gt; LastMouseZ
				GetNextWeapon()
				LastMouseZ = MouseZ()
			End If
			If MouseZ() &lt; LastMouseZ
				GetPrevWeapon()
				LastMouseZ = MouseZ()
			End If
		End If
					

	End Method
	Method ShowWeaponInfo()
		SetColor 0, 0, 0
		DrawText "Current Weapon: " + currentweapon.WepName, 10, 405
		DrawText "Rounds:", 10, 425
		DrawText "Weapons: " + weaponcount, 3, 465
		If CurrentWeapon.Reloading = True
			DrawText "RELOADING!", 75, 425
		Else
			DrawText currentweapon.Rounds + "/" + currentweapon.MaxRounds, 75, 425
		End If
		If currentweapon.MaxAmmo = INFINITE Then DrawText "Ammo: Infinite", 26, 445 Else DrawText "Ammo: " + currentweapon.Ammo + "/" + currentweapon.MaxAmmo, 26, 445
	End Method
	
	Method Draw_Player(Visual:Int)
		SetAlpha ALPHABLEND
				
		Select Visual
			Case BLINKING
				SetColor 0, 0, 0
				If StateCount &lt; 10 Then DrawRect x - 4, y - 4, 8, 8
				If StateCount &gt; 20 And StateCount &lt; 30 DrawRect x - 4, y - 4, 8, 8
				If StateCount &gt; 40 And StateCount &lt; 50 DrawRect x - 4, y - 4, 8, 8
				If StateCount &gt; 60 And StateCount &lt; 70 DrawRect x - 4, y - 4, 8, 8
				If StateCount &gt; 80 And StateCount &lt; 90 DrawRect x - 4, y - 4, 8, 8
				'draw weapon
				Local d:Float = ATan2(y - MouseY(), x - MouseX()) + 180
				SetRotation d
				DrawLine x, y, x + 10, y
				SetRotation 0
			Case SOLID
				SetColor 0, 0, 0
				DrawRect x - 4, y - 4, 8, 8
				'draw weapon
				Local d:Float = ATan2(y - MouseY(), x - MouseX()) + 180
				SetRotation d
				DrawLine x, y, x + 10, y
				SetRotation 0
		End Select

		
	End Method
	
	Method Draw_DelayBar()
		'Draw ShotDelay
			Local ShotBar:Float = currentweapon.ShotDelay / 16
			For Local n:Float = 1 To shotbar Step 1
			SetColor 255, 0, 0
			DrawLine x - 4, y + 5, x - 4 + shotbar, y + 5
			Next			
		
	End Method
	
	Method Draw_CrossHair()
		'Draw CrossHair
		SetColor 100, 100, 055
		DrawLine(MouseX() - 6, MouseY() , MouseX() + 6, MouseY())
		DrawLine(MouseX(), MouseY() - 6, MouseX(), MouseY() + 6)		
	End Method
End Type

'#Region Weapons
Type tWeapon
	Field HasThis:Int
	Field Ammo:Int
	Field MaxAmmo:Int
	Field Rounds:Int
	Field MaxRounds:Int
	Field Damage:Int
	Field ShotDelay:Float
	Field DelayModifier:Float
	Field WepName:String
	Field WepID:Int
	Field Reloading:Int
	Method Shoot(x:Float, y:Float) Abstract
	Method Reload(ReloadDelay:Float)
		If MaxAmmo = INFINITE
			Reloading = True
			Rounds = MaxRounds
			ShotDelay = ReloadDelay
			Return
		End If
		If Ammo &gt; 0
			Reloading = True
			Local RoundsNeeded:Int = MaxRounds - Rounds
			If RoundsNeeded &gt; Ammo Then RoundsNeeded = Ammo
			Ammo:-RoundsNeeded
			Rounds:+RoundsNeeded
			ShotDelay = ReloadDelay
		End If
	End Method
	Method Update()
		If shotdelay &lt; 10 Reloading = False
		If shotdelay &gt; 0 Then shotdelay:+DelayModifier
	End Method
End Type

Type tPistol Extends tWeapon
	Method New()
		Ammo = INFINITE
		MaxAmmo = INFINITE
		Rounds = 15
		MaxRounds = 15
		WepName = "Pistol"
		WepID = PISTOL_ID
		DelayModifier = -1
		Damage = 3
	End Method
	Method Shoot(x:Float, y:Float)
		If Rounds &gt; 0 And shotdelay = 0
			Rounds:-1
			Local shot:tShot = New tShot
			shot.x = x ; shot.y = y
			shot.speed = 10
			shot.d = ATan2(y - MouseY(), x - MouseX()) + 180 + Rand(- 1, 0) + RndFloat()
			shot.xv = Cos(shot.d) * shot.speed
			shot.yv = Sin(shot.d) * shot.speed
			shotdelay = 35
		Else
			Reload(120)
		End If
	End Method

End Type

Type tShotgun Extends tWeapon
	Method New()
		Ammo = 0
		MaxAmmo = 100
		Rounds = 0
		MaxRounds = 20
		WepName = "Shotgun"
		WepID = SHOTGUN_ID
		DelayModifier = -1
		Damage = 2
	End Method
	Method Shoot(x:Float, y:Float)
		If Rounds &gt; 0 And shotdelay = 0
			Rounds:-1
			Local shot:tShot
			For Local blast:Int = 1 To 9
				shot:tShot = New tShot
				shot.x = x ; shot.y = y
				shot.speed = Rand(8, 12)
				shot.d = ATan2(y - MouseY(), x - MouseX()) + 180 + Rand(- 8, 8) + RndFloat()
				shot.xv = Cos(shot.d) * shot.speed
				shot.yv = Sin(shot.d) * shot.speed
				shotdelay = 50
			Next
		Else
			Reload(120)
		End If
	End Method

End Type

Type tMinigun Extends tWeapon
	Method New()
		Ammo = 0
		MaxAmmo = 300
		Rounds = 0
		MaxRounds = 60
		WepName = "Minigun"
		WepID = MINIGUN_ID
		DelayModifier = -1
	End Method
	Method Shoot(x:Float, y:Float)
		If Rounds &gt; 0 And shotdelay = 0
			Rounds:-1
			Local shot:tShot = New tShot
			shot.x = x ; shot.y = y
			shot.speed = 11
			shot.d = ATan2(y - MouseY(), x - MouseX()) + 180 + Rand(- 4, 4) + RndFloat()
			shot.xv = Cos(shot.d) * shot.speed
			shot.yv = Sin(shot.d) * shot.speed
			shotdelay = 5
		Else
			Reload(120)
		End If
	End Method

End Type

Type tPickup Extends tGameObject
	Global List:TList = New TList
	Field pickuptype:Int
	Field duration:Int
	Field Swell:Float
	Field Swell_step:Float
	Field Image:TImage
	Field Wep_ID:Int
	Field Ammo:Int
	Function Make(x:Float, y:Float, pickuptype:Int)
		Local newpk:tPickup = New tPickup
		newpk.x = x
		newpk.y = y
		newpk.pickuptype = pickuptype
		If pickuptype = PICKUP_SH_AMMO Then newpk.Image = iShotGunAmmo ; newpk.Wep_ID = SHOTGUN_ID ; newpk.Ammo = 20
		If pickuptype = PICKUP_MG_AMMO Then newpk.Image = iMiniGunAmmo ; newpk.Wep_ID = MINIGUN_ID ; newpk.Ammo = 40
		If pickuptype = PICKUP_SHOTGUN Then newpk.Image = iShotGun ; newpk.Wep_ID = SHOTGUN_ID
		If pickuptype = PICKUP_MINIGUN Then newpk.Image = iMiniGun ; newpk.Wep_ID = MINIGUN_ID
		newpk.Swell = 1
		newpk.Swell_step =.1
		tPickup.List.AddLast(newpk)
		
	End Function
	Method Update()
		duration:+1
		If duration &gt; 500 Then tPickup.List.Remove(Self)
		check_collisions()
		draw()
	End Method
	Method check_collisions()
		For Local plyr:tPlayer = EachIn tPlayer.List
			If Collision(x, y, plyr.x, plyr.y, Image.Width - 4, Image.Height - 4) = True
			list.Remove(Self)
				If Ammo = Null Then plyr.PickupWeapon(Wep_ID) Else plyr.PickupAmmo(Wep_ID, Ammo)
			End If
		Next
	End Method

	Method draw()
		swell:+swell_step
		If swell &gt; 1 swell_step = -.01
		If swell &lt;.5 swell_step =.01
		SetScale swell, swell
		SetBlend ALPHABLEND
		SetAlpha 1
		DrawImage(Image, x, y)
		SetScale 1, 1
	End Method
	
End Type

Type tShot Extends tGameObject
	Global List:TList = New TList
	Field d:Float
	Field speed:Float
	Method New()
		list.AddLast(Self)
	End Method
	Method Update()
		If OutofBounds(x, y) Then list.Remove(Self)
		x:+xv
		y:+yv
		Draw()
	End Method
	Method Draw()
		SetBlend ALPHABLEND
		SetColor 0, 0, 0
		DrawRect(x - 1, y - 1, 2, 2)

	End Method
End Type
'#End Region

'#Region Baddies


'#End Region

'#End Region

'#Region Custom Functions
Function Make_Images()
	SetClsColor 255, 255, 255
	AutoMidHandle(True)
	SetBlend ALPHABLEND
	'Make the ShotGun Ammo Box and save it to memory
	Cls
	SetColor 0, 0, 0
	DrawRect 0, 0, 26, 14
	SetColor 254, 254, 254
	SetScale 1, .9
	DrawText "=s=", 0, 0
	setscale 1,1
	iShotGunAmmo:TImage = CreateImage(27, 15, 1, DYNAMICIMAGE | MASKEDIMAGE)
	GrabImage iShotGunAmmo, 0, 0
	'Make the MachineGun Ammo Box and save it to memory
	Cls
	SetColor 0, 0, 0
	DrawRect 0, 0, 30, 14
	SetColor 254, 254, 254
	SetScale.9, .9
	DrawText "=mg=", 0, 0
	setscale 1,1
	iMiniGunAmmo:TImage = CreateImage(31, 15, 1, DYNAMICIMAGE | MASKEDIMAGE)
	GrabImage iMiniGunAmmo, 0, 0
	SetClsColor 0, 0, 0

	'Make the ShotGun and save it to memory
	Cls
	SetColor 1, 1, 1
	DrawRect 0, 0, 4, 10
	DrawRect 4, 0, 12, 4
	SetColor 255, 255, 255
	iShotGun:TImage = CreateImage(16, 13, 1, DYNAMICIMAGE | MASKEDIMAGE)
	GrabImage iShotGun, 0, 0

	'Make the MachineGun and save it to memory
	Cls
	SetColor 1, 1, 1
	DrawRect 0, 0, 14, 16
	DrawRect 8, 0, 34, 12
	SetColor 245, 245, 245
	DrawLine 16, 4, 40, 4
	iMiniGun:TImage = CreateImage(47, 17, 1, DYNAMICIMAGE | MASKEDIMAGE)
	GrabImage iMiniGun, 0, 0
	SetClsColor 255, 255, 255

End Function
Function OutofBounds:Int(x:Float, y:Float)
	If x &lt; 0 Return True
	If Y &lt; 0 Return True
	If x &gt; 640 Return True
	If Y &gt; 400 Return True
	Return False
End Function

Function Collision:Byte(x:Float, y:Float, collidewith_x:Float, collidewith_y:Float, xsize:Float = 16, ysize:Float = 16)
	If Abs(x - collidewith_x) &lt; xsize And Abs(y - collidewith_y) &lt; ysize Then Return True
	Return False
End Function

Function makepowerups()
	For Local gg:Int = 1 To 10
		tPickup.make(Rand(640), Rand(400), Rand(0, 3))
	Next
End Function

Function UpdateInstances()
	For Local pkup:tPickup = EachIn tPickup.List
		pkup.Update()
	Next
	For Local plyr:tPlayer = EachIn tPlayer.List
		plyr.Update()
	Next
	For Local shot:tShot = EachIn tShot.List
		shot.Update()
	Next
	
End Function

Function DrawHud()
	'Draw Crosshair
	SetBlend ALPHABLEND
	SetColor 0, 0, 0
	DrawLine 0, 400, 640, 400


End Function
'#End Region

'#Region Start Game
HideMouse()
Graphics 640, 480
Make_Images()
SetBlend LIGHTBLEND
MoveMouse 320, 180
New tPlayer
'#End Region 

'#Region Main Loop
Repeat
	Cls
	UpdateInstances()
	DrawHud()
	If KeyHit(KEY_SPACE) Then makepowerups()
	Flip
	
Until KeyHit(KEY_ESCAPE) Or AppTerminate()
'#End Region
</textarea><br><br>Now to show you how I go to the mouse wheel:<br><br>in Method Control:<br><pre class=code>
	Method Control()		
		'Move
		If KeyDown(KEY_W) And y &gt; 2 y:-2
		If KeyDown(KEY_A) And x &gt; 2 x:-2
		If KeyDown(KEY_S) And y &lt; 390 y:+2
		If KeyDown(KEY_D) And x &lt; 638 x:+2
		'Shoot
		If MouseDown(1) And CurrentWeapon.ShotDelay &lt;= 0 Then CurrentWeapon.Shoot(x, y)
		'Reload
		If KeyHit(KEY_R) CurrentWeapon.Reload(90)
		'ChangeWeapon
		If KeyHit(KEY_1) Then ChangeWeapon(PISTOL_ID)
		If KeyHit(KEY_2) Then ChangeWeapon(SHOTGUN_ID)
		If KeyHit(KEY_3) Then ChangeWeapon(MINIGUN_ID)
		If MouseZ() &lt;&gt; LastMouseZ
			If MouseZ() &gt; LastMouseZ
				GetNextWeapon()
				LastMouseZ = MouseZ()
			End If
			If MouseZ() &lt; LastMouseZ
				GetPrevWeapon()
				LastMouseZ = MouseZ()
			End If
		End If
					

	End Method
</pre><br><br>Here are the two methods called depending on the mouse wheel change:<br><pre class=code>
	Method GetNextWeapon()
		Local LastID:Int = CurrentWeapon.WepID
		For Local IDSearch:Int = CurrentWeapon.WepID To 9 Step 1
			ChangeWeapon(IDSearch)
			If CurrentWeapon.WepID &lt;&gt; LastID Then Return
		Next

	End Method
	
	Method GetPrevWeapon()
		Local LastID:Int = CurrentWeapon.WepID
		For Local IDSearch:Int = CurrentWeapon.WepID To 0 Step - 1
			ChangeWeapon(IDSearch)
			If CurrentWeapon.WepID &lt;&gt; LastID Then Return
		Next
		
	End Method
</pre><br><br>As you can see, I move through a list of say, 10 weapons, and since the Change Weapon Method will only change a weapon if the weapon.hasthis = true then bingo I will always change to the next weapon.  This is helpful if I only have weapons 1 and 5 in my position for example.  The problem is that now I return Null weapons with the new getweapon method.  Previously I was assigning the weapon info in the tpickup object, but I found getweapon() to be so cool and use less redundant code that was in the tpickup object.  <br><br>Now a workaround is to default the Wep_ID to pistol so I'll always return a pistol if in the For..next loop I can't find a weapon (until the loop finds the next weapon in the player's inventory)<br><br>The funny thing is, if I'm using an assert to help debugging I can't force the weapon to be a pistol because the assert will close the application.  While I think using asserts are a great idea, in this case it just causes problems.  I was wondering though if you had another solution, or if you recommend I keep it this way.<br><br>EDIT: Also, I'd like to point out I've made massive improvements to the TPlayer code as a whole!  You can also copy/paste this into BLIDE and use the parser and Fold all and everything is neatly organized into regions, etc. <br><br></td></tr></table><br>
<a name="990068"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am going to throw a curve to you regarding lists. I hope I can explain it clear enough for you.<br><br>when you create an object and add it to a list, there is a way to access it directly from there. First a Tlist is just a link list. there are ways to access them directly and remove them the same way. if you know how link lists work, you know that every object is connected to its previous and next object by its link.<br>when you add an an object to a list it returns the direct position in the list and it is done like this:<br><pre class=code>
local list:Tlist = new Tlist
type Tcar
   field x:float
   field y:float
   field link :Tlink ' this is going to be used to acces the object directly from the list.
end type

local car:Tcar = new Tcar
car.x = 10
car y = 20
car.link = list.addlast(car) ' this stores car in the list and assigns its positon in the list to the link variable.
local van:Tcar = new Tcar
van.x = 30
van.y = 50
van.link = list.addlast(van) ' this stores van in the list and assigns its position in the list to the link variable.
</pre><br>at this point you know that car knows its position in the list because it's saved in its link variable. if you need to access the next item in the list you can do it like this:<br><pre class=code>
local link:Tlink  = car.link.nextlink()
if link &lt;&gt; null 
    van = Tcar(link.value())
else
    went past the last item in the list.
    process accordingly
    you can wrap it around like this:
    van = Tcar(list.first())
endif
</pre><br>if there is an object in the list before the current one, it can be acces with link.prevlink()<br><br>if you want to remove the objects from the list you can do it like this:<br>car.link.remove()<br>van.link.remove()<br>Note I am just giving you and alternative so you won't need to go through the whole list just to access the next object also for this to work the objects have to be in the same list but its obvious. correct? <br><br></td></tr></table><br>
<a name="990070"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ooh!  I think that using a list would be most beneficial for the mouse-wheel function to work properly.  I think using nextlink and assuming something like prevlink would make a fast and easy way to cycle through the weapons when the mouseZ changes.   I'm thinking I could even put the weapon list inside tplayer too.  Thanks for the response!  I've learned a lot more than I ever thought I would with this thread and I appreciate it! <br><br></td></tr></table><br>
<a name="990071"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br> I think using nextlink and assuming something like prevlink would make a fast and easy way to cycle through the weapons when the mouseZ changes.<br> <br></div><br>that's why I posted. I am glad you got it. <br><br></td></tr></table><br>
<a name="990073"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> There's only a minor problem I'm dealing with using a list to cycle through the weapons.  I'd like to make sure that Minigun is always the 3rd item in the list, even if the 2nd item isn't obtained yet.  To do this, I have the objects created when the player is, so they're all assigned to the list properly.  However, then I run into another problem where if I just use NextLink() I end up pulling weapons I haven't picked up yet because their hasthis value is set to false. <br><br></td></tr></table><br>
<a name="990074"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ahh, So I'm doing it this way:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
	Method GetNextWeapon()
		Local NextWeaponLink:TLink = CurrentWeapon.Link.NextLink()
			If NextWeaponLink &lt;&gt; Null
				CurrentWeapon = tWeapon(NextWeaponLink.Value())
				If Not CurrentWeapon.HasThis Then GetNextWeapon()
			Else
				CurrentWeapon = tWeapon(list.first())
			End If
	End Method
	
	Method GetPrevWeapon()
		Local PrevWeaponLink:TLink = CurrentWeapon.Link.PrevLink()
			If PrevWeaponLink &lt;&gt; Null
				CurrentWeapon = tWeapon(PrevWeaponLink.Value())
				If Not CurrentWeapon.HasThis Then GetPrevWeapon()
			Else
				CurrentWeapon = tWeapon(list.last())

			End If
	End Method
</textarea><br><br>Only problem is I get an unhandled exception error with "CurrentWeapon = tweapon(list.last())" (or list.first respectivly) <br><br></td></tr></table><br>
<a name="990097"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> just make sure there are weapons in the list before accessing items in the list.<br><pre class=code>
if list.IsEmpty() return
</pre> <br><br></td></tr></table><br>
<a name="990100"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#23">[#23]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> The funny thing is, if I'm using an assert to help debugging I can't force the weapon to be a pistol because the assert will close the application. <br></div> Try replacing the Assert and Return Null with Return Pistol. Then if it's ask for a weapon it can't find, it'll always return the pistol. <br><br></td></tr></table><br>
<a name="990172"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#24">[#24]</a></td></tr></table></td></tr><tr ><td class="posttext"> Czar, then it returns a pistol when it shouldn't.<br>Jesse the list shouldn't be empty because it is generated right when the player is..<br><br>Problem solved this way:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
	Method GetNextWeapon()
		Local NextWeaponLink:TLink = CurrentWeapon.Link.NextLink()
			If NextWeaponLink &lt;&gt; Null
				CurrentWeapon = tWeapon(NextWeaponLink.Value())
				Print currentweapon.WepID
				If Not CurrentWeapon.HasThis Then GetNextWeapon()
			Else
				GetPrevWeapon()
			End If
	End Method
	
	Method GetPrevWeapon()
		Local PrevWeaponLink:TLink = CurrentWeapon.Link.PrevLink()
			If PrevWeaponLink &lt;&gt; Null
				CurrentWeapon = tWeapon(PrevWeaponLink.Value())
				If Not CurrentWeapon.HasThis Then GetPrevWeapon()
			Else
				GetNextWeapon()
				

			End If
	End Method
</textarea><br><br>Except it's really sensitive.  Also, the reason why the list was null is because tweapon.list is completely different than currentweapon.list as currentweapon has a list while tweapon does not. <br><br></td></tr></table><br>
<a name="990180"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#25">[#25]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok I fixed it!! Sorry for posting so quickly..<br><br>I've figured out how to get the first weapon and last weapon correctly, and I also learned that I need a delay to check the mouseZ change otherwise it is just way too sensitive.<br><br>Works PERFECT:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
	Method GetNextWeapon()
		Local NextWeaponLink:TLink = CurrentWeapon.Link.NextLink()
		Local FirstWeaponLink:TLink = CurrentWeapon.List.FirstLink()
			If NextWeaponLink &lt;&gt; Null
				CurrentWeapon = tWeapon(NextWeaponLink.Value())
				If Not CurrentWeapon.HasThis Then GetNextWeapon()
			Else
				CurrentWeapon = tWeapon(FirstWeaponLink.Value())
				If Not CurrentWeapon.HasThis Then GetNextWeapon()
			End If
	End Method
	
	Method GetPrevWeapon()
		Local LastWeaponLink:TLink = CurrentWeapon.List.LastLink()
		Local PrevWeaponLink:TLink = CurrentWeapon.Link.PrevLink()
			If PrevWeaponLink &lt;&gt; Null
				CurrentWeapon = tWeapon(PrevWeaponLink.Value())
				If Not CurrentWeapon.HasThis Then GetPrevWeapon
				Return
			Else
				CurrentWeapon = tWeapon(LastWeaponLink.Value())			
				If Not CurrentWeapon.HasThis Then GetPrevWeapon
			End If
	End Method
</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
