<!DOCTYPE html><html lang="en" ><head ><title >Finding an object in a list?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Finding an object in a list?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >Finding an object in a list?</a><br><br>
<a name="533158"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >EOF</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> How can I check for the existance of an object in a list without having to iterate through the list?<br>I'm after a ListFindObject() type of function.<br>ListFindLink() is closest but how do I get the 'fruit' type object?<br><br>Quick example:<br><pre class=code>Global mylist:TList=CreateList()

Type fruit
	Field name$
	Method New()
		ListAddLast mylist,Self
	End Method
End Type

Local f:fruit

f=New fruit ; f.name$="apple"
f=New fruit ; f.name$="banana"
f=New fruit ; f.name$="pear"
f=New fruit ; f.name$="orange"
f=New fruit ; f.name$="peach"

f=ListFindObject("pear") ????????????????
If f ListRemove mylist,f</pre> <br><br></td></tr></table><br>
<a name="533162"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >klepto2</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Its not very effective but you could do it this way.<br><br><pre class=code>

Global mylist:TList=CreateList()

Type fruit
	Field name$
	
	Function FindObject:fruit(_Name:String)
		For t:fruit = EachIn mylist
			If t.name = _name Then 
				Return t
				Exit
			EndIf
			Next
			Return
	End Function
	
	Method New()
		ListAddLast mylist,Self
	End Method
	
End Type

Local f:fruit

f=New fruit ; f.name$="apple"
f=New fruit ; f.name$="banana"
f=New fruit ; f.name$="pear"
f=New fruit ; f.name$="orange"
f=New fruit ; f.name$="peach"

f = Null ' Important

f=fruit.FindObject("pear") 

If f ListRemove mylist,f

For f = EachIn mylist

Print f.name

Next

</pre> <br><br></td></tr></table><br>
<a name="533174"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >EOF</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have tried playing with TLink() but always get a Null object ..<br><pre class=code>Global mylist:TList=CreateList()

Type fruit
	Field name$
End Type

Local f:fruit

f=New fruit ; f.name$="apple"  ; ListAddLast mylist,f
f=New fruit ; f.name$="banana" ; ListAddLast mylist,f
f=New fruit ; f.name$="pear"   ; ListAddLast mylist,f
f=New fruit ; f.name$="orange" ; ListAddLast mylist,f
f=New fruit ; f.name$="peach"  ; ListAddLast mylist,f
'DebugStop

f=FindFruit("pear")
If f Print f.name$ Else Print "not found"
End

Function FindFruit:fruit(value:Object)
	Local t:TLink=ListFindLink(mylist,value)
	If t Return fruit(t.value())
End Function</pre> <br><br></td></tr></table><br>
<a name="533185"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >kyoryu</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Unfortunately, you probably can't.  That's just a limitation of linked lists.  I plan on making a hash-map module at some point in the near future, just have to get around to it. <br><br></td></tr></table><br>
<a name="533191"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >klepto2</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Another way to find an object is to give the object a special id and then use this.<br><br><pre class=code>

Global mylist:TList=CreateList()

Type fruit
	Field name$
	Field id = mylist.Count()
End Type

Local f:fruit

f=New fruit ; f.name$="apple"  ; ListAddLast mylist,f
f=New fruit ; f.name$="banana" ; ListAddLast mylist,f
f=New fruit ; f.name$="pear"   ; ListAddLast mylist,f
f=New fruit ; f.name$="orange" ; ListAddLast mylist,f
f=New fruit ; f.name$="peach"  ; ListAddLast mylist,f

f = fruit(mylist.Valueatindex(2))
If f Print f.name$ Else Print "not found"
End

</pre> <br><br></td></tr></table><br>
<a name="533810"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >EOF</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks you two. I've been getting confused by object and value it seems.<br>Looks like I do need to iterate through the list ..<br><pre class=code>
Global mylist:TList=CreateList()

Type fruit
	Field name$
End Type

Global flag
Local f:fruit

f=New fruit ; f.name$="apple"  ; ListAddLast mylist,f
f=New fruit ; f.name$="banana" ; ListAddLast mylist,f
f=New fruit ; f.name$="pear"   ; ListAddLast mylist,f
f=New fruit ; f.name$="orange" ; ListAddLast mylist,f
f=New fruit ; f.name$="peach"  ; ListAddLast mylist,f
'DebugStop

f=FindFruit("pear")
If f ListRemove mylist,f

For f=EachIn mylist
	Print f.name$
Next

FlushMem
End

Function FindFruit:fruit(name$)
	For Local fr:fruit=EachIn mylist
		If fr.name$=name$ Return fr
	Next
	Return Null
End Function
</pre> <br><br></td></tr></table><br>
<a name="533856"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tibit</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> You can always give an object an unique handle. See my bottom example. This does not replace the need to find objects by field but in many cases you can save both time and code by having direct links to objects;types you use often.<br><br>When you used MyList.FindLink("Pear") you gave the method an String and it cannot convert that to an object. In other words that string was not found in the list. In the example below I add strings to the list. You could do the same with 'real' objects also. Depends on what you really want to do in the end.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict
Global MyList:TList
	MyList = CreateList()
	MyList.AddLast("Pear")
	MyList.AddLast("Apple")
	MyList.AddLast("Banana")

Print "--LIST--"
For Local Fruit$ = EachIn MyList
	Print Fruit
Next

'Let's say we want to add an Orange after Pear
Print "Insert Orange After Pear"
MyList.InsertAfterLink("Orange" , MyList.FindLink("Pear") )

Print "Remove Apple"
MyList.FindLink("Apple").Remove()

Print "--LIST--"
For Local Fruit$ = EachIn MyList
	Print Fruit
Next</textarea><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Global mylist:TList=CreateList()

Type fruit
	Field name$
	Method New() ListAddLast mylist,Self EndMethod
End Type

Global flag
Local f:fruit

Apple:Fruit		=	New fruit ; f.name$="apple" 
Banana:Fruit	=	New fruit ; f.name$="banana" 
Pear:Fruit		=	New fruit ; f.name$="pear"  
Orange:Fruit	=	New fruit ; f.name$="orange" 
Peach:Fruit		=	New fruit ; f.name$="peach" 


Print "This fruit is a "+Pear.Name
'Finds and remove pear
mylist.Remove( Pear )

For f=EachIn mylist
	Print f.name$
Next
</textarea><br><br>Hope those examples shed some light on Lists =) <br><br></td></tr></table><br>
<a name="533969"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sweenie</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here is my version of a hashtable.<br>I'm sure someone can make a better one, but it has worked so far for me...<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Type TListNode
 Field NextNode:TListNode
 Field PrevNode:TListNode
 Field Key:String
 Field Value:Object

 'Method Delete()
  'Print "Key "+Key + " says: -"+Chr(34)+"yeah, I'm free!"+Chr(34)
 'End Method

 Method FindKey:Int(Key:String)
  Local CurrNode:TListNode = Self
  Repeat
   If CurrNode.Key = Key Then Return True
   If CurrNode.NextNode = Null Then Exit
   CurrNode = CurrNode.NextNode
  Forever
  Return False
 End Method

 Method RemoveKey:Int(Key:String)
  Local CurrNode:TListNode = Self
  Repeat

   If CurrNode.Key = Key Then 
	If Not(CurrNode.PrevNode = Null) Then 
     CurrNode.PrevNode.NextNode = CurrNode.NextNode
	 If Not(CurrNode.NextNode=Null) Then CurrNode.NextNode.PrevNode = CurrNode.PrevNode
	 FlushMem()
    EndIf

    Return True
   EndIf	

   If CurrNode.NextNode = Null Then Exit
   CurrNode = CurrNode.NextNode
  Forever
  Return False
 End Method

 Method CutPrevNodeLinks()
  Local CurrNode:TListNode = Self
  'Simply go through all nodes and cut the prevnode link...
  Repeat
   CurrNode.PrevNode = Null
   If CurrNode.NextNode = Null Then Exit
   CurrNode = CurrNode.NextNode
  Forever
  
 End Method

 Method GetValueByKey:Object(Key:String)
  Local CurrNode:TListNode = Self
  Repeat
   If CurrNode.Key = Key Then Return CurrNode.Value
   If CurrNode.NextNode = Null Then Exit
   CurrNode = CurrNode.NextNode
  Forever
  Return Null
 End Method

 Method Add:Int(Key:String,Value:Object)

  Local CurrNode:TListNode = Self

   Repeat
    If CurrNode.Key = Key Then Return False
    If CurrNode.NextNode = Null Then Exit
    CurrNode = CurrNode.NextNode
   Forever

  CurrNode.NextNode = New TListNode
  CurrNode.NextNode.Key = Key
  CurrNode.NextNode.Value = Value
  CurrNode.NextNode.PrevNode = CurrNode

  Return True

 End Method

End Type

Type HashTable

 Field Buckets:Int		  ' The nr of rows in the array
 Field HTable:TListNode[]  

 Method New()
  Buckets = 4093 ' Prime...
  HTable = HTable[..Buckets]
  ClearTable()  
 End Method

 Method ClearTable()
  For Local i=0 To Buckets-1
   HTable[i] = Null
  Next
 End Method

 Function Create:HashTable(NrOfBuckets:Int = 4093)
  Local NewHT:HashTable = New HashTable
  NewHT.Buckets = NrOfBuckets
  NewHT.HTable = NewHT.HTable[..NrOfBuckets]
  NewHT.ClearTable()
  Return NewHT
 End Function

 Method Add:Int(Key:String,Value:Object)

  FlushMem()

  Local hash = HashValue(Key)

  If HTable[hash] = Null Then	'Ok, no collision. Just create a node here...
   HTable[hash] = New TListNode
   HTable[hash].Key = Key
   HTable[hash].Value = Value
   Return True
  Else 'Collision, make sure the key doesn't exist already. If it doesn't add a new node at the end...
    Return HTable[hash].Add(Key,Value) 
  EndIf	

 End Method

 Method GetValue:Object(Key:String)
  Local hash = HashValue(Key)
  If HTable[hash] = Null Then Return Null
  Return HTable[hash].GetValueByKey(Key)
 End Method

 Method Remove:Int(Key:String)
  Local hash = HashValue(Key)
  If HTable[hash] = Null Then Return Null
  If HTable[hash].Key = Key Then 'Remove Rootnode
   HTable[hash] = HTable[hash].NextNode
   FlushMem()
  Else
   'Print "Removed Sub Node"
   Return HTable[hash].RemoveKey(Key)
  EndIf
 End Method

 Method RemoveAllKeys()
  For Local i = 0 To Buckets-1
	If Not(HTable[i]=Null) Then 
	 HTable[i].CutPrevNodeLinks()
     HTable[i] = Null
     FlushMem() ' This should release all nodes
    EndIf
  Next
 End Method


 Method ContainsKey:Int(Key:String)
  Local hash = HashValue(Key)
  If HTable[hash] = Null Then Return False
  Return HTable[hash].FindKey(Key) 
 End Method

 Method HashValue:Int(Key:String)
  'Return Key Mod Buckets
  Local hash
  For Local i=0 To Key.Length-1
   hash = hash*613 + Key[i]
  Next
  
  If hash&gt;=0 Then Return hash Mod Buckets Else Return -hash Mod Buckets

 End Method

End Type

</textarea><br><br>Use it like this...<br><pre class=code>

Local MyTable:HashTable = HashTable.Create(500)

MyTable.Add("Apple","I'm an apple!")
MyTable.Add("Pear","I'm a pear!")
MyTable.Add("Orange","I'm an orange!")

Print String(MyTable.GetValue("Pear"))

If MyTable.ContainsKey("Peach")=False then Print "No peach in the table"

</pre><br><br>Each key must be unique and if you try to add one  that already exist, it will just skip adding it. <br><br></td></tr></table><br>
<a name="533987"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Azathoth</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Why not just give your object a compare method? FindLink will use it.<br><br><pre class=code>
Type TMe
	Field name:String
	
	Function Create:TMe(n:String)
		Local x:TMe=New TMe
		x.name=n
		Return x
	EndFunction
	
	Method ToString:String()
		Return name
	EndMethod
	
	Method Compare:Int(o:Object)
		other:TMe=TMe(o)
		If other
			If name=other.name
				Return 0
			ElseIf name&lt;other.name
				Return -1
			ElseIf name&gt;other.name
				Return 1
			EndIf
		EndIf
	EndMethod
EndType


Local l:TList

l=New TList

l.AddLast(TMe.Create("AB"))
l.AddLast(TMe.Create("BB"))
l.AddLast(TMe.Create("BC"))

link:TLink=l.FindLink(TMe.Create("BB"))

DebugLog link.value().tostring()
</pre> <br><br></td></tr></table><br>
<a name="534213"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >EOF</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> This opens up a whole new world. Its just a matter of knowing how to tackle them. Waves first example will do for me since I only need to have a list of lines read from a file.<br><br>Thanks everyone. Good stuff indeed. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
