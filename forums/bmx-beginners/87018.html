<!DOCTYPE html><html lang="en" ><head ><title >Shape Invaderz!</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Shape Invaderz!</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >Shape Invaderz!</a><br><br>
<a name="986209"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> EDIT:<br>This is an OLDER VERSION<br>Please review the LATEST VERSION <a href="http://www.gamedev.net/community/gds/viewentry.asp?projectid=548722" target="_blank">Here</a><br><br>Just finished a Space Invaders clone.<br><br>My primary focus is memorizing blitzmax code, especially OOP handling so I figured this would be a good exercise.<br><br>I programmed this in 3 hours.  It could have been done a lot faster but I had to reference code and had to tweak collisions, etc<br><br>You can just copy/paste the code and play, no other downloads are required, please give me feedback!<br><br>Highlights:<br>The code uses Superstrict!<br>The code uses different gamestates to handle titlescreen / game play / fail or win/ pause screen<br>The code uses TLists<br>The code uses polygon graphics thanks to Indiepath<br><br>Wishes:<br>I need to learn how to update the screen based on hertz instead of pass-thru's <br>I need to learn how to make a highscore list<br><br>Feedback welcome!<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

SuperStrict

Graphics 320, 260

'Create enemy lists
Global objectlist:TList = CreateList()
Global enemylist:TList = CreateList()
Global playerlist:TList = CreateList()
Global bulletlist:TList = CreateList()
Global playershots:TList = CreateList()
Global particlelist:TList = CreateList()
Global direction:Byte
Global GameState:Byte
Global Level:Int
Global PS:Int
Global Score:Int

Const MOVELEFT:Byte = 0
Const MOVERIGHT:Byte = 1
Const STATE_TITLE:Byte = 0
Const STATE_PLAY:Byte = 1
Const STATE_PAUSE:Byte = 2
Const STATE_WIN:Byte = 3
Const STATE_FAIL:Byte = 4


Function DrawGeom(x:Float, y:Float, sides:Float, length:Float, angle:Float, r:Double = 255, g:Double = 255, b:Double = 255, glow:Byte = True)

	Local aStep# = 360 / sides
	Length:Float = (length / 2) / Sin(aStep:Float / 2) 'Calculate the correct length of a side 			
	For Local a:Byte = 0 To sides - 1
		Local x1:Float = x:Float - (Sin(angle + (aStep * a)) * length)
		Local y1:Float = y:Float - (Cos(angle + (aStep * a)) * length)
		Local x2:Float = x:Float - (Sin(angle + (aStep * (a + 1))) * length)
		Local y2:Float = y:Float - (Cos(angle + (aStep * (a + 1))) * length)
		If glow = True
			SetColor r / 4, g / 4, b / 4
			DrawOval x1 - 5, y1 - 5, 11, 11							' Draw Circle at Vertex
		End If
		SetColor r, g, b
		DrawLine x1, y1, x2, y2									' Draw Connecting Lines
	Next
	
End Function
Function drawtri(x:Double, y:Double, x2:Double, y2:Double)
	'Find coordinates
	Local xPoint:Double = x2 / 2
	x2:+x
	y2:+y
	xPoint:+x
	'Draw base
	DrawLine(x, y2, x2, y2)
	'left corner
	DrawLine(x, y2, xPoint, y)
	'right corner
	DrawLine(xPoint, y, x2, y2)
	
	
End Function

'I'm pretty sure this is all I need for the game object lists, this is to make an update one pass instead of many
Type tGameobject
	Field x:Float, y:Float, xv:Float, yv:Float, hp:Float
	Method update() Abstract
End Type

Type tenemy Extends tGameobject
	Field enemytype:Byte 'Determines the color and shape
	Field r:Double, g:Double, b:Double 'Colorsettings 
	Field sides:Double 'Sides settings
	Field Level:Int
	
	Function make(x:Float, y:Float, hp:Float = 1, enemytype:Byte = 1, Level:Int)
		Local obj:tenemy = New tenemy
		obj.x = x
		obj.y = y
		obj.hp = hp
		obj.Level = Level
		obj.xv = Level *.1
		If obj.xv &gt; 2 obj.xv = 2
		obj.enemytype = enemytype
		'Depending on the type, we confugre it's shape and color
		Select enemytype
			Case 1
				obj.r = 255
				obj.g = 0
				obj.b = 0
				obj.sides = 4
				
			Case 2
				obj.r = 0
				obj.g = 255
				obj.b = 0
				obj.sides = 3
			
			Case 3
				obj.r = 0
				obj.g = 0
				obj.b = 255
				obj.sides = 5
		End Select
		'Now lets add it to two lists
		objectlist.AddLast obj:tenemy
		enemylist.AddLast obj:tenemy		
	End Function
	Method update()
		If direction = MOVELEFT x:-xv
		If direction = MOVERIGHT x:+xv
		
		If x &lt; 0
			direction = MOVERIGHT
			For Local obj:tenemy = EachIn enemylist
				obj.y:+4
			Next
		End If

		If x &gt; 320
			direction = MOVELEFT
			For Local obj:tenemy = EachIn enemylist
				obj.y:+4
			Next
		End If

		DrawGeom(x, y, sides, 10, 0, r, g, b, False)
		' make it shoot
		Local R:Int = Rnd(1000)
		If R &lt; Level + 1 tbullet.make(x + 8, y)
	End Method
End Type

Type tplayer Extends tGameobject
	Field shotdelay:Float
	Function make()
		Local obj:tplayer = New tplayer
		obj.x = 320 / 2
		obj.y = 222
		obj.hp = 1
		objectlist.AddLast obj:tplayer
		playerlist.AddLast obj:tplayer
	End Function
	Method update()
		shotdelay:+1
		If KeyDown(KEY_RIGHT) x:+3
		If KeyDown(KEY_LEFT) x:-3
		If KeyHit(KEY_Z) And shotdelay &gt; 5
			If CountList(playershots) &lt; 2 tplayershot.make(x + 7, y) ;shotdelay = 0'shoot
		End If
		If x &lt; 0 x = 0
		If x &gt; 304 x = 304
		
		SetColor 255, 255, 255
		drawtri(x, y, 16, 16)
	End Method
End Type

Type tbullet Extends tGameobject
	Function make(x:Float, y:Float)
		Local obj:tbullet = New tbullet
		obj.x = x
		obj.y = y
		obj.yv = 3
		objectlist.AddLast obj:tbullet
		bulletlist.AddLast obj:tbullet
	End Function
	Method update()
		y:+yv
		If y &gt; 244 Then objectlist.Remove(Self) ;bulletlist.Remove(Self)
		SetColor 255, 255, 0
		DrawOval x, y, 4, 4
		For Local obj:tplayer = EachIn playerlist
			If Abs(obj.x - x + 6) &lt; 8 And Abs(obj.y - y) &lt; 2
				objectlist.Remove(Self)
				bulletlist.Remove(Self)
				GameState = STATE_FAIL
				
			End If
		Next
	End Method
End Type

Type tplayershot Extends tGameobject
	Function make(x:Float, y:Float)
		Local obj:tplayershot = New tplayershot
		obj.x = x
		obj.y = y
		obj.yv = -8
		objectlist.AddLast obj:tplayershot
		playershots.AddLast obj:tplayershot
	End Function
	Method update()
		y:+yv
		If y &lt; - 4 Then objectlist.Remove(Self) ; playershots.Remove(Self)
		SetColor 0, 255, 255
		DrawOval x, y, 2, 4
		For Local obj:tenemy = EachIn enemylist
			If Abs(obj.x - x) &lt; 10 And Abs(obj.y - y) &lt; 12
				If obj.enemytype = 2 Score:+1
				If obj.enemytype = 1 Score:+15
				If obj.enemytype = 3 Score:+70
				objectlist.Remove(obj)
				enemylist.Remove(obj)
				objectlist.Remove(Self)
				playershots.Remove(Self)
			End If
		Next
	End Method
End Type


Function MakeGame(Level:Int)
	tplayer.make()
	For Local N:Double = 2 To 14
		tenemy.make(N * 20, 16 * 2,, 3, Level)
	Next
	For Local N:Double = 2 To 14
		tenemy.make(N * 20, 16 * 4,, 1, Level)
	Next
	For Local N:Double = 2 To 14
		tenemy.make(N * 20, 16 * 6,, 2, Level)
	Next
End Function
Function ClearEverything()
	For Local obj:tGameobject = EachIn objectlist
		objectlist.Remove(obj)

	Next
	For Local obj:tenemy = EachIn enemylist
		enemylist.remove(obj)
	Next
	For Local obj:tplayer = EachIn playerlist
		playerlist.remove(obj)
	Next
	For Local obj:tbullet = EachIn bulletlist
		bulletlist.remove(obj)
	Next
	For Local obj:tplayershot = EachIn playershots
		playershots.remove(obj)
	Next
	'For Local obj:tplayer = EachIn playerlist
	'	playerlist.remove(obj)
	'Next
End Function
Repeat
	Cls
	Select GameState
		Case STATE_TITLE
			DrawText "Shape Invaderz", 105, 20
			DrawText "ARROW KEYS", 30, 90
			DrawText "Z KEY", 30, 110
			DrawText "P KEY", 30, 130
			DrawText ". . .", 140, 90
			DrawText ". . .", 140, 110
			DrawText ". . .", 140, 130
			DrawText "Move Ship", 200, 90
			DrawText "Shoot", 200, 110
			DrawText "Pause", 200, 130
			DrawText "Press SPACE to play or ESC to Exit", 25, 220
			If KeyHit(KEY_SPACE)
				Level = 1
				SCORE = 0
				MakeGame(Level)
				GameState = STATE_PLAY
			End If
		Case STATE_PLAY
			For Local obj:tGameobject = EachIn objectlist
				obj.update()
				If KeyHit (KEY_P) GameState = STATE_PAUSE					
			Next
			SetColor 0, 0, 200
			DrawLine 0, 240, 320, 240
			SetColor 255, 255, 255
			DrawText "Level:" + String(Level), 10, 244
			DrawText "Score:" + String(Score), 200, 244
			If CountList(enemylist:TList) &lt; 1
				GameState = STATE_WIN
			End If
		Case STATE_PAUSE
			PS:+1
			If PS &gt; 320 PS = -320
			DrawText "Game PAUSED, Press SPACE to Continue", PS, 210
				If KeyHit(KEY_SPACE) GameState = STATE_PLAY
		Case STATE_WIN
			DrawText "WIN!", 145, 20
			DrawText "You Have owned Level " + String(Level) + "!!!!!!!!!!!!", 30, 60
			DrawText "Press SPACE to CONTINUE or ESC to Exit", 06, 210
			If KeyHit(KEY_SPACE)
				ClearEverything()
				Level:+1
				MakeGame(Level)
				GameState = STATE_PLAY
				
			End If
			If KeyHit(KEY_ESCAPE)
				ClearEverything()
				GameState = STATE_TITLE
			End If
		Case STATE_FAIL
			DrawText "FAIL!", 145, 20
			DrawText "You DIED on Level " + String(Level) + "!!!!!!!!!!!!", 30, 60
			DrawText "You Scored " + String(Score) + " Points FTW", 10, 90
			DrawText "Press SPACE to CONTINUE or ESC to Exit", 06, 210
			If KeyHit(KEY_SPACE)
				ClearEverything()
				Level = 1
				GameState = STATE_TITLE
				SetColor 255, 255, 255
			End If
		
	End Select
	Flip
Until KeyHit (KEY_ESCAPE)
</textarea> <br><br></td></tr></table><br>
<a name="986215"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_Skully</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice :) <br><br></td></tr></table><br>
<a name="986225"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Very nice!!! I can only think of a few nitpicks ;)<br><br>1. Don't bother with bytes (or shorts) unless you have a reason to. You might think you're saving memory, but it'll be less than 1kb which is nothing, and they are actually slower than ints! Don't become paranoid though, it's very slight ;) But bytes and shorts are 8 and 16 bit respectively, which causes some minor inconvenience to a 32 bit processor, whereas 32 bit ints do not. (What happens on a 64 bit system, I'm not sure)<br><br>2. If you're going for OOP, don't use countlist(list), use list.count() :P<br><br>3. The colours for SetColor are integers, not floats/doubles. You are being needlessly rational :P (sorry maths joke) Make them ints.. or you could put them as bytes if you want to, as colours are in the range 0-255.<br><br>4. You are not being consistant with your capitalisation. Some type names have capitals, others are lower case.<br><br>5. Personally, I'd put the list declarations within the appropriate types themselves. I find it makes things better organised.<br><br>6. You only need to declare the type of a variable once. objectlist.AddLast obj:tplayer  - the :tplayer is unnecessary, unwise even, as it already knows what kind obj is. If you've done it to remember that obj is a player in this case, it's better to rename obj to player.<br><br>7. Try using the Blide editor, you can download a free version. I find it easier to code in, than the editor that comes with BlitzMax.<br><br><br>If you're up for it, here is some advanced code to show a better way of removing items from lists! The problem with list.remove(o), is that it needs to scan through the list, until it finds the object in question. If it's at the end of the list, it will potentially have to traverse the entire list looking for it! This, needless to say, is not very efficient. Although, in practice, the performance impact probably isn't that much.<br><br>A better way nonetheless, is to store in each object a "link" to that object's position in the list. Then, when we need to remove it, we can pull it directly from the list. Here is the code.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict
Type TSomething
	Global all:TList = CreateList()
	Field me:TLink
	
	Field a:Int, b:Int
	
	'list management ------------------------------------
	Method New()
		me = all.AddLast(Self)
	End Method
	
	Method Create:TSomething(fun:Int, apple:Int)
		a = fun
		b = apple
		Return Self
	End Method
	
	Method die()
		me.Remove()
	End Method
	
	'output ---------------------------------------------
	Method speak()
		Print "A = " + a + ", B = " + b + "."
	End Method
	
	Function all_speak()
		For Local thing:TSomething = EachIn all
			thing.speak()
		Next
		print ""
	End Function
End Type

Local bob:TSomething = New TSomething.Create(5, 8)
Local alice:TSomething = New TSomething.Create(1, 1)
Local trudy:TSomething = New TSomething.Create(99999, 0)

TSomething.all_speak()

alice.die()
alice = null

TSomething.all_speak()
</textarea><br><br>One thing you may notice, is the unusal way I've made the types, by using a create method instead of a create/make function. I'm just showing off and it isn't relevent to the problem :D It isn't inherently better or worse than the way you have done it, it's just an alternative. Which you may or may not find more natural :)<br><br>The key thing is the me:TLink - this lets objects know where they are in the list. The New() method is a special built-in method, that you can add to. It is automatically ran when any object springs forth into existance, so I've put the code there to put it in the list.<br><br>When removing the object, it's important to make the distinction between truely deleting the object, or just removing it from the list. An object is only deleted, when no more variables "reference" it. If the object exists only in the list, removing it will delete it. In my example, they also exist as local variables, so I have to remember to Null alice after killing it(her).<br><br>I welcome comments from anybody about that method.<br><br>I hope I haven't confused you but given you some useful tips! <br><br></td></tr></table><br>
<a name="986231"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> @ Skully! Thanks :) I enjoyed making it<br><br>@Czar Flavius:<br><br>Thanks a million for nitpicking my code!  This is the only way I'm going to improve, so I really appreciate your time.<br><br>1. I didn't know that bytes would actually be slower!  My whole reasoning for using them was to speed things up.  I always thought Integers used more memory and I figured if I was dealing with numbers that were smaller than 1000 to store them as doubles. And true/falsies as Bytes!  With that knowledge I'll have to rethink my coding.  Too bad this bytes/double/int thing is a habit.  So I guess there's no reason to mess with bytes or doubles at all then?<br><br>2. Why is list.count better? I'm assuming it's faster?<br><br>3. Didnt know Setcolor were integers, thanks a million :)<br><br>4. Yeah I got tired of hitting the shift key while coding.  That was laziness, I'll try to be more consistent though as it DOES make it easier to read.<br><br>5.  I think you're on to something with putting list decelerations in the types themselves.  But if I wanted to declare them wouldn't I have to put them outside of the Type? Maybe one line above the type declaration?  I found myself scrolling to the top of the file to reference the list names a few times so I think this would be helpful in speeding the coding process up.<br><br>6. objectlist.addlast obj:tplayer could be player.addlast player instead, more readable!<br><br>7.  Oh yeah, I already am using BLIDE ;)<br><br><br>Finally, I your code snippet looks unfamiliar to me, I'm seeing some new codez!!  I'll have to study it for a bit (After I'm done updating my code to your suggestions!!!) tonight and apply that method.    I'll touch more on it once I'm ready.<br><br>Thanks again, I really appreciate the criticism as I want to improve my coding skills. <br><br></td></tr></table><br>
<a name="986232"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> 2. Why is list.count better? I'm assuming it's faster? <br></div><br>It'll be slightly faster (not that you will ever notice), but mainly because it is a call to the method on the Type, rather than a call to a procedural "wrapper" function.<br>Since you mentioned you are interested in OOP, The Czar simply points out that using list.count is an OOP style call  ;-) <br><br></td></tr></table><br>
<a name="986233"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> 1. The effect is not huge, so don't panic about it :) You use floats and doubles when you are dealing with decimal numbers (your height for example). If the values are always going to be integer, there is no point using them. Bytes are used mainly when interfacing with outside code. For example, the programming language C models strings using arrays of bytes, so if you want to include C code in your program, you might need bytes then. But forget about that for ages and ages.<br><br>5. It's as easy as this<br><pre class=code>Type tplayer Extends tGameobject
	Global playerlist:TList = CreateList()
	Field shotdelay:Float</pre> ;)<br><br>6. I'm not too sure what you mean by this. Is there a reason you have an object list for everything, and seperate lists for the player etc which you use together? <br><br></td></tr></table><br>
<a name="986236"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> *cough*<br><pre class=code>
Global playerlist:TList = New TList
</pre><br>*cough*<br><br>:-p <br><br></td></tr></table><br>
<a name="986237"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> New TList or Createlist() ? that is the question!<br><br>5. thanks :)<br><br>6.  The reason I had an object list for everything is so I could sweep through the entire list once on method.update() abstract<br><br>for instance:<br><pre class=code>
for local obj:tgameobject = eachin objectlist
       obj.update()
Next
</pre><br>instead of<br><pre class=code>
for local obj:tobject1 = eachin object1list
    obj.update()
Next
for local obj:tobject2 = eachin object2list
    obj.update()
Next
And for object3 and 4 and 5 and 6
</pre><br>Thus reducing redundancy and lines of code. <br><br></td></tr></table><br>
<a name="986238"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> If you wanted to be really clever, you could have a list of lists ;) <br><br></td></tr></table><br>
<a name="986240"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> objectlist IS a list of lists.. The only thing I dont like about doing it my way is that I'm putting an instance in two lists, thus having to remove it from both lists.  Reducing lines of code is my goal <br><br></td></tr></table><br>
<a name="986242"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Beaker</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> I agree that the lists should be inside the type itself. You can even name it _list or similar so you can cut and paste the handling code without worrying about what you called the TList. Seems silly to reference "tPlayer.playerlist" when you can reference "tPlayer._list".<br><br>Also, you can then change your clearEverything function to:<br><pre class=code>Function ClearEverything()
	tPlayer._list = Null
	tGameObject._list = Null
	''etc
End Function</pre><br>Or even better have a type function to clear the list and call it like so:<br>tPlayer.Clear() <br><br></td></tr></table><br>
<a name="986243"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi, thanks a lot for your help!<br><br>Question:<br>Using tPlayer._list = Null clears the list without having to For Next loop the list and clear them each one by one? If so that's fast! :D<br><br>If I used it that way then how would I declare the list?<br><br>I'm not really sure how I could apply the latest suggestions on lists and such.  Not to be a burden, but care to elaborate? <br><br></td></tr></table><br>
<a name="986245"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Beaker</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>	' Global list which holds all instances
	Global _list:TList

	'
	' This adds a new instance to the global list
	Method New()
		If _list = Null
			_list = New TList
		EndIf
		_list.addlast(Self)
	EndMethod</pre><br>Taken from here:<br><a href="http://blitzmax.com/Community/posts.php?topic=50285#573109" target="_blank">http://blitzmax.com/Community/posts.php?topic=50285#573109</a><br>(see my adjustment of Fredborgs code) <br><br></td></tr></table><br>
<a name="986248"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Amazing! Not sure why I should do it that way though? <br><br></td></tr></table><br>
<a name="986257"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think it's more trouble than its worth to delete the lists themselves, because you have to worry about making them again. If you just want to clear the list and start afresh, use list.Clear().<br><br><pre class=code>Function ClearEverything()
	blahblahlist.Clear()
	.......
End Function</pre><br><br>The method you used originally, using the loops and the Remove list method, is VERY inefficient. From my previous post, can you deduce why? :)<br><br><div class="quote"> objectlist IS a list of lists. <br></div>I don't think you follow what I mean, here is an example: (I've renamed objectlist to superlist)<br><br><pre class=code>Global superlist:TList = CreateList()

Global enemylist:TList = CreateList()
superlist.AddLast(enemylist)
Global playerlist:TList = CreateList()
superlist.AddLast(playerlist)
Global bulletlist:TList = CreateList()
superlist.AddLast(bulletlist)
Global playershots:TList = CreateList()
superlist.AddLast(playershots)
Global particlelist:TList = CreateList()
superlist.AddLast(particlelist)</pre><br><br>Lists can contain any object, and lists themselves are objects! So superlist doesn't contain any game objects directly, but now contains all the above lists.<br><br>When making, say a player, you only need to add it to the playerlist, NOT to superlist.<br><br><pre class=code>'---- do not need anymore - superlist.AddLast obj
playershots.AddLast obj</pre><br><br>To update all the objects in your game, use this:<br><br><pre class=code>For Local a_list:TList = EachIn superlist
	For Local an_object:tGameobject = EachIn a_list
		an_object.update()
	Next
Next</pre><br><br>The game will go through all lists in super list, and then go through all of them, and update the objects they contain. When you restart the game, you don't want to Clear superlist (as you'd have to add all the lists into it again) but should only clear its sublists.<br><br><br><pre class=code>Function ClearEverything()
	For Local a_list:TList = EachIn superlist
		a_list.Clear()
	Next
End Function</pre> <br><br></td></tr></table><br>
<a name="986258"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> THANK YOU! That makes PERFECT Sense! You're awesome! :)  I can't wait to redo the codez.  I'll post a more efficient version soon.<br><br>I have learned that TLists are actually objects and that you can put them inside other lists, I had no idea this was possible until today.. again, thank you. <br><br></td></tr></table><br>
<a name="986269"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's Version 2:<br><br>UPDATE INFO:<br>1. Removed Bytes and Doubles and replaced them with Integers<br>2. Changed CountList() to List.Count()<br>3. Fixed inconsistencies with Code Capitalization<br>4. Added more Code Comments<br>5. Added a SuperList and put all Lists in there, thus removing lots of code redundancy! I wanted to declare the lists in the types themselves, but doing it this way brought me syntax errors!<br>6. Changed "objlist.addlast obj:tobj" to "objlist.addlast obj"<br>7. If the shapes reach the bottom you FAIL!<br><br>Thanks a lot guys!  Feedback is welcome!<br><br><pre class=code>
SuperStrict
'DrawGeom function is used to draw all the shapes in the game.
Function DrawGeom(x:Float, y:Float, sides:Float, length:Float, angle:Float, r:Int = 255, g:Int = 255, b:Int = 255, glow:Int = True)

	Local aStep# = 360 / sides
	Length:Float = (length / 2) / Sin(aStep:Float / 2) 'Calculate the correct length of a side 			
	For Local a:Byte = 0 To sides - 1
		Local x1:Float = x:Float - (Sin(angle + (aStep * a)) * length)
		Local y1:Float = y:Float - (Cos(angle + (aStep * a)) * length)
		Local x2:Float = x:Float - (Sin(angle + (aStep * (a + 1))) * length)
		Local y2:Float = y:Float - (Cos(angle + (aStep * (a + 1))) * length)
		If glow = True
			SetColor r / 4, g / 4, b / 4
			DrawOval x1 - 5, y1 - 5, 11, 11							' Draw Circle at Vertex
		End If
		SetColor r, g, b
		DrawLine x1, y1, x2, y2									' Draw Connecting Lines
	Next
	
End Function

'Initialize the Game *****************************************
	'Set the playfield
	Graphics 320, 260
	
	'Declare Global Variables
	Global Direction:Int
	Global GameState:Int
	Global Level:Int
	Global PS:Int
	Global Score:Int
	
	'Declare Constants
	Const MOVELEFT:Int = 0
	Const MOVERIGHT:Int = 1
	Const STATE_TITLE:Int = 0
	Const STATE_PLAY:Int = 1
	Const STATE_PAUSE:Int = 2
	Const STATE_WIN:Int = 3
	Const STATE_FAIL:Int = 4
	
	'Create Object Lists
	Global SuperList:TList = New TList
	
	Global EnemyList:TList = New TList
	SuperList.AddLast(EnemyList)
	Global BulletList:TList = New TList
	SuperList.AddLast(BulletList)
	Global PlayerShots:TList = New TList
	SuperList.AddLast(PlayerShots)
	Global PlayerList:TList = New TList
	SuperList.AddLast(PlayerList)
'Game Initialized *****************************************

'All objects contain x,y,xv,and yv variables
Type tGameObject
	Field x:Float, y:Float, xv:Float, yv:Float
	Method update() Abstract
End Type

Type tEnemy Extends tGameObject
	
	'Create additional fields that are used for the Enemy Type
	Field r:Int, g:Int, b:Int 'Colorsettings 
	Field Sides:Float 'Sides settings
	Field Level:Int 'Level the Enemy is 
	Field EnemyType:Int 'Type the Enemy is
	'The Make function is used to create and confiugre a new Instance of the Enemy
	Function Make(x:Float, y:Float, EnemyType:Int = 1, Level:Int)
		Local NewEnemy:tEnemy = New tEnemy
		NewEnemy.x = x
		NewEnemy.y = y
		NewEnemy.EnemyType = EnemyType
		NewEnemy.Level = Level
		NewEnemy.xv = Level *.1 'The higher the level the faster the enemy will be
		If NewEnemy.xv &gt; 2 NewEnemy.xv = 2 'No need to make it TOO fast though, cap it at 2
		'Depending on the type, we confugre it's shape and color
		Select EnemyType
			Case 1
				NewEnemy.r = 255
				NewEnemy.g = 0
				NewEnemy.b = 0
				NewEnemy.Sides = 4
				
			Case 2
				NewEnemy.r = 0
				NewEnemy.g = 255
				NewEnemy.b = 0
				NewEnemy.Sides = 3
			
			Case 3
				NewEnemy.r = 0
				NewEnemy.g = 0
				NewEnemy.b = 255
				NewEnemy.Sides = 5
		End Select
		'Let's add it to the List
		EnemyList.AddLast(NewEnemy)
	End Function
	
	'The Update Method updates the instance every step
	Method Update()
		'Determine it's direction and move accordingly
		If Direction = MOVELEFT x:-xv
		If Direction = MOVERIGHT x:+xv
		'If any Enemy it's the edge of the screen, 
		'move all enemies downward and reverse the direction
		If x &lt; 0
			Direction = MOVERIGHT
			For Local Enemy:tEnemy = EachIn EnemyList
				Enemy.y:+4
			Next
		End If
		If x &gt; 320
			Direction = MOVELEFT
			For Local Enemy:tEnemy = EachIn EnemyList
				Enemy.y:+4
			Next
		End If
		'If the Enemy reaches the bottom, the game ends
		If y &gt; 240 Then GameState = STATE_FAIL
		'Draw the Enemy
		DrawGeom(x, y, Sides, 10, 0, r, g, b, False)
		' make it shoot
		Local R:Int = Rnd(10000)
		'The Higher the level the more frequent they shoot
		If R &lt; Level + 1 tBullet.make(x, y)
	End Method
End Type

Type tPlayer Extends tGameObject
	Field ShotDelay:Float
	
	Function Make()
		Local NewPlayer:tPlayer = New tPlayer
		NewPlayer.x = 320 / 2
		NewPlayer.y = 232
		PlayerList.AddLast(NewPlayer)
	End Function
	
	Method Update()
		'Increment the ShotDelay
		ShotDelay:+1
		'Playership is controlled with arrow keys and Z
		If KeyDown(KEY_RIGHT) x:+3
		If KeyDown(KEY_LEFT) x:-3
		'Shoot if the ShotDelay permits
		If KeyHit(KEY_Z) And ShotDelay &gt; 5
			If PlayerShots.Count() &lt; 2 tPlayerShot.Make(x, y) ;ShotDelay = 0
		End If
		'Bound the player in the playfield
		If x &lt; 0 x = 0
		If x &gt; 304 x = 304
		'Draw the player
		DrawGeom(x, y, 3, 16, 0,,, , False)
	End Method
End Type

Type tBullet Extends tGameObject
	
	'Make Function Called when instance is created
	Function Make(x:Float, y:Float)
		Local Bullet:tBullet = New tBullet
		Bullet.x = x
		Bullet.y = y
		Bullet.yv = 3
		Bulletlist.AddLast(Bullet)
	End Function
	
	'Update Function called with each step
	Method Update()
		y:+yv 'Move the bulled downard, remove if out of bounds
		If y &gt; 244 Then BulletList.Remove(Self)
		SetColor 255, 255, 0
		DrawOval x, y, 4, 4
		
		'Check for a collision with the Player
		For Local Player:tPlayer = EachIn PlayerList
			If Abs(Player.x - x - 2) &lt; 10 And Abs(Player.y - y) &lt; 8
				BulletList.Remove(Self)
				GameState = STATE_FAIL
				
			End If
		Next
	End Method
End Type

Type tPlayerShot Extends tGameObject
	
	'Make Function called when the Instance is created
	Function Make(x:Float, y:Float)
		Local PlayerShot:tPlayerShot = New tPlayerShot
		PlayerShot.x = x
		PlayerShot.y = y
		PlayerShot.yv = -8 'sets the speed to go upward 8 pixels per step
		playershots.AddLast(PlayerShot)
	End Function
	
	Method Update()
		y:+yv 'Move the shot
		'Destroy the instance if out of bounds
		If y &lt; - 4 Then PlayerShots.Remove(Self)
		'Check for a collision with enemies
		For Local Enemy:tEnemy = EachIn EnemyList
			If Abs(Enemy.x - x - 2) &lt; 8 And Abs(Enemy.y - y) &lt; 10
				If Enemy.EnemyType = 2 Score:+1
				If Enemy.EnemyType = 1 Score:+15
				If Enemy.EnemyType = 3 Score:+70
				EnemyList.Remove(Enemy)
				PlayerShots.Remove(Self)
			End If
		Next
		'Draw the Instance
		SetColor 0, 255, 255
		DrawOval x, y, 2, 4
	End Method
End Type

Function MakeGame(Level:Int)
	tPlayer.Make()
	For Local N:Int = 2 To 14
		tEnemy.Make(N * 20, 16 * 2, 3, Level)
	Next
	For Local N:Int = 2 To 14
		tEnemy.Make(N * 20, 16 * 4, 1, Level)
	Next
	For Local N:Int = 2 To 14
		tEnemy.Make(N * 20, 16 * 6, 2, Level)
	Next
End Function

Function ClearEverything()
	For Local a_list:TList = EachIn SuperList
		a_list.Clear()
	Next
End Function


'MAIN LOOP
Repeat
	Cls
	'Determine the GameState and act accordingly
	Select GameState
		Case STATE_TITLE
			DrawText "Shape Invaderz", 105, 20
			DrawText "ARROW KEYS", 30, 90
			DrawText "Z KEY", 30, 110
			DrawText "P KEY", 30, 130
			DrawText ". . .", 140, 90
			DrawText ". . .", 140, 110
			DrawText ". . .", 140, 130
			DrawText "Move Ship", 200, 90
			DrawText "Shoot", 200, 110
			DrawText "Pause", 200, 130
			DrawText "Press SPACE to play or ESC to Exit", 25, 220
			If KeyHit(KEY_SPACE)
				Level = 1
				SCORE = 0
				MakeGame(Level)
				GameState = STATE_PLAY
			End If
			
		Case STATE_PLAY
			'Update All Objects
			For Local a_list:TList = EachIn SuperList
				For Local an_object:tGameObject = EachIn a_list
					an_object.update()
				Next
			Next
			'Pause the screen if player pushes the P Key			
			If KeyHit (KEY_P) GameState = STATE_PAUSE
			
			'Draw HUD
			SetColor 0, 0, 200
			DrawLine 0, 240, 320, 240
			SetColor 255, 255, 255
			DrawText "Level:" + String(Level), 10, 244
			DrawText "Score:" + String(Score), 200, 244
			If CountList(EnemyList:TList) &lt; 1 GameState = STATE_WIN			
			
		Case STATE_PAUSE
			'PS is used to Scroll the "Game paused" text
			PS:+1
			If PS &gt; 320 PS = -320
			DrawText "Game PAUSED, Press SPACE to Continue", PS, 210
			If KeyHit(KEY_SPACE) GameState = STATE_PLAY

		Case STATE_WIN
			DrawText "WIN!", 145, 20
			DrawText "You Have owned Level " + String(Level) + "!!!!!!!!!!!!", 30, 60
			DrawText "Press SPACE to CONTINUE or ESC to Exit", 06, 210

			If KeyHit(KEY_SPACE)
				ClearEverything()
				Level:+1
				MakeGame(Level)
				GameState = STATE_PLAY			
			End If
			
			If KeyHit(KEY_ESCAPE)
				ClearEverything()
				GameState = STATE_TITLE
			End If
			
		Case STATE_FAIL
			DrawText "FAIL!", 145, 20
			DrawText "You DIED on Level " + String(Level) + "!!!!!!!!!!!!", 30, 60
			DrawText "You Scored " + String(Score) + " Points FTW", 10, 90
			DrawText "Press SPACE to CONTINUE or ESC to Exit", 06, 210
			
			If KeyHit(KEY_SPACE)
				ClearEverything()
				Level = 1
				GameState = STATE_TITLE
				SetColor 255, 255, 255
			End If
		
	End Select
	Flip
Until KeyHit (KEY_ESCAPE)
</pre> <br><br></td></tr></table><br>
<a name="986325"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >amonite</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cool little game !<br>I have learnt new things thanks to this thread :)<br>I like your DrawGeom function. <br><br></td></tr></table><br>
<a name="986366"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks!  The DrawGeom function was borrowed from Indiepath :) <br><br></td></tr></table><br>
<a name="986377"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >matibee</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> I read you wanted to make your lists part of the type they belonged to.  In a (for a lack of a better word) 'real'  OOP language that would be a 'static' member or field.  I didn't think static members were supported but I had to google it anyway;<br><br><a href="http://www.blitzbasic.com/Community/posts.php?topic=59233" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=59233</a><br><br>You can put the global inside the type which makes things a bit tidier.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Type tEnemy Extends tGameObject

	Global EnemyList:TList = New TList
	Global RegisteredToSuperList:Int = 0
	
	Function GetList:TList ()
		Return EnemyList
	End Function 
	
	Function RemoveEnemy( e:tEnemy )
		EnemyList.Remove( e )
	End Function
	
	Function ClearEnemies()
		EnemyList.Clear()
	End Function 
	
	Function EnemyCount:Int ()
		Return EnemyList.Count()
	End Function 

</textarea><br><br>See the second Global?  This is a bit subjective but I made the type self register with the global list so you don't have to worry about that anymore either.  <br><br><pre class=code>
Function Make(x:Float, y:Float, EnemyType:Int = 1, Level:Int)
		If ( Not RegisteredToSuperList ) Then 
			SuperList.AddLast(EnemyList)
			RegisteredToSuperList = 1
		End If 
</pre><br><br>It's subjective because tEnemy is now tied to a global variable called SuperList which I don't like.  If we we're taking tEnemy into it's own source file or module we could add a tEnemy.Init( SuperList:TList ) function or add the SuperList to the Make() function arguments.<br><br>I personally wouldn't want tEnemy.EnemyList accessed anywhere apart from within the tEnemy code but I initially had to add the GetList() function for your bullet/collision updates.  That was avoided by passing a bullet position to a tEnemy function and asking it to check for collisions...<br><br><pre class=code>
	Function CheckCollision:tEnemy( x:Int, y:Int )
		For Local Enemy:tEnemy = EachIn EnemyList
			If Abs(Enemy.x - x - 2) &lt; 8 And Abs(Enemy.y - y) &lt; 10
				Return Enemy
			End If 
		Next 
		Return NULL
	End Function 
</pre><br><br>If it returns an enemy object you can act on it.<br><br>All in all though, it's a very, very good job :) <br><br></td></tr></table><br>
<a name="986378"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think that's making it a bit more complicated than it needs to be. <br><br></td></tr></table><br>
<a name="986384"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> I wanted to keep the global list declaration inside the object code but I received syntax errors when combining with "superlist.addlast(tlist)" so I refrained from doing so.  In the end I really REALLY like that I can parse through the superlist and update each object that way with a few lines of code.<br><br>I have spent officially 12 hours on this project, I'm considering making it a 24 hour project then ending it there.  I am thinking I'll make 24 hour game projects just to see where I'll go.  I should do them from scratch, borrowing maths codes but remaining true to memorizing blitzmax coding.<br><br>I have a newer version but I'm not sure if I should post it or not for a couple of reason, if you guys want me to I'll post it but it's got a huge amount of code!<br><br>1. I programmed a neat "mod" (per se) that allows me to type one line of code and get a fancy vector text font.  For instance:<br><pre class=code>
VecDrawText("Hello World",x,y,size)
</pre><br>draws the text in a neat vector style font.  I painstakingly created the function(s) that draw each letter in the alphabet and all the numbers.  However no other symbols so far(except chevrons but that was so I could draw a K)<br><br>I'll post a separate topic on the matter, I think you guys might find it useful and I'd be happy to share it. <br><br></td></tr></table><br>
<a name="986412"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#23">[#23]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> but I received syntax errors <br></div> If you post the errors, we can help you with them. When using a list outside of a type, you need to prefix it with the type name. So if you put Global playerlist:TList inside Type TPlayer, any code inside TPlayer can just say playerlist, but outside you need to write TPlayer.playerlist. <br><br></td></tr></table><br>
<a name="986440"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Arabia</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#24">[#24]</a></td></tr></table></td></tr><tr ><td class="posttext"> I got 21910, died on level 20 :)<br><br>Nice game.  I think I'll try and figure out your source code a bit and have a play around.  Might be a good starting point for others to do something with this. <br><br></td></tr></table><br>
<a name="986875"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#25">[#25]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi guys, I have a download available for version .5 (the 12th hour of development) and would like some feedback.<br><br>The latest version includes:<br><br>VectorText - A font upgrade that is inefficient(I'm thankful for the guys who reviewed the VT code and pointing this out to me), I plan on changing this to a bitmap font by the time of completion.<br><br>Shields - You now have destroyable shields that help protect you from enemy bullets, but also block yours (just like in space invaders)<br><br>Fixed fail boundary -  When the enemies reach the bottom you fail the game, before they went past the bottom.<br><br>Added CTRL fire key - For those who have a different playstyle you may use the CTRL keys for firing.  My primary reason is for accessibility, as one-handed people cannot play a game with the fire key being too far from the controls.<br><br>Game Difficulty - I have re-programmed the way the enemies behave for a more balanced approach.<br><br>I plan on releasing the source code once I finish the final 12 hours of this game project.  Once I hit 24 hours it'll be completed, and after scrutiny I'd like to release a comprehensive tutorial for beginners on how to make this game.  <br><br>You can download it (500kb, zip file with 1 exe file) here:<br><a href="http://www.gamedev.net/community/gds/viewentry.asp?projectid=548722" target="_blank">http://www.gamedev.net/community/gds/viewentry.asp?projectid=548722</a> <br><br></td></tr></table><br>
<a name="986876"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#26">[#26]</a></td></tr></table></td></tr><tr ><td class="posttext"> OH one last thing:<br><br>@ Czar:<br><br>Here's my syntax error problem:<br><pre class=code>
Type tEnemy Extends tGameObject
	Global EnemyList:TList = New TList
	SuperList.AddLast(EnemyList)
</pre><br>on the line "Superlist"... I get:<br><pre class=code>
Compile Error: Syntax error in user defined type declaration
Build Error: failed to compile C:/BlitzMax/projects/ShapeInvaderz.bmx
</pre> <br><br></td></tr></table><br>
<a name="986904"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#27">[#27]</a></td></tr></table></td></tr><tr ><td class="posttext"> you have to put use of superlist.addlast(enemylist) in a method or function.<br>you can do it like this:<br><pre class=code>
type tEnemy Extends TGameObject
	Global EnemyList:TList = New TList

        method new()
           SuperList.AddLast(EnemyList)
        End Method
        
</pre><br>when you do:<br><pre class=code>
game = new TGameObject
</pre><br>it will automatically call the "new" method<br>you don't have to do this:<br>game.new()<br>the method new is used by the constructor to initialize any variables or do anything at the time of creating the current object. <br><br></td></tr></table><br>
<a name="986977"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#28">[#28]</a></td></tr></table></td></tr><tr ><td class="posttext"> Jesse's code would try to add the enemylist to superlist everytime you created a new enemy, which isn't our intention. You should leave the superlist.adds outside of type declarations, and leave them where they were in the original code (in the initialize the game area), but move the Global declarations.<br><br>Remember to call the lists outside the type block like this - TEnemy.EnemyList. You could perhaps remain the lists to just "list", to make it less wordy. So SuperList.AddLast(TEnemy.list).. SuperList.AddLast(TPlayer.list) etc <br><br></td></tr></table><br>
<a name="987016"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#29">[#29]</a></td></tr></table></td></tr><tr ><td class="posttext"> I get the same syntax error if I move the global declarations but keep the superlist.addlast in the initialization area.  I'm not exactly sure how to avoid them unless I just keep the list declaration in the top with the superlist.addlast(list) <br><br></td></tr></table><br>
<a name="987022"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#30">[#30]</a></td></tr></table></td></tr><tr ><td class="posttext"> wrong post <br><br></td></tr></table><br>
<a name="987039"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#31">[#31]</a></td></tr></table></td></tr><tr ><td class="posttext"> If you post the complete code we can help you solve the problem. That code doesn't tell us much other than, you are not following syntax rules somewhere. <br><br></td></tr></table><br>
<a name="987044"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#32">[#32]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok here's the problem:<br><br><pre class=code>
SuperStrict

Global SuperList:TList = New TList

Type tEnemy
Global EnemyList:TList = New TList
	SuperList.AddLast(EnemyList)
End Type
</pre><br><br>Creates same problem.  It clearly highlights at "SuperList.AddLast(EnemyList)" as a syntax error. (Type Declaration)<br><br>So, now try moving SuperList.AddLast(EnemyList) to the top as suggested:<br><br><pre class=code>
SuperStrict

Global SuperList:TList = New TList
SuperList.AddLast(EnemyList)

Type tEnemy
Global EnemyList:TList = New TList
End Type
</pre><br>Syntax Error @ SuperList.addlast(EnemyList) is now (missing EnemyList)<br><br>NOW: The only way I can get it to work:<br><br><pre class=code>

Global SuperList:TList = New TList
Global EnemyList:TList = New TList
SuperList.AddLast(EnemyList)

Type tEnemy
End Type
</pre><br><br>Thus I have to keep the enemylist creation outside of the type. <br><br></td></tr></table><br>
<a name="987050"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#33">[#33]</a></td></tr></table></td></tr><tr ><td class="posttext"> If you're using the list outside of tEnemy, you need to call it tEnemy.EnemyList <pre class=code>SuperStrict

Global SuperList:TList = New TList
SuperList.AddLast(tEnemy.EnemyList)

Type tEnemy
Global EnemyList:TList = New TList
Method something_inside_the_type()
Print Enemylist.Count() + " no need for tEnemy. inside here!"
End Method
End Type</pre> <br><br></td></tr></table><br>
<a name="987063"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#34">[#34]</a></td></tr></table></td></tr><tr ><td class="posttext"> I wanted to use the list inside the tenemy to "tidy up the code" <br><br></td></tr></table><br>
<a name="987064"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >REDi</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#35">[#35]</a></td></tr></table></td></tr><tr ><td class="posttext"> As Jesse said in post #27, you cant have code inside a type like that, it must be in a method or function...<br><br><pre class=code>
SuperStrict

Global SuperList:TList = New TList

Type tEnemy
	Global EnemyList:TList = New TList
	
	Method New()
		SuperList.AddLast(EnemyList)
	EndMethod
End Type
</pre> <br><br></td></tr></table><br>
<a name="987066"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#36">[#36]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks!  Also Sorry Jesse I see you answered it but I didnt understand at the time :S  I appreciate your help! <br><br></td></tr></table><br>
<a name="987071"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >REDi</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#37">[#37]</a></td></tr></table></td></tr><tr ><td class="posttext"> OOPS, I just read post #28 :D<br><br>I Didn't even notice its a global list in the type (its been a long night), anyway this is what you're after...<br><br><pre class=code>
SuperStrict

Global SuperList:TList = New TList
SuperList.AddLast(tEnemy.EnemyList)

Type tEnemy
	Global EnemyList:TList = New TList
End Type
</pre> <br><br></td></tr></table><br>
<a name="987122"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#38">[#38]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah it was a late night for me too, not to mention how frustrating coding has been in the last day or so.  Anyway, you can't put superlist.addlast(tenemy.enemylist) at the top there unless you put the enemylist at the top with it, just tyry it; doesn't work.  <br><br>EDIT:<br>The only way you can put the superlist.addlast command inside the tenemy is if you put it into a method, but you don't want to keep adding the list into the superlist.   <br><br>From what I gather, you'd have to call the method only one time as some sort of init method.<br><br>EDIT 2:<br>It's too complicated to just get the list declaration in the enemy type.  It's more practical to put the list declaration at the top of the file along with the superlist stuff.  If you need to remember what the lists were called, it's easy to simply use BLIDE and use a split screen where  one side shows the declarations and the other side shows where you're coding. <br><br></td></tr></table><br>
<a name="987173"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#39">[#39]</a></td></tr></table></td></tr><tr ><td class="posttext"> I don't understand why you would want many lists inside a List if the objects are similar. The compiler is going to take longer to process if it has to determine what type of object the list contain. personally I would have an array of lists as the super list. That is not how I code but if I did that is how I would do it. <br>in my games what I do is, I crate a list of baddies and  two list of bullets(one for the baddies and one for the player). Each of the baddies extend from the base type. I use the base to process everything. like this:<br><br>for bullets:Tbase = eachin bulletlist <br>    'process bullet <br>next <br><br>there is a tutorial on OOP: <a href="/posts.php?topic=78860" target="_blank">Polymorphism for beginners</a> It helped me out a bit, it Might help you also. <br><br></td></tr></table><br>
<a name="987176"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#40">[#40]</a></td></tr></table></td></tr><tr ><td class="posttext"> The reason why I like the superlist is that you only have to iterate through the objects one time and update them all, so if you add new objects and add them to the superlist you do not have to add any additional code to update them.<br><br>For instance:<br><br>If you have 5 lists without a superlist you must make 5 for..next loops.  If you have 5 lists within a superlist then you only need 1 for..next loop.  (^_^)=b!!<br><br>So instead of having several blocks of code to update all the objects in the game you only need one.  This is why I believe I'll be using superlists from here on out.  If you look at the top and read my first post I was not using a superlist at all.  Someone showed me an interesting method (using superlists) and I think they are better than not using them.<br><br>Lastly I'm not sure what the big deal is about these arrays or why you would want to put lists in array[].  It's far easier to read the code when things arent [0] or [4] .. just my humble opinion on the matter.  <br><br>I suppose if using brackets with numbers in them makes it easier to read for you then go for it.  But one of the reasons why I like blitzmax so much is that you can put things in types instead of brackets and numbers.  My only concern about using arrays and their funky brackets with numbers is that it could increase speed.  But if the speed increase is negligable then they seem rather archaic.  The whole purpose of BASIC is that its an easy language to read.  using "dim" and [] brackets with numbers in them just isn't that easy for reading and it doesn't seem to be OOP but more like the times of QBasic and GWBasic etc.  If I was going to program games with arrays all day then I'd probably be using DarkBASIC. <br><br></td></tr></table><br>
<a name="987185"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >REDi</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#41">[#41]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>Anyway, you can't put superlist.addlast(tenemy.enemylist) at the top there unless you put the enemylist at the top with it, just tyry it; doesn't work.<br> <br></div><br><br>I beg to differ, have you tried that code snippet? <br><br></td></tr></table><br>
<a name="987187"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >REDi</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#42">[#42]</a></td></tr></table></td></tr><tr ><td class="posttext"> Quick tester...<br><pre class=code>
SuperStrict

Global SuperList:TList = New TList
SuperList.AddLast(tEnemy.EnemyList)

New TEnemy
New TEnemy
New TEnemy

Local list:TList, enemy:TEnemy
For list=EachIn SuperList
	For enemy=EachIn list
		enemy.test()
	Next 
Next

Type tEnemy
	Global EnemyList:TList = New TList
	Field Link:TLink = EnemyList.AddLast(Self)
	Field index%=EnemyList.Count()
	Method test()
		Print "Enemy "+index
	EndMethod
End Type
</pre> <br><br></td></tr></table><br>
<a name="987189"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#43">[#43]</a></td></tr></table></td></tr><tr ><td class="posttext"> Awesome!  I stand corrected! :D  I had to put tenemy.enemylist instead of enemylist...<br><br>I think this was mentioned earlier but it when right over my head, I really apologize for that.  It finally makes sense! Yes it sunk in:)  Thanks a million.<br><br>For some reason I thought that if you put "Global" inside a Type it would be the same as putting a "Global" OUTSIDE of a type.<br><br>So basically tenemy.list would be if I used a GLOBAL declaration of a list inside the type.  Therefore I could have tbullet.list,tplayer.list,tenemy.list and I love the idea!  <br><br>Thanks a lot! <br><br></td></tr></table><br>
<a name="987213"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#44">[#44]</a></td></tr></table></td></tr><tr ><td class="posttext"> Matt, just so you know, I am not picking on you. I don't know if I sound to rough. I was just trying to say that a group of lists in a list is not such a great idea,that arrays are faster and a better choice. at least in my opinion and mind I am no expert. I don't even code with arrays generally. The only time I have used arrays is with my tile map engine I have in the code archives. I do use lists a lot and now with direct link removal but that's for another topic. if you would like to take a look at how I code. you can take a look at my Community Framework contest entry here:<br><a href="http://www.filefront.com/13724392/bmfc_c.zip" target="_blank">http://www.filefront.com/13724392/bmfc_c.zip</a><br>you can take a look at the source code.<br>it's not too well optimized or complete as it was made in a hurry for the contest. but it works. <br><br></td></tr></table><br>
<a name="987215"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#45">[#45]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok guys: I have the 21st hour source code for you.  To play you need two blitzmax files..<br><br>UPDATES:<br><br>MAJOR SPEED INCREASES!<br>***** 1 - Created a new VectorDraw_img_Text Function that saves all the letters into image files, and the font loads faster than DrawText() - The letters are still drawn with lines, but saved into uiamges using GrabImage()<br>***** 2 -  All shapes are no longer drawn in realtime, but drawn int he beginning and then saved into images using GrabImage.<br>***** 3 - Using a more modern way to iterate through a string.<br><br>It's amazing that these optimizations took me 9 hours to do, partially because I had a lot of learning to do and I appreciate the blitzmax community in helping me along the way!<br><br>Filename "VectorText.bmx"  <br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

'Initialize Text *****

Graphics 320, 240
Cls
Global VecChar:TImage[96]
SetColor 255, 255, 255
SetBlend ALPHABLEND
Const FONTSIZE:Float = 6
For Local i:Int = 0 To 95
	Cls
	If i = 15 DrawFullS(0, 0, FontSize)
	If i = 16 DrawNumber_0(0, 0, FontSize)
	If i = 17 DrawNumber_1(0, 0, FontSize)
	If i = 18 DrawNumber_2(0, 0, FontSize)
	If i = 19 DrawNumber_3(0, 0, FontSize)
	If i = 20 DrawNumber_4(0, 0, FontSize)
	If i = 21 DrawNumber_5(0, 0, FontSize)
	If i = 22 DrawNumber_6(0, 0, FontSize)
	If i = 23 DrawNumber_7(0, 0, FontSize)
	If i = 24 DrawNumber_8(0, 0, FontSize)
	If i = 25 DrawNumber_9(0, 0, FontSize)
	If i = 28 DrawLeftChevron(0, 0, FontSize)
	If i = 30 DrawRightChevron(0, 0, FontSize)
	If i = 33 Or i = 65 DrawLetter_A(0, 0, FontSize)
	If i = 34 Or i = 66 DrawLetter_B(0, 0, FontSize)
	If i = 35 Or i = 67 DrawLetter_C(0, 0, FontSize)
	If i = 36 Or i = 68 DrawLetter_D(0, 0, FontSize)
	If i = 37 Or i = 69 DrawLetter_E(0, 0, FontSize)
	If i = 38 Or i = 70 DrawLetter_F(0, 0, FontSize)
	If i = 39 Or i = 71 DrawLetter_G(0, 0, FontSize)
	If i = 40 Or i = 72 DrawLetter_H(0, 0, FontSize)
	If i = 41 Or i = 73 DrawLetter_I(0, 0, FontSize)
	If i = 42 Or i = 74 DrawLetter_J(0, 0, FontSize)
	If i = 43 Or i = 75 DrawLetter_K(0, 0, FontSize)
	If i = 44 Or i = 76 DrawLetter_L(0, 0, FontSize)
	If i = 45 Or i = 77 DrawLetter_M(0, 0, FontSize)
	If i = 46 Or i = 78 DrawLetter_N(0, 0, FontSize)
	If i = 47 Or i = 79 DrawLetter_O(0, 0, FontSize)
	If i = 48 Or i = 80 DrawLetter_P(0, 0, FontSize)
	If i = 49 Or i = 81 drawletter_Q(0, 0, FontSize)
	If i = 50 Or i = 82 drawletter_R(0, 0, FontSize)
	If i = 51 Or i = 83 DrawLetter_S(0, 0, FontSize)
	If i = 52 Or i = 84 DrawLetter_T(0, 0, FontSize)
	If i = 53 Or i = 85 DrawLetter_U(0, 0, FontSize)
	If i = 54 Or i = 86 DrawLetter_V(0, 0, FontSize)
	If i = 55 Or i = 87 DrawLetter_W(0, 0, FontSize)
	If i = 56 Or i = 88 DrawLetter_X(0, 0, FontSize)
	If i = 57 Or i = 89 DrawLetter_Y(0, 0, FontSize)
	If i = 58 Or i = 90 DrawLetter_Z(0, 0, FontSize)
	If i = 60 DrawFullS(0, 0, FontSize)
	VecChar[i] = CreateImage(FontSize + 1, FontSize + 1, 1, DYNAMICIMAGE | MASKEDIMAGE)
	GrabImage VecChar[i], 0, 0
Next

Function DrawVleftST(Ox:Float, Oy:Float, LetterSize:Float)

	Local Y:Float = Oy + Lettersize
	DrawLine (Ox, Y, Ox, oY) 'Verticle Left Straight

End Function
']
Function DrawVrightST(Ox:Float, Oy:Float, LetterSize:Float)
	
	Local X:Float = Ox + LetterSize
	Local Y:Float = Oy + LetterSize
    DrawLine (X, Y, X, Oy)  'Verticle Right Straight
 	
End Function
'Half Top [
Function DrawVleftTopST(Ox:Float, Oy:Float, LetterSize:Float)

	Local Hy:Float = Oy + Lettersize / 2
	DrawLine (Ox, oY, Ox, Hy) 'Verticle Left TOP Straight
	
End Function
'Half Bottom [
Function DrawVleftBottomST(Ox:Float, Oy:Float, LetterSize:Float)

	Local Hy:Float = Oy + Lettersize / 2
	Local Y:Float = Oy + Lettersize
	DrawLine (Ox, Y, Ox, Hy) 'Verticle Left BOTTOM Straight
	
End Function
'Half Top ]
Function DrawVrightTopST(Ox:Float, Oy:Float, LetterSize:Float)
	
	Local X:Float = Ox + LetterSize
	Local Hy:Float = Oy + Lettersize / 2
    DrawLine (X, oY, X, Hy)  'Verticle Right Straight
	
End Function
'Half Bottom ]
Function DrawVrightBottomST(Ox:Float, Oy:Float, LetterSize:Float)

	Local Hy:Float = Oy + Lettersize / 2
	Local X:Float = Ox + LetterSize
	Local Y:Float = Oy + LetterSize
    DrawLine (X, Y, X, Hy)  'Verticle Right Straight
	
End Function
'Top
Function DrawHtopST(Ox:Float, Oy:Float, LetterSize:Float)
	
	Local X:Float = Ox + Lettersize
	DrawLine (ox, oy, x, oy) 'Horizontal Top Straight
	
End Function
'Bottom
Function DrawHbottomST(Ox:Float, Oy:Float, LetterSize:Float)

	Local X:Float = Ox + Lettersize
	Local Y:Float = Oy + Lettersize
	DrawLine (Ox, Y, X, Y) 'Horizontal Bottom Straight
	
End Function
'\
Function DrawFullBS(Ox:Float, Oy:Float, LetterSize:Float)
	
	Local X:Float = Ox + Lettersize
	Local Y:Float = Oy + Lettersize
	DrawLine (Ox, Oy, X, Y) 'Full \ BackSlash
	
End Function
'Top \
Function DrawHalfTBS(Ox:Float, Oy:Float, LetterSize:Float)
	
	Local Hx:Float = Ox + Lettersize / 2
	Local Hy:Float = Oy + Lettersize / 2
	DrawLine (Ox, Oy, hx, hy) 'Half Upper \ BackSlash
	
End Function
'Bottom \
Function DrawHalfLBS(Ox:Float, Oy:Float, LetterSize:Float)
	Local X:Float = Ox + Lettersize
	Local Y:Float = Oy + Lettersize
	Local Hx:Float = Ox + Lettersize / 2
	Local Hy:Float = Oy + Lettersize / 2
	DrawLine (x, y, Hx, Hy) 'Half Lower \ BackSlash
End Function
'/
Function DrawFullS(Ox:Float, Oy:Float, LetterSize:Float)
	
	Local X:Float = Ox + Lettersize
	Local Y:Float = Oy + Lettersize
	DrawLine (Ox, Y, X, Oy) 'Full / Slash

End Function
'Top /
Function DrawTS(Ox:Float, Oy:Float, LetterSize:Float)
	
	Local X:Float = Ox + Lettersize
	Local Hx:Float = Ox + Lettersize / 2
	Local Hy:Float = Oy + Lettersize / 2
	DrawLine (X, Oy, Hx, Hy) 'Half Upper / Slash
	
End Function
'Bottom /
Function DrawLS(Ox:Float, Oy:Float, LetterSize:Float)
	
	Local Y:Float = Oy + Lettersize
	Local Hx:Float = Ox + Lettersize / 2
	Local Hy:Float = Oy + Lettersize / 2
	DrawLine (Ox, Y, Hx, Hy) 'Half Lower / Slash
	
End Function
'-
Function DrawFullDash(Ox:Float, Oy:Float, LetterSize:Float)

	Local X:Float = Ox + Lettersize
	Local Hy:Float = Oy + Lettersize / 2
	DrawLine (Ox, Hy, X, Hy)  'Draw a Full -

End Function
'Left -
Function DrawLeftDash(Ox:Float, Oy:Float, LetterSize:Float)

	Local Hx:Float = Ox + Lettersize / 2
	Local Hy:Float = Oy + Lettersize / 2
	DrawLine (Ox, Hy, HX, Hy)  'Draw a Left - (half a full dash)

End Function
'Right -
Function DrawRightDash(Ox:Float, Oy:Float, LetterSize:Float)
	
	Local X:Float = Ox + Lettersize
	Local Hx:Float = Ox + Lettersize / 2
	Local Hy:Float = Oy + Lettersize / 2
	DrawLine (X, Hy, HX, Hy)  'Draw a Right - (half a full dash)

End Function
'|
Function DrawPipe(Ox:Float, Oy:Float, LetterSize:Float)

	Local Y:Float = Oy + Lettersize
	Local Hx:Float = Ox + Lettersize / 2
	DrawLine (Hx, Y, Hx, Oy)
	
End Function
'Low |
Function DrawBottomPipe(Ox:Float, Oy:Float, LetterSize:Float)
	
	Local Y:Float = Oy + Lettersize
	Local Hx:Float = Ox + Lettersize / 2
	Local Hy:Float = Oy + Lettersize / 2
	DrawLine(Hx, Y, Hx, Hy)
	
End Function
'Top |
Function DrawTopPipe(Ox:Float, Oy:Float, LetterSize:Float)

	Local Hx:Float = Ox + Lettersize / 2
	Local Hy:Float = Oy + Lettersize / 2
	DrawLine(Hx, Oy, Hx, Hy)
	
End Function
'Top &gt;
Function DrawTopRightChevron(Ox:Float, Oy:Float, LetterSize:Float)
	
	Local X:Float = Ox + Lettersize
	Local Hy:Float = Oy + Lettersize / 2
	DrawLine(Ox, Oy, X, Hy)
End Function
'Bottom &gt;
Function DrawBottomRightChevron(Ox:Float, Oy:Float, LetterSize:Float)
	
	Local X:Float = Ox + Lettersize
	Local Y:Float = Oy + Lettersize
	Local Hy:Float = Oy + Lettersize / 2	
	DrawLine(Ox, Y, x, Hy)
End Function
'Full &gt;
Function DrawRightChevron(Ox:Float, Oy:Float, LetterSize:Float)
	
	Local X:Float = Ox + Lettersize
	Local Y:Float = Oy + Lettersize
	Local Hy:Float = Oy + Lettersize / 2
	DrawLine(Ox, Oy, X, Hy)
	DrawLine(Ox, Y, x, Hy)
	
End Function
'Top &lt;
Function DrawTopLeftChevron(Ox:Float, Oy:Float, LetterSize:Float)
	
	Local X:Float = Ox + Lettersize
	Local Hy:Float = Oy + Lettersize / 2
	DrawLine(X, OY, Ox, Hy)
End Function
'Bottom &lt;
Function DrawBottomLeftChevron(Ox:Float, Oy:Float, LetterSize:Float)
	
	Local X:Float = Ox + Lettersize
	Local Y:Float = Oy + Lettersize
	Local Hy:Float = Oy + Lettersize / 2	
	DrawLine(X, Y, Ox, Hy)
End Function
'Full &lt;
Function DrawLeftChevron(Ox:Float, Oy:Float, LetterSize:Float)
	
	Local X:Float = Ox + Lettersize
	Local Y:Float = Oy + Lettersize
	Local Hy:Float = Oy + Lettersize / 2
	DrawLine(X, Y, Ox, Hy)
	DrawLine(X, OY, Ox, Hy)
	
End Function
'A damn D
Function DrawaDamnD(Ox:Float, Oy:Float, LetterSize:Float)
	
	Local X:Float = Ox + Lettersize
	Local Y:Float = Oy + Lettersize
	Local Hx:Float = Ox + Lettersize / 3
	Local Hy:Float = Oy + Lettersize / 28
	Local Hyd:Float = Oy + Lettersize / 1.028
	DrawLine(Ox, Oy, X, Hy)
	DrawLine(Ox, Y, x, Hyd)
	DrawLine(x, Hy, X, Hyd)
End Function
'A damn V
Function DrawaV(Ox:Float, Oy:Float, LetterSize:Float)
	Local X:Float = Ox + Lettersize
	Local Y:Float = Oy + Lettersize
	Local Hx:Float = Ox + Lettersize / 2
	DrawLine(ox, oy, hx, y)
	DrawLine(x, oy, hx, y)
End Function
'Upside Down Carrot
Function DrawUSDCarrot(Ox:Float, Oy:Float, LetterSize:Float)
	Local X:Float = Ox + Lettersize
	Local Y:Float = Oy + Lettersize
	Local Hx:Float = Ox + Lettersize / 2
	Local Hy:Float = Oy + Lettersize / 2
	DrawLine(ox, oy, hx, hy)
	DrawLine(x, oy, hx, hy)
		
End Function
'Left Half Carrot
Function DrawLeftHalfCarrot(Ox:Float, Oy:Float, LetterSize:Float)
	Local X:Float = Ox + Lettersize
	Local Y:Float = Oy + Lettersize
	Local Hx:Float = Ox + Lettersize / 2
	Local Hy:Float = Oy + Lettersize / 2
	DrawLine(hx, oy, ox, hy)
		
End Function



Function DrawLetter_A (X:Float, Y:Float, S:Float)

	DrawVleftST(X, Y, S)
	DrawHtopST(X, Y, S)
	DrawVrightST(X, Y, S)
	DrawFullDash(X, Y, S)
	
End Function

Function DrawLetter_B (X:Float, Y:Float, S:Float)
		
	DrawVleftST(X, Y, S)
	DrawaDamnD(X, Y, S)
	DrawFullDash(X, Y, S)
	
End Function

Function DrawLetter_C (X:Float, Y:Float, S:Float)
	
	DrawVleftST(X, Y, S)
	DrawHtopST(X, Y, S)
	DrawHbottomST(X, Y, S)

End Function

Function DrawLetter_D (X:Float, Y:Float, S:Float)
	DrawVleftST(X, Y, S)
	DrawaDamnD(X, Y, S)
End Function

Function DrawLetter_E (X:Float, Y:Float, S:Float)
	
	DrawVleftST(X, Y, S)
	DrawHtopST(X, Y, S)
	DrawHbottomST(X, Y, S)
	DrawFullDash(X, Y, S)
	
End Function

Function DrawLetter_F (X:Float, Y:Float, S:Float)
	
	DrawVleftST(X, Y, S)
	DrawHtopST(X, Y, S)
	DrawFullDash(X, Y, S)
	
End Function

Function DrawLetter_G (X:Float, Y:Float, S:Float)
	
	DrawVleftST(X, Y, S)
	DrawHbottomST(X, Y, S)
	DrawHtopST(X, Y, S)
	DrawRightDash(X, Y, S)
	DrawVrightBottomST(X, Y, S)
	
End Function

Function DrawLetter_H (X:Float, Y:Float, S:Float)
	
	DrawVleftST(X, Y, S)
	DrawVrightST(X, Y, S)
	DrawFullDash(X, Y, S)
	
End Function

Function DrawLetter_I (X:Float, Y:Float, S:Float)
	
	DrawPipe(X, Y, S)
	DrawHtopST(X, Y, S)
	DrawHbottomST(X, Y, S)
	
End Function

Function DrawLetter_J (X:Float, Y:Float, S:Float)
	
	DrawVrightST(X, Y, S)
	DrawVleftBottomST(X, Y, S)
	DrawHbottomST(X, Y, S)
	
End Function

Function DrawLetter_K (X:Float, Y:Float, S:Float)
	
	DrawVleftST(X, Y, S)
	DrawLeftChevron(X, Y, S)
	
End Function

Function DrawLetter_L (X:Float, Y:Float, S:Float)
	
	DrawVleftST(X, Y, S)
	DrawHbottomST(X, Y, S)
	
End Function
Function DrawLetter_M (X:Float, Y:Float, S:Float)
	
	DrawHtopST(X, Y, S)
	DrawTopPipe(X, Y, S)
	DrawVleftST(X, Y, S)
	DrawVrightST(X, Y, S)
	
	
End Function

Function DrawLetter_N (X:Float, Y:Float, S:Float)
	
	DrawFullBS(X, Y, S)
	DrawVleftST(X, Y, S)
	DrawVrightST(X, Y, S)
	
	
End Function

Function DrawLetter_O (X:Float, Y:Float, S:Float)
	
	DrawVleftST(X, Y, S)
	DrawVrightST(X, Y, S)
	DrawHbottomST(X, Y, S)
	DrawHtopST(X, Y, S)
		
	
End Function

Function DrawLetter_P (X:Float, Y:Float, S:Float)
	
	DrawVleftST(X, Y, S)
	DrawHtopST(X, Y, S)
	DrawVrightTopST(X, Y, S)
	DrawFullDash(X, Y, S)
	
End Function
Function drawletter_Q (X:Float, y:Float, S:Float)

	DrawVleftST(X, Y, S)
	DrawVrightST(X, Y, S)
	DrawHbottomST(X, Y, S)
	DrawHtopST(X, Y, S)
	DrawHalfLBS(X, Y, S)
	
End Function

Function drawletter_R (X:Float, y:Float, S:Float)

	DrawVleftST(X, Y, S)
	DrawHtopST(X, Y, S)
	DrawVrightTopST(X, Y, S)
	DrawFullDash(X, Y, S)
	DrawBottomLeftChevron(X, Y, S)
End Function

Function DrawLetter_S (X:Float, Y:Float, S:Float)
	
	DrawVleftTopST(X, Y + 1, S)
	DrawVrightBottomST(X, Y, S - 1)
	DrawHtopST(X, Y, S)
	DrawHbottomST(X, Y, S)
	DrawFullDash(X, Y, S)
	
End Function

Function DrawLetter_T (X:Float, Y:Float, S:Float)
	
	DrawPipe(X, Y, S)
	DrawHtopST(X, Y, S)
End Function

Function DrawLetter_U (X:Float, Y:Float, S:Float)
	
	DrawVleftST(X, Y, S)
	DrawVrightST(X, Y, S)
	DrawHbottomST(X, Y, S)

End Function

Function DrawLetter_V (X:Float, Y:Float, S:Float)
	
	DrawaV(X, Y, S)

End Function

Function DrawLetter_W (X:Float, Y:Float, S:Float)
	
	DrawHbottomST(X, Y, S)
	DrawBottomPipe(X, Y, S)
	DrawVleftST(X, Y, S)
	DrawVrightST(X, Y, S)
	
	
End Function

Function DrawLetter_X (X:Float, Y:Float, S:Float)
	DrawFullS(X, Y, S)
	DrawFullBS(X, Y, S)
End Function

Function DrawLetter_Y(X:Float, Y:Float, S:Float)
	DrawUSDCarrot(X, Y, S)
	DrawBottomPipe(X, Y, S)
End Function

Function DrawLetter_Z(X:Float, Y:Float, S:Float)
	DrawHtopST(X, Y, S)
	DrawFullS(X, Y, S)
	DrawHbottomST(X, Y, S)
End Function

Function DrawNumber_0(X:Float, Y:Float, S:Float)
	DrawLetter_O(X, Y, S)
	DrawFullS(X, Y, S)
End Function

Function DrawNumber_1(X:Float, Y:Float, S:Float)
	DrawPipe(X, Y, S)
	DrawLeftHalfCarrot(X, Y, S)
	DrawHbottomST(X, Y, S)
End Function

Function DrawNumber_2(X:Float, Y:Float, S:Float)
	DrawVrightTopST(X, Y, S)
	DrawHtopST(X, Y, S)
	DrawHbottomST(X, Y, S)
	DrawVleftBottomST(X, Y, S)
	DrawFullDash(X, Y, S)
End Function

Function DrawNumber_3(X:Float, Y:Float, S:Float)
	DrawVrightST(X, Y, S)
	DrawHtopST(X, Y, S)
	DrawHbottomST(X, Y, S)
	DrawFullDash(X, Y, S)
End Function

Function DrawNumber_4(X:Float, Y:Float, S:Float)
	DrawVleftTopST(X, Y, S)
	DrawVrightST(X, Y, S)
	DrawFullDash(X, Y, S)
End Function
Function DrawNumber_5(X:Float, Y:Float, S:Float)
	DrawVleftTopST(X, Y, S)
	DrawVrightBottomST(X, Y, S)
	DrawHtopST(X, Y, S)
	DrawFullDash(x, y, s)
	DrawHbottomST(X, Y, S)
End Function
Function DrawNumber_6(X:Float, Y:Float, S:Float)
	DrawVleftST(X, Y, S)
	DrawVrightBottomST(X, Y, S)
	DrawHtopST(X, Y, S)
	DrawHbottomST(X, Y, S)
	DrawFullDash(X, Y, S)
End Function
Function DrawNumber_7(X:Float, Y:Float, S:Float)
	DrawHtopST(X, Y, S)
	DrawFullS(X, Y, S)
End Function
Function DrawNumber_8(X:Float, Y:Float, S:Float)
	DrawLetter_O(X, Y, S)
	DrawFullDash(X, Y, S)
	
End Function

Function DrawNumber_9(X:Float, Y:Float, S:Float)

	DrawVrightST(X, Y, S)
	DrawHtopST(X, Y, S)
	DrawVleftTopST(X, Y, S)
	DrawFullDash(X, Y, S)
	
End Function




Function VecDrawText(Text:String, X:Int, Y:Int, Scale:Float = 6)
	Local Seperate:Float = Scale * 2
	For Local N:Int = 1 To Len(Text)
		If Mid:String(Upper(Text), N, 1) = "A" DrawLetter_A (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "B" DrawLetter_B (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "C" DrawLetter_C (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "D" DrawLetter_D (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "E" DrawLetter_E (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "F" DrawLetter_F (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "G" DrawLetter_G (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "H" DrawLetter_H (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "I" DrawLetter_I (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "J" DrawLetter_J (x + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "K" DrawLetter_K (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "L" DrawLetter_L (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "M" DrawLetter_M (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "N" DrawLetter_N (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "O" DrawLetter_O (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "P" DrawLetter_P (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "Q" drawletter_Q (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "R" drawletter_R (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "S" DrawLetter_S (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "T" DrawLetter_T (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "U" DrawLetter_U (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "V" DrawLetter_V (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "W" DrawLetter_W (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "X" DrawLetter_X (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "Y" DrawLetter_Y (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "Z" DrawLetter_Z (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "0" DrawNumber_0 (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "1" DrawNumber_1 (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "2" DrawNumber_2 (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "3" DrawNumber_3 (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "4" DrawNumber_4 (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "5" DrawNumber_5 (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "6" DrawNumber_6 (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "7" DrawNumber_7 (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "8" DrawNumber_8 (X + Seperate * N, Y, Scale)
		If Mid:String(Upper(Text), N, 1) = "9" DrawNumber_9 (X + Seperate * N, Y, Scale)
			

'		Print Mid:String(Upper(Text), N, 1)
	Next
End Function


Function VecDraw_img_Text(Text:String, X:Float, Y:Float, Scale:Float = FONTSIZE)
	SetScale scale *.1, scale *.1
	For Local n:Int = 0 To Text.Length - 1
		DrawImage(VecChar[Text[n] - 32], x, y)
		x:+scale * (FONTSIZE *.15)
	Next
	SetScale(1, 1)
End Function
'Local PrintText:String = "This is just a Test"

'Graphics 640, 480
'Repeat
'
'  If endtext = 0 Then
'  start = MilliSecs()
'  For Local z:Int =0 To 1000
'    VecDrawText(PrintText, 10, 10)
'  Next
'  start2 = MilliSecs()
'  For Local zz:Int = 0 To 1000
'    DrawText PrintText, 10, 30
'  Next
'  start3 = MilliSecs()
'  For Local zzz:Int = 0 To 1000
'    VecDraw_img_Text PrintText, 10, 50
'  Next
'  endtext = MilliSecs()
'  End If
'  DrawText "Time for 1000x VecDrawText() : " + Trim:String(start2 - start), 10, 100
'  DrawText "Time for 1000x DrawText ()   : " + Trim:String(start3 - start2), 10, 120
'  DrawText "Time for 1000x VecDraw_img_Text ()   : " + Trim:String(endtext - start3), 10, 140
'  
'
'
'
'Flip
'
'
'Until KeyHit(KEY_ESCAPE) Or AppTerminate()
'
'
</textarea><br>Filename "ShapeInvaderz.bmx"  <br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

'HOUR 21, 3 more to go!
Include "VectorText.bmx"
'DrawGeom function is used to draw all the shapes in the game.
Function DrawGeom(x:Float, y:Float, sides:Float, length:Float, angle:Float, r:Int = 255, g:Int = 255, b:Int = 255, glow:Int = True)

	Local aStep# = 360 / sides
	Length:Float = (length / 2) / Sin(aStep:Float / 2) 'Calculate the correct length of a side 			
	For Local a:Byte = 0 To sides - 1
		Local x1:Float = x:Float - (Sin(angle + (aStep * a)) * length)
		Local y1:Float = y:Float - (Cos(angle + (aStep * a)) * length)
		Local x2:Float = x:Float - (Sin(angle + (aStep * (a + 1))) * length)
		Local y2:Float = y:Float - (Cos(angle + (aStep * (a + 1))) * length)
		If glow = True
			SetColor r / 4, g / 4, b / 4
			DrawOval x1 - 5, y1 - 5, 11, 11							' Draw Circle at Vertex
		End If
		SetColor r, g, b
		DrawLine x1, y1, x2, y2									' Draw Connecting Lines
	Next
	
End Function

'Initialize the Game *****************************************
	'Set the playfield
	Graphics 320, 260
	'SetOrigin 320 / 2, 240 / 2
	'Declare Global Variables
	Global Direction:Int
	Global GameState:Int
	Global Level:Int
	Global PS:Int
	Global Score:Int
	Global gblEnemySpeed:Float
	Global Lives:Int
	
	'Speed Flags
	Global SpeedBoost_A:Int
	Global SpeedBoost_B:Int
	Global SpeedBoost_C:Int
	Global SpeedBoost_D:Int
	Global SpeedBoost_E:Int
	Global SpeedBoost_F:Int
			
	'Declare Constants
	Const MOVELEFT:Int = 0
	Const MOVERIGHT:Int = 1
	Const STATE_TITLE:Int = 0
	Const STATE_PLAY:Int = 1
	Const STATE_PAUSE:Int = 2
	Const STATE_WIN:Int = 3
	Const STATE_FAIL:Int = 4
	
	'Create the superlist
	Global SuperList:TList = New TList
	SuperList.AddLast(tEnemy.List)
	SuperList.AddLast(tBullet.List)
	SuperList.AddLast(tPlayerShot.List)
	SuperList.AddLast(tPlayer.List)
	SuperList.AddLast(tShield.List)
	
'Initialize the Graphics *****************************
	SetBlend LIGHTBLEND
	Cls
	AutoMidHandle(True)
	DrawGeom(16, 16, 5, 10, 0, 255, 0, 0, False) 'EnemyType 1
	Global RedPentagon:TImage = CreateImage(32, 32, 1)
	GrabImage RedPentagon, 0, 0
	MidHandleImage RedPentagon
	Cls
	DrawGeom(16, 16, 4, 10, 0, 0, 200, 0, False) 'EnemyType 2
	Global GreenSquare:TImage = CreateImage(32, 32, 1, dynamicimage | maskedimage)
	GrabImage GreenSquare, 0, 0
	Cls
	DrawGeom(16, 16, 6, 10, 0, 255, 0, 200, False) 'EnemyType 3
	Global PurpleHexagon:TImage = CreateImage(32, 32, 1, DYNAMICIMAGE | MASKEDIMAGE)
	GrabImage PurpleHexagon, 0, 0
	Cls
	DrawGeom(16, 16, 3, 10, 0, 90, 90, 200, False) 'EnemyType 4
	Global BlueTriangle:TImage = CreateImage(32, 32, 1, DYNAMICIMAGE | MASKEDIMAGE)
	GrabImage BlueTriangle, 0, 0
	
	Cls
	'Make the Player Image then save it to memory
	For Local N:Int = 2 To 24 Step 2
		DrawGeom(16, 16, 3, 00 + N, 0, 160 - (N * 10), 200 - (N * 10), 100 - (N * 10), False)
	Next
	
	Global PlayerTriangle:TImage = CreateImage(32, 32, 1, DYNAMICIMAGE | MASKEDIMAGE)
	GrabImage PlayerTriangle, 0, 0
	Cls
	'Make the Shield Part Image and SAVE
	
	SetColor 0, 100, 0
	DrawRect(0, 0, 4, 2)
	SetColor 0, 60, 100
	DrawRect (1, 1, 3, 1)
	
	Global ShieldImage:TImage = CreateImage(4, 2, 1, DYNAMICIMAGE | MASKEDIMAGE)
	GrabImage ShieldImage, 0, 0
	'Make the bullets and save them
	Cls
	SetColor 255, 0, 0
	DrawRect (0, 0, 3, 7)
	SetColor 255, 255, 0
	DrawRect (0, 0, 2, 6)
	Global EnemyBulletImage:TImage = CreateImage(4, 8, 1, DYNAMICIMAGE | MASKEDIMAGE)
	GrabImage EnemyBulletImage, 0, 0
	Cls
	SetColor 0, 50, 50
	DrawRect (0, 0, 8, 8)
	SetColor 0, 155, 155
	DrawRect (3, 2, 1, 4)
	Global PlayerShotImage:TImage = CreateImage (6, 6, 1, DYNAMICIMAGE | MASKEDIMAGE)
	GrabImage PlayerShotImage, 0, 0
	SetColor 255, 255, 255
'All objects contain x,y,xv,and yv variables
Type tGameObject
	Field x:Float, y:Float, xv:Float, yv:Float, Image:TImage
	Method update() Abstract
End Type

Type tEnemy Extends tGameObject

	Global List:TList = New TList
	'Create additional fields that are used for the Enemy Type
	Field r:Int, g:Int, b:Int 'Colorsettings 
	Field Sides:Float 'Sides settings
	Field Level:Int 'Level the Enemy is 
	Field EnemyType:Int 'Type the Enemy is
	'The Make function is used to create and confiugre a new Instance of the Enemy
	Function Make(x:Float, y:Float, EnemyType:Int = 1, Level:Int)
		Local NewEnemy:tEnemy = New tEnemy
		NewEnemy.x = x
		NewEnemy.y = y
		NewEnemy.EnemyType = EnemyType
		NewEnemy.Level = Level
		If enemytype = 1 Then newenemy.Image = RedPentagon
		If enemytype = 2 Then newenemy.Image = GreenSquare
		If enemytype = 3 Then newenemy.Image = PurpleHexagon
		If enemytype = 4 Then newenemy.Image = BlueTriangle
		'Depending on the type, we confugre it's shape and color
		'Let's add it to the List
		List.AddLast(NewEnemy)
	End Function
	
	'The Update Method updates the instance every step
	Method Update()
		'Determine it's direction and move accordingly
		If Direction = MOVELEFT x:-gblEnemySpeed
		If Direction = MOVERIGHT x:+gblEnemySpeed
		'If any Enemy it's the edge of the screen, 
		'move all enemies downward and reverse the direction
		'Also count the enemy list, speed up relative to the list
		If x &lt; 0
			Direction = MOVERIGHT
			For Local Enemy:tEnemy = EachIn List
				Enemy.y:+4
			Next
		End If
		If x &gt; 320
			Direction = MOVELEFT
			For Local Enemy:tEnemy = EachIn List
				Enemy.y:+4
			Next
		End If
		'If the Enemy reaches the bottom, the game ends
		If y &gt; 220 Then GameState = STATE_FAIL
		'Draw the Enemy
		DrawImage (Image, x, y)

		' make it shoot
		Local R:Int = Rnd(1, 100 + List.Count() * 10)
		'The Higher the level the more frequent they shoot
		If R &lt; Level + 1 And tBullet.List.Count() &lt; Level * 2 tBullet.make(x, y)
		'Check for a collision with the Shields
		If y &gt; 160
			For Local Shield:tShield = EachIn tShield.List
				If Abs(Shield.x - x) &lt; 16 And Abs(Shield.y - y) &lt; 16
					tShield.List.Remove(Shield)
				End If
			Next
		End If
	End Method
End Type

Type tPlayer Extends tGameObject
	Field ShotDelay:Float
	Global List:TList = New TList
	
	Function Make()
		Local NewPlayer:tPlayer = New tPlayer
		NewPlayer.x = 320 / 2
		NewPlayer.y = 232
		newplayer.Image = PlayerTriangle
		tPlayer.List.AddLast(NewPlayer)
	End Function
	
	Method Update()
		'Increment the ShotDelay
		ShotDelay:+1
		'Playership is controlled with arrow keys and Z
		If KeyDown(KEY_RIGHT) x:+3
		If KeyDown(KEY_LEFT) x:-3
		'Shoot if the ShotDelay permits
		If KeyHit(KEY_Z) Or KeyHit(KEY_RCONTROL) Or KeyHit(KEY_LCONTROL) And ShotDelay &gt; 5
			If tPlayerShot.List.Count() &lt; 2 tPlayerShot.Make(x, y) ;ShotDelay = 0
		End If
		'Bound the player in the playfield
		If x &lt; 0 x = 0
		If x &gt; 304 x = 304
		'Draw the player
		DrawImage(Image, x, y)
	End Method
End Type

Type tBullet Extends tGameObject
	Global List:TList = New TList
	'Make Function Called when instance is created
	Function Make(x:Float, y:Float)
		Local Bullet:tBullet = New tBullet
		Bullet.x = x
		Bullet.y = y
		Bullet.yv = 1 + Level *.10
		bullet.Image = EnemyBulletImage
		tBullet.List.AddLast(Bullet)
	End Function
	
	'Update Function called with each step
	Method Update()
		y:+yv 'Move the bulled downard, remove if out of bounds
		If y &gt; 244 Then tBullet.List.Remove(Self)
		DrawImage(Image, x, y)
		'Check for a collision with the Player
		For Local Player:tPlayer = EachIn tPlayer.List
			If Abs(Player.x - x - 2) &lt; 10 And Abs(Player.y - y) &lt; 8
				tBullet.List.Remove(Self)
				GameState = STATE_FAIL
				
			End If
		Next
		'Check for a collision with the shields
		For Local Shield:tShield = EachIn tShield.List
			If Abs(Shield.x - x) &lt; 3 And Abs(Shield.y - y) &lt; 4
				List.Remove(Self)
				Shield.List.Remove(Shield)
			End If
		Next
	End Method
End Type

Type tPlayerShot Extends tGameObject
	Global List:TList = New TList
	
	'Make Function called when the Instance is created
	Function Make(x:Float, y:Float)
		Local PlayerShot:tPlayerShot = New tPlayerShot
		playershot.Image = PlayerShotImage
		PlayerShot.x = x
		PlayerShot.y = y
		PlayerShot.yv = -6 'sets the speed to go upward 6 pixels per step
		List.AddLast(PlayerShot)
	End Function
	
	Method Update()
		y:+yv 'Move the shot
		'Destroy the instance if out of bounds
		If y &lt; - 4 Then List.Remove(Self)
		'Check for a collision with enemies
		For Local Enemy:tEnemy = EachIn tEnemy.List
			If Abs(Enemy.x - x - 2) &lt; 8 And Abs(Enemy.y - y) &lt; 10
				If Enemy.EnemyType = 4 Score:+1 + Level
				If Enemy.EnemyType = 2 Score:+3 + Level
				If Enemy.EnemyType = 1 Score:+15 + Level
				If Enemy.EnemyType = 3 Score:+70 + Level
				tEnemy.List.Remove(Enemy)
				List.Remove(Self)
				gblEnemySpeed:+.01
			End If
		Next
		'Check for a collision with shields
		For Local Shield:tShield = EachIn tShield.List
			If Abs(Shield.x - x) &lt; 3 And Abs(Shield.y - y) &lt; 5
				List.Remove(Self)
				tShield.List.Remove(Shield)
			End If
		Next
		DrawImage Image, x, y
	End Method
End Type

Type tShield Extends tGameObject
	Global List:TList = New TList
	
	Function Make(x:Float, y:Float)
		Local NewShield:tShield = New tShield
		NewShield.x = x
		NewShield.y = y
		newshield.Image = ShieldImage
		List.AddLast(NewShield)
	End Function
	Method Update()
		'draw on screen
		DrawImage (Image, x, y)
		
		
		
	End Method
End Type

Function MakeGame(Level:Int)
	Global List:TList = New TList
	gblEnemySpeed = Level *.05
	tPlayer.Make()
	SpeedBoost_A = False
	SpeedBoost_B = False
	SpeedBoost_C = False
	SpeedBoost_D = False
	SpeedBoost_E = False
	SpeedBoost_F = False
	For Local N:Int = 2 To 16
		tEnemy.Make(N * 20, 16 * 2, 3, Level)
	Next
	For Local N:Int = 2 To 16
		tEnemy.Make(N * 20, 16 * 4, 1, Level)
	Next
	For Local N:Int = 2 To 16
		tEnemy.Make(N * 20, 16 * 6, 2, Level)
	Next
	For Local N:Int = 2 To 16
		tEnemy.Make(N * 20, 16 * 8, 4, Level)
	Next

	'make the shields ********************************
	'Left Shield:

	For Local ShieldStep:Int = 1 To 20 Step 5
		For Local a_N:Int = 2 To 4 Step 2
			For Local N:Int = 3 To 8
				tShield.Make(N * 4 + 16 * ShieldStep, 176 + a_N)
			Next
		Next
		For Local a_N:Int = 2 To 20 Step 2
			For Local N:Int = 1 To 10
				tShield.Make(N * 4 + 16 * ShieldStep, 180 + a_N)
			Next
		Next
		For Local a_N:Int = 2 To 10 Step 2
			For Local N:Int = 1 To 3
				tShield.Make(N * 4 + 16 * ShieldStep, 200 + a_N)
			Next
		Next
		For Local a_N:Int = 2 To 10 Step 2
			For Local N:Int = 8 To 10
				tShield.Make(N * 4 + 16 * ShieldStep, 200 + a_N)
			Next
		Next
	Next


	
			
End Function

Function ClearEverything()
	For Local a_list:TList = EachIn SuperList
		a_list.Clear()
	Next
End Function

SetBlend LIGHTBLEND
'MAIN LOOP
Repeat
	Cls
	'Determine the GameState and act accordingly
	Select GameState
		Case STATE_TITLE
			VecDrawText "Shape Invaderz", - 10, 10, 11
			VecDrawText "by Matt McFarland", 134, 30, 5
			VecDraw_img_Text "ARROW KEYS", 30, 110, 11
			VecDraw_img_Text "Z/CTRL KEY", 30, 130, 11
			VecDraw_img_Text "P KEY", 30, 150, 11
			VecDraw_img_Text "Move Ship", 200, 110, 11
			VecDraw_img_Text "Shoot", 200, 130, 11
			VecDraw_img_Text "Pause", 200, 150, 11
			VecDrawText "Press SPACE to play or ESC to Exit", 0, 220, 4.5

			If KeyHit(KEY_SPACE)
				Level = 1
				SCORE = 0
				MakeGame(Level)
				GameState = STATE_PLAY
			End If
			
		Case STATE_PLAY
			'Update All Objects
			For Local a_list:TList = EachIn SuperList
				For Local an_object:tGameObject = EachIn a_list
					an_object.update()
				Next
			Next
			
			'Speed the game up depending on enemy count
			Local eCount:Int = tEnemy.List.Count()
			If eCount = 30 And SpeedBoost_A = False gblEnemySpeed:+.10;SpeedBoost_A = True
			If eCount = 20 And SpeedBoost_B = False gblEnemySpeed:+.15;SpeedBoost_B = True
			If eCount = 10 And SpeedBoost_C = False gblEnemySpeed:+.15;SpeedBoost_C = True
			If eCount = 5 And SpeedBoost_D = False gblEnemySpeed:+.30;SpeedBoost_D = True
			If eCount = 3 And SpeedBoost_E = False gblEnemySpeed:+.60;SpeedBoost_E = True
			If eCount = 1 And SpeedBoost_F = False gblEnemySpeed:+1.80;SpeedBoost_F = True

			'Pause the screen if player pushes the P Key			
			If KeyHit (KEY_P) GameState = STATE_PAUSE
			
			'Draw HUD
			SetColor 0, 0, 200
			DrawLine 0, 240, 320, 240
			SetColor 255, 255, 255
			VecDraw_img_Text "Lvl:" + String(Level), 260, 244, 11
			VecDraw_img_Text "Score:" + String(Score), 10, 244, 11
			If CountList(tEnemy.List:TList) &lt; 1 GameState = STATE_WIN
			
			
		Case STATE_PAUSE
			'PS is used to Scroll the "Game paused" text
			PS:+1
			If PS &gt; 320 PS = -320
			VecDrawText "Game PAUSED, Press SPACE to Continue", PS, 210
			If KeyHit(KEY_SPACE) GameState = STATE_PLAY

		Case STATE_WIN
			VecDrawText "WIN!", - 40, 100, 44
			VecDrawText "You Have owned Level " + String(Level) + "!!!!!!!!!!!!", 18, 60
			VecDrawText "Press SPACE to CONTINUE or ESC to Exit", 02, 240, 4

			If KeyHit(KEY_SPACE)
				ClearEverything()
				Level:+1
				MakeGame(Level)
				GameState = STATE_PLAY			
			End If
			
			If KeyHit(KEY_ESCAPE)
				ClearEverything()
				GameState = STATE_TITLE
			End If
			
		Case STATE_FAIL
			VecDrawText "FAIL!", - 70, 100, 42
			VecDrawText "You DIED on Level " + String(Level) + "!!!!!!!!!!!!", 30, 40
			VecDrawText "You Scored " + String(Score) + " Points FTW", 10, 190
			VecDrawText "Press SPACE to CONTINUE or ESC to Exit", 02, 240, 4
			
			If KeyHit(KEY_SPACE)
				ClearEverything()
				Level = 1
				GameState = STATE_TITLE
				SetColor 255, 255, 255
			End If
		
	End Select
	Flip
Until KeyHit (KEY_ESCAPE)
</textarea><br><br>Download the .8 version on gamedev here:<br><a href="http://www.gamedev.net/community/gds/viewentry.asp?projectid=548722&amp;mode=gameinfo" target="_blank">http://www.gamedev.net/community/gds/viewentry.asp?projectid=548722&amp;mode=gameinfo</a><br><br>Enjoy!!!<br><br>PS: @ Jesse, thanks :)  No worries.  I plan on using arrays more too.   Sorry about the rant about arrays, kinda irrelevent.  I'll check out your source, sounds interesting. <br><br></td></tr></table><br>
<a name="987227"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#46">[#46]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> I was just trying to say that a group of lists in a list is not such a great idea,that arrays are faster and a better choice <br></div> I don't think it will make that much difference, at the end of the day.<br><br>It's possible you could consider having only one list that stores all your objects, rather than multiple lists tied together. <br><br></td></tr></table><br>
<a name="987255"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#47">[#47]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>I don't think it will make that much difference, at the end of the day.<br> <br></div><br>I think it does, although I haven't really tested it. but I bet when you have hundreds of bullets and hundreds particles per say, and an instruction set have to determine if to assign it to a specific type of object or not, logic tells me it will definitely take a speed hit. <br>Have you tried Grid Wars, from Mark Incitti if you haven't you can download the game including source code from my signature link. One of the reasons this game is so fast is his use of recycling of objects and use of arrays. nothing to figure out, no garbage collector taking over your program, no memory fragmentation, and no guessing. just smart coding. :) <br><br></td></tr></table><br>
<a name="987285"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#48">[#48]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> and an instruction set have to determine if to assign it to a specific type of object or not, logic tells me it will definitely take a speed hit. <br></div>What does this mean? <br><br></td></tr></table><br>
<a name="987311"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#49">[#49]</a></td></tr></table></td></tr><tr ><td class="posttext"> I know I am really bad at explaining stuff. Sorry.<br>an "instruction set" is a set of code instructions assigned to do a specific task. it can be a function/call or even an simple if statement. Does that help? maybe not. Sorry, best I can do.<br><br>There have to be code written to determine whether to assign the object type to a specified variable or not. When you have all different type of objects in a  list, how is it determined to put one type of object into a variable or not when doing a For/EachIn loop. There is obviously some hidden code added to the executable by the compiler at the time of creating to deal with this type of situation. But then again, maybe I am wrong. I don't really know what's inside the Processor or the Bios. :)<br><br>Just ignore me. I am in a different, and weird mentality. <br><br></td></tr></table><br>
<a name="987313"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#50">[#50]</a></td></tr></table></td></tr><tr ><td class="posttext"> If all the objects in the list are of the same type, does that remove the problem? As each list only contains one type of object (tenemy, tplayer..) <br><br></td></tr></table><br>
<a name="987327"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#51">[#51]</a></td></tr></table></td></tr><tr ><td class="posttext"> don't really know but I would guess the more it varies the more the work. consider it still have to go through all of the objects even if it only have to test one kind of object for collision or anything similar. <br><br></td></tr></table><br>
<a name="987434"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#52">[#52]</a></td></tr></table></td></tr><tr ><td class="posttext"> That is going to happen anyway, to draw the objects and move the aliens and bullets, is it not? <br><br></td></tr></table><br>
<a name="987440"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#53">[#53]</a></td></tr></table></td></tr><tr ><td class="posttext"> if you say so. Personally, I wouldn't do it that way. maybe it's my inexperience but I do think my way is faster/better. who knows, I might end up figuring out in the long run that your way is better. <br><br></td></tr></table><br>
<a name="987470"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#54">[#54]</a></td></tr></table></td></tr><tr ><td class="posttext"> It's not that I'm saying my way is faster, per se, but rather on today's computers, it doesn't make enough difference to lose any sleep over ;) <br><br></td></tr></table><br>
<a name="988184"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matt McFarland</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#55">[#55]</a></td></tr></table></td></tr><tr ><td class="posttext"> The game is fully finished and when compiled its only 64kb.  Check the <a href="/posts.php?topic=87204" target="_blank">showcase</a> for details! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
