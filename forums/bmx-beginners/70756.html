<!DOCTYPE html><html lang="en" ><head ><title >Drawing rotated ellipses, Please help.</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Drawing rotated ellipses, Please help.</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >Drawing rotated ellipses, Please help.</a><br><br>
<a name="790938"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ryan Burnside</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Once again I've come across a problem that I need assistance with. The goal was to draw a rotated ellipse if a given width and height. My initial code could produce the ellipse with no rotation. The code simply used a for loop and n many points. I found that this method worked but the ellipses formed were not always symmetrical. This probably arises from the fact that a single center point was used regardless if the shape was odd or even in width and height. This is for a lower resolution game so odd shaped ellipses aren't passable and anti aliasing would ruin the feeling of the art. I really need symmetry in these n-gon circles.<br><br>The second problem is how to go about rotating the shapes at a given angle. I tinkered around a bit with my code but could never actually implement rotation.<br><br><br>The two problems at a glance:<br>1. Symmetry is important here with no anti aliasing<br>2. Rotation of the ellipse is unclear to me.<br><br>The code:<br><pre class=code>
Function draw_oval(centerx#,centery#,width#,height#,rot#,n#)
	' draw the shape with a for loop
	Local angle#=360.0/n
	Local r1#=width/2.0
	Local r2#=height/2.0
	For Local i=0 To n-1
		Local x1=centerx+Cos(i*angle)*r1
		Local x2=centerx+Cos((i+1)*angle)*r1
		Local y1=centery+Sin(i*angle)*r2
		Local y2=centery+Sin((i+1)*angle)*r2
		DrawLine(x1,y1,x2,y2)
	Next
End Function
</pre><br><br>A picture showing both unsymmetrical ellipses and the intended result with rotation parameter. If you look at the quadrants of the ellipse you can see that 1 and 2 are more round that qudrants 3 and 4.<br><br><img src="http://img528.imageshack.us/img528/9909/circlesoi6.png"> <br><br></td></tr></table><br>
<a name="790946"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Azathoth</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Something like:<br><pre class=code>Graphics 640,480
i=0
While Not KeyHit(KEY_ESCAPE)
	Cls
	SetColor 255,255,255
	SetHandle 150/2,50/2
	SetRotation i
	DrawOval 100,100,150,50
	i:+1
	If i=360 Then i=0
	Flip
Wend</pre> <br><br></td></tr></table><br>
<a name="790951"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ryan Burnside</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> No, the need to be n-gons and unfilled. <br><br></td></tr></table><br>
<a name="790961"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >assari</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> How about something like this<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Function CreatePolygon:TImage(width#,height#,n#)
	' draw the shape with a for loop
	Local image:TImage=CreateImage(width+2,height+2,1,DYNAMICIMAGE|FILTEREDIMAGE)
	Local angle#=360.0/n
	Local r1=width/2.0
	Local r2=height/2.0
	Local centerx:Float = width / 2.0
	Local centery:Float = height / 2.0

	For Local i=0 To n-1
		Local x1=centerx+Cos(i*angle)*r1
		Local x2=centerx+Cos((i+1)*angle)*r1
		Local y1=centery+Sin(i*angle)*r2
		Local y2=centery+Sin((i+1)*angle)*r2
		DrawLine(x1 , y1 , x2 , y2)
	Next
	GrabImage Image , 0 , 0
	Return Image
End Function

Graphics 640 , 480
AutoMidHandle True
Local Polygon:TImage=CreatePolygon(100 , 200 ,33)
Local rot:Float = 0

Repeat
	Cls
	SetRotation rot
	DrawImage Polygon,320,240
	Flip
	Delay(2000)
	rot :+ 20.0
Until AppTerminate()
</textarea> <br><br></td></tr></table><br>
<a name="790970"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ryan Burnside</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for all the help so far.<br><br>After a brief head explosion and some mopping of the floor I came up with this.  <pre class=code>
Function draw_oval(centerx#,centery#,width#,height#,rot#,n#)
	' draw the shape with a for loop
	
	
	Local angle#=360.0/n
	Local r1#=width/2.0
	Local r2#=height/2.0

	For Local i#=0 To n-1
	
		Local x1#=centerx+Cos(i*angle)*r1
		Local y1#=centery+Sin(i*angle)*r2
		Local x2#=centerx+Cos((i+1)*angle)*r1
		Local y2#=centery+Sin((i+1)*angle)*r2
		' get new data
		Local d1#=point_distance(centerx,centery,x1,y1)
		Local a1#=point_direction(centerx,centery,x1,y1)
		Local fx#=centerx+Cos(a1+rot)*d1
		Local fy#=centery+Sin(a1+rot)*d1
		
		Local d2#=point_distance(centerx,centery,x2,y2)
		Local a2#=point_direction(centerx,centery,x2,y2)
		Local fx2#=centerx+Cos(a2+rot)*d2
		Local fy2#=centery+Sin(a2+rot)*d2
	
		DrawLine(fx,fy,fx2,fy2)
		
	Next
End Function
</pre><br><br>The problem if drawing even width ovals remains. I don't know how to draw an oval with an even width. Using a central pixel always will make the width odd. If I tell it to draw a 32x32 oval I get a 31x31 oval. <br><br></td></tr></table><br>
<a name="791011"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DJWoodgate</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Maybe a bit faster.  Have not really tested it much though, so not sure about the even width problem you mention.<br><br><pre class=code>
Function draw_oval(centerx#,centery#,width#,height#,rot#,n#)
	' draw the shape with a for loop

	Local angle# = 360.0/n
	Local r1# = width/2.0
	Local r2# = height/2.0
	
	Local crot# = Cos(rot)
	Local srot# = Sin(rot)

	Local lx# = centerx + r1 * crot
	Local ly# = centery + r1 * srot

	For Local i#=1 To n
	
		Local rx# = Cos(i*angle)*r1
		Local ry# = Sin(i*angle)*r2
		
		Local tx# = centerx+(rx * crot - ry * srot)
		Local ty# = centery+(rx * srot + ry * crot)
	
		DrawLine(lx,ly,tx,ty,False)
		
		lx=tx
		ly=ty
		
	Next
	
End Function
</pre> <br><br></td></tr></table><br>
<a name="791050"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ryan Burnside</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok thanks for the tip. I was double calculating I suppose...<br><br>I do NOT want to use rotated pixmaps for this because this bit of code is for a drawing program for artists. I need precision that tends to make symmetrical circles rather than mathmatically represented ones on pixels. Why do some circles produced lack symmetry? It's as if the plotting method truncates fractional pixel values rather than rounding them. <br><br></td></tr></table><br>
<a name="791086"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DJWoodgate</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am not sure exactly how it works in DirectX.  However my driver offers an option to change the pixel centre so as far as I can see you can't be certain exactly which pixel will be filled for fractional values without running some sort of profiling routine...<br><br>Maybe there is some way to get DX to cough up that information?<br><br>Failing that perhaps the best option is to do your own rounding and truncation. <br><br></td></tr></table><br>
<a name="791517"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Look up a fully integer-based bresenham-like rotatable ellipse routine, it will be much quicker than doing all that trigonometry. <br><br></td></tr></table><br>
<a name="793263"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dreamora</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> and much slower again due to the 2D through 3D implementation and the need to draw to pixmap and draw that one to the backbuffer. <br><br></td></tr></table><br>
<a name="793264"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> You want to draw the ellipse rotating directly to the display in realtime, I take it?<br><br>You could assemble a vertex array and pass it to OpenGL <br><br></td></tr></table><br>
<a name="1086741"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jimmy</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> No one seem to actually post a Bresenham like code for rotated ellipses solution, only reffering to them, so I post it here.<br><br>It produces excellent and smooth ellipses, which is rather scarce thing to see these days.<br><br><pre class=code>
Graphics  800,600,16,2:SetBuffer BackBuffer()
Repeat:Cls:xx=MouseX():yy=MouseY()
Color 0,255,0:ellipse(512,384,1+yy/2.0,200,xx/200.0)
Color 255,255,255:Plot 512,384:Plot 512+100,384:Plot 512-100,384:Plot 512,384+200:Plot 512,384-200
Flip
Until MouseDown(2)

Function ellipse (iXC#,iYC#,A#,B#,angle#)
;
; General ellipse
;
; Known bugs:
; jumpy movement sometimes, as it uses huge numbers, too large To fit storage
; It has few lines with trigonometry, these could very possibly be replaced with integer table or 
; tables of degrees Or similiar. It wraps the angle into an acceptable range, and sets xflag accordingly.
;
R2D# = 180.0/Pi:D2R# = Pi/180.0
If angle# &lt; Pi/2.0
xflag#=1
ElseIf angle#&gt;Pi/2 And angle#&lt;Pi
temp#=angle#-Pi/2.0:angle#=Pi/2.0-temp#:xflag#=-1
ElseIf angle#&gt;=Pi And angle#&lt;Pi*1.5
angle#=angle#-Pi:xflag#=1
ElseIf angle#&gt;=Pi*1.5 And angle#&lt;Pi*2.0
angle#=angle#-Pi:temp#=angle#-Pi/2.0:angle#=Pi/2.0-temp#:xflag#=-1
EndIf

; ...To output these 4 integer points. (and the said xflag), These are where major and minor axis of ellipse ends (determines shape as in aligned axis algorithms).
ixa#=Int(Cos(angle#*R2D#)*A#)
iya#=Int(Sin(angle#*R2D#)*A#)
ixb#=Int(Cos((angle#+(Pi/2.0))*R2D#)*B#)
iyb#=Int(Sin((angle#+(Pi/2.0))*R2D#)*B#)

; rest of code uses only variables:
; ixa,iya,ixb,iyb
; ixc, iyc, xflag
; these are as every one else from now on, integers

Plot ixc#-ixa#,iyc#-iya#
Plot ixc#-ixb#,iyc#-iyb#
Plot ixc#-ixb#,iyc#-iya#
Plot ixc#-ixa#,iyc#-iyb#

; From now on, integers only, it uses multiplication much, and needs very large integers (in large resolutions even more than 64bits)
; therefor single precision are used to alloud the space, but theres no fixed point Or floating point involved.

   ixa2#=ixa#*ixa#
   iya2#=iya#*iya#
   ixb2#=ixb#*ixb#
   iyb2#=iyb#*iyb#
   ixaya#=ixa#*iya#
   ixbyb#=ixb#*iyb#
   ila2#=ixa2#+iya2#
   ila4#=ila2#*ila2#
   ilb2#=ixb2#+iyb2#
   ilb4#=ilb2#*ilb2#
   ia#=ixa2#*ilb4#+ixb2#*ila4#
   ib#=ixaya#*ilb4#+ixbyb#*ila4#
   ic#=iya2#*ilb4#+iyb2#*ila4#
   id#=ila4#*ilb4#

   If iYA# &lt;= iXA#
       ; Start AT (-xA,-yA) 
       iX# = -iXA#:iY# = -iYA#:iDx# = -(iB#*iXA#+iC#*iYA#):iDy# = iA#*iXA#+iB#*iYA# 

       ; Arc FROM (-xA,-yA) TO point (x0,y0) where dx/dy = 0 
       While iDx# &lt;= 0
           Plot iXC#+iX#*xflag#,iYC#+iY#
           Plot iXC#-iX#*xflag#,iYC#-iY#
           iY#=iY#+1
           iSigma# = iA#*iX#*iX#+2*iB#*iX#*iY#+iC#*iY#*iY#-iD# 
           If iSigma# &lt; 0 Then iDx#=iDx#-iB#:iDy#=iDy#+iA#:iX#=iX#-1
           iDx#=iDx# + iC# 
           iDy#=iDy# - iB# 
       Wend 

       ; Arc FROM (x0,y0) TO point (x1,y1) where dy/dx = 1 
       While iDx# &lt;= iDy# 
           Plot iXC#+iX#*xflag#,iYC#+iY# 
           Plot iXC#-iX#*xflag#,iYC#-iY# 
           iY#=iY#+1
           iXp1# = iX#+1 
           iSigma# = iA#*iXp1#*iXp1#+2*iB#*iXp1#*iY#+iC#*iY#*iY#-iD# 
           If iSigma# &gt;= 0 Then iDx#=iDx# + iB#:iDy#=iDy# - iA#:iX# = iXp1# 
           iDx#=iDx# + iC# 
           iDy#=iDy# - iB# 
       Wend 

       ; Arc FROM (x1,y1) TO point (x2,y2) where dy/dx = 0 
       While iDy# &gt;= 0
           Plot iXC#+iX#*xflag#,iYC#+iY#
           Plot iXC#-iX#*xflag#,iYC#-iY#
           iX=iX+1 
           iSigma# = iA#*iX#*iX#+2*iB#*iX#*iY#+iC#*iY#*iY#-iD# 
           If iSigma# &lt; 0 Then iDx#=iDx# + iC# : iDy#=iDy# - iB# : iY#=iY#+1 
           iDx#=iDx# + iB# 
           iDy#=iDy# - iA# 
       Wend 

       ; Arc FROM (x2,y2) TO point (x3,y3) where dy/dx = -1 
       While iDy# &gt;= -iDx# 
           Plot iXC#+iX#*xflag#,iYC#+iY#
           Plot iXC#-iX#*xflag#,iYC#-iY#
           iX#=iX#+1 
           iYm1# = iY#-1 
           iSigma# = iA#*iX#*iX#+2*iB#*iX#*iYm1#+iC#*iYm1#*iYm1#-iD# 
           If iSigma# &gt;= 0 Then iDx#=iDx# - iC#:iDy#=iDy# + iB#:iY# = iYm1# 
           iDx#=iDx# + iB# 
           iDy#=iDy# - iA# 
       Wend 

       ; Arc FROM (x3,y3) TO (xa,ya) 
       While iY# &gt;= iYA# 
           Plot iXC#+iX#*xflag#,iYC#+iY#
           Plot iXC#-iX#*xflag#,iYC#-iY#
           iY#=iY#-1
           iSigma# = iA#*iX#*iX#+2*iB#*iX#*iY#+iC#*iY#*iY#-iD# 
           If iSigma# &lt; 0 Then iDx#=iDx# + iB#:iDy#=iDy# - iA#:iX#=iX#+1 
           iDx#=iDx# - iC# 
           iDy#=iDy# + iB# 
       Wend 

   Else 
       ; Start AT (-xa,-ya) 
       iX# = -iXA#:iY# = -iYA#:iDx# = -(iB#*iXA#+iC#*iYA#):iDy# = iA#*iXA#+iB#*iYA#

       ; Arc FROM (-xa,-ya) TO point (x0,y0) where dy/dx = -1 
       While -iDx# &gt;= iDy# 
           Plot iXC#+iX#*xflag#,iYC#+iY#
           Plot iXC#-iX#*xflag#,iYC#-iY# 
           iX#=iX#-1
           iYp1# = iY#+1 
           iSigma# = iA#*iX#*iX#+2*iB#*iX#*iYp1#+iC#*iYp1#*iYp1#-iD# 
           If iSigma# &gt;= 0 Then iDx#=iDx# + iC# : iDy#=iDy# - iB#:iY# = iYp1# 
           iDx#=IDx# - iB# 
           iDy#=Idy# +iA# 
       Wend 

       ; Arc FROM (x0,y0) TO point (x1,y1) where dx/dy = 0 
       While iDx# &lt;= 0
           Plot iXC#+iX#*xflag#,iYC#+iY#
           Plot iXC#-iX#*xflag#,iYC#-iY#
           iY#=iY#+1
           iSigma# = iA#*iX#*iX#+2*iB#*iX#*iY#+iC#*iY#*iY#-iD# 
           If iSigma# &lt; 0 Then iDx#=iDx# - iB#:iDy#=iDy# + iA#:iX#=iX#-1
           iDx#=iDx# + iC# 
           iDy#=iDy# - iB# 
       Wend 

       ; Arc FROM (x1,y1) TO point (x2,y2) where dy/dx = 1 
       While iDx# &lt;= iDy# 
           Plot iXC#+iX#*xflag#,iYC#+iY#
           Plot iXC#-iX#*xflag#,iYC#-iY#
           iY#=iY#+1
           iXp1# = iX#+1 
           iSigma# = iA#*iXp1#*iXp1#+2*iB#*iXp1#*iY#+iC#*iY#*iY#-iD# 
           If iSigma# &gt;= 0 Then iDx#=IDx# + iB# :iDy#=Idy# - iA# : iX# = iXp1# 
           iDx#=iDx# + iC# 
           iDy#=iDy# - iB# 
       Wend 

       ; Arc FROM (x2,y2) TO point (x3,y3) where dy/dx = 0 
       While iDy# &gt;= 0
           Plot iXC#+iX#*xflag#,iYC#+iY#
           Plot iXC#-iX#*xflag#,iYC#-iY# 
           iX#=iX#+1 
           iSigma# = iA#*iX#*iX#+2*iB#*iX#*iY#+iC#*iY#*iY#-iD# 
           If iSigma# &lt; 0 Then iDx#=iDx# + iC# : iDy#=iDy# - iB# : iY#=iY#+1 
           iDx#=iDx# + iB# 
           iDy#=iDy# - iA# 
       Wend 

       ; Arc FROM (x3,y3) TO (xa,ya) 
       While iX# &lt;= iXA#
           Plot iXC#+iX#*xflag#,iYC#+iY# 
           Plot iXC#-iX#*xflag#,iYC#-iY# 
           iX#=iX#+1 
           iYm1# = iY#-1 
           iSigma# = iA#*iX#*iX#+2*iB#*iX#*iYm1#+iC#*iYm1#*iYm1#-iD# 
           If iSigma# &gt;= 0 Then iDx#=iDx# - iC# : iDy#=iDy# + iB# :iY# = iYm1# 
           iDx#=iDx# + iB# 
           iDy#=iDy# - iA# 
       Wend 

   EndIf
End Function
</pre> <br><br></td></tr></table><br>
<a name="1086808"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Wow thats a lot of code for an ellipse ;-)<br><br>Good work tho. <br><br></td></tr></table><br>
<a name="1086815"></a>

<a name="1086817"></a>

<a name="1086818"></a>

<a name="1086819"></a>

<a name="1086821"></a>

<a name="1086822"></a>

<a name="1086823"></a>

<a name="1086824"></a>

<a name="1086826"></a>

<a name="1086827"></a>

<a name="1086832"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> if you like it shorter, take this:<br><br><pre class=code>SuperStrict

Graphics 800,600
SetColor 255,255,255
SetColor 255,255,255
For Local i%=0 To 360 Step 1
	Cls
	DrawRotatedOval 300,300,200,100,i
	Flip 0
Next
WaitKey


Function DrawRotatedOval(centerX#, centerY# , Width# , Height# , Rotation#)
	Local i# , distance# , angle# , actSin# , actCos# , preSin#, preCos# 
	For i#=0 To 360 Step .1
		distance= Sqr( (Sin(i)*Width)^2  + (Cos(i)*Height)^2 )
		angle=ATan2( Sin(i)*Width , Cos(i)*Height)
		actSin = Sin(angle + Rotation)*distance+centerX
		actCos = Cos(angle + Rotation)*distance+centerY
		If i&gt;0
			DrawLine actSin , actCos , preSin , preCos
		EndIf
		preSin = actSin
		preCos = actCos 
	Next
End Function

</pre><br><br>...and speed it up with an "step 1" or "step 10"<br><br>or more dynamic with variable steprate and 3 additional features<br> "filled" , "Startangle" and "EndAngle":<br><br><pre class=code>Function DrawRotatedOval(centerX#, centerY# , Width# , Height# , Rotation# , StartAngle#=0, EndAngle#=360 , Filled%=0)
	Local i# , distance# , angle# , actSin# , actCos# , preSin#, preCos# , StepRate#
	stepRate=(250-200*filled)/Sqr(width^2 + height^2)
	i=StartAngle

	While i&lt;EndAngle+stepRate
		
		distance= Sqr( (Sin(i)*Width)^2  + (Cos(i)*Height)^2 )
		angle=ATan2( Sin(i)*Width , Cos(i)*Height)
		actSin = Sin(angle + Rotation)*distance + centerX
		actCos = Cos(angle + Rotation)*distance + centerY
		If i&gt;0
			DrawLine actSin , actCos , preSin  , preCos
			If Filled&lt;&gt;0 Then
				DrawLine centerX , centerY , actSin  , actCos
			EndIf
		EndIf
		i=i+stepRate
		preSin = actSin
		preCos = actCos 
	Wend

End Function

</pre><br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
