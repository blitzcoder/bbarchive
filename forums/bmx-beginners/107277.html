<!DOCTYPE html><html lang="en" ><head ><title >Doing my swede in</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Doing my swede in</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >Doing my swede in</a><br><br>
<a name="1327298"></a>

<a name="1327300"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzMan</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> hi guys<br><br>Do not no why it is not printing the button i have clicked on.<br><br><br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Graphics 800,600

SetClsColor 32,32,32 

Global Button_List:TList = CreateList()

Button("0",92,32,0)
Button("1",149,32,1)
Button("2",214,32,2)
Button("3",271,32,3)

' \\ Main

Repeat

Cls 

DrawButton()

Flip

Until KeyHit (KEY_ESCAPE) Or AppTerminate()
End

' \\ End Main

'CREATE BUTTON

Type TButton
	
	Field label:String
	Field x:Int
	Field y:Int
	Field width:Int
	Field height:Int
	Field id:Int
	Field hit:Byte
	
End Type

Function Button(label: String,x:Int,y:Int,id:Int)

     Local Button:TButton = New TButton

     Button.label = label
     Button.x = x
     Button.y = y
     Button.width = TextWidth(Button.label)+17 
     Button.height = 32  
     Button.id = id
     Button.hit = False
	
     ListAddLast(Button_List,Button)
	
End Function

'DRAW BUTTON

Function DrawButton()

For Local Button:TButton = EachIn Button_List

'Mouse Over Button

	If MouseX()&gt;Button.x And MouseX()&lt;Button.x+Button.width And MouseY()&gt;Button.y And MouseY()&lt;Button.y+Button.height Then
	 
		Button.hit = True
	
			SetColor 0,0,0 ' outline
			Rect Button.x,Button.y,Button.width,Button.height , 1
			SetColor 96,96,96 ' middle
			Rect Button.x+1,Button.y+1,Button.width-2,Button.height-2 , 0 
			SetColor 255,255,255 ' text
			DrawText Button.label,Button.x+8,Button.y+9
			SetColor 0,0,0
			
			If MouseHit(MOUSE_LEFT) And Button.hit Then ButtonEvent(Button.id)
			
		Else
		
'Normal Button
		
		Button.hit= False
		
			SetColor 0,0,0 ' outline
			Rect Button.x,Button.y,Button.width,Button.height , 1 
			SetColor 64,64,64 ' middle
			Rect Button.x+1,Button.y+1,Button.width-2,Button.height-2 , 0
			SetColor 255,255,255 ' text
			DrawText Button.label,Button.x+8,Button.y+9
			SetColor 255,255,255

		EndIf
Next

End Function

'BUTTON EVENT

Function ButtonEvent(id)'; Call the Button by its id number

	Select id

		Case 0
			DrawText "Button hit" + id ,200,100
	
		Case 1
			DrawText "Button hit" + id ,200,100
			
		Case 2
			DrawText "Button hit" + id ,200,100
			
		Case 3
			DrawText "Button hit" + id ,200,100		
	
	End Select		

End Function

'-----------------------------------------

'RECT

Function Rect(x:Int, y:Int, w:Int, h:Int,style:Byte=0)
	If style =&lt; 0
		DrawRect x,y,w,h
	Else
		DrawLine(x,y,x+w-1,y)
		DrawLine(x+w-1,y,x+w-1,y+h-1)
		DrawLine(x+w-1,y+h-1,x,y+h-1)
		DrawLine(x,y+h-1,x,y)
	EndIf
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1327315"></a>

<a name="1327319"></a>

<a name="1327320"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> th text is printed on the screen, but it is black and visible only for a moment. Try this change to see it:<br><pre class=code>Function ButtonEvent(id)'; Call the Button by its id number
	SetColor 255,255,255
	Print "YES" + id

	Select id
		Case 0
			DrawText "Button hit" + id ,200,100
....
</pre><br><br>You could optimize your buttons by integrating the functions into the type. <br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict

Graphics 800,600

SetClsColor 32,32,32 


TButton.Add("0",92,32,0)
TButton.Add("1",149,32,1)
TButton.Add("2",214,32,2)
TButton.Add("3",271,32,3)

' \\ Main

Repeat
		Cls 
		TButton.DrawAll()
		Flip
Until KeyHit (KEY_ESCAPE) Or AppTerminate()
End


' \\ End Main

'CREATE BUTTON

Type TButton
	Global List:TList = CreateList()
	
	Field Label:String
	Field X:Int, Y:Int, Width:Int, Height:Int, Id:Int
	Field Hit:Byte
	

	
	Function Add(label: String,x:Int,y:Int,id:Int)
	    Local Button:TButton= New TButton
		Button.Set label, x, y, id
	    ListAddLast List,Button
	End Function



	Method Set(_label: String,_x:Int,_y:Int,_id:Int)
	     Label = _label
	     X = _x
	     Y = _y
	     Width = TextWidth(Label)+17 
	     Height = 32  
	     Id = _id
	     Hit = False
	End Method



	Function DrawAll()
		For Local Button:TButton = EachIn List
			Button.Draw()
		Next
	End Function



	Method Draw()
			SetColor 0,0,0 ' outline
			DrawRect X, Y, Width, Height
			Hit= False
			SetColor 64,64,64 ' middle
			If MouseX()&gt;x And MouseX()&lt;x+width And MouseY()&gt;y And MouseY()&lt;y+height Then
				Hit = True
				SetColor 96,96,96 ' middle
				
				If MouseHit(MOUSE_LEFT)
					Event Id
				EndIf
			EndIf

			DrawRect x+1, y+1, Width-2, Height-2
			SetColor 255,255,255 ' text
			DrawText Label, X+8, Y+9
	End Method
		
End Type


'OUTSIDE: BUTTON EVENT

Function Event(id)'; Call the Button by its id number
	SetColor 255,255,255
	Print "Event:" + id
	DrawText "Button hit" + id ,200,100

	Select id
		Case 0
	
		Case 1
			
		Case 2
			
		Case 3
	
	End Select		

End Function
</textarea><br>Encapsulate the code relating the buttons has a lot of advantages:<br>1. You need not write the header "button." in front of each variable.<br>2. The function names and variables will never collide with other names in your code.<br>3. The code keeps closed together like in a class <br><br></td></tr></table><br>
<a name="1327506"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzMan</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Midimaster it was the color command.<br><br>for the life of me i didnt see it <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
