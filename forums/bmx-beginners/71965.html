<!DOCTYPE html><html lang="en" ><head ><title >Help!! Where are the mouseEvents?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Help!! Where are the mouseEvents?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >Help!! Where are the mouseEvents?</a><br><br>
<a name="804422"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kistjes</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> If you take the createCanvas.bmx example and you add a line to show the mouseX() and mouseY() the mouseposition remains 0,0!!!!<br><br>Check this: <textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' createcanvas.bmx

Strict 

Global GAME_WIDTH=320
Global GAME_HEIGHT=240

' create a centered window with client size GAME_WIDTH,GAME_HEIGHT

Local wx=(ClientWidth(Desktop())-GAME_WIDTH)/2
Local wy=(ClientHeight(Desktop())-GAME_HEIGHT)/2

Local window:TGadget=CreateWindow("My Canvas",wx,wy,GAME_WIDTH,GAME_HEIGHT,Null,WINDOW_TITLEBAR|WINDOW_CLIENTCOORDS)

' create a canvas for our game

Local canvas:TGadget=CreateCanvas(0,0,320,240,window)

' create an update timer

CreateTimer 60

While WaitEvent()
	Select EventID()
		Case EVENT_TIMERTICK
			RedrawGadget canvas

		Case EVENT_GADGETPAINT
			SetGraphics CanvasGraphics(canvas)
			SetOrigin 160,120
			SetLineWidth 5
			Cls
			DrawText String(MouseX()) +","+String(MouseY()), 10,10
			Local t=MilliSecs()
			DrawLine 0,0,120*Cos(t),120*Sin(t)
			DrawLine 0,0,80*Cos(t/60),80*Sin(t/60)
			Flip

		Case EVENT_MOUSEMOVE
			Print "MOVE!"

		Case EVENT_WINDOWCLOSE
			FreeGadget canvas
			End

		Case EVENT_APPTERMINATE
			End
	End Select
Wend</textarea><br><br>How can I check the mouse location in a graphics canvas? <br><br></td></tr></table><br>
<a name="804424"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >klepto2</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> <textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' createcanvas.bmx

Strict 

Global GAME_WIDTH=320
Global GAME_HEIGHT=240

' create a centered window with client size GAME_WIDTH,GAME_HEIGHT

Local wx=(ClientWidth(Desktop())-GAME_WIDTH)/2
Local wy=(ClientHeight(Desktop())-GAME_HEIGHT)/2

Local window:TGadget=CreateWindow("My Canvas",wx,wy,GAME_WIDTH,GAME_HEIGHT,Null,WINDOW_TITLEBAR|WINDOW_CLIENTCOORDS)

' create a canvas for our game

Local canvas:TGadget=CreateCanvas(0,0,320,240,window)

' create an update timer

CreateTimer 60

Local MX:Int = 0 'MouseX()
Local MY:Int = 0 'MouseY()
Local MB:Int = 0 'MouseButton()

While WaitEvent()
	Select EventID()
		Case EVENT_TIMERTICK
			RedrawGadget canvas

		Case EVENT_GADGETPAINT
			SetGraphics CanvasGraphics(canvas)
			SetOrigin 160,120
			SetLineWidth 5
			Cls
			DrawText  MX +","+ MY + " --&gt; " + MB, 10,10
			Local t=MilliSecs()
			DrawLine 0,0,120*Cos(t),120*Sin(t)
			DrawLine 0,0,80*Cos(t/60),80*Sin(t/60)
			Flip
		
		'MouseEvents
		Case EVENT_MOUSEMOVE
			MX = EventX()
			MY = EventY()
			
		Case EVENT_MOUSEDOWN
			MB = EventData()
		
		Case EVENT_MOUSEUP
			MB = 0

		Case EVENT_WINDOWCLOSE
			FreeGadget canvas
			End

		Case EVENT_APPTERMINATE
			End
	End Select
Wend
</textarea> <br><br></td></tr></table><br>
<a name="804425"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I dont' remember, but MouseX() MouseY() work with Graphics enabled, to retrieve info about the pointer under MaxGUI you need to check EventX(), EventY() [I'm at work at the moment...so I can't check]. <br><br></td></tr></table><br>
<a name="804426"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> MouseX/Y are Graphics mode functions<br><br>EventX() and EventY() are the ones you want... but they'll only be available from you EVENT_MOUSEMOVE event, not EVENT_GADGETPAINT.<br><br>:-)<br><br>Edit : or klepto can rewrite it for you :-p <br><br></td></tr></table><br>
<a name="804429"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kistjes</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok. I found the answer myself. I checked the event objects reference.<br><br>I was trying to make a nice combination of a graphical interface and TGadgets but that seems hard to realize.<br><br>Perhaps anyone of you can give me some advice.<br>I'm making a prototype of a not-windows-like user interface: interactive graphic objects, a user can manipulate with the mouse (like click and drag). Some objects have a popup menu (also not windows like). But in this popup a user can select a numeric value and changes that. For this value I was planned to use a TextField gadget with a Slider gadget (the stepper version). Everything works fine so far until I discovered that mouseEvents in a CanvasGraphics area are handled differently.<br><br>I see two approaches:<br>1. create my own "textfield-with-stepper"-gadget object or<br>2. create a new mouseEvent controller using EventID()<br><br>I have no idea what to choose. Any help is very much appreciated! <br><br></td></tr></table><br>
<a name="804431"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >klepto2</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here is a second Version using MouseEvents on the one hand and <br>the traditional MouseX() etc commands. To get the normal Key and Mouse commands to work within a Canvas you have to enable the 'polled Input'. This is done by using the undocumented command 'EnablePolleDInput()'. But keep in mind that when using this you don't get the whole CPU Advantage a only eventdriven app does have.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' createcanvas.bmx

Strict 

Global GAME_WIDTH=320
Global GAME_HEIGHT=240

' create a centered window with client size GAME_WIDTH,GAME_HEIGHT

Local wx=(ClientWidth(Desktop())-GAME_WIDTH)/2
Local wy=(ClientHeight(Desktop())-GAME_HEIGHT)/2

Local window:TGadget=CreateWindow("My Canvas",wx,wy,GAME_WIDTH,GAME_HEIGHT,Null,WINDOW_TITLEBAR|WINDOW_CLIENTCOORDS)

' create a canvas for our game

Local canvas:TGadget=CreateCanvas(0,0,320,240,window)

EnablePolledInput()

' create an update timer

CreateTimer 60

Local MX:Int = 0 'MouseX()
Local MY:Int = 0 'MouseY()
Local MB:Int = 0 'MouseButton()

While WaitEvent()
	Select EventID()
		Case EVENT_TIMERTICK
			RedrawGadget canvas

		Case EVENT_GADGETPAINT
			SetGraphics CanvasGraphics(canvas)
			SetOrigin 160,120
			SetLineWidth 5
			Cls
			DrawText  MX +","+ MY + " --&gt; " + MB, 10,10
			DrawText  MouseX()+","+ MouseY() + " --&gt; " + MouseDown(1), 10,30

			Local t=MilliSecs()
			DrawLine 0,0,120*Cos(t),120*Sin(t)
			DrawLine 0,0,80*Cos(t/60),80*Sin(t/60)
			Flip
		
		'MouseEvents
		Case EVENT_MOUSEMOVE
			MX = EventX()
			MY = EventY()
			
		Case EVENT_MOUSEDOWN
			MB = EventData()
		
		Case EVENT_MOUSEUP
			MB = 0

		Case EVENT_WINDOWCLOSE
			FreeGadget canvas
			End

		Case EVENT_APPTERMINATE
			End
	End Select
Wend
</textarea> <br><br></td></tr></table><br>
<a name="804435"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kistjes</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> EnabledPolledInput()...hmmm<br>That does the trick! <br>Don't you think I can safely use it? After all, it is the easiest solution. <br><br></td></tr></table><br>
<a name="804438"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >klepto2</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have used it many times without any problems. If you want to use it, use it. Some people will say it is not save but for me everything was fine using this. So keep using this. <br><br></td></tr></table><br>
<a name="804444"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kistjes</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Klept02: many thanks!<br>Based on your example, I wrote a mouseType to save the status of the mouse.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">' createcanvas.bmx

Strict 

Global GAME_WIDTH=320
Global GAME_HEIGHT=240

' create a centered window with client size GAME_WIDTH,GAME_HEIGHT

Local wx=(ClientWidth(Desktop())-GAME_WIDTH)/2
Local wy=(ClientHeight(Desktop())-GAME_HEIGHT)/2

Local window:TGadget=CreateWindow("My Canvas",wx,wy,GAME_WIDTH,GAME_HEIGHT,Null,WINDOW_TITLEBAR|WINDOW_CLIENTCOORDS)

' create a canvas for our game

Local canvas:TGadget=CreateCanvas(0,0,320,240,window)
Local mouse:TMouse = New TMouse
' create an update timer

CreateTimer 60

While WaitEvent()
	Select EventID()
		Case EVENT_TIMERTICK
			RedrawGadget canvas

		Case EVENT_GADGETPAINT
			SetGraphics CanvasGraphics(canvas)
			SetOrigin 160,120
			SetLineWidth 5
			Cls
			DrawText String(mouse.getX()) +","+String(mouse.getY())+ "--&gt;"+String(mouse.leftMouseDown()), 10,10
			Local t=MilliSecs()
			DrawLine 0,0,120*Cos(t),120*Sin(t)
			DrawLine 0,0,80*Cos(t/60),80*Sin(t/60)
			Flip

		Case EVENT_MOUSEMOVE
			mouse.setMousePos(EventX(), EventY())
		Case EVENT_MOUSEDOWN
			mouse.doMouseDown(EventData())
		Case EVENT_MOUSEUP
			mouse.doMouseUp(EventData())
		
		Case EVENT_WINDOWCLOSE
			FreeGadget canvas
			End

		Case EVENT_APPTERMINATE
			End
	End Select
Wend


Type TMouse
	Field piX% = 0
	Field piY% = 0
	Field pbLeftMouseDown:Byte = False
	Field pbRightMouseDown:Byte = False
	
	Method setMousePos(x%, y%)
		piX = x
		piY = y
	End Method
	
	Method getX%()
		Return piX
	End Method
	
	Method getY%()
		Return piY
	End Method
	
	Method doMouseDown(btn%)
		Select btn
			Case 1 pbLeftMouseDown = True
			Case 2 pbRightMouseDown = True
		End Select
	End Method
	
	Method doMouseUp(btn%)
		Select btn
			Case 1 pbLeftMouseDown = False
			Case 2 pbRightMouseDown = False
		End Select
	End Method
	
	Method leftMouseDown()
		Return pbLeftMouseDown
	End Method
	
	Method rightMouseDown()
		Return pbRightMouseDown
	End Method
	
End Type</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
