<!DOCTYPE html><html lang="en" ><head ><title >Issue with lists</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Issue with lists</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >Issue with lists</a><br><br>
<a name="522721"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >altitudems</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm trying to figure out why my list of entities is only updating two object at a time and not all of them.<br><br>Below is the code for tEntity:<br><pre class=code>Strict 

Global EntityList:TList = CreateList()

Type tEntity
	Field X:Float, Y:Float
	Field VX:Float, VY:Float
	Field Width:Int
	Field Height:Int
	Field Pushable:Int

	Method New()
		EntityList.AddLast(Self)
		X = Rnd(0,640)
		Y = Rnd(0,480)
		Width = 32
		Height = 16
		Pushable = True
	End Method

	Method Compare(_O:Object)
		Return Y - tEntity(_O).Y
	End Method
	
	Method Delete ()
		EntityList.Remove(Self)
	End Method
	
	Method Render ()
		SetColor 0,0,0
		DrawRect X-1, Y-Height-1, Width+2, Height+Height+2
		SetColor 200,200,200
		DrawRect X, Y, Width, Height
		SetColor 125,125,125
		DrawRect X, Y-Height, Width, Height
	End Method
	
	Method Update ()
		MoveIfNotCollided(VX, VY)
	End Method
	
	Method MoveIfNotCollided(_VX:Float, _VY:Float)
		Local e:tEntity
		
		X :+ _VX	
		e = CollideWithEntity()

		If (e &lt;&gt; Null)
			If e.Pushable
				X :- (_VX * .5)
				e.X :+ (_VX * .5)
			Else
				X :- _VX
			End If
		End If
		
		Y :+ _VY		
		e = CollideWithEntity()
		If (e &lt;&gt; Null)
			If e.Pushable
				Y :- (_VY * .5)
				e.Y :+ (_VY * .5)
			Else
				Y :- _VY
			End If
		End If

	End Method
	
	Method CollideWithEntity:tEntity ()
		Local e:tEntity
		For e = EachIn EntityList
			If e &lt;&gt; Self
				If (e.X &gt; X + Width) Return Null
				If (e.X + e.Width &lt; X) Return Null
				If (e.Y &gt; Y + Height) Return Null
				If (e.Y + e.Height &lt; Y) Return Null
				Return e
			End If
		Next
	End Method
End Type</pre><br><br>Here is the game loop:<pre class=code>Graphics 640,480
SetClsColor 0,150,0

Local p1:tEntity = New tEntity
Local p2:tEntity = New tEntity
Local p3:tEntity = New tEntity

Local Controls:Int [] = [KEY_UP, KEY_DOWN, KEY_LEFT, KEY_RIGHT]

While Not KeyHit(KEY_ESCAPE)
	Cls
	
	p1.VX = 0; p1.VY = 0
	If (KeyDown(Controls[0])) p1.VY :+ -2 'UP
	If (KeyDown(Controls[1])) p1.VY :+ 2 'DOWN
	If (KeyDown(Controls[2])) p1.VX :+ -2 'LEFT
	If (KeyDown(Controls[3])) p1.VX :+ 2 'RIGHT
	
	EntityList.Sort
	For Local e:tEntity = EachIn EntityList
		e.Update()
		e.Render()
	Next

	Flip
	FlushMem
Wend</pre><br><br>When you run the code Sometimes your box will collide with the others and sometimes not. Why? If you only create two enities, everything is fine. This is driving me up a wall. Any help is greatly appreciated. <br><br></td></tr></table><br>
<a name="522724"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Perturbatio</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> it alternates between them when the Y value of each is the same. <br><br></td></tr></table><br>
<a name="522725"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >altitudems</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok yeah I see that, hmm. I wonder whats causing that.<br>My first guess was that list sorting was causing the problem,  but I don't its the main problem. Comment out the sort command and there is still an issue. Man, what is going on here <br><br></td></tr></table><br>
<a name="524645"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >altitudems</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Jeez! I still can't figure it out. Somebody please help me? Someone? I can't do anything until this is finished. I've debuged and printed every single variable I can think of. Either I'm a huge idiot or there is a major flaw with the list system. HELP <br><br></td></tr></table><br>
<a name="524672"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>
	Method CollideWithEntity:tEntity ()
		Local e:tEntity
		For e = EachIn EntityList
			If e &lt;&gt; Self
				If e.X &gt; X + Width Continue 
				If e.X + e.Width &lt; X Continue
				If e.Y &gt; Y + Height Continue
				If e.Y + e.Height &lt; Y Continue
				Return e
			End If
		Next
	End Method
</pre> <br><br></td></tr></table><br>
<a name="524683"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Perturbatio</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> <textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict 

Global EntityList:TList = CreateList()

Type tEntity
	Field X:Float, Y:Float
	Field VX:Float, VY:Float
	Field Width:Int
	Field Height:Int
	Field Pushable:Int
	Field Name:String

	Method New()
		EntityList.AddLast(Self)
		X = Rnd(0,640)
		Y = Rnd(0,480)
		Width = 32
		Height = 16
		Pushable = True
	End Method

	Method Compare(_O:Object)
		Return Y - tEntity(_O).Y
	End Method
	
	Method Delete ()
		EntityList.Remove(Self)
	End Method
	
	Method Render ()
		SetColor 0,0,0
		DrawRect X-1, Y-Height-1, Width+2, Height+Height+2
		SetColor 200,200,200
		DrawRect X, Y, Width, Height
		SetColor 125,125,125
		DrawRect X, Y-Height, Width, Height
		SetColor 0, 0, 0
		DrawText(self.name, X, Y)
		SetColor 255, 255, 255
		DrawText(self.name, X+1, Y+1)
	End Method
	
	Method Update ()
		MoveIfNotCollided(VX, VY)
	End Method
	
	Method MoveIfNotCollided(_VX:Float, _VY:Float)
		Local e:tEntity
		
		X :+ _VX	
		Y :+ _VY	
		e = CollideWithEntity()

		If (e &lt;&gt; Null)
			If e.Pushable
				X :- (_VX * .5)
				Y :- (_VY * .5)
				e.X :+ (_VX * .5)
				e.Y :+ (_VY * .5)	
			Else
				X :- _VX
				Y :- _VY	
			End If
		End If

	End Method
	

	Method CollideWithEntity:tEntity ()
		Local e:tEntity
		For e = EachIn EntityList
			If e &lt;&gt; Self
				If e.X &gt; X + Width Continue 
				If e.X + e.Width &lt; X Continue
				If e.Y &gt; Y + (Height*2) Continue
				If e.Y + (e.Height*2) &lt; Y Continue
				Return e
			End If
		Next
	End Method

End Type


Graphics 640,480,0,0
SetClsColor 0,150,0

Local p1:tEntity = New tEntity
p1.name = "player1"
p1.pushable = False
Local p2:tEntity = New tEntity
p2.name="player2"
Local p3:tEntity = New tEntity
p3.name="player3"

Local Controls:Int [] = [KEY_UP, KEY_DOWN, KEY_LEFT, KEY_RIGHT]

While Not KeyHit(KEY_ESCAPE)
	Cls
	
	p1.VX = 0; p1.VY = 0
	If (KeyDown(Controls[0])) p1.VY :+ -2 'UP
	If (KeyDown(Controls[1])) p1.VY :+ 2 'DOWN
	If (KeyDown(Controls[2])) p1.VX :+ -2 'LEFT
	If (KeyDown(Controls[3])) p1.VX :+ 2 'RIGHT
	
	For Local e:tEntity = EachIn EntityList
		e.Update()
		e.Render()
	Next

	Flip
	FlushMem
Wend
</textarea><br><br>*EDIT*<br>I really should refresh before posting if I've spent a while on these things :) <br><br></td></tr></table><br>
<a name="524803"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >altitudems</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> @skidracer<br>Your slight modification to the CollideWithEntity method doesen't really make any difference that I can see. But thanks for the effort.<br><br>@Perturbatio<br>Ok your changes make the difference, but I'm still not sure why. The only thing you realy did was make it so the it tests both axis at the same time. Instead of testing for collision on x and y seperately. So essentialy youve removed sliding collision. This led to the solution though. It seem that I was swapping entities back and forth.<br><br>Here is the fixed version if your interested:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Strict 

Global EntityList:TList = CreateList()

Type tEntity
	Field X:Float, Y:Float
	Field VX:Float, VY:Float
	Field Width:Int
	Field Height:Int
	Field Pushable:Int
	Field LastCollide:tEntity

	Method New()
		EntityList.AddLast(Self)
		X = Rnd(0,640)
		Y = Rnd(0,480)
		Width = 32
		Height = 16
		Pushable = True
	End Method

	Method Compare(_O:Object)
		Return Y - tEntity(_O).Y
	End Method
	
	Method Delete ()
		EntityList.Remove(Self)
	End Method
	
	Method Render ()
		SetColor 0,0,0
		DrawRect X-1, Y-Height-1, Width+2, Height+Height+2
		SetColor 200,200,200
		DrawRect X, Y, Width, Height
		SetColor 125,125,125
		DrawRect X, Y-Height, Width, Height
		SetColor 0,0,0
	End Method
	
	Method Update ()
		MoveIfNotCollided(VX, VY)
	End Method
	
	Method MoveIfNotCollided(_VX:Float, _VY:Float)
		
		X :+ _VX
		Local cx:tEntity = CollideWithEntity()

		If (cx &lt;&gt; Null)
			If cx.Pushable
				X :- (_VX * .5)
				cx.X :+ (_VX * .5)
			Else
				X :- _VX
			End If
		End If

		Y :+ _VY
		Local cy:tEntity = CollideWithEntity()

		If (cy &lt;&gt; Null)
			If cy.Pushable
				Y :- (_VY * .5)
				cy.Y :+ (_VY * .5)
			Else
				Y :- _VY
			End If
		End If

	End Method
	
	Method CollideWithEntity:tEntity ()
		Local e:tEntity
		For e = EachIn EntityList
			If e &lt;&gt; Self
				If e.X &gt; X + Width Continue 
				If e.X + e.Width &lt; X Continue
				If e.Y &gt; Y + (Height) Continue
				If e.Y + (e.Height) &lt; Y Continue
				Return e
			End If
		Next
	End Method
End Type


Graphics 640,480,0
SetClsColor 0,150,0
Local p1:tEntity = New tEntity
Local p2:tEntity = New tEntity
Local p3:tEntity = New tEntity

Local Controls:Int [] = [KEY_UP, KEY_DOWN, KEY_LEFT, KEY_RIGHT]

While Not KeyHit(KEY_ESCAPE)
	Cls
	
	p1.VX = 0; p1.VY = 0
	If (KeyDown(Controls[0])) p1.VY :+ -2 'UP
	If (KeyDown(Controls[1])) p1.VY :+ 2 'DOWN
	If (KeyDown(Controls[2])) p1.VX :+ -2 'LEFT
	If (KeyDown(Controls[3])) p1.VX :+ 2 'RIGHT
	
	EntityList.Sort()
	
	For Local e:tEntity = EachIn EntityList
		e.Update()
		e.Render()
	Next

	DrawText "p1",p1.x,p1.y
	DrawText "p2",p2.x,p2.y
	DrawText "p3",p3.x,p3.y

	Flip
	FlushMem
Wend
</textarea><br><br>Thanks again guys for all your help. Now I just have to figure out why the other boxes don't collide when when your pushing one. <br><br></td></tr></table><br>
<a name="524804"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> If it didn't collide with the first non self entity in the list it would return instead of checking the next in the list (hence the use of Continue instead of return null). <br><br></td></tr></table><br>
<a name="524805"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Perturbatio</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Now I just have to figure out why the other boxes don't collide when when your pushing one.  <br></div><br><br>solved:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict 

Global EntityList:TList = CreateList()

Type tEntity
	Field X:Float, Y:Float
	Field VX:Float, VY:Float
	Field Width:Int
	Field Height:Int
	Field Pushable:Int
	Field LastCollide:tEntity

	Method New()
		EntityList.AddLast(Self)
		X = Rnd(0,640)
		Y = Rnd(0,480)
		Width = 32
		Height = 16
		Pushable = True
	End Method

	Method Compare(_O:Object)
		Return Y - tEntity(_O).Y
	End Method
	
	Method Delete ()
		EntityList.Remove(Self)
	End Method
	
	Method Render ()
		SetColor 0,0,0
		DrawRect X-1, Y-Height-1, Width+2, Height+Height+2
		SetColor 200,200,200
		DrawRect X, Y, Width, Height
		SetColor 125,125,125
		DrawRect X, Y-Height, Width, Height
		SetColor 0,0,0
	End Method
	
	Method Update ()
		MoveIfNotCollided(VX, VY)
		VX=0
		VY=0
	End Method
	
	Method MoveIfNotCollided(_VX:Float, _VY:Float)
		
		X :+ _VX
		Local cx:tEntity = CollideWithEntity()

		If (cx &lt;&gt; Null)
			If cx.Pushable
				X :- (_VX * .5)
				'cx.X :+ (_VX * .5)
				cx.VX = _VX
			Else
				X :- _VX
			End If
		End If

		Y :+ _VY
		Local cy:tEntity = CollideWithEntity()

		If (cy &lt;&gt; Null)
			If cy.Pushable
				Y :- (_VY * .5)
				cy.VY = _Vy
			Else
				Y :- _VY
			End If
		End If

	End Method
	
	Method CollideWithEntity:tEntity ()
		Local e:tEntity
		For e = EachIn EntityList
			If e &lt;&gt; Self
				If e.X &gt; X + Width Continue 
				If e.X + e.Width &lt; X Continue
				If e.Y &gt; Y + (Height) Continue
				If e.Y + (e.Height) &lt; Y Continue
				Return e
			End If
		Next
	End Method
End Type


Graphics 640,480,0
SetClsColor 0,150,0
Local p1:tEntity = New tEntity
Local p2:tEntity = New tEntity
Local p3:tEntity = New tEntity

Local Controls:Int [] = [KEY_UP, KEY_DOWN, KEY_LEFT, KEY_RIGHT]

While Not KeyHit(KEY_ESCAPE)
	Cls
	
	p1.VX = 0; p1.VY = 0
	If (KeyDown(Controls[0])) p1.VY :+ -2 'UP
	If (KeyDown(Controls[1])) p1.VY :+ 2 'DOWN
	If (KeyDown(Controls[2])) p1.VX :+ -2 'LEFT
	If (KeyDown(Controls[3])) p1.VX :+ 2 'RIGHT
	
	EntityList.Sort()
	
	For Local e:tEntity = EachIn EntityList
		e.Update()
		e.Render()
	Next

	DrawText "p1",p1.x,p1.y
	DrawText "p2",p2.x,p2.y
	DrawText "p3",p3.x,p3.y

	Flip
	FlushMem
Wend

</textarea><br><br>You need to set and reset their VX and VY values as well. <br><br></td></tr></table><br>
<a name="524816"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >altitudems</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> @skidracer<br>Oops, your right. Thanks for pointing that out. I guess that does make a difference. :)<br><br>@Perturbatio<br>Thanks for that quick response :) It works a treat. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
