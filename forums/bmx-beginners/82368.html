<!DOCTYPE html><html lang="en" ><head ><title >Copy Object to a New Object</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Copy Object to a New Object</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >Copy Object to a New Object</a><br><br>
<a name="928578"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi All,<br><br>Is there a command to copy one object to another object?<br><br>If I do this:<br><br><pre class=code>
Type TFoo
  Field test$
End Type

Local foo:TFoo = New TFoo
foo.test = "FOO"

Local bar:TFoo = foo

bar.test = "BAR"

Print foo.test
Print bar.test
</pre><br><br>Both prints out "BAR"... as it is coping by reference. I want both objects to be a different instance of each other, whilst keeping all the data.<br><br>Please help.<br><br>Cheers! <br><br></td></tr></table><br>
<a name="928588"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TaskMaster</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> You have to do the copy yourself.<br><br>Make a new object and copy over all of the fields.<br><br>There may be a way using reflection to get an object to copy itself, but I have not played with it yet. <br><br></td></tr></table><br>
<a name="928594"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks TaskMaster...<br><br>Looking at my code again, and I'm now not sure what the problem is, I thought it was this.<br><br>Ive got a set of buildings each with a set of slots, my problem is that if I click on one building it sets the slot of each building with that type.<br><br>Eg 3 buildings, 2 houses and 1 bar. If I click the first house, the slots in both houses are used...!?!? I just want the building I clicked on!<br><br>Here is a simplified version of my code:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Graphics 800,600,0

Type TSlot
	Field x%,y%
	Field empty%
	Field slotList:TSlotList
End Type

Type TSlotList Extends TList
	Method AddSlot:TSlot(x:Int, y:Int)
		Local s:TSlot = New TSlot
		s.x = x
		s.y = y
		s.empty = True
		s.slotList = Self
		AddLast(s)
		Return s
	End Method
	
	Method findFreeSlot:TSlot()
		For Local s:TSlot = EachIn Self
			If s.empty Then Return s
		Next
		' reset slots if all are full
		For Local s:TSlot = EachIn Self
			 s.empty = True
		Next
		Return Null
	End Method
End Type

Type TBuildingInfo
	Field name$
	Field slots:TSlotList
	Field buildingInfoList:TBuildingInfoList 
End Type

Type TBuildingInfoList Extends TList
	Method AddNew:TBuildingInfo(name:String)
		Local b:TBuildingInfo = New TBuildingInfo
		b.name = name		
		b.slots = New TSlotList
		
		b.buildingInfoList = Self
		
		AddLast(b)
		Return b
	End Method
	
	Method Find:TBuildingInfo(name:String)
		For Local i:TBuildingInfo = EachIn Self
			If Upper(i.name) = Upper(name) Then Return i
		Next		
		Return Null
	End Method
End Type
	
Type TBuilding
	Field x%,y%
	Field h%,w%
	Field info:TBuildingInfo
	Field name$
	Field buildings:TBuildingList 'points to list it's part of
	
	Function Create:TBuilding(tx%, ty%,name$)
		Local b:TBuilding = New TBuilding
		b.x = tx
		b.y = ty
		b.h = 100
		b.w = 100
		b.name = name
		b.info = New TBuildingInfo
		b.info.slots = New TSlotList 
		Return b
	End Function
End Type

Type TBuildingList Extends TList
	Method AddNew:TBuilding(x%,y%,name$)
		Local b:TBuilding = TBuilding.Create(x, y,name)
		b.buildings = Self 'point to this list		
		AddLast(b)
		Return b
	End Method
	
	Method Draw()
		For Local b:TBuilding = EachIn Self
			SetColor 255,255,255
			DrawRect b.x,b.y,b.h,b.w
			SetColor 255,0,0
			DrawText b.name, b.x,b.y
			For Local s:TSlot = EachIn b.info.slots
				If s.empty SetColor 0,255,0 Else SetColor 255,0,0
				DrawOval b.x+s.x , b.y+s.y , 10 , 10
			Next
		Next
		SetColor 255,255,255		
	End Method
	
	Method find:TBuilding(name$)
		For Local b:TBuilding = EachIn Self
			If Upper(b.name) = Upper(name) Return b
		Next
		Return Null
	End Method
End Type

' set up objects

Local infoList:TBuildingInfoList = New TBuildingInfoList 
Local info:TBuildingInfo = infoList.AddNew("HOUSE")
info.slots.AddSlot(-20,10)
info.slots.AddSlot(50,120)
info.slots.AddSlot(120,10)

info:TBuildingInfo = infoList.AddNew("BAR")
info.slots.AddSlot(-20,10)
info.slots.AddSlot(50,120)
info.slots.AddSlot(120,10)


Local buildings:TBuildingList = New TBuildingList
Local b:TBuilding
b = buildings.AddNew(100,100, "HOUSE")
Local houseInfo:TbuildingInfo = infoList.Find("HOUSE")
b.info = houseInfo

b = buildings.AddNew(300,300, "HOUSE")
b.info = houseInfo

b = buildings.AddNew(500,100, "BAR")
Local barInfo:TbuildingInfo = infoList.Find("BAR")
b.info = barInfo

Repeat
	If MouseHit(1)
		For Local b:TBuilding = EachIn buildings
			If RectsOverlap(MouseX(),MouseY(),1,1,b.x,b.y,b.w,b.h)
				Local freeSlot:TSlot = b.info.slots.findFreeSlot()
				If freeSlot &lt;&gt; Null
					freeSlot.empty = False
				End If
			EndIf
		Next		
	End If

	Cls
		buildings.Draw()
	Flip
Until KeyHit(KEY_ESCAPE)

Function RectsOverlap:Int(x0:Float, y0:Float, w0:Float, h0:Float, x2:Float, y2:Float, w2:Float, h2:Float)
	If x0 &gt; (x2 + w2) Or (x0 + w0) &lt; x2 Then Return False
	If y0 &gt; (y2 + h2) Or (y0 + h0) &lt; y2 Then Return False
	Return True
End Function
</textarea><br><br>I thought of adding the following to TBuildingInfo:<br><br><pre class=code>
	Method clone:TBuildingInfo()
		Local b:TBuildingInfo = New TBuildingInfo
		b.name = name
		b.slots = slots
		b.buildinginfoList = buildingInfoList
		Return b
	End Method
</pre><br><br>and assigning like this:<br><br><pre class=code>
Local houseInfo:TbuildingInfo = infoList.Find("HOUSE")
b.info = houseInfo.clone()
</pre><br><br>But I still have the problem... any ideas?<br><br>Thanks! <br><br></td></tr></table><br>
<a name="928603"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Okay I think I found my problem... and it was that one object (TSlot) was still getting copied by reference...<br><br>I did this in TBuildingInfo:<br><br><pre class=code>
	Method clone:TBuildingInfo()
		Local b:TBuildingInfo = New TBuildingInfo
		b.name = name
		b.slots = New TSlotList
		For Local s:TSlot = EachIn slots
			Local sl:TSlot = b.slots.clone(s, slots)
			b.slots.AddSlot(sl.x,sl.y)
		Next		
		b.buildinginfoList = buildingInfoList
		Return b
	End Method
</pre><br><br>And added clone to TSlotList:<br><br><pre class=code>
	Method clone:TSlot(s:TSlot, sl:TSlotList )
		Local ss:TSlot = s.clone(sl)
		Return ss
	End Method
</pre><br><br>Finally added clone to TSlot:<br><br><pre class=code>
	Method clone:TSlot(sl:TSlotList )
		Local s:TSlot = New TSlot
		s.x = x
		s.y = y
		s.empty = empty
		s.slotList = sl
		Return s
	End Method
</pre><br><br>The working code:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Graphics 800,600,0

Type TSlot
	Field x%,y%
	Field empty%
	Field slotList:TSlotList
	
	Method clone:TSlot(sl:TSlotList )
		DebugLog "CLONE SLOT"
		Local s:TSlot = New TSlot
		s.x = x
		s.y = y
		s.empty = empty
		s.slotList = sl
		DebugLog s.x
		Return s
	End Method
End Type

Type TSlotList Extends TList
	Method AddSlot:TSlot(x:Int, y:Int)
		Local s:TSlot = New TSlot
		s.x = x
		s.y = y
		s.empty = True
		s.slotList = Self
		AddLast(s)
		Return s
	End Method
	
	Method findFreeSlot:TSlot()
		For Local s:TSlot = EachIn Self
			If s.empty Then Return s
		Next
		' reset slots if all are full
		For Local s:TSlot = EachIn Self
			 s.empty = True
		Next
		Return Null
	End Method
	
	Method clone:TSlot(s:TSlot, sl:TSlotList )
		Local ss:TSlot = s.clone(sl)
		Return ss
	End Method
End Type

Type TBuildingInfo
	Field name$
	Field slots:TSlotList
	Field buildingInfoList:TBuildingInfoList 
	
	Method clone:TBuildingInfo()
		Local b:TBuildingInfo = New TBuildingInfo
		b.name = name
		b.slots = New TSlotList
		For Local s:TSlot = EachIn slots
			Local sl:TSlot = b.slots.clone(s, slots)
			b.slots.AddSlot(sl.x,sl.y)
		Next		
		b.buildinginfoList = buildingInfoList
		Return b
	End Method

End Type

Type TBuildingInfoList Extends TList
	Method AddNew:TBuildingInfo(name:String)
		Local b:TBuildingInfo = New TBuildingInfo
		b.name = name		
		b.slots = New TSlotList
		
		b.buildingInfoList = Self
		
		AddLast(b)
		Return b
	End Method
	
	Method Find:TBuildingInfo(name:String)
		For Local i:TBuildingInfo = EachIn Self
			If Upper(i.name) = Upper(name) Then Return i
		Next		
		Return Null
	End Method
End Type
	
Type TBuilding
	Field x%,y%
	Field h%,w%
	Field info:TBuildingInfo
	Field name$
	Field buildings:TBuildingList 'points to list it's part of
	
	Function Create:TBuilding(tx%, ty%,name$)
		Local b:TBuilding = New TBuilding
		b.x = tx
		b.y = ty
		b.h = 100
		b.w = 100
		b.name = name
		b.info = New TBuildingInfo
		b.info.slots = New TSlotList 
		Return b
	End Function
End Type

Type TBuildingList Extends TList
	Method AddNew:TBuilding(x%,y%,name$)
		Local b:TBuilding = TBuilding.Create(x, y,name)
		b.buildings = Self 'point to this list		
		AddLast(b)
		Return b
	End Method
	
	Method Draw()
		For Local b:TBuilding = EachIn Self
			SetColor 255,255,255
			DrawRect b.x,b.y,b.h,b.w
			SetColor 255,0,0
			DrawText b.name, b.x,b.y
			DrawText b.info.name, b.x,b.y+10
			For Local s:TSlot = EachIn b.info.slots
				If s.empty SetColor 0,255,0 Else SetColor 255,0,0
				DrawOval b.x+s.x , b.y+s.y , 10 , 10
			Next
		Next
		SetColor 255,255,255		
	End Method
	
	Method find:TBuilding(name$)
		For Local b:TBuilding = EachIn Self
			If Upper(b.name) = Upper(name) Return b
		Next
		Return Null
	End Method
End Type

' set up objects

Local infoList:TBuildingInfoList = New TBuildingInfoList 
Local info:TBuildingInfo = infoList.AddNew("HOUSE")
info.slots.AddSlot(-20,10)
info.slots.AddSlot(50,120)
info.slots.AddSlot(120,10)

info:TBuildingInfo = infoList.AddNew("BAR")
info.slots.AddSlot(-20,10)
info.slots.AddSlot(50,120)
info.slots.AddSlot(120,10)


Local buildings:TBuildingList = New TBuildingList
Local b:TBuilding
b = buildings.AddNew(100,100, "HOUSE")
Local houseInfo:TbuildingInfo = infoList.Find("HOUSE")
b.info = houseinfo.clone()

b = buildings.AddNew(300,300, "HOUSE")
b.info = houseinfo.clone()
b.info.name = "H"

b = buildings.AddNew(500,100, "BAR")
Local barInfo:TbuildingInfo = infoList.Find("BAR")
b.info = barInfo.clone()

Repeat
	If MouseHit(1)
		For Local b:TBuilding = EachIn buildings
			If RectsOverlap(MouseX(),MouseY(),1,1,b.x,b.y,b.w,b.h)
				Local freeSlot:TSlot = b.info.slots.findFreeSlot()
				If freeSlot &lt;&gt; Null
					freeSlot.empty = False
				End If
			EndIf
		Next		
	End If

	Cls
		buildings.Draw()
	Flip
Until KeyHit(KEY_ESCAPE)

Function RectsOverlap:Int(x0:Float, y0:Float, w0:Float, h0:Float, x2:Float, y2:Float, w2:Float, h2:Float)
	If x0 &gt; (x2 + w2) Or (x0 + w0) &lt; x2 Then Return False
	If y0 &gt; (y2 + h2) Or (y0 + h0) &lt; y2 Then Return False
	Return True
End Function
</textarea><br><br>This is all when and good for objects with a small number of fields, but my proper TBuildingInfo has 40 fields ATM and will be more soon!<br><br>Is there a smaller/simpler way to do this? <br><br></td></tr></table><br>
<a name="928627"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm back again!<br><br>Found this post <a href="http://www.blitzbasic.com/Community/posts.php?topic=73573#822043" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=73573#822043</a> by Azathoth and it looks like it might work...<br><br>I guess this is how you use the metadata tags(?):<br><br><pre class=code>
Type TBuildingInfo
	Field name$ {Clone}
	Field slots:TSlotList{Clone}
	Field buildingInfoList:TBuildingInfoList {Clone}
End Type
</pre><br><br>And assign them by:<br><pre class=code>
b = buildings.AddNew(100,100, "HOUSE")
Local houseInfo:TbuildingInfo = infoList.Find("HOUSE")
b.info = TBuildingInfo(CloneObject(houseInfo))
</pre><br><br>This looks like it works for the name$ field, but still slots:TSlotList is still by reference... any ideas?<br><br>Cheers <br><br></td></tr></table><br>
<a name="928726"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think there might be a way to memcopy the objects directly, but I'm not sure how to do this. <br><br></td></tr></table><br>
<a name="928730"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> I think there might be a way to memcopy the objects directly <br></div><br>Not advisable, me thinks. <br><br></td></tr></table><br>
<a name="928732"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TaskMaster</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> You have to create a new slot list, and re-add each object in the list (if you want the same objects in the list).  If you want the objects in the list to also be copies, you will have to copy them as well. <br><br></td></tr></table><br>
<a name="928735"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes. There are 2 ways you can think of copying :<br><br>A basic copy, which in effect creates a new objects and populates it with the objects the original contains. Those objects will be the same instances.<br><br>A deep copy, which will create new copies of ALL objects. If you have a TList field, it would create a new TList, and new instances of all objects inside that list.<br><br>The second form is obviously a lot more intensive, since it potentially has a lot of work to do.<br>It all depends how much you need copied? Just the object you are dealing with, or all the objects it uses?<br><br>My persistence module can do "proper" deep copying, albeit in a roundabout way - it converts the object-structure into XML, which you can then de-serialise to get a whole new set of objects. (i.e. it's designed for snap-shots of your objects for persisting on Disk, rather than on-the-fly copying). <br><br></td></tr></table><br>
<a name="928823"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for the replies guys.<br><br>@Czar Flavius - Ive read that doing the memcopy can really mess up the GC, especially if you have got objects within objects.<br><br>@TaskMaster, looks like your right... Azathoth CloneObject function doesnt like to copy the TSlotList field (I kept getting null pointers!? when trying to {Clone} it)...so here is what Ive come up with:<br><br>TBuildingInfo (notice the NoClone on the lists)<br><br><pre class=code>
Type TBuildingInfo
	Field name$ {Clone}
	Field slots:TSlotList{NoClone}
	Field buildingInfoList:TBuildingInfoList {NoClone}
	
	Method clone:TBuildingInfo()
		Local b:TBuildingInfo =  TBuildingInfo (CloneObject(Self))
		b.slots = New TSlotList
		For Local s:TSlot = EachIn Self.slots
			Local ss:TSlot  = s.clone(slots)
			b.slots.AddSlot(ss.x,ss.y)
		Next
		b.buildingInfoList = Self.buildingInfoList
		Return b
	End Method
End Type
</pre><br><br>And to create the clone:<br><br><pre class=code>
Local buildings:TBuildingList = New TBuildingList ' create the global building list
Local b:TBuilding
b = buildings.AddNew(100,100, "HOUSE") ' create a new building and add it to the list
Local houseInfo:TbuildingInfo = infoList.Find("HOUSE") 'get the data for the building
b.info = houseinfo.clone() ' create a new copy of the data and assign it to the new building
b.info.name = "1st House" ' check to see if the clone has worked
</pre><br><br>Here is the full code:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Graphics 800,600,0

Type TSlot
	Field x%,y%
	Field empty%
	Field slotList:TSlotList
	
	Method clone:TSlot(sl:TSlotList )		
		Local s:TSlot = New TSlot
		s.x = x
		s.y = y
		s.empty = empty
		s.slotList = sl
		DebugLog s.x
		Return s
	End Method
End Type

Type TSlotList Extends TList
	Method AddSlot:TSlot(x:Int, y:Int)
		Local s:TSlot = New TSlot
		s.x = x
		s.y = y
		s.empty = True
		s.slotList = Self
		AddLast(s)
		Return s
	End Method
	
	Method findFreeSlot:TSlot()
		For Local s:TSlot = EachIn Self
			If s.empty Then Return s
		Next
		' reset slots if all are full
		For Local s:TSlot = EachIn Self
			 s.empty = True
		Next
		Return Null
	End Method
End Type

Type TBuildingInfo
	Field name$ {Clone}
	Field slots:TSlotList{NoClone}
	Field buildingInfoList:TBuildingInfoList {NoClone}
	
	Method clone:TBuildingInfo()
		Local b:TBuildingInfo =  TBuildingInfo (CloneObject(Self))
		b.slots = New TSlotList
		For Local s:TSlot = EachIn Self.slots
			Local ss:TSlot  = s.clone(slots)
			b.slots.AddSlot(ss.x,ss.y)
		Next
		b.buildingInfoList = Self.buildingInfoList
		Return b
	End Method
End Type

Type TBuildingInfoList Extends TList
	Method AddNew:TBuildingInfo(name:String)
		Local b:TBuildingInfo = New TBuildingInfo
		b.name = name		
		b.slots = New TSlotList
		
		b.buildingInfoList = Self
		
		AddLast(b)
		Return b
	End Method
	
	Method Find:TBuildingInfo(name:String)
		For Local i:TBuildingInfo = EachIn Self
			If Upper(i.name) = Upper(name) Then Return i
		Next		
		Return Null
	End Method
End Type
	
Type TBuilding
	Field x%,y%
	Field h%,w%
	Field info:TBuildingInfo
	Field name$
	Field buildings:TBuildingList 'points to list it's part of
	
	Function Create:TBuilding(tx%, ty%,name$)
		Local b:TBuilding = New TBuilding
		b.x = tx
		b.y = ty
		b.h = 100
		b.w = 100
		b.name = name
		b.info = New TBuildingInfo
		b.info.slots = New TSlotList 
		Return b
	End Function
End Type

Type TBuildingList Extends TList
	Method AddNew:TBuilding(x%,y%,name$)
		Local b:TBuilding = TBuilding.Create(x, y,name)
		b.buildings = Self 'point to this list		
		AddLast(b)
		Return b
	End Method
	
	Method Draw()
		For Local b:TBuilding = EachIn Self
			SetColor 255,255,255
			DrawRect b.x,b.y,b.h,b.w
			SetColor 255,0,0
			DrawText b.name, b.x,b.y
			DrawText b.info.name, b.x,b.y+10
			For Local s:TSlot = EachIn b.info.slots
				If s.empty SetColor 0,255,0 Else SetColor 255,0,0
				DrawOval b.x+s.x , b.y+s.y , 10 , 10
			Next
		Next
		SetColor 255,255,255		
	End Method
	
	Method find:TBuilding(name$)
		For Local b:TBuilding = EachIn Self
			If Upper(b.name) = Upper(name) Return b
		Next
		Return Null
	End Method
End Type

' set up objects

Local infoList:TBuildingInfoList = New TBuildingInfoList 
Local info:TBuildingInfo = infoList.AddNew("HOUSE")
info.slots.AddSlot(-20,10)
info.slots.AddSlot(50,120)
info.slots.AddSlot(120,10)

info:TBuildingInfo = infoList.AddNew("BAR")
info.slots.AddSlot(-20,10)
info.slots.AddSlot(50,120)
info.slots.AddSlot(120,10)


Local buildings:TBuildingList = New TBuildingList
Local b:TBuilding
b = buildings.AddNew(100,100, "HOUSE")
Local houseInfo:TbuildingInfo = infoList.Find("HOUSE")
b.info = houseinfo.clone()
b.info.name = "1st House"

b = buildings.AddNew(300,300, "HOUSE")
b.info = houseinfo.clone()
b.info.name = "2nd House"

b = buildings.AddNew(500,100, "BAR")
Local barInfo:TbuildingInfo = infoList.Find("BAR")
b.info = barInfo.clone()
b.info.name = "The Bar"


Repeat
	If MouseHit(1)
		For Local b:TBuilding = EachIn buildings
			If RectsOverlap(MouseX(),MouseY(),1,1,b.x,b.y,b.w,b.h)
				Local freeSlot:TSlot = b.info.slots.findFreeSlot()
				If freeSlot &lt;&gt; Null
					freeSlot.empty = False
				End If
			EndIf
		Next		
	End If

	Cls
		buildings.Draw()
	Flip
Until KeyHit(KEY_ESCAPE)

Function RectsOverlap:Int(x0:Float, y0:Float, w0:Float, h0:Float, x2:Float, y2:Float, w2:Float, h2:Float)
	If x0 &gt; (x2 + w2) Or (x0 + w0) &lt; x2 Then Return False
	If y0 &gt; (y2 + h2) Or (y0 + h0) &lt; y2 Then Return False
	Return True
End Function

' Clones an object and returns the clone. - by Azathoth
' Any fields that references an object only gets the reference copied unless MetaData contains {Clone}
' which then a copy is also made of the object referenced.
' {NoClone} prevents the field being copied.
Function CloneObject:Object(obj:Object)
	Local cobj:Object
	
	If obj=Null Then Return Null
	
	Local objId:TTypeId=TTypeId.ForObject(obj)
	
	If objId.ExtendsType(StringTypeId)
		Return String(obj)
	EndIf
	
	If objId.ExtendsType(ArrayTypeId)
		If objId.ArrayLength(obj)&gt;0
			cobj=objId.NewArray(objId.ArrayLength(obj))
			
			If cobj
				For Local i:Int = 0 Until objId.ArrayLength(obj)
					If objId.ElementType().ExtendsType(ArrayTypeId) Or objId.ElementType().ExtendsType(StringTypeId) ..
						Or objId.ElementType().ExtendsType(ObjectTypeId)
						objId.SetArrayElement(cobj,i,CloneObject(objId.GetArrayElement(obj,i)))
					Else
						objId.SetArrayElement(cobj,i,objId.GetArrayElement(obj,i))
					EndIf
				Next
			EndIf
		EndIf
		
		Return cobj
	EndIf
	
	cobj=New obj
	
	For Local fld:TField=EachIn objId.EnumFields()
		Local fldId:TTypeId=fld.TypeId()
		If fld.Get(obj)&lt;&gt;Null And fld.MetaData("NoClone")=Null
			DebugLog fld.name()+"  "+fld.MetaData()+"  " fld.get(obj)
			If Not fld.MetaData("Clone")=Null
				fld.Set(cobj,CloneObject(fld.Get(obj)))
			Else
				fld.Set(cobj,fld.Get(obj))
			EndIf
		EndIf
	Next
	
	Return cobj
	
EndFunction
</textarea><br><br>@Brucey, thanks for the info - Ill have a look at your persistence module - sounds interesting... <br><br></td></tr></table><br>
<a name="928903"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Azathoth</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> @TaskMaster, looks like your right... Azathoth CloneObject function doesnt like to copy the TSlotList field (I kept getting null pointers!? when trying to {Clone} it) <br></div>I think that has to do with {Clone} only affecting that specific field and not TSlotList's fields (it extends TList which doesn't contain the {Clone} metadata).<br><br>Edit: This was to prevent lock ups when trying to clone cyclic references. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
