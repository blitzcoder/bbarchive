<!DOCTYPE html><html lang="en" ><head ><title >Letting a player program.</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Letting a player program.</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >Letting a player program.</a><br><br>
<a name="822978"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ryan Burnside</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Recently I want to allow the player to program behaviors into objects. My question is how to allow the player to use math functions and the object's variables.<br><br>Lets say the object has an speed value. I currently have a dialog box that pops up and asks for a number to describe the speed. How would I allow the player to set the speed using mathmatical operators while addressing other field values within the object?<br><br>Maybe te player would like to set the speed based on another field variable coupled with some operators. Can this be done? Can they type in "cos(direction)*13" and operations such as this? <br><br></td></tr></table><br>
<a name="822989"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImaginaryHuman</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sure, you just would have to `parse` what they type in and set up your own `mini script language` of keywords that are allowed, and then you'd search through the string and extract whatever bits interest you and process them. Ideally you'd probably turn the user input into a series of function calls or something so you don't have to parse it every time you want to execute it. <br><br></td></tr></table><br>
<a name="822991"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> There are a few expression interpreters in the code archives. and as ImaginaryHuman suggest, generating an intermediate representation (like a node tree feks) would speed things up a little. or go a step further and create your own virtual machine. <br><br></td></tr></table><br>
<a name="823040"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ryan Burnside</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah ok, I thought so. I suppose there is no way to directly evaluate a string of commands then? Without using word parsing?<br>There might be around 500 objects preforming actions at once. I think parsing may be too taxing for that... <br><br></td></tr></table><br>
<a name="823043"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> I suppose there is no way to directly evaluate a string of commands then? Without using word parsing? <br></div><br>Im afraid not. and if there were, that method would still have to parse the string ;)<br><br>I would suggest parsing/compiling down to a simple stack machine with a single number type, and implementing functions as opcodes. <br><br></td></tr></table><br>
<a name="823047"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ryan Burnside</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm really new to this concept. What do you mean my compiling down and single number type and opcodes? <br><br></td></tr></table><br>
<a name="823119"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> I'm really new to this concept. <br></div><br>My mistake, i forgot this was in "Beginners Area".. hehe<br><br>If you havent done any parsing before i suggest reading up on it before jumping in.<br><a href="http://en.wikipedia.org/wiki/Parsing" target="_blank">http://en.wikipedia.org/wiki/Parsing</a><br><a href="http://en.wikipedia.org/wiki/Recursive_descent_parser" target="_blank">http://en.wikipedia.org/wiki/Recursive_descent_parser</a> &lt;-- easiest to implement in my oppinion<br><br>What it boils down to is that an expression like this:<br><pre class=code>
x = a + b * 2
</pre><br>Is translated into either a node structure:<br><pre class=code>
(ASSIGN
  (VARIABLE x)
  (ADD
    (VARIABLE a)
    (MUL
       (VARIABLE b)
       (NUMBER 2)
    )
  )
)
</pre><br>A node can defined like this.<br><pre class=code>
Type TNode
  Field Left:TNode
  Field Right:TNode
  Field Value:String
EndType
</pre><br><br>Or directly to assembler (doesnt have to be real machine asm though, could be an imaginary machine like below)<br><pre class=code>
GET a
GET b
PUSH 2
MUL
ADD
SET x
</pre><br>Compilers usually compile to a node tree, then work (doing optimizations etc) on that tree and then spit out some assembler code.<br>You dont have to use a virtual machine though, you could jsut as well execute the node structure, but it would be a little slower.<br><br>What i meant by single number type was just for simplicity of the virtual machine, eg no strings and other fancy structures like proper function calls.<br><br>an OpCode is roughly the same as an Instruction, a single operation a CPU or a virtual machine executes.<br><br>I adapted an expression interpreter i made a while back to spit out code for a very simple virtual machine, so you can see how it all fits together:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Framework BRL.StandardIO
Import BRL.LinkedList
Import BRL.Math

SuperStrict

Private

' COMPILER
Global source:String
Global pos:Int
Global code:Int[]
Global cp:Int
Global vars:TList = New TList
Global numvars:Int

Type TVariable
	Field Name:String
	Field Index:Int
	Field Value:Float
	
	Function Create:TVariable( name:String, value:Float, index:Int)
		Local v:TVariable = New TVariable
		v.Name = name
		v.Value = value
		v.Index = index
		Return v
	EndFunction
EndType

Function AddCode( op:Int)
	If code.Length = 0 Then
		code = New Int[32]
	ElseIf cp &gt;= code.Length Then
		Local sz:Int = code.Length
		code = code[sz..sz*2]
	EndIf
	code[cp] = op
	cp :+ 1
EndFunction

Function AddParam( value:Float)
	If code.Length = 0 Then
		code = New Int[32]
	ElseIf cp &gt;= code.Length Then
		Local sz:Int = code.Length
		code = code[sz..sz*2]
	EndIf
	Float Ptr(Varptr code[cp])[0] = value
	cp :+ 1
EndFunction

Function ReportError( s:String, printpos:Int = True)
	If printpos Then 
		Print "ERROR: pos="+pos+" : "+s
	Else
		Print "ERROR: "+s
	EndIf
EndFunction

Function EatWhitespace()
	While (pos &lt; source.Length) And ((source[pos] = Asc(" ")) Or (source[pos] = Asc("~t")) Or (source[pos] = Asc("~n")) Or (source[pos] = Asc("~r")))
		pos :+ 1
	Wend
EndFunction

Function EatIdent:String()
	Local start:Int = pos
	While (pos &lt; source.Length) And (((source[pos] &gt;= Asc("a")) And (source[pos] &lt;= Asc("z"))) Or ..
		((source[pos] &gt;= Asc("A")) And (source[pos] &lt;= Asc("Z"))) Or ..
		((source[pos] &gt;= Asc("0")) And (source[pos] &lt;= Asc("9"))) Or (source[pos] = Asc("_")))
		pos :+ 1
	Wend
	Return source[start..pos]
EndFunction

Function EatNumber:Double()
	Local start:Int = pos
	Local gotsep:Int = False
	Local res:String	
	While (pos &lt; source.Length) And ((source[pos] &gt;= Asc("0")) And (source[pos] &lt;= Asc("9")))
		pos :+ 1
		If pos &gt;= source.Length Then Exit
		If source[pos] = Asc(".") Then
			If gotsep Then 
				ReportError( "error in number")
				Return False
			EndIf
			gotsep = True
			pos :+ 1
		EndIf
	Wend
	Return source[start..pos].ToDouble()
EndFunction

Function Primary:Int()
	EatWhitespace()
	If source[pos] = Asc("(") Then
		pos :+ 1
		AddExpression()
    		If source[pos] &lt;&gt; Asc(")") Then ReportError( "expected )")
    		pos :+ 1
	ElseIf (source[pos] &gt;= Asc("0")) And (source[pos] &lt;= Asc("9")) Then
		AddCode( OP_PUSH)
		AddParam( EatNumber())
	ElseIf source[pos] = Asc("-") Then
		pos :+ 1
		AddCode( OP_PUSH)
		AddParam( - EatNumber())
		If Not AddExpression() Return False
	ElseIf ((source[pos] &gt;= Asc("a")) And (source[pos] &lt;= Asc("z"))) Or ..
		((source[pos] &gt;= Asc("A")) And (source[pos] &lt;= Asc("Z"))) Or (source[pos] = Asc("_")) Then
		Local ident:String = EatIdent()
		If source[pos] = Asc("(") Then
			pos :+ 1
			EatWhitespace()
			If source[pos] = Asc(")") Then
				' no parameters
				pos :+ 1
				CompileFunction( ident)
			Else			
				AddExpression()
				If source[pos] = Asc(")") Then
					' 1 parameter
					pos :+ 1
					CompileFunction( ident)
				ElseIf source[pos] = Asc(",") Then
					' 2 parameters
					pos :+ 1
					AddExpression()					
		    			If source[pos] &lt;&gt; Asc(")") Then ReportError( "expected )")
	    				pos :+ 1			
					CompileFunction( ident)
				Else
					ReportError( "invalid function expression =&gt; " + ident)
					Return False
				EndIf
			EndIf
		Else
			Local id:Int = LookupVariable( ident)
			If id &lt;&gt; -1 Then 
				AddCode( OP_GET)
				AddCode( id)
			Else
				ReportError( "undefined variable " + ident)
				Return False
			EndIf			
		EndIf
	Else
		ReportError( "expected number or -number or (expression)")
		Return False
	EndIf
	EatWhitespace()
	Return True
EndFunction

Function MulExpression:Int()
	EatWhitespace()
	If Not Primary() Then Return False
	While (pos &lt; source.Length) And ((source[pos] = Asc("*")) Or (source[pos] = Asc("/")))
		If source[pos] = Asc("*") Then
			pos :+ 1
			If Not Primary() Then Return False
			AddCode( OP_MUL)
		ElseIf source[pos] = Asc("/") Then
			pos :+ 1
			If Not Primary() Then Return False
			AddCode( OP_DIV)
		EndIf
	Wend
	EatWhitespace()
	Return True
EndFunction
	
Function AddExpression:Int()
	EatWhitespace()
	If Not MulExpression() Then Return False
	While (pos &lt; source.Length) And ((source[pos] = Asc("+")) Or (source[pos] = Asc("-")))
		If source[pos] = Asc("+") Then
			pos :+ 1
			If Not MulExpression() Then Return False
			AddCode( OP_ADD)
		ElseIf source[pos] = Asc("-") Then
			pos :+ 1
			If Not MulExpression() Then Return False
			AddCode( OP_SUB)
		EndIf
	Wend
	EatWhitespace()
	Return True
EndFunction

Function CompileFunction:Int( ident:String)
	Select ident.ToLower()
		Case "sin"	AddCode( OP_SIN)
		Case "cos"	AddCode( OP_COS)
		Default
			ReportError( "undefined function " + ident)
			Return False
	EndSelect	
	Return True
EndFunction

Function LookupVariable:Int( name:String)
	name = name.ToLower()
	For Local v:TVariable = EachIn vars
		If v.Name = name Then Return v.Index
	Next
	Return -1
EndFunction

Public

Function AddVariable( name:String, value:Float = 0.0)
	Local v:TVariable = TVariable.Create( name.ToLower(), value, numvars)
	vars.AddLast( v)
	numvars :+ 1
EndFunction

Function Compile:Int( s:String)
	source = s
	Local id:Int
	Local idx:Int = source.Find( "=")
	If idx &gt; 0 Then
		Local ident:String = EatIdent()
		id = LookupVariable( ident)
		If id = -1 Then 
			ReportError( "undefined variable " + ident)
			Return False
		EndIf
		EatWhitespace()
		pos :+ 1
		If Not AddExpression() Then Return False
		AddCode( OP_SET)
		AddCode( id)
		Return True
	ElseIf idx = 0 Then
		ReportError( "invalid assignment", False)
		Return False
	EndIf
	If Not AddExpression() Then Return False
	Return True
EndFunction

Function GetCompiledCodeData( c:Int[] Var, d:Float[] Var)
	c = code[..cp]	
	d = New Float[numvars]
	Local i:Int = 0
	For Local v:TVariable = EachIn vars
		d[i] = v.Value
		i :+ 1
	Next
EndFunction

Function ResetCompiler()
	source = Null
	pos = 0
	code = Null
	cp = 0
	vars.Clear()
	numvars = 0
EndFunction



' VIRTUAL MACHINE
Const OP_PUSH:Int = 0
Const OP_GET:Int = 1
Const OP_SET:Int = 2
Const OP_ADD:Int = 3
Const OP_SUB:Int = 4
Const OP_MUL:Int = 5
Const OP_DIV:Int = 6
Const OP_SIN:Int = 7
Const OP_COS:Int = 8

Type TMicroVM
	Field Data:Float[]
	Field Code:Int[]
	Field Stack:Int[]
	Field CP:Int
	Field SP:Int
	
	Method Execute:Float()
		CP = 0
		SP = Stack.Length
		While CP &lt; Code.Length
			Select Code[CP]
				Case OP_PUSH
					CP :+ 1
					SP :- 1
					Stack[SP] = Code[CP]
				Case OP_GET
					CP :+ 1
					SP :- 1
					Float Ptr(Varptr Stack[SP])[0] = Data[ Code[CP]]
				Case OP_SET
					CP :+ 1
					Data[ Code[CP]] = Float Ptr(Varptr Stack[SP])[0]
					SP :+ 1
				Case OP_ADD
					Float Ptr(Varptr Stack[SP+1])[0] :+ Float Ptr(Varptr Stack[SP])[0]
					SP :+ 1
				Case OP_SUB
					Float Ptr(Varptr Stack[SP+1])[0] :- Float Ptr(Varptr Stack[SP])[0]
					SP :+ 1
				Case OP_MUL
					Float Ptr(Varptr Stack[SP+1])[0] :* Float Ptr(Varptr Stack[SP])[0]
					SP :+ 1
				Case OP_DIV
					Float Ptr(Varptr Stack[SP+1])[0] :/ Float Ptr(Varptr Stack[SP])[0]
					SP :+ 1			
				Case OP_SIN
					Float Ptr(Varptr Stack[SP])[0] = Sin( Float Ptr(Varptr Stack[SP])[0])
				Case OP_COS
					Float Ptr(Varptr Stack[SP])[0] = Sin( Float Ptr(Varptr Stack[SP])[0])					
			EndSelect
			CP :+ 1
		Wend
		If SP &lt; Stack.Length Then 
			Return Float Ptr(Varptr Stack[SP])[0]
		Else
			Return Float Ptr(Varptr Stack[Stack.Length-1])[0]
		EndIf
	EndMethod
EndType



' TEST
Local vm:TMicroVM = New TMicroVM
ResetCompiler()
AddVariable( "a", 1)
AddVariable( "b", 2)
AddVariable( "x")
Compile( "x = a + b * 2")
vm.Stack = New Int[32]
GetCompiledCodeData( vm.Code, vm.Data)
Print vm.Execute()
</textarea> <br><br></td></tr></table><br>
<a name="823248"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >fredborg</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I recommend you take a look at BriskVM, which is a scripting language you can easily use in BlitzMax.<br><br>Not only is it very easy to implement for you, it's also very easy to script in, because the syntax is 99.9% the same as BlitzMax.<br><br>You can find it here: <a href="http://www.koriolis-fx.com/" target="_blank">http://www.koriolis-fx.com/</a><br><br>Highly recommended! <br><br></td></tr></table><br>
<a name="823279"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ryan Burnside</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks guys I see I'll have a busy evening. If I have trouble I will post again. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
