<!DOCTYPE html><html lang="en" ><head ><title >Updateing Framecount individually?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Updateing Framecount individually?</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >Updateing Framecount individually?</a><br><br>
<a name="803343"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Amon</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have an array. The Array is of Type TTile.  TTile has an image field and a frame field.<br><br>if Array[x,y].frame = 0<br>DrawBomb , blah, blah, 0<br><br>if Array[x,y].frame = 1<br>DrawBomb , blah, blah, Frame<br><br>Frame:+1<br><br>Now, what I want to do is to have the frame update seperately for each Bomb when the mouse is on it.<br><br>So, if there are 5 bombs in a row and I move the mouse on the first bomb, I want its frame to update. When I move the mouse on the second bomb I want it frame to update but from the begining. The first Bombs frame could be half way through and when I start the second bombs frame it starts from the begining for the second bomb.<br><br><br>Currently I create a bomb wherever there is one in the array by using "local b:bomb = new Bomb.<br><br>then I assign the frames "b.frame = 0" and timer "b.Timer = millisecs()"<br><br>am I making sense?<br><br>[edit] It's 4 am here in London and I'm still up. I'm watching StarTrek the Motion Picture and trying to code a game with the new platform skills I've been tought :) <br><br></td></tr></table><br>
<a name="803368"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> If I have unterstand correctly what you want, something like this?<br><br><pre class=code>
Type ttile
	Field x:Int , y:Int
	Field frame:Int , frame_start:Int , frame_end:Int , frame_delay:Int
	Field frame_timer:Int
	
	Field mouse_is_over:Int=0
	
	Function create:ttile(xx:Int , yy:Int , fstart:Int = 0,fend:Int=5)
		Local t:ttile = New ttile
		t.x = xx
		t.y = yy
		t.frame = fstart
		t.frame_start = fstart
		t.frame_end=fend
		t.frame_delay = 125
		t.frame_timer = MilliSecs()

		Return t
	End Function
	
	Method Update()
		
		If MouseX() &gt; x And MouseX() &lt; x + 15 And MouseY() &gt; y And MouseY() &lt; y + 15 mouse_is_over = 1 Else mouse_is_over = 0Ã¹
		
		If mouse_is_over=1
		'the mouse is on the tile
		If MilliSecs() &gt; frame_timer + frame_delay
		
			frame:+ 1
			
			If frame &gt; frame_end frame = frame_start
		
			frame_timer = MilliSecs()
		End If
		End If
	'drawmimage frame....
	SetColor 128 , 128 , 128
	DrawRect x , y , 15 , 15
	SetColor 255 , 255 , 255
	
	DrawText frame , x , y
	
	End Method
End Type

Graphics 640 , 480
Local arr:ttile[10 , 10]

'initialize new array

For x1 = 0 Until 10
	For y1 = 0 Until 10
		arr[x1 , y1] = ttile.Create(x1*16 , y1*16)
	Next
Next

While Not AppTerminate() 
	Cls
	For x1 = 0 Until 10
	For y1 = 0 Until 10
		arr[x1 , y1].update
	Next
Next
Flip
Wend
</pre><br><br>When the mouse is over the tile (which size I fixed in 16x16 pixels - see the MOuseX()... line) the frame counter is activated else is stopped. <br><br></td></tr></table><br>
<a name="803414"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Amon</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi! Thanks for the reply. I have created some very messy code to show you what I want doing.<br><br>You can download here: <a href="http://www.kamikazekrow.com/storage/bombtest.zip" target="_blank">http://www.kamikazekrow.com/storage/bombtest.zip</a><br><br>Basically it draws bombs to screen. When the mouse is clicked and it is over a bomb it activates the anim.<br><br>Notice that when a second or third bomb is activated the animation starts from the same position as the last one. ie it doesn't start from the begining for the newly activated bomb.<br><br>heres the code <br><br><pre class=code>
SuperStrict


Graphics 800, 600

Global bomb:TImage = LoadAnimImage("bomb.png", 32, 32, 0, 14) 


Global MapWidth:Int = 25
Global MapHeight:Int = 600 / 32

Global MapArray:TTile[MapWidth, MapHeight]

Type TTile
	Field x:Int
	Field y:Int
	Field image:TImage
	Field Frame:Int
	Field BombFRame:Int
	Field BombFrameTimer:Int
	
	Global List:TList
	
	Function Create:TTile() 
		Return New TTile
	End Function
	
	Function PrefillMapArray() 
		For Local y:Int = 0 Until MapHeight
			For Local x:Int = 0 Until MapWidth
				MapArray[x, y]= TTile.Create() 
				MapArray[x, y].Frame = 0
				If MapArray[x, y].Frame = 0
					TTile.CreateBomb
				End If
			Next
		Next
	End Function
	
	Function Draw() 
		For Local x:Int = 0 Until MapWidth
			For Local y:Int = 0 Until MapHeight
				Select MapArray[x, y].Frame
					Case 0
						DrawImage bomb, X * 32, Y * 32, 0
					Case 1
						' This bit below. I want the bombs to animate seperetaly when the character is on it.
						' Useing the mouse position to activate the anim would be fine for this example.
						For Local b:TTile = EachIn TTile.List
							DrawImage bomb, x * 32, y * 32, b.BombFRame
							b.UpdateBomb
						Next
				End Select
			Next
		Next
	End Function

	Function CreateBomb() 
		If not TTile.List
			TTile.List = CreateList() 
		End If
		Local b:TTile = New TTile
		b.BombFRame = 0
		b.BombFrameTimer = MilliSecs() 
		TTile.List.AddLast(b) 
	End Function
	
	Method UpdateBomb() 
		If MilliSecs() &gt; BombFRameTimer + 200
			BombFrame:+1
			If BombFrame &gt; 13 Then BombFrame = 0
			BombFrameTimer = MilliSecs() 
		End If
	End Method
	
	Function ActivateBomb() 
		If MapArray[MouseX() / 32, MouseY() / 32].Frame = 0
			If MouseHit(MOUSE_LEFT) 
				MapArray[MouseX() / 32, MouseY() / 32].Frame = 1
			End If
		End If
	End Function
End Type


TTile.PrefillMapArray

While not KeyHit(KEY_ESCAPE) 
	Cls
		TTile.Draw
		TTile.ActivateBomb
	Flip
	
WEnd
</pre><br><br>I know the codes a mess. <br><br></td></tr></table><br>
<a name="803416"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm not sure what you're asking. You create a bomb, and each time the mouse goes over the bomb, its frame resets to 0? <br><br></td></tr></table><br>
<a name="803419"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>
Case 1
' This bit below. I want the bombs to animate seperetaly when the character is on it.
' Useing the mouse position to activate the anim would be fine for this example.
'For Local b:TTile = EachIn TTile.List
'	DrawImage bomb, x * 32, y * 32, b.BombFRame
'		b.UpdateBomb
'Next

Local amap:ttile = MapArray[x , y]
amap.UpdateBomb()
DrawImage bomb, x * 32, y * 32, amap.BombFRame
</pre><br>This?<br><br>I dont' see any reason to use a list... <br><br></td></tr></table><br>
<a name="803420"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Amon</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> No. The map array is filled with bombs.<br><br>if MapArray[x,y].frame = 0 then Draw Bomb but dont animate<br><br>if MapArray[x,y].frame = 1 then Draw Bomb and animate.<br><br>I want the bombs to animate individually when clicked. Try out the code I posted by downloading the zip.<br><br>If i click on a bomb it starts animating. If I click on another bomb it doesn't start animating from the begining. Instead it flicks to the saem frame as the previously clicked bomb.<br><br>[edit] I'll try your method Degac. thanks :) <br><br></td></tr></table><br>
<a name="803424"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Amon</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nope. I can't seem to get it to work. I don't know how to update each bombs frames individually. <br><br></td></tr></table><br>
<a name="803430"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Amon, where do you ever populate b.bombframe to be used in your draw function?<br>The 'b' variable used in Createbom is local to that function. <br><br></td></tr></table><br>
<a name="803433"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Amon</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> I don't think I understand. I create bombs with this.<br><br><pre class=code>
	Function CreateBomb() 
		If not TTile.List
			TTile.List = CreateList() 
		End If
		Local b:TTile = New TTile
		b.BombFRame = 0
		b.BombFrameTimer = MilliSecs() 
		TTile.List.AddLast(b) 
	End Function
</pre><br><br>And if MapArray[x,y].frame = 0, I TTile.createbomb() <br><br></td></tr></table><br>
<a name="803434"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> How? Doen't it work?<br><br>I post you source (I deleted some thing unused)<br>When you click with the mouse on a bomb it starts to animate (from frame 1). If you click on another bomb it starts to animate from frame 1 too!<br><br><br><pre class=code>
SuperStrict


Graphics 800, 600

Global bomb:TImage = LoadAnimImage("bomb.png", 32, 32, 0, 14) 


Global MapWidth:Int = 25
Global MapHeight:Int = 600 / 32

Global MapArray:TTile[MapWidth, MapHeight]

Type TTile
	Field x:Int
	Field y:Int
	Field image:TImage
	Field Frame:Int
	Field BombFRame:Int
	Field BombFrameTimer:Int
	
		

	Function PrefillMapArray() 
		For Local y:Int = 0 Until MapHeight
			For Local x:Int = 0 Until MapWidth
				MapArray[x, y]= TTile.CreateBomb() 
			Next
		Next
	End Function
	
	Function Draw() 
		For Local x:Int = 0 Until MapWidth
			For Local y:Int = 0 Until MapHeight
				Select MapArray[x, y].Frame
					Case 0
						DrawImage bomb, X * 32, Y * 32, 0
					Case 1
						Local amap:ttile = MapArray[x , y]
						amap.UpdateBomb()
						DrawImage bomb, x * 32, y * 32, amap.BombFRame

				End Select
				
			Next
		Next
	End Function

	Function CreateBomb:ttile() 
		Local b:TTile = New TTile
		b.BombFRame = 0
		b.BombFrameTimer = MilliSecs() 
		Return b
		End Function
	
	Method UpdateBomb() 
		If MilliSecs() &gt; BombFRameTimer + 200
			BombFrame:+1
			If BombFrame &gt; 13 Then BombFrame = 0
			BombFrameTimer = MilliSecs() 
		End If
	End Method
	
	Function ActivateBomb() 
		If MapArray[MouseX() / 32, MouseY() / 32].Frame = 0
			If MouseHit(MOUSE_LEFT) 
				MapArray[MouseX() / 32, MouseY() / 32].Frame = 1
			End If
		End If
	End Function
End Type


TTile.PrefillMapArray

While Not KeyHit(KEY_ESCAPE) 
	Cls
		TTile.Draw
		TTile.ActivateBomb
	Flip
	
Wend
</pre> <br><br></td></tr></table><br>
<a name="803435"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Can't you just create a global field for the current animation frame, and give each bomb and invidial "ticking"/"active" frame set to true or false? <br><br></td></tr></table><br>
<a name="803436"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> I commented out all of the "redundant" stuff. you don't need to create a list if you are storing the values in the arrays.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict


Graphics 800, 600

Global bomb:TImage = LoadAnimImage("bomb.png", 32, 32, 0, 14) 


Global MapWidth:Int = 25
Global MapHeight:Int = 600 / 32

Global MapArray:TTile[MapWidth, MapHeight]

Type TTile
	Field x:Int
	Field y:Int
	Field image:TImage
	Field Frame:Int
	Field BombFRame:Int
	Field BombFrameTimer:Int
	
	Global List:TList
	
	Function Create:TTile() 
		Return New TTile
	End Function
	
	Function PrefillMapArray() 
		For Local y:Int = 0 Until MapHeight
			For Local x:Int = 0 Until MapWidth
				MapArray[x, y]= TTile.Create() 
				MapArray[x, y].Frame = 0
				MapArray[x, y].BombFrameTimer = MilliSecs() ' I moved this here from CreateBomb Function.
				'If MapArray[x, y].Frame = 0
				'	TTile.CreateBomb
				'End If
			Next
		Next
	End Function
	
	Function Draw() 
		For Local x:Int = 0 Until MapWidth
			For Local y:Int = 0 Until MapHeight
				Select MapArray[x, y].Frame
					Case 0
						DrawImage bomb, X * 32, Y * 32, 0
					Case 1
						' This bit below. I want the bombs to animate seperetaly when the character is on it.
						' Useing the mouse position to activate the anim would be fine for this example.
						'For Local b:TTile = EachIn TTile.List
							DrawImage bomb, x * 32, y * 32, MapArray[x,y].BombFRame
							MapArray[x,y].UpdateBomb
						'Next
				End Select
			Next
		Next
	End Function

	'Function CreateBomb() 
	'	If Not TTile.List
	'		TTile.List = CreateList() 
	'	End If
	'	Local b:TTile = New TTile
	'	b.BombFRame = 0
	'	b.BombFrameTimer = MilliSecs() 
	'	TTile.List.AddLast(b) 
	'End Function
	
	Method UpdateBomb() 
		If MilliSecs() &gt; BombFRameTimer + 200
			BombFrame:+1
			If BombFrame &gt; 13 Then BombFrame = 0
			BombFrameTimer = MilliSecs() 
		End If
	End Method
	
	Function ActivateBomb() 
		If MapArray[MouseX() / 32, MouseY() / 32].Frame = 0
			If MouseHit(MOUSE_LEFT) 
				MapArray[MouseX() / 32, MouseY() / 32].Frame = 1
			End If
		End If
	End Function
End Type


TTile.PrefillMapArray

While Not KeyHit(KEY_ESCAPE) 
	Cls
		TTile.Draw
		TTile.ActivateBomb
	Flip
	
Wend
</textarea> <br><br></td></tr></table><br>
<a name="803438"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Amon</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Degac. Thanks for your help. Tha works perfectly.<br><br>What is this doing though?<br><br><pre class=code>
						Local amap:ttile = MapArray[x , y]
						amap.UpdateBomb()
						DrawImage bomb, x * 32, y * 32, amap.BombFRame
</pre> <br><br></td></tr></table><br>
<a name="803439"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> I could write<br><pre class=code>
MapArray[x , y].UpdateBomb()
DrawImage bomb, x * 32, y * 32, MapArray[x , y].BombFRame
</pre><br><br>It was only for clear reading <br><br></td></tr></table><br>
<a name="803440"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Amon</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ahh!. That actually makes sense.<br><br>Thanks very much for your help. I really appreciate it. :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
