<!DOCTYPE html><html lang="en" ><head ><title >General Game Structure: Part 2</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >General Game Structure: Part 2</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >General Game Structure: Part 2</a><br><br>
<a name="801671"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Apollonius</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Everyone!<br><br>I'm still trying to figure out how I'll ultimately structure my game for good performance and ease of read, but I haven't found the ultimate way of doing this but I have had some ideas after playing around with Types and Functions.<br><br>If I understand correctly, Types can be used for almsot everything that needs to be redone more then once. The only downside is the fact that they depends on inside values to function so the difference between two of the same type is limited. If that makes any sense. The way I see it, is:<br><br>Let's say you have Type TButton... and you have a TRoom and you wana place Buttons into a room it can be done(via method and functions) but it will be the same for all the rooms you may be able to change the values of those button inside the TRoom type but not how many you have of them.<br><br>So basicly Types is basicly duplicable storage space with internal behavior (mewthod and functions), no?<br><br>So I though then I have no other way to make it so every room is different then to make an outside Functions to contain all the information of all the different room as.. follow:<br><br>This is the simple structure I came out with, it's jsut a global idea of how I'd do it:<br><br><pre class=code>
Graphics 800,600,0

Global CurrentRoom:Int=0

Global intro:TRoom=New TRoom
intro.Name="Intro"
Global menu:TRoom=New TRoom
menu.Name="Menu"

While Not KeyHit(KEY_ESCAPE)

UpdateRoom()

Flip; Cls
Wend; End


Type TRoom
	Field Name:String
End Type

Function UpdateRoom()
	If CurrentRoom=0 Then
		' Room Intro
		DrawText intro.Name,10,10
		
		If KeyHit(KEY_ENTER)
			CurrentRoom:+1
		EndIf
		
	EndIf
	If CurrentRoom=1 Then
		' Room Menu
		DrawText menu.Name,10,10
		
	EndIf
End Function
</pre><br><br>What do you guys think? Any better ideas? Am I way off? Or over complicating stuff.. like I usually do lol? <br><br></td></tr></table><br>
<a name="801685"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here is a great tutorial on types if you haven't read it yet.<br><a href="http://www.blitzbasic.com/Community/posts.php?topic=59233" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=59233</a><br><br>I'd might do your example like this, keeping Update() within the type:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics 800,600,0


Local intro:TRoom=New TRoom
Local CurrentRoom:TRoom = intro 'point CurrentRoom to the first room

intro.Name="Intro"

Local menu:TRoom=New TRoom
menu.Name="Menu"

intro.NextRoom = menu 'the room you enter when you press "Enter" key
menu.NextRoom = intro

While Not KeyHit(KEY_ESCAPE)
	Cls
	CurrentRoom = CurrentRoom.Update()

	Flip
Wend

Type TRoom
	Field Name:String
	Field NextRoom:TRoom
	
	Method Update:TRoom()
		DrawText Name,10,10
		
		If KeyHit(KEY_ENTER)
			Return NextRoom 'if enter is pressed, return the next room
		Else
			Return Self 'if not, then return the current room
		End If
	End Method
End Type
</textarea> <br><br></td></tr></table><br>
<a name="801687"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Apollonius</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I know about doing it that way, however did you read my second or third paragraph, what if you wana add alot of different information for a room, you cannot insert that kind of information into an object that is why I did it the way I did.<br><br>If you have TButton, TTextfield... Let's say I wana place 3 TButton and 2 TTextfield in the first room then 5 TButton and 1 TTextfield in the second room.. You cannot insert very differencial information into an object...<br><br><pre class=code>
Graphics 800,600,0


Local intro:TRoom=New TRoom
Local CurrentRoom:TRoom = intro 'point CurrentRoom to the first room

intro.Name="Intro"

Local menu:TRoom=New TRoom
menu.Name="Menu"

intro.NextRoom = menu 'the room you enter when you press "Enter" key
menu.NextRoom = intro

While Not KeyHit(KEY_ESCAPE)
	Cls
	CurrentRoom = CurrentRoom.Update()

	Flip
Wend

Type TRoom
	Field Name:String
	Field NextRoom:TRoom
	
	Method Update:TRoom()
		DrawText Name,10,10
		
		If KeyHit(KEY_ENTER)
			Return NextRoom 'if enter is pressed, return the next room
		Else
			Return Self 'if not, then return the current room
		End If
	End Method
End Type

Type TButton
	Field x:Int, y:Int
	Field width:Int
	Field height:Int
	
	Method Create( xstart:Int, ystart:Int, wstart:Int, hstart:Int)
		x=xstart
		y=ystart
		w=wstart
		h=hstart
	End Method
	
End Type
</pre><br><br>Can't add 2 TButtons to the Update of TRoom without it applying to all rooms .. what if you want to add different number of TButtons per room.. Hmm<br><br>Hope it make sense somewhat?<br><br>EDITED<br>rooms will contain alot of stuff thus why i think i need to make a function to make/controle all the stff inside the room...<br><br>i wonder why shouldt i use Select Case instead of if then? <br><br></td></tr></table><br>
<a name="801703"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Did you read the tutorial in the link I provided?  It will give you a lot of info about types and how to better use them.  <br>As far as different objects in the room, it is quite possible to have each room with it's own  TButtons, TTexts, TItems, TMonsters, etc....  Each room can have the same number, different number or none at all.<br>There are different ways to do this, reading the tutorial and some of the posts here will show you a few.  One way is to use arrays.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SuperStrict
Graphics 800,600,0

Local intro:TRoom=New TRoom
Local CurrentRoom:TRoom = intro 'point CurrentRoom to the first room

intro.Name="Intro"
intro.Buttons = New TButton[3] 'add 3 buttons to intro
For Local i:Int = 0 To 2
	intro.Buttons[i] = New TButton
	intro.Buttons[i].create(i*100,30,90,30,"intro "+i)
Next

Local menu:TRoom=New TRoom
menu.Name="Menu"
menu.Buttons = New TButton[5] 'add 5 buttons to menu
For Local i:Int = 0 To 4
	menu.Buttons[i] = New TButton
	menu.Buttons[i].create(i*80,30,70,50,"menu "+i)
Next

intro.NextRoom = menu 'the room you enter when you press "Enter" key
menu.NextRoom = intro

While Not KeyHit(KEY_ESCAPE)
	Cls
	CurrentRoom = CurrentRoom.Update()

	Flip
Wend

Type TRoom
	Field Name:String
	Field NextRoom:TRoom
	Field Buttons:TButton[]
	
	Method Update:TRoom()
		SetColor 255,255,255
		DrawText Name,10,10
		For Local i:Int = 0 To Buttons.length - 1
			Buttons[i].Draw()
		Next
		
		If KeyHit(KEY_ENTER)
			Return NextRoom 'if enter is pressed, return the next room
		Else
			Return Self 'if not, then return the current room
		End If
	End Method
End Type

Type TButton
	Field x:Int, y:Int
	Field width:Int
	Field height:Int
	Field text:String
	
	Method create( xstart:Int, ystart:Int, wstart:Int, hstart:Int, textstart:String)
		x=xstart
		y=ystart
		width=wstart
		height=hstart
		text=textstart
	End Method
	
	Method Draw()
		SetColor 255,255,0
		DrawRect x,y,width,height
		SetColor 0,0,0
		DrawText text,x+2,y+2
	End Method
End Type
</textarea> <br><br></td></tr></table><br>
<a name="801709"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Apollonius</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is just awesome, I didn't know you could use types like this, I have read Types tutorials but I couldn't come up with something like this.<br><br>That is extremely useful, I'll have to re-read a few time and try to understand this better.<br><br>Thanks a bunch. <br>And about that tutorial I just finished downloading the PDF read(reformated recently). <br><br></td></tr></table><br>
<a name="801793"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Apollonius</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oh my oh my, I think I understand! I can put a list of object inside another object such as TButton into TRoom via an array. And then use the TRoom's Update to loop through via a for loop to update all the buttons in that array.<br><br>There's still a place I'm abit fuzzy on but not quite clear on what it is so when I am, I'll post back here lol. <br><br></td></tr></table><br>
<a name="801800"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes yes I think you're getting it now!<br><br>You'll basically have an array of TRooms and each TRoom will have its own array of buttons, monsters etc.<br><br><br>These rooms... are the rooms in your level, or "scenes" of some kind? Because intro and menu don't seem like names of rooms in a dungeon lol. <br><br></td></tr></table><br>
<a name="801827"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Apollonius</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah, that is why I need to change the concept abit.<br><br>So basicly TRoom will be all the stuff before actually being in-game, like:<br><br>@ Introduction( intro:TRoom )<br>Logo(s), Story Clip(Cinematic if you can call it that)<br><br>@ Main Menu ( menu:TRoom )<br>The player could now choose from "Singleplayer", "Multiplayer"(not any time soon), "Settings", "Credits", "Exit"<br><br>And then Rooms for all thoses probably except Exit.<br><br>I'll focus on the Singleplayer story mode and making the mechanics of the game, like the tile system, player, enemies... ect.<br><br>It will take long but it sure will be a challenge and fun :D<br><br>As for ingame, I was thinking of using TWorld and TArea(inside the TWorld) hehe <br><br></td></tr></table><br>
<a name="801960"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Apollonius</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> I gotta say, I'm having trouble understanding some parts completely, I tried to make my own example:<br><pre class=code>
SuperStrict
Graphics 800,600,0

Global startup:TRoom=New TRoom
startup.Buttons=New TButton[1]

startup.Buttons[0]=New TButton
startup.Buttons[0].Create(10,10,70,15,"Click Now") 


While Not KeyHit(KEY_ESCAPE)

	startup.UpdateRoom()

Flip; Cls
Wend; End

Type TRoom
	Field Buttons:TButton[]
	Method UpdateRoom()
		For Local i:Int = 0 To Buttons.length-1
			Buttons[i].Update()
		Next
	End Method
End Type

Type TButton
	Field Text:String
	
	Field xpos:Int
	Field ypos:Int
	Field Width:Int
	Field Height:Int
	
	Field Clicked:Int=False
	
	Method Create(xstart:Int, ystart:Int, wstart:Int, hstart:Int, tstart:String)
		xpos=xstart
		ypos=ystart
		Width=wstart
		Height=hstart
		text=tstart
	End Method
	
	Method Update()
		' Clicked
		If(MouseX()&gt;xpos)And(MouseX()&lt;xpos+Width)And(MouseY()&gt;ypos)And(MouseY()&lt;ypos+Height)Then
			If MouseHit(1)=True Then
				Clicked=True
			EndIf
		EndIf
		If Clicked=True Then Text="Clicked"
		
		Draw()
	End Method
	
	Method Draw()
		SetColor(100,100,100)
		DrawRect xpos, ypos, Width, Height
		SetColor(255,255,255)
		DrawText Text,xpos,ypos
	End Method
End Type
</pre><br><br>Ok this works, now here's teh first place I'd need more explication on:<br><br>startup.Buttons=New TButton[1]<br>startup.Buttons[0]=New TButton<br><br>First one seems to create a new TButton Object. But isn't it the startup.Buttons being the array? TButton isnt an array its an object why is there a [1]<br><br>As for teh second line it stores a TButton object into the first Row of the array.<br><br>-------<br>The Next part I'm having trouble with is the <br>For Local i:Int = 0 To Buttons.length-1<br><br>Button.length gives you the number of rows within a array I think? At first the -1 was confusing me but then I though 1 entry total length 1 but Array line 0.. cuz 0,1,2,3.. So yeah I understand why -1 now .. hehe<br><br>edited<br>Love this:<br><pre class=code>
Global startup:TRoom=New TRoom
startup.Buttons=New TButton[5]

For Local i:Int = 0 To startup.Buttons.length-1
startup.Buttons[i]=New TButton
Next

startup.Buttons[0].Create(10,10,90,15,"Click Now") 
startup.Buttons[1].Create(10,30,90,15,"Click Now") 
startup.Buttons[2].Create(10,50,90,15,"Click Now") 
startup.Buttons[3].Create(10,70,90,15,"Click Now") 
startup.Buttons[4].Create(10,90,90,15,"Click Now") 
</pre><br><br>EDITED<br>Even more Practice, I made two Rooms, if you click all the buttons in the first room it will go to the next room! :)<br>(or you can cheat by pressing enter)<br><br><pre class=code>
SuperStrict
Graphics 800,600,0

'--------------------
' FIRST ROOM: STARTUP
Global startup:TRoom=New TRoom
startup.Buttons=New TButton[5]

For Local i:Int = 0 To startup.Buttons.length-1
startup.Buttons[i]=New TButton
Next

startup.Buttons[0].Create(10,10,90,15,"Click Now") 
startup.Buttons[1].Create(10,30,90,15,"Click Now") 
startup.Buttons[2].Create(10,50,90,15,"Click Now") 
startup.Buttons[3].Create(10,70,90,15,"Click Now") 
startup.Buttons[4].Create(10,90,90,15,"Click Now") 

'--------------------
' SECOND ROOM: SECOND
Global second:TRoom=New TRoom
second.Buttons=New TButton[2]

For Local i:Int = 0 To second.Buttons.length-1
second.Buttons[i]=New TButton
Next

second.Buttons[0].Create(10,10,90,15,"Click Now") 
second.Buttons[1].Create(110,10,90,15,"Click Now") 

'--------------------
' VARIABLES
Global CurrentRoom:TRoom=startup:TRoom


While Not KeyHit(KEY_ESCAPE)

	CurrentRoom.UpdateRoom()

Flip; Cls
Wend; End

Type TRoom
	Field Buttons:TButton[]
	Method UpdateRoom()
		Local totalB:Int
		For Local i:Int = 0 To Buttons.length-1
			Buttons[i].Update()
			
			If(Buttons[i].Clicked=True)Then
				totalB:+1
				If(totalB=Buttons.length)Then
					CurrentRoom=second:TRoom
				EndIf
			
			EndIf
		Next
		
		If KeyHit(KEY_ENTER) Then CurrentRoom=second:TRoom
	End Method
End Type

Type TButton
	Field Text:String
	
	Field xpos:Int
	Field ypos:Int
	Field Width:Int
	Field Height:Int
	
	Field Clicked:Int=False
	
	Method Create(xstart:Int, ystart:Int, wstart:Int, hstart:Int, tstart:String)
		xpos=xstart
		ypos=ystart
		Width=wstart
		Height=hstart
		text=tstart
	End Method
	
	Method Update()
		' Clicked
		If(MouseX()&gt;xpos)And(MouseX()&lt;xpos+Width)And(MouseY()&gt;ypos)And(MouseY()&lt;ypos+Height)Then
			If MouseHit(1)=True Then
				Clicked=True
			EndIf
		EndIf
		If Clicked=True Then Text="Clicked"
		
		Draw()
	End Method
	
	Method Draw()
		SetColor(100,100,100)
		DrawRect xpos, ypos, Width, Height
		SetColor(255,255,255)
		DrawText Text,xpos,ypos
	End Method
End Type
</pre> <br><br></td></tr></table><br>
<a name="801982"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TomToad</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> startup.Buttons=New TButton[1]<br>startup.Buttons[0]=New TButton <br></div><br>When startup is created, you have a field called Buttons which is an array of TButtons, but it hasn't been initialized with a dimension yet.<br>Startup.Buttons=new TButtons[1] tells the program that you now want Buttons to be initialized with a size of 1.  You are not assigning it a specific TButton, but telling it to allocate an array with a dimension of 1 for TButtons.<br>Startup.Buttons[0] = New TButton now assigns element 0 of the array to an actual TButton object, so now you can use the object in your program.<br>Another example here, using Ints and TImages:<br><pre class=code>Local IntArray:Int[]
IntArray = New Int[10] 'Make IntArray 10 elements long
IntArray[0] = 4 'assign a value to IntArray[0]

Local ImageArray:TImage[] 'an array of images
ImageArray = new TImage[10] 'Make ImageArray 10 elements long
ImageArray[0] = New TImage ' assign a TImage to ImageArray[0]
</pre> <br><br></td></tr></table><br>
<a name="802106"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> If you're doing a -1 thingy on a For loop, you can use Until<br><br>So <pre class=code>For Local i:Int = 0 Until second.Buttons.length</pre> is the same as<br><pre class=code>For Local i:Int = 0 To second.Buttons.length-1</pre><br><br>It just makes things a little less cluttered :) <br><br></td></tr></table><br>
<a name="802127"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Apollonius</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> hehe nice.<br><br>For a practice, I'm trying to design what you would call a "Simple GUI" for my game. Basicly it's 4 type objects, TWindow, TButton, TTextfield and TTextview. This should be good practice, I called it  "QGI" which means Quick Game Interface.<br><br>QGI.bmx just needs to be included into a project for it to work. But I've been having "mouse Over" Problems with my button object...<br><br>I feel my button doesn't light up fast enough so I tried to use "Flip 0", it did improve it however it feels as though my style of coding or teh way I coded it is making it slow.<br><br>So maybe you could check how good and optimised my code is, it's quite readable.<br><br>Ok first file is the main.bmx:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict
Graphics 800,600,0

Include "QGI.bmx"

' Windows
Global test:QGI_TWindow=New QGI_TWindow
test.Create(5,5,100,85)

' Objects
test.Buttons=New QGI_TButton[4]

For Local i:Int=0 To test.Buttons.Length-1
	test.Buttons[i]=New QGI_TButton
Next

test.Buttons[0].Create(10,10,90,15,"Testing")
test.Buttons[0].bgColor(50,50,50,100,50,100)
test.Buttons[0].bdColor(100,100,150,200,100,200)
test.Buttons[0].txColor(255,255,255,255,150,255)

test.Buttons[1].Create(10,30,90,15,"Testing")
test.Buttons[1].bgColor(50,50,50,100,50,100)
test.Buttons[1].bdColor(100,100,150,200,100,200)
test.Buttons[1].txColor(255,255,255,255,150,255)

test.Buttons[2].Create(10,50,90,15,"Testing")
test.Buttons[2].bgColor(50,50,50,100,50,100)
test.Buttons[2].bdColor(100,100,150,200,100,200)
test.Buttons[2].txColor(255,255,255,255,150,255)

test.Buttons[3].Create(10,70,90,15,"Testing")
test.Buttons[3].bgColor(50,50,50,100,50,100)
test.Buttons[3].bdColor(100,100,150,200,100,200)
test.Buttons[3].txColor(255,255,255,255,150,255)

While Not KeyHit(KEY_ESCAPE)
	
	test.Update()
	
Flip ; Cls
Wend
End
</textarea><br><br>QGI File<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Type QGI_TWindow
	Field Title:String
	
	Field Xpos:Int
	Field Ypos:Int
	
	Field Width:Int
	Field Height:Int
	
	' Color Scheme
	Field bg_red:Int, bg_green:Int, bg_blue:Int
	Field bd_red:Int, bd_green:Int, bd_blue:Int
	Field tx_red:Int, tx_green:Int, tx_blue:Int
	
	' Internal Objects
	Field Buttons:QGI_TButton[]
	Field Textfields:QGI_TTextfield[]
	Field Textviews:QGI_TTextview[]
	
	Method Create( xstart:Int, ystart:Int, wstart:Int, hstart:Int )
		Xpos=xstart
		Ypos=ystart
		
		Width=wstart
		Height=hstart
	End Method
	
	' Draw Window
	Method Draw()
		SetColor(255,255,255)
		DrawLine( Xpos, Ypos, Xpos+Width, Ypos) 					' top
		DrawLine( Xpos, Ypos+Height, Xpos+Width, Ypos+Height ) 		' bottom
		DrawLine( Xpos, Ypos, Xpos, Ypos+Height ) 					' left
		DrawLine( Xpos+Width, Ypos, Xpos+Width, Ypos+Height ) 		' right	
	End Method
	
	' Handles Every Objects
	Method Update()
		' Buttons
		For Local i:Int = 0 To Buttons.Length-1
			Buttons[i].Update()
		Next
		' Textfields
		For Local i2:Int = 0 To Textfields.length-1
			Textfields[i2].Update()
		Next
		' Textviews
		For Local i3:Int = 0 To Textviews.Length-1
			Textviews[i3].Update()
		Next
		
		Draw()
	End Method
End Type

Type QGI_TButton
	Field Text:String
	Field Clicked:Int
	
	Field Xpos:Int
	Field Ypos:Int
	
	Field Width:Int=10
	Field Height:Int=15
	
	' Color Scheme
	Field bg_red:Int, bg_green:Int, bg_blue:Int
	Field bd_red:Int, bd_green:Int, bd_blue:Int
	Field tx_red:Int, tx_green:Int, tx_blue:Int
	
	Field mbg_red:Int, mbg_green:Int, mbg_blue:Int
	Field mbd_red:Int, mbd_green:Int, mbd_blue:Int
	Field mtx_red:Int, mtx_green:Int, mtx_blue:Int
	
	' Create
	Method Create( xstart:Int, ystart:Int, wstart:Int, hstart:Int, txtstart:String )
		Xpos=xstart
		Ypos=ystart
		
		Width=wstart
		Height=hstart
		
		Text=txtstart
	End Method
	
	' Set Colors
	Method bgColor( r:Int, g:Int, b:Int, mr:Int, mg:Int, mb:Int )
		bg_red=r
		bg_green=g
		bg_blue=b
		
		mbg_red=mr
		mbg_green=mg
		mbg_blue=mb
	End Method
	
	Method bdColor( r:Int, g:Int, b:Int, mr:Int, mg:Int, mb:Int )
		bd_red=r
		bd_green=g
		bd_blue=b
		
		mbd_red=mr
		mbd_green=mg
		mbd_blue=mb
	End Method
	
	Method txColor( r:Int, g:Int, b:Int, mr:Int, mg:Int, mb:Int )
		tx_red=r
		tx_green=g
		tx_blue=b
		
		mtx_red=mr
		mtx_green=mg
		mtx_blue=mb
	End Method
	
	' Drawing
	Method Draw()
		If(MouseX()&gt;Xpos)And(MouseX()&lt;Xpos+Width)And(MouseY()&gt;Ypos)And(MouseY()&lt;Ypos+Height)
		
		' Background
		SetColor( mbg_red, mbg_green, mbg_blue )
		DrawRect Xpos, Ypos, Width, Height
		
		' Borders
		SetColor( mbd_red, mbd_green, mbd_blue )
		DrawLine( Xpos, Ypos, Xpos+Width, Ypos) 					' top
		DrawLine( Xpos, Ypos+Height, Xpos+Width, Ypos+Height ) 		' bottom
		DrawLine( Xpos, Ypos, Xpos, Ypos+Height ) 					' left
		DrawLine( Xpos+Width, Ypos, Xpos+Width, Ypos+Height ) 		' right
		
		' Text
		SetColor( mtx_red, mtx_green, mtx_blue )
		DrawText Text,Xpos+1,Ypos+1
		
		Else
	
		' Background
		SetColor( bg_red, bg_green, bg_blue )
		DrawRect Xpos, Ypos, Width, Height
		
		' Borders
		SetColor( bd_red, bd_green, bd_blue )
		DrawLine( Xpos, Ypos, Xpos+Width, Ypos) 					' top
		DrawLine( Xpos, Ypos+Height, Xpos+Width, Ypos+Height ) 		' bottom
		DrawLine( Xpos, Ypos, Xpos, Ypos+Height ) 					' left
		DrawLine( Xpos+Width, Ypos, Xpos+Width, Ypos+Height ) 		' right
		
		' Text
		SetColor( tx_red, tx_green, tx_blue )
		DrawText Text,Xpos+1,Ypos+1
		
		EndIf
	End Method
	
	' Handles Everything
	Method Update()
		
		
		Draw()
	End Method
End Type

Type QGI_TTextfield
	Field Name:String
	Field Text:String
	Field Selected:Int
	
	' Color Scheme
	Field bg_red:Int, bg_green:Int, bg_blue:Int
	Field bd_red:Int, bd_green:Int, bd_blue:Int
	Field tx_red:Int, tx_green:Int, tx_blue:Int
	
	' Drawing
	Method Draw()
		
	End Method
	
	' Handles Everything
	Method Update()
		
	End Method
End Type

Type QGI_TTextview
	Field Name:String
	Field Text:String[]
	
	' Color Scheme
	Field bg_red:Int, bg_green:Int, bg_blue:Int
	Field bd_red:Int, bd_green:Int, bd_blue:Int
	Field tx_red:Int, tx_green:Int, tx_blue:Int
	
	' Drawing
	Method Draw()
		
	End Method
	
	' Handles Everything
	Method Update()
		
	End Method
End Type
</textarea><br><br>I'm only working with the TWindow and TButton, I haven't worked really on the others yet.<br><br>I appreciate the help Czar Flavius! <br><br></td></tr></table><br>
<a name="802146"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Try using [ codebox ] instead of [ code ] then you get nice scroll bars on your code : ) <br><br></td></tr></table><br>
<a name="802148"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Apollonius</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Done, so you see the mouse over button things is not really optimised and I wonder how can I optimise them to make the mouse over effect faster? <br><br></td></tr></table><br>
<a name="802153"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> The speed seems OK for me.<br>You've duplicated some code in the QGI_TButton<br> draw() method but that shouldn't matter.<br>The other thing you need to do is store off mousex() and mousey() values to check against.<br>You might want to consider images which can be checked using a collision layer for all the buttons. <br><br></td></tr></table><br>
<a name="802179"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Apollonius</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah I though the duplication of the Draw Method could slow it down, but then I couldn't figure out another way that would improve it.<br><br>as for<br>"The other thing you need to do is store off mousex() and mousey() values to check against."<br><br>What do you mean? (kinda lost with that sentence) <br><br></td></tr></table><br>
<a name="802180"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> Test  mousex() and mousey() once per loop and <br>return the values into variables:<br>  local mx:int=mousex()<br>  local my:int=mousey()<br>You might consider creating a mouse object to hold this kind of data.<br>You're not doing enough in your update() to cause anything to slowdown so it could be a driver issue. Try GL driver and see what happens. Try omitting the drawline stuff and see what happens.<br>There's nothing I can see wrong with the code itself.<br><br>&lt;edit&gt; <a href="/posts.php?topic=71041#794254" target="_blank"> P.S. Have you bought Bmax yet? </a> <br><br></td></tr></table><br>
<a name="802190"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Apollonius</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> Haven't ever tried OpenGL so I'm scared to dive into it right now :o<br><br>To the EDIT<br>Maybe I have maybe I haven't ;D <br><br></td></tr></table><br>
<a name="802196"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Haven't ever tried OpenGL so I'm scared to dive into it right now :o<br> <br></div><br>You simply add<br>   Setgraphicsdriver glmax2ddriver() <br>just before your graphics statement. As long as you're using native Bmax commands it does the rest for you.<br><br><div class="quote"> Maybe I have maybe I haven't ;D <br></div><br>OK, I'll check again each time you post. <br><br></td></tr></table><br>
<a name="802244"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Apollonius</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> Seems to run way better with the GLMax2DDriver() thingy. This is great, does it mean it can run on mac now, since it's using openGL drivers? :o <br><br></td></tr></table><br>
<a name="802272"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> If you're using native Bmax commands only then it's very possible it'll compile and run on Mac. <br><br></td></tr></table><br>
<a name="802639"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Apollonius</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm looking around but I can't find.. how can you destroy an object? I tried Delete but, no good? <br><br></td></tr></table><br>
<a name="802640"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >tonyg</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#23">[#23]</a></td></tr></table></td></tr><tr ><td class="posttext"> The object will be destroyed when there are no more references to it. <br><br></td></tr></table><br>
<a name="802649"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Apollonius</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#24">[#24]</a></td></tr></table></td></tr><tr ><td class="posttext"> If KeyHit(KEY_ENTER) Then test.Buttons[0]=Null<br><br>Wouldn't that remove the reference to it? i got an error :S <br><br></td></tr></table><br>
<a name="802652"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#25">[#25]</a></td></tr></table></td></tr><tr ><td class="posttext"> What error?<br><br>Setting all references to it to Null should delete the object. <br><br></td></tr></table><br>
<a name="802658"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Apollonius</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#26">[#26]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>
If KeyHit(KEY_ENTER) Then test.Buttons[0]=Null
</pre><br><br><pre class=code>
	Method Update()
		' Buttons
		For Local i:Int = 0 To Buttons.Length-1
			If Not Button[i]=Null Then
			Buttons[i].Update()
			EndIf
		Next
</pre><br><br>Error's at the Update() so i added a if not NULL.. then.. but that didn't help either it makes an error at it now lol. <br><br></td></tr></table><br>
<a name="802662"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Czar Flavius</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#27">[#27]</a></td></tr></table></td></tr><tr ><td class="posttext"> You're calling it Button and ButtonS is that the problem? What is the actualy error message you are receiving? <br><br></td></tr></table><br>
<a name="803448"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Apollonius</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#28">[#28]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sorry for the late reply, Czar Flavius. I wasn't at home for a few days.<br><br>Thanks for pointing that out it missed an "s" as soon as I corrected that everything worked. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
