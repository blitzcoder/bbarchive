<!DOCTYPE html><html lang="en" ><head ><title >Objects Question</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Objects Question</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >Objects Question</a><br><br>
<a name="956017"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dezzo</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> If you press the Key 1 or 2 you can select/highlight the Instance as long as you keep the key pressed. How can i make the selection stay until i press the other key ? ( almost like a switch ). You should be able to highlight 1 Instance only. I totaly miss something important here i know,  just can´t figure out what ?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Graphics 640,480

Global active:Int

Type button
	Field x_pos:Int
	Field y_pos:Int
	Field width :Int,height :Int
	Field color_r :Int,color_g :Int ,color_b :Int
	Field Active :Int
	Field name:Int
	Field key:Int
	
	
	Method highlight()
		SetColor (255,255,255)
		DrawRect (x_pos-5,y_pos-5,110,110)
		DrawText "Name / State: " + name + " " + Active ,x_pos,y_pos+120
		DrawText "KEY Number : "+ key ,x_pos,y_pos+220
	End Method
		
	Method drawbutton()
		klicked()
		SetColor (color_r,color_g,color_b)
		DrawRect (x_pos,y_pos,width,height)
	End Method
	
	Method klicked()
		If KeyDown(key)
			highlight()
			Active = True
			Else Active = False
		EndIf
	End Method
End Type

Local one:button = New button
Local two:button = New button

one.x_pos = 20
one.y_pos = 20
one.width = 100
one.height = 100
one.color_r = 255
one.color_g = 0
one.color_b = 255
one.name = 1
one.key = key_1

two.x_pos = 200
two.y_pos = 20
two.width = 100
two.height = 100
two.color_r = 0
two.color_g = 0
two.color_b = 255
two.name = 2
two.key = key_2

Repeat

one.drawbutton()
two.drawbutton()	

			
Flip;Cls
Until KeyHit(key_escape)
End

</textarea> <br><br></td></tr></table><br>
<a name="956019"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Glenn Dodd</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> here is a thought starter for you.<br><br>you should only turn highlight on if active is true.<br><br>	Method drawbutton()<br>		klicked()<br>		If active = True Then highlight()<br>		SetColor (color_r,color_g,color_b)<br>		DrawRect (x_pos,y_pos,width,height)<br>	End Method<br>	<br>	Method klicked()<br>		If KeyHit(key)<br>'		If KeyDown(key)<br>'			highlight()<br>			Active = True<br>'			Else Active = False<br>		EndIf<br><br><br>you now need a mechanism to turn the other buttons active status to false when you turn one on. the above just turns the active status on and leaves it on.<br><br>hope this points you in the right direction <br><br></td></tr></table><br>
<a name="956591"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dezzo</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thx for the fast Reply !<br>I found a Solution but i don´t think that it´s an elegant one ...<br>What do you think ?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Graphics 640,480

'Global active:Int


Type button
	Field x_pos:Int
	Field y_pos:Int
	Field width :Int,height :Int
	Field color_r :Int,color_g :Int ,color_b :Int
	Field Active :Int = False
	Field name:Int
	Field key:Int
	
	Method highlight()
		SetColor (255,255,255)
		DrawRect (x_pos-5,y_pos-5,110,110)
		DrawText "Name / State: " + name + " " + Active ,x_pos,y_pos+120
		DrawText "KEY Number : "+ key ,x_pos,y_pos+220
	End Method
		
	Method drawbutton()
		check()
		If Active = True Then highlight()
		SetColor (color_r,color_g,color_b)
		DrawRect (x_pos,y_pos,width,height)
	End Method
	
	Method check() 
		If KeyHit(key_1) = True
			one.Active = True
			two.Active = False
			three.Active = False
		EndIf
		If KeyHit(key_2) = True
			one.Active = False
			two.Active = True
			three.Active = False
		EndIf
		If KeyHit(key_3) 
			one.Active = False
			two.Active = False
			three.Active = True
		EndIf
	End Method
	
End Type

Global one:button = New button
Global two:button = New button
Global three:button = New button

one.x_pos = 20
one.y_pos = 20
one.width = 100
one.height = 100
one.color_r = 255
one.color_g = 0
one.color_b = 255
one.name = 1
one.key = key_1

two.x_pos = 200
two.y_pos = 20
two.width = 100
two.height = 100
two.color_r = 0
two.color_g = 0
two.color_b = 255
two.name = 2
two.key = key_2

three.x_pos = 400
three.y_pos = 20
three.width = 100
three.height = 100
three.color_r = 0
three.color_g = 255
three.color_b = 0
three.name = 3
three.key = key_3

Repeat
	one.drawbutton()
	two.drawbutton()
	three.drawbutton()				
Flip;Cls
Until KeyHit(key_escape)
End

</textarea> <br><br></td></tr></table><br>
<a name="956597"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jsp</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Have a look for some comments in the code<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Graphics 640,480

'Global active:Int


Type button
	Global AllButtons:TList = New TList	'A list of all created buttons

	Field x_pos:Int
	Field y_pos:Int
	Field Width :Int,Height :Int
	Field color_r :Int,color_g :Int ,color_b :Int
	Field Active :Int = False
	Field name:Int
	Field key:Int

	'A function which can be used ever and ever again, it returns you new button
	Function Create:button(X:Int, Y:Int, Width:Int, Height:Int, r:Int, g:Int, b:Int, Name:Int, key:Int)
		Local MyButton:button = New button
		MyButton.x_pos = X
		MyButton.y_pos = Y
		MyButton.Width = Width
		MyButton.Height = Height
		MyButton.color_r = r
		MyButton.color_g = g
		MyButton.color_b = b
		MyButton.name = Name
		MyButton.key = key
	
		Return MyButton
	End Function
		
	Method New()
		AllButtons.AddLast(Self)	'The new method stores automatically the button in a list
	End Method
	
	Method highlight()
		SetColor (255,255,255)
		DrawRect (x_pos-5,y_pos-5,110,110)
		DrawText "Name / State: " + name + " " + Active ,x_pos,y_pos+120
		DrawText "KEY Number : "+ key ,x_pos,y_pos+220
	End Method
		
	Method drawbutton()
'		check()						'better not to mix drawing and logic
		If Active = True Then highlight()
		SetColor (color_r,color_g,color_b)
		DrawRect (x_pos,y_pos,Width,Height)
	End Method
Rem	
	Method check() 
		If KeyHit(key_1) = True
			one.Active = True
			two.Active = False
			three.Active = False
		EndIf
		If KeyHit(key_2) = True
			one.Active = False
			two.Active = True
			three.Active = False
		EndIf
		If KeyHit(key_3) 
			one.Active = False
			two.Active = False
			three.Active = True
		EndIf
	End Method
End Rem
	Method check()	'more generic, thus you can have as many buttons as you want and don't need to extend the method
		If KeyHit( key ) Then Active = True ; SwitchOffOthers()
	End Method
	
	Method SwitchOffOthers()	'just to switch all other buttons off
		For Local i:button = EachIn AllButtons
			If i = Self Then Continue	'skip the button who active
			i.Active = False			'other are off now
		Next
	End Method
End Type

'Global one:button = New button
'Global two:button = New button
'Global three:button = New button

Global one:button = button.Create(20,20,100,100,255,0,255,1,Key_1) 'call the create function
Rem
one.x_pos = 20
one.y_pos = 20
one.Width = 100
one.Height = 100
one.color_r = 255
one.color_g = 0
one.color_b = 255
one.name = 1
one.key = key_1
End Rem
Global two:button = button.Create(200,20,100,100,0,0,255,2,Key_2)

Rem
two.x_pos = 200
two.y_pos = 20
two.Width = 100
two.Height = 100
two.color_r = 0
two.color_g = 0
two.color_b = 255
two.name = 2
two.key = key_2
End Rem
Global three:button = button.Create(400,20,100,100,0,255,0,3,Key_3)

Rem
three.x_pos = 400
three.y_pos = 20
three.Width = 100
three.Height = 100
three.color_r = 0
three.color_g = 255
three.color_b = 0
three.name = 3
three.key = key_3
End Rem

Repeat
'	one.drawbutton()
'	two.drawbutton()
'	three.drawbutton()
Cls	
	For Local i:button = EachIn button.AllButtons 'create as many buttons as you need without adding them here
		i.Check()
		i.drawbutton()
	Next		
Flip
Until KeyHit(key_escape)
End


</textarea> <br><br></td></tr></table><br>
<a name="956618"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Jesse</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I would do it something like this. <br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Graphics 640,480


Type button
	Field x_pos:Int
	Field y_pos:Int
	Field width :Int,height :Int
	Field color_r :Int,color_g :Int ,color_b :Int
	Field name:Int
	Field key:Int
	
	Method highlight()
		SetColor (255,255,255)
		DrawRect (x_pos-5,y_pos-5,110,110)
		DrawText "Name / State: " + name + " " ,x_pos,y_pos+120
		DrawText "KEY Number : "+ key ,x_pos,y_pos+220
	End Method
		
	Method draw(active:Int)
		If Active = key Then highlight()
		SetColor (color_r,color_g,color_b)
		DrawRect (x_pos,y_pos,width,height)
	End Method
	
	
End Type

Type Tbuttons
	Field one:button
	Field two:button
	Field three:button
	Field active:Int = 0
	
	
	Method Create()
		one = New button
		one.x_pos = 20
		one.y_pos = 20
		one.width = 100
		one.height = 100
		one.color_r = 255
		one.color_g = 0
		one.color_b = 255
		one.name = 1
		one.key = key_1
		
		two = New button
		two.x_pos = 200
		two.y_pos = 20
		two.width = 100
		two.height = 100
		two.color_r = 0
		two.color_g = 0
		two.color_b = 255
		two.name = 2
		two.key = key_2
		
		three = New button
		three.x_pos = 400
		three.y_pos = 20
		three.width = 100
		three.height = 100
		three.color_r = 0
		three.color_g = 255
		three.color_b = 0
		three.name = 3
		three.key = key_3
	End Method

	Method check()
		Select True
			Case KeyHit(key_1) active = key_1
			Case KeyHit(key_2) active = key_2
			Case KeyHit(key_3) active = key_3
		End Select
	End Method
	
	Method draw()
		one.draw(active)
		two.draw(active)
		three.draw(active)
	End Method
End Type

Global buttons:Tbuttons = New Tbuttons
buttons.Create()

Repeat
	buttons.check
	buttons.draw	
	Flip;Cls
Until KeyHit(key_escape)
End
</textarea><br><br>but I was devating whether or not to pass the variable in the draw method.<br>doing it this way saves several if statements but in my opinion makes it less flexible. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
