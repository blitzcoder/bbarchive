<!DOCTYPE html><html lang="en" ><head ><title >events</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >events</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=101" >BlitzMax Beginners Area</a>/<a href="#bottom" >events</a><br><br>
<a name="611002"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm trying to figure out things like eventhooks 'n stuff.. can someone explain it a bit, for example by adding such a mechanism to the small example-source below?<br>So, the idea is that I don't need that t.ev in the mainloop anymore, everything should become transparent, just like the standard built-in GUI objects..<br>It should also emit some new events I could read out with EventSource()..<br><br>MaxGUI really needs more examples.., as good as B+ was, as minimalistic/sober MaxGUI is .. ._.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Type test

	Field canvas:TGadget
	
	Method ev()
		If EventSource()=canvas
			If EventID()=EVENT_MOUSEENTER
				update 1
			EndIf
			If EventID()=EVENT_MOUSELEAVE
				update
			EndIf
			If EventID()=EVENT_GADGETPAINT
				update
			EndIf
		EndIf
	End Method

	Method update(state:Byte=0)
		SetGraphics CanvasGraphics(canvas)
			SetClsColor 128+state*127,128+state*127,128+state*127
			Cls
		Flip
	End Method

End Type

Function createtest:test(parent:TGadget)
	Local a:test=New test
	a.canvas=CreateCanvas(32,32,48,24,parent)
	Return a
End Function

Local win:TGadget=CreateWindow("o_O",0,0,640,480)

Local t:test=createtest(win)

Repeat
	WaitEvent()
	t.ev
	If EventID()=EVENT_WINDOWCLOSE End
Forever

</textarea> <br><br></td></tr></table><br>
<a name="611007"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Try this...<br><pre class=code>
Strict

Type test

	Field canvas:TGadget
	
	Method ev(event:TEvent)
		
		Select event.ID
			Case EVENT_MOUSEENTER
			 	update 1
		
			Case EVENT_MOUSELEAVE,EVENT_GADGETPAINT
				update
		End Select
		
	End Method

	Method update(state:Byte=0)
		SetGraphics CanvasGraphics(canvas)
			SetClsColor 128+state*127,128+state*127,128+state*127
			Cls
		Flip
	End Method
	
	Function create:test(parent:tgadget)
		Local a:test=New test
	a.canvas=CreateCanvas(10,10,20,20,parent)
	Return a
	End Function 

End Type


Local win:TGadget=CreateWindow("o_O",0,0,400,120)

Global t:test=test.create(win)

AddHook EmitEventHook,EventHook

Repeat
	WaitEvent()
	't.ev
	If EventID()=EVENT_WINDOWCLOSE End
Forever

Function EventHook:Object(iId,tData:Object,tContext:Object)
'event is in TDATA
'source --&gt; event.Source (method)
'event ID (EVENT_MOUSEENTER...) --&gt; event.ID

    Local Event:TEvent=TEvent(tData)

	Print "Event : "+event.tostring()
	Print "Source: "+String(event.source)

    Select Event.Source
      Case t.canvas
		t.ev(event)
		Print "event catched..."
        Return Null      
		Default
		Print "No event..."
      End Select
    Return Event         'Event pass through (nothing thats been captured)
End Function
</pre><br>I modified some thing to reach the goal...I extended your TEST type to catch the event, the EventHook function is from this forum - I don't remember who wrote it. <br><br></td></tr></table><br>
<a name="611009"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >degac</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>
Strict

Type test

	Field canvas:TGadget
	
	Method ev(event:TEvent)
		
		Select event.ID
			Case EVENT_MOUSEENTER
			 	update 1
		
			Case EVENT_MOUSELEAVE,EVENT_GADGETPAINT
				update
		End Select
		
	End Method

	Method update(state:Byte=0)
		SetGraphics CanvasGraphics(canvas)
			SetClsColor 128+state*127,128+state*127,128+state*127
			Cls
		Flip
	End Method
	
	Function create:test(parent:tgadget)
		Local a:test=New test
	a.canvas=CreateCanvas(10,10,20,20,parent)
	
	AddHook EmitEventHook,a.EventHook
	Return a
	End Function 
	
	Function EventHook:Object(iId,tData:Object,tContext:Object)
'event is in TDATA
'source --&gt; event.Source (method)



    Local Event:TEvent=TEvent(tData)

	Print "Event : "+event.tostring()
	Print "Source: "+String(event.source)

    Select Event.Source
      Case t.canvas
		t.ev(event)
		Print "event catched..."
        Return Null      
		Default
		Print "No event..."
      End Select
    Return Event         'Event pass through (nothing thats been captured)
End Function
End Type


Local win:TGadget=CreateWindow("o_O",0,0,400,120)
Global t:test=test.create(win)

'AddHook EmitEventHook,t.EventHook

Repeat
	WaitEvent()
	't.ev
	If EventID()=EVENT_WINDOWCLOSE End
Forever
</pre><br>Just now I realized that I can 'inglobe' all the function - even EventHook - in the type...<br>[edit]: modified again: now when you create a TEST type you add the eventhook <br><br></td></tr></table><br>
<a name="614805"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> That example is not that useful to me.. <br><br>Globals are evil and should be avoided.. :(<br><br>it should be like this:<br><br>Local t:test=test.create(win)<br><br>And obviously in the EventHook function there can't be a reference to that global t .. <br><br></td></tr></table><br>
<a name="614829"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >klepto2</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>
Strict

Type Test
	Global test_List:TList = New TList
	Global Act_Can:Test = Null

	Field canvas:TGadget
	
	Function Hook:Object(id,data:Object,context:Object)
	Local ev:TEvent=TEvent(data)
	If Not ev Return data
	'Print ev.toString()
		Select ev.id
			Case Event_MouseEnter
				Act_Can = GetSelectedCanvas(Ev)
					If Act_Can &lt;&gt; Null Then
						Act_Can.Update(1)
					EndIf
			Case Event_MouseLeave
					If Act_Can &lt;&gt; Null 
						Act_Can.Update(0) 
						Act_Can = Null
					EndIf
			Case Event_TimerTick
				For Local T:Test = EachIn Test.Test_List
					RedrawGadget T.Canvas
				Next
		End Select
			
	Return data
	
	End Function
	
	Function GetSelectedCanvas:Test(Event:TEvent)
		For Local T:Test = EachIn Test.Test_List
			If T.Canvas = Event.Source Then
			Return T
			End If
		Next
		Return Null
	End Function

	Method update(state:Byte=0)
		SetGraphics CanvasGraphics(canvas)
			SetClsColor 128+state*127,128+state*127,128+state*127
			Cls
		Flip
	End Method
	
	Function create:Test(x,y,w,h,parent:TGadget)
		Local a:Test=New Test
		a.canvas=CreateCanvas(x,y,w,h,parent)
		a.Update(0)
		Test.Test_List.Addlast(A)
		AddHook EmitEventHook,Test.Hook,A,0
		Return a
	End Function

End Type

Function createtest:Test(x,y,w,h,parent:TGadget)
	Return Test.Create(x,y,w,h,parent)
End Function

Local win:TGadget=CreateWindow("o_O",0,0,640,480)

Local t:Test=createtest(32,32,48,24,win)
t:Test=createtest(32,60,48,24,win)
t:Test=createtest(32,88,48,24,win)




Repeat
	WaitEvent()
	If EventID()=EVENT_WINDOWCLOSE End
Forever

</pre><br><br>This is a small workaround, I hope this helps you <br><br></td></tr></table><br>
<a name="614837"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Also not what I mean .. sorry ..<br><br>It may even all work, but I really wonder why it isn't possible to have it all a bit more transparent like the rest.<br><br>A hook for each instance, without instances even knowing other instances.. like it is now.. since they're in the same list.<br><br>So create them like this:<br><pre class=code>
local t:createtest(10,20,30,40,win)
local t2:createtest(30,40,50,60,win)
local doggy:createtest(50,60,70,80,win)
local helloworld:createtest(150,160,170,180,win)
local bratwurst:createtest(150,160,170,180,win)

.. and each one should work indepentently..
</pre><br>All these, not being in <i>any</i> list, and each with its own private hook. No globals, no type-globals, no lists.. It should look/feel just like the first sample I posted, but then automated with hooks..<br><br>With degac's last one I'd be done, except for the global..<br><br>Yes, sorry for my anti-globalism-campaign, but uhm.. I hate them.. :D<br><br>Any new suggestions? :P <br><br></td></tr></table><br>
<a name="615062"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >klepto2</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>
Strict

Type Test

	Field canvas:TGadget
	
	Function Hook:Object(id,data:Object,context:Object)
	Local ev:TEvent=TEvent(data)
	If Not ev Return data
	'Print ev.toString()
	Local Act_Can:Test = Test(TGadget(ev.source).Context)
	If Act_Can &lt;&gt; Null Then
		Select ev.id
			Case Event_MouseEnter
					Act_Can.Update(1)
			Case Event_MouseLeave
						Act_Can.Update(0) 
			Case Event_TimerTick
					RedrawGadget Act_Can.Canvas
		End Select
	EndIf		
	Return data
	
	End Function
	
	Method update(state:Byte=0)
		SetGraphics CanvasGraphics(canvas)
			SetClsColor 128+state*127,128+state*127,128+state*127
			Cls
		Flip
	End Method
	
	Function create:Test(x,y,w,h,parent:TGadget)
		Local a:Test=New Test
		a.canvas=CreateCanvas(x,y,w,h,parent)
		a.canvas.Context = a
		a.Update(0)
		AddHook EmitEventHook,Test.Hook,Null,0
		Return a
	End Function

End Type

Function createtest:Test(x,y,w,h,parent:TGadget)
	Return Test.Create(x,y,w,h,parent)
End Function

Local win:TGadget=CreateWindow("o_O",0,0,640,480)

Local t:Test=createtest(32,32,48,24,win)
t:Test=createtest(32,60,48,24,win)
t:Test=createtest(32,88,48,24,win)


Repeat
	WaitEvent()
	If EventID()=EVENT_WINDOWCLOSE End
Forever
</pre><br><br>This is my new suggestion, as you can add context objects to  <br>each Gadget you could attach the actual Test Type to the cnavas. With Local Act_Can:Test = Test(TGadget(ev.source).Context) you can receive the Test Type attached with the event.source. and use this to update your things. <br><br></td></tr></table><br>
<a name="615144"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >CS_TBL</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> almost ..<br><br>..but not yet :D<br><br>I think there should simply be an events-method. And the only thing the hook should do is call that events-method. The hook function shouldn't do the actual events itself.<br><br>This event function would look like this:<br><br><pre class=code>
Method Events()

  If EventSource()=gadget1
    If EventID()=id1
    EndIf
    If EventID()=id2
    EndIf
  Endif

  If EventID()=id3 ; just for this order-swapping I'm not much a fan of Select-Case constructions..
    If EventSource()=gadget2
    EndIf
    If EventSource()=gadget3
    EndIf
  Endif
  ; And yes, you can assume there's more than 1 gadget for an object like this..

End Method
</pre><br><br><br>Actually I'm surprised this baby takes so long to deliver :P The idea isn't that strange I'd say.<br><br>- create independent unlisted local object<br>- create event-function for object<br>- create eventhook that calls that event-function<br><br>- and next step: emit new unique events so I can use this object everywhere<br><br><br>The endgoal is that there's a means to create new GUI gadgets and other self-contained objects that look 100% transparent with the rest of the gadgets. So it must work in a <i>100% similar</i> way. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
