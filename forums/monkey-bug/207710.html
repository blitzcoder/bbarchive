<!DOCTYPE html><html lang="en" ><head ><title >Debug Uncaught SyntaxError: Unexpected token )</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Debug Uncaught SyntaxError: Unexpected token )</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=501" >Monkey Bug Reports</a>/<a href="#bottom" >Debug Uncaught SyntaxError: Unexpected token )</a><br><br>
<a name="2074944"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> When running Jake's (GreyAlien) maze code in HTML5 in Debug mode, the generated JS creates an incorrect line or two:<br><br>Uncaught SyntaxError: Unexpected token ) <br><br>dbg_array(t_rows,)[dbg_index]=dbg_array(t_rows,)[dbg_index]+"###"<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Strict

Function Main:Int()

	Local maze:Level = New Level(16, 22)
	maze.ToString()

	Return 0
End

Class Level

	Field rooms:TRoom[][]
	Field width:Int = 10
	Field height:Int = 10
		
	Method New(width:Int, height:Int)
	
		self.width = width
		self.height = height
		GenerateRooms()
	
	End	
		
	Method GenerateRooms:Void()
		
		rooms = ArrayUtil&lt;TRoom&gt;.CreateArray(width, height) 'This makes a 2D Array
		
		'Create a room For each Array slot.
		For Local x:Int = 0 To width - 1
			For Local y:Int = 0 To height - 1
				Local r:TRoom = New TRoom
				r.x = x
				r.y = y
				rooms[x][y] = r
			Next
		Next
		
		'Make a maze
		Local x:Int = 0
		Local y:Int = 1
		Local roomStack:Stack&lt;TRoom&gt; = New Stack&lt;TRoom&gt;() 'stores rooms that have unvisited neighbours
		
		'Get a starting room
		Local r:TRoom = rooms[x][y]
		r.startRoom = 1
		
		'Create an exit room on an edge (after we’ve marked the start room)
		'First pick an an edge
		Local loop:Int = 1
		
		While loop
		
			Local edge:Int = Floor(Rnd(0, 3)) 'ccRand(0, 3) 'clockwise with 0 = North
			'Now pick a position on the edge. MarkAsExit returns false if the chosen room is the start room.
			Select edge
				Case 0
					If rooms[Floor(Rnd(0, width))][0].MarkAsExit(edge) Then loop = 0
				Case 1
					If rooms[width - 1][Floor(Rnd(0, height))].MarkAsExit(edge) Then loop = 0
				Case 2
					If rooms[Floor(Rnd(0, width))][height - 1].MarkAsExit(edge) Then loop = 0
				Case 3
				
				If rooms[0][Floor(Rnd(0, height))].MarkAsExit(edge) Then loop = 0
			End Select
		End While
		
		loop = 1
		While loop = 1
			r.generated = 1 'Mark this room as visited this room during the generation process.
		
			'Build list of ungenerated neighbours
			Local possibleRooms:List&lt;TRoom&gt; = New List&lt;TRoom&gt;()
			If x &gt; 0 And rooms[x - 1][y].generated = 0 Then possibleRooms.AddLast(rooms[x - 1][y])
			If x &lt; width - 1 And rooms[x + 1][y].generated = 0 Then possibleRooms.AddLast(rooms[x + 1][y])
			If y &gt; 0 And rooms[x][y - 1].generated = 0 Then possibleRooms.AddLast(rooms[x][y - 1])
			If y &lt; height - 1 And rooms[x][y + 1].generated = 0 Then possibleRooms.AddLast(rooms[x][y + 1])
		
			'Pick a destination room randomly
			If possibleRooms.Count() &gt; 0 Then
			'Remove the wall (wall indexes are clockwise starting from 0=North)
			Local newRoom:TRoom = possibleRooms.ToArray()[Floor(Rnd(0, possibleRooms.Count()))]
			If x &gt; newRoom.x Then
				r.walls[3] = 0
				newRoom.walls[1] = 0
			Elseif x &lt; newRoom.x Then
				r.walls[1] = 0
				newRoom.walls[3] = 0
			Elseif y &gt; newRoom.y Then
				r.walls[0] = 0
				newRoom.walls[2] = 0
				Elseif y &lt; newRoom.y Then
				r.walls[2] = 0
				newRoom.walls[0] = 0
			Endif
		
		'Only store the current room if it has more than one desintation
		If possibleRooms.Count() &gt; 1 Then roomStack.Push(r)
			r = newRoom
			x = r.x
			y = r.y
			'continue looping
		Else
		'There are no possible destinations so go back And get a room from the stack.
			If roomStack.Length() &gt; 0 Then
				r = roomStack.Pop()
				x = r.x
				y = r.y
				'continue looping
			Else
			'We’ve run out of previous rooms so stop looping.
				loop = 0
			Endif
		Endif
		Wend
	End Method
	
	Method ToString:Void()	
	
		Print "MAZE GENERATED:"
		Local rows:String[height * 3]
		For Local y:Int = 0 To height - 1
			For Local x:Int = 0 To width - 1
				Local r:TRoom = rooms[x][y]
				If r.walls[0] Then
					rows[y * 3] += "###"
				Else
					rows[y * 3] += "# #"
				Endif
				If r.walls[3] Then
					rows[y * 3 + 1] += "#"
				Else
					rows[y * 3 + 1] += " "
				Endif
				If r.exitRoom &gt; - 1 Then
					rows[y * 3 + 1] += "E"
				Else
					rows[y * 3 + 1] += " "
				Endif
				If r.walls[1] Then
					rows[y * 3 + 1] += "#"
				Else
					rows[y * 3 + 1] += " "
				Endif
				If r.walls[2] Then
					rows[y * 3 + 2] += "###"
				Else
					rows[y * 3 + 2] += "# #"
				Endif
			Next
			Print rows[y * 3]
			Print rows[y * 3 + 1]
			Print rows[y * 3 + 2]
		Next	
	End Method	
		
End Class


Class TRoom	
	Field exitRoom:Int
	Field generated:Int
	Field startRoom:Int
	Field x:Int
	Field y:Int
	Field walls:Int[] 'clockwise starting with North.

	Method MarkAsExit:Int(edge:Int)
		'Is this a start room? If so abort.
		If startRoom Then Return 0

		'Edge is clockwise And 0=North
		exitRoom = edge
		walls[edge] = 0
		Return 1
	End Method

	Method New()
		'Create 4 walls
		walls = New Int[4]
		For Local i:Int = 0 To 3
			walls[i] = 1
		Next
		exitRoom = -1
	End Method
End Class


Class ArrayUtil&lt;T&gt;

	Function CreateArray:T[][] (rows:Int, cols:Int) 'by muddy_shoes
		Local a:T[][] = New T[rows][]
		For Local i:Int = 0 Until rows
			a[i] = New T[cols]
		Next

		Return a
	End Function
	
End Class
</textarea> <br><br></td></tr></table><br>
<a name="2075052"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grey Alien</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> It also bugs out when compiling for Flash in Debug mode. It's fine in GLFW debug. This applies to my original code that was converted into the above code. Haven't actually tested the above code in Flash yet. <br><br></td></tr></table><br>
<a name="2075218"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >marksibly</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Fixed in next update. <br><br></td></tr></table><br>
<a name="2075221"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >therevills</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Mark.<br><br>BTW you might want to update the README.TXT on Github:<br><br>For more Monkey fun, please visit: <a href="http://www.monkeycoder.co.nz" target="_blank">http://www.monkeycoder.co.nz</a> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
