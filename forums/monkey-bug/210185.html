<!DOCTYPE html><html lang="en" ><head ><title >Mojo2 object scope and clearing ILayer/ILight</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Mojo2 object scope and clearing ILayer/ILight</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=501" >Monkey Bug Reports</a>/<a href="#bottom" >Mojo2 object scope and clearing ILayer/ILight</a><br><br>
<a name="2112492"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Richard Betson</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>I am having a couple issues with mojo2. The first is that I can't seem to create a mojo2 class object outside the scope on 'OnCreate' In the modified 'Renderer' demo (below) if you try to create a mojo2 object within a 'Field' it will fail with a 'Null' object. I have also tried creating a mojo2 class instances (object) outside of 'OnCreate' and also get a 'Null' object. So in the code below you can try to initialize an object within a 'Field' in the MyApp class and it will return a 'Null' Object.<br><br>The second issue is in clearing a mojo2 render image layer (ILayer) and lights (ILight). I have added a class called 'Test' to this modified "Renderer' demo in which I clear the lights and layer. Within 'Test' (class) the ClearLight() method clears the ILayer and ILight lists. I think this is the right method to clear the layers and lights. The demo below continually adds to a layer then clears it. This slows the application to a crawl and I am either doing it wrong or it's a bug. ;)<br><br>The best way to get a sense of what I mean is to put this demo (below) in the same directory as the 'Renderer' demo and run it. Then comment out the first two lines (commands) in 'OnRender' and run it again. You will see the difference in speed.<br><br>Being able to add to a layer and clearing it each frame is what is needed to support scrolling tile maps. So I hope it's me thats wrong here.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'very simple demo of using Renderer for &gt;4 lights + shadows.

Import mojo2
Import monkey

Const NUM_LIGHTS:=5


'create an orthographics projection matrix
Function Mat4Ortho:Float[]( left:Float,right:Float,bottom:Float,top:Float,znear:Float,zfar:Float )

	Local w:=right-left,h:=top-bottom,d:=zfar-znear
	
	Return [ 2.0/w,0,0,0, 0,2.0/h,0,0, 0,0,2.0/d,0, -(right+left)/w,-(top+bottom)/h,-(zfar+znear)/d,1 ]
End

Class MyLightA Implements ILight

	'note: x,y,z,w go in last 4 components of matrix...
	Field matrix:=[1.0,0.0,0.0,0.0, 0.0,1.0,0.0,0.0, 0.0,0.0,1.0,0.0, 0.0,0.0,-50.0,1.0]
	Field color:=[0.2,0.2,0.2,1.0]
	Field range:=800.0

	'implement ILight interface...
	'
	Method LightMatrix:Float[]()
		Return matrix
	End
	
	Method LightType:Int()
		Return 1
	End
	
	Method LightColor:Float[]()
		Return color
	End
	
	Method LightRange:Float()
		Return range
	End
	
	Method LightImage:Image()
		Return Null
	End

End



Class MyLayerA Extends DrawList Implements ILayer

	Field lights:=New Stack&lt;MyLightA&gt;
	Field layerMatrix:=[1.0,0.0,0.0,0.0, 0.0,1.0,0.0,0.0, 0.0,0.0,1.0,0.0, 0.0,0.0,0.0,1.0]
	Field layerFogColor:=[0.0,0.0,0.0,0.0]

	'implement ILayer interface...
	'
	Method LayerMatrix:Float[]()
		Return layerMatrix
	End
	
	Method LayerFogColor:Float[]()
		Return layerFogColor
	End
	
	Method LayerLightMaskImage:Image()
		Return Null
	End
	
	Method EnumLayerLights:Void( lights:Stack&lt;ILight&gt; )
		For Local light:=Eachin Self.lights
			lights.Push light
		Next
	End

	Method OnRenderLayer:Void( drawLists:Stack&lt;DrawList&gt; )
		drawLists.Push Self
	End

End



Class Test
	Field _rend:Renderer
	Field _layer:MyLayerA

	Method AddLight(tile:Image,renderer:Renderer,layer0:MyLayerA)

		For Local i:=0 Until 5
			Self._layer.lights.Push(New MyLightA)
		Next

		For Local x:=0 Until 1280 Step 128
			For Local y:=0 Until 720 Step 128	
				Self._layer.DrawImage tile,x,y
			Next
		Next
		Self._rend.Layers.Push layer0
	End Method

	Method ClearLight()		
			Self._rend.Layers.Clear()
			Self._layer.lights.Clear()
	End Method

	Method SetRenderer(render:Renderer)
		Self._rend=render
	End Method

	Method SetLayer(layer:MyLayerA)
		Self._layer=layer
	End Method
	
	Method MyRenderer(renderer:Renderer)
		renderer.Render()
	End Method

End Class


Class MyApp Extends App
Field rtime,rframes,rfps
	Field tile:Image
	Field shadowCaster:ShadowCaster
	Field renderer:Renderer'=New Renderer
	Field layer0:MyLayerA'=New MyLayerA
	Field rimage:Image
	Field canvas:Canvas'=New Canvas
	'Field weird:=New DrawList
	Field test:Test=New Test
	
	Method OnCreate()
		Self.canvas=New Canvas
		
		'create renderer
		renderer=New Renderer
		renderer.SetViewport( 0,0,DeviceWidth,DeviceHeight )
		renderer.SetProjectionMatrix( Mat4Ortho( 0,DeviceWidth,0,DeviceHeight,-1,1 ) )
		renderer.SetAmbientLight( [0.1,0.1,0.1,1.0] )
		
		'load some gfx
		tile=Image.Load( "t3.png",0,0 )
		
		'create layer 0
		layer0=New MyLayerA
		
		
		'create simple rect shadow caster
		shadowCaster=New ShadowCaster
		shadowCaster.SetVertices( [0.0,0.0, 32.0,0.0, 32.0,32.0, 0.0,32.0] )
		
		'draw some shadow casters
		For Local x:=100 Until 640 Step 220
		
			For Local y:=60 Until 480 Step 180
			
				layer0.SetColor 1,1,0
				layer0.DrawRect x-16,y-16,32,32
				layer0.SetColor 1,1,1
				
				layer0.AddShadowCaster shadowCaster,x-16,y-16
			Next
		Next

		'add layer to renderer		
		renderer.Layers.Push layer0

		test.SetRenderer(renderer)
		test.SetLayer(layer0)
		test.AddLight(tile,renderer,layer0)
	End


	
	Method OnRender()
		'-------------------------------------------------------------
		'These clear Ilayer and Ilight. Comment them out to for 
		'normal run.
		'-------------------------------------------------------------
		test.ClearLight()	
		test.AddLight(tile,renderer,layer0)

		
		rframes+=1
		Local e=Millisecs-rtime
		If e&gt;=1000
			rfps=rframes
			rframes=0
			rtime+=e
		Endif
		
			
		canvas.Clear(.4,0,0)
		
		'move lights around a bit
		For Local i:=0 Until NUM_LIGHTS
			Local light:=layer0.lights.Get(i)
			Local radius:=120.0
			Local an:=(i*360.0/NUM_LIGHTS)+(Millisecs/50.0)
			light.matrix[12]=Cos( an )*radius+320
			light.matrix[13]=Sin( an )*radius+240
		Next

		'render scene
		test.MyRenderer(renderer)
			
		
		Self.canvas.SetColor(1,1,1)
		Self.canvas.DrawText("FPS-&gt; "+rfps,550,10)
		Self.canvas.Flush()	
	End
End

Function Main()

	New MyApp

End

</textarea> <br><br></td></tr></table><br>
<a name="2112563"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >marksibly</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> &gt; The first is that I can't seem to create a mojo2 class object outside the scope on 'OnCreate' In the modified 'Renderer' demo (below) if you try to create a mojo2 object within a 'Field' it will fail with a 'Null' object<br><br>Yes, this is a similar limitation to that with images - you can't create any graphics objects until the graphics system is initialized (eg: window+GL context is created) which doesn't happen until just before OnCreate.<br><br>Your rendering code is slowing down because you are continually adding new tiles to the layer via the 'DrawImage' in AddLight. You only need to draw these tiles once, or you can use DrawList.Reset() to 'clear' a drawlist (although this will also clear shadowcasters).<br><br>Thanks for the nice code sample too! <br><br></td></tr></table><br>
<a name="2112578"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Richard Betson</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Thanks for the nice code sample too! <br></div><br>Way better then two lines of code. :D<br><br>Once you made the above comments I started to see what mojo2 is doing with 'Renderer'. Here is my little demo using SetCameraMatrix() for screen scrolling.<br><a href="http://www.monkey-x.com/Community/posts.php?topic=10187" target="_blank">http://www.monkey-x.com/Community/posts.php?topic=10187</a><br><br>Neat stuff now that I grasp whats up :)<br><br>Thanks! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
