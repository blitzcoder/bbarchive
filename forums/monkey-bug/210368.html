<!DOCTYPE html><html lang="en" ><head ><title >DeviceHeight() misreports when using renderer.</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >DeviceHeight() misreports when using renderer.</h1><a href="forums.php" >Monkey Forums</a>/<a href="topics.php?forum=501" >Monkey Bug Reports</a>/<a href="#bottom" >DeviceHeight() misreports when using renderer.</a><br><br>
<a name="2116011"></a>

<a name="2116012"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Richard Betson</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>Below is a copy of the renderer demo modified (see commented sections) to show an issue with DeviceHeight() reporting the wrong device height when using mojo2 renderer. Adding the following (to code) will cause DeviceHeight() to miss report the actual device height.<br><pre class=code>
#GLFW_WINDOW_WIDTH=1280
#GLFW_WINDOW_HEIGHT=720
#GLFW_WINDOW_RESIZABLE=True
#GLFW_WINDOW_RENDER_WHILE_RESIZING=True
</pre><br><br>The code below will print into the console the device height. On the first iteration DeviceHeight() reports the right height (720). On the following iterations it reports the wrong device height (691).<br><br>I have tested this on Monkey 86e and compiled it on my Linux Mint 17.2 system.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'very simple demo of using Renderer for &gt;4 lights + shadows.

Import mojo2

'-------------------------------------
'Added to test for DeviceHieght() bug.
'-------------------------------------
#GLFW_WINDOW_WIDTH=1280
#GLFW_WINDOW_HEIGHT=720
#GLFW_WINDOW_RESIZABLE=True
#GLFW_WINDOW_RENDER_WHILE_RESIZING=True
'-------------------------------------

Const NUM_LIGHTS:=5

'create an orthographics projection matrix
Function Mat4Ortho:Float[]( left:Float,right:Float,bottom:Float,top:Float,znear:Float,zfar:Float )

	Local w:=right-left,h:=top-bottom,d:=zfar-znear
	
	Return [ 2.0/w,0,0,0, 0,2.0/h,0,0, 0,0,2.0/d,0, -(right+left)/w,-(top+bottom)/h,-(zfar+znear)/d,1 ]
End

Class MyLight Implements ILight

	'note: x,y,z,w go in last 4 components of matrix...
	Field matrix:=[1.0,0.0,0.0,0.0, 0.0,1.0,0.0,0.0, 0.0,0.0,1.0,0.0, 0.0,0.0,-100.0,1.0]
	Field color:=[0.2,0.2,0.2,1.0]
	Field range:=400.0

	'implement ILight interface...
	'
	Method LightMatrix:Float[]()
		Return matrix
	End
	
	Method LightType:Int()
		Return 1
	End
	
	Method LightColor:Float[]()
		Return color
	End
	
	Method LightRange:Float()
		Return range
	End
	
	Method LightImage:Image()
		Return Null
	End

End

Class MyLayer Extends DrawList Implements ILayer

	Field lights:=New Stack&lt;MyLight&gt;
	Field layerMatrix:=[1.0,0.0,0.0,0.0, 0.0,1.0,0.0,0.0, 0.0,0.0,1.0,0.0, 0.0,0.0,0.0,1.0]
	Field layerFogColor:=[0.0,0.0,0.0,0.0]

	'implement ILayer interface...
	'
	Method LayerMatrix:Float[]()
		Return layerMatrix
	End
	
	Method LayerFogColor:Float[]()
		Return layerFogColor
	End
	
	Method LayerLightMaskImage:Image()
		Return Null
	End
	
	Method EnumLayerLights:Void( lights:Stack&lt;ILight&gt; )
		For Local light:=Eachin Self.lights
			lights.Push light
		Next
	End

	Method OnRenderLayer:Void( drawLists:Stack&lt;DrawList&gt; )
		drawLists.Push Self
	End

End

Class MyApp Extends App

	Field tile:Image
	Field shadowCaster:ShadowCaster
	Field renderer:Renderer
	Field layer0:MyLayer
	Field rimage:Image
	Field weird:=New DrawList
	
	Method OnCreate()
	
		'create renderer
		renderer=New Renderer
		renderer.SetViewport( 0,0,DeviceWidth(),DeviceHeight() )
		renderer.SetProjectionMatrix( Mat4Ortho( 0,640,0,480,-1,1 ) )
		renderer.SetAmbientLight( [0.1,0.1,0.1,1.0] )
		
		'load some gfx
		tile=Image.Load( "t3.png",0,0 )
		
		'create layer 0
		layer0=New MyLayer
		
		'add some lights to layer
		For Local i:=0 Until NUM_LIGHTS
			layer0.lights.Push New MyLight
		Next

		For Local x:=0 Until 640 Step 128
			For Local y:=0 Until 480 Step 128	
				layer0.DrawImage tile,x,y
			Next
		Next
		
		'create simple rect shadow caster
		shadowCaster=New ShadowCaster
		shadowCaster.SetVertices( [0.0,0.0, 32.0,0.0, 32.0,32.0, 0.0,32.0] )
		
		'draw some shadow casters
		For Local x:=100 Until 640 Step 220
		
			For Local y:=60 Until 480 Step 180
			
				layer0.SetColor 1,1,0
				layer0.DrawRect x-16,y-16,32,32
				layer0.SetColor 1,1,1
				
				layer0.AddShadowCaster shadowCaster,x-16,y-16
			Next
		Next

		'add layer to renderer		
		renderer.Layers.Push layer0
	End
	
	Method OnRender()
		'--------------------------------
		'Added to test DeviceHeight() bug
		'--------------------------------
		Print "DEVICE HEIGHT ----------------&gt; "+DeviceHeight()
		'--------------------------------
		
		'move lights around a bit
		For Local i:=0 Until NUM_LIGHTS
			Local light:=layer0.lights.Get(i)
			Local radius:=120.0
			Local an:=(i*360.0/NUM_LIGHTS)+(Millisecs/50.0)
			light.matrix[12]=Cos( an )*radius+320
			light.matrix[13]=Sin( an )*radius+240
		Next

		'render scene
		renderer.Render
		
	End
End

Function Main()

	New MyApp

End
</textarea> <br><br></td></tr></table><br>
<a name="2116014"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ImmutableOctet(SKNG)</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think this is a Linux specific issue (Or even a display server issue), because it only says 720 for me.<br><br>I'm running Windows 10 Pro (x64) with an AMD Radeon HD 6950 using the 15.10 drivers (Which I need to update). <br><br></td></tr></table><br>
<a name="2116013"></a>

<a name="2116017"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Richard Betson</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Emm,<br><br>I guess not a bug. What is going on is when the application window is set to be re-sizable (#GLFW_WINDOW_RESIZABLE=True) it is initially 720 in height. Then when the application window opens it is resized (by OS) to fit KDE's desktop allowing for the task bar height essentially automatically adjusting the applications window height. <br><br>Learning is fun. :/ <br><br></td></tr></table><br>
<a name="2116128"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dawlane</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Richard: Your right it's not a bug. It's actually the correct behaviour for the window managers on Linux. You will find that DeviceWidth will also report something different from what you expect. If you look closely, you will see that when GLFW_WINDOW_RESIZABLE is set to false, you will be missing the right and bottom window boarders. When GLFW_WINDOW_RESIZABLE is true and the window exceeds the physical display mode set for that monitor, then the window manager correctly adjusts the window to fit.<br>A quirk that you should be aware of when using full screen modes is, you must preserve the current resolution so you can restore it, if you intend to change a resolution for your application. No doing this before exiting the application will put the display server into virtual display mode. By this I mean the physical monitor resolution mode will small; say 800x600, while the desktop could be 1920x1200. <br><br></td></tr></table><br>
<a name="2116127"></a>

<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
