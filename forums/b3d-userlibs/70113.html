<!DOCTYPE html><html lang="en" ><head ><title >Bass and Avi movie</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Bass and Avi movie</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=94" >Blitz3D Userlibs</a>/<a href="#bottom" >Bass and Avi movie</a><br><br>
<a name="784500"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ZJP</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>I want to build (draw) a spectrum analyser over an avi movie. Any solution to have a sort of "What you Hear is What you Get" with Abrexxes's Bass23?<br><br>ex :<br>Init Bass in "What you Hear is What you Get" mode<br>OpenMovie (Blitz3d command)<br>Repeat<br>         DrawMovie (Blitz3d command)<br>         GET Avi stream audio infos<br>         Draw the spectrum or anything<br>Until end of Movie<br>Close process<br><br>Thanks<br>JP <br><br></td></tr></table><br>
<a name="784533"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Abrexxes</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> "GET Avi stream audio infos"<br><br>Thats the problem. The only way to get the output data is to record the BASS_INPUT_TYPE_WAVE device. But thats really dirty and not in realtime because the Channel_Get_Data will work over the recording buffer. So you can not "grab" only your informations on the fly. <br><br></td></tr></table><br>
<a name="784562"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ZJP</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thx<br><br>JP <br><br></td></tr></table><br>
<a name="784624"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Abrexxes</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have locked around.<br><br>What you can do is open the avi file and read out the audio stream into a bank, or a bigger one into a new temp file. After you play your video with Blitz and use the new stream in the same time for audio and Channel_Get_Data.<br><br>But AVI ist just a container, if there is an obscur divx audio format bass will be not able to play them (Blitz also not). It is  possible  but you must "open" the avi containers .<br><br><a href="http://msdn2.microsoft.com/en-us/library/ms779638.aspx" target="_blank">http://msdn2.microsoft.com/en-us/library/ms779638.aspx</a><br><br>If you have simple PCM in your file here is some code from me to write a wav file with blitz. For a simple one dont use the SMPL Part. this was only to loop samples perfect in b3d  for Sfx effects without any lib. ;)<br><br><pre class=code>
; You dont need this global, but you can use it to see the data size of your wav file for max value
Global s_datalenght%


Graphics 640,480,0,2
SetBuffer BackBuffer ()

;LoadLoopSound("test.wav",0,0) ; no loop
LoadLoopSound("test.wav",1,s_datalenght) ; maximum loop
;LoadLoopSound("test.wav",800,8000) ; what you want but not more then datalenght

musik = LoadSound ("s_temp.wav")
DeleteFile "s_temp.wav"


While Not KeyHit (1)
Cls
If KeyHit (57) Then
PlaySound (musik)
EndIf
Text 10,10,"Press space to play"
Text 10,20, "Datasize = "+s_datalenght
Flip
Wend
End



Function LoadLoopSound(s_file$,s_start%,s_end%)

;**********************************
;**** Read Source Wav File *****
;**********************************
s_source = ReadFile (s_file)

; read RIFF header (1179011410) to see if it is a wav file
s_check = ReadInt (s_source)
      If s_check = 1179011410 Then ; RIFF found
            s_check = ReadInt (s_source)
            s_sourcesize = (s_check + 8) ; size of sourcefile
            s_check = ReadInt (s_source) ; read "wave"
      EndIf   
If Not s_check = 1163280727 Then Return 0 ; no wave..go away with that :)
                     
; search and read   FMT chunk for sample infos      
s_position = 12
s_goal = 0
Repeat
      SeekFile s_source, s_position
            s_check = ReadInt (s_source)
                  If s_check = 544501094 Then ; FMT found
                        s_goal = 1 ; now lets read and store all the infos
                              s_fmtsize = ReadInt (s_source) ;&lt;fmt length&gt;  size of this chunk, normal = 16 byte from here
                              s_format = ReadShort (s_source)   ;&lt;format tag&gt;    sample format, normal = 1 (hex) for PCM
                              s_channels = Readshort (s_source) ;&lt;channels&gt;  1 = mono, 2 = stereo
                              s_rate = ReadInt (s_source) ;&lt;sample rate&gt;    (hz)
                              s_bytes = ReadInt (s_source) ;&lt;bytes/second&gt;    Sample-Rate * Block-Align
                              s_block = Readshort (s_source) ;&lt;block align&gt;    Channels * bits/sample / 8
                              s_bit = Readshort (s_source) ;&lt;bits/sample&gt;    8, 16 or 24
                  EndIf
            s_position = s_position +1 ;FMT not found, next step
If s_goal = 1 Then Exit   ; ok    
Until Eof (s_source) = 1
If s_goal = 0 Then Return 0 ; no FMT found, Error

; search and read DATA chunk
s_position =12
s_goal = 0
Repeat
      SeekFile s_source, s_position
            s_check = ReadInt (s_source)
            If s_check = 1635017060 Then ; DATA found
                  s_goal = 1
                  s_datalenght = ReadInt (s_source) ;&lt;length&gt; of data block
                  sbank_cache = CreateBank (s_datalenght) ; now we store the sample data
                        For i = 0 To (s_datalenght-1)
                              s_value = ReadByte (s_source)
                              PokeByte sbank_cache,i,s_value
                        Next
            EndIf
      s_position = s_position +1 ; DATA not found, next step
If s_goal = 1 Then Exit ; ok
Until Eof (s_source) = 1
If s_goal = 0 Then Return 0 ; no DATA found, Error

CloseFile s_source ; we have what we need, all other chunks we can ignore

;**********************************
;**** Write temp Wav File *****
;**********************************

s_temp = WriteFile ("s_temp.wav") ; create a temp

;write RIFF header
WriteInt s_temp, 1179011410 ; RIFF
WriteInt s_temp, s_sourcesize-8 ;size of header
WriteInt s_temp, 1163280727 ; WAVE

;write FMT chunk
WriteInt s_temp, 544501094 ; FMT
WriteInt s_temp, s_fmtsize ;&lt;fmt length&gt;
WriteShort s_temp, s_format ;&lt;format tag&gt;
WriteShort s_temp, s_channels ;&lt;channels&gt;
WriteInt s_temp, s_rate ;&lt;sample rate&gt;
WriteInt s_temp, s_bytes ;&lt;bytes/second&gt;
WriteShort s_temp, s_block ;&lt;block align&gt;
WriteShort s_temp, s_bit ;&lt;bits/sample&gt;
                  
;write SMPL chunk
WriteInt s_temp, 1819307379 ;SMPL
WriteInt s_temp, 60 ;36 + 1 sample loop section (24)
WriteInt s_temp, 0 ; Manufacturer , we dont need    
WriteInt s_temp, 0   ; ProductID , we dont need
WriteInt s_temp, 0   ;Sample Period    ,not supported by blitzbasic
WriteInt s_temp, 0   ;MIDI Unity Note    ,not supported by blitzbasic
WriteInt s_temp, 0   ;MIDI Pitch Fraction    ,not supported by blitzbasic
WriteInt s_temp, 0   ;SMPTE Format    ,not supported by blitzbasic
WriteInt s_temp, 0   ;SMPTE Offset    ,not supported by blitzbasic
WriteInt s_temp, 0 ;Num Sample Loops    ,not supported by blitzbasic
WriteInt s_temp, 0   ;Sampler Data    ,not supported by blitzbasic

WriteInt s_temp, 0 ;Cue Point ,not supported by blitzbasic
WriteInt s_temp, 0 ;Type    ,not supported by blitzbasic
WriteInt s_temp, s_start ;   Start loop
WriteInt s_temp, s_end ;End loop
WriteInt s_temp, 0 ;Fraction ,not supported by blitzbasic
WriteInt s_temp, 0 ;Play Count ,not supported by blitzbasic

;write DATA chunk &amp; data
WriteInt s_temp, 1635017060 ; DATA
WriteInt s_temp, s_datalenght ;&lt;length&gt;
WriteBytes (sbank_cache,s_temp,0,s_datalenght) ;sample data from bank
FreeBank sbank_cache ; we no need this longer

CloseFile s_temp

End Function </pre> <br><br></td></tr></table><br>
<a name="784879"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ZJP</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Again. Works on it. Another solution (maybe) with<br>"winmm.dll". A sort of "get sound level" API.<br><br>JP <br><br></td></tr></table><br>
<a name="784885"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Abrexxes</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hmmmm.... i dont now what you want to do. If you can use wmv files, the wma plugin of bass is able to play the audiostream of wmv files.... <br><br></td></tr></table><br>
<a name="784912"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ZJP</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> "Hmmmm.... i dont now what you want to do"<br><br>A sort of "BASS_ChannelGetLevel_AVI" in real time.<br><br>JP <br><br></td></tr></table><br>
<a name="786883"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ZJP</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Found a solution.<br><br>BLZPeekInt is in the ISL lib <a href="http://www.blitzbasic.com/Community/posts.php?topic=34301" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=34301</a><br>------------------------------------------------------<br><pre class=code>
Graphics 800,600,32,2
SetBuffer FrontBuffer()

Const WAVE_INVALIDFORMAT = $000 ;invalid forma
Const WAVE_FORMAT_1M08   = $001 ;11.025 kHz,Mono,   8-bit
Const WAVE_FORMAT_1S08   = $002 ;11.025 kHz,Stereo, 8-bit
Const WAVE_FORMAT_1M16   = $004 ;11.025 kHz,Mono,  16-bit
Const WAVE_FORMAT_1S16   = $008 ;11.025 kHz,Stereo,16-bit
Const WAVE_FORMAT_2M08   = $010 ;22.05  kHz,Mono,   8-bit
Const WAVE_FORMAT_2S08   = $020 ;22.05  kHz,Stereo, 8-bit
Const WAVE_FORMAT_2M16   = $040 ;22.05  kHz,Mono,  16-bit
Const WAVE_FORMAT_2S16   = $080 ;22.05  kHz,Stereo,16-bit
Const WAVE_FORMAT_4M08   = $100 ;44.1   kHz,Mono,   8-bit
Const WAVE_FORMAT_4S08   = $200 ;44.1   kHz,Stereo, 8-bit
Const WAVE_FORMAT_4M16   = $400 ;44.1   kHz,Mono,  16-bit
Const WAVE_FORMAT_4S16   = $800 ;44.1   kHz,Stereo,16-bit
Const WAVE_FORMAT_PCM    = $001
Const WHDR_DONE          = $001
Const WHDR_PREPARED      = $002
Const WHDR_BEGINLOOP     = $004
Const WHDR_ENDLOOP       = $008
Const WHDR_INQUEUE       = $010

WaveFmt = CreateBank(18)
PokeShort WaveFmt,00,WAVE_FORMAT_PCM ; FormatTag
PokeShort WaveFmt,02,1               ; Channels
PokeInt   WaveFmt,04,11025           ; SamplesPerSec
PokeInt   WaveFmt,08,11025           ; AvgBytesPerSec | BlockAlign * SamplesPerSec
PokeShort WaveFmt,12,1               ; BlockAlign | (Channels * BitsPerSample) / 8 
PokeShort WaveFmt,14,8               ; BitsPerSample
PokeShort WaveFmt,16,0               ; ExtraDataSize

WaveCaps = CreateBank(48)
Dim Device(waveInGetNumDevs())
For I = 0 To waveInGetNumDevs() - 1
	waveInGetDevCaps(I,WaveCaps,48)
	If PeekInt(WaveCaps,40) And WAVE_FORMAT_1M08 Then
		For C = 8 To 39
			Char = PeekByte(WaveCaps,C)
			If Char = 0 Then Exit
			ProductName$ = ProductName$ + Chr$(Char)
		Next
		Print "(" + Str$(Dev) + ") " + ProductName$
		ProductName$ = ""
		Dev = Dev + 1
		Device(Dev) = I
	EndIf
Next 

If Dev = 0 Then
	Print "Can't find a sounddevice!"
	WaitKey : End
EndIf

Repeat
	DevUsed = Input("Select a device(0-" + Str$(Dev - 1) + "): ")
Until DevUsed =&gt; 0 And DevUsed &lt;= Dev - 1
DevUsed = Device(DevUsed)

DevHandle = CreateBank(4)
waveInOpen(DevHandle,DevUsed,WaveFmt,0,0,0)
If PeekInt(DevHandle,0) = 0 Then
	Print "Can't open wavedevice!"
	WaitKey : End
EndIf
waveInStart(PeekInt(DevHandle,0))

WavData = CreateBank(1024)

WaveHead = CreateBank(32)
PokeInt WaveHead,00,BLZPeekInt(WavData+4) ; lpData
PokeInt WaveHead,04,1024              ; dwBufferLength
waveInPrepareHeader(PeekInt(DevHandle,0),WaveHead,32)

;SetBuffer BackBuffer()
film=OpenMovie("c:\movie.avi")

While Not KeyDown(1)
	Cls
	DrawMovie film,50,50
	waveInAddBuffer(PeekInt(DevHandle,0),WaveHead,32)
	For I = 0 To 1023
		Line I,PeekByte(WavData,I),I,0
	Next
	
	Flip
Wend
CloseMovie film

waveInUnprepareHeader(PeekInt(DevHandle,0),WaveHead,32)
waveInReset(PeekInt(DevHandle,0))
waveInClose(PeekInt(DevHandle,0))

;.lib "winmm.dll" 
;waveInGetDevCaps%(uDeviceID,WaveInCapsPointer*,WaveInCapsStructSize) : "waveInGetDevCapsA"
;waveInOpen%(WaveDeviceInputHandle*,WhichDevice,WaveFormatExPointer*,CallBack,CallBackInstance,Flags) : "waveInOpen"
;waveInGetNumDevs%() : "waveInGetNumDevs"
;waveInClose%(WaveDeviceInputHandle) : "waveInClose"
;waveInStart%(WaveDeviceInputHandle) : "waveInStart"
;waveInReset%(WaveDeviceInputHandle) : "waveInReset"
;waveInStop%(WaveDeviceInputHandle) : "waveInStop"
;waveInAddBuffer%(InputDeviceHandle,WaveHdrPointer*,WaveHdrStructSize) : "waveInAddBuffer"
;waveInPrepareHeader%(InputDeviceHandle,WaveHdrPointer*,WaveHdrStructSize) : "waveInPrepareHeader"
;waveInUnprepareHeader%(InputDeviceHandle,WaveHdrPointer*,WaveHdrStructSize) : "waveInUnprepareHeader"
;
</pre> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
