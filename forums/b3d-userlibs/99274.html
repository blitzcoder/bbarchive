<!DOCTYPE html><html lang="en" ><head ><title >Newton Wrapper for Blitz3D by Itmbin</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Newton Wrapper for Blitz3D by Itmbin</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=94" >Blitz3D Userlibs</a>/<a href="#bottom" >Newton Wrapper for Blitz3D by Itmbin</a><br><br>
<a name="1164578"></a>

<a name="1164684"></a>

<a name="1174622"></a>

<a name="1174623"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Zethrax</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hiya. I've been in touch with Itmbin, the author of the Newton physics engine wrapper for Blitz3D, and he's agreed to allow me to publish my license key for the wrapper so anyone can use it. He also mentioned that he may open-source the wrapper code and provide a version with no license key restrictions if people are interested.<br><br><b>EDIT: Itmbin contacted me again with a link for the source code version of the wrapper. Link is below.</b><br><br>- Source code version provided by Itmbin -<br><a href="https://docs.google.com/file/d/0BxnwgQiJB0oZZzRlNUtsU1JiQ00/edit" target="_blank">https://docs.google.com/file/d/0BxnwgQiJB0oZZzRlNUtsU1JiQ00/edit</a><br><br><br>- Compiled license key version -<br><br>The download link and license key are below. I've also placed the license key into all the samples and into a text file named 'License Key.txt'.<br><br>Download<br><a href="http://files.pmnova.com/stuff/Itmbin-newton-wrapper.zip" target="_blank">http://files.pmnova.com/stuff/Itmbin-newton-wrapper.zip</a><br><br>License Key<br>N62e5710q501'cq3l2}5UeLfUcU2T3E9<br><br><br>The original thread for this wrapper<br><a href="http://www.blitzbasic.com/Community/posts.php?topic=66659" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=66659</a><br><br><font class="tiny">Last edited 2013</font> <br><br></td></tr></table><br>
<a name="1164765"></a>

<a name="1164766"></a>

<a name="1165090"></a>

<a name="1165104"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Zethrax</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's a basic FPS setup I threw together which makes use of Newton via Itmbin's Newton wrapper. Needs a lot more tweaking and I'm probably doing a few things wrong.<br><br>EDIT: Tweaked the code a bit to fix some issues. I've also had to double the gravity to get a realistic gravity value for some reason. I tried a drop test and found that it was taking two seconds to fall 9.8 metres instead of one second.<br><br>The code still needs more work, but it's a little further along..<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

; Debug variables




; Physics custom type.
Type phx
	Field body
	Field mesh
	Field dont_rotate_mesh
End Type


; -- Tweening and timing setup code.
Const C_TWEEN_LOGIC_UPDATE_RATE = 60 ; The desired number of logic updates per second (the logic FPS).
Const C_TWEEN_PERIOD = 1000 / C_TWEEN_LOGIC_UPDATE_RATE
Const C_DELTA_TIME# = 1.0 / C_TWEEN_LOGIC_UPDATE_RATE ; This multiplier should be used with any initialization values that may be affected by the 'C_TWEEN_LOGIC_UPDATE_RATE' value being changed.
Global G_TWEEN_tween#
Global G_TWEEN_ticks
Global G_TWEEN_time_taken
Global G_TWEEN_loop_count
Global G_old_time
Global G_world_time



; Set the graphics mode, the mousepointer state, and the random number seed.
Graphics3D 1024, 768, 0, 2
SetBuffer BackBuffer()
HidePointer
SeedRnd MilliSecs()


; -- Viewport.
Global G_viewport_center_x = GraphicsWidth() / 2
Global G_viewport_center_y = GraphicsHeight() / 2
;^^^^^^


; -- Mouselook.
Global G_mouselook_x_inc# = 0.4 ; This sets both the sensitivity and direction (+/-) of the mouse on the X axis.
Global G_mouselook_y_inc# = 0.4 ; This sets both the sensitivity and direction (+/-) of the mouse on the Y axis.
Global G_mouse_left_limit = 250 ; Used to limit the mouse movement to within a certain number of pixels (250 is used here) from the center of the screen. This produces smoother mouse movement than continuously moving the mouse back to the center each loop.
Global G_mouse_right_limit = GraphicsWidth () - 250 ; As above.
Global G_mouse_top_limit = 250 ; As above.
Global G_mouse_bottom_limit = GraphicsHeight () - 250 ; As above.
;^^^^^^


; -- Mouse smoothing que.
Global G_mouse_smoothing_que_size = 5 ; The number of array elements.
Global G_mouse_smoothing_que_max = G_mouse_smoothing_que_size - 1 ; The highest array element index.
Dim A_mouse_x_speed#( G_mouse_smoothing_que_max )
Dim A_mouse_y_speed#( G_mouse_smoothing_que_max )




; -- Create physics world.
phWorldCreate( 0,"N62e5710q501'cq3l2}5UeLfUcU2T3E9" )
phWorldSetGravity( 0.0, -9.8 * 2.0, 0.0 )
;phMatSetDefFriction( .9,.8 )


; -- Create user entity.
Global G_user = CreatePivot()
Global G_user_movement_max_speed# = 4.0; * C_DELTA_TIME# ; The user's max movement speed.
Global G_user_movement_impulse# = 1.0
Global G_user_aircontrol_max_speed# = 0.5 ; The user's max aircontrol speed.
Global G_user_aircontrol_impulse# = 0.1
Global G_user_jump_impulse# = 4.0
Global G_user_jump_y_velocity_threshold# = G_user_jump_impulse# ; Holds the y velocity threshold value to prevent a jump if the user is already moving upwards.
Global G_user_camera_pitch#
Global G_user_height# = 1.8
Global G_user_radius# = 0.6
Global G_user_capsule_height# = G_user_height# + G_user_radius#
Global G_user_centerpoint_height# = G_user_capsule_height# / 2.0
Global G_user_groundtest_height# = G_user_centerpoint_height# - 0.5
Global G_user_camera_height# = ( ( G_user_capsule_height# / 2.0 ) - G_user_radius# ) - 0.1
Global G_old_ground_contact_time
Global G_ground_contact_timeout = 100
Global G_airborne


; -- Create user's camera.
Global G_user_camera = CreateCamera( G_user )
CameraRange G_user_camera, 0.40, 100.0
CameraClsColor G_user_camera, 0.0, 106.0, 213.0
CameraZoom G_user_camera, 1.6 ; Set the camera focus to the correct value for the human eye.
PositionEntity G_user_camera, 0.0, G_user_camera_height#, 0.0, True


; -- Create physics for user.
;Global G_char.phx = phxCreatePhysSphere( G_user_centerpoint_height#, G_user_centerpoint_height#, G_user_centerpoint_height#, 150.0 )
Global G_char.phx = phxCreateCharCapsule( G_user_radius#, G_user_capsule_height#, 100.0 )
;phBodySetPos( G_char\body, 0.0, G_user_centerpoint_height# + 0.01, 0.0 )
;phBodySetPos( G_char\body, 8.0, 2.0, 8.0 )
;phBodySetPos( G_char\body, -114, 7.01, -61 )
charvector = phJointUpVectorCreate( 0.0, 1.0, 0.0, G_char\body )
phBodySetDamping( G_char\body, 1, 1 )
G_char\dont_rotate_mesh = True
G_char\mesh = G_user
PositionEntity G_user, phBodyGetX( G_char\body ), phBodyGetY( G_char\body ), phBodyGetZ( G_char\body ), True

; -- Create the user material.
matHero = phMatCreate()
phBodySetMat( G_char\body, matHero )
phMatSetFriction( matHero, 0, 1.99, 1.8 )
phMatSetElasticity( matHero, 0, 0.1 )
phMatSetSoftness( matHero, 0, 0.2 )


; -- Create a light;
Global G_light = CreateLight( 1 )
RotateEntity G_light, 45.0, 45.0, 0.0


; Create HUD camera.
Global G_hudcam = CreateHudCamera( -1, 0, -1000.0, 0 )


; -- Create user's gun.
Global G_gun = CreateCube()
ScaleMesh G_gun, 0.05, 0.05, 0.8
PositionMesh G_gun, 0.0, 0.0, 0.8
UpdateNormals G_gun
EntityParent G_gun, G_hudcam
PositionEntity G_gun, 0.3, -0.3, 0.3

If 0
	; -- Load level and create physics for it.
	;level = LoadAnimMesh( "C:\Users\Bill\Desktop\My Files\My Projects\FPS GAME ENGINE\OTHER STUFF\FPS GAME ENGINE media\MEDIA2\MODELS\MAPLET MODELS\arena\SEGMENTED ARENA 2\arena2.b3d" )
	
	level = LoadAnimMesh( "C:\Users\Bill\Desktop\My Files\My Projects\FPS GAME ENGINE\OTHER STUFF\FPS GAME ENGINE media\MEDIA2\MODELS\MAPLET MODELS\arena\SEGMENTED ARENA 2\arena3.b3d" )

	phBodySetPos( G_char\body, -114, 7.01, -61 )
EndIf


If 1
	level = CreateCube()
	PositionMesh level, 0.0, -1.0, 0.0
	ScaleMesh level, 50.0, 1.0, 50
	UpdateNormals level

	matBox = phMatCreate()
	phMatSetFriction( matHero, matBox, 0.9, 0.7 )
	For x = 1 To 30
		size# = Rnd(.1,3)
		cub.phx = phxCreateBox(size,size,size,100.0)
		phBodySetPos(cub\body,Rnd(-30,30),30,Rnd(-30,30))
		phBodySetMat( cub\body, matBox )
		EntityColor cub\mesh,Rnd(0,255),Rnd(0,255),Rnd(0,255)
	Next
	
	; -- Create grid texture.
	Color 255, 255, 255
	G_grid_2d_tex = CreateTexture( 256, 256, 11 )
	SetBuffer TextureBuffer( G_grid_2d_tex )
	For i = 0 To 4
		Rect i, i, 256 - i - i, 256 - i - i, False
	Next
	For y = 5 To 250
		For x = 5 To 250
			WritePixel x, y, 0
		Next
	Next
	SetBuffer BackBuffer()

	; -- Create 2D grid.
	;level = CreatePlane()
	ScaleTexture G_grid_2d_tex, 0.01, 0.01
	EntityTexture level, G_grid_2d_tex
	EntityFX level, 9

	phBodySetPos( G_char\body, 0.0, G_user_centerpoint_height# + 0.01, 0.0 )
EndIf


phLevelBuildBegin()
	LevelAddMesh( level )
phLevelBuildEnd()


; At this point all renderable map entities should be loaded with their positions at the world center and should not be hidden or invisible. Positioning, hiding, and alpha should happen after the 'PrecacheMapMedia' function has been run.


Precache3DMedia


; Code to position, hide, and set alpha for all renderable map entities should go here.


; -- Initialize the loop.
InitializeGameSession ; Run this when starting a new game session.
InitializeInputTimingAndGraphics ; Run this immediately before starting a new game session and returning from a pause.

; -- Main loop.
While Not KeyHit ( 1 ) ; Exit the main loop if the escape key is pressed.
	
	; -- Update tweening.
	Repeat
		G_TWEEN_time_taken = MilliSecs() - G_old_time
	Until G_TWEEN_time_taken
	
	; How many 'frames' have elapsed	.
	G_TWEEN_ticks = G_TWEEN_time_taken / C_TWEEN_PERIOD
	
	; Calculate the fractional remainder.
	G_TWEEN_tween# = Float( G_TWEEN_time_taken Mod C_TWEEN_PERIOD ) / Float( C_TWEEN_PERIOD )
	
	For G_TWEEN_loop_count = 1 To G_TWEEN_ticks
		G_old_time = G_old_time + C_TWEEN_PERIOD
		G_world_time = G_world_time + C_TWEEN_PERIOD ; Update the world time variable used with timeouts, etc.
		If G_TWEEN_loop_count = G_TWEEN_ticks	
			CaptureWorld
		EndIf
	
		Delay 1 ; Delay 1 millisecond to allow the OS to update.
	
		; == Update main game functions :: START ==
	
		MouseLook()
		MoveUser()
		WorldStep( C_DELTA_TIME# )
	
	
		; == Update main game functions :: END ==
	
		UpdateWorld
	
	Next ; End of tweening loop.
	
	RenderWorld G_TWEEN_tween#
	
	; == Draw 2D graphics and text :: START ==

	y = 10

	Text 10, y, "Use mouse to look, W, A, S, D, SPACEBAR, LEFT-SHIFT to move, and ESCAPE to exit." : y=y+20

	;Text 10, y, "Airborne: " + G_airborne : y=y+20

	;Text 10, y, "Camera Height: " + EntityY( G_user_camera, True ) : y=y+20

	;Text 10, y, "User X: " + EntityX( G_user, True ) : y=y+20

	;Text 10, y, "User Y: " + EntityY( G_user, True ) : y=y+20

	;Text 10, y, "User Z: " + EntityZ( G_user, True ) : y=y+20

	;Text 10, y, "Phys Body Height: " + phBodyGety( G_char\body ) : y=y+20

	;Text 10, y, "Cam - User Distance: " + ( EntityY( G_user_camera, True ) - EntityY( G_user, True ) ) : y=y+20

	;Text 10, y, "Camera Pitch: " + G_user_camera_pitch# : y=y+20


	; == Draw 2D graphics and text :: END ==

	VWait : Flip False
	
Wend
;^^^^^^

EndProgram


; == Functions ==


Function MouseLook()

	; -- Update the smoothing que to smooth the movement of the mouse.
	x_speed# = 0.0 : y_speed# = 0.0
	For i = G_mouse_smoothing_que_max To 1 Step -1
		A_mouse_x_speed#( i ) = A_mouse_x_speed#( i - 1 )
		A_mouse_y_speed#( i ) = A_mouse_y_speed#( i - 1 )
		x_speed# = x_speed# + A_mouse_x_speed#( i )
		y_speed# = y_speed# + A_mouse_y_speed#( i )
	Next
	A_mouse_x_speed#( 0 ) = MouseXSpeed()
	A_mouse_y_speed#( 0 ) = MouseYSpeed()
	yaw# = ( ( x_speed# + A_mouse_x_speed#( 0 ) ) / G_mouse_smoothing_que_size ) * G_mouselook_x_inc#
	pitch# = ( ( y_speed# + A_mouse_y_speed#( 0 ) ) / G_mouse_smoothing_que_size ) * G_mouselook_y_inc#

	TurnEntity G_user, 0.0, -yaw#, 0.0 ; Turn the user on the Y (yaw) axis.
	phBodySetOmega( G_char\body, 0.0, yaw#, 0.0 )

	G_user_camera_pitch# = G_user_camera_pitch# + pitch#
	
	; -- Limit the user's camera to within 180 degrees of pitch rotation. 'EntityPitch()' returns useless values so we need to use a variable to keep track of the camera pitch.
	If G_user_camera_pitch# &gt; 90.0 Then G_user_camera_pitch# = 90.0
	If G_user_camera_pitch# &lt; -90.0 Then G_user_camera_pitch# = -90.0

	 ; Pitch the user's camera up and down.
	RotateEntity G_user_camera, G_user_camera_pitch#, 0.0, 0.0

	; Synchronize the rotation of the HUD camera to that of the user's camera to get correct lighting on the gun.
	RotateEntity G_hudcam, EntityPitch# ( G_user_camera, True ), EntityYaw# ( G_user, True ), 0.0, True
	
	; -- Limit the mouse's movement. Using this method produces smoother mouselook movement than centering the mouse each loop.
	If ( MouseX() &gt; G_mouse_right_limit ) Or ( MouseX() &lt; G_mouse_left_limit ) Or ( MouseY() &gt; G_mouse_bottom_limit ) Or ( MouseY() &lt; G_mouse_top_limit )
		MoveMouse G_viewport_center_x, G_viewport_center_y
	EndIf
	;^^^^^^
End Function


Function MoveUser()

	; Note: Also need to check if the character foot area collision was with something that could be considered as ground. Possibly check if the mass = 0 (static body) or is high enough to be something that can be walked on. In the later case jump and movement impulse forces should push against the colliding physics body.
		
	x# = phBodyGetX( G_char\body )
	y# = phBodyGetY( G_char\body )
	z# = phBodyGetZ( G_char\body )
	
	; Test for ground contact.
	ground_contact = False	
	max_contact_index = phBodyGetCollNum( G_char\body ) - 1
	For i = 0 To max_contact_index
		;collider_body = phBodyGetCollBody%( G_char\body, i )
		coll_y# = phBodyGetCollY#( G_char\body, i )
		If y# - coll_y# &gt; G_user_groundtest_height# Then ground_contact = True
	Next

	If G_airborne

		If ground_contact
			G_airborne = False
			G_old_ground_contact_time = G_world_time
		EndIf

	Else
	
		If ground_contact
			G_old_ground_contact_time = G_world_time
		Else
			If ( G_world_time - G_old_ground_contact_time ) &gt; G_ground_contact_timeout
				G_airborne = True
			EndIf
		EndIf

	EndIf

	vx# = 0.0 : vy# = 0.0 : vz# = 0.0
	h_control_input_received = False
	v_control_input_received = False
	set_airborne = False
	
	If KeyDown( 32 ) Or KeyDown( 205 ) ; Right. The 'D' and 'CURSOR RIGHT' keys.
		vx# = 1.0
		h_control_input_received = True
	Else If KeyDown( 30 ) Or KeyDown( 203 ) ; Left. The 'A' and 'CURSOR LEFT' keys.
		vx# = -1.0
		h_control_input_received = True
	EndIf

	If Not G_airborne
		If KeyDown( 57 ) Or KeyDown( 54 ) ; Up. The 'SPACEBAR' and 'RIGHT SHIFT' keys.
			If phBodyGetVelY( G_char\body ) &lt; G_user_jump_y_velocity_threshold#
				vy# = G_user_jump_impulse#
				v_control_input_received = True
				set_airborne = True
			EndIf
		EndIf
	EndIf

	If KeyDown( 17 ) Or KeyDown( 200 ) ; Forward. The 'W' and 'CURSOR UP' keys.
		vz# = 1.0
		h_control_input_received = True
	Else If KeyDown( 31 ) Or KeyDown( 208 ) ; Backward. The 'S' and 'CURSOR DOWN' keys.
		vz# = -1.0
		h_control_input_received = True
	EndIf

	If h_control_input_received Or v_control_input_received

		If h_control_input_received ; TFormNormal doesn't like to be fed all zeros, so this check ensures that one of the horizontal direction keys has been pressed and that the vx# or vz# variables have a non-zero value as a result.

			TFormNormal vx#, 0.0, vz#, G_user_camera, 0
	
			If G_airborne
				vx# = TFormedX() * G_user_aircontrol_impulse#
				vz# = TFormedZ() * G_user_aircontrol_impulse#
			Else
				vx# = TFormedX() * G_user_movement_impulse#
				vz# = TFormedZ() * G_user_movement_impulse#			
			EndIf
	
			vx2# = phBodyGetVelX( G_char\body ) + vx#
			vz2# = phBodyGetVelZ( G_char\body ) + vz#		
	
			hvel# = Sqr( ( vx2# * vx2# ) + ( vz2# * vz2# ) )
	
			If G_airborne
				If hvel# &gt; G_user_aircontrol_max_speed#
					multiplier# = G_user_aircontrol_max_speed# / hvel#
					vx# = vx# * multiplier#
					vz# = vz# * multiplier#
				EndIf
			Else
				If hvel# &gt; G_user_movement_max_speed#
					multiplier# = G_user_movement_max_speed# / hvel#
					vx# = vx# * multiplier#
					vz# = vz# * multiplier#
				EndIf
			EndIf

		EndIf

		phBodyAddImpulse( G_char\body, x#, y#, z#, vx#, vy#, vz# )

		If set_airborne Then G_airborne = True

	EndIf

End Function


Function Precache3DMedia()
	; NOTES:
	; This function should only need to be run when a new map is first loaded. It should normally be executed by the map file loader routine after loading and creating the renderable media, but before the media is positioned, hidden or has its alpha set to zero.

	; -- Precache the game's 3D graphics media. This forces the media to be uploaded to the video card. This should be done before any of the media is positioned, hidden or has its alpha set to zero to turn off rendering. Note that the camera will end up pointing back to where it started, so there is no need to reset its orientation.
	
	For i = 1 To 4
		TurnEntity G_user_camera, 0.0, 90.0, 0.0, True
		RenderWorld
	Next
	;^^^^^^
	
End Function


Function InitializeInputTimingAndGraphics()
	; NOTES:
	; This function should be run immediately before a game session begins and also after resuming from a pause.

	; *** At this point everything should be setup and ready to start/restart the game immediately. ***

	; Reset the input devices.
	MoveMouse G_viewport_center_x, G_viewport_center_y
	FlushMouse
	FlushKeys 
	MouseXSpeed()
	MouseYSpeed()
	MouseHit(1)
	MouseHit(2)
	MouseHit(3)

	; Set and render the backbuffer, and then flip it to the frontbuffer.
	SetBuffer BackBuffer()
	RenderWorld
	Flip
	G_old_time = MilliSecs()
End Function


Function InitializeGameSession()
	; NOTES:
	; This function initializes the game world state (timing, etc).
	; Run this immediately before initially starting the main game loop for the first time in a match or session.

	G_world_time = 0
	G_old_ground_contact_time = 0
	G_airborne = 0
End Function


Function CreateHudCamera( order, x#, y#, z# )
	; USAGE:
	; Creates a camera to be used for a heads-up display.

	; PARAMETERS:
	; order - This sets the drawing order of the camera. 'order' should be a negative number, so that the HUD camera is drawn on top of the main camera graphics. The highest value negative number will be drawn last (eg. The '-2' camera will be drawn after the '-1' camera.).

	; x#, y#, z# - Set the camera's absolute world position. The position should set so that the camera and its associated 3D graphic objects do not interfere with any other cameras.

	; RETURNS:
	; The new HUD camera's entity handle is returned.


	Local cam = CreateCamera()
	PositionEntity cam, x#, y#, z#, True ; 
	CameraRange cam, 0.2, 5.0
	CameraZoom cam, 1.6
	EntityOrder cam, order
	CameraClsMode cam, False, True ; This prevents the background graphics from being overwritten by the camera's clearscreen color.
	Return cam
End Function


Function EndProgram()
	phWorldDestroy()
	End
End Function


; -- Newton Wrapper stuff --


;------------------


 ;		----------------------------------------------------		;
 ; 		---                                              ---		;
 ; 		---   Some functions simplifying wrapper using   ---		;
 ; 		---                                              ---		; 
 ;		----------------------------------------------------		;
 ;
 ; NOTE: You can modify all these fucntions as you want



 ; Create body with hull geometry from mesh. Mesh can not be scaled via ScaleEntity.
 ; Only ScaleMesh is supported.
Function BodyCreateHull%(mesh%,mass#)
	; Obtain surfaces and vertices number
	Local nsurf = CountSurfaces(mesh)
	Local nvert = 0
	For ns = 1 To nsurf
		Local surf = GetSurface(mesh,ns)
		nvert = nvert + CountVertices(surf)
	Next
	
	Local vbank = CreateBank(nvert*4*3)
	nv = 0
	; Poke vertices coordinates to bank
	For ns = 1 To nsurf
		surf = GetSurface(mesh,ns)
		nvv = CountVertices(surf)
		For nvc = 0 To nvv - 1
			PokeFloat vbank,nv*12+0,VertexX(surf,nvc)
			PokeFloat vbank,nv*12+4,VertexY(surf,nvc)
			PokeFloat vbank,nv*12+8,VertexZ(surf,nvc)
			nv = nv+1
		Next
	Next
	;call wrapper function
	Local bbb% = phBodyCreateHull(vbank,nvert,mass)
	FreeBank vbank
	Return bbb
End Function

 ; Create body with mesh geometry from mesh. Mesh can not be scaled via ScaleEntity.
 ; Only ScaleMesh is supported. Not recommended to use. It is better to use hulls.
Function BodyCreateMesh%(mesh%,mass#)
	; Obtain surfaces And triangles number
	Local nsurf = CountSurfaces(mesh)
	Local nvert = 0
	For ns = 1 To nsurf
		Local surf = GetSurface(mesh,ns)
		nvert = nvert + (CountTriangles(surf)*3)
	Next
	
	Local vbank = CreateBank(nvert*4*3)
	nv = 0
	; Poke vertices coordinates to bank
	For ns = 1 To nsurf
		surf = GetSurface(mesh,ns)
		nvv = CountTriangles(surf)
		For nvc = 0 To nvv - 1
			For t = 0 To 2
				If(nv &lt; nvert) Then 
					PokeFloat vbank,nv*12+0,VertexX(surf,TriangleVertex(surf,nvc,t))
					PokeFloat vbank,nv*12+4,VertexY(surf,TriangleVertex(surf,nvc,t))
					PokeFloat vbank,nv*12+8,VertexZ(surf,TriangleVertex(surf,nvc,t))
					nv = nv+1
				EndIf
			Next
		Next
	Next
	Local bbb% = phBodyCreateMesh(vbank,nvert,mass)
	FreeBank vbank
	Return bbb
End Function

 ; Add all triangles from mesh to level.
Function LevelAddMesh(mesh%)
	Local x1#,y1#,z1#,x2#,y2#,z2#,x3#,y3#,z3#
	Local ns%
	Local nsurf = CountSurfaces(mesh)
	For ns = 1 To nsurf
		Local surf = GetSurface(mesh,ns)
		Local nvv = CountTriangles(surf)
		For nvc = 0 To nvv - 1
			x1# = VertexX(surf,TriangleVertex(surf,nvc,0))
			y1# = VertexY(surf,TriangleVertex(surf,nvc,0))
			z1# = VertexZ(surf,TriangleVertex(surf,nvc,0))
			
			x2# = VertexX(surf,TriangleVertex(surf,nvc,1))
			y2# = VertexY(surf,TriangleVertex(surf,nvc,1))
			z2# = VertexZ(surf,TriangleVertex(surf,nvc,1))
			
			x3# = VertexX(surf,TriangleVertex(surf,nvc,2))
			y3# = VertexY(surf,TriangleVertex(surf,nvc,2))
			z3# = VertexZ(surf,TriangleVertex(surf,nvc,2))
			
			phLevelAddFace(x1#,y1#,z1#,x2#,y2#,z2#,x3#,y3#,z3#)
		Next
	Next
End Function


Function LevelAddTerrain(terrain%)
	;Stop
	ts = TerrainSize(terrain)
	For x = 0 To ts
		For z = 0 To ts
			y# = TerrainHeight(terrain,x,z)
			TFormPoint x,y,z,terrain,0
			x1# = TFormedX():y1# = TFormedY():z1# = TFormedZ()
			y# = TerrainHeight(terrain,x+1,z)
			TFormPoint x+1,y,z,terrain,0
			x2# = TFormedX():y2# = TFormedY():z2# = TFormedZ()
			y# = TerrainHeight(terrain,x,z+1)
			TFormPoint x,y,z+1,terrain,0
			x3# = TFormedX():y3# = TFormedY():z3# = TFormedZ()
			phLevelAddFace(x1#,y1#,z1#,x2#,y2#,z2#,x3#,y3#,z3#)
;--
			y# = TerrainHeight(terrain,x,z+1)
			TFormPoint x,y,z+1,terrain,0
			x1# = TFormedX():y1# = TFormedY():z1# = TFormedZ()
			y# = TerrainHeight(terrain,x+1,z)
			TFormPoint x+1,y,z,terrain,0
			x2# = TFormedX():y2# = TFormedY():z2# = TFormedZ()
			y# = TerrainHeight(terrain,x+1,z+1)
			TFormPoint x+1,y,z+1,terrain,0
			x3# = TFormedX():y3# = TFormedY():z3# = TFormedZ()
			phLevelAddFace(x1#,y1#,z1#,x2#,y2#,z2#,x3#,y3#,z3#)
		Next
	Next
End Function

 ; Create body with terrain geometry
 ; cellsize# is size of one cell of the terrain.
 ; maxheight# is height of the brightest point (i.e. color is 255)
 ; when you scale the terrain, scaleX and scaleZ are cellsize, scaleY is maxheight
 ; I.e. ScaleEntity terrain,cellseize,mexheight,cellsize
Function TerrainCreate%(terrain%,cellsize#,maxheight#)
	; Poke heightfield to bank
	nSize% = TerrainSize (terrain)
	bank = CreateBank((nSize+1)*(nSize+1)*4)
	For z%=0 To nSize
		For x% = 0 To nSize
			Y# = TerrainHeight(terrain,x,z)*maxheight
			PokeFloat bank,(z*(nSize+1)+x)*4,y
		Next
	Next
	; Call wrapper function
	body% = phTerrainCreate(nSize+1,Cellsize,bank)
	FreeBank(bank)
	Return body
End Function

 ; Step the world and reposition entity to body's position.
Function WorldStep(dtime#)
	Local p.phx
	; Step the world
	phWorldStep(dtime#)
	; position and rotate all meshes as the corresponding bodies
	For p = Each phx
		PositionEntity p\mesh,phBodyGetX(p\body),phBodyGety(p\body),phBodyGetz(p\body), True
		If Not p\dont_rotate_mesh Then RotateEntity p\mesh,phBodyGetpitch(p\body),phBodyGetyaw(p\body),phBodyGetroll(p\body), True
	Next
End Function

 ; Create mesh and body for a box
Function phxCreateBox.phx(w#,h#,d#,mass#)
	Local p.phx
	p = New phx
	p\body = phBodyCreateBox(w,h,d,mass)
	p\mesh = CreateCube()
	ScaleMesh p\mesh,w*.5,h*.5,d*.5
	Return p
End Function

 ; Create mesh and body for a sphere
Function phxCreatePhysSphere.phx(rx#,ry#,rz#,mass#)
	Local p.phx
	p = New phx
	p\body = phBodyCreateSphere(rx,ry,rz,mass)
	;p\mesh = CreateSphere()
	;ScaleMesh p\mesh,rx,ry,rz
	Return p
End Function

 ; Create mesh and body for a sphere
Function phxCreateSphere.phx(rx#,ry#,rz#,mass#)
	Local p.phx
	p = New phx
	p\body = phBodyCreateSphere(rx,ry,rz,mass)
	p\mesh = CreateSphere()
	ScaleMesh p\mesh,rx,ry,rz
	Return p
End Function

 ; Create mesh and body for a cylinder
Function phxCreatePhysCyl.phx(r#,h#,mass#)
	Local p.phx
	p = New phx
	p\body = phBodyCreateCyl(r, h, mass)
	;p\mesh = CreateCylinder()
	;ScaleMesh p\mesh,r,h*.5,r
	Return p
End Function

 ; Create mesh and body for a cylinder
Function phxCreateCyl.phx(r#,h#,mass#)
	Local p.phx
	p = New phx
	p\body = phBodyCreateCyl( r, h, mass )
	p\mesh = CreateCylinder()
	ScaleMesh p\mesh,r,h*.5,r
	Return p
End Function

 ; Create body for a caracter capsule
Function phxCreateCharCapsule.phx( r#, h#, mass# )
	Local p.phx
	p = New phx
	p\body = phBodyCreateCapsule( r, ( h - r ) - r, mass )
	Return p	
End Function

 ; Create body for a capsule
Function phxCreatePhysCapsule.phx( r#, h#, mass# )
	Local p.phx
	p = New phx
	p\body = phBodyCreateCapsule( r, h, mass )
	Return p	
End Function

 ; Create mesh and body for a capsule
Function phxCreateCapsule.phx( r#, h#, mass# )
	Local p.phx
	p = New phx
	p\body = phBodyCreateCapsule(r,h,mass)
	m = CreateCylinder()
	ScaleMesh m,r,h*.5,r
	m1 = CreateSphere()
	ScaleMesh m1,r,r,r
	PositionMesh m1,0,h*.5,0
	AddMesh m1,m
	FreeEntity m1
	m1 = CreateSphere()
	ScaleMesh m1,r,r,r
	PositionMesh m1,0,-h*.5,0
	AddMesh m1,m
	FreeEntity m1
	p\mesh = m
	Return p	
End Function

 ; Create pivot and null body
Function phxCreateNull.phx(mass#)
	Local p.phx
	p = New phx
	p\body = phBodyCreateNull(mass)
	p\mesh = CreatePivot()
	Return p
End Function

Function CompoundAddHull(mesh%)
	Local nsurf = CountSurfaces(mesh)
	Local nvert = 0
	For ns = 1 To nsurf
		Local surf = GetSurface(mesh,ns)
		nvert = nvert + CountVertices(surf)
	Next
	Local vbank = CreateBank(nvert*4*3)
	nv = 0
	For ns = 1 To nsurf
		surf = GetSurface(mesh,ns)
		nvv = CountVertices(surf)
		For nvc = 0 To nvv - 1
			PokeFloat vbank,nv*12+0,VertexX(surf,nvc)
			PokeFloat vbank,nv*12+4,VertexY(surf,nvc)
			PokeFloat vbank,nv*12+8,VertexZ(surf,nvc)
			nv = nv+1
		Next
	Next
	phBodyCompoundAddHull(vbank,nvert)
	FreeBank vbank
	Return bbb
End Function

</textarea><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1164995"></a>

<a name="1164996"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yue</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> oh! Thanks!!<br><br>Hey take this opportunity to ask you a question.<br><br>You can take a rag doll mesh and restore to its original state, for example the mesh is hit by a car and then gets up again. Is this possible?<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1165099"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Zethrax</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> It should be possible Yue, but I haven't done anything with ragdolls so I don't have any specific knowledge on it.<br><br>What I'd experiment with is calculating the number of degrees of rotation required to transition the ragdoll's joints from their current rotation in their local space to the desired rotation for the restored state of the ragdoll, divide that by the number of update steps, and add the resulting value during each update step.<br><br>Say for the x rotational axis for a particular joint your desired restoration angle was +45 degrees and your current angle was -45 degrees. +45 - -45 = +90 degrees to restore the joint to the desired rotation in its local space. Assuming you wanted to restore the joint in 60 steps (60 iterations of an update loop) then for each step you would add +1.5 degrees (90 / 60 = 1.5) to the joint's angle on its x axis. Do that for all the axiis of each joint for all joints.<br><br>It's probably not as simple as that, but it's the best I can come up with. <br><br></td></tr></table><br>
<a name="1165108"></a>

<a name="1165110"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yue</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think it's really complicated and has not taken it well.<br><br>By the way I am looking for a demo of the lib ragdollo where soldiers appeared by chance you do not have that example.<br><br><img src="http://newtondynamics.com/images/BlitzNewton.jpg"><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1174624"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Zethrax</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Itmbin contacted me again with a link for the source code version of the wrapper. Link is below. I've also edited it into the first post.<br><br>- Source code version provided by Itmbin -<br><a href="https://docs.google.com/file/d/0BxnwgQiJB0oZZzRlNUtsU1JiQ00/edit" target="_blank">https://docs.google.com/file/d/0BxnwgQiJB0oZZzRlNUtsU1JiQ00/edit</a> <br><br></td></tr></table><br>
<a name="1175928"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yue</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I download all in one package? <br><br></td></tr></table><br>
<a name="1221374"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> THANK YOU! I have been looking for Newton Physics for AGES! :D <br><br></td></tr></table><br>
<a name="1223043"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rick Nasher</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Very nice indeed. This may come in handy, especially the FPS example by Zethrax. <br><br></td></tr></table><br>
<a name="1228492"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chispon</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm looking for that Ragdoll Soldier example too, I downloaded it a long time ago, but I lost it due to OS Problems. <br>Do anybody still have that or a tutorial of how to make a mesh a ragdoll? (not those made of pure primitives) <br><br></td></tr></table><br>
<a name="1228923"></a>

<a name="1228924"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rick Nasher</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Found the Newton Ragdoll example on my ext harddrive. If your still interested: <br><a href="http://www.mediafire.com/download/6ac6mjd5ju48clu/ragdoll.zip" target="_blank">http://www.mediafire.com/download/6ac6mjd5ju48clu/ragdoll.zip</a> <br><br></td></tr></table><br>
<a name="1232934"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chispon</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Wow, Thank you! <br><br></td></tr></table><br>
<a name="1234246"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yue</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Rick Thanks You!! <br><br></td></tr></table><br>
<a name="1234284"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rick Nasher</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Don't mention it. ;-) <br><br></td></tr></table><br>
<a name="1234290"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yue</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Maybe you have an example of cloth? <br><br></td></tr></table><br>
<a name="1234395"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rick Nasher</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nope. Don't know if exists also. Long time no hear/no see Yue, all fine? Is Bullet not doing all you need? <br><br></td></tr></table><br>
<a name="1236034"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yue</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have no problem, just do not even understand the concept of fabric does, because the example shows several tables joined together and is totally different from PhysX. <br><br>A greeting. <br><br></td></tr></table><br>
<a name="1237537"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >rio29</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> i think today is my very very lucky day:) because "i have a very limited paycheck:( so i can't afford all the apps out there:(" anyway Blitz3D and BlitzPlus now an open source this is like a dream come true and now Newton Wrapper for free thank you so very much appreciated <br><br></td></tr></table><br>
<a name="1316923"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yue</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> I like more this system of wheels than the bullet. :) <br><br></td></tr></table><br>
<a name="1317258"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yue</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> I love the system and collision tires, something I could not do with the physical system bullet never found the way to do it but with this library looks great when the tire climbs a walk. :)<br><br><br><br><iframe width="560" height="345" src="http://www.youtube.com/embed/HPqIaHvo__w" frameborder="0" allowfullscreen></iframe> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
