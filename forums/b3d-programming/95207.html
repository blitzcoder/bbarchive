<!DOCTYPE html><html lang="en" ><head ><title >Render tweening and stutter/jerkiness</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Render tweening and stutter/jerkiness</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Render tweening and stutter/jerkiness</a><br><br>
<a name="1095184"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> So, I have an embarrassingly n00bish question...<br><br>Who here is using render tweening? Of those, who has managed to get it to work as advertised and provide "silky smooth" screen updates? 'Cause I'm having difficulty with this.<br><br>The first thing I tried is the castle demo in Samples\mak. It seems like an odd choice for a demo of something supposed to smooth updates, because I found it actually really jerky: once every second (it's quite regular) the demo seems to skip a frame and jerk to the next position. It's unsettling and gives me a headache; certainly not an appropriate effect for a released product!<br><br>I've played with the samples a lot over the years, so I re-downloaded it, just to be sure; the stutter definitely appears in Mark's original version. It's not easy to spot (but still present) in windowed mode, but in fullscreen it's quite obvious.<br><br>Anyway, I never used that code as it didn't quite suit my purposes. I came up with a slightly different version, based on the famous <a href="http://gafferongames.com/game-physics/fix-your-timestep/" target="_blank">Fix Your Timestep!</a> article (chopped this out of the middle of my program):<br><br><pre class=code>

Local renderTime = Floor(1000.0 / ProgSettings\DrawRate), updateTime# = 1000.0 / ProgSettings\UpdateRate
Local cTime = MilliSecs(), accum#;, limited = True
Local ShowDebugInfo = False, FPScount, FPStime, FPSframes		;Debug info

Global RenderTween# = 1.0		;Global for reasons not shown here


While Not KeyDown(1)		;Outermost main loop - rendering and all secondary functions
	accum = accum + (MilliSecs() - cTime)		;Accumulator for update loop
	cTime = MilliSecs()		;"Official" start of loop
	
	
	;[Block] Update loop (for render tweening) - all movement, input and update happens within this one
	While accum &gt;= updateTime
		CaptureWorld
		
		;Place updates here
		
		accum = accum - updateTime
	Wend
	RenderTween = accum / updateTime		;[End block]
	
	
	;Draw everything that needs drawing (mostly 2D stuff)
	
	
	RenderWorld RenderTween		;...and render it
	
	If ProgSettings\LimiterOn Then Delay (renderTime - (MilliSecs() - cTime)) - 1		;Free spare CPU time
	If ProgSettings\VSync Then VWait
	Flip False
Wend
</pre><br><br>It's not as obvious, but the jerks are also less regular (I'm not sure - is that better or worse?). Still nowhere near good enough for release.<br><br>I'm sure I must be missing something obvious here, as surely nobody would use render tweening if it normally gave the kind of results I'm getting. Does the castle demo work correctly for other people? Maybe it's my computer?<br><br>Lucky for me I'm designing my program for a specific machine, so I can always use a fixed update/render rate for the moment. I don't want to do that though as it would hurt the inevitable port to general PC release... and I want to understand why this is going wrong! It's really ticking me off...<br><br><br>---<br><br>I also have the feeling that there may be a bug with render CaptureWorld and deactivated cameras - has anyone experienced any problems with anything like that? I do hope it's not a B3D issue. <br><br></td></tr></table><br>
<a name="1095186"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have never used render tweening. I just use a delta value and adjust everything based on how long a frame takes. <br><br></td></tr></table><br>
<a name="1095190"></a>

<a name="1095191"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> That would probably be adequate, but I don't want to do it for two reasons:<br><br>1) Delta timing requires modifying your movement code throughout the program, which is rather like hard work (and a pain to maintain/refactor). Tweening is already built in to Blitz3D, and shouldn't require much more modification than an elaborate loop code.<br><br>2) For reasons explained in <a href="http://gafferongames.com/game-physics/fix-your-timestep/" target="_blank">the aforementioned article</a>, delta timing isn't really appropriate for anything requiring precision or consistency as it's much harder to get the maths right (e.g. anything involving physics).<br><br>Here's an example of that algorithm in use, dug out of my old projects folder (I am <i>far</i> too lazy to write a new one):<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Const VP_MAXPOINTS = 19		;Extend these as far as necessary
Const VP_MAXLINKS = 52

Type verlet		;Verlet points
	Field cx#,cy#,cz#	;Current position
	Field ox#,oy#,oz#	;Previous position
	Field radius#		;Collision radius
	Field pivot			;3D entity (necessary for collisions with non-VPhysics entities)
	Field collided		;True if verlet is in contact with another verlet or B3D entity
End Type

Type vlink		;VLinks
	Field p1.verlet
	Field p2.verlet
	Field minlength#
	Field maxlength#
	Field offset#		;The actual distance between p1 and p2, to pass on to child link
	Field parent.vlink	;Copy the length of another link (prevents rotation of hinges)
End Type

Type vpe		;Physics entities
	Field state			;0=Inactive, no collisions; 1=Active, VPhysics interactions
	Field ptype			;Type of primitive. 1=single point, 2=cube, 3=ragdoll, 4=fabric
	Field points		;Number of VPoints
	Field p.verlet[VP_MAXPOINTS]	;List of VPoints
	Field links			;Numver of VLinks
	Field l.vlink[VP_MAXLINKS]	;List of VLinks
	Field radius#
	Field mesh			;The mesh itself
End Type


Global gwidth=1024,gheight=768
Global VP_FPS#=50		;Fixed timestep for physics

Global airres#=0.985	;Air resistance (ie. slowdown when ver\collided=False)
Global colres#=0.88		;Ground resistance (ie. slowdown when ver\collided=True)
Global gravity#=0.15
Global physicstype=1	;B3D Collsion Type of physics objects


Graphics3D gwidth,gheight,32,6
SetBuffer BackBuffer()
;Include "Includes\Draw3Dz.bb"


;Scene
camera=CreateCamera();:PositionEntity camera,0,30,-100
PositionEntity camera,0,30,-60
light = CreateLight()
;PositionEntity light,0,100,-50
RotateEntity light,90,0,0
ground=CreateMesh():PointEntity camera, ground
parquet=CreateSurface(ground)
v1=AddVertex(parquet,-125,0,150):v2=AddVertex(parquet,125,0,150):v3=AddVertex(parquet,125,0,-100)
AddTriangle(parquet,v1,v2,v3):v2=AddVertex(parquet,-125,0,-100):AddTriangle(parquet,v1,v3,v2)
EntityColor ground,0,0,255:EntityType ground,2
block=CreateCube():ScaleEntity block,20,5,20
EntityType block,2


dmesh=CreateCube()
PositionEntity dmesh,0,15,0
ScaleEntity dmesh,7,7,7
EntityColor dmesh,0,255,0
;EntityAlpha dmesh,0.5

cmesh=CreateCube()
PositionEntity cmesh,20,30,0
ScaleEntity cmesh,7,7,7
EntityColor cmesh,0,255,0
;EntityAlpha cmesh,0.5

t1.vpe=CreateVCube(dmesh,4,4,4,3.25)
t2.vpe=CreateVCube(cmesh,5,5,5,2.25)


Local phystime#=1000.0/VP_FPS
Local ctime=MilliSecs()
Local accum#=0


SC_FPS=60	;Desired framerate
rtime=Floor(1000.0/SC_FPS)
limited=True

Collisions physicstype,2,2,2

While Not KeyDown(1)
	accum=accum+(MilliSecs()-ctime)
	ctime=MilliSecs()
	
	While accum&gt;=phystime	;This is the update loop for ALL time-dependent functions, not just physics
		CaptureWorld	;The most important thing! Instant-movement should happen before this
		
		UpdateVP
		
		;Forces need to stay within this loop
		ApplyForce(t2,(KeyDown(205)-KeyDown(203))/8.0,4*KeyHit(57),(KeyDown(200)-KeyDown(208))/8.0)
		
		IntegrateVP 5
		UpdateWorld
		
		accum=accum-phystime
	Wend

	If KeyHit(38) Then limited=Not limited

	If MilliSecs()-render_time=&gt;1000 Then fps=frames:frames=0:render_time=MilliSecs():Else frames=frames+1
	
	RenderWorld accum/phystime
	Text 0,30,"FPS: "+fps

	Delay (rtime-(MilliSecs()-ctime))-(limited+1)
	Flip limited
Wend

End



Function AddVerlet.verlet(ent%,x#,y#,z#,r#)		;Places the VPoint at x,y,z relative to entity
	ver.verlet=New verlet
;	ver\vbody=ent
	
	ver\cx#=EntityX(ent,1)+x:ver\ox#=ver\cx#
	ver\cy#=EntityY(ent,1)+y:ver\oy#=ver\cy#
	ver\cz#=EntityZ(ent,1)+z:ver\oz#=ver\cz#
	ver\radius#=r
	
;	ver\pivot=CreateSphere(8)
;	ScaleEntity ver\pivot,r,r,r		;Remove this later
	ver\pivot=CreatePivot()
	PositionEntity ver\pivot,ver\cx,ver\cy,ver\cz,1
	ResetEntity ver\pivot	;Make sure it doesn't collide with anything before things begin
	EntityRadius ver\pivot,r
	EntityType ver\pivot,physicstype
	
	Return ver
End Function

Function FreeVerlet(ver.verlet)
	For link.vlink=Each vlink
		If link\p1=ver Or link\p2=ver Then Delete link
	Next
	Delete ver
End Function

Function LinkVerlets.vlink(v1.verlet,v2.verlet,forced#=0,maxlen#=0,p=0)	;p=Handle(parent.vlink), because Null isn't a constant
	Local link.vlink=New vlink
	link\p1=v1
	link\p2=v2
	
	Local dx#=v1\cx-v2\cx
	Local dy#=v1\cy-v2\cy
	Local dz#=v1\cz-v2\cz
	Local length#=Sqr(dx*dx+dy*dy+dz*dz)
	
	If forced=0
		link\minlength=length
	Else
		link\minlength=forced
	EndIf
	
	link\maxlength=link\minlength
	If maxlen&gt;0 Then link\maxlength=maxlen
	If maxlen&lt;0 Then link\maxlength=length	;In case the starting positions are the max length
	
	If p=0
		link\parent=Null
	Else
		link\parent=Object.vlink(p)
	EndIf
	Return link
End Function

Function UnlinkVerlets(v1.verlet,v2.verlet)
	For link.vlink=Each vlink
		If (link\p1=v1 And link\p2=v2) Or (link\p1=v2 And link\p2=v1)
			Delete link
		EndIf
	Next
End Function

Function CreateVPoint.vpe(mesh,x#=0,y#=0,z#=0,radius#=1,yradius#=0)
	point.vpe=New vpe
	point\state=1
	point\mesh=mesh
	point\ptype=1
	point\points=1
	point\links=0
	
	point\p[1]=AddVerlet(mesh,x,y,z,radius)
	
	;Adjust the Y radius here if desired - for characters
	
	Return point
End Function

Function CreateVCube.vpe(mesh,xs#=1,ys#=1,zs#=1,rs#=1)
	cube.vpe=New vpe
	cube\state=1
	cube\mesh=mesh
	cube\ptype=2
	cube\points=9
	cube\links=30
	
	cube\p[1]=AddVerlet(mesh, xs, ys, zs,rs)
	cube\p[2]=AddVerlet(mesh,-xs, ys, zs,rs)
	cube\p[3]=AddVerlet(mesh,-xs, ys,-zs,rs)
	cube\p[4]=AddVerlet(mesh, xs, ys,-zs,rs)
	cube\p[5]=AddVerlet(mesh, xs,-ys, zs,rs)
	cube\p[6]=AddVerlet(mesh,-xs,-ys, zs,rs)
	cube\p[7]=AddVerlet(mesh,-xs,-ys,-zs,rs)
	cube\p[8]=AddVerlet(mesh, xs,-ys,-zs,rs)

	cube\l[1]=LinkVerlets(cube\p[1],cube\p[2]):cube\l[2]=LinkVerlets(cube\p[2],cube\p[3])
	cube\l[3]=LinkVerlets(cube\p[3],cube\p[4]):cube\l[4]=LinkVerlets(cube\p[4],cube\p[1])
	cube\l[5]=LinkVerlets(cube\p[5],cube\p[6]):cube\l[6]=LinkVerlets(cube\p[6],cube\p[7])
	cube\l[7]=LinkVerlets(cube\p[7],cube\p[8]):cube\l[8]=LinkVerlets(cube\p[8],cube\p[5])
	cube\l[9]=LinkVerlets(cube\p[1],cube\p[5]):cube\l[10]=LinkVerlets(cube\p[2],cube\p[6])
	cube\l[11]=LinkVerlets(cube\p[3],cube\p[7]):cube\l[12]=LinkVerlets(cube\p[4],cube\p[8])

	cube\l[13]=LinkVerlets(cube\p[2],cube\p[4]):cube\l[14]=LinkVerlets(cube\p[2],cube\p[5])
	cube\l[15]=LinkVerlets(cube\p[3],cube\p[6]):cube\l[16]=LinkVerlets(cube\p[3],cube\p[8])
	cube\l[17]=LinkVerlets(cube\p[4],cube\p[5]):cube\l[18]=LinkVerlets(cube\p[6],cube\p[8])
	cube\l[19]=LinkVerlets(cube\p[2],cube\p[8]):cube\l[20]=LinkVerlets(cube\p[4],cube\p[6])
	cube\l[21]=LinkVerlets(cube\p[1],cube\p[7]):cube\l[22]=LinkVerlets(cube\p[3],cube\p[5])
	
	cs#=xs
	If cs&lt;ys Then cs=ys		;Fit the centre point to the smallest dimension
	If cs&lt;zs Then cs=zs
	cs=cs+rs				;Then add corner radius to fill out the sides
	cube\p[9]=AddVerlet(mesh,0,0,0,cs)
	
	cube\l[23]=LinkVerlets(cube\p[1],cube\p[9]):cube\l[24]=LinkVerlets(cube\p[7],cube\p[9])
	cube\l[25]=LinkVerlets(cube\p[3],cube\p[9]):cube\l[26]=LinkVerlets(cube\p[5],cube\p[9])
	cube\l[27]=LinkVerlets(cube\p[2],cube\p[9]):cube\l[28]=LinkVerlets(cube\p[8],cube\p[9])
	cube\l[29]=LinkVerlets(cube\p[4],cube\p[9]):cube\l[30]=LinkVerlets(cube\p[6],cube\p[9])
	
	Return cube
End Function

;Function CreateVCloth.vpe()
;End Function

Function CreateRagdoll.vpe(body)
	rd.vpe=New vpe
	rd\state=1
	rd\mesh=body
	rd\ptype=3
	rd\points=19
	rd\links=52

	rd\p[1]=AddVerlet(body,0,27,0,1.5)		;Head
	rd\p[2]=AddVerlet(body,-3.32,23.5,0,1.1)	;Shoulders
	rd\p[3]=AddVerlet(body,3.32,23.5,0,1)
	rd\p[4]=AddVerlet(body,-1.64,15.8,1,1.2)	;Hips
	rd\p[5]=AddVerlet(body,1.64,15.8,1,1.2)
	rd\p[6]=AddVerlet(body,-1.64,15.8,-1,1.2)
	rd\p[7]=AddVerlet(body,1.64,15.8,-1,1.2)

	rd\p[8]=AddVerlet(body,-8.6,23,0,0.8)	;Arms
	rd\p[9]=AddVerlet(body,-8.6,24,0,0.8)
	rd\p[10]=AddVerlet(body,-13.6,23.5,0,0.6)
	rd\p[11]=AddVerlet(body,8.6,23,0,0.8)
	rd\p[12]=AddVerlet(body,8.6,24,0,0.8)
	rd\p[13]=AddVerlet(body,13.6,23.5,0,0.6)

	rd\p[14]=AddVerlet(body,-2,7.8,0,1.1)	;Legs
	rd\p[15]=AddVerlet(body,-1.2,7.8,0,1.1)
	rd\p[16]=AddVerlet(body,-1.6,0,0,1)
	rd\p[17]=AddVerlet(body,2,7.8,0,1.1)
	rd\p[18]=AddVerlet(body,1.2,7.8,0,1.1)
	rd\p[19]=AddVerlet(body,1.6,0,0,1)

	rd\l[1]=LinkVerlets(rd\p[1],rd\p[2],3,-1):rd\l[2]=LinkVerlets(rd\p[1],rd\p[3],3,-1)
	rd\l[3]=LinkVerlets(rd\p[2],rd\p[3])
	rd\l[4]=LinkVerlets(rd\p[2],rd\p[4]):rd\l[5]=LinkVerlets(rd\p[3],rd\p[5])
	rd\l[6]=LinkVerlets(rd\p[2],rd\p[6]):rd\l[7]=LinkVerlets(rd\p[3],rd\p[7])
	rd\l[8]=LinkVerlets(rd\p[4],rd\p[6]):rd\l[9]=LinkVerlets(rd\p[5],rd\p[7])
	rd\l[10]=LinkVerlets(rd\p[4],rd\p[5]):rd\l[11]=LinkVerlets(rd\p[6],rd\p[7])
	rd\l[12]=LinkVerlets(rd\p[4],rd\p[7]):rd\l[13]=LinkVerlets(rd\p[5],rd\p[6])
	rd\l[14]=LinkVerlets(rd\p[2],rd\p[5]):rd\l[15]=LinkVerlets(rd\p[2],rd\p[7])
	rd\l[16]=LinkVerlets(rd\p[3],rd\p[4]):rd\l[17]=LinkVerlets(rd\p[3],rd\p[6])
	rd\l[18]=LinkVerlets(rd\p[1],rd\p[6],11.5,-1):rd\l[19]=LinkVerlets(rd\p[1],rd\p[7],11.5,-1)

	rd\l[20]=LinkVerlets(rd\p[2],rd\p[8]):rd\l[21]=LinkVerlets(rd\p[2],rd\p[9])
	rd\l[22]=LinkVerlets(rd\p[8],rd\p[10]):rd\l[23]=LinkVerlets(rd\p[9],rd\p[10])
	rd\l[24]=LinkVerlets(rd\p[3],rd\p[9],6,-1):rd\l[25]=LinkVerlets(rd\p[2],rd\p[10],5,-1)
	rd\l[26]=LinkVerlets(rd\p[8],rd\p[9]):rd\l[27]=LinkVerlets(rd\p[11],rd\p[12])
	rd\l[28]=LinkVerlets(rd\p[3],rd\p[11]):rd\l[29]=LinkVerlets(rd\p[3],rd\p[12])
	rd\l[30]=LinkVerlets(rd\p[11],rd\p[13]):rd\l[31]=LinkVerlets(rd\p[12],rd\p[13])
	rd\l[32]=LinkVerlets(rd\p[2],rd\p[12],6,-1):rd\l[33]=LinkVerlets(rd\p[3],rd\p[13],5,-1)

	rd\l[34]=LinkVerlets(rd\p[4],rd\p[14]):rd\l[35]=LinkVerlets(rd\p[4],rd\p[15])
	rd\l[36]=LinkVerlets(rd\p[14],rd\p[16]):rd\l[37]=LinkVerlets(rd\p[15],rd\p[16])
	rd\l[38]=LinkVerlets(rd\p[14],rd\p[15]):rd\l[39]=LinkVerlets(rd\p[6],rd\p[16],7,15.6)
	rd\l[40]=LinkVerlets(rd\p[6],rd\p[14],8,10):rd\l[41]=LinkVerlets(rd\p[6],rd\p[15],8,10,Handle(rd\l[40]))
	rd\l[42]=LinkVerlets(rd\p[5],rd\p[17]):rd\l[43]=LinkVerlets(rd\p[5],rd\p[18])
	rd\l[44]=LinkVerlets(rd\p[17],rd\p[19]):rd\l[45]=LinkVerlets(rd\p[18],rd\p[19])
	rd\l[46]=LinkVerlets(rd\p[17],rd\p[18]):rd\l[47]=LinkVerlets(rd\p[7],rd\p[19],7,15.6)
	rd\l[48]=LinkVerlets(rd\p[7],rd\p[17],8,10):rd\l[49]=LinkVerlets(rd\p[7],rd\p[18],8,10,Handle(rd\l[48]))
	rd\l[50]=LinkVerlets(rd\p[15],rd\p[18],2,12)
	rd\l[51]=LinkVerlets(rd\p[5],rd\p[15],7,14):rd\l[52]=LinkVerlets(rd\p[4],rd\p[18],8,14)
	
	Return rd
End Function

Function FreeVEntity(ent.vpe)
	For v=1 To ent\points
		FreeVerlet(ent\p[v])
	Next
	Delete ent
End Function

Function HideVEntity(ent.vpe,hidemesh=False)
	For v=1 To ent\points
		HideEntity ent\p[v]\pivot
	Next
	ent\state=False
	If hidemesh Then HideEntity ent\mesh
End Function

Function ShowVEntity(ent.vpe)
	For v=1 To ent\points
		ShowEntity ent\p[v]\pivot
	Next
	ent\state=True
	ShowEntity ent\mesh
End Function

Function SetColType(ent.vpe,ctype)
	For v=1 To ent\points
		EntityType ent\p[v]\pivot,ctype
	Next
End Function

Function ApplyForce(ent.vpe,x#,y#,z#)
	If ent\state
		For v=1 To ent\points
			ver.verlet=ent\p[v]
			ver\cx=ver\cx+x
			ver\cy=ver\cy+y
			ver\cz=ver\cz+z
		Next
	EndIf
End Function

Function SetForce(ent.vpe,x#,y#,z#)
	If ent\state
		For v=1 To ent\points
			ver.verlet=ent\p[v]
			ver\ox=ver\cx
			ver\oy=ver\cy
			ver\oz=ver\cz
			ver\cx=ver\cx+x
			ver\cy=ver\cy+y
			ver\cz=ver\cz+z
		Next
	EndIf
End Function

Function ApplyPointForce(ver.verlet,x#,y#,z#)
	ver\cx=ver\cx+x
	ver\cy=ver\cy+y
	ver\cz=ver\cz+z
End Function

Function SetPointForce(ver.verlet,x#,y#,z#)
	ver\ox=ver\cx
	ver\oy=ver\cy
	ver\oz=ver\cz
	ver\cx=ver\cx+x
	ver\cy=ver\cy+y
	ver\cz=ver\cz+z
End Function

Function MoveVerlet(ver.verlet,x#,y#,z#,n=1,r=1)
	ver\cx=ver\cx+x
	ver\cy=ver\cy+y
	ver\cz=ver\cz+z
	If n
		ver\ox=ver\cx
		ver\oy=ver\cy
		ver\oz=ver\cz
	Else
		ver\ox=ver\cx+x
		ver\oy=ver\cy+y
		ver\oz=ver\cz+z
	EndIf
	PositionEntity ver\pivot,ver\cx,ver\cy,ver\cz
	If r Then ResetEntity ver\pivot
End Function

Function MoveVEntity(ent.vpe,x#,y#,z#,n=1,r=1)
	For v=1 To ent\points
		ver.verlet=ent\p[v]
		ver\cx=ver\cx+x
		ver\cy=ver\cy+y
		ver\cz=ver\cz+z
		If n
			ver\ox=ver\cx
			ver\oy=ver\cy
			ver\oz=ver\cz
		Else
			ver\ox=ver\cx+x
			ver\oy=ver\cy+y
			ver\oz=ver\cz+z
		EndIf
		PositionEntity ver\pivot,ver\cx,ver\cy,ver\cz
		If r Then ResetEntity ver\pivot
	Next
	TranslateEntity ent\mesh,x,y,z,1
End Function

Function PositionVerlet(ver.verlet,x#,y#,z#,n=1)
	If n
		ver\ox=x
		ver\oy=y
		ver\oz=z
	Else
		ver\ox=x-(ver\cx-ver\ox)
		ver\oy=y-(ver\cy-ver\oy)
		ver\oz=z-(ver\cz-ver\oz)
	EndIf
	ver\cx=x
	ver\cy=y
	ver\cz=z
	PositionEntity ver\pivot,ver\cx,ver\cy,ver\cz
	ResetEntity ver\pivot
End Function

Function PositionVEntity(ent.vpe,x#,y#,z#,n=1)
	For v=1 To ent\points
		ver.verlet=ent\p[v]
		If n
			ver\ox=x
			ver\oy=y
			ver\oz=z
		Else
			ver\ox=x-(ver\cx-ver\ox)
			ver\oy=y-(ver\cy-ver\oy)
			ver\oz=z-(ver\cz-ver\oz)
		EndIf
		ver\cx=x+(ver\cx-EntityX(ent\mesh,1))
		ver\cy=y+(ver\cy-EntityY(ent\mesh,1))
		ver\cz=z+(ver\cz-EntityZ(ent\mesh,1))
		PositionEntity ver\pivot,ver\cx,ver\cy,ver\cz
		ResetEntity ver\pivot
	Next
	PositionEntity ent\mesh,x,y,z,1
End Function

Function IntegrateVP(itns%)
	Local dx#,dy#,dz#,length#,diff#

	For vb.vpe=Each vpe
	If vb\state		;Proceed if vb is activated
	For v=1 To vb\points
		ver.verlet=vb\p[v]
		drag#=airres
		If ver\collided Then drag=colres
		
		xvel#=(ver\cx-ver\ox)*drag
		If Abs(xvel)&lt;0.002 Then xvel=0	;Remove tiny errors that cause things to spin without forces...
		ver\ox=ver\cx
		ver\cx=ver\cx+xvel

		yvel#=(ver\cy-ver\oy)*drag
		If Abs(yvel)&lt;0.002 Then yvel=0
		ver\oy=ver\cy
		ver\cy=ver\cy+yvel-gravity
		
		zvel#=(ver\cz-ver\oz)*drag
		If Abs(zvel)&lt;0.002 Then zvel=0	;...which actually speeds up simulation
		ver\oz=ver\cz
		ver\cz=ver\cz+zvel
		
		;Collisions
		ver\collided=False
		For vt.vpe=Each vpe
			If vt&lt;&gt;vb And vt\state		;Check that vt is also activated
				For v2=1 To vt\points
					vet.verlet=vt\p[v2]
					dx#=ver\cx-vet\cx
					dy#=ver\cy-vet\cy
					dz#=ver\cz-vet\cz
					dd#=Sqr(dx*dx+dy*dy+dz*dz)
					tr#=ver\radius+vet\radius
					If dd&lt;tr	;If the verlets are nearer together than their combined radii
						ddt#=dd-tr
						ddx#=ddt*(dx/dd)
						ddy#=ddt*(dy/dd)
						ddz#=ddt*(dz/dd)
						
						ver\cx=ver\cx-ddx
						ver\cy=ver\cy-ddy
						ver\cz=ver\cz-ddz
						
						vet\cx=vet\cx+ddx
						vet\cy=vet\cy+ddy
						vet\cz=vet\cz+ddz
						
						ver\collided=True
					EndIf
				Next
			EndIf
		Next
		
		;Screen boundaries
		If ver\cx&lt;-100 Then ver\cx=-100
	;	If ver\cy&lt;ver\radius Then ver\cy=ver\radius
		If ver\cz&lt;-75 Then ver\cz=-75
		If ver\cx&gt;100 Then ver\cx=100
		If ver\cy&gt;100 Then ver\cy=100
		If ver\cz&gt;125 Then ver\cz=125
	Next
	EndIf	;State
	Next	;vpe

	For vb.vpe=Each vpe
	If vb\state		;Proceed if vb is activated
	For loop = 1 To itns	;iterations
	For link=1 To vb\links
		c.vlink=vb\l[link]
		dx=c\p2\cx-c\p1\cx
		dy=c\p2\cy-c\p1\cy
		dz=c\p2\cz-c\p1\cz
		
		length=Sqr(dx*dx+dy*dy+dz*dz)	;distance between p1 and p2

		If length&gt;0
			If c\parent=Null
				diff=0
				c\offset=length
				If length&lt;c\minlength Then diff=(length-c\minlength)/length:c\offset=c\minlength	;vector length minus vlink length
				If length&gt;c\maxlength Then diff=(length-c\maxlength)/length:c\offset=c\maxlength
			Else
				diff=(length-c\parent\offset)/length	;Snap to length of parent
			EndIf
		EndIf

		dx=dx/2:dy=dy/2:dz=dz/2	;find the midpoint

		c\p1\cx=c\p1\cx+diff*dx
		c\p1\cy=c\p1\cy+diff*dy
		c\p1\cz=c\p1\cz+diff*dz

		c\p2\cx=c\p2\cx-diff*dx
		c\p2\cy=c\p2\cy-diff*dy
		c\p2\cz=c\p2\cz-diff*dz
	Next	;VLinks
	Next	;Iterations
	EndIf	;State
	Next	;vpe
	
	For ver.verlet=Each verlet
		PositionEntity ver\pivot,ver\cx,ver\cy,ver\cz
	Next
End Function

Function UpdateVP()
	For ver.verlet=Each verlet
		If CountCollisions(ver\pivot)
			ver\cx=EntityX(ver\pivot)
			ver\cy=EntityY(ver\pivot)
			ver\cz=EntityZ(ver\pivot)
			ver\collided=True
		EndIf
	Next
;	For c.vlink = Each vlink
;		PositionEntity c\mesh,c\p1\cx,c\p1\cy,c\p1\cz,1
;		PointEntity c\mesh,c\p2\pivot
;	Next
	For body.vpe=Each vpe
		If body\state
			If body\ptype=1
				If body\mesh&lt;&gt;0 Then PositionEntity body\mesh,body\p[1]\cx,body\p[1]\cy,body\p[1]\cz
			EndIf
			If body\ptype=2
				mx#=(body\p[3]\cx+body\p[5]\cx)/2
				my#=(body\p[3]\cy+body\p[5]\cy)/2
				mz#=(body\p[3]\cz+body\p[5]\cz)/2
				PositionEntity body\mesh,mx,my,mz	
				AlignToVector body\mesh,(body\p[1]\cx+body\p[3]\cx)/2-mx,(body\p[1]\cy+body\p[3]\cy)/2-my,(body\p[1]\cz+body\p[3]\cz)/2-mz,2,1
				AlignToVector body\mesh,(body\p[4]\cx+body\p[5]\cx)/2-mx,(body\p[4]\cy+body\p[5]\cy)/2-my,(body\p[4]\cz+body\p[5]\cz)/2-mz,3,1
			EndIf
		EndIf
	Next
End Function
</textarea><br><br>(The crappy attempt at a physics engine "justifies" the need for fixed-rate logic. The crappy coding style is just a younger, even more insufferable version of me... ugh.)<br><br>Anyway, I find that the effect is pretty obvious if you push the cube to the rear of the area and drive it back and forth across the screen. It has time to "stutter" once or twice per pass.<br><br>If this (and the castle demo) looks smooth to other people, perhaps there's something wrong with my computer. It strikes me as odd that neither well-known algorithm should work correctly!<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1095202"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rroff</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've never noticed render tweening to be jerky except when I've messed something up - I do make sure I use 30Hz gamelogic updates tho rather than 20 or a higher value. <br><br></td></tr></table><br>
<a name="1095224"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Adam Novagen</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've never noticed jerkiness, but I have noticed that "snap-to" actions get interpolated; to clarify, a call of <b>PositionEntity()</b> to move something from one point to another ordinarily just PUTS it at the new location. With render tweening, it can often be seen - only in a single frame, mind - moving through a waypoint. It doesn't appear to <i>move</i> per se, but it gives an annoying flickering impression. <br><br></td></tr></table><br>
<a name="1095250"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RifRaf</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> It can be caused by a code execution delay without updating the rendertween value..<br><br>for example, a pause menu.  If in the pause function you dont continue to update tweening timing, when you unpause you will get a big "jerk" or even a rendering lockup because of the timing. <br><br></td></tr></table><br>
<a name="1095261"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I <i>think</i> that I have worked this out, and that I have been quite stupid...<br><br>In case nobody looked at the code (both Mark's castle and my demo above), I've been testing these with VSync enabled (Flip True in both demos; VWait in my real application).<br><br><i>All I had to do</i> to make the funny effect go away was try it with no VSync of either kind (you can see the difference quite easily by trying both ways with the demo posted above). I guess that render tweening isn't intended to be used alongside other kinds of frame limiter codes as well? (I probably need to fine-tune the Delay line so that it doesn't interfere with the tweening too.)<br><br>So that also means that Mark's code is correct after all (that old mantra we see every so often on this site still holds: "trust Mark's code!"). Slightly odd choice for the demo of it to have Flip True on, but whatever.<br><br>I could be wrong. I've been staring at that darn castle demo for about three hours now. My eyes hurt.<br><br>(How I found this out: by adding a simple WaitTimer to the castle demo in the hopes that I could slow the render FPS down and catch the effect by eye more easily, to match it up with crap spewed to the debug console every frame... and instead noticed it made the effect worse. Hence, a connection to timing code. Then thought to try VSync.) <br><br></td></tr></table><br>
<a name="1095262"></a>

<a name="1095263"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rroff</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Could do something like with idtech5 where it analyses performance and when the framerate is slowing down it doesn't wait for the vertical scanline to reset before rendering the frame, by doing something similiar but also adjusting so that tween updates aren't delayed it could produce smoother performance in both regards (also less input lag) and only minimal tearing.<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
