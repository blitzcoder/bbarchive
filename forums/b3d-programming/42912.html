<!DOCTYPE html><html lang="en" ><head ><title >animated splash screen with win api stuff</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >animated splash screen with win api stuff</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >animated splash screen with win api stuff</a><br><br>
<a name="480090"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >nadia</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've been playing around with win api stuff and came up with this animated splash screen. Don't know how usefull this is for 'real' games. Unfortunately it crashes when run in full screen mode :(<br>Anyway, play around with it and com up with improvements, ideas<br><br>You have to compile this code before running the testing code below.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;*******************************************************************************************
; animated splash screen by nadia, Jan 2005
;
; to run this sample code you need to compile this 'animSplash.bb' to 'animSplash.exe
; and save it in the same folder as 'test animSplash.bb'
;
; make sure you add the following declarations to the user libs:
;
;		.lib "Gdi32.dll"
;		api_CreateRectRgn%(X1%, Y1%, X2%, Y2%): "CreateRectRgn"
;		api_CreateRoundRectRgn%(X1%, Y1%, X2%, Y2%, X3%, Y3%): "CreateRoundRectRgn"
;
;		.lib "user32.dll"
;		api_FindWindow_0% (lpClassName%, lpWindowName$) : "FindWindowA"
;		api_SendMessage% (hWnd%, wMsg%, wParam%, lParam%): "SendMessageA"
;		api_SetWindowRgn%(hWnd%, hRgn%, bRedraw): "SetWindowRgn"
;		api_SetWindowPos%(hWnd% ,hWndInsertAfter%, x%, y%, cx%, cy%, wFlags%): "SetWindowPos"
;
;;*******************************************************************************************


; API Constants
Const WM_CLOSE 	= $10
Const SWP_NOMOVE 	= 2
Const SWP_NOSIZE 	= 1
Const FLAGS = SWP_NOMOVE Or SWP_NOSIZE
Const HWND_TOPMOST 	= -1
Const HWND_NOTOPMOST = -2

; width and height of anim splash screen app
Const SCR_TITLEBAR_HEIGHT = 27	; height of window title bar
Const SCR_RADIUS = 20				; corner radius for rounded region 


Const SCR_WIDTH  = 250
Const SCR_HEIGHT = 200
Const APP_TITLE$ = "Animated Splash Screen"

AppTitle APP_TITLE$
Graphics SCR_WIDTH,SCR_HEIGHT,16,2

roundedRgn = False ; flag for splash screen with rounded corners

; get the command line input used for displayed message
msg$ = CommandLine()

; extract indicator if rounded corners are required
If Instr(msg$, "[roundRect]") Then
	roundedRgn = True
	msg$ = Replace(msg$, "[roundRect]", "")
End If

; if we didn't get a message via command line, set a default message
If msg$ = "" Then
	msg$ = "loading...!
End If




If Not roundedRgn Then 
	; create the region to restrict the visible part of the splash screen app
	hRgn = api_CreateRectRgn(0,SCR_TITLEBAR_HEIGHT,SCR_WIDTH + 5, SCR_HEIGHT + SCR_TITLEBAR_HEIGHT +5)
Else 
	; create a region with rounded corners
	hRgn = api_CreateRoundRectRgn(5,SCR_TITLEBAR_HEIGHT + 5 ,SCR_WIDTH, SCR_HEIGHT + SCR_TITLEBAR_HEIGHT - 5, SCR_RADIUS, SCR_RADIUS)
End If 

; get the win handle of this app
hWnd = FindWindow(APP_TITLE$)

; applay the win region 
If hRgn &lt;&gt; 0 And hWnd &lt;&gt; 0 Then
	api_SetWindowRgn(hWnd, hRgn, True)

	; set this app to stay as topmost window while open
	api_SetWindowPos(hWnd, HWND_TOPMOST, 0, 0, 0, 0, FLAGS)
End If


; create and load a font
fnt = LoadFont("arial", 20, True)
SetFont fnt

; set uo some variables for the text animation
txtW = StringWidth(msg$)
txtH = StringHeight(msg$)
curX = 20 + (txtW/2)
curY = 20 + (txtH/2)
incX = 2
incY = 2

Repeat
	Cls 
	Delay 10

	; emergency exit !!!!
	If KeyHit(1) End	

	; draw some animated lines
	For i = 0 To 200
		Color Rand(50,255), Rand(80), 0; Rand(255)
		Line Rand(SCR_WIDTH), 0, Rand(SCR_WIDTH), SCR_HEIGHT
	Next

	; bounce the message around
	Color 250, 200, 0
	If (curX + incX - (txtW / 2) &lt; 0 ) Or (curX + incX + (txtW / 2) &gt; SCR_WIDTH) Then incX = incX * -1
	If (curY + incY - (txtH / 2) &lt; 0 ) Or (curY + incY + (txtH / 2) &gt; SCR_HEIGHT) Then incY = incY * -1
	curX = CurX + incX
	curY = CurY + incY
	Text curX, curY, msg$, True, True 
	Flip
Forever

;===============================
; uses semar's code from code library
; FindWindow without class name
; api_FindWindow_0% (lpClassName%, lpWindowName$) : "FindWindowA"
Function FindWindow(name$)
	hWnd% =  api_FindWindow_0% (0,name$)
	Return hWnd
End Function 
</textarea><br><br>Here is the testing code<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;*******************************************************************************************
; testing animated splash screen by nadia, Jan 2005
;
; to run this sample code you need to compile 'animSplash.bb' to 'animSplash.exe
; and save it in the same folder as this file.
; DON'T try this in FULL SCREEN MODE, it will crash!!!!!
;
; make sure you add the following declarations to the user libs:
;
;		.lib "Gdi32.dll"
;		api_CreateRectRgn%(X1%, Y1%, X2%, Y2%): "CreateRectRgn"
;		api_CreateRoundRectRgn%(X1%, Y1%, X2%, Y2%, X3%, Y3%): "CreateRoundRectRgn"
;
;		.lib "user32.dll"
;		api_FindWindow_0% (lpClassName%, lpWindowName$) : "FindWindowA"
;		api_SendMessage% (hWnd%, wMsg%, wParam%, lParam%): "SendMessageA"
;		api_SetWindowRgn%(hWnd%, hRgn%, bRedraw): "SetWindowRgn"
;		api_SetWindowPos%(hWnd% ,hWndInsertAfter%, x%, y%, cx%, cy%, wFlags%): "SetWindowPos"
;
;;*******************************************************************************************


; API Constants
Const WM_CLOSE 	= $10

; Splash screen data
Const SPL_APP_TITLE$ = "Animated Splash Screen"	; must be the same as AppTitle of animSplash.exe
Const SPL_EXE_NAME$ 	= "animSplash.exe"			; file name if animSplash app
																; make sure 'animSplash.exe' is in the
																; same folder as this app

Const USE_ROUND_RECT$ = "[roundRect]"				; draws regions with rounded corners
																; also send with command line

; text displayed in the animSplash.exe
; send to splash screen as command line
msg$ = "loading stuff..."	; &lt;&lt;---	change this to anything...
									
roundedRgn = True 			; &lt;&lt;---  set to true for splash screen with rounded corners!!!
; create command line
If roundedRgn Then
	cmdLine$ = msg$ + USE_ROUND_RECT$
Else
	cmdLine$ = msg$
End If 
; run the splash screen app
ExecFile(SPL_EXE_NAME$ + " " + cmdLine$)

; get the win handle of the splash screen app
; without loop the handle gets missed
hWnd = 0	
Repeat
	Delay 10
	hWnd = FindWindow(SPL_APP_TITLE$)
Until hWnd &lt;&gt; 0


; initialise the game engine
Graphics3D 640,480,16,2 ; &lt;&lt;&lt;--- if you use full screen mode this will crash!!!
ClsColor 0, 0, 100


; \./\./ here you would load all the game related stuff \./\./\./\./\./\./\./\./\

; some loops to waste time
secs = 10
start# = MilliSecs()
wait# = start# + (1000 * secs)
Repeat
	elapsed# = (MilliSecs() - start#) / 1000
	Cls
	Color 255, 255, 255
	Text 10, 10, "Splash Screen Handle = " + hWnd
	Text 10, 30, "Rgn Handel = " + hRgn
	Color 255, 0, 0
	Text 10, 50, "Closing Splash Screen in " + (secs - Int(elapsed#))  + " secs"
	Flip
Until wait &lt; MilliSecs()

Cls

; finished loading the game stuff
;==============================================================================

; unload the the splash screen app
If KillWindow(hWnd) Then
	msg$ = "Animated splash screen closed ok!"
Else
	msg$ = "Failed to close splash screen!"
End If
Color 255, 255, 255
Text 10, 10,  msg$
Text 10, 30, "Hit any key to close me"
Flip 

WaitKey 
End


Function KillWindow(hWND)
	If hWND = 0 Then Return
	
	ret = api_SendMessage(hWND, WM_CLOSE, 0, 0) 
	If ret = 0 Then 	; I think this always returns 0,
							; regardless if the function succeeds or not
		Return True
	Else
		Return False
	End If 

End Function 

;===============================
; uses semar's code from code library
; FindWindow without class name
; api_FindWindow_0% (lpClassName%, lpWindowName$) : "FindWindowA"
Function FindWindow(name$)
	hWnd% =  api_FindWindow_0% (0,name$)
	Return hWnd
End Function 
</textarea> <br><br></td></tr></table><br>
<a name="480146"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Lane</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I got nutten!! <br><br></td></tr></table><br>
<a name="480147"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >fall_x</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Could you post a screenshot? No offence, but I don't want to try compiling this if I don't know wether I'm interested or not :) <br><br></td></tr></table><br>
<a name="480198"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >nadia</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nope, no screenies… <br>They wouldn’t help much anyway. You have to run this stuff to see what it does or forever wonder in agony, what marvelously ingenious code you might have missed out on ;-)<br><br>Ok, here is a bit of a description what it does:<br>The first bit is a simple little splash screen, which runs an animation. On loading it applies win regions to itself to cut off the window title bar and optionally rounds off the windows corners. It also sets itself as topmost to stay on top of every other open window.<br><br>The second code bit is for testing. It simulates the game app. First it attempts to load the above app (compiled into an exe). Then it goes through some routines, which in a real game would be contents loading stuff. Finally, ones finished with the ‘loading stuff’ it will kill the animated splash screen, which was running in the foreground…<br><br>Note:<br>This stuff only runs in window mode!<br>Make sure you declare all the API call declarations in the right user libs!<br><br>..let me know if you get it to run. <br><br></td></tr></table><br>
<a name="480282"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Paolo</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Both runs ok here,<br>although sometimes, the small window in front flickers to black color, and that doesn't look so good ...<br>So, if I understand it well, the second code loads the first compiled code and show it in front while the stuff is being<br>loaded in the back, isn't it? (oops, yes, that's what it does, I have just read your second post :)<br><br>Also, remember there's a blitz sys dll (or something) that let you create windows without frames and more ... just a thought...<br><br>Paolo. <br><br></td></tr></table><br>
<a name="480306"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jfk EO-11110</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I still wonder if it's possible to use a fake-fullscreen resolution that is using the entire screen for a window without frame. This way we could use multithreading things like this in Games that are looking like fullscreen (but actually are not). Thanks for sharing. <br><br></td></tr></table><br>
<a name="481228"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >nadia</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Jfk- nice idea, faking full screen with a frameless full screen window. One would have to use api calls to check out the current monitor settings and store them. Then one could fire the ChangeDisplaySettings function to reset the display res. Then before closing the app one can reset the screen res back to the initial setting (unless blitz crashes which would leave the user to reset the display manually). <br><br>If I've got a bit of time I'll try it out... <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
