<!DOCTYPE html><html lang="en" ><head ><title >3D Lines</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >3D Lines</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >3D Lines</a><br><br>
<a name="548244"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >gosse</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> It makes a few times that I see people requesting code for 3D lines but I've never seen a good solution popping up.<br>Here's something I hacked today when someone asked it on CodersWorkshop.<br>Use CreateLine to create a new 3d line (with a wide range of parameters) and use UpdateLines in your main loop so the system can adjust them according to the camera.<br>Have fun with it! ;)<br><pre class=code>
Const scrWidth = 640
Const scrHeight = 480
Graphics3D scrWidth, scrHeight, 0, 2
AmbientLight 255, 255, 255
AppTitle "3D Lines"
AntiAlias True
SeedRnd MilliSecs()

camDefault = CreateCamera()
CameraClsColor camDefault, 110, 220, 250
MoveEntity camDefault, 0, 5, 0

lgtDefault = CreateLight(2, camDefault)
LightRange lgtDefault, 5
MoveEntity lgtDefault, 0, 3, 0

mdlBall = CreateSphere()
MoveEntity mdlBall, 6, 2, 3
mdlCube = CreateCube()
MoveEntity mdlCube, 4, 3.5, 4
ScaleEntity mdlCube, 3.4, 0.3, 0.3
mdlCylinder = CreateCylinder()
ScaleEntity mdlCylinder, 1, 5, 1
EntityColor mdlCylinder, 255, 200, 50
EntityColor mdlCube, 6, 11, 19
EntityColor mdlBall, 255, 0, 100

mdlGround = CreatePlane(16)
EntityColor mdlGround, 50, 150, 50

Global FrameStart
Global DeltaTime# = 0.1
Global GameTime# = 0
Global FPS = 1200
Global FPSCount = FPS
Global timFPS

fntDefault = LoadFont("FixedSys", 24)
SetFont fntDefault

ln = createline(0, 10, 10, 8, 5, 10, camDefault)

CreateLine(2, 2, 2, 2, 2, 4, camDefault, 255, 255, 255, 1, 1, 1, 1, -1)
CreateLine(2, 2, 4, 4, 2, 4, camDefault, 255, 255, 255, 1, 1, 1, 1, -1)
CreateLine(4, 2, 4, 4, 2, 2, camDefault, 255, 255, 255, 1, 1, 1, 1, -1)
CreateLine(4, 2, 2, 2, 2, 2, camDefault, 255, 255, 255, 1, 1, 1, 1, -1)

CreateLine(2, 2, 2, 2, 4, 2, camDefault, 255, 255, 255, 1, 1, 1, 1, -1)
CreateLine(2, 2, 4, 2, 4, 4, camDefault, 255, 255, 255, 1, 1, 1, 1, -1)
CreateLine(4, 2, 4, 4, 4, 4, camDefault, 255, 255, 255, 1, 1, 1, 1, -1)
CreateLine(4, 2, 2, 4, 4, 2, camDefault, 255, 255, 255, 1, 1, 1, 1, -1)

CreateLine(2, 4, 2, 2, 4, 4, camDefault, 255, 255, 255, 1, 1, 1, 1, -1)
CreateLine(2, 4, 4, 4, 4, 4, camDefault, 255, 255, 255, 1, 1, 1, 1, -1)
CreateLine(4, 4, 4, 4, 4, 2, camDefault, 255, 255, 255, 1, 1, 1, 1, -1)
CreateLine(4, 4, 2, 2, 4, 2, camDefault, 255, 255, 255, 1, 1, 1, 1, -1)

For i = 0 To 100
	CreateLine(Rnd(0, 30) - 15, Rnd(0, 10), Rnd(0, 30) - 15, Rnd(0, 30) - 15, Rnd(0, 10), Rnd(0, 30) - 15, camDefault, Rand(0, 255), Rand(0, 255), Rand(0, 255), Rnd(0.2, 1), 1)
Next


While Not KeyDown(1)

	; Frame Update
	FrameStart = MilliSecs()
	If timFPS &lt; FrameStart Then
		FPS = FPSCount
		FPSCount = 0
		timFPS = FrameStart + 1000
		DeltaTime = 1.0 / Float(FPS)
		GameTime = GameTime + DeltaTime
	End If
	
	; Movement
	mx# = KeyDown(203) - KeyDown(205)
	mx = mx * DeltaTime * 50
	mz# = KeyDown(200) - KeyDown(208)
	mz = mz * DeltaTime * 3
	MoveEntity camDefault, 0, 0, mz
	TurnEntity camDefault, 0, mx, 0
	
	UpdateLines()
		
	; 3D Drawing
	SetBuffer BackBuffer()
	Cls
	RenderWorld
	
	Text 1, 0, "FPS: " + FPS
	
	Flip False
	
	FPSCount = FPSCount + 1

Wend

Type Line3D
	Field x#[1], y#[1], z#[1]
	Field cam
	Field thick#
	Field mesh, sfc
	Field vertex
	Field pvt[2]
End Type

Function CreateLine(x1#, y1#, z1#, x2#, y2#, z2#, cam, r = 255, g = 255, b = 255, a# = 1, thick = 1, fx = 1, blend = 1, order = 0, zoom# = 1)
	Local l.Line3D = New Line3D
	l\x[0] = x1
	l\y[0] = y1
	l\z[0] = z1
	l\x[1] = x2
	l\y[1] = y2
	l\z[1] = z2
	l\cam = cam
	l\thick = Float(thick) / zoom
	l\mesh = CreateMesh()
	l\sfc = CreateSurface(l\mesh)
	EntityColor l\mesh, r, g, b
	EntityAlpha l\mesh, a
	EntityOrder l\mesh, order
	EntityFX l\mesh, fx
	EntityBlend l\mesh, blend
	l\vertex = AddVertex(l\sfc, x1, y1, z1)
	AddVertex(l\sfc, x2, y2, z2)
	AddVertex(l\sfc, x2, y2, z2)
	AddVertex(l\sfc, x1, y1, z1)
	AddTriangle(l\sfc, l\vertex, l\vertex+1, l\vertex+2)
	AddTriangle(l\sfc, l\vertex, l\vertex+2, l\vertex+3)
	AddTriangle(l\sfc, l\vertex+2, l\vertex+1, l\vertex)
	AddTriangle(l\sfc, l\vertex+3, l\vertex+2, l\vertex)
	AddTriangle(l\sfc, l\vertex+3, l\vertex, l\vertex+1)
	AddTriangle(l\sfc, l\vertex+3, l\vertex+1, l\vertex+2)
	l\pvt[0] = CreatePivot()
	PositionEntity l\pvt[0], x1, y1, z1
	l\pvt[1] = CreatePivot()
	PositionEntity l\pvt[1], x2, y2, z2
	l\pvt[2] = CreatePivot()
	Return Handle(l)
End Function

Function ChangeLine(ln, x1#, y1#, z1#, x2#, y2#, z2#, cam, r = 255, g = 255, b = 255, a# = 1, thick = 1, fx = 1, blend = 1, order = 0, zoom# = 1)
	Local l.Line3D = Object.Line3D(ln)
	l\x[0] = x1
	l\y[0] = y1
	l\z[0] = z1
	l\x[1] = x2
	l\y[1] = y2
	l\z[1] = z2
	l\cam = cam
	l\thick = Float(thick) / zoom
	EntityColor l\mesh, r, g, b
	EntityAlpha l\mesh, a
	EntityOrder l\mesh, order
	EntityFX l\mesh, fx
	EntityBlend l\mesh, blend
	PositionEntity l\pvt[0], x1, y1, z1
	PositionEntity l\pvt[1], x2, y2, z2
End Function

Function DeleteLine(ln)
	Local l.Line3D = Object.Line3D(ln)
	FreeEntity l\mesh
	Delete l
End Function

Function UpdateLines()
	Local x[1], y[1], r#, d#
	For l.Line3D = Each Line3D

		PositionEntity l\pvt[2], l\x[0], l\y[0], l\z[0]
		PointEntity l\pvt[2], l\pvt[1]
		r = VectorYaw(l\x[0] - EntityX(l\cam), 0, l\z[0] - EntityZ(l\cam))
		RotateEntity l\pvt[2], EntityPitch(l\pvt[2]), r-90, 0
		d = EntityDistance(l\cam, l\pvt[2]) * l\thick * 0.00125
		MoveEntity l\pvt[2], 0, -d, 0
		VertexCoords l\sfc, l\vertex, EntityX(l\pvt[2]), EntityY(l\pvt[2]), EntityZ(l\pvt[2])
		MoveEntity l\pvt[2], 0, d*2, 0
		VertexCoords l\sfc, l\vertex+3, EntityX(l\pvt[2]), EntityY(l\pvt[2]), EntityZ(l\pvt[2])

		PositionEntity l\pvt[2], l\x[1], l\y[1], l\z[1]
		PointEntity l\pvt[2], l\pvt[0]
		r = VectorYaw(l\x[1] - EntityX(l\cam), 0, l\z[1] - EntityZ(l\cam))
		RotateEntity l\pvt[2], EntityPitch(l\pvt[2]), r+90, 0
		d = EntityDistance(l\cam, l\pvt[2]) * l\thick * 0.00125
		MoveEntity l\pvt[2], 0, -d, 0
		VertexCoords l\sfc, l\vertex+1, EntityX(l\pvt[2]), EntityY(l\pvt[2]), EntityZ(l\pvt[2])
		MoveEntity l\pvt[2], 0, d*2, 0
		VertexCoords l\sfc, l\vertex+2, EntityX(l\pvt[2]), EntityY(l\pvt[2]), EntityZ(l\pvt[2])

	Next
End Function
</pre> <br><br></td></tr></table><br>
<a name="549632"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jfk EO-11110</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think there already is a 3D Line function in the archives, it has even been "code snippet of the month". However, you may post your solution to the archives too, so it won't "sink". Thanks BTW. <br><br></td></tr></table><br>
<a name="551980"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >nadia</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> great! very usefull, thanks for sharing :-) <br><br></td></tr></table><br>
<a name="551983"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >zortzblatz</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> createcylinder(3)<br>then scaling  and rotating appropriatly is another way to make 3d lines <br><br></td></tr></table><br>
<a name="553797"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >nadia</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> zortzblatz: yup, that works too but you'll find you add quite a few lines of code to position the end points of your cylinder at a specific position, (usually done with the help of an additional pivot). <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
