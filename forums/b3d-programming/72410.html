<!DOCTYPE html><html lang="en" ><head ><title >Character shading</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Character shading</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Character shading</a><br><br>
<a name="809267"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Naughty Alien</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> ..let say I have lightmapped level, with some trees on it..so when I move my character under some of the trees (inside shadow area), how to achieve that my character have effect like my character receiving that specific shadow over surface while its moving under tree?? <br><br></td></tr></table><br>
<a name="809307"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mortiis</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> It's called dynamic lightning and blitz doesn't support that afaik, maybe you can calculate the uv coordinates where your lightmap shadows are and check if your player is inside the shadow area then tint the player's color...<br><br>Or you can use a dynamic shadow userlib like devil's child's to cast shadows from trees but that would be a huge performance hit. <br><br></td></tr></table><br>
<a name="809313"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >IPete2</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> The simplest way may be to change the entitycolor of the entity you want to affect as it comes underneath things such as trees etc.  <br><br>IPete2. <br><br></td></tr></table><br>
<a name="809359"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mortiis</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes but there will be no detailed shadows projected on player. And it may be a pain in the back to code it for every simple shadow casting model, for tweaking the distance properly.<br><br>Anyway;<br><pre class=code>
If EntityDistance( Player, Tree ) &lt; 300
    EntityColor Player, 100, 100, 100
EndIf
</pre> <br><br></td></tr></table><br>
<a name="809362"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >IPete2</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well if you wanted to be a little more creative and perhaps clever, you'd put a pivot 1000 units directly above the player and cast down to it, if you see the player do nothing, if you don't use the above code.  That way you only need to have one small raycast per loop and not have to worry about putting loads of code for objects.<br><br>Also you could find out what was intefering and adapt the sahdow based on that response.  Just needs a little creative thinking that is all.<br><br>IPete2. <br><br></td></tr></table><br>
<a name="809412"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jfk EO-11110</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> An other, probably too sophisticated method is to pre-scan the 3D world and store the global illumination of every space cluster in a 3D grid. Depending on the resolution/waste of RAM you'll be able to do realtime GI. But this isn't very practical (and not even dynamic). So I'd do it like IPete said. <br><br></td></tr></table><br>
<a name="809455"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RifRaf</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just have a lightmap texture for each object the player can walk on.  Then pick below the character. get the UV coords of the pick, then check the lightmap at those UV coords to get the proper color / shade to apply to the character.;<br><br>edit:  i put somthing together from some code archive submissions that should work.  Just call shadeentity on the character and pass the lightmap texture into it.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Function shadeentity(entity,lmaptex,ambient=90)
If LinePick(EntityX(Entity), EntityY(Entity)+10, EntityZ(Entity), 0, -125, 0, 0)
	If PickedUVW()
		Local ARGB# = GetTexColor(lmaptex, PickedU(), PickedV()) And $FFFFFF
        Local RD# = (ARGB Shr 16) And $ff) + ambient
        Local GD# = (ARGB Shr 8) And $ff) + ambient
        Local BD# = (ARGB And $ff) + ambient
		If RD# &gt; 255 Then RD = 255
		If GD# &gt; 255 Then GD = 255
		If BD# &gt; 255 Then BD = 255
		EntityColor(Entity, RD, GD, BD)
EndIf
EndIf

End Function 

Function GetTexColor(tex,u#,v#)
	u#=u#*(TextureWidth(tex)-1)
	v#=v#*(TextureHeight(tex)-1)
	col#=ReadPixel(u,v,TextureBuffer(tex))
	Return col#
End Function

Type PickedTri
	Field ent,surf,tri			
	Field px#,py#,pz#			
	Field pu#[1],  pv#[1]  ,pw#[1]  
    Field vx#[2],  vy#[2]  ,vz#[2]  
	Field vnx#[2], vny#[2] ,vnz#[2] 
	Field vu#[5],  vv#[5]  ,vw#[5]  
End Type

Global g_PickedTri.pickedtri = New pickedtri

Function PickedU#(coordset = 0)
	ptri.pickedtri = g_PickedTri
	If (PickedX()&lt;&gt;ptri\px) Or (PickedY()&lt;&gt;ptri\py) Or (PickedZ()&lt;&gt;ptri\pz) Or (PickedSurface()&lt;&gt;ptri\surf)
		uvwTime=PickedUVW()
	End If
	Return ptri\pu[coordset]
End Function

Function PickedV#(coordset = 0)
	ptri.pickedtri = g_PickedTri
	If (PickedX()&lt;&gt;ptri\px) Or (PickedY()&lt;&gt;ptri\py) Or (PickedZ()&lt;&gt;ptri\pz) Or (PickedSurface()&lt;&gt;ptri\surf)
		uvwTime=PickedUVW()
	End If
	Return ptri\pv[coordset]
End Function

Function PickedW#(coordset = 0)
	ptri.pickedtri = g_PickedTri
	If (PickedX()&lt;&gt;ptri\px) Or (PickedY()&lt;&gt;ptri\py) Or (PickedZ()&lt;&gt;ptri\pz) Or (PickedSurface()&lt;&gt;ptri\surf)
		uvwTime=PickedUVW()
	End If
	Return ptri\pw[coordset]
End Function

Function PickedUVW()
	ptri.pickedtri = g_PickedTri
	If EntityClass(PickedEntity()) = "Terrain"
		terrain = PickedEntity()
		tsize# = TerrainSize(terrain)
		TFormPoint(PickedX(), PickedY(), PickedZ(), 0, terrain)
		ptri\pu[0] = TFormedX() / tsize
		ptri\pv[0] = 1.0 - (TFormedZ() / tsize)
		ptri\pu[1] = ptri\pu[0]
		ptri\pv[1] = ptri\pv[0]
		Return True
	EndIf
	
	If PickedSurface()
		ptri\ent  = PickedEntity()
		ptri\surf = PickedSurface()
		ptri\tri  = PickedTriangle()
		ptri\px = PickedX()
		ptri\py = PickedY()
		ptri\pz = PickedZ()
		For i = 0 To 2
			TFormPoint VertexX(ptri\surf,TriangleVertex(ptri\surf,ptri\tri,i)),VertexY(ptri\surf,TriangleVertex(ptri\surf,ptri\tri,i)),VertexZ(ptri\surf,TriangleVertex(ptri\surf,ptri\tri,i)),ptri\ent,0
			ptri\vx[i] = TFormedX()
			ptri\vy[i] = TFormedY()
			ptri\vz[i] = TFormedZ()
			ptri\vnx[i] = VertexNX(ptri\surf,TriangleVertex(ptri\surf,ptri\tri,i))
			ptri\vny[i] = VertexNY(ptri\surf,TriangleVertex(ptri\surf,ptri\tri,i))
			ptri\vnz[i] = VertexNZ(ptri\surf,TriangleVertex(ptri\surf,ptri\tri,i))
			ptri\vu[i+0] = VertexU(ptri\surf,TriangleVertex(ptri\surf,ptri\tri,i),0)
			ptri\vv[i+0] = VertexV(ptri\surf,TriangleVertex(ptri\surf,ptri\tri,i),0)
			ptri\vw[i+0] = VertexW(ptri\surf,TriangleVertex(ptri\surf,ptri\tri,i),0)
			ptri\vu[i+3] = VertexU(ptri\surf,TriangleVertex(ptri\surf,ptri\tri,i),1)
			ptri\vv[i+3] = VertexV(ptri\surf,TriangleVertex(ptri\surf,ptri\tri,i),1)
			ptri\vw[i+3] = VertexW(ptri\surf,TriangleVertex(ptri\surf,ptri\tri,i),1)
		Next

		Local coords = 3

		If Abs(PickedNX()) &gt; Abs(PickedNY())
			If Abs(PickedNX())&gt;Abs(PickedNZ()) Then coords = 1
		Else
			If Abs(PickedNY())&gt;Abs(PickedNZ()) Then coords = 2
		EndIf
		
		Local a0#,a1#,b0#,b1#,c0#,c1#
		
		If (coords = 3)
			a0# = ptri\vx[1] - ptri\vx[0]
			a1# = ptri\vy[1] - ptri\vy[0]
			b0# = ptri\vx[2] - ptri\vx[0]
			b1# = ptri\vy[2] - ptri\vy[0]
			c0# = PickedX() - ptri\vx[0]
			c1# = PickedY() - ptri\vy[0]
		Else		
			If (coords = 2)
				a0# = ptri\vx[1] - ptri\vx[0]
				a1# = ptri\vz[1] - ptri\vz[0]
				b0# = ptri\vx[2] - ptri\vx[0]
				b1# = ptri\vz[2] - ptri\vz[0]
				c0# = PickedX() - ptri\vx[0]
				c1# = PickedZ() - ptri\vz[0]
			Else
				a0# = ptri\vy[1] - ptri\vy[0]
				a1# = ptri\vz[1] - ptri\vz[0]
				b0# = ptri\vy[2] - ptri\vy[0]
				b1# = ptri\vz[2] - ptri\vz[0]
				c0# = PickedY() - ptri\vy[0]
				c1# = PickedZ() - ptri\vz[0]
			End If
		End If

		Local u# = (c0*b1 - b0*c1) / (a0*b1 - b0*a1)
		Local v# = (a0*c1 - c0*a1) / (a0*b1 - b0*a1)

		If (u&lt;0.0 Or u&gt;1.0) Or (v&lt;0.0 Or v&gt;1.0)
			Return False
		End If
		
		ptri\pu[0] = (ptri\vu[0] + ((ptri\vu[1] - ptri\vu[0]) * u) + ((ptri\vu[2] - ptri\vu[0]) * v)) Mod 1
		ptri\pv[0] = (ptri\vv[0] + ((ptri\vv[1] - ptri\vv[0]) * u) + ((ptri\vv[2] - ptri\vv[0]) * v)) Mod 1
		ptri\pw[0] = (ptri\vw[0] + ((ptri\vw[1] - ptri\vw[0]) * u) + ((ptri\vw[2] - ptri\vw[0]) * v)) Mod 1

		If ptri\pu[0]&lt;0.0 Then ptri\pu[0] = 1.0 + ptri\pu[0]
		If ptri\pv[0]&lt;0.0 Then ptri\pv[0] = 1.0 + ptri\pv[0]
		If ptri\pw[0]&lt;0.0 Then ptri\pw[0] = 1.0 + ptri\pw[0]
	
		ptri\pu[1] = (ptri\vu[3] + ((ptri\vu[4] - ptri\vu[3]) * u) + ((ptri\vu[5] - ptri\vu[3]) * v)) Mod 1
		ptri\pv[1] = (ptri\vv[3] + ((ptri\vv[4] - ptri\vv[3]) * u) + ((ptri\vv[5] - ptri\vv[3]) * v)) Mod 1
		ptri\pw[1] = (ptri\vw[3] + ((ptri\vw[4] - ptri\vw[3]) * u) + ((ptri\vw[5] - ptri\vw[3]) * v)) Mod 1

		If ptri\pu[1]&lt;0.0 Then ptri\pu[1] = 1.0 + ptri\pu[1]
		If ptri\pv[1]&lt;0.0 Then ptri\pv[1] = 1.0 + ptri\pv[1]
		If ptri\pw[1]&lt;0.0 Then ptri\pw[1] = 1.0 + ptri\pw[1]
		
		Return True
	End If
	
	Return False
End Function
</textarea> <br><br></td></tr></table><br>
<a name="809464"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mortiis</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> @IPete2:"Well if you wanted to be a little more creative and perhaps clever, you'd put a pivot 1000 units directly above the player and cast down to it, if you see the player do nothing, if you don't use the above code. That way you only need to have one small raycast per loop and not have to worry about putting loads of code for objects."<br><br>What if the shadow casting object isn't a tree but a wall? What if the shadow is not under the tree? :P<br><br>RifRaf's solution is the best IMO I would stick with it. <br><br></td></tr></table><br>
<a name="809477"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jfk EO-11110</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> well, naughty actually was talking explicitely about trees. RifRafs solution may be pretty slow since it is using ReadPixel in the texturebuffer. The texture probably even ain't using flag 256, so this is going to take a while I'm afraid. Instead you better use an array or bank to hold that lightmap data, tho the redundant data my be a waste of RAM. <br><br></td></tr></table><br>
<a name="809479"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RifRaf</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> ive used the picking UV method before<br>Slow depends on how you use it. if you have 200 characters to pick every frame.. yeah.  But you can even get clever on that and pick one character per frame, and you shouldnt be able to notice any latency in shading.  Or you may just update characters in view, or within a given range to camera ect. cycling them one char per frame.<br><br>If you wanted to take the time to make a hybrid of an array and UV picking minus the readpixel you would have the best of speed and accuracy.  Just make your array the same size as your lightmap (this is IF you have one world lightmap) <br><br></td></tr></table><br>
<a name="809480"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Naughty Alien</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> thank you RifRaf..your idea seems to be very nice...I need it only for main character, and eventually for 3-4 additional characters, per scene..thats all.. <br><br></td></tr></table><br>
<a name="809483"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RifRaf</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Youre welcome..  I went ahead and made an array version if you run into speed problems you can give it a go. The only differnce is you have to load the lightmap first with <br>Lightmap_Array(lightmap)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Dim LightmapArray(0,0)
Global LMArray_width#
Global LMArray_height#

Function shadeentity(entity,ambient=90)
If LinePick(EntityX(Entity), EntityY(Entity)+10, EntityZ(Entity), 0, -125, 0, 0)
	If PickedUVW()
        Local ARGB# = GetTexColor(PickedU(), PickedV())
	    Local RD# = (ARGB Shr 16) And $ff + ambient
        Local GD# = (ARGB Shr 8) And $ff + ambient
        Local BD# = (ARGB And $ff) + ambient
		If RD# &gt; 255 Then RD = 255
		If GD# &gt; 255 Then GD = 255
		If BD# &gt; 255 Then BD = 255
		EntityColor(Entity, RD, GD, BD)
EndIf
EndIf

End Function 

Function GetTexColor(u#,v#)
	u#=u#*(LMArray_width#-1)
	v#=v#*(LMArray_height#-1)
	Return lightmaparray(u,v)
End Function

Type PickedTri
	Field ent,surf,tri			;picked entity, surface and triangle
	Field px#,py#,pz#			;picked xyz
	Field pu#[1],  pv#[1]  ,pw#[1]  ;picked uvw x 2
	
	Field vx#[2],  vy#[2]  ,vz#[2]  ;vertex xyz
	Field vnx#[2], vny#[2] ,vnz#[2] ;vertex normals
	Field vu#[5],  vv#[5]  ,vw#[5]  ;vertex uvw x 2
End Type

Global g_PickedTri.pickedtri = New pickedtri

Function PickedU#(coordset = 0)
	ptri.pickedtri = g_PickedTri
	If (PickedX()&lt;&gt;ptri\px) Or (PickedY()&lt;&gt;ptri\py) Or (PickedZ()&lt;&gt;ptri\pz) Or (PickedSurface()&lt;&gt;ptri\surf)
		uvwTime=PickedUVW()
	End If
	Return ptri\pu[coordset]
End Function

Function PickedV#(coordset = 0)
	ptri.pickedtri = g_PickedTri

	If (PickedX()&lt;&gt;ptri\px) Or (PickedY()&lt;&gt;ptri\py) Or (PickedZ()&lt;&gt;ptri\pz) Or (PickedSurface()&lt;&gt;ptri\surf)
		uvwTime=PickedUVW()
	End If
	Return ptri\pv[coordset]
End Function

Function PickedW#(coordset = 0)
	ptri.pickedtri = g_PickedTri
	
	If (PickedX()&lt;&gt;ptri\px) Or (PickedY()&lt;&gt;ptri\py) Or (PickedZ()&lt;&gt;ptri\pz) Or (PickedSurface()&lt;&gt;ptri\surf)
		uvwTime=PickedUVW()
	End If
	Return ptri\pw[coordset]
End Function

Function PickedUVW()
	ptri.pickedtri = g_PickedTri

	If EntityClass(PickedEntity()) = "Terrain"
		terrain = PickedEntity()
		tsize# = TerrainSize(terrain)
		
		TFormPoint(PickedX(), PickedY(), PickedZ(), 0, terrain)
		ptri\pu[0] = TFormedX() / tsize
		ptri\pv[0] = 1.0 - (TFormedZ() / tsize)
		
		ptri\pu[1] = ptri\pu[0]
		ptri\pv[1] = ptri\pv[0]
		
		Return True
	EndIf
	
	If PickedSurface()
		ptri\ent  = PickedEntity()
		ptri\surf = PickedSurface()
		ptri\tri  = PickedTriangle()
			
		ptri\px = PickedX()
		ptri\py = PickedY()
		ptri\pz = PickedZ()
		
		For i = 0 To 2
			TFormPoint VertexX(ptri\surf,TriangleVertex(ptri\surf,ptri\tri,i)),VertexY(ptri\surf,TriangleVertex(ptri\surf,ptri\tri,i)),VertexZ(ptri\surf,TriangleVertex(ptri\surf,ptri\tri,i)),ptri\ent,0

			ptri\vx[i] = TFormedX()
			ptri\vy[i] = TFormedY()
			ptri\vz[i] = TFormedZ()

			ptri\vnx[i] = VertexNX(ptri\surf,TriangleVertex(ptri\surf,ptri\tri,i))
			ptri\vny[i] = VertexNY(ptri\surf,TriangleVertex(ptri\surf,ptri\tri,i))
			ptri\vnz[i] = VertexNZ(ptri\surf,TriangleVertex(ptri\surf,ptri\tri,i))
					
			ptri\vu[i+0] = VertexU(ptri\surf,TriangleVertex(ptri\surf,ptri\tri,i),0)
			ptri\vv[i+0] = VertexV(ptri\surf,TriangleVertex(ptri\surf,ptri\tri,i),0)
			ptri\vw[i+0] = VertexW(ptri\surf,TriangleVertex(ptri\surf,ptri\tri,i),0)

			ptri\vu[i+3] = VertexU(ptri\surf,TriangleVertex(ptri\surf,ptri\tri,i),1)
			ptri\vv[i+3] = VertexV(ptri\surf,TriangleVertex(ptri\surf,ptri\tri,i),1)
			ptri\vw[i+3] = VertexW(ptri\surf,TriangleVertex(ptri\surf,ptri\tri,i),1)
		Next

		Local coords = 3

		If Abs(PickedNX()) &gt; Abs(PickedNY())
			If Abs(PickedNX())&gt;Abs(PickedNZ()) Then coords = 1
		Else
			If Abs(PickedNY())&gt;Abs(PickedNZ()) Then coords = 2
		EndIf
		
		Local a0#,a1#,b0#,b1#,c0#,c1#
		
		If (coords = 3)
			a0# = ptri\vx[1] - ptri\vx[0]
			a1# = ptri\vy[1] - ptri\vy[0]
			b0# = ptri\vx[2] - ptri\vx[0]
			b1# = ptri\vy[2] - ptri\vy[0]
			c0# = PickedX() - ptri\vx[0]
			c1# = PickedY() - ptri\vy[0]
		Else		
			If (coords = 2)
				a0# = ptri\vx[1] - ptri\vx[0]
				a1# = ptri\vz[1] - ptri\vz[0]
				b0# = ptri\vx[2] - ptri\vx[0]
				b1# = ptri\vz[2] - ptri\vz[0]
				c0# = PickedX() - ptri\vx[0]
				c1# = PickedZ() - ptri\vz[0]
			Else
				a0# = ptri\vy[1] - ptri\vy[0]
				a1# = ptri\vz[1] - ptri\vz[0]
				b0# = ptri\vy[2] - ptri\vy[0]
				b1# = ptri\vz[2] - ptri\vz[0]
				c0# = PickedY() - ptri\vy[0]
				c1# = PickedZ() - ptri\vz[0]
			End If
		End If

		Local u# = (c0*b1 - b0*c1) / (a0*b1 - b0*a1)
		Local v# = (a0*c1 - c0*a1) / (a0*b1 - b0*a1)

		If (u&lt;0.0 Or u&gt;1.0) Or (v&lt;0.0 Or v&gt;1.0)
			Return False
		End If
		
		ptri\pu[0] = (ptri\vu[0] + ((ptri\vu[1] - ptri\vu[0]) * u) + ((ptri\vu[2] - ptri\vu[0]) * v)) Mod 1
		ptri\pv[0] = (ptri\vv[0] + ((ptri\vv[1] - ptri\vv[0]) * u) + ((ptri\vv[2] - ptri\vv[0]) * v)) Mod 1
		ptri\pw[0] = (ptri\vw[0] + ((ptri\vw[1] - ptri\vw[0]) * u) + ((ptri\vw[2] - ptri\vw[0]) * v)) Mod 1

		If ptri\pu[0]&lt;0.0 Then ptri\pu[0] = 1.0 + ptri\pu[0]
		If ptri\pv[0]&lt;0.0 Then ptri\pv[0] = 1.0 + ptri\pv[0]
		If ptri\pw[0]&lt;0.0 Then ptri\pw[0] = 1.0 + ptri\pw[0]
	
		ptri\pu[1] = (ptri\vu[3] + ((ptri\vu[4] - ptri\vu[3]) * u) + ((ptri\vu[5] - ptri\vu[3]) * v)) Mod 1
		ptri\pv[1] = (ptri\vv[3] + ((ptri\vv[4] - ptri\vv[3]) * u) + ((ptri\vv[5] - ptri\vv[3]) * v)) Mod 1
		ptri\pw[1] = (ptri\vw[3] + ((ptri\vw[4] - ptri\vw[3]) * u) + ((ptri\vw[5] - ptri\vw[3]) * v)) Mod 1

		If ptri\pu[1]&lt;0.0 Then ptri\pu[1] = 1.0 + ptri\pu[1]
		If ptri\pv[1]&lt;0.0 Then ptri\pv[1] = 1.0 + ptri\pv[1]
		If ptri\pw[1]&lt;0.0 Then ptri\pw[1] = 1.0 + ptri\pw[1]
		
		Return True
	End If
	
	Return False
End Function

Function Lightmap_Array(lightmap)
LMarray_width#=TextureWidth(lightmap)
LMarray_height#=TextureHeight(lightmap)
Dim Lightmaparray(LMarray_width#,LMarray_height#)
For LoopX=0 To LMArray_width#-1
	For Loopy=0 To LMArray_height#-1
    Lightmaparray(loopx,loopy)=ReadPixel(loopx,loopy,TextureBuffer(lightmap))      
    Next
Next
End Function 

</textarea> <br><br></td></tr></table><br>
<a name="809505"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >IPete2</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah what jfk said really - the question was about trees, and I said a very simple solution, not even crafted really, off the top of my head.  The entity color idea was fresh in my mind because I had just used it recently for a neat effect with hot water and my main character.<br><br>RifRaf's idea is far more creative and what a neat idea to use lightmaps!  I'm gonna look into that myself now. Fab work RifRaf!<br><br>The other person to ask, but he's away in Europe next week, is Josh at Leadwerks, as he released a b3d demo a while back, with trees casting shadows onto the floor and moving in synch with the trees branches. There's a video on his website with it in.<br><br><br>You can see it here:<br><a href="http://www.leadwerks.com/" target="_blank">http://www.leadwerks.com/</a><br><br>it is called Living Forest Movie on the right side bar menu<br><br>IPete2. <br><br></td></tr></table><br>
<a name="809701"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pete Carter</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> that living forest demo is very cool. im going to try rifraf's idea myself good job. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
